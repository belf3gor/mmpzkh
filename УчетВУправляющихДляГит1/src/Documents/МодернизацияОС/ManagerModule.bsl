#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 13, 0);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата, Организация");
	
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Дата, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка",                           ДокументСсылка);
	Запрос.УстановитьПараметр("ПлательщикНалогаНаПрибыль",        УчетнаяПолитика.ПлательщикНалогаНаПрибыль(
		Реквизиты.Организация, Реквизиты.Дата));
	Запрос.УстановитьПараметр("ПлательщикНДС",					  УчетнаяПолитика.ПлательщикНДС(
		Реквизиты.Организация, Реквизиты.Дата));	
	Запрос.УстановитьПараметр("ПрименяетсяУСН",                   УчетнаяПолитика.ПрименяетсяУСН(
		Реквизиты.Организация, Реквизиты.Дата));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходыМинусРасходы", УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(
		Реквизиты.Организация, Реквизиты.Дата));
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст =
		ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицаОС(НомераТаблиц)
		+ ТекстЗапросаСобытияОС(НомераТаблиц)
		+ ТекстЗапросаПараметрыАмортизацииОС(НомераТаблиц)
		+ ТекстЗапросаМодернизацияОС(НомераТаблиц)
		+ ТекстЗапросаПроверкиПоОС(НомераТаблиц)
		+ ТекстЗапросаОплаты(НомераТаблиц)
		+ ТекстЗапросаНДС(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

Функция ПодготовитьТаблицуОплатДляУСН(ТаблицаПоОС, ОплатыОСдляУСНСвернутая, Отказ) Экспорт

	ТаблицаОплатПоОС = Новый ТаблицаЗначений();
	ТаблицаОплатПоОС.Колонки.Добавить("ОсновноеСредство");
	ТаблицаОплатПоОС.Колонки.Добавить("ДатаОплаты");
	ТаблицаОплатПоОС.Колонки.Добавить("СуммаОплаты");

	ТаблицаБазыРаспределения = ТаблицаПоОС.Скопировать(,"ОсновноеСредство, СуммаМодернизацииУСН");
	Если ТаблицаБазыРаспределения.Количество() = 0 Тогда
		Возврат ТаблицаОплатПоОС;
	КонецЕсли;

	ИтогСуммаМодернизацииУСН = ТаблицаБазыРаспределения.Итог("СуммаМодернизацииУСН");

	Для Каждого СтрокаОплата Из ОплатыОСдляУСНСвернутая Цикл
		Если СтрокаОплата.ДатаОплаты < Дата("20070101") Тогда
			//Такие оплаты не учитываются
			Продолжить;
		КонецЕсли;

		Если Окр(ИтогСуммаМодернизацииУСН, 2, 1) = 0 Тогда
			Возврат ТаблицаОплатПоОС;
		КонецЕсли;

		СуммаОплатыКРаспределению = Мин(СтрокаОплата.СуммаОплаты, ИтогСуммаМодернизацииУСН);
		КоэффОплаты = СуммаОплатыКРаспределению / ИтогСуммаМодернизацииУСН;

		Для Каждого СтрокаОС Из ТаблицаБазыРаспределения Цикл
			СуммаОплатыОС = Мин(Окр(КоэффОплаты*СтрокаОС.СуммаМодернизацииУСН,2,1), СтрокаОС.СуммаМодернизацииУСН);
			Если СуммаОплатыОС = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицыОплатПоОС = ТаблицаОплатПоОС.Добавить();
			СтрокаТаблицыОплатПоОС.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
			СтрокаТаблицыОплатПоОС.ДатаОплаты       = СтрокаОплата.ДатаОплаты;
			СтрокаТаблицыОплатПоОС.СуммаОплаты      = СуммаОплатыОС;
			//
			СуммаОплатыКРаспределению               = СуммаОплатыКРаспределению - СуммаОплатыОС;
			СтрокаОС.СуммаМодернизацииУСН           = СтрокаОС.СуммаМодернизацииУСН - СуммаОплатыОС;
			ИтогСуммаМодернизацииУСН                = ИтогСуммаМодернизацииУСН - СуммаОплатыОС;
		КонецЦикла;

		//Распределим погрешность округления, списывая ее пропорционально убыванию остатков расходов
		ТаблицаБазыРаспределения.Сортировать("СуммаМодернизацииУСН Убыв");
		Если СуммаОплатыКРаспределению <> 0 Тогда
			Для Каждого СтрокаОС Из ТаблицаБазыРаспределения Цикл
				Если СуммаОплатыКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;
				СуммаОплатыОС = Мин(СуммаОплатыКРаспределению, СтрокаОС.СуммаМодернизацииУСН);
				Если СуммаОплатыОС = 0 Тогда
					Продолжить;
				КонецЕсли;
				СтрокаТаблицыОплатПоОС = ТаблицаОплатПоОС.Добавить();
				СтрокаТаблицыОплатПоОС.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
				СтрокаТаблицыОплатПоОС.ДатаОплаты       = СтрокаОплата.ДатаОплаты;
				СтрокаТаблицыОплатПоОС.СуммаОплаты      = СуммаОплатыОС;
				//
				СуммаОплатыКРаспределению               = СуммаОплатыКРаспределению - СуммаОплатыОС;
				СтрокаОС.СуммаМодернизацииУСН           = СтрокаОС.СуммаМодернизацииУСН - СуммаОплатыОС;
				ИтогСуммаМодернизацииУСН                = ИтогСуммаМодернизацииУСН - СуммаОплатыОС;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;
	ТаблицаОплатПоОС.Свернуть("ОсновноеСредство, ДатаОплаты", "СуммаОплаты");

	Возврат ТаблицаОплатПоОС;

КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Акт о приеме-сдаче ОС (ОС-3)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС3";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приеме-сдаче ОС (ОС-3)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Модернизация ОС""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС3") Тогда
	
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС3", НСтр("ru = 'ОС-3 (Акт о приеме-сдаче модернизированных ОС)'"), 
			ПечатьОС3(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.МодернизацияОС.ПФ_MXL_ОС3");
	
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	

КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация, СуммаДокумента", "ОбъектСтроительства", "СтоимостьБУ");
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц = Неопределено)

	Если типЗнч(НомераТаблиц) = Тип("Структура") Тогда
		НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата                    КАК Период,
	|	Реквизиты.Организация             КАК Организация,
	|	Реквизиты.Ссылка                  КАК ДокументМодернизации,
	|	&ПлательщикНалогаНаПрибыль        КАК ПлательщикНалогаНаПрибыль,
	|	&ПрименяетсяУСН                   КАК ПрименяетсяУСН,
	|	&ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы
	|ИЗ
	|	Документ.МодернизацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаОС(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОсновныеСредства", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка                       КАК Регистратор,
	|	ТаблицаОС.НомерСтроки                  КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.СуммаМодернизацииБУ          КАК СуммаМодернизацииБУ,
	|	ТаблицаОС.СуммаМодернизацииНУ          КАК СуммаМодернизацииНУ,
	|	ТаблицаОС.СуммаМодернизацииПР          КАК СуммаМодернизацииПР,
	|	ТаблицаОС.СуммаМодернизацииВР          КАК СуммаМодернизацииВР,
	|	ТаблицаОС.СуммаМодернизацииУСН         КАК СуммаМодернизацииУСН,
	|	ТаблицаОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ТаблицаОС.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	ТаблицаОС.ОбъемПродукцииРаботБУ        КАК ОбъемПродукцииРаботБУ,
	|	ТаблицаОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ КАК СуммаКапитальныхВложенийВключаемыхВРасходыНУ
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.МодернизацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСобытияОС(НомераТаблиц)

	НомераТаблиц.Вставить("СобытияОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СобытияОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер,
	|	Реквизиты.Организация,
	|	Реквизиты.СобытиеОС КАК СобытиеОС
	|ИЗ
	|	Документ.МодернизацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство     КАК ОсновноеСредство,
	|	ТаблицаОС.СуммаМодернизацииБУ  КАК СуммаЗатратБУ,
	|	ТаблицаОС.СуммаМодернизацииНУ  КАК СуммаЗатратНУ,
	|	ТаблицаОС.СуммаМодернизацииУСН КАК СуммаЗатратУСН
	|ИЗ
	|	ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПараметрыАмортизацииОС(НомераТаблиц)

	НомераТаблиц.Вставить("ПараметрыАмортизацииОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПараметрыАмортизацииОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация
	|ИЗ
	|	Документ.МодернизацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.СуммаМодернизацииБУ КАК СуммаМодернизацииБУ,
	|	ТаблицаОС.СуммаМодернизацииПР КАК СуммаМодернизацииПР,
	|	ТаблицаОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ТаблицаОС.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	ТаблицаОС.ОбъемПродукцииРаботБУ КАК ОбъемПродукцииРаботБУ
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаМодернизацияОС(НомераТаблиц)

	НомераТаблиц.Вставить("МодернизацияОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("МодернизацияОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ОбъектСтроительства КАК ОбъектСтроительства,
	|	Реквизиты.СчетУчетаВнеоборотногоАктива КАК СчетУчетаВнеоборотногоАктива,
	|	Реквизиты.МестонахождениеОС КАК Подразделение,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеВнеоборотногоАктива,
	|	Реквизиты.СубконтоПоАмортизационнойПремии1 КАК СубконтоПоАмортизационнойПремии1,
	|	Реквизиты.СубконтоПоАмортизационнойПремии2 КАК СубконтоПоАмортизационнойПремии2,
	|	Реквизиты.СубконтоПоАмортизационнойПремии3 КАК СубконтоПоАмортизационнойПремии3,
	|	Реквизиты.СчетУчетаЗатратПоАмортизационнойПремии КАК СчетУчетаЗатратПоАмортизационнойПремии,
	|	Реквизиты.ПодразделениеОрганизацииПоАмортизационнойПремии КАК ПодразделениеПоАмортизационнойПремии,
	|	Реквизиты.РеквизитыДокументаОплаты КАК РеквизитыДокументаОплаты
	|ИЗ
	|	Документ.МодернизацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.СуммаМодернизацииБУ КАК СуммаМодернизацииБУ,
	|	ТаблицаОС.СуммаМодернизацииНУ КАК СуммаМодернизацииНУ,
	|	ТаблицаОС.СуммаМодернизацииПР КАК СуммаМодернизацииПР,
	|	ТаблицаОС.СуммаМодернизацииВР КАК СуммаМодернизацииВР,
	|	ТаблицаОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ КАК СуммаКапитальныхВложенийВключаемыхВРасходыНУ
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОплаты(НомераТаблиц)

	НомераТаблиц.Вставить("ОплатыОСдляУСНСвернутая", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаОСДляУСН", НомераТаблиц.Количество());

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Оплаты.ДатаОплаты, ДЕНЬ) КАК ДатаОплаты,
	|	СУММА(Оплаты.СуммаОплаты)              КАК СуммаОплаты
	|ИЗ
	|	Документ.МодернизацияОС.Оплата КАК Оплаты
	|ГДЕ
	|	Оплаты.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Оплаты.ДатаОплаты, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство     КАК ОсновноеСредство,
	|	ТаблицаОС.СуммаМодернизацииУСН КАК СуммаМодернизацииУСН
	|ИЗ
	|	ТаблицаОС
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПроверкиПоОС(НомераТаблиц)

	НомераТаблиц.Вставить("ПроверкиПоОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	""ОС"" КАК ИмяСписка,
	|	Реквизиты.МестонахождениеОС КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК МОЛ
	|ИЗ
	|	Документ.МодернизацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОбъектыСтроительстваНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаСписанныеОбъектыСтроительстваНДС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	0 КАК ПроцентАмортизационнойПремии,
	|	Реквизиты.СчетУчетаЗатратПоАмортизационнойПремии КАК СчетУчетаЗатратПоАмортизационнойПремии,
	|	Реквизиты.ПодразделениеОрганизацииПоАмортизационнойПремии КАК ПодразделениеПоАмортизационнойПремии,
	|	Реквизиты.СубконтоПоАмортизационнойПремии1 КАК СубконтоПоАмортизационнойПремии1,
	|	Реквизиты.СубконтоПоАмортизационнойПремии2 КАК СубконтоПоАмортизационнойПремии2,
	|	Реквизиты.СубконтоПоАмортизационнойПремии3 КАК СубконтоПоАмортизационнойПремии3
	|ИЗ
	|	Документ.МодернизацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаОС.Ссылка.ОбъектСтроительства КАК Номенклатура,
	|	1 КАК Количество,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	ТаблицаОС.СпособУчетаНДС КАК НовыйСпособУчетаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.СуммаМодернизацииНУ = 0
	|			ТОГДА 0
	|		КОГДА ТаблицаОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ >= ТаблицаОС.СуммаМодернизацииНУ
	|			ТОГДА 100
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ / ТаблицаОС.СуммаМодернизацииНУ * 100 КАК ЧИСЛО(5, 2))
	|	КОНЕЦ КАК ПроцентАмортизационнойПремии
	|ИЗ
	|	Документ.МодернизацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ПлательщикНДС = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	""Основные средства"" КАК СинонимСписка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	ТаблицаОС.Ссылка.ОбъектСтроительства КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК Партия,
	|	1 КАК Количество,
	|	1 КАК ВидКорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	ТаблицаОС.ОсновноеСредство КАК КорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ТаблицаОС.Ссылка.МестонахождениеОС КАК КорПодразделение,
	|	ТаблицаОС.Ссылка.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.МодернизацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ПлательщикНДС = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ПечатьОС3(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Форма_ОС3";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.МодернизацияОС.ПФ_MXL_ОС3");

	// Области
	ОбластьШапка    = Макет.ПолучитьОбласть("Шапка1");
	СтрокаТаблицы   = Макет.ПолучитьОбласть("Строка1");
	Подвал          = Макет.ПолучитьОбласть("Строка1П");
	ОбластьШапка2   = Макет.ПолучитьОбласть("Шапка2");
	СтрокаТаблицы2  = Макет.ПолучитьОбласть("Строка2");
	Подвал2         = Макет.ПолучитьОбласть("Строка2П");
	ПодвалДокумента = Макет.ПолучитьОбласть("Подвал");
	ОбластьПечати   = Макет.ПолучитьОбласть("ОборотнаяСторона");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументМодернизацияОС.Ссылка КАК Ссылка,
	|	ДокументМодернизацияОС.Организация КАК Организация,
	|	МодернизацияОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МодернизацияОСОС.ОсновноеСредство.Наименование КАК НаимОС,
	|	МодернизацияОСОС.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	МодернизацияОСОС.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
	|	МодернизацияОСОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБухгалтерскийУчет.СтоимостьДляВычисленияАмортизации ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ПараметрыАмортизацииОСБухгалтерскийУчет.СтоимостьДляВычисленияАмортизации - МодернизацияОСОС.СуммаМодернизацииБУ
	|	КОНЕЦ КАК СтоимостьБУ,
	|	МодернизацияОСОС.СуммаМодернизацииБУ КАК СуммаЗатрат,
	|	МодернизацияОСОС.НомерСтроки КАК НомерСтроки,
	|	МодернизацияОСОС.НомерСтроки КАК ПорядковыйНомер,
	|	МАКСИМУМ(ПервоначальныеСведенияОСБухгалтерскийУчет.Период) КАК МаксДата,
	|	СостоянияОСОрганизаций.Период КАК МаксДатаСостояния
	|ПОМЕСТИТЬ МодернизацияОСОС
	|ИЗ
	|	Документ.МодернизацияОС КАК ДокументМодернизацияОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
	|		ПО ДокументМодернизацияОС.Ссылка = МодернизацияОСОС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО (МодернизацияОСОС.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство)
	|			И (МодернизацияОСОС.Ссылка.Дата >= ПервоначальныеСведенияОСБухгалтерскийУчет.Период)
	|			И (МодернизацияОСОС.Ссылка.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|		ПО (МодернизацияОСОС.Ссылка.Организация = СостоянияОСОрганизаций.Организация)
	|			И (МодернизацияОСОС.ОсновноеСредство = СостоянияОСОрганизаций.ОсновноеСредство)
	|			И (МодернизацияОСОС.Ссылка.Дата >= СостоянияОСОрганизаций.Период)
	|			И (СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет КАК ПараметрыАмортизацииОСБухгалтерскийУчет
	|		ПО (МодернизацияОСОС.Ссылка = ПараметрыАмортизацииОСБухгалтерскийУчет.Регистратор)
	|			И (МодернизацияОСОС.ОсновноеСредство = ПараметрыАмортизацииОСБухгалтерскийУчет.ОсновноеСредство)
	|ГДЕ
	|	ДокументМодернизацияОС.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументМодернизацияОС.Ссылка,
	|	МодернизацияОСОС.ОсновноеСредство,
	|	МодернизацияОСОС.СуммаМодернизацииБУ,
	|	МодернизацияОСОС.НомерСтроки,
	|	МодернизацияОСОС.ОсновноеСредство.Наименование,
	|	МодернизацияОСОС.ОсновноеСредство.ДатаВыпуска,
	|	МодернизацияОСОС.ОсновноеСредство.НомерПаспорта,
	|	МодернизацияОСОС.ОсновноеСредство.ЗаводскойНомер,
	|	ДокументМодернизацияОС.Организация,
	|	СостоянияОСОрганизаций.Период,
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБухгалтерскийУчет.СтоимостьДляВычисленияАмортизации ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ПараметрыАмортизацииОСБухгалтерскийУчет.СтоимостьДляВычисленияАмортизации - МодернизацияОСОС.СуммаМодернизацииБУ
	|	КОНЕЦ,
	|	МодернизацияОСОС.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МодернизацияОСОС.Ссылка КАК Ссылка,
	|	МодернизацияОСОС.Ссылка.Организация.КодПоОКПО КАК КодПоОКПООрганизации,
	|	МодернизацияОСОС.Ссылка.Номер КАК Номер,
	|	МодернизацияОСОС.Ссылка.Дата КАК Дата,
	|	МодернизацияОСОС.Ссылка.Организация КАК Организация,
	|	МодернизацияОСОС.Ссылка.МестонахождениеОС КАК Подразделение,
	|	МодернизацияОСОС.Ссылка.СобытиеОС КАК ВидРабот,
	|	МодернизацияОСОС.ОсновноеСредство,
	|	МодернизацияОСОС.НаимОС,
	|	МодернизацияОСОС.ДатаВыпуска,
	|	МодернизацияОСОС.НомерПаспорта,
	|	МодернизацияОСОС.ЗаводскойНомер,
	|	МодернизацияОСОС.СтоимостьБУ КАК СтоимостьБУ,
	|	МодернизацияОСОС.СуммаЗатрат КАК СуммаЗатрат,
	|	МодернизацияОСОС.НомерСтроки,
	|	МодернизацияОСОС.ПорядковыйНомер КАК ПорядковыйНомер,
	|	СведенияБУ.ИнвентарныйНомер КАК ИнвНомер,
	|	ВЫБОР
	|		КОГДА СостоянияОСОрганизаций.ДатаСостояния ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(СостоянияОСОрганизаций.ДатаСостояния, МодернизацияОСОС.Ссылка.Дата, МЕСЯЦ)
	|	КОНЕЦ КАК СрокЭксплуатации
	|ИЗ
	|	МодернизацияОСОС КАК МодернизацияОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК СведенияБУ
	|		ПО МодернизацияОСОС.ОсновноеСредство = СведенияБУ.ОсновноеСредство
	|			И МодернизацияОСОС.Организация = СведенияБУ.Организация
	|			И МодернизацияОСОС.МаксДата = СведенияБУ.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|		ПО МодернизацияОСОС.ОсновноеСредство = СостоянияОСОрганизаций.ОсновноеСредство
	|			И МодернизацияОСОС.Организация = СостоянияОСОрганизаций.Организация
	|			И МодернизацияОСОС.МаксДатаСостояния = СостоянияОСОрганизаций.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	ПорядковыйНомер
	|ИТОГИ
	|	СУММА(СтоимостьБУ),
	|	СУММА(СуммаЗатрат),
	|	СУММА(СрокЭксплуатации)
	|ПО
	|	Ссылка";

	ПервыйДокумент = Истина;
	Шапка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Ссылка");

	Пока Шапка.Следующий() Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ВыборкаПоОС = Шапка.Выбрать();

		ВыборкаПоОС.Следующий();

		// Печать лицевой стороны.
		// Секция № 1

		ОбластьШапка.Параметры.Заполнить(ВыборкаПоОС);
		
		ОбластьШапка.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Ложь);

		СведенияОбОрганизации       = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата);
		ПредставлениеОрганизации    = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
		ОбластьШапка.Параметры.Организация = ПредставлениеОрганизации;

		ПодразделениеОтветственныхЛиц = Шапка.Подразделение;

		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата, ПодразделениеОтветственныхЛиц);

		ОбластьШапка.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
		ОбластьШапка.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;
		ТабличныйДокумент.Вывести(ОбластьШапка);

		ДокВводаБУ  = Неопределено;
		ДатаВводаБУ = '00010101';

		ВыборкаПоОС.Сбросить();

		Пока ВыборкаПоОС.Следующий() Цикл

			СтрокаТаблицы.Параметры.Заполнить(ВыборкаПоОС);
			УчетОС.ПолучитьДокументБухСостоянияОС(ВыборкаПоОС.ОсновноеСредство, Шапка.Организация,
				Перечисления.СостоянияОС.ПринятоКУчету, ДокВводаБУ, ДатаВводаБУ);

			СтрокаТаблицы.Параметры.СрокЭкспл = ?(НЕ ЗначениеЗаполнено(ВыборкаПоОС.СрокЭксплуатации), "-",
				Строка(ВыборкаПоОС.СрокЭксплуатации) + " мес.");

			СтрокаТаблицы.Параметры.ВосстановительнаяСтоимостьПеч = ВыборкаПоОС.СтоимостьБУ;

			ТабличныйДокумент.Вывести(СтрокаТаблицы);

		КонецЦикла;

		// Секция № 2

		ТабличныйДокумент.Вывести(ОбластьШапка2);

		ВыборкаПоОС.Сбросить();
		Пока ВыборкаПоОС.Следующий() Цикл
			СтрокаТаблицы2.Параметры.Заполнить(ВыборкаПоОС);
			ТабличныйДокумент.Вывести(СтрокаТаблицы2);
		КонецЦикла;

		ПодвалДокумента.Параметры.ИтогоСуммаЗатрат = Шапка.СуммаЗатрат;

		Если ВыборкаПоОС.Количество() = 1 И
			(ЗначениеЗаполнено(Шапка.СтоимостьБУ) Или ЗначениеЗаполнено(Шапка.СуммаЗатрат)) Тогда
			ПодвалДокумента.Параметры.СтоимостьКонечнаяПеч =  Шапка.СтоимостьБУ +  Шапка.СуммаЗатрат;
		Иначе
			ПодвалДокумента.Параметры.СтоимостьКонечнаяПеч = Неопределено;
		КонецЕсли;

		ТабличныйДокумент.Вывести(ПодвалДокумента);

		// Печать оборотной стороны.
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ОбластьПечати.Параметры.ГлавБух = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		ТабличныйДокумент.Вывести(ОбластьПечати);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти

#КонецЕсли