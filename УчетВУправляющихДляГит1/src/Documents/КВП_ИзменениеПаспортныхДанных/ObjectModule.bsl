
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытийДокумента

// Обработка проведения документа.
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ФизЛицо",         ФизическоеЛицо);
	Запрос.УстановитьПараметр("Дата",            НачалоДня(Дата)); // Периодичность регистров - "В пределах дня".
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	
	// Паспортные данные.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыФизическихЛиц.Период,
	|	ДокументыФизическихЛиц.Физлицо,
	|	ДокументыФизическихЛиц.ВидДокумента,
	|	ДокументыФизическихЛиц.Серия,
	|	ДокументыФизическихЛиц.Номер,
	|	ДокументыФизическихЛиц.ДатаВыдачи,
	|	ДокументыФизическихЛиц.СрокДействия,
	|	ДокументыФизическихЛиц.КемВыдан,
	|	ДокументыФизическихЛиц.КодПодразделения,
	|	ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ДокументыФизическихЛиц.Представление,
	|	ДокументыФизическихЛиц.УдалитьВидДокумента,
	|	ДокументыФизическихЛиц.ПричинаИзменения,
	|	ДокументыФизическихЛиц.ДокументРегистратор
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|ГДЕ
	|	ДокументыФизическихЛиц.Физлицо = &ФизЛицо
	|	И ДокументыФизическихЛиц.ДокументРегистратор <> &ТекущийДокумент
	|	И ДокументыФизическихЛиц.Период <> &Дата";
	
	// Формируем таблицу со всеми записями по данному физлицу, исключая записи, сделанные
	// данным документом и на данную дату.
	ТаблицаЗаписей = Запрос.Выполнить().Выгрузить();
	НаборЗаписей = РегистрыСведений.ДокументыФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	
	// Добавляем в полученную таблицу новую запись.
	НовВыборка = НаборЗаписей.Добавить();
	НовВыборка.Период                           = КонецДня(Дата);
	НовВыборка.ФизЛицо                          = ФизическоеЛицо;
	НовВыборка.ВидДокумента                     = ВидДокумента;
	НовВыборка.Серия                            = СерияДокумента;
	НовВыборка.Номер                            = НомерДокумента;
	НовВыборка.ДатаВыдачи                       = ДатаВыдачиДокумента;
	НовВыборка.КемВыдан                         = КемВыданДокумент;
	НовВыборка.КодПодразделения                 = КодПодразделения;
	НовВыборка.ЯвляетсяДокументомУдостоверяющимЛичность = Истина;
	НовВыборка.ДокументРегистратор              = Ссылка;
	НовВыборка.ПричинаИзменения                 = ПричинаИзменения;
	
	// Записываем новую "историю" в регистр.
	НаборЗаписей.Записать();
	
	// ФИО физических лиц.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФИОФизическихЛиц.Период,
	|	ФИОФизическихЛиц.ФизическоеЛицо,
	|	ФИОФизическихЛиц.Фамилия,
	|	ФИОФизическихЛиц.Имя,
	|	ФИОФизическихЛиц.Отчество,
	|	ФИОФизическихЛиц.ПричинаИзменения,
	|	ФИОФизическихЛиц.ДокументРегистратор
	|ИЗ
	|	РегистрСведений.ФИОФизическихЛиц КАК ФИОФизическихЛиц
	|ГДЕ
	|	ФИОФизическихЛиц.ФизическоеЛицо = &ФизЛицо
	|	И ФИОФизическихЛиц.ДокументРегистратор <> &ТекущийДокумент
	|	И ФИОФизическихЛиц.Период <> &Дата";
	
	// Формируем таблицу со всеми записями по данному физлицу, исключая записи, сделанные
	// данным документом и на данную дату.
	ТаблицаЗаписей = Запрос.Выполнить().Выгрузить();
	НаборЗаписей = РегистрыСведений.ФИОФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	
	// Добавляем в полученную таблицу новую запись.
	НовВыборка = НаборЗаписей.Добавить();
	НовВыборка.Период              = КонецДня(Дата);
	НовВыборка.ФизическоеЛицо      = ФизическоеЛицо;
	НовВыборка.Фамилия             = Фамилия;
	НовВыборка.Имя                 = Имя;
	НовВыборка.Отчество            = Отчество;
	НовВыборка.ДокументРегистратор = Ссылка;
	НовВыборка.ПричинаИзменения    = ПричинаИзменения;
	
	// Записываем новую "историю" в регистр.
	НаборЗаписей.Записать();
	
КонецПроцедуры // ОбработкаПроведения()

// Обработка создания документа на основании.
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	УПЖКХ_ТиповыеМетодыСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.УПЖКХ_Жильцы") Тогда

		ФизическоеЛицо = ДанныеЗаполнения.ФизЛицо;

	КонецЕсли;

КонецПроцедуры // ОбработкаЗаполнения()

// Обработка удаления проведения документа.
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Паспортные данные.
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("ФизЛицо",         ФизическоеЛицо);
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыФизическихЛиц.Период,
	|	ДокументыФизическихЛиц.Физлицо,
	|	ДокументыФизическихЛиц.ВидДокумента,
	|	ДокументыФизическихЛиц.Серия,
	|	ДокументыФизическихЛиц.Номер,
	|	ДокументыФизическихЛиц.ДатаВыдачи,
	|	ДокументыФизическихЛиц.СрокДействия,
	|	ДокументыФизическихЛиц.КемВыдан,
	|	ДокументыФизическихЛиц.КодПодразделения,
	|	ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ДокументыФизическихЛиц.Представление,
	|	ДокументыФизическихЛиц.УдалитьВидДокумента,
	|	ДокументыФизическихЛиц.ПричинаИзменения,
	|	ДокументыФизическихЛиц.ДокументРегистратор
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|ГДЕ
	|	ДокументыФизическихЛиц.Физлицо = &ФизЛицо
	|	И ДокументыФизическихЛиц.ДокументРегистратор <> &ТекущийДокумент";
	
	ТаблицаЗаписей = Запрос.Выполнить().Выгрузить();
	НаборЗаписей = РегистрыСведений.ДокументыФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	НаборЗаписей.Записать();
	
	// ФИО физических лиц.
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("ФизЛицо",         ФизическоеЛицо);
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизическихЛиц.Период,
	|	ФИОФизическихЛиц.ФизическоеЛицо,
	|	ФИОФизическихЛиц.Фамилия,
	|	ФИОФизическихЛиц.Имя,
	|	ФИОФизическихЛиц.Отчество,
	|	ФИОФизическихЛиц.ПричинаИзменения,
	|	ФИОФизическихЛиц.ДокументРегистратор
	|ИЗ
	|	РегистрСведений.ФИОФизическихЛиц КАК ФИОФизическихЛиц
	|ГДЕ
	|	ФИОФизическихЛиц.ФизическоеЛицо = &ФизЛицо
	|	И ФИОФизическихЛиц.ДокументРегистратор <> &ТекущийДокумент";
	
	ТаблицаЗаписей = Запрос.Выполнить().Выгрузить();
	НаборЗаписей = РегистрыСведений.ФИОФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	НаборЗаписей.Записать();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

#КонецОбласти

#КонецЕсли