#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 6, 0);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();

	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);

	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;

	НомераТаблиц = Новый Структура;

	Запрос.УстановитьПараметр("ПлательщикНалогаНаПрибыль", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период));

	Запрос.Текст =
		ТекстЗапросаТаблицаОС(НомераТаблиц)
		+ ТекстЗапросаСобытияОС(НомераТаблиц)
		+ ТекстЗапросаСостоянияОС(НомераТаблиц) // таблица СостоянияОС сформируется только для ВидыСобытийОС = Передача или Списание
		+ ТекстЗапросаНачисленияАмортизацииОС(НомераТаблиц) // таблицы будут сформированы в зависимости от галочек ВлияетНаНачислениеАмортизации, ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете
		+ ТекстЗапросаПроверкиПоОС(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизитыДокумента()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата        КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Документ.ИзменениеСостоянияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаОС(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОсновныеСредства", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка                               КАК Регистратор,
	|	ТаблицаОС.НомерСтроки                          КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство                     КАК ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СобытиеОС.ВидСобытияОС        КАК ВидСобытияОС,
	|	ТаблицаОС.Ссылка.ВлияетНаНачислениеАмортизации КАК ВлияетНаНачислениеАмортизации,
	|	ТаблицаОС.Ссылка.НачислятьАмортизацию          КАК НачислятьАмортизацию,
	|	ТаблицаОС.Ссылка.ОтражатьВБухгалтерскомУчете   КАК ОтражатьВБухгалтерскомУчете,
	|	ТаблицаОС.Ссылка.ОтражатьВНалоговомУчете       КАК ОтражатьВНалоговомУчете
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.ИзменениеСостоянияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор      КАК Регистратор,
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСобытияОС(НомераТаблиц)

	НомераТаблиц.Вставить("СобытияОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СобытияОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка      КАК Регистратор,
	|	Реквизиты.Дата        КАК Период,
	|	Реквизиты.Номер       КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СобытиеОС   КАК СобытиеОС
	|ИЗ
	|	Документ.ИзменениеСостоянияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор      КАК Регистратор,
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	0                          КАК СуммаЗатратБУ,
	|	0                          КАК СуммаЗатратНУ,
	|	0                          КАК СуммаЗатратУСН
	|ИЗ
	|	ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСостоянияОС(НомераТаблиц)

	// таблицы СостоянияОС сформируются только для СобытиеОС.ВидыСобытийОС = Передача или Списание

	НомераТаблиц.Вставить("СостоянияОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СостоянияОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата   КАК Период,
	|	Реквизиты.Номер  КАК Номер,
	|	ВЫБОР
	|		КОГДА Реквизиты.СобытиеОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Передача) Тогда
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|		КОГДА Реквизиты.СобытиеОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Списание) Тогда
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|		Иначе
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка)
	|	КОНЕЦ КАК СостояниеОС,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Документ.ИзменениеСостоянияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	// Если событие не Передача и не Списание, то таблица будет пустая
	|	И (
	|		Реквизиты.СобытиеОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Передача)
	|		ИЛИ Реквизиты.СобытиеОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Списание)
	|	)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор      КАК Регистратор,
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	ТаблицаОС
	|ГДЕ
	|	// Если событие не Передача и не Списание, то таблица будет пустая
	|	ТаблицаОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Передача)
	|	ИЛИ ТаблицаОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Списание)
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНачисленияАмортизацииОС(НомераТаблиц)

	НомераТаблиц.Вставить("НачисленияАмортизацииОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НачисленияАмортизацииОСБУТаблица", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НачисленияАмортизацииОСНУТаблица", НомераТаблиц.Количество());

	ТекстЗапроса = "
	|// НачисленияАмортизацииОС
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка               КАК Регистратор,
	|	Реквизиты.Дата                 КАК Период,
	|	Реквизиты.Номер                КАК Номер,
	|	Реквизиты.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	&ПлательщикНалогаНаПрибыль     КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.Организация          КАК Организация
	|ИЗ
	|	Документ.ИзменениеСостоянияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// НачисленияАмортизацииОСБУТаблица
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор      КАК Регистратор,
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	ТаблицаОС
	|ГДЕ
	|	// Если одна из галочек отключена, то таблица будет пустая
	|	ТаблицаОС.ВлияетНаНачислениеАмортизации
	|	И ТаблицаОС.ОтражатьВБухгалтерскомУчете
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// НачисленияАмортизацииОСНУТаблица
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор      КАК Регистратор,
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	ТаблицаОС
	|ГДЕ
	|	// Если одна из галочек отключена, то таблица будет пустая
	|	ТаблицаОС.ВлияетНаНачислениеАмортизации
	|	И ТаблицаОС.ОтражатьВНалоговомУчете
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПроверкиПоОС(НомераТаблиц)

	НомераТаблиц.Вставить("ПроверкиПоОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка      КАК Регистратор,
	|	Реквизиты.Дата        КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	""ОС""                КАК ИмяСписка
	|ИЗ
	|	Документ.ИзменениеСостоянияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

#КонецОбласти

#КонецЕсли