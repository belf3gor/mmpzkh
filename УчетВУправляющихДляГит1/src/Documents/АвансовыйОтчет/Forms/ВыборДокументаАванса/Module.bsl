
#Область ОписаниеПеременных

&НаКлиенте
Перем ДействиеВыбрано;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаАвансовогоОтчета 	= Параметры.ДатаАвансовогоОтчета;
	Организация 			= Параметры.Организация;
	ФизЛицо 				= Параметры.ФизЛицо;
	ВалютаДокумента 		= Параметры.ВалютаДокумента;
	ТекущийДокумент			= Параметры.ТекущийДокумент;
	ВыбранныеДокументы		= Параметры.ВыбранныеДокументы;
	
	ШаблонЗаголовка = НСтр("ru='Выплаты сотруднику (%1)'");
	ФИО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизЛицо, "ФИО");
	Заголовок = СтрШаблон(ШаблонЗаголовка, ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ФИО));
	
	ТолькоПоследние = 1;
	ЗаполнитьСписокДокументов();
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	Если ДействиеВыбрано <> Истина Тогда
		ОповеститьОВыборе(Неопределено);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказатьВсеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДокументыОплаты.ТекущиеДанные;
	ТекущаяСтрока = Неопределено;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущаяСтрока = ТекущиеДанные.Ссылка;
	КонецЕсли;	
	
	ЗаполнитьСписокДокументов();
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ПоискСтрок = ДокументыОплаты.НайтиСтроки(Новый Структура("Ссылка", ТекущаяСтрока));
		Если ПоискСтрок.Количество() > 0 Тогда
			Элементы.ДокументыОплаты.ТекущаяСтрока = ПоискСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ДокументыОплатыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗавершитьВыбор();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ЗавершитьВыбор();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Выбранные документы выделяются серым текстом
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыОплаты"); // вся строка
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ДокументыОплаты.Выбран", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстПодобранногоЗначенияЦвет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыбор()
		
	ДействиеВыбрано = Истина;
	ТекущиеДанные = Элементы.ДокументыОплаты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.Выбран Тогда
		Результат = Неопределено;
	ИначеЕсли ТекущиеДанные <> Неопределено Тогда
		Результат = ТекущиеДанные.Ссылка;
	КонецЕсли;	
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДокументов()
	
	ПоказатьТолькоПоследние = (ТолькоПоследние = 1);
	
	Если ПоказатьТолькоПоследние Тогда
		ДокументыОплаты.Очистить();
		ГраницаПоиска = Неопределено;
	КонецЕсли;	
	
	НайденныеДокументыОплаты = УчетКомандировок.ДокументыВыплатыПодотчетномуЛицу(
		Организация,
		?(ГраницаПоиска = Неопределено, ДатаАвансовогоОтчета, ГраницаПоиска.Дата),
		ФизЛицо,
		ВалютаДокумента,
		ТекущийДокумент,
		?(ПоказатьТолькоПоследние, 10, 0),
		ПоказатьТолькоПоследние,
		ГраницаПоиска);
				
	Для	Каждого ДокументОплаты Из НайденныеДокументыОплаты Цикл
		НовыйЭлементСписка = ДокументыОплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйЭлементСписка, ДокументОплаты);
		НовыйЭлементСписка.Выбран = (ВыбранныеДокументы.НайтиПоЗначению(НовыйЭлементСписка.Ссылка) <> Неопределено);	
	КонецЦикла;	
			
КонецПроцедуры

#КонецОбласти