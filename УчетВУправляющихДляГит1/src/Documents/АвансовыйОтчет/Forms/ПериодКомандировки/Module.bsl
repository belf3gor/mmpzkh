
#Область ОписаниеПеременных

&НаКлиенте
Перем ДействиеВыбрано;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ДатаНачала = Параметры.ДатаНачалаКомандировки;
	ДатаОкончания = Параметры.ДатаОкончанияКомандировки;
	
	Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		ДатаОкончания = Макс(ТекущаяДатаСеанса(), ДатаНачала);
	КонецЕсли;
	
	Календарь = ДобавитьМесяц(ДатаОкончания, - 1);
	ВыделитьПериод(Элементы.Календарь, ДатаНачала, ДатаОкончания);
	
	// Признак того, что текущая дата выбрана при создании формы.
	// Служит для того, чтобы при первом вводе интервала была возможность указать нужный интервал 2-мя кликами (дата начала - дата окончания),
	// а не 3-мя (т.к. без специальной обработки первый клик выбирает вторую границу интервала, т.к. первая граница уже задана при открытии - текущая дата).
	НачальныйВыбор = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	Если ДействиеВыбрано <> Истина Тогда
		ОповеститьОВыборе(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КалендарьПриАктивизацииДаты(Элемент)
	
	Если Элемент.ВыделенныеДаты.Количество() > 1 Тогда
		// Уже выделен период с помощью клавиши shift
		ДатаНачала = Элемент.ВыделенныеДаты[0];
		ДатаОкончания = Элемент.ВыделенныеДаты[Элемент.ВыделенныеДаты.Количество() - 1];
		Возврат;
	КонецЕсли;	
	
	ВыбраннаяДата = Элемент.ВыделенныеДаты[0];
	
	Если НЕ НачальныйВыбор И ДатаНачала = ДатаОкончания Тогда
		// Ранее уже выбрана одна из дат интервала.
		// Если выбрана новая дата не ранее уже выбранной, то это означает выбор даты окончания интервала.
		// В ином случае новая дата означает выбор начала интервала.
		Если ВыбраннаяДата >= ДатаНачала Тогда
			ДатаОкончания = ВыбраннаяДата;
		Иначе	
			ДатаНачала = ВыбраннаяДата;
		КонецЕсли;	
	Иначе
		// Ранее уже выбран интервал дат или это начальный выбор интервала.
		// Поэтому выбор даты здесь означает выбор нового интервала в один день.
		// Ожидаем, что следующим кликом пользователь выберет другую границу интервала (см. первую ветку условия).
		ДатаНачала = ВыбраннаяДата;
		ДатаОкончания = ВыбраннаяДата;
	КонецЕсли;	
	
	НачальныйВыбор = Ложь;
	
	Если ДатаНачала <> ДатаОкончания Тогда
		ВыделитьПериод(Элемент, ДатаНачала, ДатаОкончания);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
		
	ДействиеВыбрано = Истина;
	ОповеститьОВыборе(Новый Структура("ДатаНачала, ДатаОкончания", ДатаНачала, ДатаОкончания));
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ДействиеВыбрано = Истина;
	ОповеститьОВыборе(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВыделитьПериод(Элемент, ДатаНачала, ДатаОкончания)
		
	Элемент.ВыделенныеДаты.Очистить();
	
	ДатаКалендаря = ДатаНачала;
	Пока ДатаКалендаря <= ДатаОкончания Цикл
		Элемент.ВыделенныеДаты.Добавить(ДатаКалендаря);
		ДатаКалендаря = КонецДня(ДатаКалендаря) + 1;
	КонецЦикла;	
		
КонецПроцедуры

#КонецОбласти
