#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним реквизиты формы из параметров.
	Организация = Параметры.Организация;
	ДатаДокумента = Параметры.ДатаДокумента;
	Если ЗначениеЗаполнено(Параметры.АдресХранилищаСоставКомиссии) Тогда
		СоставКомиссии.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаСоставКомиссии));
	КонецЕсли;

	ПеренумероватьСтроки();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
	ИначеЕсли НЕ ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент И Модифицированность Тогда
		АдресХранилищаСоставКомиссии = ПоместитьСоставКомиссииВоВременноеХранилище();
		ОповеститьОВыборе(АдресХранилищаСоставКомиссии);
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура СоставКомиссииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.НомерСтроки = СоставКомиссии.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКомиссииПослеУдаления(Элемент)
	
	ПеренумероватьСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКомиссииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Для каждого ФизЛицо Из ВыбранноеЗначение Цикл
		
		НоваяСтрока = СоставКомиссии.Добавить();
		НоваяСтрока.ФизЛицо = ФизЛицо;
		ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Организация, ФизЛицо, ДатаДокумента);
		НоваяСтрока.Должность = ДанныеФизЛица.Должность;
		
	КонецЦикла;
	
	ПеренумероватьСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКомиссииФизЛицоПриИзменении(Элемент)
	
	ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(
		Организация,
		Элементы.СоставКомиссии.ТекущиеДанные.ФизЛицо,
		ДатаДокумента);
	Элементы.СоставКомиссии.ТекущиеДанные.Должность = ДанныеФизЛица.Должность;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПодборФизическихЛиц(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Истина);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыОткрытия.Вставить("АдресСпискаПодобранныхСотрудников", АдресСпискаПодобранныхСотрудников());
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыОткрытия, Элементы.СоставКомиссии,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПеренумероватьСтроки()
	
	Для Каждого Строка Из СоставКомиссии Цикл
		ИндексСтроки = СоставКомиссии.Индекс(Строка);
		СоставКомиссии[ИндексСтроки].НомерСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьСоставКомиссииВоВременноеХранилище()

	Возврат ПоместитьВоВременноеХранилище(СоставКомиссии.Выгрузить(), АдресХранилищаСоставКомиссии);

КонецФункции

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(ОбщегоНазначения.ВыгрузитьКолонку(СоставКомиссии, "ФизЛицо"), УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти 
