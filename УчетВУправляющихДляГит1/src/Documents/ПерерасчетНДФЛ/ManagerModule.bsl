#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Сотрудники.Сотрудник, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПерерасчетНДФЛ;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьПримененныеВычетыНаДетейИИмущественные() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерерасчетНДФЛСотрудники.Ссылка
		|ПОМЕСТИТЬ ВТДокументыДляОбработки
		|ИЗ
		|	Документ.ПерерасчетНДФЛ.Сотрудники КАК ПерерасчетНДФЛСотрудники
		|ГДЕ
		|	ПерерасчетНДФЛСотрудники.ИдентификаторСтрокиНДФЛ = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТДокументыДляОбработки.Ссылка
		|ИЗ
		|	ВТДокументыДляОбработки КАК ВТДокументыДляОбработки";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПерерасчетНДФЛСотрудники.НомерСтроки КАК ИдентификаторСтрокиНДФЛ,
			|	ПерерасчетНДФЛСотрудники.*
			|ПОМЕСТИТЬ ВТТЧСотрудники
			|ИЗ
			|	ВТДокументыДляОбработки КАК ДокументыДляОбработки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПерерасчетНДФЛ.Сотрудники КАК ПерерасчетНДФЛСотрудники
			|		ПО ДокументыДляОбработки.Ссылка = ПерерасчетНДФЛСотрудники.Ссылка";
			
		Запрос.Выполнить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка КАК Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ КАК ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетей КАК РазмерВычета,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код108) КАК КодВычета
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетей <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейДвойной,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код110)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейДвойной <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейДвойнойВторой,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код111)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейДвойнойВторой <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейИнвалидов,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код109)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейИнвалидов <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейИнвалидовДвойной,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код112)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейИнвалидовДвойной <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейИнвалидовДвойнойВторой,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код113)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаДетейИнвалидовДвойнойВторой <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаВторогоРебенка,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код115)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаВторогоРебенка <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаВторогоРебенкаДвойной,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код119)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаВторогоРебенкаДвойной <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаВторогоРебенкаДвойнойВторой,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код123)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаВторогоРебенкаДвойнойВторой <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаТретьегоРебенка,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код116)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаТретьегоРебенка <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаТретьегоРебенкаДвойной,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код120)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаТретьегоРебенкаДвойной <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетНаТретьегоРебенкаДвойнойВторой,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код124)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетНаТретьегоРебенкаДвойнойВторой <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетИмущественныйРасходы,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код311)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетИмущественныйРасходы <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетИмущественныйПроцентыПоКредитам,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код312)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетИмущественныйПроцентыПоКредитам <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка,
			|	ТЧСотрудники.ИдентификаторСтрокиНДФЛ,
			|	ТЧСотрудники.УдалитьПримененныйВычетИмущественныйПроцентыПриПерекредитовании,
			|	ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.Код318)
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|ГДЕ
			|	ТЧСотрудники.УдалитьПримененныйВычетИмущественныйПроцентыПриПерекредитовании <> 0
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка";
			
		ТаблицаВычетов = Запрос.Выполнить().Выгрузить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТЧСотрудники.Ссылка КАК Ссылка,
			|	ТЧСотрудники.*
			|ИЗ
			|	ВТТЧСотрудники КАК ТЧСотрудники
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка";
			
		ТаблицаСотрудников = Запрос.Выполнить().Выгрузить();
			
		Пока Выборка.Следующий() Цикл
			
			СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			ДокументОбъект.Сотрудники.Загрузить(ТаблицаСотрудников.Скопировать(СтруктураПоиска));
			ДокументОбъект.ПримененныеВычетыНаДетейИИмущественные.Загрузить(ТаблицаВычетов.Скопировать(СтруктураПоиска));
			
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
