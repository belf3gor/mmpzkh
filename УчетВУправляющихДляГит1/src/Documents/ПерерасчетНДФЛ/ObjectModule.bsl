#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, Организация, КонецМесяца(МесяцПерерасчета), ДанныеДляПроведения.НалогиВычеты, , , Дата(НалоговыйПериод + 1, 1, 1));
	
	// Заполним описание данных для проведения в учете начисленной зарплаты.
	ДанныеДляПроведенияУчетЗарплаты = ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения();
	ДанныеДляПроведенияУчетЗарплаты.Движения 				= Движения;
	ДанныеДляПроведенияУчетЗарплаты.Организация 			= Организация;
	ДанныеДляПроведенияУчетЗарплаты.ПериодРегистрации 		= МесяцПерерасчета;
	ДанныеДляПроведенияУчетЗарплаты.ПорядокВыплаты 			= Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
	ДанныеДляПроведенияУчетЗарплаты.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;

	УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.РезультатыРасчетаНДФЛ, Организация, КонецМесяца(МесяцПерерасчета));
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛИКорректировкиВыплаты(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.РезультатыРасчетаНДФЛ, ДанныеДляПроведения.РезультатыРасчетаКорректировкиВыплаты);
	
	ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
	ДанныеДляПроведения.НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Выгрузить();
	СтрокаСписокТаблиц = "НачисленныйНДФЛ";
	ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, НачалоМесяца(МесяцПерерасчета), ДанныеДляПроведения, СтрокаСписокТаблиц);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл
		
		СуммаВычетовНаДетейИИмущественных = ПримененныеВычетыНаДетейИИмущественные.Выгрузить(
			Новый Структура("ИдентификаторСтрокиНДФЛ", СтрокаСотрудника.ИдентификаторСтрокиНДФЛ), "РазмерВычета").Итог("РазмерВычета");
		
			Если СтрокаСотрудника.НалогПоСтавке09 * СтрокаСотрудника.НалогПоСтавке09 
				+ СтрокаСотрудника.НалогПоСтавке13 * СтрокаСотрудника.НалогПоСтавке13 
				+ СтрокаСотрудника.НалогПоСтавке35 * СтрокаСотрудника.НалогПоСтавке35
				+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке09 * СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке09
				+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке13 * СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке13
				+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке35 * СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке35
				+ СтрокаСотрудника.ПримененныйВычетЛичный * СтрокаСотрудника.ПримененныйВычетЛичный
				+ СтрокаСотрудника.ПримененныйВычетЛичныйКЗачетуВозврату * СтрокаСотрудника.ПримененныйВычетЛичныйКЗачетуВозврату
				+ СуммаВычетовНаДетейИИмущественных * СуммаВычетовНаДетейИИмущественных = 0 Тогда
			
			НалогКЗачетуВозврату = 0;
			НайденныеСтроки = КорректировкиВыплаты.НайтиСтроки(Новый Структура("ФизическоеЛицо", СтрокаСотрудника.Сотрудник));
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НалогКЗачетуВозврату = НалогКЗачетуВозврату + НайденнаяСтрока.КорректировкаВыплаты;
			КонецЦикла;
			
			Если НалогКЗачетуВозврату = 0 Тогда
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке №%1 не указаны суммы перерасчета налога.'"), СтрокаСотрудника.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения()
	
	СписокФизическихЛиц = Неопределено;
	Если ДополнительныеСвойства.Свойство("ФизическиеЛица")
		И ДополнительныеСвойства.ФизическиеЛица.Количество() > 0 Тогда
		
		СписокФизическихЛиц = ДополнительныеСвойства.ФизическиеЛица;
		ОтборПоФизическимЛицам = Истина;
		
	Иначе
		ОтборПоФизическимЛицам = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ЗарплатаКадры.ГоловнаяОрганизация(Организация));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерерасчетНДФЛСотрудники.Ссылка КАК Ссылка,
	|	ПерерасчетНДФЛСотрудники.НомерСтроки КАК НомерСтроки,
	|	ПерерасчетНДФЛСотрудники.ОбособленноеПодразделение КАК Подразделение,
	|	ПерерасчетНДФЛСотрудники.Сотрудник КАК ФизическоеЛицо,
	|	ПерерасчетНДФЛСотрудники.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ПерерасчетНДФЛСотрудники.КатегорияДохода КАК КатегорияДохода,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке09 КАК НалогПоСтавке09,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13 КАК НалогПоСтавке13,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке35 КАК НалогПоСтавке35,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13 КАК Налог,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичный КАК ПримененныйВычетЛичный,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКодВычета КАК ПримененныйВычетЛичныйКодВычета,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКЗачетуВозврату КАК ПримененныйВычетЛичныйКЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКЗачетуВозвратуКодВычета КАК ПримененныйВычетЛичныйКЗачетуВозвратуКодВычета,
	|	ПерерасчетНДФЛСотрудники.ИдентификаторСтрокиНДФЛ КАК ИдентификаторСтрокиНДФЛ,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке09 КАК ЗачтеноАвансовыхПлатежейПоСтавке09,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК ЗачтеноАвансовыхПлатежейПоСтавке13,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК ЗачтеноАвансовыхПлатежей,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке35 КАК ЗачтеноАвансовыхПлатежейПоСтавке35
	|ПОМЕСТИТЬ ВТСтрокиДокумента
	|ИЗ
	|	Документ.ПерерасчетНДФЛ.Сотрудники КАК ПерерасчетНДФЛСотрудники
	|ГДЕ
	|	ПерерасчетНДФЛСотрудники.Ссылка = &Ссылка
	|	И ПерерасчетНДФЛСотрудники.Сотрудник В(&ФизическиеЛица)";
	
	Если ОтборПоФизическимЛицам Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица", СписокФизическихЛиц);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПерерасчетНДФЛСотрудники.Сотрудник В(&ФизическиеЛица)", "");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	ОписательВТ = КадровыйУчет.ОписаниеВременнойТаблицыОтборовФизическихЛиц("ВТСтрокиДокумента");
	
	КадровыйУчет.СоздатьВТТекущиеКадровыеДанныеСотрудниковФизическихЛиц(
		Запрос.МенеджерВременныхТаблиц, ОписательВТ, Ложь, Организация, "ТекущееПодразделение,ОсновноеРабочееМестоВОрганизации,ГоловнаяОрганизация", "ВТТекущиеКадровыеДанныеСотрудников");

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПерерасчетНДФЛКорректировкиВыплаты.Ссылка КАК Ссылка,
	|	ПерерасчетНДФЛКорректировкиВыплаты.НомерСтроки КАК НомерСтроки,
	|	ПерерасчетНДФЛКорректировкиВыплаты.ОбособленноеПодразделение КАК Подразделение,
	|	ПерерасчетНДФЛКорректировкиВыплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПерерасчетНДФЛКорректировкиВыплаты.КорректировкаВыплаты КАК КорректировкаВыплаты,
	|	ПерерасчетНДФЛКорректировкиВыплаты.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиКорректировкиВыплатыДокумента
	|ИЗ
	|	Документ.ПерерасчетНДФЛ.КорректировкиВыплаты КАК ПерерасчетНДФЛКорректировкиВыплаты
	|ГДЕ
	|	ПерерасчетНДФЛКорректировкиВыплаты.Ссылка = &Ссылка
	|	И ПерерасчетНДФЛКорректировкиВыплаты.ФизическоеЛицо В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.Подразделение КАК Подразделение,
	|	СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДокумента.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СтрокиДокумента.НалогПоСтавке09 КАК НалогПоСтавке09,
	|	СтрокиДокумента.НалогПоСтавке35 КАК НалогПоСтавке35,
	|	СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке09 КАК ЗачтеноАвансовыхПлатежейПоСтавке09,
	|	СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке35 КАК ЗачтеноАвансовыхПлатежейПоСтавке35
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|ГДЕ
	|	(СтрокиДокумента.НалогПоСтавке35 <> 0
	|			ИЛИ СтрокиДокумента.НалогПоСтавке09 <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТСтрокиДокумента КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	0 КАК СуммаДохода,
	|	0 КАК СуммаВычета,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение
	|ПОМЕСТИТЬ ВТДанныеДляРасчетаНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДанныеДокумента.Сумма) КАК Сумма,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеСотрудника,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Сотрудник,
	|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ДанныеДокумента.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке13 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК Сумма,
	|		СтрокиДокумента.Подразделение КАК Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|		СтрокиДокумента.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке13 <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке09 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|		СтрокиДокумента.Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо,
	|		СтрокиДокумента.МесяцНалоговогоПериода
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке09 <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке35 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке35,
	|		СтрокиДокумента.Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо,
	|		СтрокиДокумента.МесяцНалоговогоПериода
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке35 <> 0) КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО (ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо)
	|			И (ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение,
	|	ДанныеДокумента.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДанныеДокумента.КорректировкаВыплаты) КАК КорректировкаВыплаты,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеСотрудника,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Сотрудник,
	|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	ВТСтрокиКорректировкиВыплатыДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО (ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо)
	|			И (ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации)
	|ГДЕ
	|	ДанныеДокумента.КорректировкаВыплаты <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение";
	
	Если Не ОтборПоФизическимЛицам Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПерерасчетНДФЛКорректировкиВыплаты.ФизическоеЛицо В(&ФизическиеЛица)", "");
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ОписаниеТаблиц = Новый Структура("ИмяТаблицыСНалогами,ИмяТаблицыСВычетами", "ВТСтрокиДокумента", "Документ.ПерерасчетНДФЛ.ПримененныеВычетыНаДетейИИмущественные");
	НалогиВычеты = УчетНДФЛ.ДанныеДокументаОНалогеПоОсновнойСтавкеИВычетах(Ссылка, ОписаниеТаблиц, Запрос.МенеджерВременныхТаблиц);
	
	Если Не Результаты[1].Пустой() Тогда
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке09", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке35", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("ЗачтеноАвансовыхПлатежейПоСтавке09", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("ЗачтеноАвансовыхПлатежейПоСтавке35", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		Выборка = Результаты[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НалогиВычеты, Выборка);
		КонецЦикла;
	КонецЕсли;
	
	РезультатыРасчетаНДФЛ = Результаты[4].Выгрузить();
	РезультатыРасчетаКорректировкиВыплаты = Результаты[5].Выгрузить();
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		РезультатыРасчета = Новый Структура("РезультатыРасчетаНДФЛ,РезультатыРасчетаКорректировкиВыплаты",РезультатыРасчетаНДФЛ,РезультатыРасчетаКорректировкиВыплаты);
		ОтражениеЗарплатыВБухучетеВнутренний.ПерерасчетНДФЛРаспределитьПоСтатьям(Организация, НачалоМесяца(МесяцПерерасчета), Ссылка, РезультатыРасчета);
		РезультатыРасчетаНДФЛ = РезультатыРасчета.РезультатыРасчетаНДФЛ;
		РезультатыРасчетаКорректировкиВыплаты = РезультатыРасчета.РезультатыРасчетаКорректировкиВыплаты;
	КонецЕсли;
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("НалогиВычеты", НалогиВычеты);
	ДанныеДляПроведения.Вставить("РезультатыРасчетаНДФЛ", РезультатыРасчетаНДФЛ);
	ДанныеДляПроведения.Вставить("РезультатыРасчетаКорректировкиВыплаты", РезультатыРасчетаКорректировкиВыплаты);
	ДанныеДляПроведения.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
