
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	

	Если ДополнительныеСвойства.Свойство("ФайлРегистраУчета") Тогда
		
		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Описание);
		
		ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
		НастройкиФормата = ТаблицаФорматов.НайтиСтроки(Новый Структура("Ссылка", ФорматСохраненияФайла))[0];
		
		ПараметрыФайла = Новый Структура();
		ПараметрыФайла.Вставить("Автор", Пользователи.ТекущийПользователь());
		ПараметрыФайла.Вставить("ВладелецФайлов", Ссылка);
		ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяФайла);
		ПараметрыФайла.Вставить("РасширениеБезТочки", НастройкиФормата.Расширение);
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", Дата);
		
		ДобавленныйФайл = РаботаСФайлами.ДобавитьФайл(
			ПараметрыФайла, ДополнительныеСвойства.ФайлРегистраУчета, "", Описание);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ЭтотОбъект.Дата = ТекущаяДатаСеанса();
		ЭтотОбъект.ФорматСохраненияФайла = Документы.РегистрУчета.ПолучитьФорматСохраненияРегистров();
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
