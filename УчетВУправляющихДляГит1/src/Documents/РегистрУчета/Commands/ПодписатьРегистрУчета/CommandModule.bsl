
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СвойстваФайла = ПолучитьСвойстваПрисоединенногоФайла(ПараметрКоманды);
	
	СообщениеОбОшибке = "";
	
	Если НЕ СвойстваФайла.ФайлРегистраУчетаПрисоединен Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = '%1 не содержит присоединенного файла'"), ПараметрКоманды);
	КонецЕсли;
	
	Если ПустаяСтрока(СообщениеОбОшибке) И НЕ СвойстваФайла.ИспользоватьЭП Тогда
		СообщениеОбОшибке = НСтр("ru = 'Для подписи регистра включите использование ЭП в настройках программы'");
	КонецЕсли;
	
	Если ПустаяСтрока(СообщениеОбОшибке) И СвойстваФайла.ПодписанЭП Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = '%1 уже подписан ЭП'"), ПараметрКоманды);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ПараметрКоманды);
		Возврат;
	КонецЕсли;
	
	Если Тип(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") Тогда
		УникальныйИдентификаторФормы = ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор;
	Иначе
		УникальныйИдентификаторФормы = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	РаботаСФайламиКлиент.ПодписатьФайл(СвойстваФайла.ПрисоединенныйФайл, УникальныйИдентификаторФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСвойстваПрисоединенногоФайла(ДокументРегистр)
	
	СвойстваФайла = Документы.РегистрУчета.ПолучитьСвойстваПрисоединенногоФайлаРегистра(ДокументРегистр, Истина);
	
	СвойстваФайла.Вставить("ИспользоватьЭП", ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи"));
	
	Возврат СвойстваФайла;
	
КонецФункции

#КонецОбласти
