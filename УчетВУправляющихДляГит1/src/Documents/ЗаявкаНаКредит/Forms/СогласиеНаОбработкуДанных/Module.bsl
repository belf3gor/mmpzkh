
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.ПараметрыСогласия) Тогда
		ВызватьИсключение НСтр("ru = 'Не указан параметры для подготовки текста согласия'");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметры.Банки) Тогда
		ВызватьИсключение НСтр("ru = 'Не указан параметр ""Банк""'");
	КонецЕсли;
	
	ТекстСогласияHTML = ТекстПросьбыПодождать();
	ТекстСогласия.УстановитьHTML(ТекстСогласияHTML, Новый Структура);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ПараметрыСогласия", Параметры.ПараметрыСогласия);
	ПараметрыЗаполнения.Вставить("Банки",             Параметры.Банки);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение согласия на обработку данных из заявки на кредит'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("ЗаявкиНаКредит.ЗаполнитьТекстСогласияВФоне",
		ПараметрыЗаполнения, ПараметрыВыполненияВФоне);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОжидатьЗаполненияСогласия", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандныхПанелейФормы


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстПросьбыПодождать()
	
	ТекстПросьбы = 
	"<HTML><HEAD>
	|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
	|<META name=GENERATOR content=""MSHTML 11.00.9600.19180""></HEAD>
	|<BODY><FONT size=18><STRONG>%1</STRONG></FONT></BODY></HTML>";
	
	ТекстПросьбы = СтрШаблон(ТекстПросьбы, НСтр("ru = 'Пожалуйста, подождите...'"));
	
	Возврат ТекстПросьбы;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОжидатьЗаполненияСогласия()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьЗаполненноеСогласие", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Заполнение согласия на обработку данных'");
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗаполненноеСогласие(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки, , "ТекстСогласия");
		Возврат;
	КонецЕсли;
	
	// Получаем из фонового задания подготовленный текст и вложения.
	Если ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
		УстановитьТекстСогласия(Результат.АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстСогласия(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(Результат) <> Тип("ХранилищеЗначения") Тогда
		Возврат;
	КонецЕсли;

	ТекстСогласия.УстановитьHTML(Результат.Получить(), Новый Структура);
	
КонецПроцедуры

#КонецОбласти
