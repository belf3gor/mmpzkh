
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицо = Параметры.ФизическоеЛицо;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("Дата", Параметры.Дата);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГражданствоФизическихЛиц.Период
	|ИЗ
	|	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних(&Дата, ФизическоеЛицо = &ФизическоеЛицо) КАК ГражданствоФизическихЛиц";

	МенеджерЗаписи = РегистрыСведений.ГражданствоФизическихЛиц.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ФизическоеЛицо = ФизическоеЛицо;

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		МенеджерЗаписи.Период = Выборка.Период;
		МенеджерЗаписи.Прочитать();
	Иначе
		МенеджерЗаписи.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений();
		МенеджерЗаписи.Страна = Справочники.СтраныМира.Россия;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "ГражданствоФизическихЛиц");
	ГражданствоФизическихЛицПрежняяСтрана = МенеджерЗаписи.Страна;
	
	// Выставим переключатель в зависимости от заполнения страны.
	Если ЗначениеЗаполнено(МенеджерЗаписи.Страна) Тогда
		ГражданствоФизическихЛицЛицоБезГражданства = 0;
	Иначе
		ГражданствоФизическихЛицЛицоБезГражданства = 1;
	КонецЕсли;
	
	ОбновитьДоступностьПолейВводаГражданства(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьИЗакрыть();

	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	ПроверяемыеРеквизиты.Очистить();	

	Если ГражданствоФизическихЛицЛицоБезГражданства = 0 
		И НЕ ЗначениеЗаполнено(ГражданствоФизическихЛиц.Страна) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Страна'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "ГражданствоФизическихЛиц.Страна", Отказ);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ГражданствоФизическихЛиц.Период) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Действует с'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "ГражданствоФизическихЛиц.Период", Отказ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГражданствоФизическихЛицЛицоБезГражданстваПриИзменении(Элемент)
	
	Если ГражданствоФизическихЛицЛицоБезГражданства = 0 Тогда
		
		ГражданствоФизическихЛиц.Страна = ГражданствоФизическихЛицПрежняяСтрана;
		Если НЕ ЗначениеЗаполнено(ГражданствоФизическихЛиц.Страна) Тогда
			ГражданствоФизическихЛиц.Страна = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
			ГражданствоФизическихЛиц.ИНН = "";
		КонецЕсли;
		
	Иначе
		
		ГражданствоФизическихЛиц.Страна = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
		ГражданствоФизическихЛиц.ИНН = "";
		
	КонецЕсли;
	
	ОбновитьДоступностьПолейВводаГражданства(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоФизическихЛицСтранаПриИзменении(Элемент)
	
	ОбновитьДоступностьПолейВводаГражданства(ЭтотОбъект);
	Если ГражданствоФизическихЛиц.Страна = ПредопределенноеЗначение("Справочник.СтраныМира.Россия") Тогда
		ГражданствоФизическихЛиц.ИНН = "";
	КонецЕсли;
	
	ГражданствоФизическихЛицПрежняяСтрана = ГражданствоФизическихЛиц.Страна;
	
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоФизическихЛицСтранаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.СтранаМираОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоФизическихЛицИННПриИзменении(Элемент)
	
	ОбновитьДоступностьПолейВводаГражданства(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьПолейВводаГражданства(Форма)
	
	Форма.Элементы.ГражданствоФизическихЛицСтрана.Доступность = (Форма.ГражданствоФизическихЛицЛицоБезГражданства = 0);
	Форма.Элементы.ГражданствоФизическихЛицИНН.Доступность = 
		Форма.ГражданствоФизическихЛицЛицоБезГражданства = 0
		И Форма.ГражданствоФизическихЛиц.Страна <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Если СохранитьНаСервере() Тогда
		Оповестить("Запись_ГражданствоФизЛица",, ФизическоеЛицо);
		Закрыть(ГражданствоФизическихЛиц.Страна);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНаСервере()

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;

	МенеджерЗаписи = РеквизитФормыВЗначение("ГражданствоФизическихЛиц");
	МенеджерЗаписи.ФизическоеЛицо = ФизическоеЛицо;
	
	Документы.ЗаявкаНаКредит.ЗаписатьГражданствоФизЛица(МенеджерЗаписи);
	
	Модифицированность = Ложь;

	Возврат Истина;

КонецФункции

#КонецОбласти
