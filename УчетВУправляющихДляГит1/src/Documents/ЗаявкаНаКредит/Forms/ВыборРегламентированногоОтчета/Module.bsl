#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Запоминаем переданные параметры.
	Заголовок      = Параметры.Заголовок;
	Организация    = Параметры.Организация;
	ДатаНачала     = Параметры.ДатаНачала;
	ДатаОкончания  = Параметры.ДатаОкончания;
	ИсточникОтчета = Параметры.ИсточникОтчета;

	ЗаполнитьРегламентированныеОтчеты();
	
	// Кнопки создания показываем только, если требуется бух.отчетность.
	Элементы.ФормаГруппаСоздатьБухОтчетность.Видимость = (ИсточникОтчета = "БухгалтерскаяОтчетность");
	
	// Спозиционируемся на отчете в списке.
	ВыделитьОтчетВСписке(Параметры.ВыделенныйОтчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_РегламентированныйОтчет" ИЛИ ИмяСобытия = "Позиционирование в списке отчетов" Тогда
		// Обновим список после записи регламентированного отчета.
		Если ТипЗнч(Параметр) <> Тип("Структура")
			ИЛИ НЕ Параметр.Свойство("Ссылка")
			ИЛИ НЕ Параметр.Свойство("Организация") Тогда
			Возврат
		КонецЕсли;
		
		Если Параметр.Организация <> Организация Тогда
			// Регл.отчет не относится к текущей организации.
			Возврат;
		КонецЕсли;

		ОбработкаОповещенияЗаписьРегламентированногоОтчета(Параметр);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура РегламентированныеОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборОтчета();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОтчет(Команда)
	
	ОбработатьВыборОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	ТекущиеДанные = Элементы.РегламентированныеОтчеты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.РегламентированныйОтчет);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьБухОтчетность(Команда)
	ОткрытьФормуНовогоБухОтчетность("РегламентированныйОтчетБухОтчетность");
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБухОтчетностьМП(Команда)
	ОткрытьФормуНовогоБухОтчетность("РегламентированныйОтчетБухОтчетностьМП");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРегламентированныеОтчеты()

	// Запомним отчет, который был выделен до перестроения списка, чтобы потом к нему вернуться.
	Если Элементы.РегламентированныеОтчеты.ТекущаяСтрока <> Неопределено Тогда
		ДанныеСтроки = РегламентированныеОтчеты.НайтиПоИдентификатору(Элементы.РегламентированныеОтчеты.ТекущаяСтрока);
		Если ДанныеСтроки <> Неопределено Тогда
			ВыделенныйОтчет = ДанныеСтроки.РегламентированныйОтчет;
		КонецЕсли;
	Конецесли;

	РегламентированныеОтчеты.Очистить();

	ЭтоВыборБухОтчетности = (ИсточникОтчета = "БухгалтерскаяОтчетность");
	
	МетаданныеДокумента = Метаданные.Документы.ЗаявкаНаКредит;
	
	// Выполняем поиск среди сохранных в базе регламентированных отчетов.
	ПараметрыРеглОтчетов = Новый ТаблицаЗначений;
	ПараметрыРеглОтчетов.Колонки.Добавить("ИсточникОтчета", МетаданныеДокумента.ТабличныеЧасти.Отчетность.Реквизиты.ИсточникОтчета.Тип);
	ПараметрыРеглОтчетов.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПараметрыРеглОтчетов.Колонки.Добавить("ДатаОкончания",  ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ПараметрыРеглОтчетов.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ПараметрыРеглОтчетов.Колонки.Добавить("Правило",                     Новый ОписаниеТипов("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов"));
	
	Если ЭтоВыборБухОтчетности Тогда
		// Выполняем поиск среди всех возможных источников бух.отчетности.
		ИсточникиБухОтчетности = Документы.ЗаявкаНаКредит.ИсточникБухгалтерскойОтчетности();
		
		Для каждого ИсточникБухОтчетности Из ИсточникиБухОтчетности Цикл
			ПараметрРеглОтчета = ПараметрыРеглОтчетов.Добавить();
			ПараметрРеглОтчета.ИсточникОтчета = ИсточникБухОтчетности;
			ПараметрРеглОтчета.Организация    = Организация;
			ПараметрРеглОтчета.ДатаОкончания  = ДатаОкончания;
		КонецЦикла;
		
	Иначе
	
		ПараметрРеглОтчета = ПараметрыРеглОтчетов.Добавить();
		ПараметрРеглОтчета.ИсточникОтчета = ИсточникОтчета;
		ПараметрРеглОтчета.Организация    = Организация;
		ПараметрРеглОтчета.ДатаОкончания  = ДатаОкончания;
	
	КонецЕсли;
	
	// Выберем все подходящие отчеты.
	ИнтерфейсыВзаимодействияБРО.ЗаполнитьСсылкиНаРеглОтчеты(ПараметрыРеглОтчетов);
	
	// Переносим найденные отчеты в таблицу на форме, попутно проверяя статус сдачи.
	Для каждого ПараметрыРеглОтчета Из ПараметрыРеглОтчетов Цикл
	
		Для каждого РеглОтчет Из ПараметрыРеглОтчета.Документы Цикл
			
			Если НЕ ЭтоВыборБухОтчетности Тогда
				// Налоговые отчеты показываем только те, которые были сданы в ФНС через 1С-Отчетность.
				Если РеглОтчет.СостояниеСдачиОтчетности <> Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока = РегламентированныеОтчеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, РеглОтчет);
			НоваяСтрока.РегламентированныйОтчет = РеглОтчет.Ссылка;
			НоваяСтрока.ИсточникОтчета          = ИсточникОтчета;
		
		КонецЦикла;
	
	КонецЦикла;
		
	// Спозиционируемся на отчете, который был выделен до обновления списка.
	ВыделитьОтчетВСписке(ВыделенныйОтчет);

КонецПроцедуры

&НаСервере
Процедура ВыделитьОтчетВСписке(ВыделенныйОтчет)

	Если НЕ ЗначениеЗаполнено(ВыделенныйОтчет) Тогда
		Возврат;
	КонецЕсли;

	Отбор = Новый Структура();
	Отбор.Вставить("РегламентированныйОтчет", ВыделенныйОтчет);
	НайденныеСтроки = РегламентированныеОтчеты.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.РегламентированныеОтчеты.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборОтчета()

	ТекущиеДанные = Элементы.РегламентированныеОтчеты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОтчета = ОписаниеОтчета(ТекущиеДанные.ПолучитьИдентификатор());
	
	Если ОписаниеОтчета.Свойство("ТекстПредупреждения")
		И ЗначениеЗаполнено(ОписаниеОтчета.ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ОписаниеОтчета.ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура();
	РезультатВыбора.Вставить("РегламентированныйОтчет", ТекущиеДанные.РегламентированныйОтчет);
	РезультатВыбора.Вставить("НаименованиеОтчета",      ОписаниеОтчета.НаименованиеОтчета);
	РезультатВыбора.Вставить("СостояниеОтчета",         ОписаниеОтчета.СостояниеОтчета);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеОтчета(ИдСтроки)

	ТекущиеДанные = РегламентированныеОтчеты.НайтиПоИдентификатору(ИдСтроки);
	
	Результат = Документы.ЗаявкаНаКредит.ОписаниеОтчета(ТекущиеДанные);

	// Пока не утвержден формат выгрузки бух.отчетности по форме 2019 года
	// используем форму как в 2018 году, чтобы ее можно было выгрузить для передачи в банк в электронном виде.
	// Поэтому даем выбрать только ее.
	Если ТипЗнч(ТекущиеДанные.РегламентированныйОтчет) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		ИсточникРегОтчета = РегламентированнаяОтчетностьВызовСервера.ИсточникРегламентированногоОтчета(ТекущиеДанные.РегламентированныйОтчет);
		
		Если ИсточникРегОтчета = "РегламентированныйОтчетБухОтчетность" И ДатаОкончания >= '2019-01-01' Тогда 
			ВыбраннаяФорма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеДанные.РегламентированныйОтчет, "ВыбраннаяФорма");
			Если ВыбраннаяФорма <> "ФормаОтчета2011Кв4" Тогда
				Результат.Вставить("ТекстПредупреждения", 
					НСтр("ru = 'Формат выгрузки для бухгалтерской отчетности за 2019 год не утвержден.
						|Для подачи заявки на кредит сейчас выберите отчетность по форме в редакции приказа Минфина России от 06.03.2018 № 41н.'"));
			КонецЕсли;

		ИначеЕсли ИсточникРегОтчета = "РегламентированныйОтчетБухОтчетностьМП" И ДатаОкончания >= '2019-01-01' Тогда
			Результат.Вставить("ТекстПредупреждения", 
				НСтр("ru = 'Формат выгрузки для упрощенной бухгалтерской отчетности за 2019 год не утвержден.
					|Для подачи заявки на кредит сейчас выберите отчетность по общей форме в редакции приказа Минфина России от 06.03.2018 № 41н.'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуНовогоБухОтчетность(ИсточникБухОтчетности)

	// Открываем форму нового отчета.
	ПараметрыФормы = ПараметрыОткрытияФормыНовогоБухОтчетность(ИсточникБухОтчетности);
	
	Если ПараметрыФормы = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не удалось определить подходящую редакцию формы, попробуйте создать отчет в меню ""Отчеты - 1С-Отчетность - Регламентированные отчеты""'"));
		Возврат;
	КонецЕсли;

	Если ИсточникБухОтчетности = "РегламентированныйОтчетБухОтчетность" И ДатаОкончания >= '2019-01-01' Тогда
		// Пока не утвержден формат выгрузки бух.отчетности по форме 2019 года
		// используем форму как в 2018 году, чтобы ее можно было выгрузить для передачи в банк в электронном виде.
		ПараметрыФормы.мВыбраннаяФорма = "ФормаОтчета2011Кв4";

	ИначеЕсли ИсточникБухОтчетности = "РегламентированныйОтчетБухОтчетностьМП" И ДатаОкончания >= '2019-01-01' Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Формат выгрузки для упрощенной бухгалтерской отчетности за 2019 год не утвержден.
								|Для подачи заявки на кредит сейчас подготовьте отчетность по общей форме в редакции приказа Минфина России от 06.03.2018 № 41н.'"));
		Возврат;
	КонецЕсли;

	ИмяФормыОтчета = "Отчет." + ИсточникБухОтчетности + ".Форма." + ПараметрыФормы.мВыбраннаяФорма;

	ОткрытьФорму(ИмяФормыОтчета, ПараметрыФормы);

КонецПроцедуры

&НаСервере
Функция ПараметрыОткрытияФормыНовогоБухОтчетность(Знач ИсточникБухОтчетности)

	Результат = ИнтерфейсыВзаимодействияБРО.ПараметрыОткрытияФормыРеглОтчета(
		ИсточникБухОтчетности,
		Организация,
		Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка(),
		ДатаНачала,
		ДатаОкончания,
		Перечисления.Периодичность.Год);

	Возврат Результат;

КонецФункции

&НаСервере
Процедура ОбработкаОповещенияЗаписьРегламентированногоОтчета(Параметр)

	СвойстваОтчета = ИнтерфейсыВзаимодействияБРО.СвойстваОтчета(Параметр.Ссылка);
	
	Если ИсточникОтчета = "БухгалтерскаяОтчетность" Тогда
		ИсточникиБухОтчетности = Документы.ЗаявкаНаКредит.ИсточникБухгалтерскойОтчетности();
		Если ИсточникиБухОтчетности.Найти(СвойстваОтчета.ИсточникОтчета) = Неопределено Тогда
			Возврат;
		КонецЕсли;

	ИначеЕсли СвойстваОтчета.ИсточникОтчета <> ИсточникОтчета Тогда
		// Отчет не совпадает с текущим отбором, игнорируем его изменение.
		Возврат;
	КонецЕсли;
	
	Если СвойстваОтчета.ДатаОкончанияОП <> ДатаОкончания Тогда
		// Отчет за другой период.
		Возврат;
	КонецЕсли;
	
	// Обновляем список отчетов и позиционируемся на отчет из оповещения.
	ЗаполнитьРегламентированныеОтчеты();
	
	ВыделитьОтчетВСписке(Параметр.Ссылка);

КонецПроцедуры

#КонецОбласти