
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыИФункцииПечати

Функция ПечатьДоговор(МассивОбъектов, ОбъектыПечати, ИмяМакета)
	
	РезДокумент = Новый ТабличныйДокумент;
	
	Для Каждого Объект Из МассивОбъектов Цикл
	
		МестоРегистрации = ПолучитьСокрАдрес(Объект.Проживающий.Владелец.Адрес.Владелец, Перечисления.КВП_ВидыАдресов.Здание);
		
		Город = МестоРегистрации.Город;
		
		Город = ?(Город = "", МестоРегистрации.НаселенныйПункт, Город);
		
		Город = ?(Город = "", МестоРегистрации.Регион, Город);
	
		ТабДок = Новый ТабличныйДокумент;
		Макет = ПолучитьМакет(ИмяМакета);
		
		Область = Макет.ПолучитьОбласть("Область");
		
		// Общие
		Область.Параметры.Город    = Город;
		Область.Параметры.Дата     = Объект.Дата;
		Область.Параметры.Адрес    = ""+Объект.ЛицевойСчет.Адрес.Владелец+", "+Объект.ЛицевойСчет.Адрес;
		
		// Реквизиты документа на право собственности
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УПЖКХ_СобственникиПомещенийСрезПоследних.НомерДокумента,
		|	УПЖКХ_СобственникиПомещенийСрезПоследних.ДатаДокумента
		|ИЗ
		|	РегистрСведений.УПЖКХ_СобственникиПомещений.СрезПоследних(
		|			,
		|			Собственник = &Собственник
		|				И Помещение = &Помещение
		|				И Действует) КАК УПЖКХ_СобственникиПомещенийСрезПоследних";
		
		Запрос.УстановитьПараметр("Помещение", Объект.ЛицевойСчет.Адрес);
		ТекОтветственныйСобственник = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьОтветственногоСобственникаЛицевогоСчета(Объект.ЛицевойСчет, Объект.Дата);
		
		ПараметрСобственник = Неопределено;
		
		Если ТипЗнч(ТекОтветственныйСобственник) = Тип("СправочникСсылка.УПЖКХ_Жильцы") Тогда
			ПараметрСобственник = ТекОтветственныйСобственник.ФизЛицо;
		ИначеЕсли ТипЗнч(ТекОтветственныйСобственник) = Тип("СправочникСсылка.Контрагенты") Тогда
			ПараметрСобственник = ТекОтветственныйСобственник;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Собственник", ПараметрСобственник);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
			Область.Параметры.РеквизитыДоговора = ПолучитьПредставлениеДокументаСобственности(Выборка.НомерДокумента, Выборка.ДатаДокумента);
		КонецЕсли;
	
		// 1. Ссудодатель
		ДанныеОбОтв = ПолучитьДанныеОбОтветственном(ТекОтветственныйСобственник, Объект.Организация, Объект.Дата);
	
		// 1.1 ФЛ
		Если ДанныеОбОтв.ТипОтв = "ФизическиеЛица" Тогда
			
			Область.Параметры.АдресФиз = ДанныеОбОтв.АдресПрописка;
			
			Область.Параметры.ФИО          = ДанныеОбОтв.Представление;
			Область.Параметры.ГодРождения  = Формат(Год(ДанныеОбОтв.ДатаРождения),"ЧЦ=4; ЧН=; ЧГ=");
	
			// Область.Параметры.ДатаНачала = "________________";
			// Область.Параметры.ДатаКонца =  "________________";
	
			ДокУдостоверениеЛичности = СокрЛП(""+ДанныеОбОтв.ВидДокумента+" серия:"+ДанныеОбОтв.Серия+" номер:"+ДанныеОбОтв.Номер
			+" выдан "+ДанныеОбОтв.КемВыдан+" "+Формат(ДанныеОбОтв.ДатаВыдачи, "ДФ=dd.MM.yyyy"));

			Область.Параметры.ДокУдостоверениеЛичности = ДокУдостоверениеЛичности;
			
			ФИОПаспорт = ДанныеОбОтв.Фамилия+" "+ДанныеОбОтв.Имя+" "+ДанныеОбОтв.Отчество
			+". "+ДокУдостоверениеЛичности
			+". Зарегистрирован по адресу:"+ДанныеОбОтв.АдресПрописка;
	
			Область.Параметры.ФИОПаспорт = ФИОПаспорт;
	
		// 1.2 Контрагент
		ИначеЕсли ДанныеОбОтв.ТипОтв = "Контрагенты" Тогда
	
			Область.Параметры.АдресФиз = ДанныеОбОтв.Адрес;
			
			Область.Параметры.ФИО  = ДанныеОбОтв.ФИО;
	
			ДокУдостоверениеЛичности = СокрЛП(ДанныеОбОтв.ДокументУдостоверяющийЛичность);
	
			Область.Параметры.ДокУдостоверениеЛичности = СокрЛП(ДокУдостоверениеЛичности);
	
			ФИОПаспорт = ДанныеОбОтв.ФИО
			+". "+ДокУдостоверениеЛичности
			+". Зарегистрирован по адресу:"+ДанныеОбОтв.Адрес;
	
			Область.Параметры.ФИОПаспорт = ФИОПаспорт;
	
		КонецЕсли;
	
		// 2. Ссудополучатель
		Ссудополучатель = УПЖКХ_ТиповыеМетодыВызовСервера.ДанныеФизЛица(Объект.Организация, Объект.Проживающий.ФизЛицо, Объект.Дата, Ложь);
	
		Область.Параметры.ФИОП         = Ссудополучатель.Представление;
		Область.Параметры.ГодРожденияП = Формат(Год(Объект.Проживающий.ФизЛицо.ДатаРождения),"ЧН=; ЧГ=");
	
		Запрос = Новый Запрос;
		Запрос.Текст = ПолучитьТекстЗапросаПоКонтактнойИнформацииСправочникаЖильцы();
		Запрос.УстановитьПараметр("Объект", Объект.Проживающий);
		Запрос.УстановитьПараметр("ТипКИ",  Перечисления.ТипыКонтактнойИнформации.Адрес);
		Запрос.УстановитьПараметр("ВидКИ",  Справочники.ВидыКонтактнойИнформации.УПЖКХ_АдресПрибытияЖильца);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		АдресП = "";
		Если Выборка.Следующий() Тогда
			АдресП = Выборка.Представление;
			Область.Параметры.АдресП = АдресП; 
		Иначе
			АдресП = "____________________________________________";
			Область.Параметры.АдресП = АдресП;
		КонецЕсли;
	
		ДокУдостоверениеЛичностиП = СокрЛП(""+Ссудополучатель.ВидДокумента+" серия:"+Ссудополучатель.Серия+" номер:"+Ссудополучатель.Номер
			+" выдан "+Ссудополучатель.КемВыдан+" "+Формат(Ссудополучатель.ДатаВыдачи, "ДФ=dd.MM.yyyy"));
	
			Область.Параметры.ДокУдостоверениеЛичностиП = ДокУдостоверениеЛичностиП;
	
		ФИОПаспортП = Ссудополучатель.Фамилия+" "+Ссудополучатель.Имя+" "+Ссудополучатель.Отчество
		+". "+ДокУдостоверениеЛичностиП
		+". Зарегистрирован по адресу:"+АдресП;

		Область.Параметры.ФИОПаспортП = ФИОПаспортП;
	
		ТабДок.Вывести(Область);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
		РезДокумент.Вывести(ТабДок);
		
	КонецЦикла;
	
	РезДокумент.ТолькоПросмотр = Ложь;
	РезДокумент.ОтображатьСетку = Ложь;
	РезДокумент.ОтображатьЗаголовки = Ложь;
	
	Возврат РезДокумент;
	
КонецФункции	

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "Договор") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Договор", "Договор безвозмездного пользования по месту жительства", ПечатьДоговор(МассивОбъектов, ОбъектыПечати, "ДоговорБезвозмездногоПользования"));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает текст запроса по ТЧ "Контактная информация" справочника "Жильцы".
Функция ПолучитьТекстЗапросаПоКонтактнойИнформацииСправочникаЖильцы() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_ЖильцыКонтактнаяИнформация.Ссылка,
	|	УПЖКХ_ЖильцыКонтактнаяИнформация.Представление
	|ИЗ
	|	Справочник.УПЖКХ_Жильцы.КонтактнаяИнформация КАК УПЖКХ_ЖильцыКонтактнаяИнформация
	|ГДЕ
	|	УПЖКХ_ЖильцыКонтактнаяИнформация.Тип = &ТипКИ
	|	И УПЖКХ_ЖильцыКонтактнаяИнформация.Вид = &ВидКИ
	|	И УПЖКХ_ЖильцыКонтактнаяИнформация.Ссылка = &Объект";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаПоКонтактнойИнформацииСправочникаЖильцы()

// Функция получает данные об ответственном собственнике/нанимателе
//
Функция ПолучитьДанныеОбОтветственном(Ответственный, ОрганизацияДок, ДатаДок)
	
	//ФЛ
	Если ТипЗнч(Ответственный) = Тип("СправочникСсылка.УПЖКХ_Жильцы") Тогда
	
		ФЛ = Ответственный.ФизЛицо;
		
		ДанныеФЛ = УПЖКХ_ТиповыеМетодыВызовСервера.ДанныеФизЛица(ОрганизацияДок, ФЛ, ДатаДок, Ложь);
		АдресПрописка = УПЖКХ_ТиповыеМетодыСервер.ПолучитьКонтактнуюИнформацияОбъекта(ФЛ, Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица);
		
		ДанныеФЛ.Вставить("АдресПрописка", АдресПрописка);
		ДанныеФЛ.Вставить("ТипОтв", "ФизическиеЛица");
		
		Возврат ДанныеФЛ;
		
	//Контрагент
	ИначеЕсли ТипЗнч(Ответственный) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		ДанныеК = Новый Структура("ТипОтв, ФИО, ДокументУдостоверяющийЛичность, Адрес", "", "", "");
		ДанныеК.ТипОтв = "Контрагенты";
		ДанныеК.ФИО = Ответственный.НаименованиеПолное;
		ДанныеК.ДокументУдостоверяющийЛичность = Ответственный.ДокументУдостоверяющийЛичность;
		
		АдресК = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(Ответственный, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента,, ДатаДок);
		
		ДанныеК.Адрес = АдресК;
		
		Возврат ДанныеК;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли