
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// Печать

#Область Печать

// Функция возвращая табличный документ на основе Форма6.
//
Функция ПечатьФорма6_12(МассивОбъектов, ОбъектыПечати, НомерФормы)
	
	РезДокумент = Новый ТабличныйДокумент;
	
	Для Каждого Объект Из МассивОбъектов Цикл
		
		ТабДокумент = Новый ТабличныйДокумент;
		
		Отчет = Отчеты.КВП_ФормыРегистрационногоУчета.Создать();
		Отчет.Организация = Объект.Организация;
		Отчет.Дата = Объект.Дата;
		
		СтрокаТч = Отчет.ТаблицаДляПечати.Добавить();
		
		Если Объект.ВидОперации = Перечисления.КВП_ВидыОперацийРегистрацииПоМестуЖительства.Регистрация Тогда
			
			Если НомерФормы = "Ф6" Тогда 
				
				СтрокаТч.НомерФормы  = "Ф6_Р";
				СтрокаТЧ.Собственник = Объект.Собственник;
				
			ИначеЕсли НомерФормы = "Ф12" Тогда 
				
				СтрокаТч.НомерФормы  = "Ф12_П";
				
			Иначе
				
				Возврат ТабДокумент;
				
			КонецЕсли;
			
		Иначе
			
			Если НомерФормы = "Ф6" Тогда 
				
				СтрокаТч.НомерФормы  = "Ф6_С";
				
			ИначеЕсли НомерФормы = "Ф12" Тогда 
				
				СтрокаТч.НомерФормы  = "Ф12_В";
				
			Иначе
				
				Возврат ТабДокумент;
			
		КонецЕсли;
				
		КонецЕсли;
		
		СтрокаТч.Проживающий = Объект.Проживающий;
		
		Отчет.ОбновитьОтчет(ТабДокумент);
		
		РезДокумент.Вывести(ТабДокумент);
		
	КонецЦикла;
	
	Возврат РезДокумент;
	
КонецФункции

// Функция возвращая табличный документ на основе форм.
//
Функция ПечатьФорм(МассивОбъектов, ОбъектыПечати, НомерФормы)
	
	РезДокумент = Новый ТабличныйДокумент;
	
	Для Каждого Объект Из МассивОбъектов Цикл
		
		Если (Объект.ВидОперации = Перечисления.КВП_ВидыОперацийРегистрацииПоМестуЖительства.Регистрация И
			(НомерФормы = "Ф2" Или НомерФормы = "С_ЗП" Или НомерФормы = "С_ПР" Или НомерФормы = "З_С" Или НомерФормы = "РФЛ1")) Или 
			(Объект.ВидОперации = Перечисления.КВП_ВидыОперацийРегистрацииПоМестуЖительства.СнятиеСРегистрации И
			НомерФормы = "Ф7") Тогда
		
			ТабДокумент = Новый ТабличныйДокумент;
			
			Отчет = Отчеты.КВП_ФормыРегистрационногоУчета.Создать();
			
			Отчет.Организация  = Объект.Организация;
			Отчет.Дата         = Объект.Дата;
			Отчет.РазмерБумаги = Перечисления.КВП_РазмерБумаги.А4;
			
			СтрокаТч = Отчет.ТаблицаДляПечати.Добавить();
			СтрокаТч.НомерФормы  = НомерФормы;
			СтрокаТч.Проживающий = Объект.Проживающий;
			
			Отчет.ОбновитьОтчет(ТабДокумент);
			
			РезДокумент.Вывести(ТабДокумент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезДокумент;
	
КонецФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ф6") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Ф6", "Заявление о регистрации (снятии с регистрационного учета) по месту жительства", ПечатьФорма6_12(МассивОбъектов, ОбъектыПечати, "Ф6"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ф12") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Ф12", "Листок статистического учета прибытия (выбытия)", ПечатьФорма6_12(МассивОбъектов, ОбъектыПечати, "Ф12"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ф2") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Ф2", "Адресный листок прибытия", ПечатьФорм(МассивОбъектов, ОбъектыПечати, "Ф2"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ф7") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Ф7", "Адресный листок убытия", ПечатьФорм(МассивОбъектов, ОбъектыПечати, "Ф7"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "С_ЗП") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "С_ЗП", "Справка об отсутствии паспорта в связи с заменой", ПечатьФорм(МассивОбъектов, ОбъектыПечати, "С_ЗП"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "С_ПР") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "С_ПР", "Справка об отсутствии паспорта (на регистрации)", ПечатьФорм(МассивОбъектов, ОбъектыПечати, "С_ПР"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "З_С") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "З_С", "Заявление собственников для регистрации", ПечатьФорм(МассивОбъектов, ОбъектыПечати, "З_С"));
	КонецЕсли;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "РФЛ1") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РФЛ1", "Сведения о регистрации гражданина РФ по месту жительства", ПечатьФорм(МассивОбъектов, ОбъектыПечати, "РФЛ1"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли