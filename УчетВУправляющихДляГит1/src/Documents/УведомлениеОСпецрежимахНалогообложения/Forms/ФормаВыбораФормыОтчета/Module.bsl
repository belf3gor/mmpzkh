#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Неопределено;
	Параметры.Свойство("Данные", Данные);
	АдресДанные = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);

	Параметры.Свойство("Организация", Организация);
	Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
	Параметры.Свойство("ИмяОтчета", ИмяОтчета);
	Параметры.Свойство("ВидУведомления", ВидУведомления);
	ЗначениеВРеквизитФормы(Отчеты[ИмяОтчета].ПолучитьТаблицуФорм(), "ТаблицаФорм");
	
	Элементы.Создать.Доступность = ЗначениеЗаполнено(Организация);
	
	СписокВыбора = Элементы.ПолеРедакцияФормы.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого Стр Из ТаблицаФорм Цикл 
		СписокВыбора.Добавить(Стр.ИмяФормы, Стр.ОписаниеФормы);
		ПолеРедакцияФормы = Стр.ИмяФормы;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Элементы.Создать.Доступность = ЗначениеЗаполнено(Организация);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Создать(Команда)
	Данные = ПолучитьИзВременногоХранилища(АдресДанные);
	ПараметрыОткрытия = Новый Структура("Организация,ЗначениеКопирования,Данные,ВидУведомления", Организация, ЗначениеКопирования, Данные, ВидУведомления);
	ОткрытьФорму("Отчет."+ИмяОтчета+".Форма."+ПолеРедакцияФормы, ПараметрыОткрытия, ВладелецФормы, Истина);
	Закрыть();
КонецПроцедуры

#КонецОбласти