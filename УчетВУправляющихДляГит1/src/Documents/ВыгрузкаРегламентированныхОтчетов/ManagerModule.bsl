
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик обновления БРО 1.1.13.3
//
Процедура УдалитьПояснительныеЗапискиЕНВД(Параметры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыгрузкаРегламентированныхОтчетов.Ссылка,
	               |	ВыгрузкаРегламентированныхОтчетов.Организация
	               |ИЗ
	               |	Документ.ВыгрузкаРегламентированныхОтчетов КАК ВыгрузкаРегламентированныхОтчетов
	               |ГДЕ
	               |	ВыгрузкаРегламентированныхОтчетов.Дата >= &ДатаДокумента";
	
	Запрос.УстановитьПараметр("ДатаДокумента", '20180307');
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрТаблЗнач Из Результат Цикл
		
		Док = СтрТаблЗнач.Ссылка.ПолучитьОбъект();
		КолВыгрузок = Док.Выгрузки.Количество();
		ТребуетсяЗаписьОбъекта = Ложь;
		
		Для Инд = 0 По КолВыгрузок - 1 Цикл
			
			ОбрИнд = КолВыгрузок - 1 - Инд;
			ИмяФайла = СокрЛП(Док.Выгрузки[ОбрИнд].ИмяФайла);
			ОкончаниеИмениФайла = Врег(Прав(ИмяФайла, 4));
			
			Если ОкончаниеИмениФайла = ".PDF" Тогда
				НачалоИмениФайла = Врег(Лев(ИмяФайла, 42));
				Если НачалоИмениФайла = "ПОЯСНИТЕЛЬНАЯ ЗАПИСКА К ДЕКЛАРАЦИИ ПО ЕНВД" Тогда
					Док.Выгрузки.Удалить(ОбрИнд);
					ТребуетсяЗаписьОбъекта = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТребуетсяЗаписьОбъекта Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Док, , Истина, РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
