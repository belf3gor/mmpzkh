#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,
		"Номенклатура,ДатаПринятияНаУчет,Организация,
		|НалоговаяБазаАкцизы,ТвердаяСтавкаАкциза,АдвалорнаяСтавкаАкциза,ЕдиницаИзмеренияАкциза,СуммаАкциза");
	
	ОпределитьНалоговуюБазуПоУмолчанию(Параметры);
	ОпределитьВидРасчетаАкциза();
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьУсловноеОформление();
	УстановитьЗаголовокФормы();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ФормаРедактированияАкцизов_Закрыть" И Источник = ВладелецФормы Тогда
		// Сообщение от основной формы документа при нажатии там Esc.
		// Сбрасываем флаг модифицированности и закрываем форму редактирования строки без вопросов.
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		СтруктураРезультат = Новый Структура(
			"НалоговаяБазаАкцизы,ТвердаяСтавкаАкциза,АдвалорнаяСтавкаАкциза,ЕдиницаИзмеренияАкциза,СуммаАкциза");
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтаФорма);
		Если ВидАкциза = 0 Тогда
			СтруктураРезультат.ТвердаяСтавкаАкциза    = 0;
			СтруктураРезультат.АдвалорнаяСтавкаАкциза = 0;
			СтруктураРезультат.ЕдиницаИзмеренияАкциза = "";
		ИначеЕсли ВидАкциза = 1 Тогда
			СтруктураРезультат.ТвердаяСтавкаАкциза    = СтавкаАкциза;
			СтруктураРезультат.АдвалорнаяСтавкаАкциза = 0;
		ИначеЕсли ВидАкциза = 2 Тогда
			СтруктураРезультат.ТвердаяСтавкаАкциза    = 0;
			СтруктураРезультат.АдвалорнаяСтавкаАкциза = СтавкаАкциза;
			СтруктураРезультат.ЕдиницаИзмеренияАкциза = "";
		КонецЕсли;
		ОповеститьОВыборе(СтруктураРезультат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И (Модифицированность ИЛИ ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;

	Если ПеренестиВДокумент И НЕ Отказ Тогда
		Отказ = НЕ ПроверитьЗаполнениеНаКлиенте();
	КонецЕсли;

	Если Отказ Тогда
		ПеренестиВДокумент = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидАкцизаПриИзменении(Элемент)
	
	ЕдиницаИзмеренияАкциза = "";
	НалоговаяБазаАкцизы    = 0;
	СтавкаАкциза           = 0;
	СуммаАкциза            = 0;
	
	УправлениеФормой(ЭтотОбъект);
	РассчитатьСуммуАкциза();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяБазаАкцизыПриИзменении(Элемент)
	
	РассчитатьСуммуАкциза();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияАкцизаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаАкцизаПриИзменении(Элемент)
	
	РассчитатьСуммуАкциза();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.НалоговаяБазаАкцизы.Доступность  = Форма.ВидАкциза <> 0;
	Элементы.ЕдиницаИзмеренияАкциза.Видимость = Форма.ВидАкциза = 1;
	Элементы.ДекорацияРубли.Видимость         = Форма.ВидАкциза = 2;
	Элементы.СтавкаАкциза.Доступность         = Форма.ВидАкциза <> 0;
	Элементы.ЕдиницаСтавки.Видимость          = Форма.ВидАкциза <> 0;
	Элементы.СуммаАкциза.Доступность          = Форма.ВидАкциза <> 0;
	
	Если Форма.ВидАкциза = 0 Тогда
		
		Форма.ЕдиницаСтавки          = "";
		Форма.ЕдиницаИзмеренияАкциза = "";
		Форма.НалоговаяБазаАкцизы    = 0;
		Форма.СтавкаАкциза           = 0;
		Форма.СуммаАкциза            = 0;
		
		Элементы.НалоговаяБазаАкцизы.Формат               = "ЧДЦ=2";
		Элементы.НалоговаяБазаАкцизы.ФорматРедактирования = "ЧДЦ=2";
		
	ИначеЕсли Форма.ВидАкциза = 1 Тогда
		
		Элементы.НалоговаяБазаАкцизы.Формат               = "ЧДЦ=6";
		Элементы.НалоговаяБазаАкцизы.ФорматРедактирования = "ЧДЦ=6";
		
		Если Форма.ЕдиницаИзмеренияАкциза = "" И Форма.НалоговаяБазаАкцизы = 0 Тогда
			Форма.ЕдиницаИзмеренияАкциза = Форма.ЕдиницаПоУмолчанию;
			Форма.НалоговаяБазаАкцизы    = Форма.КоличествоПоУмолчанию;
		КонецЕсли;
		
		Если Форма.ЕдиницаИзмеренияАкциза = "112" Тогда
			Форма.ЕдиницаСтавки = НСтр("ru='за литр'");
		ИначеЕсли Форма.ЕдиницаИзмеренияАкциза = "168" Тогда
			Форма.ЕдиницаСтавки = НСтр("ru='за тонну'");
		ИначеЕсли Форма.ЕдиницаИзмеренияАкциза = "251" Тогда
			Форма.ЕдиницаСтавки = НСтр("ru='за лошадиную силу'");
		ИначеЕсли Форма.ЕдиницаИзмеренияАкциза = "831" Тогда
			Форма.ЕдиницаСтавки = НСтр("ru='за литр 100% спирта'");
		КонецЕсли;
		
	ИначеЕсли Форма.ВидАкциза = 2 Тогда
		
		Элементы.НалоговаяБазаАкцизы.Формат               = "ЧДЦ=2";
		Элементы.НалоговаяБазаАкцизы.ФорматРедактирования = "ЧДЦ=2";
		
		Форма.ЕдиницаСтавки = "%";
		Если Форма.НалоговаяБазаАкцизы = 0 Тогда
			Форма.НалоговаяБазаАкцизы = Форма.ФактурнаяСтоимостьРуб;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// Налоговая база

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НалоговаяБазаАкцизы");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВидАкциза", ВидСравненияКомпоновкиДанных.Равно, 0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	// Единица измерения налоговой базы

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЕдиницаИзмеренияАкциза");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ВидАкциза", ВидСравненияКомпоновкиДанных.Равно, 0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ВидАкциза", ВидСравненияКомпоновкиДанных.Равно, 2);

	// Ставка акциза

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтавкаАкциза");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВидАкциза", ВидСравненияКомпоновкиДанных.Равно, 0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Сумма акциза

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СуммаАкциза");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВидАкциза", ВидСравненияКомпоновкиДанных.Равно, 0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	НаименованиеНоменклатуры = "";
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ДанныеОбъекта = Новый Структура("Дата, Организация", ДатаПринятияНаУчет, Организация);
		СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
			Номенклатура, ДанныеОбъекта, Ложь);
			НаименованиеНоменклатуры = СведенияОНоменклатуре.Наименование;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Акцизы: %1'"),
		НаименованиеНоменклатуры);

КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте()

	Отказ = Ложь;
	
	Если ВидАкциза = 0 Тогда
		Возврат Истина;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(НалоговаяБазаАкцизы) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Налоговая база'"));
		Поле = "НалоговаяБазаАкцизы";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияАкциза) И ВидАкциза = 1 Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Единица измерения налоговой базы'"));
		Поле = "ЕдиницаИзмеренияАкциза";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(СтавкаАкциза) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Ставка акциза'"));
		Поле = "СтавкаАкциза";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СуммаАкциза) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Сумма акциза'"));
		Поле = "СуммаАкциза";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;
	
	Возврат Не Отказ;

КонецФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуАкциза()
	
	Если ЗначениеЗаполнено(НалоговаяБазаАкцизы) И ЗначениеЗаполнено(СтавкаАкциза)  Тогда
		Если ВидАкциза = 1 Тогда
			СуммаАкциза = НалоговаяБазаАкцизы * СтавкаАкциза;
		ИначеЕсли ВидАкциза = 2 Тогда
			СуммаАкциза = НалоговаяБазаАкцизы * СтавкаАкциза / 100;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьНалоговуюБазуПоУмолчанию(Параметры)
	
	Если Параметры.ВалютаДокумента <> Параметры.ВалютаРеглУчета Тогда
		СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Параметры.ВалютаДокумента, Параметры.ДатаПринятияНаУчет);
		Если СтруктураКурса.Кратность = 0 Тогда
			СтруктураКурса.Курс = 1;
			СтруктураКурса.Кратность = 1;
		КонецЕсли;
		ФактурнаяСтоимостьРуб = 
			Окр((Параметры.ФактурнаяСтоимость * (СтруктураКурса.Курс/СтруктураКурса.Кратность)), 2);
	Иначе
		ФактурнаяСтоимостьРуб = Параметры.ФактурнаяСтоимость;
	КонецЕсли;
	
	КоличествоПоУмолчанию = 0;
	ЕдиницаПоУмолчанию    = "168";
	
	ЕдиницыИзмеренияНалоговойБазы = Новый Массив;
	ЕдиницыИзмеренияНалоговойБазы.Добавить("112");
	ЕдиницыИзмеренияНалоговойБазы.Добавить("166"); // Килограммы пересчитаем в тонны.
	ЕдиницыИзмеренияНалоговойБазы.Добавить("168");
	ЕдиницыИзмеренияНалоговойБазы.Добавить("251");
	ЕдиницыИзмеренияНалоговойБазы.Добавить("831");
	
	Если ЗначениеЗаполнено(Параметры.ЕдиницаИзмерения) И ЗначениеЗаполнено(Параметры.Количество) Тогда
		ЕдиницаИзмеренияКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЕдиницаИзмерения, "Код");
		Если ЕдиницыИзмеренияНалоговойБазы.Найти(ЕдиницаИзмеренияКод) <> Неопределено Тогда
			Если ЕдиницаИзмеренияКод = "166" Тогда
				КоличествоПоУмолчанию = Параметры.Количество / 1000;
				ЕдиницаПоУмолчанию    = "168";
			Иначе
				КоличествоПоУмолчанию = Параметры.Количество;
				ЕдиницаПоУмолчанию    = ЕдиницаИзмеренияКод;
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.КодТНВЭД) И ЗначениеЗаполнено(Параметры.КоличествоПоТНВЭД) Тогда
		ЕдиницаИзмеренияКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.КодТНВЭД, "ЕдиницаИзмерения.Код");
		Если ЗначениеЗаполнено(ЕдиницаИзмеренияКод) 
		   И ЕдиницыИзмеренияНалоговойБазы.Найти(ЕдиницаИзмеренияКод) <> Неопределено Тогда
			Если ЕдиницаИзмеренияКод = "166" Тогда
				КоличествоПоУмолчанию = Параметры.КоличествоПоТНВЭД / 1000;
				ЕдиницаПоУмолчанию    = "168";
			Иначе
				КоличествоПоУмолчанию = Параметры.КоличествоПоТНВЭД;
				ЕдиницаПоУмолчанию    = ЕдиницаИзмеренияКод;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьВидРасчетаАкциза()
	
	Если ЗначениеЗаполнено(ТвердаяСтавкаАкциза) Тогда
		ВидАкциза = 1;
		СтавкаАкциза = ТвердаяСтавкаАкциза;
	ИначеЕсли ЗначениеЗаполнено(АдвалорнаяСтавкаАкциза) Тогда
		ВидАкциза = 2;
		СтавкаАкциза = АдвалорнаяСтавкаАкциза;
	ИначеЕсли ЗначениеЗаполнено(НалоговаяБазаАкцизы) ИЛИ ЗначениеЗаполнено(СуммаАкциза) Тогда
		ВидАкциза = 1;
	Иначе
		ВидАкциза = 0;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти