
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивВидовОпераци = Новый Массив;
	МассивВидовОпераци.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия);
	МассивВидовОпераци.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары);
	МассивВидовОпераци.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование);
	
	ДокументыПоступления.Параметры.УстановитьЗначениеПараметра("Организация",               Параметры.Организация);
	ДокументыПоступления.Параметры.УстановитьЗначениеПараметра("Контрагент",                Параметры.Контрагент);
	ДокументыПоступления.Параметры.УстановитьЗначениеПараметра("Договор",                   Параметры.Договор);
	ДокументыПоступления.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент",           Параметры.ТекущийДокумент);
	ДокументыПоступления.Параметры.УстановитьЗначениеПараметра("МассивВыбранныхДокументов", Параметры.МассивВыбранныхДокументов);
	ДокументыПоступления.Параметры.УстановитьЗначениеПараметра("МассивВидовОпераций",       МассивВидовОпераци);
	
	Если НЕ Параметры.ПоОдномуДокументу Тогда
		Элементы.ДокументыПоступления.РежимВыделения = РежимВыделенияТаблицы.Множественный
	КонецЕсли;
	
	Элементы.ДокументыПоступления.ТекущаяСтрока = Параметры.ТекущийДокумент;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	 ОбработкаВыбораЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();

	
	// Уже выбранные документы отображаем серым цветом.

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыПоступления");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДокументыПоступления.ДокументУжеВыбран", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

КонецПроцедуры

&НаКлиенте
Процедура ДокументыПоступленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаВыбораЗначения();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЗначения()
	
	Если Параметры.ПоОдномуДокументу Тогда
		ТекущиеДанные = Элементы.ДокументыПоступления.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
	
		Если ТекущиеДанные.ДокументУжеВыбран Тогда 
			ПоказатьПредупреждение( , НСтр("ru='Этот документ уже выбран'"));
			Возврат;
		КонецЕсли;
		
		Результат = Новый Массив;
		Результат.Добавить(ТекущиеДанные.Ссылка);
		
	Иначе
		Результат = Элементы.ДокументыПоступления.ВыделенныеСтроки;
		
	КонецЕсли;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти

