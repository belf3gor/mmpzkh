
#Область СлужебныеПроцедурыИФункции

&НаСервере
// Процедура формирования документов "Управление сведениями для взаиморасчетов по л/с".
//
Функция СформироватьДокументыНаСервере()
	
	МассивДокументов = Новый Массив;
	
	ТаблицаСведений = ТаблицаДанных.Выгрузить();
	
	ТаблицаОрганизаций = ТаблицаСведений.Скопировать(, "Организация");
	ТаблицаОрганизаций.Свернуть("Организация");
	МассивОрганизаций = ТаблицаОрганизаций.ВыгрузитьКолонку("Организация");
	
	Для Каждого ТекОрганизация Из МассивОрганизаций Цикл
		
		ДокументУправления = Документы.УПЖКХ_УправлениеСведениямиДляВзаиморасчетовПоЛицевымСчетам.СоздатьДокумент();
		ДокументУправления.Организация   = ТекОрганизация;
		ДокументУправления.Дата          = ТекущаяДата();
		ДокументУправления.Ответственный =  УПЖКХ_ТиповыеМетодыКлиентСервер.ТекущийПользователь();
		ДокументУправления.Комментарий   = "#Документ сформирован автоматически по договорам контрагентов, отсутствовавшим в сведениях для взаиморасчетов";
		
		Отбор = Новый Структура;
		Отбор.Вставить("Организация", ТекОрганизация);
		
		НайденныеСтроки = ТаблицаСведений.НайтиСтроки(Отбор);
		Для Каждого ТекСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ДокументУправления.СведенияДляВзаиморасчетов.Добавить();
			НоваяСтрока.ЛицевойСчет        = ТекСтрока.ЛицевойСчет;
			НоваяСтрока.Контрагент         = ТекСтрока.Контрагент;
			НоваяСтрока.ДоговорКонтрагента = ТекСтрока.Договор;
		КонецЦикла;
		
		// Проводим или записываем документ и сохраняем в массиве.
		Если КВП_ЗаписатьОбъект(ДокументУправления, РежимЗаписиДокумента.Проведение)
		 Или КВП_ЗаписатьОбъект(ДокументУправления, РежимЗаписиДокумента.Запись) Тогда
			МассивДокументов.Добавить(ДокументУправления.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции // СформироватьДокументыНаСервере()

&НаСервереБезКонтекста
// Процедура сохраняет настройку вывода предупреждения на сервере.
//
Процедура СохранитьНастройкуПредупрежденияНаСервере()
	
	ХранилищеОбщихНастроек.Сохранить("ФормаПредупрежденияОСведенияхДляВзаиморасчетов", "ПоказыватьПредупреждение", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаДанныхДляВзаиморасчетов = Документы.УПЖКХ_УправлениеСведениямиДляВзаиморасчетовПоЛицевымСчетам.ПолучитьДоговорыКонтрагентовНеУказанныеВСведенияхДляВзаиморасчетовПоЛС();
	Если Не ТипЗнч(ТаблицаДанныхДляВзаиморасчетов) = Тип("ТаблицаЗначений") Или ТаблицаДанныхДляВзаиморасчетов.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТаблицаДанных.Загрузить(ТаблицаДанныхДляВзаиморасчетов);
	
	// Получение текста из макета HTML-документа.
	МакетПредупреждения = Документы.УПЖКХ_УправлениеСведениямиДляВзаиморасчетовПоЛицевымСчетам.ПолучитьМакет("ПредупреждениеОСведенияхДляВзаиморасчетов");
	ТекстСообщения = МакетПредупреждения.ПолучитьТекст();
	
	// Заменим текст шаблона "[СписокОрганизаций]" на список организаций через точку с запятой.
	// По этим организациям будем формировать документы.
	ТаблицаОрганизаций = ТаблицаДанныхДляВзаиморасчетов.Скопировать();
	ТаблицаОрганизаций.Свернуть("Организация");
	МассивОрганизаций = ТаблицаОрганизаций.ВыгрузитьКолонку("Организация");
	
	Если МассивОрганизаций.Количество() = 1 Тогда
		ТекстОрганизации = "организации ";
	Иначе
		ТекстОрганизации = "организациям ";
	КонецЕсли;
	
	Для Каждого ТекОрганизация Из МассивОрганизаций Цикл
		ТекстОрганизации = ТекстОрганизации + ТекОрганизация + "; ";
	КонецЦикла;
	
	ТекстОрганизации = Лев(ТекстОрганизации, СтрДлина(ТекстОрганизации) - 2);
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "[СписокОрганизаций]", ТекстОрганизации);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если БольшеНеПоказывать Тогда
		СохранитьНастройкуПредупрежденияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "СформироватьДокументы".
//
Процедура КомандаСформироватьДокументы(Команда)
	
	МассивДокументов = СформироватьДокументыНаСервере();
	Для Каждого ТекДокумент Из МассивДокументов Цикл
		ПараметрыФормы = Новый Структура("Ключ", ТекДокумент);
		ОткрытьФорму("Документ.УПЖКХ_УправлениеСведениямиДляВзаиморасчетовПоЛицевымСчетам.Форма.ФормаДокумента", ПараметрыФормы);
	КонецЦикла;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
