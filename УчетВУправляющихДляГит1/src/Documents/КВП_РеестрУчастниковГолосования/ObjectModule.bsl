
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура формирует список участников голосования.
Процедура ЗаполнитьСписокУчастников(СписокПомещений, СпПомещенийССобственниками) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата",             КонецДня(Дата));
	Запрос.УстановитьПараметр("СписокПомещений",  СписокПомещений);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СобственностьНаПомещения.Помещение.Код КАК НомерПомещения,
	|	СобственностьНаПомещения.Помещение.Владелец КАК Здание,
	|	СобственностьНаПомещения.Помещение КАК Помещение,
	|	СобственностьНаПомещения.ВидСобственности,
	|	СобственностьНаПомещения.ПодвидЧастнойСобственности
	|ПОМЕСТИТЬ втСобственность
	|ИЗ
	|	РегистрСведений.УПЖКХ_СобственностьНаПомещения.СрезПоследних(&Дата, Помещение В (&СписокПомещений)) КАК СобственностьНаПомещения
	|ГДЕ
	|	СобственностьНаПомещения.Действует
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СобственникиПомещений.Помещение,
	|	СобственникиПомещений.Собственник КАК СобственникПомещения,
	|	СобственникиПомещений.ДоляСобственникаЧислитель,
	|	СобственникиПомещений.ДоляСобственникаЗнаменатель
	|ПОМЕСТИТЬ втСобственники
	|ИЗ
	|	РегистрСведений.УПЖКХ_СобственникиПомещений.СрезПоследних(&Дата, Помещение В (&СписокПомещений)) КАК СобственникиПомещений
	|ГДЕ
	|	СобственникиПомещений.Действует
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ПлощадьПомещения.Площадь, 0) КАК Площадь,
	|	ПлощадьПомещения.Объект
	|ПОМЕСТИТЬ втПлощадь
	|ИЗ
	|	РегистрСведений.КВП_ПлощадьПомещения.СрезПоследних(
	|			&Дата,
	|			Объект В (&СписокПомещений)
	|				И ВидПлощади = ЗНАЧЕНИЕ(Справочник.УПЖКХ_ВидыПлощадей.ОбщаяПлощадь)) КАК ПлощадьПомещения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСобственность.НомерПомещения,
	|	втСобственность.Здание,
	|	втСобственность.Помещение,
	|	втСобственность.ВидСобственности,
	|	втСобственность.ПодвидЧастнойСобственности,
	|	втСобственники.СобственникПомещения,
	|	втСобственники.ДоляСобственникаЧислитель,
	|	втСобственники.ДоляСобственникаЗнаменатель
	|ПОМЕСТИТЬ втСобственно
	|ИЗ
	|	втСобственность КАК втСобственность
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСобственники КАК втСобственники
	|		ПО втСобственность.Помещение = втСобственники.Помещение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСобственно.НомерПомещения КАК НомерПомещения,
	|	втСобственно.Здание КАК Здание,
	|	втСобственно.Помещение КАК Помещение,
	|	втСобственно.ВидСобственности,
	|	втСобственно.ПодвидЧастнойСобственности,
	|	втСобственно.СобственникПомещения КАК СобственникПомещения,
	|	втСобственно.ДоляСобственникаЧислитель,
	|	втСобственно.ДоляСобственникаЗнаменатель,
	|	ЕСТЬNULL(втПлощадь.Площадь, 0) КАК Площадь,
	|	втПлощадь.Объект
	|ИЗ
	|	втСобственно КАК втСобственно
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПлощадь КАК втПлощадь
	|		ПО втСобственно.Помещение = втПлощадь.Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	Здание,
	|	НомерПомещения
	|ИТОГИ
	|	КОЛИЧЕСТВО(СобственникПомещения)
	|ПО
	|	Помещение";
	
	ТабПомещенийССобственниками = Запрос.Выполнить().Выгрузить();
	ТабПомещенийССобственниками.Свернуть("Помещение"); 
	СпПомещенийССобственниками.ЗагрузитьЗначения(ТабПомещенийССобственниками.ВыгрузитьКолонку("Помещение"));
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока Выборка.Следующий() Цикл
		ВыборкаПоПомещению = Выборка.Выбрать();
		КоличествоСобственников = Выборка.СобственникПомещения;
		Пока ВыборкаПоПомещению.Следующий() Цикл 
			НоваяСтрока = Участники.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоПомещению);
			НоваяСтрока.ВесГолоса = 
				Документы.КВП_РеестрУчастниковГолосования.ОпределитьВесГолосаСУчетомВидаСобственности(ВыборкаПоПомещению,
																										КоличествоСобственников, ВыборкаПоПомещению.Площадь);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры //ЗаполнитьСписокУчастников()

// Обработчик события "ОбработкаЗаполнения" документа.
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли