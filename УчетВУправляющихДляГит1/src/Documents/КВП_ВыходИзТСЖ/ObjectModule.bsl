
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ЭкспортныеПроцедурыИФункцииДокумента

// Процедура заполняет табличную часть документа данными из регистров сведений.
Процедура ЗаполнитьТабличнуюЧасть() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КВП_ЧленыТСЖСрезПоследних.ЧленТСЖ КАК ЧленТСЖ
	|ИЗ
	|	РегистрСведений.КВП_ЧленыТСЖ.СрезПоследних(
	|			&Период,
	|			Организация = &Организация) КАК КВП_ЧленыТСЖСрезПоследних
	|ГДЕ
	|	КВП_ЧленыТСЖСрезПоследних.Участие = ИСТИНА";
	
	Запрос.УстановитьПараметр("Период",      Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	ТЗРезультат = Запрос.Выполнить().Выгрузить();

	ЧленыТСЖ.Загрузить(ТЗРезультат);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийДокумента

// Обработчик события "ОбработкаЗаполнения" документа.
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура формирования движений документа.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = УПЖКХ_ТиповыеМетодыВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	Для Каждого ТекСтрока Из ЧленыТСЖ Цикл
		
		// регистр КВП_ЧленыТСЖ
		Движение = Движения.КВП_ЧленыТСЖ.Добавить();
		Движение.Период      = Дата;
		Движение.Организация = Организация;
		Движение.ЧленТСЖ     = ТекСтрока.ЧленТСЖ;
		Движение.Участие     = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры  //ОбработкаПроведения()

#КонецОбласти 

#КонецЕсли
