#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПрекращаемыеЗаявления", ПрекращаемыеЗаявления.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрекращаемыеЗаявления.НомерСтроки КАК НомерСтроки,
	|	ПрекращаемыеЗаявления.Заявление КАК Заявление
	|ПОМЕСТИТЬ ВТПрекращаемыеЗаявления
	|ИЗ
	|	&ПрекращаемыеЗаявления КАК ПрекращаемыеЗаявления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрекращаемыеЗаявления.НомерСтроки КАК НомерСтроки,
	|	ПрочиеПрекращаемыеЗаявления.Ссылка КАК Ссылка,
	|	ПрочиеПрекращаемыеЗаявления.Заявление КАК Заявление
	|ИЗ
	|	ВТПрекращаемыеЗаявления КАК ПрекращаемыеЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком.ПрекращаемыеЗаявления КАК ПрочиеПрекращаемыеЗаявления
	|		ПО ПрекращаемыеЗаявления.Заявление = ПрочиеПрекращаемыеЗаявления.Заявление
	|			И (ПрочиеПрекращаемыеЗаявления.Ссылка <> &Ссылка)
	|			И (НЕ ПрочиеПрекращаемыеЗаявления.Ссылка.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрекращаемыеЗаявления1.Заявление КАК Заявление,
	|	ПрекращаемыеЗаявления1.НомерСтроки КАК НомерСтроки1,
	|	ПрекращаемыеЗаявления2.НомерСтроки КАК НомерСтроки2
	|ИЗ
	|	ВТПрекращаемыеЗаявления КАК ПрекращаемыеЗаявления1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПрекращаемыеЗаявления КАК ПрекращаемыеЗаявления2
	|		ПО ПрекращаемыеЗаявления1.Заявление = ПрекращаемыеЗаявления2.Заявление
	|			И ПрекращаемыеЗаявления1.НомерСтроки < ПрекращаемыеЗаявления2.НомерСтроки";
	
	
	Результаты = Запрос.ВыполнитьПакет();
	ВГраница = Результаты.ВГраница();
	Таблица1 = Результаты[ВГраница-1].Выгрузить();
	Таблица2 = Результаты[ВГраница].Выгрузить();
	
	Для Каждого СтрокаТаблицы Из Таблица1 Цикл
		Текст = СтрШаблон(НСтр("ru = 'Документ ""%1"" уже использован в документе ""%2"".'"), СтрокаТаблицы.Заявление, СтрокаТаблицы.Ссылка);
		ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, Текст, "Заявление", "ПрекращаемыеЗаявления", СтрокаТаблицы.НомерСтроки);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из Таблица2 Цикл
		Текст = СтрШаблон(НСтр("ru = 'Документ ""%1"" уже выбран в строке %2.'"), СтрокаТаблицы.Заявление, СтрокаТаблицы.НомерСтроки1);
		ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, Текст, "Заявление", "ПрекращаемыеЗаявления", СтрокаТаблицы.НомерСтроки2);
	КонецЦикла;
	
	ТекстОшибки = Документы.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком.УведомлениеДоступноДляИзменения(Ссылка, Истина);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки);
	КонецЕсли;
	
	Если Не Отказ Тогда
		ДополнительныеСвойства.Вставить("ОбработкаПроверкиЗаполненияВыполнена", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ОбработкаПроверкиЗаполненияВыполнена") Тогда
		ТекстОшибки = Документы.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком.УведомлениеДоступноДляИзменения(Ссылка, Истина);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область МеханизмФиксацииИзменений

Функция ОбновитьВторичныеДанныеДокумента() Экспорт
	Если Не Документы.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком.УведомлениеДоступноДляИзменения(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Модифицирован = Ложь;
	ПараметрыФиксации = Документы.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком.ПараметрыФиксацииВторичныхДанных(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		РеквизитыДокумента = Новый Структура;
		МенеджерОснования = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументОснование);
		МенеджерОснования.ЗаполнитьУведомлениеОПрекращенииОтпускаПоУходуЗаРебенкомПоОснованию(ДокументОснование, РеквизитыДокумента);
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыДокумента, ЭтотОбъект, ПараметрыФиксации) Тогда
			Модифицирован = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
