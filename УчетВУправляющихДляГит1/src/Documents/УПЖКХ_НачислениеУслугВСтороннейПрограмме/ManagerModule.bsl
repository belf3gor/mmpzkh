
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение

// Функция возвращает параметры проведения.
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, ВестиУчетНачисленийПоДобровольномуСтрахованию, УслугаДобровольногоСтрахования, Отказ) Экспорт
	
	// Реквизиты документа, таблицы услуг добровольного страхования и основных услуг.
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка",                                        ДокументСсылка);
	Запрос.УстановитьПараметр("ВестиУчетНачисленийПоДобровольномуСтрахованию", ВестиУчетНачисленийПоДобровольномуСтрахованию);
	Запрос.УстановитьПараметр("УслугаДобровольногоСтрахования",                УслугаДобровольногоСтрахования);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Комментарий КАК Комментарий,
	|	Реквизиты.Ответственный КАК Ответственный,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.УПЖКХ_НачислениеУслугВСтороннейПрограмме КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Услуга КАК Услуга,
	|	Начисления.ЛицевойСчет КАК ЛицевойСчет,
	|	Начисления.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Начисления.РазделУчета КАК РазделУчета,
	|	Начисления.Льготник КАК Льготник,
	|	Начисления.ЛьготнаяКатегория КАК ЛьготнаяКатегория,
	|	Начисления.ВидНачисления КАК ВидНачисления,
	|	Начисления.ВидТарифа КАК ВидТарифа,
	|	Начисления.Количество КАК Количество,
	|	Начисления.Цена КАК Цена,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	Документ.УПЖКХ_НачислениеУслугВСтороннейПрограмме.Начисления КАК Начисления
	|ГДЕ
	|	&ВестиУчетНачисленийПоДобровольномуСтрахованию
	|	И Начисления.Ссылка = &Ссылка
	|	И Начисления.Услуга = &УслугаДобровольногоСтрахования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Услуга КАК Услуга,
	|	Начисления.ЛицевойСчет КАК ЛицевойСчет,
	|	Начисления.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Начисления.РазделУчета КАК РазделУчета,
	|	Начисления.Льготник КАК Льготник,
	|	Начисления.ЛьготнаяКатегория КАК ЛьготнаяКатегория,
	|	Начисления.ВидНачисления КАК ВидНачисления,
	|	Начисления.ВидТарифа КАК ВидТарифа,
	|	Начисления.Количество КАК Количество,
	|	Начисления.Цена КАК Цена,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	Документ.УПЖКХ_НачислениеУслугВСтороннейПрограмме.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &Ссылка
	|	И (НЕ &ВестиУчетНачисленийПоДобровольномуСтрахованию
	|			ИЛИ НЕ Начисления.Услуга = &УслугаДобровольногоСтрахования)";

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаРевизитов         = МассивРезультатов[0].Выбрать();
	НачисленияПоСтрахованию  = МассивРезультатов[1].Выгрузить();
	НачисленияБезСтрахования = МассивРезультатов[2].Выгрузить();
	
	ВыборкаРевизитов.Следующий();
	
	Реквизиты = Новый Структура("Ссылка,Номер,Дата,Организация,Комментарий,Ответственный,СуммаДокумента");
	ЗаполнитьЗначенияСвойств(Реквизиты, ВыборкаРевизитов);
	
	// Добавление реквизитов и таблиц в структуру параметров проведения.
	ПараметрыПроведения = Новый Структура;
	
	ПараметрыПроведения.Вставить("Реквизиты",                Реквизиты);
	ПараметрыПроведения.Вставить("НачисленияПоСтрахованию",  НачисленияПоСтрахованию);
	ПараметрыПроведения.Вставить("НачисленияБезСтрахования", НачисленияБезСтрахования);
	
	Возврат ПараметрыПроведения;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//  КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли