#Область ОписаниеПеременных

&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолей;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВнешниеДанные = Параметры.ВнешниеДанные;
	
	Если Не ФормаДополнена Тогда
		ДополнитьФорму();
	КонецЕсли;
	
	ПрочитатьДанныеСправкиИзВременногоХранилища(Параметры.АдресДанныхСправкиВХранилище);
	
	УчетНДФЛФормы.СведенияОДоходахПриСозданииНаСервере(ЭтаФорма, "Объект.СведенияОДоходах.КодДохода", "СведенияОДоходахКодВычета", "СведенияОДоходахСуммаВычета");
	
	УчетНДФЛФормы.СправкиНДФЛЗаполнитьСписокКонтролируемыхПолей(ЭтаФорма, Истина);
	
	Элементы.ДоходыНалоги.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("Видимость", Истина));
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуютсяСтавкиНДФЛМеждународныхДоговоров") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Ставка",
			"РежимВыбораИзСписка",
			Истина);
			
		СписокВыбора = Элементы.Ставка.СписокВыбора;
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка13);
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка30);
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка35);
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка09);
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка15);
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка05);
		СписокВыбора.Добавить(Перечисления.НДФЛСтавки.Ставка10);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не ФормаДополнена Тогда
		ДополнитьФорму();
	КонецЕсли;
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УчетНДФЛКлиент.СправкиНДФЛУстановитьСтарыеЗначенияКонтролируемыхПолей(Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ"); 
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеДанныхФизическогоЛица" Тогда
		ПрочитатьДанныеФизическогоЛица(Источник);
		УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьПризнакНаличияГражданства(ЭтаФорма, Объект);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Объект");
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Не удалось провести документ.
                              |При проверке заполнения были обнаружены ошибки.'");
		
		ЗарплатаКадрыОтображениеОшибок.ПослеПроверкиЗаполненияВФорме(
			ЭтотОбъект,
			ДокументОбъект,
			СоответствиеДанныхОбъектаДаннымФормы(),
			ОписаниеПодчиненностиДанных(),
			"Объект");
		
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗарплатаКадрыОтображениеОшибок.ПослеПроверкиЗаполненияВФорме(
			ЭтотОбъект,
			ДокументОбъект,
			СоответствиеДанныхОбъектаДаннымФормы(),
			ОписаниеПодчиненностиДанных(),
			"Объект");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	СотрудникПриИзмененииНаСервере();
	
	УчетНДФЛКлиент.СправкиНДФЛУстановитьСтарыеЗначенияКонтролируемыхПолей(Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ"); 
	
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Сотрудник");
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	УчетНДФЛФормы.СправкиНДФЛОчиститьДанныеСправки(Объект, Истина);
	
	ЗаполнитьДанныеСправкиНаСервере();
	
	УстановитьДоступностьДанныхСправки();
КонецПроцедуры	

&НаКлиенте
Процедура ФамилияПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииФиксируемогоЗначения(ЭтаФорма, Объект, "Фамилия");
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Фамилия");
КонецПроцедуры

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииФиксируемогоЗначения(ЭтаФорма, Объект, "Имя");
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Имя");
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииФиксируемогоЗначения(ЭтаФорма, Объект, "Отчество");
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Отчество");
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииФиксируемогоЗначения(ЭтаФорма, Объект, "ИНН");
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.ИНН");
КонецПроцедуры

&НаКлиенте
Процедура ДатаРожденияПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииФиксируемогоЗначения(ЭтаФорма, Объект, "ДатаРождения");
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.ДатаРождения");
КонецПроцедуры

&НаКлиенте
Процедура СтатусНалогоплательщикаПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииФиксируемогоЗначения(ЭтаФорма, Объект, "СтатусНалогоплательщика");
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.СтатусНалогоплательщика");
	УстановитьВидимостьСтрокТаблицыИтогов();
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛГражданствоПриИзменении(ЭтаФорма, Объект);
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Гражданство");
КонецПроцедуры

&НаКлиенте
Процедура ЛицоБезГражданстваПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛЛицБезГражданстваПриИзменении(ЭтаФорма, Объект);
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Гражданство");
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииУдостоверенияЛичности(ЭтаФорма, Объект);
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.ВидДокумента");
КонецПроцедуры

&НаКлиенте
Процедура СерияДокументаПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииУдостоверенияЛичности(ЭтаФорма, Объект);
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.СерияДокумента");
КонецПроцедуры

&НаКлиенте
Процедура НомерДокументаПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриИзмененииУдостоверенияЛичности(ЭтаФорма, Объект);
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.НомерДокумента");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбщаяСуммаДоходаПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОблагаемаяСуммаДоходаПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиИсчисленоПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиУдержаноПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиЗадолженностьПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиИзлишнеУдержаноПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПеречисленоПриИзменении(Элемент)
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТаблицыФормыСведенияОДоходах

&НаКлиенте
Процедура СведенияОДоходахПриАктивизацииСтроки(Элемент)
	ДанныеТекущейСтроки = Элементы.СведенияОДоходах.ТекущиеДанные;
	
	УчетНДФЛКлиент.КодДоходаАктивацииСтроки(
		ЭтаФорма, 
		Объект.НалоговыйПериод, 
		"СведенияОДоходах", 
		"КодДохода",
		"СведенияОДоходахКодВычета");
	
	УчетНДФЛКлиент.СправкиНДФЛУстановитьСтарыеЗначенияКонтролируемыхПолей(
		ДанныеТекущейСтроки, 
		СтарыеЗначенияКонтролируемыхПолей, 
		КонтролируемыеПоля, 
		"СведенияОДоходах");
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемыеДанные = Элементы.СведенияОДоходах.ТекущиеДанные;
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, РедактируемыеДанные, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "СведенияОДоходах");
	
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"Объект.СведенияОДоходах");
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахСуммаДоходаПриИзменении(Элемент)
	ОбновитьИтогиПоТекущейСправке();
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахСуммаВычетаПриИзменении(Элемент)
	ОбновитьИтогиПоТекущейСправке();
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахПослеУдаления(Элемент)
	Объект.ФиксНалоги = Истина;
	
	ОбновитьИтогиПоТекущейСправке();
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Не НаличиеТекущегоСотрудника() Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахПередУдалением(Элемент, Отказ)
	ЗарплатаКадрыОтображениеОшибокКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		"Объект.СведенияОДоходах");
КонецПроцедуры

&НаКлиенте
Процедура СведенияОДоходахКодДоходаПриИзменении(Элемент)
	УчетНДФЛКлиент.КодДоходаПриИзменении(
		ЭтаФорма, 
		Объект.НалоговыйПериод, 
		"СведенияОДоходах", 
		"КодДохода", 
		"СведенияОДоходахКодВычета", 
		"КодВычета", 
		"СуммаВычета");

	ОбновитьИтогиПоТекущейСправке();
КонецПроцедуры

&НаКлиенте
Процедура ДоходыНалогиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, Объект, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
	
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"ДоходыНалоги");
		
КонецПроцедуры
	
&НаКлиенте
Процедура ДоходыНалогиПередУдалением(Элемент, Отказ)
	ЗарплатаКадрыОтображениеОшибокКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		"ДоходыНалоги");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСведенияОВычетах

&НаКлиенте
Процедура СведенияОВычетахПриАктивизацииСтроки(Элемент)
	ДанныеТекущейСтроки = Элементы.СведенияОВычетах.ТекущиеДанные;
	
	УчетНДФЛКлиент.СправкиНДФЛУстановитьСтарыеЗначенияКонтролируемыхПолей(ДанныеТекущейСтроки, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "СведенияОВычетах");
КонецПроцедуры

&НаКлиенте
Процедура СведенияОВычетахПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	РедактируемыеДанные = Элементы.СведенияОВычетах.ТекущиеДанные;
	
	УчетНДФЛКлиент.СправкиНДФЛПриОкончанииРедактирования(ЭтаФорма, Объект, РедактируемыеДанные, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "СведенияОВычетах");
КонецПроцедуры

&НаКлиенте
Процедура СведенияОВычетахСуммаВычетаПриИзменении(Элемент)
	ОбновитьИтогиПоТекущейСправке();
КонецПроцедуры

&НаКлиенте
Процедура СведенияОВычетахПослеУдаления(Элемент)
	Объект.ФиксНалоги = Истина;

	ОбновитьИтогиПоТекущейСправке();
КонецПроцедуры

&НаКлиенте
Процедура СведенияОВычетахПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Не НаличиеТекущегоСотрудника() Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СведенияОВычетахПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"Объект.СведенияОВычетах");
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОВычетахПередУдалением(Элемент, Отказ)
	ЗарплатаКадрыОтображениеОшибокКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		"Объект.СведенияОВычетах");
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Проверить(Команда)
	Отказ = Ложь;
	ПроверитьДанныеДокумента(Отказ);
	
	Если Отказ Тогда
		ТекстПредупреждения = НСтр("ru = 'При проверке обнаружены ошибки'");
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Ошибок не обнаружено'");
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
КонецПроцедуры

&НаСервере
Процедура ПроверитьДанныеДокумента(Отказ)
	
	ЗаполнитьДанныеСправкиПоТаблицеДоходыНалоги();
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект", Тип("ДокументОбъект.СправкаНДФЛ"));
	
	ДокументОбъект.ВнешниеДанные = Истина;
	ДокументОбъект.СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.ПустаяСсылка();
	
	Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ДокументОбъект.ПроверитьДанныеДокумента(Отказ);
	
	ЗарплатаКадрыОтображениеОшибок.ПослеПроверкиЗаполненияВФорме(
		ЭтотОбъект,
		ДокументОбъект,
		СоответствиеДанныхОбъектаДаннымФормы(),
		ОписаниеПодчиненностиДанных(),
		"Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИсправленияДанныхСотрудника(Команда)
	ОтменитьИсправлениеДанныхСотрудника();
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьКарточкуСотрудника(Команда)
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		ПараметрыФормы = Новый Структура("Ключ, АктивнаяСтраница", Объект.Сотрудник, "ЛичныеДанные");
		ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНалоги(Команда)
	ОбновитьНалогиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПринятьИзменения();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьСтрокТаблицыИтогов()
	
	Если Объект.СтатусНалогоплательщика = Справочники.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Тогда
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка30);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка15);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка13);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка09);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка35);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка03);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка06);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка07);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка12);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка05);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка10);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
	ИначеЕсли Объект.СтатусНалогоплательщика = Справочники.СтатусыНалогоплательщиковПоНДФЛ.Резидент Тогда
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка13);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка09);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка35);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка30);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка15);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка03);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка06);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка07);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка12);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка05);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка10);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
	Иначе
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка13);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка30);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка15);
		СтрокаИтогов.Видимость = Истина;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка09);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка35);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка03);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка06);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка07);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка12);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка05);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
		СтрокаИтогов = СтрокаИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка10);
		Если СтрокаИтоговЗаполнена(СтрокаИтогов) Тогда
			СтрокаИтогов.Видимость = Истина;
		Иначе
			СтрокаИтогов.Видимость = Ложь;
			ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтрокаИтоговПоСтавке(СтавкаНДФЛ)
	СтруктураПоиска = Новый Структура("НДФЛСтавка", СтавкаНДФЛ);
	
	НайденныеСтроки = ДоходыНалоги.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаСервере
Функция СтрокаИтоговЗаполнена(СтрокаИтогов)
	Возврат СтрокаИтогов.Общий <> 0 
		Или СтрокаИтогов.Облагаемый <> 0
		Или СтрокаИтогов.Исчислено <> 0
		Или СтрокаИтогов.Удержано <> 0
		Или СтрокаИтогов.НеУдержано <> 0
		Или СтрокаИтогов.ИзлишнеУдержано <> 0
		Или СтрокаИтогов.Перечислено <> 0;
	
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеСправкиИзВременногоХранилища(АдресДанныхСправкиВХранилище)
	ВнешниеДанные = Истина;
	
	ДанныеСправки = ПолучитьИзВременногоХранилища(АдресДанныхСправкиВХранилище);
	ЗаполнитьЗначенияСвойств(Объект, ДанныеСправки);
	ДокументПроведен = ДанныеСправки.ДокументПроведен;
	
	НомерСправки = ДанныеСправки.НомерСправки;
	Ставка = ДанныеСправки.Ставка;
	
	Для каждого СтрокаДоходов Из ДанныеСправки.МассивСведенийОДоходах Цикл
		ЗаполнитьЗначенияСвойств(Объект.СведенияОДоходах.Добавить(), СтрокаДоходов);
	КонецЦикла;
	
	Для каждого СтрокаВычетов Из ДанныеСправки.МассивСведенийОВычетах Цикл
		ЗаполнитьЗначенияСвойств(Объект.СведенияОВычетах.Добавить(), СтрокаВычетов);
	КонецЦикла;
	
	НоваяСтрока = ДанныеСправки.НоваяСтрока;
	
	ПриПолученииДанныхНаСервере();
	
	ЗарплатаКадрыОтображениеОшибок.ПреобразоватьПутиКДаннымВСообщенияхПользователю(
		Объект,
		ЭтотОбъект,
		ДанныеСправки.ОшибкиДанныхСправки,
		СоответствиеДанныхОбъектаДаннымФормы(),
		"Объект");
	
	ЗарплатаКадрыОтображениеОшибок.ПоместитьСообщенияОбОшибкахВДанныеФормы(ЭтотОбъект, ДанныеСправки.ОшибкиДанныхСправки);
	
	ЗарплатаКадрыОтображениеОшибок.УстановитьФлагиНаличияОшибокПоСообщениямОбОшибках(ЭтотОбъект, ДанныеСправки.ОшибкиДанныхСправки, , Истина);
КонецПроцедуры

&НаСервере
Процедура ДополнитьФорму()
	
	ОписаниеЭлементовИндикации = ОписаниеЭлементовСИндикациейОшибок();
	ОписаниеПодчиненности = ОписаниеПодчиненностиДанных();
	ФормаДополнена = Истина;
	
	ЗарплатаКадрыОтображениеОшибок.ПриСозданииНаСервере(ЭтотОбъект, ОписаниеЭлементовИндикации, ОписаниеПодчиненности);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьДанныхСправки()
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		Элементы.ДанныеСправки.Доступность = Истина;
	Иначе
		Элементы.ДанныеСправки.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьНалогиНаСервере()
	
	ОчиститьРеквизитыХраненияОшибокДанныхОНалогах();
		
	Объект.СведенияОДоходах.Очистить();
	Объект.СведенияОВычетах.Очистить();
	
	УчетНДФЛФормы.ДляНалогаНаПрибыльПрочитатьДанныеОДоходахИНалогахСотрудника(
		ЭтаФорма,
		Объект,
		Объект.СведенияОДоходах, 
		Объект.СведенияОВычетах, 
		Объект.НалоговыйПериод, 
		Объект.Дата, 
		Объект.Организация,
		НомерСправки,
		Ставка);
	
	ЗаполнитьТаблицуДоходыНалоги(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСправкиНаСервере()
	
	ОчиститьРеквизитыХраненияОшибокДанныхОНалогах();
	ОчиститьРеквизитыХраненияОшибокДанныеСотрудника();
	
	Если Не Объект.ФиксНалоги Тогда
		Объект.СведенияОДоходах.Очистить();
		Объект.СведенияОВычетах.Очистить();
	КонецЕсли;
	
	ДанныеСправок = Новый Массив;
	
	ДанныеСправок.Добавить(Объект);
	
	УчетНДФЛФормы.ДляНалогаНаПрибыльПрочитатьДанные(
		ДанныеСправок, 
		Объект.СведенияОДоходах, 
		Объект.СведенияОВычетах, 
		Объект.НалоговыйПериод, 
		Объект.Дата, 
		Объект.Организация);
	
	УстановитьПредставлениеАдресов(ЭтаФорма);
	
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьСвойстваЭлементовСФиксациейДанных(ЭтаФорма, Объект, ДокументПроведен);
	ЗаполнитьТаблицуДоходыНалоги(Объект);
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьИнфонадписьИсправления(ИнфоНадписьИсправления, Объект, ДокументПроведен, Истина);
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьПризнакНаличияГражданства(ЭтаФорма, Объект);
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ПрочитатьДанныеФормы();
	
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьСвойстваЭлементовСФиксациейДанных(ЭтаФорма, Объект, ДокументПроведен);
	ЗаполнитьТаблицуДоходыНалоги(Объект);
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьИнфонадписьИсправления(ИнфоНадписьИсправления, Объект, ДокументПроведен, Истина);
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьПризнакНаличияГражданства(ЭтаФорма, Объект);
	
	УстановитьДоступностьДанныхСправки();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеФормы()
	УстановитьПредставлениеАдресов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Функция НаличиеТекущегоСотрудника()
	Возврат ЗначениеЗаполнено(Объект.Сотрудник);
КонецФункции

&НаСервереБезКонтекста
Функция СоответствиеОкончаниеИмениПоляСтавкиНДФЛ()
	
	СтавкиНДФЛ = Новый Соответствие;
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка09, "9");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка13, "13");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка15, "15");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка30, "30");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка35, "35");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка03, "3");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка06, "6");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка07, "7");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка12, "12");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка05, "5");
	СтавкиНДФЛ.Вставить(Перечисления.НДФЛСтавки.Ставка10, "10");
	
	Возврат СтавкиНДФЛ;
	
КонецФункции


#Область МетодыЗаполненияДанныхСправок

&НаСервере
Процедура ПрочитатьДанныеФизическогоЛица(ФизическоеЛицо)
	
	ОчиститьРеквизитыХраненияОшибокДанныеСотрудника();
	ДанныеСправок = Новый Массив;
	ДанныеСправок.Добавить(Объект);
	
	УчетНДФЛФормы.СправкиНДФЛПрочитатьДанныеСотрудников(ДанныеСправок, Объект.НалоговыйПериод, Объект.Дата, Не ДокументПроведен);
	УстановитьПредставлениеАдресов(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ОтменитьИсправлениеДанныхСотрудника()
	
	УчетНДФЛФормы.СправкиНДФЛОтменитьИсправлениеДанныхСотрудника(ЭтаФорма, Объект, Объект.НалоговыйПериод, Объект.Дата);
	
	ОчиститьРеквизитыХраненияОшибокДанныеСотрудника();
	
	УстановитьПредставлениеАдресов(ЭтаФорма);
	УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьПризнакНаличияГражданства(ЭтаФорма, Объект);
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиПоТекущейСправке()
	УчетНДФЛФормы.СправкиНДФЛОбновитьИтоги(Объект, Объект.СведенияОДоходах, Объект.СведенияОВычетах);
	ЗаполнитьТаблицуДоходыНалоги(Объект);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПредставлениеАдресов(Форма)
	
		Если Не ПустаяСтрока(Форма.Объект.Адрес) Тогда
			СтруктураАдреса = ЗарплатаКадры.СтруктураАдресаИзXML(
					Форма.Объект.Адрес, Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица);
			УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеАдреса(СтруктураАдреса, Форма.АдресПредставление);
		Иначе
			Форма.АдресПредставление = НСтр("ru = '<Не заполнен>'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(Форма.Объект.АдресЗарубежом) Тогда
			СтруктураАдреса = РаботаСАдресами.ПредыдущаяСтруктураКонтактнойИнформацииXML(
					Форма.Объект.АдресЗарубежом, Справочники.ВидыКонтактнойИнформации.АдресЗаПределамиРФФизическиеЛица);
			УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеАдреса(СтруктураАдреса, Форма.АдресЗарубежомПредставление);
		Иначе
			Форма.АдресЗарубежомПредставление = НСтр("ru = '<Не заполнен>'");
		КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДоходыНалоги(ДанныеСправки)
	
	ДоходыНалоги.Очистить();
	
	СтавкиНДФЛ = СоответствиеОкончаниеИмениПоляСтавкиНДФЛ();
	
	Для Каждого СтавкаНДФЛ Из СтавкиНДФЛ Цикл
		
		СтрокаДоходыНалоги = ДоходыНалоги.Добавить();
		СтрокаДоходыНалоги.НДФЛСтавка = СтавкаНДФЛ.Ключ;
		
		СтрокаДоходыНалоги.Общий 			= ДанныеСправки["ОбщаяСуммаДоходаПоСтавке" + СтавкаНДФЛ.Значение];
		СтрокаДоходыНалоги.Облагаемый 		= ДанныеСправки["ОблагаемаяСуммаДоходаПоСтавке" + СтавкаНДФЛ.Значение];
		СтрокаДоходыНалоги.Исчислено 		= ДанныеСправки["ИсчисленоПоСтавке" + СтавкаНДФЛ.Значение];
		СтрокаДоходыНалоги.Удержано 		= ДанныеСправки["УдержаноПоСтавке" + СтавкаНДФЛ.Значение];
		СтрокаДоходыНалоги.НеУдержано 		= ДанныеСправки["ЗадолженностьПоСтавке" + СтавкаНДФЛ.Значение];
		СтрокаДоходыНалоги.ИзлишнеУдержано 	= ДанныеСправки["ИзлишнеУдержаноПоСтавке" + СтавкаНДФЛ.Значение];
		СтрокаДоходыНалоги.Перечислено 		= ДанныеСправки["ПеречисленоПоСтавке" + СтавкаНДФЛ.Значение];
		
	КонецЦикла;
	
	УстановитьВидимостьСтрокТаблицыИтогов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСправкиПоТаблицеДоходыНалоги()
	Для Каждого СтрокаИтогов Из ДоходыНалоги Цикл
		ЗаполнитьДанныеСправкиПоСтрокеТаблицыДоходыНалоги(СтрокаИтогов);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСправкиПоСтрокеТаблицыДоходыНалоги(СтрокаДоходыНалоги)
	
	СтавкиНДФЛ = СоответствиеОкончаниеИмениПоляСтавкиНДФЛ();
	
	Окончание = СтавкиНДФЛ[СтрокаДоходыНалоги.НДФЛСтавка];
	Если Окончание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаДоходыНалоги.Видимость Тогда
		
		Объект["ОбщаяСуммаДоходаПоСтавке" + Окончание] 		= СтрокаДоходыНалоги.Общий;
		Объект["ОблагаемаяСуммаДоходаПоСтавке" + Окончание] = СтрокаДоходыНалоги.Облагаемый;
		Объект["ИсчисленоПоСтавке" + Окончание]				= СтрокаДоходыНалоги.Исчислено;
		Объект["УдержаноПоСтавке" + Окончание]				= СтрокаДоходыНалоги.Удержано;
		Объект["ЗадолженностьПоСтавке" + Окончание]			= СтрокаДоходыНалоги.НеУдержано;
		Объект["ИзлишнеУдержаноПоСтавке" + Окончание]		= СтрокаДоходыНалоги.ИзлишнеУдержано;
		Объект["ПеречисленоПоСтавке" + Окончание]			= СтрокаДоходыНалоги.Перечислено;
		
	Иначе
		
		Объект["ОбщаяСуммаДоходаПоСтавке" + Окончание] 		= 0;
		Объект["ОблагаемаяСуммаДоходаПоСтавке" + Окончание] = 0;
		Объект["ИсчисленоПоСтавке" + Окончание]				= 0;
		Объект["УдержаноПоСтавке" + Окончание]				= 0;
		Объект["ЗадолженностьПоСтавке" + Окончание]			= 0;
		Объект["ИзлишнеУдержаноПоСтавке" + Окончание]		= 0;
		Объект["ПеречисленоПоСтавке" + Окончание]			= 0;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьИзменения()
	
	Если Модифицированность Тогда
		АдресДанныхСправки = ПоместитьДанныеСправкиВХранилище();
		
		Оповестить("ИзмененыДанныеСправкиДляРасчетаПоНалогуНаПрибыль", АдресДанныхСправки, ВладелецФормы);
		Модифицированность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьДанныеСправкиВХранилище()
	ЗаполнитьДанныеСправкиПоТаблицеДоходыНалоги();
	
	ОшибкиДанныхСправки = ЗарплатаКадрыОтображениеОшибок.СообщенияОбОшибкахИзДанныхФормы(ЭтотОбъект);
	
	ОписаниеСвязиДанных = СоответствиеПолейТаблицыИтоговПолямИтоговСправки();
	
	ЗарплатаКадрыОтображениеОшибок.ПреобразоватьПутиКДаннымВСообщенияхПользователю(
		ЭтотОбъект,
		Объект,
		ОшибкиДанныхСправки,
		ОписаниеСвязиДанных);
		
	Возврат УчетНДФЛФормы.ПоместитьДанныеСправки2НДФЛВХранилище(
				ЭтаФорма,
				Объект,
				Объект.СведенияОДоходах,
				Объект.СведенияОВычетах,
				НомерСправки,
				ОшибкиДанныхСправки,
				НоваяСтрока,
				Ставка);
		
КонецФункции

#КонецОбласти

#Область ИндикацияОшибок

&НаСервере
Функция ОписаниеЭлементовСИндикациейОшибок() 
	КоллекцияОписанийЭлементов = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементовСИндикациейОшибок();
	
	// Элементы шапки
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "НомерСправки";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "НомерСправки";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Ставка";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Ставка";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Сотрудник";
	ОписаниеЭлемента.ОтображатьНепривязанныеОшибки = Истина;
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Сотрудник";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);

	// таблицы документа 
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ИмяЭлемента = "СведенияОДоходах";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "СведенияОДоходах";
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.СведенияОДоходах";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ИмяЭлемента = "СведенияОВычетах";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "СведенияОВычетах";
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.СведенияОВычетах";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ИмяЭлемента = "ДоходыНалоги";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "ДоходыНалоги";
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "ДоходыНалоги";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	// Личные данные налогоплательщика.
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Фамилия";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Фамилия";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Имя";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Имя";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Отчество";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Отчество";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "ИНН";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.ИНН";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "ДатаРождения";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.ДатаРождения";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "СтатусНалогоплательщика";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.СтатусНалогоплательщика";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Гражданство";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Гражданство";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "ВидДокумента";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.ВидДокумента";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "СерияДокумента";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.СерияДокумента";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "НомерДокумента";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.НомерДокумента";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Адрес";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Адрес";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "АдресЗарубежом";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.АдресЗарубежом";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	Возврат КоллекцияОписанийЭлементов;
КонецФункции

&НаСервере
Функция СоответствиеДанныхОбъектаДаннымФормы()
	
	ОписаниеСоответствияДанныхОбъектовДаннымФормы = ЗарплатаКадрыОтображениеОшибок.ОписаниеСвязиДанных();

	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(
		ОписаниеСоответствияДанныхОбъектовДаннымФормы,  
		"НомерСправки", 
		"НомерСправки");
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(
		ОписаниеСоответствияДанныхОбъектовДаннымФормы,  
		"Ставка", 
		"Ставка");
	
	ЗаполнитьСоответствиеПолейИтоговДокументаСтрокамТаблицыИтоговФормы(ОписаниеСоответствияДанныхОбъектовДаннымФормы);
	
	Возврат ОписаниеСоответствияДанныхОбъектовДаннымФормы;
КонецФункции

&НаСервере
Функция СоответствиеПолейИтоговПоСтавкамПолямТаблицыИтогов()
	ПоляИтогов = Новый Соответствие;
	ПоляИтогов.Вставить("ОбщаяСуммаДоходаПоСтавке", "Общий");
	ПоляИтогов.Вставить("ОблагаемаяСуммаДоходаПоСтавке", "Облагаемый");
	ПоляИтогов.Вставить("ИсчисленоПоСтавке", "Исчислено");
	ПоляИтогов.Вставить("УдержаноПоСтавке", "Удержано");
	ПоляИтогов.Вставить("ЗадолженностьПоСтавке", "НеУдержано");
	ПоляИтогов.Вставить("ИзлишнеУдержаноПоСтавке", "ИзлишнеУдержано");
	ПоляИтогов.Вставить("ПеречисленоПоСтавке", "Перечислено");
	
	Возврат ПоляИтогов;
КонецФункции

&НаСервере
Процедура ЗаполнитьСоответствиеПолейИтоговДокументаСтрокамТаблицыИтоговФормы(ОписаниеСоответствияДанныхОбъектовДаннымФормы)
	
	СтавкиНДФЛ = СоответствиеОкончаниеИмениПоляСтавкиНДФЛ();
	
	ПоляИтогов = СоответствиеПолейИтоговПоСтавкамПолямТаблицыИтогов();
	
	Для Каждого СтавкаНДФЛ Из СтавкиНДФЛ Цикл
		КлючСвязи = ЗарплатаКадрыОтображениеОшибок.КлючСвязиДанныхСтрокТаблиц();
		ЗарплатаКадрыОтображениеОшибок.ДобавитьЭлементКлючаСвязиПоФиксированномуЗначениюПоляПодчиненнойТаблицы(
			КлючСвязи,
			"НДФЛСтавка",
			СтавкаНДФЛ.Ключ);
			
		Для Каждого ИтоговоеПоле Из ПоляИтогов Цикл
			ПолеИтогаПоСтавке = ИтоговоеПоле.Ключ + СтавкаНДФЛ.Значение;
			
			ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитаСДаннымиСтрокиТаблицы(
				ОписаниеСоответствияДанныхОбъектовДаннымФормы,
				ПолеИтогаПоСтавке,
				"ДоходыНалоги",
				ИтоговоеПоле.Значение,
				КлючСвязи);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СоответствиеПолейТаблицыИтоговПолямИтоговСправки()
	ОписаниеСоответствияДанныхОбъектовДаннымФормы = ЗарплатаКадрыОтображениеОшибок.ОписаниеСвязиДанных();
	
	СтавкиНДФЛ = СоответствиеОкончаниеИмениПоляСтавкиНДФЛ();
	
	ПоляИтогов = СоответствиеПолейИтоговПоСтавкамПолямТаблицыИтогов();
	
	Для Каждого СтавкаНДФЛ Из СтавкиНДФЛ Цикл
		КлючСвязи = ЗарплатаКадрыОтображениеОшибок.КлючСвязиДанныхСтрокТаблиц();
		ЗарплатаКадрыОтображениеОшибок.ДобавитьЭлементКлючаСвязиПоФиксированномуЗначениюПоляВедущейТаблицы(
			КлючСвязи,
			"НДФЛСтавка",
			СтавкаНДФЛ.Ключ);
			
		Для Каждого ИтоговоеПоле Из ПоляИтогов Цикл
			ПолеИтогаПоСтавке = ИтоговоеПоле.Ключ + СтавкаНДФЛ.Значение;
			
			ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиДанныхСтрокиТаблицыСРеквизитом(
				ОписаниеСоответствияДанныхОбъектовДаннымФормы,
				"ДоходыНалоги",
				ИтоговоеПоле.Значение,
				ПолеИтогаПоСтавке,
				КлючСвязи);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ОписаниеСоответствияДанныхОбъектовДаннымФормы;
КонецФункции

&НаСервере
Функция ОписаниеПодчиненностиДанных() 
	ОписаниеПодчиненностиДанных = ЗарплатаКадрыОтображениеОшибок.ОписаниеПодчиненностиДанныхФормы();
	
	Возврат ОписаниеПодчиненностиДанных;
КонецФункции

&НаСервере
Процедура ОчиститьРеквизитыХраненияОшибокДанныеСотрудника()
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Фамилия");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Фамилия");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Имя");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Имя");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Отчество");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Отчество");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.ИНН");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.ИНН");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Адрес");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Адрес");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.ВидДокумента");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.ВидДокумента");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.СерияДокумента");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.СерияДокумента");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.НомерДокумента");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.НомерДокумента");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Гражданство");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.Гражданство");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.ДатаРождения");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.ДатаРождения");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.СтатусНалогоплательщика");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.СтатусНалогоплательщика");
		
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		"Объект.АдресЗарубежом");
		
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		"Объект.АдресЗарубежом");
			
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыХраненияОшибокДанныхОНалогах()
	
	Для Индекс = 0 По Объект.СведенияОДоходах.Количество() - 1 Цикл
		ПутьКДанным = "Объект.СведенияОДоходах[" + Формат(Индекс, "ЧН=0; ЧГ=") + "].*";
			
		ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
			ЭтотОбъект,
			ПутьКДанным);
		
		ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
			ЭтотОбъект,
			ПутьКДанным);

	КонецЦикла;
		
	Для Индекс = 0 По Объект.СведенияОВычетах.Количество() - 1 Цикл
		ПутьКДанным = "Объект.СведенияОВычетах[" + Формат(Индекс, "ЧН=0; ЧГ=") + "].*";
			
		ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
			ЭтотОбъект,
			ПутьКДанным);
		
		ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
			ЭтотОбъект,
			ПутьКДанным);

	КонецЦикла;
		
	Для Индекс = 0 По ДоходыНалоги.Количество() - 1 Цикл
		ПутьКДанным = "ДоходыНалоги[" + Формат(Индекс, "ЧН=0; ЧГ=") + "].*";
			
		ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
			ЭтотОбъект,
			ПутьКДанным);
		
		ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
			ЭтотОбъект,
			ПутьКДанным);

	КонецЦикла;
	
КонецПроцедуры
	
&НаСервере
Процедура ОчиститьРеквизитыХраненияОшибокВСтрокеИтогов(СтрокаИтогов)
	ИндексСтроки = ДоходыНалоги.Индекс(СтрокаИтогов);
	
	ПутьКДанным = "ДоходыНалоги[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=") + "]";
	
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(
		ЭтотОбъект,
		ПутьКДанным);
	
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		ПутьКДанным);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементИндикацииОшибкиНажатие(Элемент, СтандартнаяОбработка)
	ЗарплатаКадрыОтображениеОшибокКлиент.ЭлементИндикацииОшибкиНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементИндикацииПриАктивизацииЯчейки(Элемент)
	ЗарплатаКадрыОтображениеОшибокКлиент.ЭлементИндикацииПриАктивизацииЯчейки(ЭтотОбъект, Элемент);
КонецПроцедуры
	
СтарыеЗначенияКонтролируемыхПолей = Новый Соответствие;

#КонецОбласти

#КонецОбласти
