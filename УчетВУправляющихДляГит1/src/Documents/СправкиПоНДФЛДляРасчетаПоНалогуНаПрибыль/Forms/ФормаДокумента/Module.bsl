#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущийСотрудник;
&НаКлиенте
Перем ТекущийНомерСправки;
&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолей;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ФормаДополнена Тогда
		ДополнитьФорму();
	КонецЕсли;
	
	Если Параметры.Ключ = Документы.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.ПустаяСсылка() Тогда
		ЗначенияДляЗаполнения = Новый Структура("Год, Организация, Ответственный",
			"Объект.НалоговыйПериод",
			"Объект.Организация",
			"Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);

		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
	УчетНДФЛФормы.СведенияОДоходахПриСозданииНаСервере(ЭтаФорма, "Объект.СведенияОДоходах.КодДохода", "СведенияОДоходахКодВычета", "СведенияОДоходахСуммаВычета");	
	
	УчетНДФЛФормы.СправкиНДФЛЗаполнитьСписокКонтролируемыхПолей(ЭтаФорма, Истина);
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не ФормаДополнена Тогда
		ДополнитьФорму();
	КонецЕсли;
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ЗарплатаКадрыОтображениеОшибок.ПриЧтенииДанныхВФорме(
		ЭтотОбъект,
		ТекущийОбъект,
		СоответствиеДанныхОбъектаДаннымФормы(),
		ОписаниеПодчиненностиДанных());
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ЗарплатаКадрыОтображениеОшибок.ПередЗаписьюДанныхВФорме(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ДокументПроведен = Объект.Проведен;
	
	УстановитьПредставлениеАдресов(Объект.Сотрудники);
	ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(Объект.Сотрудники, Объект.НалоговыйПериод);

	Если Элементы.Сотрудники.ТекущаяСтрока <> Неопределено Тогда
		ДанныеТекущейСтроки = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьИнфонадписьИсправления(ИнфоНадписьИсправления, ДанныеТекущейСтроки, ДокументПроведен);
	КонецЕсли;
	
	ЗарплатаКадрыОтображениеОшибок.ПослеЗаписиДанныхВФорме(
		ЭтотОбъект,
		ТекущийОбъект,
		СоответствиеДанныхОбъектаДаннымФормы(),
		ОписаниеПодчиненностиДанных());
		
	ЗаполнитьСтавкиНДФЛВТаблицеСотрудники()
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеДанныхФизическогоЛица" Тогда
		
		ПрочитатьДанныеФизическогоЛица(Источник);
		
	ИначеЕсли ИмяСобытия = "ИзмененыДанныеСправкиДляРасчетаПоНалогуНаПрибыль" И Источник = ЭтаФорма Тогда
		
		ЗагрузитьДанныеСправкиИзХранилища(Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Объект");
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Не удалось провести документ.
                              |При проверке заполнения были обнаружены ошибки.'");
		
		ЗарплатаКадрыОтображениеОшибок.ПослеПроверкиЗаполненияВФорме(
			ЭтотОбъект,
			ДокументОбъект,
			СоответствиеДанныхОбъектаДаннымФормы(),
			ОписаниеПодчиненностиДанных(),
			"Объект");
		
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗарплатаКадрыОтображениеОшибок.ПослеПроверкиЗаполненияВФорме(
			ЭтотОбъект,
			ДокументОбъект,
			СоответствиеДанныхОбъектаДаннымФормы(),
			ОписаниеПодчиненностиДанных(),
			"Объект");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	УчетНДФЛФормы.СправкаНДФЛУстановитьИнфонадписьОписаниеДоходовОрганизации(ЭтаФорма, Истина, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйПериодПриИзменении(Элемент)
	
	НалоговыйПериодПриИзмененииНаСервере();
	
	ДанныеТекущейСтроки = Элементы.Сотрудники.ТекущиеДанные; 
	
	УчетНДФЛКлиент.СправкиНДФЛУстановитьСтарыеЗначенияКонтролируемыхПолей(ДанныеТекущейСтроки, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, "ИТОГИ");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	ЗарплатаКадрыОтображениеОшибокКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Объект.Организация");
КонецПроцедуры

&НаСервере
Процедура НалоговыйПериодПриИзмененииНаСервере()
	
	УчетНДФЛФормы.СправкаНДФЛУстановитьИнфонадписьОписаниеДоходовОрганизации(ЭтаФорма, Истина, , Истина);
	
	Для Каждого ДанныеСправки Из Объект.Сотрудники Цикл
		Если Не ДанныеСправки.ФиксНалоги Тогда
			УдалитьСтрокиТаблицыДоходов(ДанныеСправки.НомерСправки);
			УдалитьСтрокиТаблицыВычетов(ДанныеСправки.НомерСправки);
		КонецЕсли;
	КонецЦикла;
	
	УчетНДФЛФормы.ДляНалогаНаПрибыльПрочитатьДанные(
		Объект.Сотрудники, 
		Объект.СведенияОДоходах, 
		Объект.СведенияОВычетах, 
		Объект.НалоговыйПериод, 
		Объект.Дата, 
		Объект.Организация);
	
	УстановитьПредставлениеАдресов(Объект.Сотрудники);
	ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(Объект.Сотрудники, Объект.НалоговыйПериод);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Объект.Сотрудники.Очистить();
	Объект.СведенияОВычетах.Очистить();
	Объект.СведенияОДоходах.Очистить();
	
	УчетНДФЛФормы.СправкаНДФЛУстановитьИнфонадписьОписаниеДоходовОрганизации(ЭтаФорма, Истина, , Истина);
	
	ДанныеСправок = Новый Массив;
	Для Каждого ДанныеСправки Из Объект.Сотрудники Цикл
		
		Если Не ДанныеСправки.ФиксНалоги Тогда
			
			УдалитьСтрокиТаблицыДоходов(ДанныеСправки.НомерСправки);
			УдалитьСтрокиТаблицыВычетов(ДанныеСправки.НомерСправки);
			
			ДанныеСправок.Добавить(ДанныеСправки);
			
		КонецЕсли;
		
	КонецЦикла;
		
	УчетНДФЛФормы.ДляНалогаНаПрибыльПрочитатьДанныеОДоходахИНалогах(
		ДанныеСправок, 
		Объект.СведенияОДоходах, 
		Объект.СведенияОВычетах, 
		Объект.НалоговыйПериод, 
		Объект.Дата, 
		Объект.Организация);

КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Сотрудники.ТекущиеДанные <> Неопределено Тогда
	    АдресДанныхСправки = ПоместитьДанныеСправкиВоВременноеХранилище();
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("АдресДанныхСправкиВХранилище", АдресДанныхСправки);
		ПараметрыОткрытия.Вставить("ТолькоПросмотр", ТолькоПросмотр);
		ПараметрыОткрытия.Вставить("ВнешниеДанные", Истина);
		
		ОткрытьФорму("Документ.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Форма.ФормаСотрудника", ПараметрыОткрытия, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработкаПодбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	
	АдресДанныхСправки = ПоместитьДанныеСправкиВоВременноеХранилище(Истина);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресДанныхСправкиВХранилище", АдресДанныхСправки);
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("ВнешниеДанные", Истина);
	
	ОткрытьФорму("Документ.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Форма.ФормаСотрудника", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Оповещение = Новый ОписаниеОповещения("ВыполнитьПодключаемуюКомандуЗавершение", ЭтотОбъект, Команда);
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодключаемуюКомандуЗавершение(Результат, Команда) Экспорт
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Проверить(Команда)
	
	Отказ = Ложь;
	ПроверитьДанныеДокумента(Отказ);
	
	Если Отказ Тогда
		ТекстПредупреждения = НСтр("ru = 'При проверке обнаружены ошибки'");
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Ошибок не обнаружено'");
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)

	Если НЕ ЗарплатаКадрыКлиент.ОрганизацияЗаполнена(Объект) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗарегистрированыДоходы Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не зарегистрированы доходы.'"));
		Возврат;
	КонецЕсли;
	
	ЗаполнитьНаСервере();
	Если Объект.Сотрудники.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не обнаружены данные для заполнения документа.'");
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Внимание.'"));
	Иначе
		Элементы.Сотрудники.ТекущаяСтрока = Объект.Сотрудники[0].ПолучитьИдентификатор();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	КадровыйУчетКлиент.ПодобратьФизическихЛицОрганизации(
		Элементы.Сотрудники, 
		Объект.Организация, 
		АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры

&НаКлиенте
Процедура Пронумеровать(Команда)
	ПронумероватьСправкиНаСервере(ТекущийСотрудник, ТекущийНомерСправки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АдресСпискаПодобранныхСотрудников() Экспорт
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
КонецФункции

&НаСервере
Процедура ЗаполнитьСтавкиНДФЛВТаблицеСотрудники()
	Для Каждого СтрокаТаблицы Из Объект.Сотрудники Цикл
		ЗаполнитьСтавкиВСтрокеТаблицыСотрудники(СтрокаТаблицы);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкиВСтрокеТаблицыСотрудники(СтрокаТаблицы)
	СтрокаТаблицы.Ставка03 = Перечисления.НДФЛСтавки.Ставка03;
	СтрокаТаблицы.Ставка05 = Перечисления.НДФЛСтавки.Ставка05;
	СтрокаТаблицы.Ставка06 = Перечисления.НДФЛСтавки.Ставка06;
	СтрокаТаблицы.Ставка07 = Перечисления.НДФЛСтавки.Ставка07;
	СтрокаТаблицы.Ставка09 = Перечисления.НДФЛСтавки.Ставка09;
	СтрокаТаблицы.Ставка10 = Перечисления.НДФЛСтавки.Ставка10;
	СтрокаТаблицы.Ставка12 = Перечисления.НДФЛСтавки.Ставка12;
	СтрокаТаблицы.Ставка13 = Перечисления.НДФЛСтавки.Ставка13;
	СтрокаТаблицы.Ставка15 = Перечисления.НДФЛСтавки.Ставка15;
	СтрокаТаблицы.Ставка30 = Перечисления.НДФЛСтавки.Ставка30;
	СтрокаТаблицы.Ставка35 = Перечисления.НДФЛСтавки.Ставка35;
КонецПроцедуры

&НаСервере
Процедура ДополнитьФорму()
	ОписаниеЭлементовИндикации = ОписаниеЭлементовСИндикациейОшибок();
	ОписаниеПодчиненности = ОписаниеПодчиненностиДанных();
	ФормаДополнена = Истина;
	
	ЗарплатаКадрыОтображениеОшибок.ПриСозданииНаСервере(ЭтотОбъект, ОписаниеЭлементовИндикации, ОписаниеПодчиненности);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействия(ОповещениеЗавершения = Неопределено)
	
	ОчиститьСообщения();
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Отказ = Ложь;
	ПроверитьДанныеДокумента(Отказ);
	
	Если Отказ Тогда
		
		ТекстВопроса = НСтр("ru = 'В документе обнаружены ошибки.
							|Продолжить (не рекомендуется)?'");
							
		Оповещение = Новый ОписаниеОповещения("ПроверитьСЗапросомДальнейшегоДействияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет, НСтр("ru = 'Предупреждение.'"));
		
	Иначе 
		ПроверитьСЗапросомДальнейшегоДействияЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействияЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьДанныеДокумента(Отказ)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект", Тип("ДокументОбъект.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль"));
	
	Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ДокументОбъект.ПроверитьДанныеДокумента(Отказ);
	
	ЗарплатаКадрыОтображениеОшибок.ПослеПроверкиЗаполненияВФорме(
		ЭтотОбъект,
		ДокументОбъект,
		СоответствиеДанныхОбъектаДаннымФормы(),
		ОписаниеПодчиненностиДанных(),
		"Объект");
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ПрочитатьДанныеФормы();
	
	Если Объект.Сотрудники.Количество() > 0 Тогда
		
		Элементы.Сотрудники.ТекущаяСтрока = Объект.Сотрудники[0].ПолучитьИдентификатор();
	
		ДанныеТекущейСтроки = Объект.Сотрудники[0];
		УчетНДФЛКлиентСервер.СправкиНДФЛУстановитьИнфонадписьИсправления(ИнфоНадписьИсправления, ДанныеТекущейСтроки, ДокументПроведен);
		
	КонецЕсли;
	
	УчетНДФЛФормы.СправкаНДФЛУстановитьИнфонадписьОписаниеДоходовОрганизации(ЭтаФорма, Истина, , Истина);
	
	УстановитьОтображениеНалоговПоСтавкам();
	
	ДокументПроведен = Объект.Проведен;
	
	ЗаполнитьСтавкиНДФЛВТаблицеСотрудники()
	
КонецПроцедуры	

&НаСервере
Процедура ПрочитатьДанныеФормы()
	ПрочитатьДанныеСправок();
	УстановитьПредставлениеАдресов(Объект.Сотрудники);
	ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(Объект.Сотрудники, Объект.НалоговыйПериод);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеСправок()
	
	УчетНДФЛФормы.СправкиНДФЛПрочитатьДанныеСотрудников(Объект.Сотрудники, Объект.НалоговыйПериод, Объект.Дата, Не ДокументПроведен);
	
КонецПроцедуры

#Область МетодыЗаполненияДанныхСправок

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.Сотрудники.Очистить();
	Объект.СведенияОДоходах.Очистить();
	Объект.СведенияОВычетах.Очистить();
	
	НомерСправки = Документы.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерПервойСправки(Объект.Дата, Объект.НалоговыйПериод, Объект.Организация);
	
	ДобавляемыеСотрудники = СотрудникиДляЗаполненияСправки();
	
	Для каждого СтрокаСотрудника Из ДобавляемыеСотрудники Цикл
		
		СтрокаТаблицы = Объект.Сотрудники.Добавить();
		СтрокаТаблицы.Сотрудник = СтрокаСотрудника.Сотрудник;
		СтрокаТаблицы.Ставка = СтрокаСотрудника.Ставка;
		СтрокаТаблицы.НомерСправки = НомерСправки;
		
		НомерСправки = НомерСправки + 1;
	КонецЦикла;
	
	УчетНДФЛФормы.ДляНалогаНаПрибыльПрочитатьДанные(
		Объект.Сотрудники, 
		Объект.СведенияОДоходах, 
		Объект.СведенияОВычетах, 
		Объект.НалоговыйПериод, 
		Объект.Дата, 
		Объект.Организация);
	
	ЗаполнитьСтавкиНДФЛВТаблицеСотрудники();
	УстановитьОтображениеНалоговПоСтавкам();
	
	УстановитьПредставлениеАдресов(Объект.Сотрудники);
	ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(Объект.Сотрудники, Объект.НалоговыйПериод);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПодбораНаСервере(ДобавляемыеСотрудники)
	
	ИдентификаторТекущейСтроки = Элементы.Сотрудники.ТекущаяСтрока;
	
	Если Объект.Сотрудники.Количество() = 0 Тогда
		НомерСправки = Документы.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерПервойСправки(Объект.Дата, Объект.НалоговыйПериод, Объект.Организация);
	Иначе
		НомерСправки = Объект.Сотрудники[Объект.Сотрудники.Количество() - 1].НомерСправки + 1;
	КонецЕсли;
	
	ДобавляемыеСотрудникиСоСтавками = СотрудникиДляЗаполненияСправки(ДобавляемыеСотрудники);
	
	ДанныеСправок = Новый Массив;
	Для каждого СтрокаСотрудника Из ДобавляемыеСотрудникиСоСтавками Цикл
		
		СтрокаТаблицы = Объект.Сотрудники.Добавить();
		СтрокаТаблицы.Сотрудник = СтрокаСотрудника.Сотрудник;
		СтрокаТаблицы.Ставка = СтрокаСотрудника.Ставка;
		СтрокаТаблицы.НомерСправки = НомерСправки;
		
		ЗаполнитьСтавкиВСтрокеТаблицыСотрудники(СтрокаТаблицы);
		
		НомерСправки = НомерСправки + 1;
		
		ДанныеСправок.Добавить(СтрокаТаблицы);
		
		ИдентификаторТекущейСтроки = СтрокаТаблицы.ПолучитьИдентификатор();
		
	КонецЦикла;
	
	УчетНДФЛФормы.ДляНалогаНаПрибыльПрочитатьДанные(
		ДанныеСправок,
		Объект.СведенияОДоходах, 
		Объект.СведенияОВычетах, 
		Объект.НалоговыйПериод, 
		Объект.Дата, 
		Объект.Организация);
	
	УстановитьПредставлениеАдресов(Объект.Сотрудники);
	ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(Объект.Сотрудники, Объект.НалоговыйПериод);
	
	Элементы.Сотрудники.ТекущаяСтрока = ИдентификаторТекущейСтроки;
	
КонецПроцедуры

&НаСервере
Функция НомерПервойСправки()
	Если Объект.Сотрудники.Количество() = 0 Тогда
		Возврат Документы.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерПервойСправки(Объект.Дата, Объект.НалоговыйПериод, Объект.Организация);
	Иначе
		Возврат Объект.Сотрудники[Объект.Сотрудники.Количество() - 1].НомерСправки + 1;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеФизическогоЛица(ФизическоеЛицо)
	
	СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ФизическоеЛицо));
	
	Если СтрокиПоСотруднику.Количество() > 0 Тогда
		УчетНДФЛФормы.СправкиНДФЛПрочитатьДанныеСотрудников(СтрокиПоСотруднику, Объект.НалоговыйПериод, Объект.Дата, Истина);
		УстановитьПредставлениеАдресов(СтрокиПоСотруднику);
		ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(СтрокиПоСотруднику, Объект.НалоговыйПериод);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиТаблицыДоходов(НомерСправки)
	
	УдаляемыеСтрокиТаблицы = Объект.СведенияОДоходах.НайтиСтроки(Новый Структура("НомерСправки", НомерСправки));
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтрокиТаблицы Цикл
		Объект.СведенияОДоходах.Удалить(Объект.СведенияОДоходах.Индекс(УдаляемаяСтрока));
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиТаблицыВычетов(НомерСправки)
	
	УдаляемыеСтрокиТаблицы = Объект.СведенияОВычетах.НайтиСтроки(Новый Структура("НомерСправки", НомерСправки));
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтрокиТаблицы Цикл
		Объект.СведенияОВычетах.Удалить(Объект.СведенияОВычетах.Индекс(УдаляемаяСтрока));
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПронумероватьСправкиНаСервере(ТекущийСотрудник, ТекущийНомерСправки)
	
	НомерПервойСправки = Документы.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НомерПервойСправки(Объект.Дата, Объект.НалоговыйПериод, Объект.Организация);
	
	ТаблицаДоходов = Объект.СведенияОДоходах.Выгрузить();
	ТаблицаВычетов = Объект.СведенияОВычетах.Выгрузить();
	Объект.СведенияОДоходах.Очистить();
	Объект.СведенияОВычетах.Очистить();
	СтруктураПоиска = Новый Структура("НомерСправки");
	Для каждого СтрокаТЧ Из Объект.Сотрудники Цикл
		СтруктураПоиска.НомерСправки = СтрокаТЧ.НомерСправки;
		СтрокаТЧ.НомерСправки = НомерПервойСправки;
		Для каждого СтрокаМассива Из ТаблицаДоходов.НайтиСтроки(СтруктураПоиска) Цикл
			НоваяСтрока = Объект.СведенияОДоходах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаМассива);
			НоваяСтрока.НомерСправки = НомерПервойСправки;
		КонецЦикла;
		Для каждого СтрокаМассива Из ТаблицаВычетов.НайтиСтроки(СтруктураПоиска) Цикл
			НоваяСтрока = Объект.СведенияОВычетах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаМассива);
			НоваяСтрока.НомерСправки = НомерПервойСправки;
		КонецЦикла;
		НомерПервойСправки = НомерПервойСправки + 1;
	КонецЦикла;
	
	Если Элементы.Сотрудники.ТекущаяСтрока <> Неопределено Тогда
		
		ДанныеТекущейСтроки = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		ТекущийНомерСправки = ДанныеТекущейСтроки.НомерСправки;
		ТекущийСотрудник = ДанныеТекущейСтроки.Сотрудник;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СотрудникиДляЗаполненияСправки(Сотрудники = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ГоловнаяОрганизация"));
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("НалоговыйПериод", Объект.НалоговыйПериод);
	Запрос.УстановитьПараметр("КонецНП",КонецГода(Дата(Объект.НалоговыйПериод,1,1)));
	Запрос.УстановитьПараметр("НачалоНП", НачалоГода(Дата(Объект.НалоговыйПериод,1,1)));
	Запрос.УстановитьПараметр("НачалоОсмотраДоходов", НачалоГода(Дата(Объект.НалоговыйПериод - 1,1,1)));
	Запрос.УстановитьПараметр("ДатаПодачиСведений", Объект.Дата);
	Запрос.УстановитьПараметр("КодыДоходов", УчетНДФЛ.КодыДоходовПоЦеннымБумагам(Объект.НалоговыйПериод));
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо КАК Сотрудник,
	|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛОбороты.КодДохода.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	СведенияОДоходахНДФЛОбороты.ДатаПолученияДохода КАК Период,
	|	СведенияОДоходахНДФЛОбороты.КодДохода КАК КодДохода,
	|	СведенияОДоходахНДФЛОбороты.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТФизическиеЛицаПолучавшиеДоходы
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛОбороты
	|ГДЕ
	|	СведенияОДоходахНДФЛОбороты.Период МЕЖДУ &НачалоОсмотраДоходов И &ДатаПодачиСведений
	|	И СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И СведенияОДоходахНДФЛОбороты.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
	|	И СведенияОДоходахНДФЛОбороты.ФизическоеЛицо В(&Сотрудники)
	|	И СведенияОДоходахНДФЛОбороты.Организация = &Организация
	|	И СведенияОДоходахНДФЛОбороты.КодДохода В(&КодыДоходов)";
	Если Сотрудники = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "И СведенияОДоходахНДФЛОбороты.ФизическоеЛицо В(&Сотрудники)", "")
	Иначе
		Запрос.Текст = ТекстЗапроса	
	КонецЕсли;
	Запрос.Выполнить();
	
	Если Объект.НалоговыйПериод < 2017 Тогда
		УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента2016(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛицаПолучавшиеДоходы", "УчитыватьИзмененияСтатусаДляОтчетности");
	Иначе
		УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛицаПолучавшиеДоходы", "УчитыватьИзмененияСтатусаДляОтчетности");
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизическиеЛицаПолучавшиеДоходы.Сотрудник КАК Сотрудник,
	|	СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ КАК Ставка,
	|	ФизическиеЛицаПолучавшиеДоходы.Сотрудник.Наименование КАК СотрудникНаименование
	|ПОМЕСТИТЬ ВТФизическиеЛицаСоСтавками
	|ИЗ
	|	ВТФизическиеЛицаПолучавшиеДоходы КАК ФизическиеЛицаПолучавшиеДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО ФизическиеЛицаПолучавшиеДоходы.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И ФизическиеЛицаПолучавшиеДоходы.КатегорияДохода = СтавкаНДФЛПоСтавкеРезидента.КатегорияДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 3000
	|	ФизическиеЛицаПолучавшиеДоходы.Сотрудник КАК Сотрудник,
	|	ФизическиеЛицаПолучавшиеДоходы.Ставка КАК Ставка,
	|	ФизическиеЛицаПолучавшиеДоходы.СотрудникНаименование КАК СотрудникНаименование
	|ИЗ
	|	ВТФизическиеЛицаСоСтавками КАК ФизическиеЛицаПолучавшиеДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Сотрудники КАК СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыльСотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль КАК СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль
	|			ПО СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыльСотрудники.Ссылка = СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Ссылка
	|				И (СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Организация = &Организация)
	|				И (СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.НалоговыйПериод = &НалоговыйПериод)
	|				И (СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Дата <= &ДатаПодачиСведений)
	|				И (СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Проведен)
	|		ПО ФизическиеЛицаПолучавшиеДоходы.Сотрудник = СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыльСотрудники.Сотрудник
	|			И (СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыльСотрудники.Ссылка <> &Ссылка)
	|ГДЕ
	|	СправкиПоНДФЛДляРасчетаПоНалогуНаПрибыль.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	СотрудникНаименование,
	|	Ставка";
	Если Объект.НалоговыйПериод < 2017 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ФизическиеЛицаПолучавшиеДоходы.КатегорияДохода = СтавкаНДФЛПоСтавкеРезидента.КатегорияДохода", "ФизическиеЛицаПолучавшиеДоходы.КодДохода = СтавкаНДФЛПоСтавкеРезидента.КодДохода")
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьПредставлениеАдресов(ДанныеСправок)
	
	Для Каждого ДанныеСправкиПоСотруднику Из ДанныеСправок Цикл
		
		Если Не ПустаяСтрока(ДанныеСправкиПоСотруднику.Адрес) Тогда
			СтруктураАдреса = ЗарплатаКадры.СтруктураАдресаИзXML(
					ДанныеСправкиПоСотруднику.Адрес, Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица);
			УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеАдреса(СтруктураАдреса, ДанныеСправкиПоСотруднику.АдресПредставление);
		Иначе
			ДанныеСправкиПоСотруднику.АдресПредставление = НСтр("ru = '<Не заполнен>'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(ДанныеСправкиПоСотруднику.АдресЗарубежом) Тогда
			СтруктураАдреса = РаботаСАдресами.ПредыдущаяСтруктураКонтактнойИнформацииXML(
					ДанныеСправкиПоСотруднику.АдресЗарубежом, Справочники.ВидыКонтактнойИнформации.АдресЗаПределамиРФФизическиеЛица);
			УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеАдреса(СтруктураАдреса, ДанныеСправкиПоСотруднику.АдресЗарубежомПредставление);
		Иначе
			ДанныеСправкиПоСотруднику.АдресЗарубежомПредставление = НСтр("ru = '<Не заполнен>'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьИнфоНадписиОДокументахУдостоверяющихЛичность(СтрокиПоСотрудникам, НалоговыйПериод)
	
	Если ТипЗнч(СтрокиПоСотрудникам) = Тип("ДанныеФормыЭлементКоллекции") Тогда
		МассивСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокиПоСотрудникам.Сотрудник);
		СтрокиПоСотрудникам = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокиПоСотрудникам);
	ИначеЕсли ТипЗнч(СтрокиПоСотрудникам) = Тип("Массив") Тогда
		МассивСотрудников = Новый Массив;
		Для каждого СтрокаПоСотруднику Из СтрокиПоСотрудникам Цикл
			МассивСотрудников.Добавить(СтрокаПоСотруднику.Сотрудник);
		КонецЦикла;
	Иначе
		МассивСотрудников = СтрокиПоСотрудникам.Выгрузить(, "Сотрудник").ВыгрузитьКолонку("Сотрудник");
	КонецЕсли;
	
	ТекущиеУдостоверенияЛичности = КадровыйУчетФормы.ТекущиеУдостоверенияЛичностиФизическихЛиц(
		МассивСотрудников, КонецГода(Дата(НалоговыйПериод, 1, 1)));
	
	Для каждого СтрокаСотрудника Из СтрокиПоСотрудникам Цикл
		СтрокаСотрудника.ИнфоОДокументеУдостоверяющемЛичностьНадпись = КадровыйУчетФормы.ИнфоНадписьОДокументеУдостоверяющемЛичность(
			ТекущиеУдостоверенияЛичности, СтрокаСотрудника.Сотрудник, СтрокаСотрудника.ВидДокумента, СтрокаСотрудника.СерияДокумента, СтрокаСотрудника.НомерДокумента);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ПоместитьДанныеСправкиВоВременноеХранилище(НоваяСтрока = Ложь)
	
	Если НоваяСтрока Тогда
		ДанныеСотрудника = Новый Структура;
		ДанныеСотрудника.Вставить("НомерСправки", НомерПервойСправки());
		ДанныеСотрудника.Вставить("Ставка", Перечисления.НДФЛСтавки.Ставка13);
		
		ОшибкиДанныхСправки = Новый Массив;
		СведенияОДоходах = Новый Массив;
		СведенияОВычетах = Новый Массив;
	Иначе	
		ДанныеСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		
		ИндексСтроки = Объект.Сотрудники.Индекс(ДанныеСотрудника);
		
		ПутьКДанным = "Объект.Сотрудники[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=") + "]";
	
		ОшибкиДанныхСправки = ЗарплатаКадрыОтображениеОшибок.СообщениеОбОшибкахДляПередачиВФормуРедактированияСтрокиТаблицыФормы(
								ЭтотОбъект, 
								ПутьКДанным, 
								ОписаниеПодчиненностиДанных());
		
		СтруктураПоиска = Новый Структура("НомерСправки", ДанныеСотрудника.НомерСправки);
		
		СведенияОДоходах = Объект.СведенияОДоходах.НайтиСтроки(СтруктураПоиска);
		СведенияОВычетах = Объект.СведенияОВычетах.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;
	
	Возврат УчетНДФЛФормы.ПоместитьДанныеСправки2НДФЛВХранилище(
				ЭтотОбъект,
				ДанныеСотрудника,
				СведенияОДоходах,
				СведенияОВычетах,
				ДанныеСотрудника.НомерСправки,
				ОшибкиДанныхСправки,
				НоваяСтрока, 
				ДанныеСотрудника.Ставка);
	
КонецФункции

&НаСервере
Процедура ЗагрузитьДанныеСправкиИзХранилища(АдресДанныхСправкиВХранилище)
	
	ДанныеСправки = ПолучитьИзВременногоХранилища(АдресДанныхСправкиВХранилище);
	
	Если ДанныеСправки.НоваяСтрока Тогда
		ДанныеСотрудника = Объект.Сотрудники.Добавить();
		Элементы.Сотрудники.ТекущаяСтрока = ДанныеСотрудника.ПолучитьИдентификатор();
		ЗаполнитьСтавкиВСтрокеТаблицыСотрудники(ДанныеСотрудника);
	Иначе
		ДанныеСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеСотрудника, ДанныеСправки);
	
	УдалитьСтрокиТаблицыДоходов(ДанныеСотрудника.НомерСправки);
	УдалитьСтрокиТаблицыВычетов(ДанныеСотрудника.НомерСправки);
	
	Для каждого СведениеОДоходе Из ДанныеСправки.МассивСведенийОДоходах Цикл
		НоваяСтрокаСведенийОДоходе = Объект.СведенияОДоходах.Добавить();
		НоваяСтрокаСведенийОДоходе.Сотрудник = ДанныеСправки.Сотрудник;
		НоваяСтрокаСведенийОДоходе.НомерСправки = ДанныеСотрудника.НомерСправки;
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСведенийОДоходе, СведениеОДоходе);
	КонецЦикла;
	
	Для каждого СведениеОВычете Из ДанныеСправки.МассивСведенийОВычетах Цикл
		НоваяСтрокаСведений = Объект.СведенияОВычетах.Добавить();
		НоваяСтрокаСведений.Сотрудник = ДанныеСправки.Сотрудник;
		НоваяСтрокаСведений.НомерСправки = ДанныеСотрудника.НомерСправки;
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСведений, СведениеОВычете);
	КонецЦикла;
	
	УстановитьОтображениеНалоговПоСтавкам();
	
	Модифицированность = Истина;
	
	ИндексСтроки = Объект.Сотрудники.Индекс(ДанныеСотрудника);
	
	ПутьКДанным = "Объект.Сотрудники[" +  Формат(ИндексСтроки, "ЧН=0; ЧГ=") + "]";
	                                                                  
	СоответствиеПутиКДаннымТаблиц = Новый Соответствие;
	СоответствиеПутиКДаннымТаблиц.Вставить("Объект.СведенияОДоходах", "Объект.СведенияОДоходах");
	СоответствиеПутиКДаннымТаблиц.Вставить("Объект.СведенияОВычетах", "Объект.СведенияОВычетах");
	
	ЗарплатаКадрыОтображениеОшибокКлиентСервер.УдалитьОшибкиИзДанныхФормыПоПутиКДанным(ЭтотОбъект, ПутьКДанным + ".*"); 
	
	ЗарплатаКадрыОтображениеОшибок.ОчиститьФлагНаличияОшибокПоПутиКДанным(
		ЭтотОбъект,
		ПутьКДанным,
		ОписаниеПодчиненностиДанных(),
		Истина);
		
	ОшибкиДанныхФормы = ЗарплатаКадрыОтображениеОшибок.ПоместитьСообщенияОбОшибкахИзФормыРедактированияСтрокиТаблицыФормыВОсновнуюФорму(
							ЭтотОбъект, 
							ПутьКДанным,
							ДанныеСправки.ОшибкиДанныхСправки,
							СоответствиеПутиКДаннымТаблиц,
							ОписаниеПодчиненностиДанных());
	
	ЗарплатаКадрыОтображениеОшибок.УстановитьФлагиНаличияОшибокПоСообщениямОбОшибках(
		ЭтотОбъект,
		ОшибкиДанныхФормы,
		ОписаниеПодчиненностиДанных());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеНалоговПоСтавкам()
	ЕстьВидимыеПоляИтогов = Ложь;
	Для Каждого СтавкаНДФЛ Из Перечисления.НДФЛСтавки Цикл
		ВидимостьПолей = ИтогиПоСтавкеЗаполнены(СтавкаНДФЛ);
		
		Если ВидимостьПолей Тогда
			ЕстьВидимыеПоляИтогов = Истина;
		КонецЕсли;
		
		УстановитьВидимостьПолейИтоговПоСтавке(СтавкаНДФЛ, ВидимостьПолей);
	КонецЦикла;
	
	Если Не ЕстьВидимыеПоляИтогов Тогда
		УстановитьВидимостьПолейИтоговПоСтавке(Перечисления.НДФЛСтавки.Ставка13, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИтогиПоСтавкеЗаполнены(Ставка)
	ИтогиПоСтавкеЗаполнены = Ложь;
	
	ИменаПолейИтоговПоСтавке = ИменаПолейИтоговПоСтавке(Ставка);
	
	Для Каждого ПолеИтогов Из ИменаПолейИтоговПоСтавке Цикл
		Если Объект.Сотрудники.Итог(ПолеИтогов) <> 0 Тогда
			ИтогиПоСтавкеЗаполнены = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИтогиПоСтавкеЗаполнены;
КонецФункции

&НаСервере
Процедура УстановитьВидимостьПолейИтоговПоСтавке(Ставка, Видимость)
	ИменаЭлементов = ИменаЭлементовОтображенияИтоговПоСтавке(Ставка);
	
	Для Каждого ИмяЭлемента Из ИменаЭлементов Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ИмяЭлемента,
			"Видимость",
			Видимость);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ИменаПолейИтоговПоСтавке(Ставка)
	ИменаПолейИтоговПоСтавкам = Новый Массив;
	
	Окончание = ОкончанияИменПолейПоСтавке(Ставка);
	
	Если Не ПустаяСтрока(Окончание) Тогда
		ИменаПолейИтоговПоСтавкам.Добавить("ОбщаяСуммаДоходаПоСтавке" + Окончание);
		ИменаПолейИтоговПоСтавкам.Добавить("ОблагаемаяСуммаДоходаПоСтавке" + Окончание);
		ИменаПолейИтоговПоСтавкам.Добавить("ИсчисленоПоСтавке" + Окончание);
		ИменаПолейИтоговПоСтавкам.Добавить("УдержаноПоСтавке" + Окончание);
		ИменаПолейИтоговПоСтавкам.Добавить("ЗадолженностьПоСтавке" + Окончание);
		ИменаПолейИтоговПоСтавкам.Добавить("ИзлишнеУдержаноПоСтавке" + Окончание);
		ИменаПолейИтоговПоСтавкам.Добавить("ПеречисленоПоСтавке" + Окончание);
	КонецЕсли;
	
	Возврат ИменаПолейИтоговПоСтавкам;
КонецФункции

&НаСервере
Функция ИменаЭлементовОтображенияИтоговПоСтавке(Ставка)
	ИменаЭлементовИтогов = Новый Массив;
	
	Окончание = ОкончанияИменПолейПоСтавке(Ставка);
	
	Если Не ПустаяСтрока(Окончание) Тогда
		ИменаЭлементовИтогов = Новый Массив;
		ИменаЭлементовИтогов.Добавить("СотрудникиСтавка" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиОбщаяСуммаДоходаПоСтавке" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиОблагаемаяСуммаДоходаПоСтавке" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиИсчисленоПоСтавке" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиУдержаноПоСтавке" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиЗадолженностьПоСтавке" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиИзлишнеУдержаноПоСтавке" + Окончание);
		ИменаЭлементовИтогов.Добавить("СотрудникиПеречисленоПоСтавке" + Окончание);
	КонецЕсли;
	
	Возврат ИменаЭлементовИтогов;
КонецФункции

&НаСервере
Функция ОкончанияИменПолейПоСтавке(СтавкаНДФЛ)
	
	Окончание = "";
	Если СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка09 Тогда
		Окончание = "9";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка13 Тогда
		Окончание = "13";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка15 Тогда
		Окончание = "15";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка30 Тогда
		Окончание = "30";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка35 Тогда
		Окончание = "35";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка03 Тогда
		Окончание = "3";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка06 Тогда
		Окончание = "6";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка07 Тогда
		Окончание = "7";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка12 Тогда
		Окончание = "12";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка05 Тогда
		Окончание = "5";
	ИначеЕсли СтавкаНДФЛ = Перечисления.НДФЛСтавки.Ставка10 Тогда
		Окончание = "10";
	КонецЕсли;
	
	Возврат Окончание;
	
КонецФункции

#Область ИндикацияОшибок

&НаСервере
Функция ОписаниеЭлементовСИндикациейОшибок()
	КоллекцияОписанийЭлементов = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементовСИндикациейОшибок();
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	ОписаниеЭлемента.ИмяЭлемента = "Организация";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ОтображатьНепривязанныеОшибки = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Организация";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ЗарплатаКадрыОтображениеОшибок.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();
	ОписаниеЭлемента.ИмяЭлемента = "Сотрудники";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "Сотрудники";
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Объект.Сотрудники";
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	Возврат КоллекцияОписанийЭлементов;
КонецФункции

&НаСервере
Функция СоответствиеДанныхОбъектаДаннымФормы()
	
	ОписаниеСоответствияДанныхОбъектовДаннымФормы = ЗарплатаКадрыОтображениеОшибок.ОписаниеСвязиДанных();
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(
		ОписаниеСоответствияДанныхОбъектовДаннымФормы,  
		"НалоговыйПериод", 
		"Объект.Организация");
	
	Возврат ОписаниеСоответствияДанныхОбъектовДаннымФормы;
КонецФункции

&НаСервере
Функция ОписаниеПодчиненностиДанных() 
	ОписаниеПодчиненностиДанных = ЗарплатаКадрыОтображениеОшибок.ОписаниеПодчиненностиДанныхФормы();
	
	КлючСвязиТаблицПоСотруднику = ЗарплатаКадрыОтображениеОшибок.КлючСвязиДанныхСтрокТаблиц();
	ЗарплатаКадрыОтображениеОшибок.ДобавитьЭлементКлючаСвязиПоПолямСтрокТаблиц(КлючСвязиТаблицПоСотруднику, "Сотрудник", "Сотрудник");
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеПодчиненностиДанныхСтрокТаблиц(
		ОписаниеПодчиненностиДанных,
		"Объект.Сотрудники",
		"Объект.СведенияОДоходах",
		КлючСвязиТаблицПоСотруднику);
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеПодчиненностиДанныхСтрокТаблиц(
		ОписаниеПодчиненностиДанных,
		"Объект.Сотрудники",
		"Объект.СведенияОВычетах",
		КлючСвязиТаблицПоСотруднику);
	
	Возврат ОписаниеПодчиненностиДанных;
КонецФункции

&НаКлиенте
Процедура Подключаемый_ЭлементИндикацииОшибкиНажатие(Элемент, СтандартнаяОбработка)
	ЗарплатаКадрыОтображениеОшибокКлиент.ЭлементИндикацииОшибкиНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЭлементИндикацииПриАктивизацииЯчейки(Элемент)
	ЗарплатаКадрыОтображениеОшибокКлиент.ЭлементИндикацииПриАктивизацииЯчейки(ЭтотОбъект, Элемент);
КонецПроцедуры

СтарыеЗначенияКонтролируемыхПолей = Новый Соответствие;

#КонецОбласти

#КонецОбласти
