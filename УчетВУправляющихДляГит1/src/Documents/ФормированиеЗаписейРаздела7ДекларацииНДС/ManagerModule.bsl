#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 23, 0);
	
КонецФункции

// Подготавливает данные для заполнения документа
//
// Параметры:
//   СтруктураПараметров - Струкутра - структура входных параметров с ключами:
//     * Организация           - СправочникСсылка.Организация - организация, для которой будут получены данные для заполнения документа.
//     * Дата                  - Дата - дата, на которую будут получены данные для заполнения документа.
//   АдресХранилища - Строка - адрес временного хранилища для помещения результата выполнения.
Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	ДанныеДляЗаполнения = Новый Структура;
	НеоблагаемыеНДСОперации = НовыйНеоблагаемыеНДСОперации();
	ПодтверждающиеДокументы = НовыйПодтверждающиеДокументы();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(СтруктураПараметров.Дата), ВидГраницы.Включая));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНеоблагаемыеОперацииОстатки.Организация КАК Организация,
	|	НДСНеоблагаемыеОперацииОстатки.КодОперации КАК КодОперации,
	|	НДСНеоблагаемыеОперацииОстатки.Контрагент КАК Контрагент,
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации КАК ДокументРеализации,
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаРеализацииБезНДСОстаток) КАК СуммаРеализацииБезНДСОстаток,
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаПриобретенияБезНДСОстаток) КАК СуммаПриобретенияБезНДСОстаток,
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаНДСНеПодлежащаяВычетуОстаток) КАК СуммаНДСНеПодлежащаяВычетуОстаток,
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации.Дата
	|ПОМЕСТИТЬ НеоблагаемыеОперацииПредварительная
	|ИЗ
	|	РегистрНакопления.НДСНеоблагаемыеОперации.Остатки(&ДатаГраница, Организация = &Организация) КАК НДСНеоблагаемыеОперацииОстатки
	|ГДЕ
	|	НДСНеоблагаемыеОперацииОстатки.СуммаРеализацииБезНДСОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации,
	|	НДСНеоблагаемыеОперацииОстатки.Контрагент,
	|	НДСНеоблагаемыеОперацииОстатки.КодОперации,
	|	НДСНеоблагаемыеОперацииОстатки.Организация,
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	ДокументРеализации,
	|	КодОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеоблагаемыеОперацииПредварительная.ДокументРеализации КАК ДокументРеализации,
	|	НеоблагаемыеОперацииПредварительная.КодОперации КАК КодОперации,
	|	НеоблагаемыеОперацииПредварительная.Контрагент КАК Контрагент,
	|	НеоблагаемыеОперацииПредварительная.СуммаРеализацииБезНДСОстаток КАК СуммаРеализации,
	|	НеоблагаемыеОперацииПредварительная.СуммаПриобретенияБезНДСОстаток КАК СуммаПриобретения,
	|	НеоблагаемыеОперацииПредварительная.СуммаНДСНеПодлежащаяВычетуОстаток КАК НДС,
	|	ДокументыПоНеоблагаемымНДСОперациям.ТипПодтверждающегоДокумента КАК ТипДокумента,
	|	ДокументыПоНеоблагаемымНДСОперациям.НомерДокумента КАК НомерДокумента,
	|	ДокументыПоНеоблагаемымНДСОперациям.ДатаДокумента КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА ДокументыПоНеоблагаемымНДСОперациям.ТипПодтверждающегоДокумента = ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Договор)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПорядокДокументов,
	|	ДокументыПоНеоблагаемымНДСОперациям.КодОперации.Код КАК КодОперацииКод,
	|	ДокументыПоНеоблагаемымНДСОперациям.КодОперации.Наименование КАК КодОперацииНаименование,
	|	ВЫБОР
	|		КОГДА ДокументыПоНеоблагаемымНДСОперациям.КодОперации.ВключаетсяВРеестрПодтверждающихДокументов
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВключаетсяВРеестрСортировка,
	|	ВЫБОР
	|		КОГДА ДокументыПоНеоблагаемымНДСОперациям.КодОперации.ОперацияНеПодлежитНалогообложению
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СТ149Сортировка
	|ИЗ
	|	НеоблагаемыеОперацииПредварительная КАК НеоблагаемыеОперацииПредварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыПоНеоблагаемымНДСОперациям КАК ДокументыПоНеоблагаемымНДСОперациям
	|		ПО НеоблагаемыеОперацииПредварительная.Организация = ДокументыПоНеоблагаемымНДСОперациям.Организация
	|			И НеоблагаемыеОперацииПредварительная.Контрагент = ДокументыПоНеоблагаемымНДСОперациям.Контрагент
	|			И НеоблагаемыеОперацииПредварительная.ДокументРеализации = ДокументыПоНеоблагаемымНДСОперациям.ДокументРеализации
	|			И НеоблагаемыеОперацииПредварительная.КодОперации = ДокументыПоНеоблагаемымНДСОперациям.КодОперации
	|
	|УПОРЯДОЧИТЬ ПО
	|	СТ149Сортировка,
	|	ВключаетсяВРеестрСортировка,
	|	КодОперацииКод,
	|	КодОперацииНаименование,
	|	НеоблагаемыеОперацииПредварительная.ДокументРеализацииДата,
	|	ПорядокДокументов
	|ИТОГИ
	|	МАКСИМУМ(СуммаРеализации),
	|	МАКСИМУМ(СуммаПриобретения),
	|	МАКСИМУМ(НДС),
	|	МАКСИМУМ(ТипДокумента),
	|	МАКСИМУМ(НомерДокумента),
	|	МАКСИМУМ(ДатаДокумента)
	|ПО
	|	КодОперации,
	|	Контрагент,
	|	ДокументРеализации";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		ДанныеДляЗаполнения.Вставить("НеоблагаемыеНДСОперации",         НеоблагаемыеНДСОперации);
		ДанныеДляЗаполнения.Вставить("ПодтверждающиеДокументы",         ПодтверждающиеДокументы);
		ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	ВыборкаПоКодам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоКодам.Следующий() Цикл
		
		ВыборкаПоКонтрагентам = ВыборкаПоКодам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоКонтрагентам.Следующий() Цикл
			
			ВыборкаПоДокументам = ВыборкаПоКонтрагентам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоДокументам.Следующий() Цикл
				
				КлючСтроки = ПолучитьНовыйКлючСтроки(НеоблагаемыеНДСОперации);
				
				СтрокаНеоблагаемыеНДСОперации = НеоблагаемыеНДСОперации.Добавить();
				СтрокаНеоблагаемыеНДСОперации.КодОперации = ВыборкаПоДокументам.КодОперации;
				СтрокаНеоблагаемыеНДСОперации.ДокументРеализации = ВыборкаПоДокументам.ДокументРеализации;
				СтрокаНеоблагаемыеНДСОперации.Контрагент = ВыборкаПоДокументам.Контрагент;
				СтрокаНеоблагаемыеНДСОперации.СуммаРеализации = ВыборкаПоДокументам.СуммаРеализации;
				СтрокаНеоблагаемыеНДСОперации.СуммаПриобретения = ВыборкаПоДокументам.СуммаПриобретения;
				СтрокаНеоблагаемыеНДСОперации.НДСПрямой= ВыборкаПоДокументам.НДС;
				СтрокаНеоблагаемыеНДСОперации.КлючСтроки = КлючСтроки;
				
				ВыборкаПоСтрокам = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоСтрокам.Следующий() Цикл
					Если ЗначениеЗаполнено(ВыборкаПоСтрокам.ТипДокумента) Тогда 
						СтрокаПодтверждающиеДокументы = ПодтверждающиеДокументы.Добавить();
						СтрокаПодтверждающиеДокументы.ТипДокумента = ВыборкаПоСтрокам.ТипДокумента;
						СтрокаПодтверждающиеДокументы.НомерДокумента = ВыборкаПоСтрокам.НомерДокумента;
						СтрокаПодтверждающиеДокументы.ДатаДокумента= ВыборкаПоСтрокам.ДатаДокумента;
						СтрокаПодтверждающиеДокументы.КлючСтроки = КлючСтроки;
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Отдельно распределим включенный в стоимость НДС по кодам операций
	МассивКоэффициентов = НеоблагаемыеНДСОперации.ВыгрузитьКолонку("СуммаРеализации");
	РаспределитьНДСПоКодамРаздела7(СтруктураПараметров, НеоблагаемыеНДСОперации, МассивКоэффициентов);
	
	ДанныеДляЗаполнения.Вставить("НеоблагаемыеНДСОперации",         НеоблагаемыеНДСОперации);
	ДанныеДляЗаполнения.Вставить("ПодтверждающиеДокументы",         ПодтверждающиеДокументы);
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

Функция ПолучитьНовыйКлючСтроки(НеоблагаемыеНДСОперации) Экспорт
	
	Если НеоблагаемыеНДСОперации.Количество() = 0 Тогда
		Возврат 1;
	КонецЕсли;
	
	СписокКлючей = Новый СписокЗначений;
	СписокКлючей.ЗагрузитьЗначения(НеоблагаемыеНДСОперации.ВыгрузитьКолонку("КлючСтроки"));
	СписокКлючей.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	МаксимальныйКлюч = СписокКлючей[0].Значение;
	
	Возврат МаксимальныйКлюч + 1;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйНеоблагаемыеНДСОперации()
	
	НеоблагаемыеНДСОперации = Новый ТаблицаЗначений;
	
	НеоблагаемыеНДСОперации.Колонки.Добавить("КодОперации", Новый ОписаниеТипов("СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС"));
	НеоблагаемыеНДСОперации.Колонки.Добавить("ДокументРеализации", Документы.ТипВсеСсылки());
	НеоблагаемыеНДСОперации.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	НеоблагаемыеНДСОперации.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	НеоблагаемыеНДСОперации.Колонки.Добавить("СуммаРеализации", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	НеоблагаемыеНДСОперации.Колонки.Добавить("СуммаПриобретения", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	НеоблагаемыеНДСОперации.Колонки.Добавить("НДСПрямой", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	НеоблагаемыеНДСОперации.Колонки.Добавить("НДСРаспределенный", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	НеоблагаемыеНДСОперации.Колонки.Добавить("КлючСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Возврат НеоблагаемыеНДСОперации;
	
КонецФункции

Функция НовыйПодтверждающиеДокументы()
	
	ПодтверждающиеДокументы = Новый ТаблицаЗначений;
	
	ПодтверждающиеДокументы.Колонки.Добавить("ТипДокумента", Новый ОписаниеТипов("СправочникСсылка.ТипыДокументовПодтверждающихЛьготуНДС"));
	ПодтверждающиеДокументы.Колонки.Добавить("НомерДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ПодтверждающиеДокументы.Колонки.Добавить("ДатаДокумента", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ПодтверждающиеДокументы.Колонки.Добавить("КлючСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Возврат ПодтверждающиеДокументы;
	
КонецФункции

Процедура РаспределитьНДСПоКодамРаздела7(СтруктураПараметров, НеоблагаемыеНДСОперации, МассивКоэффициентов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(СтруктураПараметров.Дата), ВидГраницы.Включая));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаНДСНеПодлежащаяВычетуОстаток) КАК НДС,
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации
	|ИЗ
	|	РегистрНакопления.НДСНеоблагаемыеОперации.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И КодОперации = ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка)) КАК НДСНеоблагаемыеОперацииОстатки
	|ГДЕ
	|	НДСНеоблагаемыеОперацииОстатки.СуммаРеализацииБезНДСОстаток = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		НДСКРаспределению = Выборка.НДС;
		Если НеоблагаемыеНДСОперации.Количество() > 0 Тогда 
			МассивРаспределеннаяСуммаНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(НДСКРаспределению, МассивКоэффициентов);
			Для Инд = 0 По НеоблагаемыеНДСОперации.Количество() - 1 Цикл
				НеоблагаемыеНДСОперации[Инд].НДСРаспределенный = МассивРаспределеннаяСуммаНДС[Инд];
			КонецЦикла;
		КонецЕсли;
	Иначе
		Для Инд = 0 По НеоблагаемыеНДСОперации.Количество() - 1 Цикл
			НеоблагаемыеНДСОперации[Инд].НДСРаспределенный = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаПараметровПроведения

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	Результат = Запрос.Выполнить();
	ПараметрыПроведения.Вставить("Реквизиты", Результат.Выгрузить());
	Реквизиты = ПараметрыПроведения.Реквизиты[0];
	
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",                Реквизиты.Организация);
	Запрос.УстановитьПараметр("ДатаДокумента",              Реквизиты.Период);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(Реквизиты.Период), ВидГраницы.Включая));
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗаписиРаздела7(НомераТаблиц)
		+ ТекстЗапросаНеоблагаемыеНДСОперации(НомераТаблиц)
		+ ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц)

	НомераТаблиц.Вставить("ДанныеРегламентнойОперации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.ФормированиеЗаписейРаздела7ДекларацииНДС) КАК РегламентнаяОперация
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаРеквизитыДокумента()

Функция ТекстЗапросаЗаписиРаздела7(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаЗаписиРаздела7", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации,
	|	СУММА(ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.СуммаРеализации) КАК СуммаРеализации,
	|	СУММА(ВЫБОР
	|			КОГДА КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению
	|				ТОГДА ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.СуммаПриобретения
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПриобретения,
	|	СУММА(ВЫБОР
	|			КОГДА КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению
	|				ТОГДА ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.НДСПрямой + ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.НДСРаспределенный
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДС,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата КАК Период,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка КАК Регистратор,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС.НеоблагаемыеНДСОперации КАК ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
	|		ПО ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации = КодыОперацийРаздела7ДекларацииПоНДС.Ссылка
	|ГДЕ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаНеоблагаемыеНДСОперации(НомераТаблиц)
	
	НомераТаблиц.Вставить("НеоблагаемыеОперацииОстаткиПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НеоблагаемыеОперацииОстатки",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НеоблагаемыеОперацииПоДокументу", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаНеоблагаемыеОперации",     НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НДСНеоблагаемыеОперацииОстатки.Организация КАК Организация,
	|	НДСНеоблагаемыеОперацииОстатки.КодОперации КАК КодОперации,
	|	НДСНеоблагаемыеОперацииОстатки.Контрагент КАК Контрагент,
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации КАК ДокументРеализации,
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаРеализацииБезНДСОстаток) КАК СуммаРеализации,
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаПриобретенияБезНДСОстаток) КАК СуммаПриобретения,
	|	СУММА(НДСНеоблагаемыеОперацииОстатки.СуммаНДСНеПодлежащаяВычетуОстаток) КАК СуммаНДС
	|ПОМЕСТИТЬ НеоблагаемыеОперацииОстаткиПредварительная
	|ИЗ
	|	РегистрНакопления.НДСНеоблагаемыеОперации.Остатки(&ДатаГраница, Организация = &Организация) КАК НДСНеоблагаемыеОперацииОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНеоблагаемыеОперацииОстатки.Контрагент,
	|	НДСНеоблагаемыеОперацииОстатки.КодОперации,
	|	НДСНеоблагаемыеОперацииОстатки.Организация,
	|	НДСНеоблагаемыеОперацииОстатки.ДокументРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСНеоблагаемыеОперации.Организация,
	|	НДСНеоблагаемыеОперации.КодОперации,
	|	НДСНеоблагаемыеОперации.Контрагент,
	|	НДСНеоблагаемыеОперации.ДокументРеализации,
	|	НДСНеоблагаемыеОперации.СуммаРеализацииБезНДС,
	|	НДСНеоблагаемыеОперации.СуммаПриобретенияБезНДС,
	|	НДСНеоблагаемыеОперации.СуммаНДСНеПодлежащаяВычету
	|ИЗ
	|	РегистрНакопления.НДСНеоблагаемыеОперации КАК НДСНеоблагаемыеОперации
	|ГДЕ
	|	НДСНеоблагаемыеОперации.Регистратор = &Ссылка
	|	И НДСНеоблагаемыеОперации.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ДокументРеализации,
	|	КодОперации,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеоблагаемыеОперацииОстаткиПредварительная.Организация,
	|	НеоблагаемыеОперацииОстаткиПредварительная.КодОперации,
	|	НеоблагаемыеОперацииОстаткиПредварительная.Контрагент,
	|	НеоблагаемыеОперацииОстаткиПредварительная.ДокументРеализации,
	|	СУММА(НеоблагаемыеОперацииОстаткиПредварительная.СуммаРеализации) КАК СуммаРеализации,
	|	СУММА(НеоблагаемыеОперацииОстаткиПредварительная.СуммаПриобретения) КАК СуммаПриобретения,
	|	СУММА(НеоблагаемыеОперацииОстаткиПредварительная.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ НеоблагаемыеОперацииОстатки
	|ИЗ
	|	НеоблагаемыеОперацииОстаткиПредварительная КАК НеоблагаемыеОперацииОстаткиПредварительная
	|ГДЕ
	|	(НеоблагаемыеОперацииОстаткиПредварительная.СуммаРеализации > 0
	|			ИЛИ НеоблагаемыеОперацииОстаткиПредварительная.СуммаПриобретения > 0
	|			ИЛИ НеоблагаемыеОперацииОстаткиПредварительная.СуммаНДС > 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	НеоблагаемыеОперацииОстаткиПредварительная.ДокументРеализации,
	|	НеоблагаемыеОперацииОстаткиПредварительная.Организация,
	|	НеоблагаемыеОперацииОстаткиПредварительная.КодОперации,
	|	НеоблагаемыеОперацииОстаткиПредварительная.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация КАК Организация,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Контрагент КАК Контрагент,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.ДокументРеализации КАК ДокументРеализации,
	|	СУММА(ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.СуммаРеализации) КАК СуммаРеализации,
	|	СУММА(ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.СуммаПриобретения) КАК СуммаПриобретения,
	|	СУММА(ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.НДСПрямой) КАК НДС,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации КАК КодОперации,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата КАК Период,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ НеоблагаемыеОперацииПоДокументу
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС.НеоблагаемыеНДСОперации КАК ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации
	|ГДЕ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.ДокументРеализации,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Контрагент,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0,
	|	СУММА(ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.НДСРаспределенный),
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка),
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС.НеоблагаемыеНДСОперации КАК ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации
	|ГДЕ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	ДокументРеализации,
	|	КодОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеоблагаемыеОперацииОстатки.Организация,
	|	НеоблагаемыеОперацииОстатки.КодОперации,
	|	НеоблагаемыеОперацииОстатки.Контрагент,
	|	НеоблагаемыеОперацииОстатки.ДокументРеализации,
	|	СУММА(ВЫБОР
	|			КОГДА НеоблагаемыеОперацииПоДокументу.СуммаРеализации > НеоблагаемыеОперацииОстатки.СуммаРеализации
	|				ТОГДА НеоблагаемыеОперацииОстатки.СуммаРеализации
	|			ИНАЧЕ НеоблагаемыеОперацииПоДокументу.СуммаРеализации
	|		КОНЕЦ) КАК СуммаРеализацииБезНДС,
	|	СУММА(ВЫБОР
	|			КОГДА НеоблагаемыеОперацииПоДокументу.СуммаПриобретения > НеоблагаемыеОперацииОстатки.СуммаПриобретения
	|				ТОГДА НеоблагаемыеОперацииОстатки.СуммаПриобретения
	|			ИНАЧЕ НеоблагаемыеОперацииПоДокументу.СуммаПриобретения
	|		КОНЕЦ) КАК СуммаПриобретенияБезНДС,
	|	СУММА(ВЫБОР
	|			КОГДА НеоблагаемыеОперацииПоДокументу.НДС > НеоблагаемыеОперацииОстатки.СуммаНДС
	|				ТОГДА НеоблагаемыеОперацииОстатки.СуммаНДС
	|			ИНАЧЕ НеоблагаемыеОперацииПоДокументу.НДС
	|		КОНЕЦ) КАК СуммаНДСНеПодлежащаяВычету,
	|	НеоблагаемыеОперацииПоДокументу.Период,
	|	НеоблагаемыеОперацииПоДокументу.Регистратор
	|ИЗ
	|	НеоблагаемыеОперацииОстатки КАК НеоблагаемыеОперацииОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НеоблагаемыеОперацииПоДокументу КАК НеоблагаемыеОперацииПоДокументу
	|		ПО НеоблагаемыеОперацииОстатки.Организация = НеоблагаемыеОперацииПоДокументу.Организация
	|			И НеоблагаемыеОперацииОстатки.КодОперации = НеоблагаемыеОперацииПоДокументу.КодОперации
	|			И НеоблагаемыеОперацииОстатки.Контрагент = НеоблагаемыеОперацииПоДокументу.Контрагент
	|			И НеоблагаемыеОперацииОстатки.ДокументРеализации = НеоблагаемыеОперацииПоДокументу.ДокументРеализации
	|
	|СГРУППИРОВАТЬ ПО
	|	НеоблагаемыеОперацииОстатки.КодОперации,
	|	НеоблагаемыеОперацииОстатки.ДокументРеализации,
	|	НеоблагаемыеОперацииОстатки.Организация,
	|	НеоблагаемыеОперацииОстатки.Контрагент,
	|	НеоблагаемыеОперацииПоДокументу.Период,
	|	НеоблагаемыеОперацииПоДокументу.Регистратор";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#Область ПроведениеДокумента

// Формирование записей раздела 7 Декларации НДС
Процедура СформироватьДвиженияРаздела7ДекларацииНДС(ТаблицаДвижений, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаДвижений) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыНДСЗаписиРаздела7Декларации(ТаблицаДвижений);
	
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаНДСЗаписиРаздела7Декларации Цикл
		Запись = Движения.НДСЗаписиРаздела7Декларации.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;
	
	Движения.НДСЗаписиРаздела7Декларации.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыНДСЗаписиРаздела7Декларации(ТаблицаДвижений)
	
	Параметры = Новый Структура;
	
	СписокОбязательныхКолонок = ""
	+ "Период,"            // <Дата> - период движений - дата документа
	+ "Регистратор,"       // <ДокументСсылка.ФормированиеЗаписейРаздела7ДекларацииНДС> - документ-регистратор движений
	+ "Организация,"       // <СправочникСсылка.Организации> - организация
	+ "КодОперации,"       // <СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС> - код операции для раздела 7 Декларации 
	+ "СуммаРеализации,"   // <Число> 
	+ "СуммаПриобретения," // <Число>
	+ "СуммаНДС";          // <Число>
	
	Параметры.Вставить("ТаблицаНДСЗаписиРаздела7Декларации", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
			ТаблицаДвижений, СписокОбязательныхКолонок));
			
	Возврат Параметры;
	
КонецФункции

Процедура СформироватьДвиженияСписаниеНеоблагаемыхНДСОпераций(ТаблицаНеоблагаемыеОперации, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНеоблагаемыеОперации) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыСписаниеНеоблагаемыхОпераций(ТаблицаНеоблагаемыеОперации);
	
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаНеоблагаемыеОперации Цикл
		Запись = Движения.НДСНеоблагаемыеОперации.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;
	
	Движения.НДСНеоблагаемыеОперации.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыСписаниеНеоблагаемыхОпераций(ТаблицаНеоблагаемыеОперации)
	
	Параметры = Новый Структура;
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                     // <Дата> - период движений - дата документа
	+ "Регистратор,"                // <ДокументСсылка.ФормированиеЗаписейРаздела7ДекларацииНДС> - документ-регистратор движений
	+ "Организация,"                // <СправочникСсылка.Организации> - организация
	+ "КодОперации,"                // <СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС> - код операции для раздела 7 Декларации
	+ "Контрагент,"                 // <СправочникСсылка.Контрагенты>
	+ "ДокументРеализации,"         // <ДокументСсылка...>  - документ-регистратор необлагаемой НДС операции
	+ "СуммаРеализацииБезНДС,"      // <Число>
	+ "СуммаПриобретенияБезНДС,"    // <Число>
	+ "СуммаНДСНеПодлежащаяВычету"; // <Число>
	
	Параметры.Вставить("ТаблицаНеоблагаемыеОперации", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
			ТаблицаНеоблагаемыеОперации, СписокОбязательныхКолонок));
			
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецЕсли