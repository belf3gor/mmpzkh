#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 6, 0);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШапкаДок.Ссылка КАК Ссылка,
	|	ШапкаДок.Организация КАК Организация,
	|	ШапкаДок.Дата КАК Период
	|ИЗ
	|	Документ.ВыработкаМатериалов КАК ШапкаДок
	|ГДЕ
	|	ШапкаДок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабЧасть.Ссылка КАК Регистратор,
	|	ТабЧасть.Ссылка.Организация КАК Организация,
	|	ТабЧасть.Ссылка.Дата КАК Период,
	|	ТабЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабЧасть.Номенклатура,
	|	ТабЧасть.Количество
	|ИЗ
	|	Документ.ВыработкаМатериалов.Материалы КАК ТабЧасть
	|ГДЕ
	|	ТабЧасть.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабЧасть.Ссылка,
	|	ТабЧасть.НомерСтроки";
	
	Результат = Запрос.ВыполнитьПакет();
	ПараметрыПроведения.Вставить("ТабличнаяЧасть", Результат[1].Выгрузить());
	Выборка = Результат[0].Выбрать();
	
	Если НЕ Выборка.Следующий() ИЛИ НЕ УчетнаяПолитика.Существует(Выборка.Организация, Выборка.Период, Истина, Выборка.Ссылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Возврат ПараметрыПроведения;

КонецФункции

Процедура СформироватьДвиженияВыработкаМатериалов(ТабличнаяЧасть, Движения, Отказ) Экспорт

	Выработка = Движения.ВыработкаМатериалов;
	ТаблицаПоМатериалам = ПодготовитьПараметрыВыработкаМатериалов(ТабличнаяЧасть);

	Для Каждого СтрокаДокумента Из ТаблицаПоМатериалам Цикл
	
		ДвижениеВыработки = Выработка.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеВыработки, СтрокаДокумента);
	
	КонецЦикла; 

	Выработка.Записывать = Истина;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьПараметрыВыработкаМатериалов(ТабличнаяЧасть)

	СписокОбязательныхКолонок = ""
		+ "НомерСтроки,"
		+ "Регистратор,"
		+ "Организация,"
		+ "Период,"
		+ "Номенклатура,"
		+ "Количество";
	
	Возврат ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
				ТабличнаяЧасть, СписокОбязательныхКолонок);
	

КонецФункции

#КонецОбласти

#КонецЕсли