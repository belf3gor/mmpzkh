#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьПараметрыВРеквизитыФормы();
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы
		И (Модифицированность ИЛИ ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
		
	ИначеЕсли ПеренестиВДокумент Тогда
		
		Отказ = НЕ ПроверитьЗаполнение();
		
		Если Отказ Тогда
			Модифицированность = Истина;
			ПеренестиВДокумент = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ПеренестиВДокумент Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("АдресХранилищаПеречислениеЗаработнойПлаты", АдресХранилищаПеречислениеЗаработнойПлаты);
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Документы.СписаниеСРасчетногоСчета.ОбработкаПроверкиЗаполненияПеречислениеЗаработнойПлаты(
		Объект, ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	// Чтобы дважды не вызывать сервер, сразу поместим во временное хранилище 
	// таблицу ПеречислениеЗаработнойПлаты.
	Если НЕ Отказ Тогда
		АдресХранилищаПеречислениеЗаработнойПлаты = ПоместитьПеречислениеЗаработнойПлатыВоВременноеХранилище();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПеречислениеЗаработнойПлаты

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыПередУдалением(Элемент, Отказ)
	
	Отказ = ПеречислениеЗаработнойПлаты.Количество() = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ПеречислениеЗаработнойПлаты.ТекущиеДанные;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПустоеЗначениеПеречислениеЗаработнойПлатыВедомость(
			ЭтотОбъект, ТекущиеДанные.Ведомость);
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные.НомерСтроки = ПеречислениеЗаработнойПлаты.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	СписаниеСРасчетногоСчетаФормыКлиент.ПеречислениеЗаработнойПлатыПриОкончанииРедактирования(
		ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыПослеУдаления(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
	ПеренумероватьСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыВедомостьПриИзменении(Элемент)
	
	СтрокаПлатеж = Элементы.ПеречислениеЗаработнойПлаты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаПлатеж.Ведомость) Тогда
		СтрокаПлатеж.СуммаПлатежа = СписаниеСРасчетногоСчетаФормыВызовСервера.СуммаЗаработнойПлатыПоВедомости(
			СтрокаПлатеж.Ведомость, Объект.Ссылка, УчетЗарплатыИКадровВоВнешнейПрограмме);
	Иначе
		СтрокаПлатеж.СуммаПлатежа = 0;
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыВедомостьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписаниеСРасчетногоСчетаФормыКлиент.ПеречислениеЗаработнойПлатыВедомостьНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыВедомостьОчистка(Элемент, СтандартнаяОбработка)
	
	СписаниеСРасчетногоСчетаФормыКлиент.ПеречислениеЗаработнойПлатыВедомостьОчистка(
		ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеЗаработнойПлатыВедомостьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СписаниеСРасчетногоСчетаФормыКлиент.ПеречислениеЗаработнойПлатыВедомостьОбработкаВыбора(
		ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьПараметрыВРеквизитыФормы()
	
	АдресХранилищаПеречислениеЗаработнойПлаты = Параметры.ПараметрыФормы.АдресХранилищаПеречислениеЗаработнойПлаты;
	
	Если ЗначениеЗаполнено(АдресХранилищаПеречислениеЗаработнойПлаты) Тогда
		ТаблицаПеречислениеЗаработнойПлаты = ПолучитьИзВременногоХранилища(АдресХранилищаПеречислениеЗаработнойПлаты);
		ПеречислениеЗаработнойПлаты.Загрузить(ТаблицаПеречислениеЗаработнойПлаты);
	КонецЕсли;
	
	Если ПеречислениеЗаработнойПлаты.Количество() = 0 Тогда
		СтрокаПлатеж = ПеречислениеЗаработнойПлаты.Добавить();
		СтрокаПлатеж.НомерСтроки = ПеречислениеЗаработнойПлаты.Количество();
	КонецЕсли;
	
	// Создаем структуру с необходимыми реквизитами шапки документа.
	Объект = Новый Структура();
	Объект.Вставить("Дата",        '0001-01-01');
	Объект.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Объект.Вставить("Ссылка",      Параметры.Ключ);
	Объект.Вставить("ВидОперации", Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПустаяСсылка());
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры.ПараметрыФормы.Шапка);
	
	// Реквизиты формы, которые не сохраняются в базе данных.
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыФормы);
	
	// Реквизиты, которые требуются на форме для указания в свойствах элементов управления и условном оформлении.
	Организация = Объект.Организация;
	НазначениеПлатежа = Параметры.ПараметрыФормы.Шапка.НазначениеПлатежа;	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ОбновитьИтоги(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Элементы = Форма.Элементы;
	
	ИтогоСуммаПлатежа = Форма.ПеречислениеЗаработнойПлаты.Итог("СуммаПлатежа");
	
	Элементы.ПеречислениеЗаработнойПлатыСуммаПлатежа.ТекстПодвала = Формат(ИтогоСуммаПлатежа, "ЧЦ=15; ЧДЦ=2");
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ПеренумероватьСтроки(ЭтотОбъект);
	
	Элементы.НазначениеПлатежа.Видимость = ЗначениеЗаполнено(НазначениеПлатежа);
	
	// Управление внешним видом формы
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренумероватьСтроки(Форма)
	
	Для Сч = 0 По Форма.ПеречислениеЗаработнойПлаты.Количество() - 1 Цикл
		СтрокаПлатеж = Форма.ПеречислениеЗаработнойПлаты[Сч];
		СтрокаПлатеж.НомерСтроки = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьПеречислениеЗаработнойПлатыВоВременноеХранилище()
	
	ТаблицаПеречислениеЗаработнойПлаты = ПеречислениеЗаработнойПлаты.Выгрузить();
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ТаблицаПеречислениеЗаработнойПлаты, УникальныйИдентификатор);
	
	Возврат АдресХранилища;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЗАВЕРШЕНИЕ НЕМОДАЛЬНЫХ ВЫЗОВОВ

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
