
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",       Параметры.Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", Параметры.НалоговыйПериод);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка КАК Ссылка,
	|	НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплатыВБанк.ПериодРегистрации, МЕСЯЦ) КАК Месяц,
	|	ВедомостьНаВыплатуЗарплатыВБанк.СуммаПоДокументу КАК Сумма,
	|	ВедомостьНаВыплатуЗарплатыВБанк.КраткийСоставДокумента КАК Сотрудники,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ВедомостьНаВыплатуЗарплатыВБанк.Проведен
	|			ТОГДА 1
	|		КОГДА ВедомостьНаВыплатуЗарплатыВБанк.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СостояниеДокумента
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплатыВБанк.ПериодРегистрации, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВКассу.Ссылка,
	|	НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплатыВКассу.ПериодРегистрации, МЕСЯЦ),
	|	ВедомостьНаВыплатуЗарплатыВКассу.СуммаПоДокументу,
	|	ВедомостьНаВыплатуЗарплатыВКассу.КраткийСоставДокумента,
	|	ВедомостьНаВыплатуЗарплатыВКассу.Подразделение,
	|	ВЫБОР
	|		КОГДА ВедомостьНаВыплатуЗарплатыВКассу.Проведен
	|			ТОГДА 1
	|		КОГДА ВедомостьНаВыплатуЗарплатыВКассу.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВКассу КАК ВедомостьНаВыплатуЗарплатыВКассу
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВКассу.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплатыВКассу.ПериодРегистрации, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)";
	
	Список.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокВыбрать(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВыбратьИЗакрыть()
	
	ТекущийДокумент = Неопределено;
	ТекущиеДанные   = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийДокумент = ТекущиеДанные.Ссылка;
	КонецЕсли;
	Закрыть(ТекущийДокумент);
	
КонецФункции

#КонецОбласти