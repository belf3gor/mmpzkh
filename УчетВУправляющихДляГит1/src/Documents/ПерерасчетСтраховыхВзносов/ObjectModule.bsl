#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения.СведенияОДоходах, Истина);
	ОснованиеДляДоначисления = ?(РасчетныйПериод < Год(УчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС()),?(ДоначислениеВзносовДляИсправленияОшибок, Перечисления.ОснованияДляДоначисленияСтраховыхВзносов.ДляИсправленияОшибок, Перечисления.ОснованияДляДоначисленияСтраховыхВзносов.КорректировкаНеПризнаннаяОшибкой), Неопределено);
	УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, Организация, КонецМесяца(ПериодРегистрации), ДанныеДляПроведения.СтраховыеВзносы, , , , ОснованиеДляДоначисления);
	Если РасчетныйПериод < Год(УчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС()) И РегистрироватьДляКорректирующегоРасчета Тогда
		Для каждого ПрошлыйПериод Из ДанныеДляПроведения.ПрошлыеПериоды Цикл
			СтрокиПериода = ДанныеДляПроведения.СтраховыеВзносы.Скопировать(ДанныеДляПроведения.СтраховыеВзносы.НайтиСтроки(Новый Структура("Период",ПрошлыйПериод)));
			УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, Организация, ПрошлыйПериод, Ссылка, СтрокиПериода);
		КонецЦикла;
	Иначе
		УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, Организация, ПериодРегистрации, Ссылка, ДанныеДляПроведения.СтраховыеВзносы);
	КонецЕсли;
	
	ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
	ДанныеДляПроведения.СтраховыеВзносы = Движения.СтраховыеВзносыПоФизическимЛицам.Выгрузить();
	СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы";
	ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, НачалоМесяца(ПериодРегистрации), ДанныеДляПроведения, СтрокаСписокТаблиц);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ГодПередачиАдминистрированияВзносовФНС", Год(УчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС()));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ДанныеОДоходах.Ссылка.РегистрироватьДляКорректирующегоРасчета
	|				И ДанныеОДоходах.Ссылка.РасчетныйПериод < &ГодПередачиАдминистрированияВзносовФНС
	|			ТОГДА ДанныеОДоходах.ДатаПолученияДохода
	|		ИНАЧЕ ДанныеОДоходах.Ссылка.ПериодРегистрации
	|	КОНЕЦ КАК Период,
	|	ДанныеОДоходах.ВидДохода КАК ВидДохода,
	|	СУММА(ДанныеОДоходах.Сумма) КАК Сумма,
	|	СУММА(ДанныеОДоходах.Скидка) КАК Скидка,
	|	ДанныеОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта КАК ЯвляетсяДоходомФармацевта,
	|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
	|	ДанныеОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	ДанныеОДоходах.ЛьготныйТерриториальныйТариф КАК ЛьготныйТерриториальныйТариф
	|ИЗ
	|	Документ.ПерерасчетСтраховыхВзносов.СведенияОДоходах КАК ДанныеОДоходах
	|ГДЕ
	|	ДанныеОДоходах.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОДоходах.ВидДохода,
	|	ДанныеОДоходах.ФизическоеЛицо,
	|	ДанныеОДоходах.ОблагаетсяЕНВД,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта,
	|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеОДоходах.КлассУсловийТруда,
	|	ВЫБОР
	|		КОГДА ДанныеОДоходах.Ссылка.РегистрироватьДляКорректирующегоРасчета
	|				И ДанныеОДоходах.Ссылка.РасчетныйПериод < &ГодПередачиАдминистрированияВзносовФНС
	|			ТОГДА ДанныеОДоходах.ДатаПолученияДохода
	|		ИНАЧЕ ДанныеОДоходах.Ссылка.ПериодРегистрации
	|	КОНЕЦ,
	|	ДанныеОДоходах.ДатаПолученияДохода,
	|	ДанныеОДоходах.ЛьготныйТерриториальныйТариф";
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("СведенияОДоходах", Запрос.Выполнить().Выгрузить());
	ДанныеДляПроведения.Вставить("СтраховыеВзносы", УчетСтраховыхВзносов.ДанныеОВзносахИзДокумента(Ссылка,,,,Истина));
	
	Если РасчетныйПериод < Год(УчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС()) И РегистрироватьДляКорректирующегоРасчета Тогда
		ПрошлыеПериоды = Новый Массив;
		ДанныеДляПроведения.СтраховыеВзносы.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		Для каждого СтрокаТЗ Из ДанныеДляПроведения.СтраховыеВзносы Цикл
			ПрошлыйПериод = НачалоМесяца(СтрокаТЗ.ДатаПолученияДохода);
			СтрокаТЗ.Период = ПрошлыйПериод;
			Если ПрошлыеПериоды.Найти(ПрошлыйПериод) = Неопределено Тогда
				ПрошлыеПериоды.Добавить(ПрошлыйПериод)
			КонецЕсли;
		КонецЦикла;
		ДанныеДляПроведения.Вставить("ПрошлыеПериоды", ПрошлыеПериоды);
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли