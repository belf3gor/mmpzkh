
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция ПолучитьСписокРазделовУчета()

	СписокРазделов = Новый СписокЗначений;
	
	Для Каждого ЭлементМетаданных Из Метаданные.Перечисления.РазделыУчетаДляВводаОстатков.ЗначенияПеречисления Цикл
		
		Раздел = Перечисления.РазделыУчетаДляВводаОстатков[ЭлементМетаданных.Имя];
		Если Раздел = Перечисления.РазделыУчетаДляВводаОстатков.РасчетыСПерсоналомПоОплатеТрудаИДепонентам
			ИЛИ Раздел = Перечисления.РазделыУчетаДляВводаОстатков.РасчетыСПодотчетнымиЛицами Тогда
			Если НЕ УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СписокРазделов.Добавить(Раздел);
	
	КонецЦикла; 
	
	Возврат СписокРазделов;

КонецФункции

&НаКлиенте
Процедура СписокРазделовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = СписокРазделов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ОткрытьДокументВида(СтрокаТаблицы.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументВида(ВыбранныйРазделУчета)
	
	Если ТипЗнч(ЗначенияЗаполнения) <> Тип("Структура") Тогда
		ЗначенияЗаполнения = Новый Структура();
	КонецЕсли;
	
	ЗначенияЗаполнения.Вставить("РазделУчета", ВыбранныйРазделУчета);
	
	Модифицированность = Ложь;
	Закрыть();
	
	ОткрытьФорму("Документ.ВводНачальныхОстатков.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), ВладелецФормы);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ВыбратьРаздел(Команда)

	СтрокаТаблицы = Элементы.СписокРазделов.ТекущиеДанные;
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		
		ОткрытьДокументВида(СтрокаТаблицы.Значение);
		
	КонецЕсли; 

КонецПроцедуры
 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Ключ") Тогда
		ОсновнаяОрганизация = Параметры.Ключ.Организация;
	Иначе
		Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
			ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
			Если ЗначенияЗаполнения.Свойство("Организация") Тогда
				ОсновнаяОрганизация = ЗначенияЗаполнения.Организация;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОсновнаяОрганизация) Тогда
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ОткрытьПомощикВводаОстатков = Обработки.ВводНачальныхОстатков.МожноИспользоватьПростуюФормуПомощника(ОсновнаяОрганизация);
	
	Если НЕ ОткрытьПомощикВводаОстатков Тогда
		СписокРазделов.ЗагрузитьЗначения(ПолучитьСписокРазделовУчета().ВыгрузитьЗначения());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОткрытьПомощикВводаОстатков Тогда
		ОткрытьФорму("Обработка.ВводНачальныхОстатков.Форма.ФормаПростая",
			Новый Структура("Организация", ОсновнаяОрганизация),
			ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры
