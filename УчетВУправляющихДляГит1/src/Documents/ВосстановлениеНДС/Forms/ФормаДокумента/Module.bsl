&НаКлиенте
Перем ПараметрыОбработчикаОжидания;
&НаКлиенте
Перем ФормаДлительнойОперации;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ВерсияПостановления1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Объект.Дата);
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьРеквизитыИзПараметровФормы(ЭтаФорма);
		ПодготовитьФормуНаСервере();
	КонецЕсли;

	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("СостояниеРегламентнойОперации", 
			?(Объект.Проведен, ПредопределенноеЗначение("Перечисление.ВидыСостоянийРегламентныхОпераций.Выполнено"), 
							   ПредопределенноеЗначение("Перечисление.ВидыСостоянийРегламентныхОпераций.НеВыполнено")));
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеВосстановлениеНДС";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента);
		
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьВКнигеПродажОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение Тогда
		ТекстВопроса = НСтр("ru='При отражении восстановления НДС в книге продаж рекомендуется 
			|формировать записи в основном разделе книги. 
			|Установить для всех строк документа признак отражения в основном разделе?'"); 
		Оповещение = Новый ОписаниеОповещения("ВопросУстановитьДляСтрокДокументаПризнакОтраженияВОснРазделеЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да)
	Иначе
		ТекстВопроса = НСтр("ru='При отражении восстановления НДС в книге покупок рекомендуется 
			|формировать записи в дополнительных листах книги. 
			|Установить для строк документа признак отражения в дополнительном листе
			|(при восстановлении записей книги предыдущих налоговых периодов)?'"); 
		Оповещение = Новый ОписаниеОповещения("ВопросУстановитьДляСтрокПризнакОтраженияВДопЛистеЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписатьВосстановленныйНДСНаЗатратыПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетСписанияНДСПриИзменении(Элемент)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС1ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС2ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(2);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС3ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(3);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

&НаКлиенте
Процедура СоставСуммаБезНДСПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Состав.ТекущиеДанные;
	ТекСтрока.НДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(ТекСтрока.СуммаБезНДС, Ложь ,
		УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ТекСтрока.СтавкаНДС));
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСтавкаНДСПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Состав.ТекущиеДанные;
	ТекСтрока.НДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(ТекСтрока.СуммаБезНДС, Ложь ,
		УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ТекСтрока.СтавкаНДС));
	
КонецПроцедуры

&НаКлиенте
Процедура СоставЗаписьДополнительногоЛистаПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Состав.ТекущиеДанные;
	Если ТекСтрока.ЗаписьДополнительногоЛиста 
		И ЗначениеЗаполнено(ТекСтрока.СчетФактура) Тогда
		ПараметрыУстановкиДопЛиста = УстановитьПризнакДопЛистаВСтроке(ТекСтрока.СчетФактура, Объект.Дата);
		ЗаполнитьЗначенияСвойств(ТекСтрока, ПараметрыУстановкиДопЛиста);
	Иначе
		ТекСтрока.ЗаписьДополнительногоЛиста = Ложь;
		ТекСтрока.КорректируемыйПериод       = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСчетФактураОтсутствуетПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Состав.ТекущиеДанные;
	Если ТекСтрока.НетДанныхОСчетеФактуре Тогда
		ТекСтрока.СчетФактура = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставДокументОплатыПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.Состав.ТекущиеДанные.ДокументОплаты) Тогда
		НомерДатаДокументаОплаты = РеквизитыДокументаОплаты(Элементы.Состав.ТекущиеДанные.ДокументОплаты);
		ЗаполнитьЗначенияСвойств(Элементы.Состав.ТекущиеДанные, НомерДатаДокументаОплаты);
	Иначе
		Элементы.Состав.ТекущиеДанные.НомерДокументаОплаты = "";
		Элементы.Состав.ТекущиеДанные.ДатаДокументаОплаты  = '00010101';
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.Состав.Количество() > 0 Тогда
		
		Если Объект.Проведен Тогда
			ТекстВопроса = НСтр("ru = 'Перед заполнением проведение документа будет отменено, а табличная часть будет очищена. Заполнить?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'");
		КонецЕсли;
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьСуммыКВосстановлениюЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ЗаполнитьСуммыКВосстановлению();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНДСПоОстаткамЗапасов(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выбрана организация! Заполнение неозможно.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Объект.Состав.Количество() > 0 Тогда

		Если Объект.Проведен Тогда
			ТекстВопроса = НСтр("ru = 'Перед заполнением проведение документа будет отменено, а табличная часть будет очищена. Заполнить?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'");
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьНДСПоОстаткамЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
	Иначе
		ЗаполнитьПоОстаткамНаКлиенте();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПризнакиДопЛиста(Команда)
	
	УстановитьПризнакДопЛистаДляСтрокДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПризнакиДопЛиста(Команда)
	
	СнятьПризнакДопЛистаДляСтрокДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ТекущаяДатаДокумента = Объект.Дата;
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
	УправлениеФормой(ЭтаФорма);
	УстановитьСостояниеДокумента();

КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ВерсияПостановления1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Объект.Дата);
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииОрганизации(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// СоставКорректируемыйПериод
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставКорректируемыйПериод");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.ЗаписьДополнительногоЛиста", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// СоставПоставщик, СоставСчетФактура
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставПоставщик");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСчетФактура");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.НетДанныхОСчетеФактуре", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// СоставДокументОплаты
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставДокументОплаты");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.ВидЦенности", ВидСравненияКомпоновкиДанных.НеВСписке,
		Перечисления.ВидыЦенностей.МассивВидовЦенностиНалоговыйАгент());
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// СоставНомерДокументаОплаты, СоставДатаДокументаОплаты
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставНомерДокументаОплаты");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставДатаДокументаОплаты");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВерсияПостановления1137", ВидСравненияКомпоновкиДанных.Меньше, 3);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// СоставДатаОплаты
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставДатаОплаты");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВерсияПостановления1137", ВидСравненияКомпоновкиДанных.БольшеИлиРавно, 3);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// СоставНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Состав.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Состав.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// СоставСчетФактура
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСчетФактура");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.НетДанныхОСчетеФактуре", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	// СоставНетДанныхОСчетеФактуре
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставНетДанныхОСчетеФактуре");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОтразитьВКнигеПродаж", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()

	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
				ЗагрузитьПодготовленныеДанные();
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал,
					Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные()

	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;                           

	Если СтруктураДанных.Свойство("ТаблицаВосстановленияНДСПоРеализации") Тогда
		Если НЕ СтруктураДанных.ТаблицаВосстановленияНДСПоРеализации = Неопределено Тогда		
			Объект.Состав.Загрузить(СтруктураДанных.ТаблицаВосстановленияНДСПоРеализации);		
		КонецЕсли; 
	ИначеЕсли СтруктураДанных.Свойство("ТаблицаВосстановленияНДСПоОстаткам") Тогда
		Объект.Состав.Загрузить(СтруктураДанных.ТаблицаВосстановленияНДСПоОстаткам);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Функция РеквизитыДокументаОплаты(ДокументОплаты)
	
	СтруктураРеквизитовДокумента = Новый Структура("НомерДокументаОплаты, ДатаДокументаОплаты", "", '00010101');
	
	МетаданныеДокумента = ДокументОплаты.Метаданные();
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("НомерВходящегоДокумента", МетаданныеДокумента)
		И ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаВходящегоДокумента", МетаданныеДокумента) Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОплаты, "НомерВходящегоДокумента, ДатаВходящегоДокумента");
		СтруктураРеквизитовДокумента.НомерДокументаОплаты = РеквизитыДокумента.НомерВходящегоДокумента;
		СтруктураРеквизитовДокумента.ДатаДокументаОплаты = РеквизитыДокумента.ДатаВходящегоДокумента;
	Иначе
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОплаты, "Номер, Дата");
		СтруктураРеквизитовДокумента.НомерДокументаОплаты = 
			ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.Номер, Истина, Ложь);
		СтруктураРеквизитовДокумента.ДатаДокументаОплаты = РеквизитыДокумента.Дата;
	КонецЕсли;
	
	Возврат СтруктураРеквизитовДокумента;
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьПризнакДопЛистаВСтроке(Знач СчетФактура, Знач Дата)
	
	ПараметрыУстановкиДопЛиста = Новый Структура;
		
	Если ЗначениеЗаполнено(СчетФактура) Тогда 
		
		НачалоНалоговогоПериода = НачалоКвартала(Дата);
		
		КорректируемыйПериод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактура , "Дата");
		
		СтруктураПризнакДопЛиста = Новый Структура;
		Если (НачалоНалоговогоПериода > НачалоМесяца(КорректируемыйПериод)) Тогда
			ПараметрыУстановкиДопЛиста.Вставить("ЗаписьДополнительногоЛиста", Истина);
			ПараметрыУстановкиДопЛиста.Вставить("КорректируемыйПериод", КорректируемыйПериод);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыУстановкиДопЛиста;
	
КонецФункции

&НаСервере
Процедура УстановитьПризнакДопЛистаДляСтрокДокумента()
	
	НачалоНалоговогоПериода = НачалоКвартала(Объект.Дата);
	
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		Если ЗначениеЗаполнено(ТекСтрока.СчетФактура) И ОбщегоНазначения.СсылкаСуществует(ТекСтрока.СчетФактура) Тогда
			КорректируемыйПериод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекСтрока.СчетФактура , "Дата");
		Иначе
			КорректируемыйПериод = Объект.Дата;
		КонецЕсли;
		Если (НачалоНалоговогоПериода > НачалоМесяца(КорректируемыйПериод)) Тогда
			ТекСтрока.ЗаписьДополнительногоЛиста = Истина;
			ТекСтрока.КорректируемыйПериод = КорректируемыйПериод;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СнятьПризнакДопЛистаДляСтрокДокумента()
	
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		Если ТекСтрока.ЗаписьДополнительногоЛиста Тогда
			ТекСтрока.ЗаписьДополнительногоЛиста = Ложь;
			ТекСтрока.КорректируемыйПериод = Неопределено;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура СнятьПризнакСчетФактураОтсутствуетДляСтрокДокумента()
	
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		Если ТекСтрока.НетДанныхОСчетеФактуре Тогда
			ТекСтрока.НетДанныхОСчетеФактуре = Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьРеквизитыПриОтраженииВКнигеПокупок(УстановитьПризнакДопЛиста)
	
	Если УстановитьПризнакДопЛиста Тогда
		УстановитьПризнакДопЛистаДляСтрокДокумента();
	КонецЕсли;
	
	СнятьПризнакСчетФактураОтсутствуетДляСтрокДокумента();
	Объект.СписатьВосстановленныйНДСНаЗатраты = Ложь;
		
КонецПроцедуры

&НаСервере
Функция ЗаполнитьТаблицуСостав()
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	Если РаздельныйУчетНДСНаСчете19 Тогда
		Возврат Новый Структура("ЗаданиеВыполнено", Истина);	
	КонецЕсли;
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Проведен, ОтразитьВКнигеПродаж");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	
	Если ПараметрыОбъекта.Проведен Тогда 
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
	Объект.Состав.Очистить();//Перед заполнением ТЧ очищается
	ТаблицаВосстановления = Объект.Состав.Выгрузить();
	ТаблицаВосстановления.Очистить();

	ПараметрыОбъекта.Вставить("ТаблицаВосстановления", ТаблицаВосстановления);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Документы.ВосстановлениеНДС.ПодготовитьДанныеДляЗаполнения(ПараметрыОбъекта, АдресХранилища);
		Результат = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Документы.ВосстановлениеНДС.ПодготовитьДанныеДляЗаполнения",
			ПараметрыОбъекта,
			НСтр("ru = 'Заполнение документа ""Восстановление НДС""'"));

		АдресХранилища = Результат.АдресХранилища;
	КонецЕсли;

	Если Результат.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанные();
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыИзПараметровФормы(Форма)
	
	ПараметрыЗаполненияФормы = Неопределено;
	
	Если Форма.Параметры.Свойство("ПараметрыЗаполненияФормы",ПараметрыЗаполненияФормы) Тогда
	
		ЗаполнитьЗначенияСвойств(Форма.Объект,ПараметрыЗаполненияФормы);			
	
	КонецЕсли; 		

КонецПроцедуры

&НаСервере
Функция ЗаполнитьПоОстаткамНаСервере() Экспорт
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Проведен");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	
	Если ПараметрыОбъекта.Проведен Тогда 
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
			
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Документы.ВосстановлениеНДС.ПодготовитьДанныеДляЗаполненияНДСПоОстаткам(ПараметрыОбъекта, АдресХранилища);
		Результат = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Документы.ВосстановлениеНДС.ПодготовитьДанныеДляЗаполненияНДСПоОстаткам",
			ПараметрыОбъекта,
			НСтр("ru = 'Заполнение документа ""Восстановление НДС"" по остаткам ценностей'"));

		АдресХранилища = Результат.АдресХранилища;
	КонецЕсли;

	Если Результат.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанные();
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВопросЗаполнитьСуммыКВосстановлениюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьСуммыКВосстановлению();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьНДСПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоОстаткамНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросУстановитьДляСтрокПризнакОтраженияВДопЛистеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ИзменитьРеквизитыПриОтраженииВКнигеПокупок(Истина);
	Иначе
		ИзменитьРеквизитыПриОтраженииВКнигеПокупок(Ложь);
	КонецЕсли;
	Элементы.СписатьВосстановленныйНДСНаЗатраты.Доступность = Ложь;
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ВопросУстановитьДляСтрокДокументаПризнакОтраженияВОснРазделеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СнятьПризнакДопЛистаДляСтрокДокумента();
	КонецЕсли;
	Элементы.СписатьВосстановленныйНДСНаЗатраты.Доступность = Истина;
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамНаКлиенте()

	Результат = ЗаполнитьПоОстаткамНаСервере();

	Если НЕ Результат.ЗаданиеВыполнено Тогда
		
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
		
		ФормаДлительнойОперации.Заголовок = НСтр("ru = 'Заполнение документа ""Восстановление НДС""'");
		ФормаДлительнойОперации.Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = НСтр("ru = 'Выполняется заполнение документа ""Восстановление НДС"".
																								|Пожалуйста, подождите...'");
		
	КонецЕсли;


КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСуммыКВосстановлению()

	Результат = ЗаполнитьТаблицуСостав();

	Если НЕ Результат.ЗаданиеВыполнено Тогда
		
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
		
		ФормаДлительнойОперации.Заголовок = НСтр("ru = 'Заполнение документа ""Восстановление НДС""'");
		ФормаДлительнойОперации.Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = НСтр("ru = 'Выполняется заполнение документа ""Восстановление НДС"".
																								|Пожалуйста, подождите...'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ГруппаСчетСписания.Видимость      = Объект.СписатьВосстановленныйНДСНаЗатраты;
	Элементы.ГруппаСтраницы.ОтображениеСтраниц = ?(Объект.СписатьВосстановленныйНДСНаЗатраты, 
		ОтображениеСтраницФормы.ЗакладкиСверху, 
		ОтображениеСтраницФормы.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСубконто(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		ЭтотОбъект, Объект, НомерСубконто, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто(Форма)

	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"СубконтоСписанияНДС", "Подразделение", "СубконтоСписанияНДС", "ПодразделениеСписания", "СчетСписанияНДС");
		
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	
	Возврат Результат;

КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
