#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.ВосстановлениеНДС.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	УчетНДС.СформироватьДвиженияВосстановлениеНДС(
		ПараметрыПроведения.ТаблицаРеквизиты, ПараметрыПроведения.ТаблицаСостав, Движения, Отказ);
		
	УчетНДС.СформироватьДвиженияСписаниеНДС(
		ПараметрыПроведения.ТаблицаРеквизиты, ПараметрыПроведения.ТаблицаСостав, Движения, Отказ);
		
	РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.СформироватьДвиженияФактВыполненияРегламентнойОперации(
		ПараметрыПроведения.ДанныеРегламентнойОперации, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры 

Процедура ОбработкаУдаленияПроведения() Экспорт 
	
	РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.СброситьФактВыполненияОперации(Ссылка);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Состав.КорректируемыйПериод");
	МассивНепроверяемыхРеквизитов.Добавить("Состав.Поставщик");
	МассивНепроверяемыхРеквизитов.Добавить("Состав.СчетФактура");
	МассивНепроверяемыхРеквизитов.Добавить("Состав.ДокументОплаты");
	
	Если НЕ СписатьВосстановленныйНДСНаЗатраты Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетСписанияНДС");
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Состав Цикл
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста 
			И СтрокаТаблицы.КорректируемыйПериод = '00010101' Тогда 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не заполнена колонка ""Корректируемый период"" в строке %1 списка ""Состав"".'"),
				СтрокаТаблицы.НомерСтроки);
			Поле = "Состав[" + Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].КорректируемыйПериод";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если НЕ СтрокаТаблицы.НетДанныхОСчетеФактуре И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не заполнена колонка ""Счет-фактура"" в строке %1 списка ""Состав"".'"),
				СтрокаТаблицы.НомерСтроки);
			Поле = "Состав[" + Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].СчетФактура";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если НЕ СтрокаТаблицы.НетДанныхОСчетеФактуре И НЕ ЗначениеЗаполнено(СтрокаТаблицы.Поставщик) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не заполнена колонка ""Поставщик"" в строке %1 списка ""Состав"".'"),
				СтрокаТаблицы.НомерСтроки);
			Поле = "Состав[" + Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].Поставщик";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если Перечисления.ВидыЦенностей.ЭтоНалоговыйАгент(СтрокаТаблицы.ВидЦенности)
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОплаты) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не заполнена колонка ""Документ оплаты"" в строке %1 списка ""Состав"".'"),
				СтрокаТаблицы.НомерСтроки);
			Поле = "Состав[" + Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].ДокументОплаты";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры


#КонецЕсли