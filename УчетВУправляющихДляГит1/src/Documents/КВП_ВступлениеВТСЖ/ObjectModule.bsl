
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ЭкспортныеПроцедурыИФункцииДокумента

// Процедура заполняет табличную часть документа данными из регистров сведений.
Процедура ЗаполнитьТабличнуюЧасть() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет
	|ПОМЕСТИТЬ втЛицевыеСчета
	|ИЗ
	|	РегистрСведений.КВП_ЛицевыеСчета.СрезПоследних(&Период, ) КАК ЛицевыеСчета
	|ГДЕ
	|	ЛицевыеСчета.Действует
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СобственникиПомещений.Собственник КАК ЧленТСЖ,
	|	МАКСИМУМ(СведенияДляВзаиморасчетовПоЛС.Контрагент) КАК Контрагент
	|ИЗ
	|	втЛицевыеСчета КАК втЛицевыеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_СобственникиПомещений.СрезПоследних(&Период, ) КАК СобственникиПомещений
	|		ПО втЛицевыеСчета.ЛицевойСчет.Адрес = СобственникиПомещений.Помещение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС.СрезПоследних(&Период, Организация = &Организация) КАК СведенияДляВзаиморасчетовПоЛС
	|		ПО втЛицевыеСчета.ЛицевойСчет = СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет
	|
	|СГРУППИРОВАТЬ ПО
	|	СобственникиПомещений.Собственник
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЧленТСЖ";
	
	Запрос.УстановитьПараметр("Период",      Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	ТЗРезультат = Запрос.Выполнить().Выгрузить();
	
	ЧленыТСЖ.Загрузить(ТЗРезультат);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийДокумента

// Обработчик события "ОбработкаЗаполнения" документа.
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура формирования движений документа.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = УПЖКХ_ТиповыеМетодыВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	Для Каждого ТекСтрока Из ЧленыТСЖ Цикл
		
		// регистр КВП_ЧленыТСЖ
		Движение = Движения.КВП_ЧленыТСЖ.Добавить();
		Движение.Период      = Дата;
		Движение.Организация = Организация;
		Движение.ЧленТСЖ     = ТекСтрока.ЧленТСЖ;
		Движение.Участие     = Истина;
		
	КонецЦикла;
	
КонецПроцедуры  //ОбработкаПроведения()

#КонецОбласти 

#КонецЕсли
