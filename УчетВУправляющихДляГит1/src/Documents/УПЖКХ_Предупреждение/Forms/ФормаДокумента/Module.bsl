#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
// Процедура устанавливает заполняет табличную часть "Должники".
Процедура ЗаполнитьТабличнуюЧасть()
	
	НастройкиУчетнойПолитикиТСЖПрошлыйПериод = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьПараметрыУчетнойПолитикиЖКХ(
												КонецМесяца(ДобавитьМесяц(Объект.Дата, -1)), Объект.Организация);
	
	СтруктураДанныхДляЗапроса = 
		Новый Структура("Организация, МинимальнаяСуммаЗадолженности, МинимальныйСрокЗадолженности, Здание");
	СтруктураДанныхДляЗапроса.Организация                   = Объект.Организация;
	СтруктураДанныхДляЗапроса.МинимальнаяСуммаЗадолженности = Объект.МинимальнаяСуммаДолга;
	СтруктураДанныхДляЗапроса.МинимальныйСрокЗадолженности  = Объект.МинимальноеКоличествоМесяцевДолга;
	СтруктураДанныхДляЗапроса.Здание                        = Объект.Здание;
	ТаблицаДолжников = УПЖКХ_РаботаСДолжниками.ПолучитьТаблицуДолжников(Объект.Дата, , 
																		СтруктураДанныхДляЗапроса, 
																		НастройкиУчетнойПолитикиТСЖПрошлыйПериод);
	
	ТаблицаДолжников.Колонки.ЛицевойСчет.Имя = "Должник";
	ТаблицаДолжников.Колонки.ОбщаяСумма.Имя  = "Сумма";
	
	Объект.Должники.Загрузить(ТаблицаДолжников);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриЧтенииНаСервере" формы.
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УПЖКХ_ТиповыеМетодыСервер.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	УПЖКХ_ТиповыеМетодыСервер.УстановитьСостояниеДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	УПЖКХ_ТиповыеМетодыСервер.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбъектДокумента = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.УПЖКХ_Предупреждение"));
		
		УПЖКХ_ЗаполнениеДокументовСервер.ЗаполнитьШапкуДокумента(ОбъектДокумента,
																	УПЖКХ_ТиповыеМетодыКлиентСервер.ТекущийПользователь());
		
		ЗначениеВДанныеФормы(ОбъектДокумента, Объект);
		
		УПЖКХ_ТиповыеМетодыСервер.УстановитьСостояниеДокумента(ЭтотОбъект);
	Иначе
		// Это существующий документ. 
		// Проверим, что его можно менять.
		СЗК_МодульЗащитыКлиентСервер.УстановитьДоступностьФормыДляРедактирования(ЭтаФорма);
	КонецЕсли;
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПослеЗаписиНаСервере" формы.
//
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УПЖКХ_ТиповыеМетодыСервер.УстановитьСостояниеДокумента(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандныхПанелей

&НаКлиенте
// Процедура - обработчик команды "КомандаЗаполнить".
//
Процедура КомандаЗаполнить(Команда)
	
	Если Объект.Должники.Количество() > 0 Тогда
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьРезультатВопросаКомандаЗаполнить", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Возврат;
	КонецЕсли;
	
	КомандаЗаполнитьПродолжение();
	
КонецПроцедуры // КомандаЗаполнить()

&НаКлиенте
// Процедура-обработчик результата вопроса, вызванного в процедуре-обработчике команды "КомандаЗаполнить()".
Процедура ОбработатьРезультатВопросаКомандаЗаполнить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	Объект.Должники.Очистить();
	
	КомандаЗаполнитьПродолжение();

КонецПроцедуры // ОбработатьРезультатВопросаКомандаЗаполнить()

&НаКлиенте
// Процедура-продолжение процедуры-обработчика команды "КомандаЗаполнить()".
Процедура КомандаЗаполнитьПродолжение()
	
	ЗаполнитьТабличнуюЧасть();

КонецПроцедуры // КомандаЗаполнитьПродолжение()

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	
	УПЖКХ_ТиповыеМетодыСервер.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти