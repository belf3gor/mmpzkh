#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	МинимальнаяДата = УчетЕНВДКлиентСервер.ДатаНачалаПримененияВычетаНаОнлайнКассы();
	Если Дата < МинимальнаяДата Тогда
		Дата = МинимальнаяДата;
	КонецЕсли;
	
	Если УменьшаемыйНалог = Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.ЕНВД Тогда
		// Для ЕНВД документ регистрируется в конце квартала.
		Дата = КонецКвартала(Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если УменьшаемыйНалог = Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.Патент Тогда
		
		РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.РаспределитьРасходыПоПатентам(
			Организация,
			РегистрацияВНалоговомОргане);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	// Заводской номер и регистрационные данные ККМ уникальны и не копируются.
	ЗаводскойНомер       = "";
	ДатаРегистрации      = '00010101';
	РегистрационныйНомер = "";
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Проверка реквизитов шапки документа
	
	ПределРасходов = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.ПределРасходовНаОнлайнКассу();
	Если СуммаДокумента > ПределРасходов Тогда
		ШаблонТекста = НСтр("ru = 'Неверно указана сумма расходов.
			|Максимально допустимая сумма для уменьшения налога - %1 руб.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, ПределРасходов);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СуммаРасходов", , Отказ);
	КонецЕсли;
	
	МинимальнаяДатаРегистрации  = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МинимальнаяДатаРегистрацииКассы();
	МаксимальнаяДатаРегистрации = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МаксимальнаяДатаРегистрацииКассы();
	Если ДатаРегистрации < МинимальнаяДатаРегистрации ИЛИ ДатаРегистрации > МаксимальнаяДатаРегистрации Тогда
		ШаблонТекста = НСтр("ru = 'Неверно указана дата регистрации.
			|Для уменьшения налога касса должна быть зарегистрирована с %1 до %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
			Формат(МинимальнаяДатаРегистрации, "ДЛФ=DD"),
			Формат(МаксимальнаяДатаРегистрации, "ДЛФ=DD"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаРегистрации", , Отказ);
	КонецЕсли;
	
	ПроверитьПовторнуюРегистрациюРасходов(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.ПодготовитьПараметрыПроведения(Ссылка);
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.СформироватьДвиженияРегистрацияРасходовУменьшающихЕНВД(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПовторнуюРегистрациюРасходов(Отказ)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",          Организация);
	Запрос.УстановитьПараметр("ЗаводскойНомер",       ЗаводскойНомер);
	Запрос.УстановитьПараметр("РегистрационныйНомер", РегистрационныйНомер);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументРасходы.УменьшаемыйНалог КАК УменьшаемыйНалог,
	|	ДокументРасходы.РегистрацияВНалоговомОргане.Наименование КАК НаименованиеИФНС
	|ИЗ
	|	Документ.РасходыНаОнлайнКассыУменьшающиеНалоги КАК ДокументРасходы
	|ГДЕ
	|	НЕ ДокументРасходы.ПометкаУдаления
	|	И ДокументРасходы.Проведен
	|	И ДокументРасходы.Организация = &Организация
	|	И ДокументРасходы.ЗаводскойНомер = &ЗаводскойНомер
	|	И ДокументРасходы.РегистрационныйНомер = &РегистрационныйНомер
	|	И &УсловиеСсылка"
	;
	
	Если ЭтоНовый() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСсылка", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСсылка", "ДокументРасходы.Ссылка <> &ТекущийДокумент");
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ТекстУменьшаемыйНалог = ?(Выборка.УменьшаемыйНалог = Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.ЕНВД,
			НСтр("ru = 'ЕНВД'"), НСтр("ru = 'налог по патенту'"));
		
		ШаблонТекста = НСтр("ru = 'Расходы по кассе с такими заводским и регистрационным номерами уже уменьшают %1 в инспекции ""%2""'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
			ТекстУменьшаемыйНалог, Выборка.НаименованиеИФНС);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли