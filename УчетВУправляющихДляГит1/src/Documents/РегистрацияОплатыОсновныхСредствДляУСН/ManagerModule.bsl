#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 20, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА
//

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация
	|ИЗ
	|	Документ.РегистрацияОплатыОсновныхСредствДляУСН КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Если НЕ УчетнаяПолитика.Существует(Выборка.Организация, Выборка.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;

	НомераТаблиц = Новый Структура;

	Запрос.УстановитьПараметр("Период", Выборка.Период);
	Запрос.УстановитьПараметр("Организация",Выборка.Организация);

	Запрос.Текст =
		ТекстЗапросаРегистрацияОплатыОсновныхСредствДляУСНРеквизиты(НомераТаблиц)
		+ ТекстЗапросаТаблицаОплата(НомераТаблиц)
		+ ТекстЗапросаТаблицаОплатаНМА(НомераТаблиц)
		+ ТекстЗапросаТаблицаОплатаМодернизацииОС(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРегистрацияОплатыОсновныхСредствДляУСНРеквизиты(НомераТаблиц)

	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация
	|ИЗ
	|	Документ.РегистрацияОплатыОсновныхСредствДляУСН КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка"
	;

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаОплата(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.НомерСтроки КАК НомерСтроки,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.ОсновноеСредство,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.ДатаОплаты,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.СуммаОплаты,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка.Дата КАК Период,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка.Организация КАК Организация
	|ИЗ
	|	Документ.РегистрацияОплатыОсновныхСредствДляУСН.Оплата КАК РегистрацияОплатыОсновныхСредствДляУСНОплата
	|ГДЕ
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"
	;

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаОплатаНМА(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаНМА", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.НомерСтроки КАК НомерСтроки,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.НематериальныйАктив,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.ДатаОплаты,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.СуммаОплаты,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка.Дата КАК Период,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка.Организация КАК Организация
	|ИЗ
	|	Документ.РегистрацияОплатыОсновныхСредствДляУСН.ОплатаНМА КАК РегистрацияОплатыОсновныхСредствДляУСНОплата
	|ГДЕ
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"
	;

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаОплатаМодернизацииОС(НомераТаблиц)

	НомераТаблиц.Вставить("ВТТаблицаМодернизацийДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаМодернизацийДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТТаблицаМодернизированныхОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТОплатыОСДляУСНОбороты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаМодернизаций", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.НомерСтроки КАК НомерСтроки,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.ДокументМодернизации КАК ДокументМодернизации,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.ДатаОплаты,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.СуммаОплаты,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка.Дата КАК Период,
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка.Организация
	|ПОМЕСТИТЬ ВТТаблицаМодернизацийДокумента
	|ИЗ
	|	Документ.РегистрацияОплатыОсновныхСредствДляУСН.ОплатаМодернизацииОС КАК РегистрацияОплатыОсновныхСредствДляУСНОплата
	|ГДЕ
	|	РегистрацияОплатыОсновныхСредствДляУСНОплата.Ссылка = &Ссылка
	|	И РегистрацияОплатыОсновныхСредствДляУСНОплата.ДатаОплаты >= ДАТАВРЕМЯ(2007, 1, 1)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументМодернизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаМодернизацийДокумента.Ссылка,
	|	ВТТаблицаМодернизацийДокумента.НомерСтроки КАК НомерСтроки,
	|	ВТТаблицаМодернизацийДокумента.ДокументМодернизации,
	|	ВТТаблицаМодернизацийДокумента.ДатаОплаты,
	|	ВТТаблицаМодернизацийДокумента.СуммаОплаты,
	|	ВТТаблицаМодернизацийДокумента.Период,
	|	ВТТаблицаМодернизацийДокумента.Организация
	|ИЗ
	|	ВТТаблицаМодернизацийДокумента КАК ВТТаблицаМодернизацийДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МодернизацияОСОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТТаблицаОС
	|ИЗ
	|	Документ.МодернизацияОС.ОС КАК МодернизацияОСОсновныеСредства
	|ГДЕ
	|	МодернизацияОСОсновныеСредства.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТТаблицаМодернизацийДокумента.ДокументМодернизации
	|			ИЗ
	|				ВТТаблицаМодернизацийДокумента КАК ВТТаблицаМодернизацийДокумента)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.СуммаОплатыОборот КАК СуммаОплатыОборот,
	|	Рег.ДокументМодернизации КАК ДокументМодернизации,
	|	Рег.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТОплатыОСДляУСНОбороты
	|ИЗ
	|	РегистрНакопления.ОплатыОСДляУСН.Обороты(
	|			,
	|			&Период,
	|			,
	|			ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ВТТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ВТТаблицаОС КАК ВТТаблицаОС)
	|				И Организация = &Организация
	|				И ДокументМодернизации В
	|					(ВЫБРАТЬ
	|						ВТТаблицаМодернизацийДокумента.ДокументМодернизации
	|					ИЗ
	|						ВТТаблицаМодернизацийДокумента КАК ВТТаблицаМодернизацийДокумента)) КАК Рег
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументМодернизации,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СобытияОСОрганизаций.ОсновноеСредство КАК ОсновноеСредство,
	|	СобытияОСОрганизаций.Регистратор КАК ДокументМодернизации,
	|	СУММА(СобытияОСОрганизаций.СуммаЗатратНУ) КАК СуммаЗатратНУ,
	|	СУММА(СобытияОСОрганизаций.СуммаЗатратБУ) КАК СуммаЗатратБУ,
	|	СУММА(СобытияОСОрганизаций.СуммаЗатратУСН - ЕСТЬNULL(ВТОплатыОСДляУСНОбороты.СуммаОплатыОборот, 0)) КАК СуммаМодернизацииУСН
	|ИЗ
	|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОплатыОСДляУСНОбороты КАК ВТОплатыОСДляУСНОбороты
	|		ПО СобытияОСОрганизаций.Регистратор = ВТОплатыОСДляУСНОбороты.ДокументМодернизации
	|			И СобытияОСОрганизаций.ОсновноеСредство = ВТОплатыОСДляУСНОбороты.ОсновноеСредство
	|ГДЕ
	|	СобытияОСОрганизаций.Организация = &Организация
	|	И СобытияОСОрганизаций.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТТаблицаМодернизацийДокумента.ДокументМодернизации
	|			ИЗ
	|				ВТТаблицаМодернизацийДокумента)
	|
	|СГРУППИРОВАТЬ ПО
	|	СобытияОСОрганизаций.ОсновноеСредство,
	|	СобытияОСОрганизаций.Регистратор"
	;

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОВЕДЕНИЕ ДОКУМЕНТА
//

Процедура СформироватьДвиженияРегистрацияОплатыОсновныхСредствДляУСН(ПараметрыПроведения, Движения, Отказ) Экспорт

	Реквизиты = ПараметрыПроведения.Реквизиты[0];
	
	Если НЕ УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ТаблицаОС                      = ПараметрыПроведения.ТаблицаОС;
	ТаблицаНМА                     = ПараметрыПроведения.ТаблицаНМА;
	ТаблицаМодернизацийДокумента   = ПараметрыПроведения.ТаблицаМодернизацийДокумента;
	ТаблицаМодернизаций            = ПараметрыПроведения.ТаблицаМодернизаций;

	// Формирование записей об оплате ОС
	Для каждого СтрокаТаблицыОС Из ТаблицаОС Цикл
		СтрокаДвижений = Движения.ОплатыОСДляУСН.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаТаблицыОС);
	КонецЦикла;

	Движения.ОплатыОСДляУСН.Записывать = Истина;

	// Формирование записей об оплате НМА
	Для каждого СтрокаТаблицыНМА Из ТаблицаНМА Цикл
		СтрокаДвижений = Движения.ОплатыНМАДляУСН.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаТаблицыНМА);
	КонецЦикла;

	Движения.ОплатыНМАДляУСН.Записывать = Истина;

	// Формирование записей об оплате модернизации ОС
	ТаблицаОплатПоОС = Новый ТаблицаЗначений();
	ТаблицаОплатПоОС.Колонки.Добавить("ОсновноеСредство");
	ТаблицаОплатПоОС.Колонки.Добавить("ДокументМодернизации");
	ТаблицаОплатПоОС.Колонки.Добавить("ДатаОплаты");
	ТаблицаОплатПоОС.Колонки.Добавить("СуммаОплаты");
	ТаблицаОплатПоОС.Колонки.Добавить("Период");
	ТаблицаОплатПоОС.Колонки.Добавить("Организация");

	Для каждого СтрокаОплата Из ТаблицаМодернизацийДокумента Цикл

		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ДокументМодернизации", СтрокаОплата.ДокументМодернизации);

		МассивДанных = ТаблицаМодернизаций.НайтиСтроки(СтруктураОтбора);

		Если МассивДанных.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;

		ИтогСуммаМодернизацииУСН = 0;
		Для каждого ТекущаяСтрока Из МассивДанных Цикл
			ИтогСуммаМодернизацииУСН = ИтогСуммаМодернизацииУСН + ТекущаяСтрока.СуммаМодернизацииУСН;
		КонецЦикла;

		Если Окр(ИтогСуммаМодернизацииУСН,2,1) = 0 Тогда
			Продолжить;
		КонецЕсли;

		СуммаОплатыКРаспределению = Мин(СтрокаОплата.СуммаОплаты, ИтогСуммаМодернизацииУСН);
		КоэффОплаты = СуммаОплатыКРаспределению / ИтогСуммаМодернизацииУСН;

		Для каждого СтрокаОС Из МассивДанных Цикл
			СуммаОплатыОС = Мин(Окр(КоэффОплаты*СтрокаОС.СуммаМодернизацииУСН,2,1), СтрокаОС.СуммаМодернизацииУСН);
			Если СуммаОплатыОС = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицыОплатПоОС = ТаблицаОплатПоОС.Добавить();
			СтрокаТаблицыОплатПоОС.ДокументМодернизации = СтрокаОС.ДокументМодернизации;
			СтрокаТаблицыОплатПоОС.ОсновноеСредство     = СтрокаОС.ОсновноеСредство;
			СтрокаТаблицыОплатПоОС.ДатаОплаты           = СтрокаОплата.ДатаОплаты;
			СтрокаТаблицыОплатПоОС.СуммаОплаты          = СуммаОплатыОС;
            СтрокаТаблицыОплатПоОС.Период               = СтрокаОплата.Период;
			СтрокаТаблицыОплатПоОС.Организация          = СтрокаОплата.Организация;
			
			СуммаОплатыКРаспределению = СуммаОплатыКРаспределению - СуммаОплатыОС;
			СтрокаОС.СуммаМодернизацииУСН = СтрокаОС.СуммаМодернизацииУСН - СуммаОплатыОС;
		КонецЦикла;

		//Распределим погрешность округления
		Если СуммаОплатыКРаспределению > 0 Тогда
			Для каждого СтрокаОС Из МассивДанных Цикл
				Если СуммаОплатыКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;
				СуммаОплатыОС = Мин(СуммаОплатыКРаспределению, СтрокаОС.СуммаМодернизацииУСН);
				Если СуммаОплатыОС = 0 Тогда
					Продолжить;
				КонецЕсли;
				СтрокаТаблицыОплатПоОС = ТаблицаОплатПоОС.Добавить();
				СтрокаТаблицыОплатПоОС.ДокументМодернизации = СтрокаОС.ДокументМодернизации;
				СтрокаТаблицыОплатПоОС.ОсновноеСредство     = СтрокаОС.ОсновноеСредство;
				СтрокаТаблицыОплатПоОС.ДатаОплаты           = СтрокаОплата.ДатаОплаты;
				СтрокаТаблицыОплатПоОС.СуммаОплаты          = СуммаОплатыОС;
				СтрокаТаблицыОплатПоОС.Период               = СтрокаОплата.Период;
				СтрокаТаблицыОплатПоОС.Организация          = СтрокаОплата.Организация;

				СуммаОплатыКРаспределению = СуммаОплатыКРаспределению - СуммаОплатыОС;
				СтрокаОС.СуммаМодернизацииУСН = СтрокаОС.СуммаМодернизацииУСН - СуммаОплатыОС;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	ТаблицаОплатПоОС.Свернуть("ОсновноеСредство, ДокументМодернизации, ДатаОплаты, Период, Организация", "СуммаОплаты");

	Для каждого Запись Из ТаблицаОплатПоОС Цикл
		СтрокаОплаты  = Движения.ОплатыОСДляУСН.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаОплаты, Запись);
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецЕсли