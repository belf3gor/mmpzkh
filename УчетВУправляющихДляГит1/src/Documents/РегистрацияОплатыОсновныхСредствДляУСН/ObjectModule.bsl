#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ
//

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата          = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект, Ложь);
	// Далее вызов ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей() не требуется,
	// т.к. был передан параметр ВыборочноОчищатьРегистры = Ложь, и все действия уже выполнены.

	ПараметрыПроведения = Документы.РегистрацияОплатыОсновныхСредствДляУСН.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ

	Документы.РегистрацияОплатыОсновныхСредствДляУСН.СформироватьДвиженияРегистрацияОплатыОсновныхСредствДляУСН(ПараметрыПроведения, Движения, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ДатаПереходаНаУСН = УчетнаяПолитика.ДатаПереходаНаУСН(Организация, Дата);

	Если НЕ УчетнаяПолитика.ПрименяетсяУСН(Организация, Дата)
		И НЕ УчетнаяПолитика.ПлательщикНДФЛ(Организация, Дата) Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Организация %1 не применяет упрощенную систему налогообложения.'"),
			Организация);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Организация", "Объект", Отказ);
		
	КонецЕсли;

	Если (Дата >= Дата(2006,1,1)) Тогда
		Для каждого СтрокаНМА Из ОплатаНМА Цикл
			ДокРегистратор = Неопределено;
			ДатаВвода      = Неопределено;
			УчетНМА.ПолучитьДокументБухСостоянияНМА(
				СтрокаНМА.НематериальныйАктив,
				Организация,
				Перечисления.ВидыСостоянийНМА.ПринятКУчету,
				ДокРегистратор,
				ДатаВвода);
			Если НЕ ЗначениеЗаполнено(ДатаВвода) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 не принято к учету.
						|Оплаты регистрируются только по принятым к учету объектам НМА.'"),
					СтрокаНМА.НематериальныйАктив);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ОплатаНМА[" + Формат(СтрокаНМА.НомерСтроки - 1, "ЧН=; ЧГ=") + "].НематериальныйАктив",
					"Объект",
					Отказ);
				Продолжить;
			КонецЕсли;

			ВыборкаЗаписей = РегистрыСведений.ПервоначальныеСведенияНМАНалоговыйУчетУСН.ПолучитьПоследнее(
				Дата,
				Новый Структура("НематериальныйАктив", СтрокаНМА.НематериальныйАктив));
			ДатаПриобретения = ?(ВыборкаЗаписей.Количество() > 0, ВыборкаЗаписей.ДатаПриобретения, 0);

			Если (ДатаПриобретения < Дата(2006,1,1))
					И (ДатаПереходаНаУСН < ДатаПриобретения)
					И (СтрокаНМА.ДатаОплаты < Дата(2006,1,1))
					И СтрокаНМА.СуммаОплаты <> 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'По %1 оплата от %2 на сумму %3 не может быть зарегистрирована.'"),
					СтрокаНМА.НематериальныйАктив,
					СтрокаНМА.ДатаОплаты,
					Формат(СтрокаНМа.СуммаОплаты, "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '; ЧГ=3,0"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ОплатаНМА[" + Формат(СтрокаНМА.НомерСтроки - 1, "ЧН=; ЧГ=") + "].НематериальныйАктив",
					"Объект",
					Отказ);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если ОплатаНМА.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'До 01.01.2006 г. оплаты по НМА не должны заполняться.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОплатаНМА", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;

	Если (Дата < Дата(2008,1,1)) Тогда
		Если ОплатаМодернизацииОС.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'До 01.01.2008 г. оплаты по модернизации ОС не должны заполняться.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОплатаМодернизацииОС", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли