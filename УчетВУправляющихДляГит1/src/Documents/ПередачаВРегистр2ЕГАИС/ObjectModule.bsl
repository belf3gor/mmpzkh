
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		ЗаполнитьНаОснованииТТНВходящейЕГАИС(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		Документы.ПередачаВРегистр2ЕГАИС.ПодобратьСправки2(ЭтотОбъект);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ЗаполнитьТоварыКОформлению") Тогда
		
		ЗаполнитьПоВыбраннымСтрокамОстатков(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	Иначе
		
		ИнтеграцияЕГАИСПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.ПередачаВРегистр2ЕГАИС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ИнтеграцияИС.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	РегистрыНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	ИнтеграцияИСПереопределяемый.ОтразитьДвиженияСерийТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	ИнтеграцияИС.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ИнтеграцияЕГАИСПереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	ИнтеграцияИС.ОчиститьДополнительныеСвойстваДляПроведения(ЭтотОбъект.ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Дата >= '20180701' Тогда
		АкцизныеМаркиЕГАИС.ПроверитьЗаполнениеАкцизныхМарок(ЭтотОбъект, Отказ,,Новый Структура("ВидДокумента",Перечисления.ВидыДокументовЕГАИС.ПередачаВРегистр2));
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Идентификатор) Тогда
		Идентификатор = ИнтеграцияЕГАИС.НовыйИдентификаторДокумента();
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияЕГАИС.СопоставитьАлкогольнуюПродукциюСНоменклатурой(ЭтотОбъект);
	
	ИнтеграцияЕГАИСПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформленияПередачаВРегистр2ЕГАИС(ЭтотОбъект, Отказ);
	
	ИнтеграцияЕГАИС.ЗаписатьСтатусДокументаЕГАИСПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование       = Неопределено;
	Идентификатор           = "";
	ИдентификаторЕГАИС      = "";
	ДатаРегистрацииДвижений = '00010101';
	
	Если Товары.Количество() > 0 Тогда
		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "Справка2");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьНаОснованииТТНВходящейЕГАИС(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК ДокументОснование,
	|	НЕ Таблица.Проведен КАК ЕстьОшибкиПроведен,
	|	Таблица.Грузополучатель КАК ОрганизацияЕГАИС,
	|	Таблица.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура         КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика       КАК Характеристика,
	|	ТабличнаяЧасть.Серия                КАК Серия,
	|	&ПустаяУпаковка                     КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Ссылка.ЕстьРасхождения
	|			ТОГДА ТабличнаяЧасть.КоличествоФакт
	|		ИНАЧЕ ТабличнаяЧасть.Количество
	|	КОНЕЦ /
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА 1
	|		КОГДА &ПолеКоэффициентПересчетаНеупакованнойПродукции = 0
	|			ТОГДА 1
	|		ИНАЧЕ &ПолеКоэффициентПересчетаНеупакованнойПродукции
	|	КОНЕЦ                               КАК Количество,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Ссылка.ЕстьРасхождения
	|			ТОГДА ТабличнаяЧасть.КоличествоФакт
	|		ИНАЧЕ ТабличнаяЧасть.Количество
	|	КОНЕЦ /
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА 1
	|		КОГДА &ПолеКоэффициентПересчетаНеупакованнойПродукции = 0
	|			ТОГДА 1
	|		ИНАЧЕ &ПолеКоэффициентПересчетаНеупакованнойПродукции
	|	КОНЕЦ                               КАК КоличествоУпаковок,
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2             КАК Справка2,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			И &ПолеКоэффициентПересчетаНеупакованнойПродукции = 0
	|				ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                               КАК ЕстьОшибкиНеупакованнаяПродукция
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И ТабличнаяЧасть.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|	И ТабличнаяЧасть.Справка2.Поштучная = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	ОформленныеТовары.Упаковка,
	|	-ОформленныеТовары.Количество,
	|	-ОформленныеТовары.КоличествоУпаковок,
	|	ОформленныеТовары.АлкогольнаяПродукция,
	|	ОформленныеТовары.Справка2,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия КАК Серия,
	|	ТоварыКОформлению.Упаковка КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2 КАК Справка2,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	МАКСИМУМ (ТоварыКОформлению.ЕстьОшибкиНеупакованнаяПродукция) КАК ЕстьОшибкиНеупакованнаяПродукция
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|	ИЛИ МАКСИМУМ (ТоварыКОформлению.ЕстьОшибкиНеупакованнаяПродукция) = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизныеМарки.Справка2 КАК Справка2,
	|	КОЛИЧЕСТВО(АкцизныеМарки.АкцизнаяМарка) КАК Количество
	|ПОМЕСТИТЬ АкцизныеМарки
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМарки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОстатки КАК втОстатки
	|		ПО (втОстатки.Справка2 = АкцизныеМарки.Справка2)
	|ГДЕ
	|	АкцизныеМарки.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ВНаличии), ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.КПостановкеНаБаланс))
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМарки.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия КАК Серия,
	|	ТоварыКОформлению.Упаковка КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2 КАК Справка2,
	|	ТоварыКОформлению.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК Количество,
	|	ТоварыКОформлению.КоличествоУпаковок - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК КоличествоУпаковок,
	|	ТоварыКОформлению.ЕстьОшибкиНеупакованнаяПродукция КАК ЕстьОшибкиНеупакованнаяПродукция
	|ИЗ
	|	ВтОстатки КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМарки КАК АкцизныеМарки
	|		ПО ТоварыКОформлению.Справка2 = АкцизныеМарки.Справка2
	|ГДЕ
	|	ТоварыКОформлению.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0) > 0
	|	ИЛИ ТоварыКОформлению.ЕстьОшибкиНеупакованнаяПродукция = ИСТИНА";
	
	ИнтеграцияЕГАИСПереопределяемый.ОпределитьКоэффициентПересчетаНеупакованнойПродукции(
		ТекстЗапроса, 
		"ТабличнаяЧасть.Номенклатура",
		"&ПолеКоэффициентПересчетаНеупакованнойПродукции");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ЭтаСсылка",         Ссылка);
	Запрос.УстановитьПараметр("ПолеКоэффициентПересчетаНеупакованнойПродукции", 1);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("ПустаяУпаковка",    ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("Упаковка"));
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ПередачаВРегистр2ЕГАИС.КонечныеСтатусы());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		
		ИнтеграцияИСПереопределяемый.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыДокумента);
		
	КонецЕсли;
	
	Товары.Очистить();
	ИнтеграцияЕГАИС.ЗаполнитьТабличнуюЧастьТовары(ЭтотОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПоВыбраннымСтрокамОстатков(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ОрганизацияЕГАИС = ДанныеЗаполнения.ОрганизацияЕГАИС;
	
	ТаблицаТовары = Новый ТаблицаЗначений;
	ТаблицаТовары.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаТовары.Колонки.Добавить("Справка2",             Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ТаблицаТовары.Колонки.Добавить("Количество",           Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТЧ Из ДанныеЗаполнения.Товары Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаТовары.Добавить(), СтрокаТЧ);
	КонецЦикла;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Справка2 КАК Справка2,
	|	Т.Количество КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Т.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция,
	|	Т.Справка2 КАК Справка2,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	втТовары КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.АлкогольнаяПродукция,
	|	Т.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2 КАК Справка2,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Характеристика) КАК Характеристика
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО (СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ТабличнаяЧасть.АлкогольнаяПродукция)
	|			И (СоответствиеНоменклатурыЕГАИС.Справка2 = ТабличнаяЧасть.Справка2)
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Номенклатура) = 1 И
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Характеристика) < 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СопоставленыеПозиции.Номенклатура КАК Номенклатура,
	|	СопоставленыеПозиции.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2 КАК Справка2,
	|	ТабличнаяЧасть.Количество / ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА 1
	|		КОГДА &ПолеКоэффициентПересчетаНеупакованнойПродукции = 0
	|			ТОГДА 1
	|		ИНАЧЕ &ПолеКоэффициентПересчетаНеупакованнойПродукции
	|	КОНЕЦ КАК Количество,
	|	ТабличнаяЧасть.Количество / ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА 1
	|		КОГДА &ПолеКоэффициентПересчетаНеупакованнойПродукции = 0
	|			ТОГДА 1
	|		ИНАЧЕ &ПолеКоэффициентПересчетаНеупакованнойПродукции
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО ТабличнаяЧасть.АлкогольнаяПродукция = СопоставленыеПозиции.АлкогольнаяПродукция
	|			И ТабличнаяЧасть.Справка2 = СопоставленыеПозиции.Справка2
	|			И (НЕ СопоставленыеПозиции.АлкогольнаяПродукция ЕСТЬ NULL)";
	
	ИнтеграцияЕГАИСПереопределяемый.ОпределитьКоэффициентПересчетаНеупакованнойПродукции(
		ТекстЗапроса, 
		"СопоставленыеПозиции.Номенклатура",
		"&ПолеКоэффициентПересчетаНеупакованнойПродукции");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПолеКоэффициентПересчетаНеупакованнойПродукции", 1);
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Товары.Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли