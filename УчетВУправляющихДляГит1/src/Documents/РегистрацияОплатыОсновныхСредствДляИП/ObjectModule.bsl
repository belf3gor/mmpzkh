#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата			= НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный	= Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Перем ДокРегистратор, ДатаВвода;
	
	Если НЕ УчетнаяПолитика.ПлательщикНДФЛ(Организация, Дата) Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Организация %1 не является предпринимателем на общей системе налогообложения.'"),
			Организация);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Организация", "Объект", Отказ);
		
	КонецЕсли;

	Если (Дата >= Дата(2006,1,1)) Тогда
		
		Для каждого СтрокаНМА Из ОплатаНМА Цикл
			
			УчетНМА.ПолучитьДокументБухСостоянияНМА(
				СтрокаНМА.НематериальныйАктив,
				Организация,
				Перечисления.ВидыСостоянийНМА.ПринятКУчету,
				ДокРегистратор,
				ДатаВвода);
				
			Если НЕ ЗначениеЗаполнено(ДатаВвода) Тогда
					
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 не принято к учету.
						|Оплаты регистрируются только по принятым к учету объектам НМА.'"),
					СтрокаНМА.НематериальныйАктив);
					
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ОплатаНМА[" + Формат(СтрокаНМА.НомерСтроки - 1, "ЧН=; ЧГ=") + "].НематериальныйАктив",
					"Объект",
					Отказ);
					
				Продолжить;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если ОплатаНМА.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'До 01.01.2006 г. оплаты по НМА не должны заполняться.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОплатаНМА", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.РегистрацияОплатыОсновныхСредствДляИП.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистрацияОплатыОСиНМАИП	= УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуРегистрацияОплатыОСиНМА(
		ПараметрыПроведения.ТаблицаОС,
		ПараметрыПроведения.ТаблицаНМА,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияРегистрацияОплатыОСиНМА(
		ТаблицаРегистрацияОплатыОСиНМАИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.РегистрацияСведенийОбОплатеОСиНМА(
		ТаблицаРегистрацияОплатыОСиНМАИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры


#КонецЕсли