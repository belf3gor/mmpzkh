#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 8, 0);
	
КонецФункции

// Заполняет счета учета номенклатуры в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт

	ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СоответствиеСчетовУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаСпискаНоменклатуры(
		ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, "Номенклатура", Истина), ДанныеОбъекта.Склад, ДанныеОбъекта.Дата);
	
	Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
		СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти.Номенклатура);
		ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СчетаУчета);
	КонецЦикла;

КонецПроцедуры

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СведенияОНоменклатуре) Экспорт
	
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОНоменклатуре.Свойство("СчетаУчета") Тогда
		// СведенияОНоменклатуре - структура сведений номенклатуры
		СчетаУчета = СведенияОНоменклатуре.СчетаУчета;
	ИначеЕсли СведенияОНоменклатуре.Свойство("СчетУчета") Тогда
		// СведенияОНоменклатуре - структура счетов учета номенклатуры
		СчетаУчета = СведенияОНоменклатуре;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ИмяТабличнойЧасти = "Товары" Тогда
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетУчета) Тогда
			СтрокаТабличнойЧасти.СчетУчета   = СчетаУчета.СчетУчета;
			СтрокаТабличнойЧасти.СчетУчетаНУ = СчетаУчета.СчетУчета;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетНДСТаможни) Тогда
			СтрокаТабличнойЧасти.СчетУчетаНДС = СчетаУчета.СчетНДСТаможни;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетаУчета.СпособУчетаНДС) Тогда
			СтрокаТабличнойЧасти.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает порядок учета НДС в значения по умолчанию
//
// Параметры:
//  Объект - документ, у которого необходимо установить порядок учета НДС
//
Процедура УстановитьПорядокУчетаНДСПоУмолчанию(Объект) Экспорт
	
	УпрощенныйУчетНДС = УчетнаяПолитика.УпрощенныйУчетНДС(Объект.Организация, Объект.Дата);
	РаздельныйУчетНДС = УчетнаяПолитика.РаздельныйУчетНДС(Объект.Организация, Объект.Дата);
	
	ВключитьНДСВСтоимость = НЕ УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата);
	
	ОтразитьВычетНДСПриРегистрацииСчетаФактуры = Объект.Дата > '20120101'
		И НЕ РаздельныйУчетНДС И НЕ ВключитьНДСВСтоимость;
		
	Объект.НДСВключенВСтоимость = ВключитьНДСВСтоимость;
	Объект.НДСПредъявленКВычету = УпрощенныйУчетНДС ИЛИ ОтразитьВычетНДСПриРегистрацииСчетаФактуры;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

Функция ТоварыПоДаннымПоступления(ДанныеОбъекта, ДокументПоступления, НомерРаздела) Экспорт
	ТаблицаРезультата  = Документы.ГТДИмпорт.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	
	РеквизитыПоступления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПоступления, "ВидОперации, Склад");
	
	РазрешенныеВидыОпераций = Новый Массив;
	РазрешенныеВидыОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия);
	РазрешенныеВидыОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары);
	РазрешенныеВидыОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование);
	
	Если РазрешенныеВидыОпераций.Найти(РеквизитыПоступления.ВидОперации) = Неопределено Тогда
		Возврат ТаблицаРезультата;
	КонецЕсли;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	
	СхемаЗапроса.УстановитьТекстЗапроса(
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Количество,
	|	ПоступлениеТоваровУслугТовары.Сумма,
	|	0 КАК ФактурнаяСтоимость,
	|	0 КАК СуммаПошлины,
	|	0 КАК СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.СчетУчета,
	|	ПоступлениеТоваровУслугТовары.СчетУчета КАК СчетУчетаНУ,
	|	ПоступлениеТоваровУслугТовары.СчетУчетаНДС,
	|	ПоступлениеТоваровУслугТовары.СпособУчетаНДС,
	|	ПоступлениеТоваровУслугТовары.ОтражениеВУСН,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения,
	|	&НомерРаздела КАК НомерРаздела,
	|	&ДокументПоступления КАК ДокументПартии
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &ДокументПоступления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ПоступлениеТоваровУслугОборудование.Сумма,
	|	0,
	|	0,
	|	0,
	|	ПоступлениеТоваровУслугОборудование.СчетУчета,
	|	ПоступлениеТоваровУслугОборудование.СчетУчета,
	|	ПоступлениеТоваровУслугОборудование.СчетУчетаНДС,
	|	ПоступлениеТоваровУслугОборудование.СпособУчетаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются),
	|	ПоступлениеТоваровУслугОборудование.СтранаПроисхождения,
	|	&НомерРаздела,
	|	&ДокументПоступления
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|ГДЕ
	|	ПоступлениеТоваровУслугОборудование.Ссылка = &ДокументПоступления");
	
	Если РеквизитыПоступления.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование  Тогда
		СхемаЗапроса.ПакетЗапросов[0].Операторы.Удалить(1);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументПоступления", ДокументПоступления);
	Запрос.УстановитьПараметр("НомерРаздела", НомерРаздела);
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	ТаблицаЗначенийТовары = Запрос.Выполнить().Выгрузить();
	
	СоответствиеСчетовУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаСпискаНоменклатуры(
		ДанныеОбъекта.Организация,
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗначенийТовары, "Номенклатура", Истина),
		РеквизитыПоступления.Склад,
		ДанныеОбъекта.Дата);
		
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	КурсДокумента      = ЗаполнениеДокументов.КурсДокумента(ДокументПоступления,      ВалютаРеглУчета);
	КратностьДокумента = ЗаполнениеДокументов.КратностьДокумента(ДокументПоступления, ВалютаРеглУчета);
	
	Для каждого СтрокаТоваров Из ТаблицаЗначенийТовары Цикл
		СтрокаРезультата = ТаблицаРезультата.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаТоваров);
		
		СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаРезультата.Номенклатура);
		Если ЗначениеЗаполнено(СтрокаРезультата.СчетУчета) Тогда
			СтрокаРезультата.СчетУчетаНДС = СчетаУчета.СчетНДСТаможни;
		Иначе
			ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(
				ДанныеОбъекта, СтрокаРезультата, "Товары", СчетаУчета);
		КонецЕсли;
		
		СтрокаРезультата.ФактурнаяСтоимость = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТоваров.Сумма,
			ДокументПоступления.ВалютаДокумента, ДанныеОбъекта.ВалютаДокумента,
			КурсДокумента, ДанныеОбъекта.КурсДокумента,
			КратностьДокумента,ДанныеОбъекта.КратностьДокумента);
		
	КонецЦикла;
	
	Возврат ТаблицаРезультата;
КонецФункции

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ, ДоговорДляОтложенногоПроведения = Неопределено) Экспорт

	ПараметрыПроведения = Новый Структура;
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ЭтоОтложенноеПроведение = ЗначениеЗаполнено(ДоговорДляОтложенногоПроведения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	
	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
	
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, Реквизиты.Ссылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Реквизиты.Вставить("ЭтоОтложенноеПроведение", ЭтоОтложенноеПроведение);
	
	// Коэффициенты пересчета сумм
	// - из валюты документа в валюту взаиморасчетов 
	// - из валюты взаиморасчетов в рубли
	Если Реквизиты.ВалютаВзаиморасчетов = ВалютаРеглУчета Тогда
		КоэффициентРуб = 1;
	Иначе
		КоэффициентРуб = Реквизиты.КурсВзаиморасчетов / Реквизиты.КратностьВзаиморасчетов;
	КонецЕсли;
	Если Реквизиты.ВалютаВзаиморасчетов = Реквизиты.ВалютаДокумента Тогда
		КоэффициентВзаиморасчетов = 1;
	Иначе
		КоэффициентВзаиморасчетов = (Реквизиты.КурсВзаиморасчетов / Реквизиты.КратностьВзаиморасчетов) / КоэффициентРуб;
	КонецЕсли;
	
	Если Реквизиты.ВалютаДокумента = ВалютаРеглУчета Тогда
		КоэффициентТаможеннаяСтоимостьРуб = 1;
	Иначе
		КоэффициентТаможеннаяСтоимостьРуб = Реквизиты.КурсДокумента / Реквизиты.КратностьДокумента;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КоэффициентВзаиморасчетов", КоэффициентВзаиморасчетов);
	Запрос.УстановитьПараметр("КоэффициентРуб",            КоэффициентРуб);
	Запрос.УстановитьПараметр("КоэффициентТаможеннаяСтоимостьРуб", КоэффициентТаможеннаяСтоимостьРуб);
	
	Запрос.УстановитьПараметр("СинонимТовары", НСтр("ru = 'Товары'"));
	
	Запрос.УстановитьПараметр("ПрименяетсяУСН",                   УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходыМинусРасходы", УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПлательщикНДФЛ",                   УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ВестиУчетПоВидамДеятельностиИП",   УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",     УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ДатаДокумента", 					  Реквизиты.Период);
	
	// счета учета
	УсловияОтбораСубсчетов = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбораСубсчетов.Забалансовый = Ложь;
	
	СчетаМПЗ = Новый Массив();
	СчетаМПЗ.Добавить(ПланыСчетов.Хозрасчетный.Материалы); // 10 счет
	Субсчета10 = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаМПЗ, УсловияОтбораСубсчетов);
	
	СчетаМПЗ.Добавить(ПланыСчетов.Хозрасчетный.Товары);   // дополняем счетом 41
	СубсчетаМПЗ = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаМПЗ, УсловияОтбораСубсчетов);
	
	Запрос.УстановитьПараметр("Субсчета10",  Субсчета10);
	Запрос.УстановитьПараметр("СубсчетаМПЗ", СубсчетаМПЗ);
	
	// Субконто для определения стоимости
	СубконтоНоменклатура = Новый Массив();
	СубконтоНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Запрос.УстановитьПараметр("СубконтоНоменклатура", СубконтоНоменклатура);
	
	ВерсияПостановленияНДС1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Реквизиты.Период);
	Запрос.УстановитьПараметр("ОтражатьФактурнуюСтоимость", ВерсияПостановленияНДС1137 >= 4);
	
	ДатаПринятияДекларацииНаТовары = Справочники.НомераГТД.ДатаПринятияДекларацииНаТовары(
		Реквизиты.НомерТаможеннойДекларации);
	
	Если ЗначениеЗаполнено(ДатаПринятияДекларацииНаТовары) Тогда
		ДатаСчетаФактуры = ДатаПринятияДекларацииНаТовары;
	Иначе
		ДатаСчетаФактуры = Реквизиты.Период;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаСчетаФактуры", ДатаСчетаФактуры);
	
	// Содержания проводок:
	Запрос.УстановитьПараметр("СодержаниеПошлина",          НСтр("ru = 'Таможенная пошлина'"));
	Запрос.УстановитьПараметр("СодержаниеПошлинаВал",       НСтр("ru = 'Таможенная пошлина (в валюте)'"));
	Запрос.УстановитьПараметр("СодержаниеШтраф",            НСтр("ru = 'Таможенный штраф'"));
	Запрос.УстановитьПараметр("СодержаниеШтрафВал",         НСтр("ru = 'Таможенный штраф (в валюте)'"));
	Запрос.УстановитьПараметр("СодержаниеСборы",            НСтр("ru = 'Таможенный сбор'"));
	Запрос.УстановитьПараметр("СодержаниеСборыВал",         НСтр("ru = 'Таможенный сбор (в валюте)'"));
	Запрос.УстановитьПараметр("СодержаниеНДС",              НСтр("ru = 'НДС, уплаченный таможенным органам'"));
	Запрос.УстановитьПараметр("СодержаниеНДСВал",           НСтр("ru = 'НДС, уплаченный таможенным органам (в валюте)'"));
	Запрос.УстановитьПараметр("СодержаниеНДСВСтоимости",    НСтр("ru = 'НДС, уплаченный таможенным органам, включен в стоимость'"));
	Запрос.УстановитьПараметр("СодержаниеНДСВСтоимостиВал", НСтр("ru = 'НДС, уплаченный таможенным органам (в валюте), включен в стоимость'"));
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицаПошлина(НомераТаблиц)
		+ ТекстЗапросаТаможенныйСбор(НомераТаблиц)
		+ ТекстЗапросаШтраф(НомераТаблиц)
		+ ТекстЗапросаНДС(НомераТаблиц)
		+ ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПереоценкаВалютныхОстатков(НомераТаблиц)
		+ ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц)
		+ ТекстЗапросаПоступлениеМПЗИП(НомераТаблиц)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента()

	// В РеквизитыДоговоров и РеквизитыАвансов - всегда две записи: для рублевого и валютного договоров.
	// В Реквизиты и СуммыПошлины - по одной записи.
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГТДИмпорт.Ссылка КАК Ссылка,
	|	ГТДИмпорт.Номер КАК Номер,
	|	ГТДИмпорт.Дата КАК Период,
	|	ГТДИмпорт.Организация КАК Организация,
	|	ГТДИмпорт.ВалютаДокумента КАК ВалютаДокумента,
	|	ГТДИмпорт.КурсДокумента КАК КурсДокумента,
	|	ГТДИмпорт.КратностьДокумента КАК КратностьДокумента,
	|	ЕСТЬNULL(ГТДИмпорт.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРеглУчета) КАК ВалютаВзаиморасчетов,
	|	ГТДИмпорт.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ГТДИмпорт.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ГТДИмпорт.Контрагент КАК Контрагент,
	|	ГТДИмпорт.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ГТДИмпорт.ДоговорКонтрагентаРегл КАК ДоговорКонтрагентаРегл,
	|	ГТДИмпорт.ТаможенныйСбор КАК ТаможенныйСбор,
	|	ГТДИмпорт.ТаможенныйСборВал КАК ТаможенныйСборВал,
	|	ГТДИмпорт.ТаможенныйШтраф КАК ТаможенныйШтраф,
	|	ГТДИмпорт.ТаможенныйШтрафВал КАК ТаможенныйШтрафВал,
	|	ГТДИмпорт.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ГТДИмпорт.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	ГТДИмпорт.СтатьяПрочихДоходовРасходов КАК СтатьяПрочихДоходовРасходов,
	|	ЕСТЬNULL(ГТДИмпорт.СтатьяПрочихДоходовРасходов.ПринятиеКналоговомуУчету, ИСТИНА) КАК ЗатратыПринимаютсяКНУ,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетУчетаРасходов,
	|	ГТДИмпорт.СчетУчетаРасчетовСКонтрагентомВал КАК СчетУчетаРасчетовСКонтрагентомВал,
	|	ГТДИмпорт.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ГТДИмпорт.НДСПредъявленКВычету КАК НДСПредъявленКВычету,
	|	ГТДИмпорт.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	ГТДИмпорт.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ГТДИмпорт КАК ГТДИмпорт
	|ГДЕ
	|	ГТДИмпорт.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛОЖЬ КАК Валютный,
	|	&ВалютаРеглУчета КАК ВалютаДокумента,
	|	1 КАК КурсДокумента,
	|	1 КАК КратностьДокумента,
	|	&ВалютаРеглУчета КАК ВалютаВзаиморасчетов,
	|	1 КАК КурсВзаиморасчетов,
	|	1 КАК КратностьВзаиморасчетов,
	|	ГТДИмпорт.ДоговорКонтрагентаРегл КАК ДоговорКонтрагента,
	|	ГТДИмпорт.ДоговорКонтрагентаРегл.ВидДоговора КАК ВидДоговора,
	|	ГТДИмпорт.ДоговорКонтрагентаРегл.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ГТДИмпорт.ТаможенныйСбор КАК ТаможенныйСбор,
	|	ГТДИмпорт.ТаможенныйШтраф КАК ТаможенныйШтраф,
	|	ГТДИмпорт.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ГТДИмпорт.СтатьяПрочихДоходовРасходов КАК СтатьяПрочихДоходовРасходов,
	|	ГТДИмпорт.ТаможенныйСбор + ГТДИмпорт.ТаможенныйШтраф КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ РеквизитыДоговоров
	|ИЗ
	|	Документ.ГТДИмпорт КАК ГТДИмпорт
	|ГДЕ
	|	ГТДИмпорт.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ГТДИмпорт.ВалютаДокумента,
	|	ГТДИмпорт.КурсДокумента,
	|	ГТДИмпорт.КратностьДокумента,
	|	ЕСТЬNULL(ГТДИмпорт.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРеглУчета),
	|	ГТДИмпорт.КурсВзаиморасчетов,
	|	ГТДИмпорт.КратностьВзаиморасчетов,
	|	ГТДИмпорт.ДоговорКонтрагента,
	|	ГТДИмпорт.ДоговорКонтрагента.ВидДоговора,
	|	ГТДИмпорт.ДоговорКонтрагента.УчетАгентскогоНДС,
	|	ГТДИмпорт.ТаможенныйСборВал,
	|	ГТДИмпорт.ТаможенныйШтрафВал,
	|	ГТДИмпорт.СчетУчетаРасчетовСКонтрагентомВал,
	|	ГТДИмпорт.СтатьяПрочихДоходовРасходов,
	|	ГТДИмпорт.ТаможенныйСборВал + ГТДИмпорт.ТаможенныйШтрафВал
	|ИЗ
	|	Документ.ГТДИмпорт КАК ГТДИмпорт
	|ГДЕ
	|	ГТДИмпорт.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ГТДИмпортРазделы.ПошлинаВВалюте
	|				ТОГДА 0
	|			ИНАЧЕ ГТДИмпортРазделы.СуммаПошлины
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ГТДИмпортРазделы.НДСВВалюте
	|				ТОГДА 0
	|			ИНАЧЕ ГТДИмпортРазделы.СуммаНДС
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ГТДИмпортРазделы.ПошлинаВВалюте
	|				ТОГДА ГТДИмпортРазделы.СуммаПошлины
	|			ИНАЧЕ 0
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ГТДИмпортРазделы.НДСВВалюте
	|				ТОГДА ГТДИмпортРазделы.СуммаНДС
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВал
	|ПОМЕСТИТЬ СуммыПошлины
	|ИЗ
	|	Документ.ГТДИмпорт.Разделы КАК ГТДИмпортРазделы
	|ГДЕ
	|	ГТДИмпортРазделы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГТДИмпорт.Ссылка КАК Ссылка,
	|	ГТДИмпорт.Номер КАК Номер,
	|	ГТДИмпорт.Период КАК Период,
	|	ГТДИмпорт.Организация КАК Организация,
	|	РеквизитыДоговоров.ВалютаДокумента КАК ВалютаДокумента,
	|	РеквизитыДоговоров.КурсДокумента КАК КурсДокумента,
	|	РеквизитыДоговоров.КратностьДокумента КАК КратностьДокумента,
	|	РеквизитыДоговоров.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	РеквизитыДоговоров.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	РеквизитыДоговоров.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ГТДИмпорт.Контрагент КАК Контрагент,
	|	РеквизитыДоговоров.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеквизитыДоговоров.ТаможенныйСбор КАК ТаможенныйСбор,
	|	РеквизитыДоговоров.ТаможенныйШтраф КАК ТаможенныйШтраф,
	|	РеквизитыДоговоров.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ГТДИмпорт.СтатьяПрочихДоходовРасходов КАК СтатьяПрочихДоходовРасходов,
	|	ЕСТЬNULL(ГТДИмпорт.СтатьяПрочихДоходовРасходов.ПринятиеКналоговомуУчету, ИСТИНА) КАК ЗатратыПринимаютсяКНУ,
	|	ГТДИмпорт.СчетУчетаРасходов КАК СчетУчетаРасходов,
	|	ГТДИмпорт.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ГТДИмпорт.НомерГТД КАК НомерГТД,
	|	РеквизитыДоговоров.СуммаВзаиморасчетов + ВЫБОР
	|		КОГДА РеквизитыДоговоров.Валютный = ИСТИНА
	|			ТОГДА СуммыПошлины.СуммаВал
	|		ИНАЧЕ СуммыПошлины.Сумма
	|	КОНЕЦ КАК СуммаВзаиморасчетов,
	|	ГТДИмпорт.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	ГТДИмпорт.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	РеквизитыДоговоров.Валютный КАК Валютный,
	|	РеквизитыДоговоров.ВидДоговора КАК ВидДоговора,
	|	РеквизитыДоговоров.УчетАгентскогоНДС КАК УчетАгентскогоНДС
	|ПОМЕСТИТЬ РеквизитыАвансов
	|ИЗ
	|	Реквизиты КАК ГТДИмпорт,
	|	РеквизитыДоговоров КАК РеквизитыДоговоров,
	|	СуммыПошлины КАК СуммыПошлины
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РеквизитыДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СуммыПошлины
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Реквизиты.КурсДокумента КАК КурсДокумента,
	|	Реквизиты.КратностьДокумента КАК КратностьДокумента,
	|	Реквизиты.НомерГТД.Код КАК НомерТаможеннойДекларации
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаТовары"          , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРазделы"         , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаСуммыПоТоварам"  , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГТДИмпортТовары.Ссылка,
	|	ГТДИмпортТовары.НомерСтроки,
	|	ГТДИмпортТовары.Номенклатура,
	|	ГТДИмпортТовары.Количество,
	|	ГТДИмпортТовары.ФактурнаяСтоимость,
	|	ГТДИмпортТовары.СуммаПошлины,
	|	ГТДИмпортТовары.СуммаНДС,
	|	ГТДИмпортТовары.ДокументПартии,
	|	ГТДИмпортТовары.СчетУчета,
	|	ГТДИмпортТовары.СчетУчетаНДС,
	|	ГТДИмпортТовары.СпособУчетаНДС,
	|	ГТДИмпортТовары.СчетУчетаНУ,
	|	ГТДИмпортТовары.СтатьяЗатратНУ,
	|	ВЫБОР
	|		КОГДА ГТДИмпортТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ГТДИмпортТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		КОГДА ГТДИмпортТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ГТДИмпортТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗатратыПринимаютсяКНУ,
	|	ГТДИмпортТовары.ОтражениеВУСН,
	|	ГТДИмпортТовары.СтранаПроисхождения,
	|	ГТДИмпортТовары.НомерРаздела КАК НомерРаздела
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	Документ.ГТДИмпорт.Товары КАК ГТДИмпортТовары
	|ГДЕ
	|	ГТДИмпортТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерРаздела
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГТДИмпортРазделы.Ссылка,
	|	ГТДИмпортРазделы.НомерСтроки КАК НомерСтроки,
	|	ГТДИмпортРазделы.ТаможеннаяСтоимость,
	|	ГТДИмпортРазделы.ПошлинаВВалюте,
	|	ГТДИмпортРазделы.СтавкаПошлины,
	|	ГТДИмпортРазделы.СуммаПошлины,
	|	ГТДИмпортРазделы.НДСВВалюте,
	|	ГТДИмпортРазделы.СтавкаНДС,
	|	ГТДИмпортРазделы.СуммаНДС,
	|	ГТДИмпортРазделы.НомерСтроки КАК НомерРаздела
	|ПОМЕСТИТЬ РазделыДокумента
	|ИЗ
	|	Документ.ГТДИмпорт.Разделы КАК ГТДИмпортРазделы
	|ГДЕ
	|	ГТДИмпортРазделы.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка,
	|	ТоварыДокумента.НомерСтроки,
	|	ТоварыДокумента.НомерРаздела,
	|	ТоварыДокумента.Номенклатура,
	|	ТоварыДокумента.Количество,
	|	ТоварыДокумента.ФактурнаяСтоимость,
	|	ТоварыДокумента.СчетУчета,
	|	ТоварыДокумента.СчетУчетаНДС,
	|	ТоварыДокумента.СпособУчетаНДС,
	|	ТоварыДокумента.СчетУчетаНУ,
	|	ТоварыДокумента.СтатьяЗатратНУ,
	|	ТоварыДокумента.ЗатратыПринимаютсяКНУ,
	|	ТоварыДокумента.ОтражениеВУСН,
	|	ТоварыДокумента.СтранаПроисхождения,
	|	ТоварыДокумента.ДокументПартии,
	|	ТоварыДокумента.ДокументПартии.Склад КАК Склад,
	|	ТоварыДокумента.ДокументПартии.ПодразделениеОрганизации КАК Подразделение,
	|	РазделыДокумента.ПошлинаВВалюте,
	|	РазделыДокумента.НДСВВалюте,
	|	РазделыДокумента.СтавкаНДС,
	|	РазделыДокумента.СтавкаПошлины,
	|	РазделыДокумента.СуммаПошлины,
	|	РазделыДокумента.СуммаНДС,
	|	ВЫБОР
	|		КОГДА РазделыДокумента.ПошлинаВВалюте
	|			ТОГДА Реквизиты.СчетУчетаРасчетовСКонтрагентомВал
	|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
	|	КОНЕЦ КАК КорСчет,
	|	ВЫБОР
	|		КОГДА РазделыДокумента.ПошлинаВВалюте
	|			ТОГДА ТоварыДокумента.СуммаПошлины
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПошлиныВВалюте,
	|	ВЫБОР
	|		КОГДА РазделыДокумента.НДСВВалюте
	|			ТОГДА ТоварыДокумента.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСВВалюте,
	|	ТоварыДокумента.СуммаПошлины * ВЫБОР
	|		КОГДА РазделыДокумента.ПошлинаВВалюте
	|			ТОГДА &КоэффициентРуб
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПошлиныРуб,
	|	ТоварыДокумента.СуммаНДС * ВЫБОР
	|		КОГДА РазделыДокумента.НДСВВалюте
	|			ТОГДА &КоэффициентРуб
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаНДСРуб,
	|	ТоварыДокумента.СуммаПошлины * ВЫБОР
	|		КОГДА РазделыДокумента.ПошлинаВВалюте
	|			ТОГДА &КоэффициентВзаиморасчетов
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПошлиныВзаиморасчетов,
	|	ТоварыДокумента.СуммаНДС * ВЫБОР
	|		КОГДА РазделыДокумента.НДСВВалюте
	|			ТОГДА &КоэффициентВзаиморасчетов
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаНДСВзаиморасчетов
	|ПОМЕСТИТЬ СуммыПоТоварам
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазделыДокумента КАК РазделыДокумента
	|		ПО ТоварыДокумента.НомерРаздела = РазделыДокумента.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТоварыДокумента.Ссылка)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаТаблицаПошлина(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаПошлинаРеквизиты"  , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПошлина"           , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Регистратор,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СуммаПошлиныВВалюте КАК СуммаВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСВВалюте КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаРуб,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаБУ,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаНУ,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Подразделение КАК Подразделение,
	|	ТаблицаТовары.ДокументПартии КАК ДокументОприходования,
	|	ТаблицаТовары.КорСчет КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ПошлинаВВалюте
	|			ТОГДА Реквизиты.ДоговорКонтрагента
	|		ИНАЧЕ Реквизиты.ДоговорКонтрагентаРегл
	|	КОНЕЦ КАК КорСубконто2,
	|	ТаблицаТовары.Ссылка КАК КорСубконто3,
	|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчетаНУ,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатратНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСубконтоСтатьиНУ,
	|	ТаблицаТовары.ЗатратыПринимаютсяКНУ,
	|	ТаблицаТовары.ОтражениеВУСН,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ТаблицаТовары.ПошлинаВВалюте
	|				ТОГДА &СодержаниеПошлинаВал
	|			ИНАЧЕ &СодержаниеПошлина
	|		КОНЕЦ КАК СТРОКА(150)) КАК Содержание
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|ГДЕ
	|	ТаблицаТовары.СуммаПошлиныРуб <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаможенныйСбор(НомераТаблиц)

	НомераТаблиц.Вставить("ТаможенныйСборРеквизиты"  , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаможенныйСборВРублях"    , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаможенныйСборВВалюте"    , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ТаможенныйСбор,
	|	Реквизиты.ТаможенныйСборВал,
	|	&КоэффициентРуб КАК КоэффициентРуб,
	|	&КоэффициентВзаиморасчетов КАК КоэффициентВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Регистратор,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	ТаблицаТовары.СуммаПошлиныВВалюте КАК СуммаВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСВВалюте КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаРуб,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаБУ,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаНУ,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Подразделение КАК Подразделение,
	|	ТаблицаТовары.ДокументПартии КАК ДокументОприходования,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагентаРегл КАК КорСубконто2,
	|	ТаблицаТовары.Ссылка КАК КорСубконто3,
	|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчетаНУ,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатратНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСубконтоСтатьиНУ,
	|	ТаблицаТовары.ЗатратыПринимаютсяКНУ,
	|	ТаблицаТовары.ОтражениеВУСН,
	|	ВЫРАЗИТЬ(&СодержаниеСборы КАК СТРОКА(150)) КАК Содержание
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Регистратор,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	ТаблицаТовары.СуммаПошлиныВВалюте КАК СуммаВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСВВалюте КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаРуб,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаБУ,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК СуммаНУ,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Подразделение КАК Подразделение,
	|	ТаблицаТовары.ДокументПартии КАК ДокументОприходования,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентомВал КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	ТаблицаТовары.Ссылка КАК КорСубконто3,
	|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчетаНУ,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатратНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСубконтоСтатьиНУ,
	|	ТаблицаТовары.ЗатратыПринимаютсяКНУ,
	|	ТаблицаТовары.ОтражениеВУСН,
	|	ВЫРАЗИТЬ(&СодержаниеСборыВал КАК СТРОКА(150)) КАК Содержание
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаШтраф(НомераТаблиц)

	НомераТаблиц.Вставить("ШтрафРеквизиты"  , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ШтрафВРублях"    , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ШтрафВВалюте"    , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ТаможенныйШтраф,
	|	Реквизиты.ТаможенныйШтрафВал,
	|	&КоэффициентРуб КАК КоэффициентРуб,
	|	&КоэффициентВзаиморасчетов КАК КоэффициентВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	0 КАК НомерСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	Реквизиты.ТаможенныйШтраф КАК СуммаРуб,
	|	Реквизиты.ТаможенныйШтраф КАК СуммаБУ,
	|	Реквизиты.ТаможенныйШтраф КАК СуммаНУ,
	|	Реквизиты.ТаможенныйШтраф КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСРуб,
	|	NULL КАК СтавкаНДС,
	|	Реквизиты.СчетУчетаРасходов КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагентаРегл КАК КорСубконто2,
	|	Реквизиты.Ссылка КАК КорСубконто3,
	|	Реквизиты.СчетУчетаРасходов КАК СчетУчетаНУ,
	|	Реквизиты.СтатьяПрочихДоходовРасходов КАК СтатьяЗатратНУ,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы) КАК ВидСубконтоСтатьиНУ,
	|	Реквизиты.ЗатратыПринимаютсяКНУ,
	|	&СодержаниеШтраф КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ТаможенныйШтраф <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	0 КАК НомерСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	ВЫРАЗИТЬ(Реквизиты.ТаможенныйШтрафВал * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб,
	|	ВЫРАЗИТЬ(Реквизиты.ТаможенныйШтрафВал * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаБУ,
	|	Реквизиты.ТаможенныйШтрафВал КАК СуммаВзаиморасчетов,
	|	ВЫРАЗИТЬ(Реквизиты.ТаможенныйШтрафВал * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаНУ,
	|	0 КАК СуммаНДСРуб,
	|	NULL КАК СтавкаНДС,
	|	Реквизиты.СчетУчетаРасходов КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентомВал КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	Реквизиты.Ссылка КАК КорСубконто3,
	|	Реквизиты.СчетУчетаРасходов КАК СчетУчетаНУ,
	|	Реквизиты.СтатьяПрочихДоходовРасходов КАК СтатьяЗатратНУ,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы) КАК ВидСубконтоСтатьиНУ,
	|	Реквизиты.ЗатратыПринимаютсяКНУ,
	|	&СодержаниеШтрафВал КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ТаможенныйШтрафВал <> 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыНДСВВалюте",  НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РеквизитыНДСВРублях",  НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТоварыНДСВВалюте",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТоварыНДСВРублях",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НДСВСтоимостиВВалюте", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НДСВСтоимостиВРублях", НомераТаблиц.Количество());
	
	НомераТаблиц.Вставить("ПредварительнаяТаблицаПоставщик",                            НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТоварыПартии",                                               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ФактурнаяСтоимостьПредварительная",                          НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ФактурнаяСтоимость",                                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ФактурнаяСтоимостьДляЖурнала",                               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПредварительнаяТаблицаРегистрацияВЖурналеУчетаСчетовФактур", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РегистрацияВЖурналеУчетаСчетовФактур",                       НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентомВал КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.НДСПредъявленКВычету КАК НДСПредъявленКВычету,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВЫРАЗИТЬ(&СодержаниеНДСВал КАК СТРОКА(150)) КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагентаРегл КАК ДоговорКонтрагента,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.НДСПредъявленКВычету КАК НДСПредъявленКВычету,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	&ВалютаРеглУчета КАК ВалютаВзаиморасчетов,
	|	ВЫРАЗИТЬ(&СодержаниеНДС КАК СТРОКА(150)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		ИНАЧЕ ""20""
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка КАК Регистратор,
	|	ТоварыДокумента.НомерРаздела КАК НомерРаздела,
	|	ТоварыДокумента.НомерСтроки КАК НомерСтроки,
	|	ТоварыДокумента.ДокументПартии КАК ДокументПартии,
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.ДокументПартии.Склад КАК Склад,
	|	ТоварыДокумента.ДокументПартии.ПодразделениеОрганизации КАК Подразделение,
	|	ТоварыДокумента.СчетУчета КАК СчетУчета,
	|	ТоварыДокумента.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТоварыДокумента.СпособУчетаНДС КАК СпособУчетаНДС,
	|	РазделыДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ТоварыДокумента.СуммаНДС * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаНДСРуб,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2014, 10, 1)
	|			ТОГДА 0
	|		ИНАЧЕ (ВЫРАЗИТЬ(ТоварыДокумента.ФактурнаяСтоимость * &КоэффициентТаможеннаяСтоимостьРуб КАК ЧИСЛО(15, 2))) + ТоварыДокумента.СуммаПошлины
	|	КОНЕЦ КАК СуммаБезНДСРуб,
	|	ТоварыДокумента.СуммаНДС КАК СуммаНДСВзаиморасчетов,
	|	ВЫРАЗИТЬ(&СодержаниеПошлинаВал КАК СТРОКА(150)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		ИНАЧЕ ""20""
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазделыДокумента КАК РазделыДокумента
	|		ПО ТоварыДокумента.НомерРаздела = РазделыДокумента.НомерСтроки
	|ГДЕ
	|	РазделыДокумента.НДСВВалюте
	|	И ТоварыДокумента.СуммаНДС > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерРаздела,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка КАК Регистратор,
	|	ТоварыДокумента.НомерРаздела КАК НомерРаздела,
	|	ТоварыДокумента.НомерСтроки КАК НомерСтроки,
	|	ТоварыДокумента.ДокументПартии КАК ДокументПартии,
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.ДокументПартии.Склад КАК Склад,
	|	ТоварыДокумента.ДокументПартии.ПодразделениеОрганизации КАК Подразделение,
	|	ТоварыДокумента.СчетУчета КАК СчетУчета,
	|	ТоварыДокумента.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТоварыДокумента.СпособУчетаНДС КАК СпособУчетаНДС,
	|	РазделыДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТоварыДокумента.СуммаНДС КАК СуммаНДСРуб,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2014, 10, 1)
	|			ТОГДА 0
	|		ИНАЧЕ (ВЫРАЗИТЬ(ТоварыДокумента.ФактурнаяСтоимость * &КоэффициентТаможеннаяСтоимостьРуб КАК ЧИСЛО(15, 2))) + ТоварыДокумента.СуммаПошлины
	|	КОНЕЦ КАК СуммаБезНДСРуб,
	|	ТоварыДокумента.СуммаНДС КАК СуммаНДСВзаиморасчетов,
	|	ВЫРАЗИТЬ(&СодержаниеПошлина КАК СТРОКА(150)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		ИНАЧЕ ""20""
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазделыДокумента КАК РазделыДокумента
	|		ПО ТоварыДокумента.НомерРаздела = РазделыДокумента.НомерСтроки
	|ГДЕ
	|	НЕ РазделыДокумента.НДСВВалюте
	|	И ТоварыДокумента.СуммаНДС > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерРаздела,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Регистратор,
	|	ТаблицаТовары.НомерРаздела КАК НомерРаздела,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	ТаблицаТовары.СуммаНДСВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаРуб,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаБУ,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНУ,
	|	ТаблицаТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.ДокументПартии КАК ДокументОприходования,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Подразделение КАК Подразделение,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентомВал КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	ТаблицаТовары.Ссылка КАК КорСубконто3,
	|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчетаНУ,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатратНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСубконтоСтатьиНУ,
	|	ТаблицаТовары.ЗатратыПринимаютсяКНУ КАК ЗатратыПринимаютсяКНУ,
	|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ВЫРАЗИТЬ(&СодержаниеНДСВСтоимостиВал КАК СТРОКА(150)) КАК Содержание
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|ГДЕ
	|	ТаблицаТовары.НДСВВалюте
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерРаздела,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Регистратор,
	|	ТаблицаТовары.НомерРаздела КАК НомерРаздела,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	ТаблицаТовары.СуммаНДСВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаРуб,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаБУ,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНУ,
	|	ТаблицаТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.ДокументПартии КАК ДокументОприходования,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Подразделение КАК Подразделение,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагентаРегл КАК КорСубконто2,
	|	ТаблицаТовары.Ссылка КАК КорСубконто3,
	|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчетаНУ,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатратНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСубконтоСтатьиНУ,
	|	ТаблицаТовары.ЗатратыПринимаютсяКНУ КАК ЗатратыПринимаютсяКНУ,
	|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ВЫРАЗИТЬ(&СодержаниеНДСВСтоимости КАК СТРОКА(150)) КАК Содержание
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|ГДЕ
	|	НЕ ТаблицаТовары.НДСВВалюте
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерРаздела,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку)
	|			ТОГДА ПоступлениеТоваровУслуг.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Посредник,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Поставщик
	|ПОМЕСТИТЬ ТаблицаПоставщик
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ПоступлениеТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ПО ТоварыДокумента.ДокументПартии = ПоступлениеТоваровУслуг.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазделыДокумента КАК РазделыДокумента
	|		ПО ТоварыДокумента.НомерРаздела = РазделыДокумента.НомерСтроки
	|ГДЕ
	|	НЕ РазделыДокумента.НДСВВалюте
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	СУММА(ТоварыДокумента.Количество) КАК Количество,
	|	ТоварыДокумента.ДокументПартии КАК ДокументПартии,
	|	СУММА(ВЫБОР
	|			КОГДА Реквизиты.НДСВключенВСтоимость
	|					ИЛИ ТоварыДокумента.СпособУЧетаНДС = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.УчитываетсяВCтоимости)
	|				ТОГДА ВЫРАЗИТЬ(ТоварыДокумента.СуммаНДС + ТоварыДокумента.СуммаПошлины КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ТоварыДокумента.СуммаПошлины
	|		КОНЕЦ) КАК НДСИПошлины,
	|	ТоварыДокумента.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ ТоварыПартии
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокумента.ДокументПартии,
	|	ТоварыДокумента.Номенклатура,
	|	ТоварыДокумента.СчетУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПартии,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыПартии.СчетУчета КАК СчетУчета,
	|	ТоварыПартии.Номенклатура КАК Номенклатура,
	|	ТоварыПартии.ДокументПартии КАК ДокументПартии,
	|	ХозрасчетныйСубконто.Период КАК Период,
	|	ХозрасчетныйСубконто.Регистратор КАК Регистратор,
	|	ХозрасчетныйСубконто.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ФактурнаяСтоимостьПредварительная
	|ИЗ
	|	ТоварыПартии КАК ТоварыПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО ТоварыПартии.ДокументПартии = ХозрасчетныйСубконто.Регистратор
	|			И ТоварыПартии.Номенклатура = ХозрасчетныйСубконто.Значение
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|ГДЕ
	|	&ОтражатьФактурнуюСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактурнаяСтоимостьПредварительная.СчетУчета КАК СчетУчета,
	|	ФактурнаяСтоимостьПредварительная.Номенклатура КАК Номенклатура,
	|	ФактурнаяСтоимостьПредварительная.ДокументПартии КАК ДокументПартии,
	|	СУММА(Хозрасчетный.Сумма) КАК СуммаОборотДт,
	|	СУММА(Хозрасчетный.КоличествоДт) КАК КоличествоОборотДт
	|ПОМЕСТИТЬ ФактурнаяСтоимость
	|ИЗ
	|	ФактурнаяСтоимостьПредварительная КАК ФактурнаяСтоимостьПредварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ФактурнаяСтоимостьПредварительная.СчетУчета = Хозрасчетный.СчетДт
	|			И ФактурнаяСтоимостьПредварительная.Регистратор = Хозрасчетный.Регистратор
	|			И ФактурнаяСтоимостьПредварительная.НомерСтроки = Хозрасчетный.НомерСтроки
	|			И ФактурнаяСтоимостьПредварительная.Период = Хозрасчетный.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактурнаяСтоимостьПредварительная.СчетУчета,
	|	ФактурнаяСтоимостьПредварительная.Номенклатура,
	|	ФактурнаяСтоимостьПредварительная.ДокументПартии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПартии,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ФактурнаяСтоимость.КоличествоОборотДт <> ТоварыПартии.Количество
	|				ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(ФактурнаяСтоимость.КоличествоОборотДт, 0) <> 0
	|							ТОГДА ВЫРАЗИТЬ(ФактурнаяСтоимость.СуммаОборотДт / ФактурнаяСтоимость.КоличествоОборотДт * ТоварыПартии.Количество + ТоварыПартии.НДСИПошлины КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ТоварыПартии.НДСИПошлины
	|					КОНЕЦ
	|			ИНАЧЕ ВЫРАЗИТЬ(ФактурнаяСтоимость.СуммаОборотДт + ТоварыПартии.НДСИПошлины КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) КАК СтоимостьДЛяЖурнала
	|ПОМЕСТИТЬ ФактурнаяСтоимостьДляЖурнала
	|ИЗ
	|	ТоварыПартии КАК ТоварыПартии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФактурнаяСтоимость КАК ФактурнаяСтоимость
	|		ПО ТоварыПартии.ДокументПартии = ФактурнаяСтоимость.ДокументПартии
	|			И ТоварыПартии.Номенклатура = ФактурнаяСтоимость.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаПоставщик.Посредник = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ТаблицаПоставщик.Поставщик
	|		ИНАЧЕ ТаблицаПоставщик.Посредник
	|	КОНЕЦ КАК Контрагент,
	|	"""" КАК КППКонтрагента,
	|	Реквизиты.Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВыставленияПолучения,
	|	1 КАК КодСпособаВыставленияПолучения,
	|	""20"" КАК КодВидаОперации,
	|	Реквизиты.НомерГТД КАК НомерСчетаФактуры,
	|	&ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	&ВалютаРеглУчета КАК Валюта,
	|	ЛОЖЬ КАК ПоСтавкеБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураНеВыставляется,
	|	ЛОЖЬ КАК РасчетыВУсловныхЕдиницах,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	0 КАК ИндексСтроки,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
	|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйПокупателю,
	|	2 КАК КодВидаСделки,
	|	НЕОПРЕДЕЛЕНО КАК Посредник,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактурыПродавца,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактурыПродавца,
	|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия,
	|	ВЫБОР
	|		КОГДА ТаблицаПоставщик.Посредник = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Продавец,
	|	СУММА(ВЫБОР
	|			КОГДА &ОтражатьФактурнуюСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ (ВЫРАЗИТЬ(РазделыДокумента.ТаможеннаяСтоимость * &КоэффициентТаможеннаяСтоимостьРуб КАК ЧИСЛО(15, 2))) + РазделыДокумента.СуммаПошлины + РазделыДокумента.СуммаНДС
	|		КОНЕЦ) КАК СуммаПоСчетуФактуре,
	|	СУММА(РазделыДокумента.СуммаНДС) КАК СуммаНДС,
	|	СУММА(0) КАК СуммаПоСчетуФактуреКомиссия,
	|	СУММА(0) КАК СуммаНДСКомиссия,
	|	СУММА(0) КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	СУММА(0) КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	СУММА(0) КАК СуммаНДСРазницаУвеличение,
	|	СУММА(0) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(0) КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	СУММА(0) КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	СУММА(0) КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	СУММА(0) КАК СуммаНДСРазницаУменьшениеКомиссия
	|ПОМЕСТИТЬ ВТРегистрацияВЖурнале
	|ИЗ
	|	РазделыДокумента КАК РазделыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПоставщик КАК ТаблицаПоставщик
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ РазделыДокумента.НДСВВалюте
	|	И Реквизиты.Период >= ДАТАВРЕМЯ(2014, 10, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.НомерГТД,
	|	Реквизиты.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаПоставщик.Посредник = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаПоставщик.Посредник = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ТаблицаПоставщик.Поставщик
	|		ИНАЧЕ ТаблицаПоставщик.Посредник
	|	КОНЕЦ,
	|	Реквизиты.Период,
	|	Реквизиты.Ссылка,
	|	Реквизиты.Ссылка,
	|	Реквизиты.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРегистрацияВЖурнале.Период КАК Период,
	|	ВТРегистрацияВЖурнале.Регистратор КАК Регистратор,
	|	ВТРегистрацияВЖурнале.Организация КАК Организация,
	|	ВТРегистрацияВЖурнале.Контрагент КАК Контрагент,
	|	ВТРегистрацияВЖурнале.КППКонтрагента КАК КППКонтрагента,
	|	ВТРегистрацияВЖурнале.СчетФактура КАК СчетФактура,
	|	ВТРегистрацияВЖурнале.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ВТРегистрацияВЖурнале.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
	|	ВТРегистрацияВЖурнале.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
	|	ВТРегистрацияВЖурнале.КодВидаОперации КАК КодВидаОперации,
	|	ВТРегистрацияВЖурнале.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ВТРегистрацияВЖурнале.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.НомерИсправления КАК НомерИсправления,
	|	ВТРегистрацияВЖурнале.ДатаИсправления КАК ДатаИсправления,
	|	ВТРегистрацияВЖурнале.Валюта КАК Валюта,
	|	ВТРегистрацияВЖурнале.Продавец КАК Продавец,
	|	ВТРегистрацияВЖурнале.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
	|	ВТРегистрацияВЖурнале.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ВТРегистрацияВЖурнале.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ВТРегистрацияВЖурнале.НомерИсправленияКорректировочногоСчетаФактуры КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ДатаИсправленияКорректировочногоСчетаФактуры КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ИндексСтроки КАК ИндексСтроки,
	|	ВТРегистрацияВЖурнале.ДокументОснование КАК ДокументОснование,
	|	ВТРегистрацияВЖурнале.Субкомиссионер КАК Субкомиссионер,
	|	ВТРегистрацияВЖурнале.СчетФактураВыданныйПокупателю КАК СчетФактураВыданныйПокупателю,
	|	ВТРегистрацияВЖурнале.КодВидаСделки КАК КодВидаСделки,
	|	ВТРегистрацияВЖурнале.Посредник КАК Посредник,
	|	ВТРегистрацияВЖурнале.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	ВТРегистрацияВЖурнале.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
	|	ВТРегистрацияВЖурнале.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	"""" КАК КодВидаОперацииНаУменьшение,
	|	СУММА(ВЫБОР
	|			КОГДА &ОтражатьФактурнуюСтоимость
	|				ТОГДА ВЫРАЗИТЬ(ФактурнаяСтоимостьДляЖурнала.СтоимостьДЛяЖурнала + Реквизиты.ТаможенныйСбор КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ВТРегистрацияВЖурнале.СуммаПоСчетуФактуре
	|		КОНЕЦ) КАК СуммаПоСчетуФактуре,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаПоСчетуФактуреКомиссия) КАК СуммаПоСчетуФактуреКомиссия,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаНДСКомиссия) КАК СуммаНДСКомиссия,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаПоСчетуФактуреРазницаУвеличение) КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаПоСчетуФактуреРазницаУменьшение) КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаНДСРазницаУвеличение) КАК СуммаНДСРазницаУвеличение,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаНДСРазницаУменьшение) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия) КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия) КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаНДСРазницаУвеличениеКомиссия) КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	СУММА(ВТРегистрацияВЖурнале.СуммаНДСРазницаУменьшениеКомиссия) КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	ЛОЖЬ КАК ИсправлениеСобственнойОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйСчетФактура,
	|	"""" КАК ИННКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК ИсправленныйСчетФактура,
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактураПолученныйОтПродавца,
	|	ЛОЖЬ КАК Сторно,
	|	"""" КАК ИННПродавца,
	|	"""" КАК КПППродавца,
	|	"""" КАК ИННСубкомиссионера,
	|	"""" КАК КППСубкомиссионера
	|ИЗ
	|	ВТРегистрацияВЖурнале КАК ВТРегистрацияВЖурнале
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФактурнаяСтоимостьДляЖурнала КАК ФактурнаяСтоимостьДляЖурнала
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРегистрацияВЖурнале.Период,
	|	ВТРегистрацияВЖурнале.Регистратор,
	|	ВТРегистрацияВЖурнале.Организация,
	|	ВТРегистрацияВЖурнале.Контрагент,
	|	ВТРегистрацияВЖурнале.КППКонтрагента,
	|	ВТРегистрацияВЖурнале.СчетФактура,
	|	ВТРегистрацияВЖурнале.ЧастьЖурнала,
	|	ВТРегистрацияВЖурнале.ДатаВыставленияПолучения,
	|	ВТРегистрацияВЖурнале.КодСпособаВыставленияПолучения,
	|	ВТРегистрацияВЖурнале.КодВидаОперации,
	|	ВТРегистрацияВЖурнале.НомерСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ДатаСчетаФактуры,
	|	ВТРегистрацияВЖурнале.НомерКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ДатаКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.НомерИсправления,
	|	ВТРегистрацияВЖурнале.ДатаИсправления,
	|	ВТРегистрацияВЖурнале.Валюта,
	|	ВТРегистрацияВЖурнале.Продавец,
	|	ВТРегистрацияВЖурнале.ПоСтавкеБезНДС,
	|	ВТРегистрацияВЖурнале.СчетФактураНеВыставляется,
	|	ВТРегистрацияВЖурнале.РасчетыВУсловныхЕдиницах,
	|	ВТРегистрацияВЖурнале.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ВТРегистрацияВЖурнале.ИндексСтроки,
	|	ВТРегистрацияВЖурнале.ДокументОснование,
	|	ВТРегистрацияВЖурнале.Субкомиссионер,
	|	ВТРегистрацияВЖурнале.СчетФактураВыданныйПокупателю,
	|	ВТРегистрацияВЖурнале.КодВидаСделки,
	|	ВТРегистрацияВЖурнале.Посредник,
	|	ВТРегистрацияВЖурнале.НомерСчетаФактурыПродавца,
	|	ВТРегистрацияВЖурнале.ДатаСчетаФактурыПродавца,
	|	ВТРегистрацияВЖурнале.КодВидаОперацииКомиссия";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты"          , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаДокумента"   , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеквизитыАвансов.Ссылка КАК Регистратор,
	|	РеквизитыАвансов.Период КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК ВидОперации,
	|	РеквизитыАвансов.Организация КАК Организация,
	|	РеквизитыАвансов.ВалютаДокумента КАК ВалютаДокумента,
	|	РеквизитыАвансов.СпособЗачетаАвансов,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСН,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСНПатент,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	ЛОЖЬ КАК ДеятельностьНаТорговомСборе,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	РеквизитыАвансов.НДСВключенВСтоимость,
	|	РеквизитыАвансов.УчетАгентскогоНДС
	|ИЗ
	|	РеквизитыАвансов КАК РеквизитыАвансов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыАвансов.Ссылка КАК ДокументРасчетов,
	|	РеквизитыАвансов.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетов,
	|	РеквизитыАвансов.СчетУчетаРасчетовСКонтрагентом КАК СчетАвансов,
	|	РеквизитыАвансов.Контрагент,
	|	РеквизитыАвансов.ДоговорКонтрагента,
	|	СУММА(РеквизитыАвансов.ТаможенныйСбор) КАК ТаможенныйСбор,
	|	СУММА(РеквизитыАвансов.ТаможенныйШтраф) КАК ТаможенныйШтраф,
	|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ДоговорыКонтрагентов.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ВЫБОР
	|		КОГДА ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	РеквизитыАвансов.ПодразделениеОрганизации КАК Подразделение,
	|	СУММА(РеквизитыАвансов.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(РеквизитыАвансов.СуммаВзаиморасчетов * &КоэффициентРуб) КАК СуммаРуб,
	|	0 КАК СуммаВзаиморасчетовКомитента,
	|	0 КАК СуммаВзаиморасчетовПатент,
	|	0 КАК СуммаВзаиморасчетовЕНВД,
	|	0 КАК СуммаВзаиморасчетовТорговыйСбор
	|ИЗ
	|	РеквизитыАвансов КАК РеквизитыАвансов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеквизитыАвансов.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеквизитыАвансов.Ссылка,
	|	РеквизитыАвансов.СчетУчетаРасчетовСКонтрагентом,
	|	РеквизитыАвансов.Контрагент,
	|	РеквизитыАвансов.ДоговорКонтрагента,
	|	ДоговорыКонтрагентов.ВидДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов,
	|	ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах,
	|	ДоговорыКонтрагентов.УчетАгентскогоНДС,
	|	РеквизитыАвансов.ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	РеквизитыАвансов.СчетУчетаРасчетовСКонтрагентом"
	
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если Реквизиты.СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаАвансов", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ЗачетАвансовТаблицаАвансов", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса  + 
		"ВЫБРАТЬ
		|	ТаблицаЗачетАвансов.НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетАвансов,
		|	Реквизиты.Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаЗачетАвансов.ДокументАванса,
		|	ТаблицаЗачетАвансов.СуммаЗачета
		|ИЗ
		|	Документ.ГТДИмпорт.ЗачетАвансов КАК ТаблицаЗачетАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РеквизитыАвансов КАК Реквизиты
		|		ПО ТаблицаЗачетАвансов.Ссылка = Реквизиты.Ссылка
		|ГДЕ
		|	ТаблицаЗачетАвансов.Ссылка = &Ссылка
		|	И Реквизиты.Валютный = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаЗачетАвансов.НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаПереоценкаВалютныхОстатков(НомераТаблиц)

	НомераТаблиц.Вставить("ПереоценкаВалютныхОстатковРеквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ВалютаДокумента
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц)

	НомераТаблиц.Вставить("УСНРеквизитыВВалюте",					НомераТаблиц.Количество());
	НомераТаблиц.Вставить("УСНРеквизитыВРублях",					НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасходыУСНВВалюте", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасходыУСНВРублях", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК Валюта,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	Реквизиты.ТаможенныйСборВал КАК ТаможенныйСбор,
	|	&ПрименяетсяУСНДоходыМинусРасходы,
	|	&КоэффициентРуб КАК КоэффициентРуб,
	|	&КоэффициентВзаиморасчетов КАК КоэффициентВзаиморасчетов,
	|	ЛОЖЬ КАК РасходыПредпринимателя
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ДоговорКонтрагентаРегл КАК ДоговорКонтрагента,
	|	&ВалютаРеглУчета КАК Валюта,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	Реквизиты.ТаможенныйСбор КАК ТаможенныйСбор,
	|	&ПрименяетсяУСНДоходыМинусРасходы,
	|	&КоэффициентРуб КАК КоэффициентРуб,
	|	&КоэффициентВзаиморасчетов КАК КоэффициентВзаиморасчетов,
	|	ЛОЖЬ КАК РасходыПредпринимателя
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.ТаможенныеПлатежи) КАК ВидРасхода,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
	|	ТаблицаТовары.Номенклатура КАК ЭлементРасхода,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Субсчета10)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Субсчета10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоМатериал,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.ДокументПартии КАК Партия,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	0 КАК Сумма,
	|	0 КАК Сбор, 				// валютный сбор распределяем по фактурной стоимости в обработке проведения
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ПошлинаВВалюте
	|			ТОГДА ТаблицаТовары.СуммаПошлиныВВалюте
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Пошлина,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НДСВВалюте
	|			ТОГДА ТаблицаТовары.СуммаНДСВВалюте
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НДС,
	|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|			ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|ГДЕ
	|	&ПрименяетсяУСНДоходыМинусРасходы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерРаздела,
	|	ТаблицаТовары.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.ТаможенныеПлатежи) КАК ВидРасхода,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
	|	ТаблицаТовары.Номенклатура КАК ЭлементРасхода,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Субсчета10)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Субсчета10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоМатериал,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.ДокументПартии КАК Партия,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	0 КАК Сумма,
	|	0 КАК Сбор, 				// рублевый сбор распределяем по фактурной стоимости в обработке проведения
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.ПошлинаВВалюте
	|			ТОГДА ТаблицаТовары.СуммаПошлиныРуб
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Пошлина,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.НДСВВалюте
	|			ТОГДА ТаблицаТовары.СуммаНДСРуб
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НДС,
	|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|			ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|ГДЕ
	|	&ПрименяетсяУСНДоходыМинусРасходы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерРаздела,
	|	ТаблицаТовары.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПоступлениеМПЗИП(НомераТаблиц)

	НомераТаблиц.Вставить("ПоступлениеМПЗИПРеквизиты",			НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеМПЗИПТаблицаТоваров",		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеМПЗИПТаможенныйСбор",		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеМПЗИПТаможенныйСборВал",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеМПЗИПТаможенныйШтраф",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеМПЗИПТаможенныйШтрафВал",	НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""ПоступлениеДопРасходов"" КАК ТипПоступления,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	НЕОПРЕДЕЛЕНО КАК ТипСклада,
	|	ЛОЖЬ КАК НДСВключенВСтоимость,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	""ГТДИмпорт"" КАК ВидОперации,
	|	Реквизиты.ТаможенныйСбор ,
	|	Реквизиты.ТаможенныйСборВал КАК ТаможенныйСборВал
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СуммаПошлиныРуб КАК Сумма,
	|	0 КАК НДС,
	|	0 КАК Количество,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НоменклатурнаяГруппа
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидРасходовНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ТаблицаТовары.ДокументПартии КАК Партия
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|ГДЕ
	|	&ПлательщикНДФЛ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК Сумма,
	|	0 КАК НДС,
	|	0 КАК Количество,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НоменклатурнаяГруппа
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидРасходовНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ТаблицаТовары.ДокументПартии КАК Партия
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|			И (Реквизиты.ТаможенныйСбор <> 0)
	|ГДЕ
	|	&ПлательщикНДФЛ
	|	И Реквизиты.ТаможенныйСбор <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ФактурнаяСтоимость КАК ФактурнаяСтоимость,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК Сумма,
	|	0 КАК НДС,
	|	0 КАК Количество,
	|	ТаблицаТовары.СчетУчетаНУ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НоменклатурнаяГруппа
	|	КОНЕЦ КАК Поле4,
	|	ТаблицаТовары.СтатьяЗатратНУ КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидРасходовНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		КОГДА ТаблицаТовары.СтатьяЗатратНУ ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СтатьяЗатратНУ КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ТаблицаТовары.ДокументПартии КАК Партия
	|ИЗ
	|	СуммыПоТоварам КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаТовары.Ссылка)
	|			И (Реквизиты.ТаможенныйСбор <> 0)
	|ГДЕ
	|	&ПлательщикНДФЛ
	|	И Реквизиты.ТаможенныйСборВал <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	"""" КАК ИмяСписка,
	|	"""" КАК СинонимСписка,
	|	0 КАК НомерСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	Реквизиты.ТаможенныйШтраф КАК Сумма,
	|	0 КАК НДС,
	|	0 КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК СчетУчета,
	|	&ОсновнаяНоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Реквизиты.СтатьяПрочихДоходовРасходов КАК СтатьяЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК ВидРасходовНУ,
	|	Реквизиты.СтатьяПрочихДоходовРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|	Реквизиты.СтатьяПрочихДоходовРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК Партия
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	&ПлательщикНДФЛ
	|	И Реквизиты.ТаможенныйШтраф <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	"""" КАК ИмяСписка,
	|	"""" КАК СинонимСписка,
	|	0 КАК НомерСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	ВЫРАЗИТЬ(Реквизиты.ТаможенныйШтрафВал * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	0 КАК НДС,
	|	0 КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК СчетУчета,
	|	&ОсновнаяНоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Реквизиты.СтатьяПрочихДоходовРасходов КАК СтатьяЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК ВидРасходовНУ,
	|	Реквизиты.СтатьяПрочихДоходовРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|	Реквизиты.СтатьяПрочихДоходовРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК Партия
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	&ПлательщикНДФЛ
	|	И Реквизиты.ТаможенныйШтрафВал <> 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Функция удаляет из таблицы взаиморасчетов сумму на оплату НДС
//
Функция ПодготовитьТаблицуВзаиморасчетовБезНДСИП(ТаблицаВзаиморасчетов, ТоварыНДСвРублях, ТоварыНДСвВалюте) Экспорт
	Перем ТаблицаВзаиморасчетовБезНДС;
	
	Если ТаблицаВзаиморасчетов = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаВзаиморасчетовБезНДС	= ТаблицаВзаиморасчетов.Скопировать();
	
	СуммаНДСРуб	= 0;
	Если НЕ ТоварыНДСвРублях = Неопределено Тогда
		СуммаНДСРуб	= СуммаНДСРуб + ТоварыНДСвРублях.Итог("СуммаНДСРуб");
	КонецЕсли;
	
	Если НЕ ТоварыНДСвВалюте = Неопределено Тогда
		СуммаНДСРуб	= СуммаНДСРуб + ТоварыНДСвВалюте.Итог("СуммаНДСРуб");
	КонецЕсли;
	
	Если СуммаНДСРуб = 0 Тогда
		Возврат ТаблицаВзаиморасчетовБезНДС;
	КонецЕсли;
	
	Для каждого СтрокаВзаиморасчетов Из ТаблицаВзаиморасчетовБезНДС Цикл
		
		Если СтрокаВзаиморасчетов.СуммаРуб = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СуммаОплаченногоНДС	= Мин(СуммаНДСРуб, СтрокаВзаиморасчетов.СуммаРуб);
		
		СтрокаВзаиморасчетов.СуммаРуб	= СтрокаВзаиморасчетов.СуммаРуб - СуммаОплаченногоНДС;
		СуммаНДСРуб	= СуммаНДСРуб - СуммаОплаченногоНДС;
		
		Если СуммаНДСРуб = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаВзаиморасчетовБезНДС;
	
КонецФункции

// Подготавливает таблицу для отражения поступления расходов УСН из переданной таблицы с распределенными по номенклатуре суммами платежей
// Фильтрует строки движений с нулевыми значениями ресурсов
// Параметры:
//  ТаблицаСуммРасходов  -  <Массив> - массив таблиц поступления расходов со структурой колонок,
// Возвращаемое значение:
// ТаблицаПоступленияРасходов  -  <ТаблицаЗначений>  -  таблица движений прихода по регистру "РасходыПриУСН"
Функция ПодготовитьТаблицуПоступленияРасходовУСН(ТаблицаСуммРасходов) Экспорт

	Если ТаблицаСуммРасходов = Неопределено ИЛИ ТаблицаСуммРасходов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаПоступленияРасходов = РегистрыНакопления.РасходыПриУСН.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаПоступленияРасходов.Колонки.Добавить("ЭтоТовар",		Новый ОписаниеТипов("Булево"));
	ТаблицаПоступленияРасходов.Колонки.Добавить("ЭтоМатериал",	Новый ОписаниеТипов("Булево"));
	
	Для каждого СтрокаРасхода Из ТаблицаСуммРасходов Цикл
		
		СтрокаРасхода.Сумма = СтрокаРасхода.Сбор + СтрокаРасхода.Пошлина + СтрокаРасхода.НДС;
		
		Если СтрокаРасхода.Сумма = 0 И СтрокаРасхода.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаПриход = ТаблицаПоступленияРасходов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПриход, СтрокаРасхода);
		
	КонецЦикла;
	
	Возврат ТаблицаПоступленияРасходов;
	
КонецФункции

// ОТЛОЖЕННОЕ ПРОВЕДЕНИЕ

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение
		ИЛИ НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период) Тогда
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеквизитыАвансов.Контрагент КАК Контрагент,
	|	РеквизитыАвансов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеквизитыАвансов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	РеквизитыАвансов.ВидДоговора КАК ВидДоговора,
	|	РеквизитыАвансов.Период КАК Дата
	|ИЗ
	|	РеквизитыАвансов КАК РеквизитыАвансов
	|ГДЕ
	|	РеквизитыАвансов.СуммаВзаиморасчетов <> 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период)
		Или Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ТаблицаОприходованныеТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТаблицаОприходованныеТовары", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыДокумента.СчетУчета КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчетСписания
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ОбработкаОтложенногоПроведения(Параметры, Отказ) Экспорт
	
	ПараметрыПроведения = ПодготовитьПараметрыПроведения(
		Параметры.Регистратор,
		Отказ,
		Параметры.ДоговорКонтрагента);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	

	ТаблицаВзаиморасчетов = УчетВзаиморасчетовОтложенноеПроведение.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		Параметры,
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ЗачетАвансовТаблицаАвансов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияЗачетАвансов(
		Параметры,
		ТаблицаВзаиморасчетов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""ГТД по импорту""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБНОВЛЕНИЯ

Процедура ЗаполнитьОтражениеВУСНАктуальныхДокументов() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГТДИмпортТовары.Ссылка
	|ИЗ
	|	Документ.ГТДИмпорт.Товары КАК ГТДИмпортТовары
	|ГДЕ
	|	ГТДИмпортТовары.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|	И ГТДИмпортТовары.Ссылка.Дата >= &НачалоАктуальногоПериода"
	;
	
	Запрос.УстановитьПараметр("НачалоАктуальногоПериода", '20140101');
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для каждого СтрокаТовар Из ДокументОбъект.Товары Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТовар.ОтражениеВУСН) Тогда
				СтрокаТовар.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		Исключение
			ШаблонСообщения = НСтр("ru = 'Не удалось заполнить колонку ""Расходы (НУ)"" по причине:
                                   |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, 
				Метаданные.Документы.ГТДИмпорт,
				Выборка.Ссылка, 
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьОтражениеВУСНДокументовПрошлыхЛет(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	ГТДИмпортТовары.Ссылка
	|ИЗ
	|	Документ.ГТДИмпорт.Товары КАК ГТДИмпортТовары
	|ГДЕ
	|	ГТДИмпортТовары.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)"
	;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ГТДИмпорт");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если документ ранее был удален или обработан другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			
			ЗаписыватьДокумент = Ложь;
			
			Для каждого СтрокаТовар Из ДокументОбъект.Товары Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТовар.ОтражениеВУСН) Тогда
					СтрокаТовар.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
					ЗаписыватьДокумент = Истина;
				КонецЕсли;
			КонецЦикла;
			
			// Если колонку уже заполнили пользователи, пропускаем документ.
			Если НЕ ЗаписыватьДокумент Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось заполнить колонку ""Расходы (НУ)"" по причине:
					|%1'"), 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.ГТДИмпорт, 
				Выборка.Регистратор, 
				ТекстСообщения);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		
		Параметры.ОбработкаЗавершена = Истина;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Ложь;

		Если ОбъектовОбработано = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедуре Документы.ГТДИмпорт.ЗаполнитьОтражениеВУСНДокументовПрошлыхЛет
					|не удалось заполнить колонку ""Расходы (НУ)"" в %1 документах.'"),
					ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
		Иначе
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Информация,
				Метаданные.Документы.ГТДИмпорт,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедура Документы.ГТДИмпорт.ЗаполнитьОтражениеВУСНДокументовПрошлыхЛет
						|обработала очередную порцию данных: %1 документов'"), 
						ОбъектовОбработано));
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Заполняет новый реквизит "СпособЗачетаАвансов"
//
Процедура ЗаполнитьРеквизитСпособЗачетаАвансов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГТДИмпорт.Ссылка,
	|	ГТДИмпорт.СпособЗачетаАвансов
	|ИЗ
	|	Документ.ГТДИмпорт КАК ГТДИмпорт
	|ГДЕ
	|	ГТДИмпорт.СпособЗачетаАвансов = ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.Автоматически;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли