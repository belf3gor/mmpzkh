
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ОбновлениеВерсииИБ

// Регистрирует данные для обработчика обновления
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	
	Данные = Новый Массив;
	
	// Заполнение истории обработки.
	Данные_ЗаполнитьИсториюОбработки = ДанныеКОбработке_ЗаполнитьИсториюОбработки();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Данные, Данные_ЗаполнитьИсториюОбработки, Истина);
	// Заполнение договора контрагента.
	Данные_ЗаполнитьДоговорКонтрагента = ДанныеКОбработке_ЗаполнитьДоговорКонтрагента();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Данные, Данные_ЗаполнитьДоговорКонтрагента, Истина);
	// Заполнение номера документа.
	Данные_ЗаполнитьНомерДокумента = ДанныеКОбработке_ЗаполнитьНомерДокумента();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Данные, Данные_ЗаполнитьНомерДокумента, Истина);
	// Переход на новую архитектуру настроек ЭДО.
	Данные_НоваяАрхитектураНастроекЭДО = ДанныеКОбработке_НоваяАрхитектураНастроекЭДО();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Данные, Данные_НоваяАрхитектураНастроекЭДО, Истина);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИнициализироватьПараметрыОбработкиДляПереходаНаНовуюВерсию(Параметры);
	
	МетаданныеОбъекта = Метаданные.Документы.ЭлектронныйДокументВходящий;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПараметрыОтметкиВыполнения = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	ПараметрыВыборки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ПараметрыВыборки.ДополнительныеИсточникиДанных.Вставить("УдалитьНастройкаЭДО");
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, ПараметрыВыборки);
	
	НаборСсылок = Новый Массив;
	Пока Выборка.Следующий() Цикл
		НаборСсылок.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	ИсторияОбработки = ИсторияОбработкиДокументов(НаборСсылок);
	Договоры = ДоговорыДокументов(НаборСсылок);
	Реквизиты = РеквизитыДокументов(НаборСсылок);
	
	Для каждого СсылкаНаОбъект Из НаборСсылок Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаНаОбъект);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Записать = Ложь;
			
			Объект = СсылкаНаОбъект.ПолучитьОбъект();
			Если Объект <> Неопределено Тогда
				ОбработатьДанные_ЗаполнитьИсториюОбработки(Объект, ИсторияОбработки, Записать);
				ОбработатьДанные_ЗаполнитьДоговорКонтрагента(Объект, Договоры, Записать);
				ОбработатьДанные_ЗаполнитьНомерДокумента(Объект, Записать);
				ОбработатьДанные_ЗаполнитьКонтрагента(Объект, Реквизиты, Записать);
				ОбработатьДанные_НоваяАрхитектураНастроекЭДО(Объект, Реквизиты, Записать);
			КонецЕсли;
			
			Если Записать Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СсылкаНаОбъект, ПараметрыОтметкиВыполнения);
			КонецЕсли;
			
			ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ШаблонСообщения = НСтр("ru = 'Не удалось обработать входящий электронный документ: %1 по причине:'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, СсылкаНаОбъект) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, СсылкаНаОбъект, ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработанныхОбъектов = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать некоторые входящие электронные документы (пропущены): %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Обработана очередная порция входящих электронных документов: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбработанныхОбъектов);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,, ТекстСообщения);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов  = Параметры.ПрогрессВыполнения.ОбработаноОбъектов  + ОбработанныхОбъектов;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ОбновлениеВерсииИБ

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обновление

Процедура ИнициализироватьПараметрыОбработкиДляПереходаНаНовуюВерсию(Параметры)
	
	// Определим общее количество объектов к обработке.
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		ПараметрыВыборки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
		ПараметрыВыборки.ВыбиратьПорциями = Ложь;
		Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Документ.ЭлектронныйДокументВходящий", ПараметрыВыборки);
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеКОбработке_ЗаполнитьИсториюОбработки() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭлектронныйДокументВходящий.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий КАК ЭлектронныйДокументВходящий
	|ГДЕ
	|	ЭлектронныйДокументВходящий.ДатаПолучения = &ПустаяДата
	|	И ЭлектронныйДокументВходящий.ДатаПодписания = &ПустаяДата
	|	И ЭлектронныйДокументВходящий.ДатаАннулирования = &ПустаяДата
	|	И НЕ ЭлектронныйДокументВходящий.СостояниеЭДО В (&СостоянияЗавершения)";
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1, 1, 1));
	СостоянияЗавершения = Новый Массив;
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.Аннулирован);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ПолученОтказОтКонтрагента);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершен);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением);
	Запрос.УстановитьПараметр("СостоянияЗавершения", СостоянияЗавершения);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеКОбработке_ЗаполнитьДоговорКонтрагента() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументВходящий.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий КАК ЭлектронныйДокументВходящий
	|ГДЕ
	|	НЕ ЭлектронныйДокументВходящий.СостояниеЭДО В (&СостоянияЗавершения)
	|	И ЭлектронныйДокументВходящий.ДоговорКонтрагента = &ПустойДоговорКонтрагента
	|	И ЭлектронныйДокументВходящий.УдалитьНастройкаЭДО.ДоговорКонтрагента <> &ПустойДоговорКонтрагента";
	СостоянияЗавершения = Новый Массив;
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.Аннулирован);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ПолученОтказОтКонтрагента);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершен);
	СостоянияЗавершения.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением);
	Запрос.УстановитьПараметр("СостоянияЗавершения", СостоянияЗавершения);
	ПустойДоговорКонтрагента = Метаданные.ОпределяемыеТипы.ДоговорСКонтрагентом.Тип.ПривестиЗначение();
	Запрос.УстановитьПараметр("ПустойДоговорКонтрагента", ПустойДоговорКонтрагента);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеКОбработке_ЗаполнитьНомерДокумента() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументВходящий.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий КАК ЭлектронныйДокументВходящий
	|ГДЕ
	|	ЭлектронныйДокументВходящий.НомерДокумента = """"
	|	И ЭлектронныйДокументВходящий.СостояниеЭДО = &НаУтверждении";
	Запрос.Параметры.Вставить("НаУтверждении", Перечисления.СостоянияВерсийЭД.НаУтверждении);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеКОбработке_НоваяАрхитектураНастроекЭДО() 
	
	// Заполнение новых реквизитов: ИдентификаторОрганизации, ИдентификаторКонтрагента, СпособОбменаЭД
	// для документов с незавершенным ЭДО.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументВходящий.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий КАК ЭлектронныйДокументВходящий
	|ГДЕ
	|	НЕ ЭлектронныйДокументВходящий.СостояниеЭДО В (&СостоянияЗавершен)
	|	И (ЭлектронныйДокументВходящий.ИдентификаторОрганизации = """"
	|			ИЛИ ЭлектронныйДокументВходящий.ИдентификаторКонтрагента = """"
	|			ИЛИ ЭлектронныйДокументВходящий.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ПустаяСсылка))";
	СостоянияЗавершен = Новый Массив;
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно);
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершен);
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением);
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.Аннулирован);
	Запрос.УстановитьПараметр("СостоянияЗавершен", СостоянияЗавершен);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработатьДанные_ЗаполнитьИсториюОбработки(Объект, ИсторияОбработки, Записать)
	
	СтрокаИсторииОбработки = ИсторияОбработки.Найти(Объект.Ссылка, "ЭлектронныйДокумент");
	Если СтрокаИсторииОбработки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДатаПолучения)
		ИЛИ ЗначениеЗаполнено(Объект.ДатаПодписания)
		ИЛИ ЗначениеЗаполнено(Объект.ДатаАннулирования) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНеизвестна = Дата(3000, 1, 1);
	
	Если Не ЗначениеЗаполнено(Объект.ДатаПолучения)
		И СтрокаИсторииОбработки.ДатаПолучения <> ДатаНеизвестна Тогда
		Объект.ДатаПолучения = СтрокаИсторииОбработки.ДатаПолучения;
		Записать = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаПодписания)
		И СтрокаИсторииОбработки.ДатаПодписания <> ДатаНеизвестна Тогда
		Объект.ДатаПодписания = СтрокаИсторииОбработки.ДатаПодписания;
		Записать = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаАннулирования)
		И СтрокаИсторииОбработки.ДатаАннулирования <> ДатаНеизвестна Тогда
		Объект.ДатаАннулирования = СтрокаИсторииОбработки.ДатаАннулирования;
		Записать = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанные_ЗаполнитьДоговорКонтрагента(Объект, Договоры, Записать)
	
	СтрокаДоговора = Договоры.Найти(Объект.Ссылка, "ЭлектронныйДокумент");
	Если СтрокаДоговора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаДоговора.ДоговорКонтрагента)
		И Не ЗначениеЗаполнено(Объект.ДоговорКонтрагента)
		И Объект.ДоговорКонтрагента <> СтрокаДоговора.ДоговорКонтрагента Тогда
		
		Объект.ДоговорКонтрагента = СтрокаДоговора.ДоговорКонтрагента;
		Записать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанные_ЗаполнитьНомерДокумента(Объект, Записать)
	
	Если Не ЗначениеЗаполнено(Объект.НомерДокумента)
		И Объект.СостояниеЭДО = Перечисления.СостоянияВерсийЭД.НаУтверждении Тогда
		
		НомерДокумента = Объект.НомерДокументаОтправителя;
		ПозицияИсправления = СтрНайти(НомерДокумента, " " + НСтр("ru = '(испр.'") + " ");
		Если ЗначениеЗаполнено(ПозицияИсправления) Тогда
			НомерДокумента = Лев(НомерДокумента, ПозицияИсправления - 1);
		КонецЕсли;
		
		Объект.НомерДокумента = НомерДокумента;
		
		Записать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанные_ЗаполнитьКонтрагента(Объект, Реквизиты, Записать)
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаРеквизитов = Реквизиты.Найти(Объект.Ссылка, "ЭлектронныйДокумент");
	Если СтрокаРеквизитов = Неопределено 
		ИЛИ Не ЗначениеЗаполнено(СтрокаРеквизитов.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Контрагент = СтрокаРеквизитов.Контрагент;
	Записать = Истина;
	
КонецПроцедуры

Процедура ОбработатьДанные_НоваяАрхитектураНастроекЭДО(Объект, Реквизиты, Записать)
	
	СтрокаРеквизитов = Реквизиты.Найти(Объект.Ссылка, "ЭлектронныйДокумент");
	Если СтрокаРеквизитов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СостоянияЗавершен = Новый Массив;
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно);
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершен);
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением);
	СостоянияЗавершен.Добавить(Перечисления.СостоянияВерсийЭД.Аннулирован);
	
	Если СостоянияЗавершен.Найти(Объект.СостояниеЭДО) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ИдентификаторОрганизации)
		И ЗначениеЗаполнено(СтрокаРеквизитов.ИдентификаторОрганизации) Тогда
		
		Объект.ИдентификаторОрганизации = СтрокаРеквизитов.ИдентификаторОрганизации;
		Записать = Истина;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ИдентификаторКонтрагента)
		И ЗначениеЗаполнено(СтрокаРеквизитов.ИдентификаторКонтрагента) Тогда
		
		Объект.ИдентификаторКонтрагента = СтрокаРеквизитов.ИдентификаторКонтрагента;
		Записать = Истина;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.СпособОбменаЭД)
		И ЗначениеЗаполнено(СтрокаРеквизитов.СпособОбменаЭД) Тогда
		
		Объект.СпособОбменаЭД = СтрокаРеквизитов.СпособОбменаЭД;
		Записать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИсторияОбработкиДокументов(Знач НаборЭлектронныхДокументов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЖурналСобытийЭД.ВладелецЭД КАК ЭлектронныйДокумент,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ЖурналСобытийЭД.СтатусЭД В (&СтатусыПолучен)
	|				ТОГДА ЖурналСобытийЭД.Дата
	|			ИНАЧЕ ДАТАВРЕМЯ(3000, 1, 1)
	|		КОНЕЦ) КАК ДатаПолучения,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ЖурналСобытийЭД.СтатусЭД В (&СтатусыПодписан)
	|				ТОГДА ЖурналСобытийЭД.Дата
	|			ИНАЧЕ ДАТАВРЕМЯ(3000, 1, 1)
	|		КОНЕЦ) КАК ДатаПодписания,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ЖурналСобытийЭД.СтатусЭД В (&СтатусыАннулирован)
	|				ТОГДА ЖурналСобытийЭД.Дата
	|			ИНАЧЕ ДАТАВРЕМЯ(3000, 1, 1)
	|		КОНЕЦ) КАК ДатаАннулирования
	|ИЗ
	|	РегистрСведений.ЖурналСобытийЭД КАК ЖурналСобытийЭД
	|ГДЕ
	|	ЖурналСобытийЭД.ВладелецЭД В(&ЭлектронныеДокументы)
	|	И ЖурналСобытийЭД.ПрисоединенныйФайл.ТипЭлементаВерсииЭД В(&ТипыПервичныхЭД)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналСобытийЭД.ВладелецЭД";
	Запрос.УстановитьПараметр("ЭлектронныеДокументы", НаборЭлектронныхДокументов);
	СтатусыПолучен = Новый Массив;
	СтатусыПолучен.Добавить(Перечисления.СтатусыЭД.Получен);
	Запрос.УстановитьПараметр("СтатусыПолучен", СтатусыПолучен);
	СтатусыПодписан = Новый Массив;
	СтатусыПодписан.Добавить(Перечисления.СтатусыЭД.Подписан);
	СтатусыПодписан.Добавить(Перечисления.СтатусыЭД.ПолностьюПодписан);
	Запрос.УстановитьПараметр("СтатусыПодписан", СтатусыПодписан);
	СтатусыАннулирован = Новый Массив;
	СтатусыАннулирован.Добавить(Перечисления.СтатусыЭД.Аннулирован);
	Запрос.УстановитьПараметр("СтатусыАннулирован", СтатусыАннулирован);
	Запрос.УстановитьПараметр("ТипыПервичныхЭД", ОбменСКонтрагентамиСлужебный.ТипыПервичныхЭД());
	
	УстановитьПривилегированныйРежим(Истина);
	ИсторияОбработки = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	ИсторияОбработки.Индексы.Добавить("ЭлектронныйДокумент");
	
	Возврат ИсторияОбработки;
	
КонецФункции

Функция ДоговорыДокументов(Знач НаборЭлектронныхДокументов) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументВходящий.Ссылка КАК ЭлектронныйДокумент,
	|	ЭлектронныйДокументВходящий.УдалитьНастройкаЭДО.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий КАК ЭлектронныйДокументВходящий
	|ГДЕ
	|	ЭлектронныйДокументВходящий.Ссылка В(&ЭлектронныеДокументы)";
	Запрос.УстановитьПараметр("ЭлектронныеДокументы", НаборЭлектронныхДокументов);
	
	УстановитьПривилегированныйРежим(Истина);
	Договоры = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	Договоры.Индексы.Добавить("ЭлектронныйДокумент");
	
	Возврат Договоры;
	
КонецФункции

Функция РеквизитыДокументов(Знач НаборЭлектронныхДокументов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументВходящий.Ссылка КАК ЭлектронныйДокумент,
	|	ЭлектронныйДокументВходящий.УдалитьНастройкаЭДО.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ЭлектронныйДокументВходящий.УдалитьНастройкаЭДО.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ЭлектронныйДокументВходящий.УдалитьНастройкаЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ЭлектронныйДокументВходящий.УдалитьНастройкаЭДО.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий КАК ЭлектронныйДокументВходящий
	|ГДЕ
	|	ЭлектронныйДокументВходящий.Ссылка В(&НаборЭлектронныхДокументов)";
	Запрос.УстановитьПараметр("НаборЭлектронныхДокументов", НаборЭлектронныхДокументов);
	
	Реквизиты = Запрос.Выполнить().Выгрузить();
	Реквизиты.Индексы.Добавить("ЭлектронныйДокумент");
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

