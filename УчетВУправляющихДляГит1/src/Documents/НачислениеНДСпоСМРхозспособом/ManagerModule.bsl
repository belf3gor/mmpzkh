#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 22, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Процедура заполнения табличной части по данным подсистемы НДС
Процедура ЗаполнитьДокумент(ПараметрыДокумента) Экспорт
	
	Если НЕ УчетнаяПолитика.Существует(ПараметрыДокумента.Организация, ПараметрыДокумента.Дата) Тогда
		Возврат;
	КонецЕсли;
		
	ТаблицаРезультатов = ЗаполнитьСМРхозспособом(ПараметрыДокумента);
		
	ПараметрыДокумента.СМРхозспособом.Загрузить(ТаблицаРезультатов);
	
КонецПроцедуры

Функция ЗаполнитьСМРхозспособом(ПараметрыДокумента)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтроительствоХозспособом.Объект КАК ОбъектСтроительства,
	|	СУММА(СтроительствоХозспособом.Сумма) КАК СуммаБезНДС,
	|	&СтавкаНДС КАК СтавкаНДС,
	|	СУММА(СтроительствоХозспособом.Сумма * &СтавкаНДСЗначение / 100) КАК НДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОбороты.Субконто1 КАК Объект,
	|		ХозрасчетныйОбороты.СуммаОборотДт КАК Сумма
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				Период,
	|				Счет В (&СчетаУчетаСтроительства),
	|				&ВидыСубконто,
	|				Организация = &Организация
	|					И (Подразделение = &Подразделение
	|						ИЛИ Подразделение ЕСТЬ NULL )
	|					И Субконто2 = &ВидСтроительстваХозспособом,
	|				,
	|				) КАК ХозрасчетныйОбороты
	|	ГДЕ
	|		ХозрасчетныйОбороты.СуммаОборотДт > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОбороты.Субконто1,
	|		- ХозрасчетныйОбороты.СуммаОборотДт
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				Период,
	|				Счет В (&СчетаУчетаСтроительства),
	|				&ВидыСубконто,
	|				Организация = &Организация
	|					И (Подразделение = &Подразделение
	|						ИЛИ Подразделение ЕСТЬ NULL )
	|					И Субконто2 = &ВидСтроительстваХозспособом
	|					И КорСубконто2 = &ВидСтроительстваХозспособом,
	|				КорСчет В (&СчетаУчетаСтроительства),
	|				&ВидыСубконто) КАК ХозрасчетныйОбороты
	|	ГДЕ
	|		ХозрасчетныйОбороты.СуммаОборотДт > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НачислениеНДСпоСМРхозспособом.ОбъектСтроительства,
	|		- НачислениеНДСпоСМРхозспособом.СуммаБезНДС
	|	ИЗ
	|		Документ.НачислениеНДСпоСМРхозспособом.СМРхозспособом КАК НачислениеНДСпоСМРхозспособом
	|	ГДЕ
	|		НачислениеНДСпоСМРхозспособом.Ссылка.Организация = &Организация
	|		И НачислениеНДСпоСМРхозспособом.Ссылка.Проведен
	|		И НачислениеНДСпоСМРхозспособом.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И НачислениеНДСпоСМРхозспособом.Ссылка <> &ТекущийДокумент
	|		И НачислениеНДСпоСМРхозспособом.Ссылка.ПодразделениеОрганизации = &Подразделение) КАК СтроительствоХозспособом
	|
	|СГРУППИРОВАТЬ ПО
	|	СтроительствоХозспособом.Объект
	|
	|ИМЕЮЩИЕ
	|	СУММА(СтроительствоХозспособом.Сумма) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектСтроительства";
	
	БлижайшийНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		ПараметрыДокумента.Организация,
		ПараметрыДокумента.Дата,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал);
		
	Если БлижайшийНалоговыйПериод.Начало <> БлижайшийНалоговыйПериод.Период Тогда
		НачалоПериода = БлижайшийНалоговыйПериод.Начало;
	Иначе
		НачалоПериода = НачалоКвартала(ПараметрыДокумента.Дата);
	КонецЕсли; 
	
	КонецПериода = КонецКвартала(ПараметрыДокумента.Дата);
	
	ВидыСубконто = Новый Массив();
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СпособыСтроительства);
	
	СчетаУчетаСтроительства = 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств);
		
	СтавкаНДСПоУмолчанию = УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(ПараметрыДокумента.Дата);
	СтавкаНДСЗначение = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДСПоУмолчанию);
	
	Запрос.УстановитьПараметр("Организация",                 ПараметрыДокумента.Организация);
	Запрос.УстановитьПараметр("Подразделение",               ПараметрыДокумента.ПодразделениеОрганизации);
	Запрос.УстановитьПараметр("НачалоПериода",               НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",                КонецПериода);
	Запрос.УстановитьПараметр("СчетаУчетаСтроительства",     СчетаУчетаСтроительства);
	Запрос.УстановитьПараметр("ВидыСубконто",                ВидыСубконто);
	Запрос.УстановитьПараметр("ВидСтроительстваХозспособом", Перечисления.СпособыСтроительства.Хозспособ);
	Запрос.УстановитьПараметр("ВидЦенностиХозспособом",      Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	Запрос.УстановитьПараметр("ТекущийДокумент",             ПараметрыДокумента.Ссылка);
	Запрос.УстановитьПараметр("СтавкаНДС",                   СтавкаНДСПоУмолчанию);
	Запрос.УстановитьПараметр("СтавкаНДСЗначение",           СтавкаНДСЗначение);
	
	ТаблицаРезультатов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРезультатов;
	
КонецФункции 

Функция ТекстЗапросаДанныеДляПечатиСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("Реквизиты",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|					ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Поставщик,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация.ИНН
	|		ИНАЧЕ Реквизиты.Организация.ИНН
	|	КОНЕЦ КАК ИННпоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК ОбособленноеПодразделениеПоставщика,
	|	НЕОПРЕДЕЛЕНО КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Покупатель,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация.ИНН
	|		ИНАЧЕ Реквизиты.Организация.ИНН
	|	КОНЕЦ КАК ИННпокупателя,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК ОбособленноеПодразделениеПокупателя,
	|	НЕОПРЕДЕЛЕНО КАК Грузополучатель,
	|	ЛОЖЬ КАК РасчетыВУсловныхЕдиницах,
	|	НЕОПРЕДЕЛЕНО КАК ВидДоговора,
	|	НЕОПРЕДЕЛЕНО КАК Основание,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеНомер,
	|	НЕОПРЕДЕЛЕНО КАК Исполнитель,
	|	НЕОПРЕДЕЛЕНО КАК ИсполнительНаОсновании,
	|	НЕОПРЕДЕЛЕНО КАК ОтпускПроизвел,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьЧерезКого,
	|	НЕОПРЕДЕЛЕНО КАК ЗаЗаказчикаНаОсновании,
	|	ИСТИНА КАК ЕстьТовары,
	|	Константы.ВалютаРегламентированногоУчета КАК Валюта,
	|	Константы.ВалютаРегламентированногоУчета КАК ВалютаВзаиморасчетов,
	|	"""" КАК АдресДоставки,
	|	"""" КАК СведенияОТранспортировкеИГрузе,
	|	НЕОПРЕДЕЛЕНО КАК ОтветственныйЗаОформление,
	|	"""" КАК СопроводительныеДокументы,
	|	НЕОПРЕДЕЛЕНО КАК Перевозчик,
	|	ЛОЖЬ КАК ПеревозкаАвтотранспортом
	|ИЗ
	|	Документ.НачислениеНДСпоСМРхозспособом КАК Реквизиты,
	|	Константы КАК Константы
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	NULL КАК Товар,
	|	НЕОПРЕДЕЛЕНО КАК ТоварКод,
	|	НЕОПРЕДЕЛЕНО КАК ТоварАртикул,
	|	""Строительно-монтажные работы для собственного потребления"" КАК ТоварНаименование,
	|	NULL КАК СтранаПроисхождения,
	|	NULL КАК ПредставлениеСтраны,
	|	NULL КАК НомерГТД,
	|	NULL КАК ПредставлениеГТД,
	|	NULL КАК РегистрационныйНомерТД,
	|	NULL КАК ЕдиницаИзмерения,
	|	NULL КАК Количество,
	|	NULL КАК Цена,
	|	ТаблицаТовары.СуммаБезНДС КАК Сумма,
	|	ТаблицаТовары.НДС КАК СуммаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ЛОЖЬ КАК СуммаВключаетНДС,
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ЭтоКомиссия,
	|	ТаблицаТовары.СуммаБезНДС КАК СуммаБезНДСРуб,
	|	ТаблицаТовары.НДС КАК НДСРуб,
	|	ТаблицаТовары.СуммаБезНДС + ТаблицаТовары.НДС КАК ВсегоРуб,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК КонтрагентСводныйСФ,
	|	НЕОПРЕДЕЛЕНО КАК ПериодичностьУслуги,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка) КАК ТоварКодТНВЭД
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.НачислениеНДСпоСМРхозспособом.СМРхозспособом КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", 				  ДокументСсылка);
	Запрос.УстановитьПараметр("ВидЦенности", 			  Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	Запрос.УстановитьПараметр("ВидНачисления", 			  Перечисления.НДСВидНачисления.НДСНачисленКУплате);
	Запрос.УстановитьПараметр("Событие", 			  	  Перечисления.СобытияПоНДСПродажи.НДСНачисленКУплате);
	Запрос.УстановитьПараметр("СобытиеПокупки", 		  Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком);
	Запрос.УстановитьПараметр("СчетУчетаНДС", 			  ПланыСчетов.Хозрасчетный.НДСприСтроительствеОсновныхСредств);
	Запрос.УстановитьПараметр("СчетУчетаНДСПоРеализации", ПланыСчетов.Хозрасчетный.НДСприСтроительствеОсновныхСредств);
		
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
				 + ТекстЗапросаСМРхозспособом(НомераТаблиц)
				 + ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц);
    	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Реквизиты.УпрощенныйУчетНДС = УчетнаяПолитика.УпрощенныйУчетНДС(Реквизиты.Организация, Реквизиты.Период);
	Реквизиты.РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизиты", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации,
	|	&СчетУчетаНДС КАК СчетУчетаНДС,
	|	Неопределено КАК УпрощенныйУчетНДС,
	|	Неопределено КАК РаздельныйУчетНДСНаСчете19
	|ИЗ
	|	Документ.НачислениеНДСпоСМРхозспособом КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаСМРхозспособом(НомераТаблиц)

	НомераТаблиц.Вставить("СМРХозспособом", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаСМРхозспособом.Ссылка КАК Регистратор,
	|	ТаблицаСМРхозспособом.ОбъектСтроительства КАК Объект,
	|	ТаблицаСМРхозспособом.СуммаБезНДС,
	|	ТаблицаСМРхозспособом.СтавкаНДС,
	|	ТаблицаСМРхозспособом.НДС,
	|	ТаблицаСМРхозспособом.Ссылка КАК СчетФактура,
	|	&ВидЦенности КАК ВидЦенности,
	|	&Событие КАК Событие,
	|	&СобытиеПокупки КАК СобытиеПокупки,
	|	&ВидНачисления КАК ВидНачисления,
	|	ТаблицаСМРхозспособом.Ссылка.Дата КАК ДатаСобытия,
	|	&СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаСМРхозспособом.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.НачислениеНДСпоСМРхозспособом.СМРхозспособом КАК ТаблицаСМРхозспособом
	|ГДЕ
	|	ТаблицаСМРхозспособом.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц)

	НомераТаблиц.Вставить("ДанныеРегламентнойОперации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.НачислениеНДСпоСМРхозспособом) КАК РегламентнаяОперация
	|ИЗ
	|	Документ.НачислениеНДСпоСМРхозспособом КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиСчетовФактур";
	
КонецПроцедуры
#КонецЕсли