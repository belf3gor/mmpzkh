#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если НЕ УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Организация, Дата) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Таблица1.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица1.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица15.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица15.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица12.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица12.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица2.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица2.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица4.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица4.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица5.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица5.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица61.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица61.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица6.ХарактерДеятельности");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица63.НоменклатурнаяГруппа");
		МассивНепроверяемыхРеквизитов.Добавить("Таблица63.ХарактерДеятельности");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата          = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПараметрыПроведения = Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПМПЗ(
		ПараметрыПроведения.ТаблицаРаздела1_МПЗ, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПИныеМатериальныеРасходы(
		ПараметрыПроведения.ТаблицаРаздела1_ИМР, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПМПЗОбороты(
		ПараметрыПроведения.ТаблицаРаздела1_5, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПДоходы(
		ПараметрыПроведения.ТаблицаРаздела1_2, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПМПЗ(
		ПараметрыПроведения.ТаблицаРаздела2_ВНА, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИППрочиеРасходы(
		ПараметрыПроведения.ТаблицаРаздела2_Амортизация, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПМПЗ(
		ПараметрыПроведения.ТаблицаРаздела4_ВНА, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИППрочиеРасходы(
		ПараметрыПроведения.ТаблицаРаздела4_Амортизация, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПРасходыНаОплатуТруда(
		ПараметрыПроведения.ТаблицаРаздела5, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИППрочиеДоходы(
		ПараметрыПроведения.ТаблицаРаздела61, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИППрочиеРасходы(
		ПараметрыПроведения.ТаблицаРаздела6, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	Документы.ЗаписьКнигиУчетаДоходовИРасходовИП.СформироватьДвиженияПоИПРБП(
		ПараметрыПроведения.ТаблицаРаздела63, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли