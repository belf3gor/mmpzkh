
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	РезультатПроверок = ПроверкаПеревыставления(ПараметрКоманды);
	
	Если НЕ РезультатПроверок.МожноПеревыставить Тогда 
		Если НЕ РезультатПроверок.КорректныйВидСФ Тогда 
			ТекстСообщения = НСтр("ru='Счет-фактура комитенту выставляется на основании счета-фактуры с видом ""Налоговый агент""'");
			ВызватьИсключение ТекстСообщения;
		ИначеЕсли НЕ РезультатПроверок.КорректныйПериод Тогда 
			ТекстСообщения = НСтр("ru='До 01.10.2017 нельзя составить счет-фактуру комитенту'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОткрытияДокумента = УчетНДСКлиент.ПараметрыСчетаФактурыНалоговогоАгентаКомитенту(ПараметрКоманды);
	
	ОткрытьФорму("Документ.СчетФактураВыданный.Форма.ФормаДокументаНалоговыйАгент", 
		ПараметрыОткрытияДокумента.ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

&НаСервере
Функция ПроверкаПеревыставления(СчетФактура)
	
	СтруктураПроверок = Новый Структура("МожноПеревыставить, КорректныйВидСФ, КорректныйПериод");
	
	РеквизитыСФ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура, "Дата, ВидСчетаФактуры");
	СтруктураПроверок.КорректныйВидСФ    = РеквизитыСФ.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент;
	СтруктураПроверок.КорректныйПериод   = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(РеквизитыСФ.Дата) >= 4;
	СтруктураПроверок.МожноПеревыставить = СтруктураПроверок.КорректныйВидСФ И СтруктураПроверок.КорректныйПериод;
	
	Возврат СтруктураПроверок;
	
КонецФункции
