#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 21, 0);
	
КонецФункции

// Проверяет есть ли в измененных пользователем макетах счета-фактуры, УПД, УКД
// область "ИдентификаторГосКонтракта". Если область отсутствует, то пользователя необходимо
// предупредить о необходимости перенести свои изменения в новый макет.
//
// Возвращаемое значение:
//  Булево - признак того, что есть измененные макеты без области "ИдентификаторГосКонтракта"
//           Истина - есть измененные макеты
//           Ложь   - нет измененных макетов или изменен макет с областью "ИдентификаторГосКонтракта"

Функция ЕстьИзмененныйМакет1137() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПользовательскиеМакетыПечати.ИмяМакета,
	|	ПользовательскиеМакетыПечати.Объект,
	|	ПользовательскиеМакетыПечати.Макет
	|ИЗ
	|	РегистрСведений.ПользовательскиеМакетыПечати КАК ПользовательскиеМакетыПечати
	|ГДЕ
	|	(ПользовательскиеМакетыПечати.Объект = ""ОбщийМакет""
	|				И (ПользовательскиеМакетыПечати.ИмяМакета = ""ПФ_MXL_СчетФактура1137""
	|					ИЛИ ПользовательскиеМакетыПечати.ИмяМакета = ""ПФ_MXL_КорректировочныйСчетФактура952"")
	|			ИЛИ ПользовательскиеМакетыПечати.Объект = ""Обработка.ПечатьУКД""
	|				И ПользовательскиеМакетыПечати.ИмяМакета = ""ПФ_MXL_УниверсальныйКорректировочныйДокумент""
	|			ИЛИ ПользовательскиеМакетыПечати.Объект = ""Обработка.ПечатьУПД""
	|				И ПользовательскиеМакетыПечати.ИмяМакета = ""ПФ_MXL_УниверсальныйПередаточныйДокумент"")";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Макет = Выборка.Макет.Получить();
		Если ТипЗнч(Макет) <> Тип("ТабличныйДокумент")
			ИЛИ ЗначениеЗаполнено(Макет.Области.Найти("ИдентификаторГосКонтракта")) Тогда 
			Продолжить;
		КонецЕсли;
		Возврат Истина;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|	ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования ПО ЭтотСписок.Ссылка = СчетФактураВыданныйДокументыОснования.Ссылка
	|
	|;
	|РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(ЭтотСписок.Организация)
	|И ЧтениеСпискаРазрешено(СчетФактураВыданныйДокументыОснования.ДокументОснование)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(ЭтотСписок.Организация)
	|  И ИзменениеСпискаРазрешено(СчетФактураВыданныйДокументыОснования.ДокументОснование)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

// Открываемая форма документа определяется в зависимости от указанного вида счета-фактуры
//
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка"
		ИЛИ ВидФормы = "ФормаВыбора" Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВидСчетаФактуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ВидСчетаФактуры");
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") 
		И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура")
		И Параметры.ЗначенияЗаполнения.Свойство("ВидСчетаФактуры") Тогда 
		ВидСчетаФактуры = Параметры.ЗначенияЗаполнения.ВидСчетаФактуры;
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") 
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
		И ТипЗнч(Параметры.ЗначениеКопирования) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда 
		ВидСчетаФактуры = Параметры.ЗначениеКопирования.ВидСчетаФактуры;
	ИначеЕсли Параметры.Свойство("Основание") 
		И ЗначениеЗаполнено(Параметры.Основание) Тогда
		
		ВидСчетаФактуры = УчетНДСПереопределяемый.ОпределитьВидСчетаФактурыВыданногоПоТипуОснования(Параметры.Основание);
		
		Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс 
			И НЕ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.СчетФактураВыданный")
			И УчетНДС.ПравоРегистрироватьВидСчетаФактуры(Перечисления.ВидСчетаФактурыВыставленного.НаАванс)
			И УчетНДС.ПравоРегистрироватьВидСчетаФактуры(Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу) Тогда
			
			Отказ = Ложь;
			
			ПараметрыЗаполнения = Документы.СчетФактураВыданный.ПодготовитьПараметрыЗаполненияАванс(
				Параметры.Основание, Неопределено, Неопределено, Отказ);
			
			Если Отказ Тогда
				
				Отказ = Ложь;
				ПараметрыЗаполнения = Документы.СчетФактураВыданный.ПодготовитьПараметрыЗаполненияСуммовыеРазницы(
					Параметры.Основание, Неопределено, Отказ);
				
				Если НЕ Отказ Тогда
					ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		ВидСчетаФактуры = Неопределено;
	КонецЕсли;
		
	СтандартнаяОбработка = Ложь;
	ФормыСчетовФактур = ПолучитьСоответствиеВидовСчетаФактурыФормам();
	ВыбраннаяФорма = ФормыСчетовФактур[ВидСчетаФактуры];
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "СчетФактура";
	КомандаОтправки.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаОтправки.Обработчик    = "ОтправкаПочтовыхСообщений.ОтправитьСчетФактураВыданный";
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьУПД) Тогда
		КомандаОтправки = КомандыОтправки.Добавить();
		КомандаОтправки.Идентификатор = "УниверсальныйПередаточныйДокумент";
		КомандаОтправки.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)'");
		КомандаОтправки.Обработчик    = "ОтправкаПочтовыхСообщений.ОтправитьУПД";
	КонецЕсли;
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьУКД) Тогда
		КомандаОтправки = КомандыОтправки.Добавить();
		КомандаОтправки.Идентификатор = "УниверсальныйКорректировочныйДокумент";
		КомандаОтправки.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД)'");
		КомандаОтправки.Обработчик    = "ОтправкаПочтовыхСообщений.ОтправитьУКД";
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("УдалитьУчитыватьИсправлениеИсходногоДокумента");
	МассивРеквизитов.Добавить("УдалитьНомерИсправленияИсходногоДокумента");
	МассивРеквизитов.Добавить("УдалитьДатаИсправленияИсходногоДокумента");
	
	Возврат МассивРеквизитов;
	
КонецФункции

Функция СоздатьДокументНаОсновании(Основание) Экспорт
	
	СчетФактура = Документы.СчетФактураВыданный.СоздатьДокумент();
	
	СчетФактура.Заполнить(Основание);
	
	РежимЗаписи = ?(Основание.Проведен, 
		РежимЗаписиДокумента.Проведение, 
		РежимЗаписиДокумента.Запись);
		
	СчетФактура.Записать(РежимЗаписи);
	
	Возврат СчетФактура.Ссылка;
	
КонецФункции

Функция ПолучитьСоответствиеВидовСчетаФактурыФормам(ОткрыватьДляПодбора = Ложь) Экспорт

	ФормыПоВидам = Новый Соответствие;
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию,              "ФормаДокументаНаРеализацию");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаАванс,                   "ФормаДокументаНаАванс");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента,          "ФормаДокументаНаАванс");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу,         "ФормаДокументаНаСуммовуюРазницу");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент,            "ФормаДокументаНалоговыйАгент");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку, "ФормаДокументаНаАванс");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс,   "ФормаДокументаНаАванс");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный, 
		?(ОткрыватьДляПодбора, "ФормаПодбора", "ФормаДокументаКорректировочный"));
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка, "ФормаДокументаСводнаяСправка");
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.КорректировочнаяСправка,
		?(ОткрыватьДляПодбора, "ФормаПодбора", "ФормаДокументаКорректировочнаяСправка"));
	
	// Исправление счета-фактуры
	ФормыПоВидам.Вставить(Перечисления.ВидСчетаФактурыВыставленного.ПустаяСсылка(), "ФормаПодбора");
	
	Возврат ФормыПоВидам;

КонецФункции

// Получаем порядок исправления счетов-фактур, 
//устанавливаем значение Истина для счетов-фактур, требующих создания корректировки
Функция ПолучитьСоответствиеВидовСчетаФактурыПорядкуИсправления() Экспорт

	ПорядокИсправления = Новый Соответствие;
	
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию,      Истина);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный,  Истина);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаАванс,           Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента,  Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу, Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент,    Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку, Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс,   Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка,    Ложь);
	ПорядокИсправления.Вставить(Перечисления.ВидСчетаФактурыВыставленного.КорректировочнаяСправка,   Ложь);

	Возврат ПорядокИсправления;

КонецФункции

Функция ПолучитьПредставлениеДокумента(ДокументСсылка, ВидСчетаФактуры) Экспорт
	
	ТекстПредставлениеКраткое  = "";
	ТекстПредставление         = "";
	ТекстОсновноеПредставление = "";
	
	Корректировочный = Ложь;
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Счет-фактура выданный на реализацию'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Счет-фактура выданный на аванс'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Счет-фактура на аванс комитента'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Счет-фактура на аванс комитента на закупку'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Корректировочный счет-фактура на аванс'");	
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Счет-фактура выданный на суммовую разницу'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Счет-фактура выданный налогового агента'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Корректировочный = Истина;
		ТекстОсновноеПредставление = НСтр("ru = 'Корректировочный счет-фактура выданный'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Сводная справка по розничным продажам'");
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.КорректировочнаяСправка Тогда
		ТекстОсновноеПредставление = НСтр("ru = 'Корректировочная справка по розничным продажам'");	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументСсылка) И ОбщегоНазначения.СсылкаСуществует(ДокументСсылка) Тогда
		РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка,
			"Дата, Номер, Исправление, НомерИсправления, НомерИсправляемогоКорректировочногоДокумента,
			| ДатаИсправляемогоКорректировочногоДокумента, НомерИсходногоДокумента, ДатаИсходногоДокумента");
		РеквизитыСчетаФактуры.Вставить("Корректировочный", Корректировочный);
		СтруктураПредставленияПоРеквизитам = ПолучитьПредставлениеПоРеквизитам(РеквизитыСчетаФактуры, ТекстОсновноеПредставление);
		ТекстПредставлениеКраткое = СтруктураПредставленияПоРеквизитам.ТекстПредставлениеКраткое;
		ТекстПредставление        = СтруктураПредставленияПоРеквизитам.ТекстПредставление;
			
	Иначе
		ТекстПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (создание)'"), ТекстОсновноеПредставление);
	КонецЕсли;
	
	СтруктураПредставления = Новый Структура;
	СтруктураПредставления.Вставить("СчетФактураКраткоеПредставление", ТекстПредставлениеКраткое);
	СтруктураПредставления.Вставить("СчетФактураПредставление",        ТекстПредставление);
	
	Возврат СтруктураПредставления;
	
КонецФункции

Функция ПолучитьПредставлениеПоРеквизитам(РеквизитыСчетаФактуры = Неопределено, ТекстОсновноеПредставление = "") Экспорт
	
	СтруктураПредставленияПоРеквизитам = Новый Структура;
	
	Если РеквизитыСчетаФактуры = Неопределено Тогда 
		ТекстПредставлениеКраткое = "";
		ТекстПредставление        = "";
	Иначе
		
		Если РеквизитыСчетаФактуры.Исправление Тогда
			
			Если РеквизитыСчетаФактуры.Корректировочный Тогда
				ТекстПредставлениеКраткое = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (испр. %2) от %3'"),
				РеквизитыСчетаФактуры.НомерИсправляемогоКорректировочногоДокумента, РеквизитыСчетаФактуры.НомерИсправления, Формат(РеквизитыСчетаФактуры.Дата,"ДЛФ=Д"));
				ТекстПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2'"), ТекстОсновноеПредставление, ТекстПредставлениеКраткое);
			Иначе
				ТекстПредставлениеКраткое = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (испр. %2) от %3'"),
				РеквизитыСчетаФактуры.НомерИсходногоДокумента, РеквизитыСчетаФактуры.НомерИсправления, Формат(РеквизитыСчетаФактуры.Дата,"ДЛФ=Д"));
				ТекстПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2'"), ТекстОсновноеПредставление, ТекстПредставлениеКраткое);
			КонецЕсли;
			
		Иначе
			ТекстПредставлениеКраткое = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыСчетаФактуры.Номер, Истина, Ложь), Формат(РеквизитыСчетаФактуры.Дата,"ДЛФ=Д"));
			ТекстПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2'"), ТекстОсновноеПредставление, ТекстПредставлениеКраткое);	
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураПредставленияПоРеквизитам.Вставить("ТекстПредставлениеКраткое", ТекстПредставлениеКраткое);
	СтруктураПредставленияПоРеквизитам.Вставить("ТекстПредставление",        ТекстПредставление);

	Возврат СтруктураПредставленияПоРеквизитам;
	
КонецФункции

// Счет-фактура на реализацию

// Возвращает реквизиты платежных документов, по которым зачтены предварительные оплаты при реализации ценностей.
//
// Параметры:
//  МассивОснований - Массив - документы реализации, по которым зачтены предварительные оплаты.
//  Контрагент - СправочникСсылка.Контрагенты - контрагент, по которому необходимо отфильтровать зачтенные оплаты.
//  ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов - договор, по которому необходимо отфильтровать 
//                       зачтенные оплаты.
// Отбор по контрагенту и договору необходим если документ реализации может содержать несколько покупателей 
// и договоров с ними (например, документ "Оказание услуг").
//
// Возвращаемое значение:
//  ТаблицаЗначений с колонками:
//    * НомерДокумента - Строка - номер платежного документа (платежного поручения или приходного кассового ордера).
//    * ДатаДокумента - Дата - дата платежного документа (платежного поручения или приходного кассового ордера).
//
Функция ДатыНомераПлатежноРасчетныхДокументов(МассивОснований, Контрагент = Неопределено, ДоговорКонтрагента = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МассивОснований", 
		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивОснований, Истина));

	МассивСчетовАвансов = Новый Массив();
	МассивСчетовАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученным); // 62.02
	МассивСчетовАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВал); // 62.22
	МассивСчетовАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымУЕ); // 62.32
	Запрос.УстановитьПараметр("МассивСчетовАвансов", МассивСчетовАвансов);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ВТ_СчетаАвансов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&МассивСчетовАвансов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Регистратор В (&МассивОснований)
	|				И СчетДт В
	|					(ВЫБРАТЬ
	|						ВТ_СчетаАвансов.Счет
	|					ИЗ
	|						ВТ_СчетаАвансов)
	|				И &УсловиеПоКонтрагентуИДоговору
	|				И ВидСубконтоДт3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)
	|				И (Субконто3 ССЫЛКА Документ.ПоступлениеНаРасчетныйСчет
	|					ИЛИ Субконто3 ССЫЛКА Документ.ПриходныйКассовыйОрдер),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ХозрасчетныйДвиженияССубконто.Организация = ДанныеПервичныхДокументов.Организация
	|			И ХозрасчетныйДвиженияССубконто.СубконтоДт3 = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ДанныеПервичныхДокументов.Номер <> """"
	|	И ДанныеПервичныхДокументов.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|	И НЕ ДанныеПервичныхДокументов.Документ ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	НомерДокумента";
	
	Если Контрагент <> Неопределено И ДоговорКонтрагента <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоКонтрагентуИДоговору", 
			"Субконто1 = &Контрагент И Субконто2 = &ДоговорКонтрагента ");
		Запрос.УстановитьПараметр("Контрагент",         Контрагент);
		Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоКонтрагентуИДоговору", "ИСТИНА");
	КонецЕсли; 
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьПривилегированныйРежим(Истина);
	ДатыНомера = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДатыНомера;
	
КонецФункции

// Счет-фактура на аванс

Функция НоменклатураСчетаНаОплату(СчетНаОплату) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетНаОплату", СчетНаОплату);
	
	НомераТаблиц = Новый Структура("ВТ_СчетаНаОплату", 0);
	
	ТекстЗапросаСчетаНаОплату =
	"ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Ссылка КАК СчетНаОплату
	|ПОМЕСТИТЬ СчетаНаОплату
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &СчетНаОплату
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату";
	
	Запрос.Текст = ТекстЗапросаСчетаНаОплату + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета()
		+ ТекстЗапросаНоменклатураСчетовНаОплату(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	НоменклатураСчета = Результат[НомераТаблиц.НоменклатураСчетовНаОплату].Выгрузить();
	Для каждого СтрокаНоменклатуры Из НоменклатураСчета Цикл
		// Изменяем ставки НДС на расчетные
		Если СтрокаНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
			СтрокаНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120;
		ИначеЕсли СтрокаНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
			СтрокаНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118;
		ИначеЕсли СтрокаНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
			СтрокаНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110;
		КонецЕсли;
	КонецЦикла;
	НоменклатураСчета.Сортировать("ДатаСчетаНаОплату,СчетНаОплату,НомерТабЧасти,НомерСтроки", Новый СравнениеЗначений);
	
	Возврат НоменклатураСчета;
	
КонецФункции

Функция РаспределитьСуммыАвансовПоНоменклатуреСчетовНаОплату(АвансыПоСчетамНаОплату, НоменклатураСчетовНаОплату, ВалютаРеглУчета) Экспорт

	НоменклатураСчетаФактуры = НоменклатураСчетовНаОплату.СкопироватьКолонки();
	НоменклатураСчетаФактуры.Колонки.Добавить("НДСИсчисляетсяНалоговымАгентом", Новый ОписаниеТипов("Булево"));
	
	КурсыВалют = Новый Соответствие; // Кэшируем значения курсов
	ОтборСтрокНоменклатуры   = Новый Структура("СчетНаОплату,СтавкаНДС");
	НоменклатураСчетовНаОплату.Индексы.Добавить("СчетНаОплату,СтавкаНДС");
	Для каждого СтрокаАванса Из АвансыПоСчетамНаОплату Цикл
		
		Если ЗначениеЗаполнено(СтрокаАванса.СчетНаОплату) Тогда
			ЗаполнитьЗначенияСвойств(ОтборСтрокНоменклатуры, СтрокаАванса);
			НоменклатураСчетаНаОплату = НоменклатураСчетовНаОплату.Скопировать(ОтборСтрокНоменклатуры);
			
			// Таблица АвансыПоСчетамНаОплату содержит только строки с расчетными ставками - 20/120, 18/118, 10/110
			// Добавляем строки с "обычными" ставками (20%, 18%, 10%)
			Если СтрокаАванса.СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
				ОтборСтрокНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС20;
			ИначеЕсли СтрокаАванса.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
				ОтборСтрокНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС18;
			ИначеЕсли СтрокаАванса.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
				ОтборСтрокНоменклатуры.СтавкаНДС = Перечисления.СтавкиНДС.НДС10;
			КонецЕсли;
			Если СтрокаАванса.СтавкаНДС <> ОтборСтрокНоменклатуры.СтавкаНДС Тогда
				НоменклатураПоОбычнойСтавке = НоменклатураСчетовНаОплату.Скопировать(ОтборСтрокНоменклатуры);
				// В счете-фактуре на аванс указывается расчетная ставка
				НоменклатураПоОбычнойСтавке.ЗаполнитьЗначения(СтрокаАванса.СтавкаНДС, "СтавкаНДС");
				ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(НоменклатураПоОбычнойСтавке, НоменклатураСчетаНаОплату);
			КонецЕсли;
			
			// Распределяем сумму аванса по отобранным строкам
			// Распределяемая сумма не может превышать общей рублевой суммы счета
			Если СтрокаАванса.ВалютаСчетаНаОплату <> ВалютаРеглУчета Тогда
				// Пересчитываем суммы счета в рубли
				КурсКратность = КурсыВалют[СтрокаАванса.ВалютаСчетаНаОплату];
				Если КурсКратность = Неопределено Тогда
					КурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(СтрокаАванса.ВалютаСчетаНаОплату, СтрокаАванса.Дата);
					КурсКратность.Кратность = ?(КурсКратность.Кратность = 0, 1, КурсКратность.Кратность);
					КурсыВалют.Вставить(СтрокаАванса.ВалютаСчетаНаОплату, КурсКратность);
				КонецЕсли;
				Для каждого СтрокаНоменклатуры Из НоменклатураСчетаНаОплату Цикл
					СтрокаНоменклатуры.Сумма = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаНоменклатуры.Сумма,
						СтрокаАванса.ВалютаСчетаНаОплату, ВалютаРеглУчета,
						КурсКратность.Курс, 1,
						КурсКратность.Кратность, 1);
				КонецЦикла;
			КонецЕсли;
			СуммаРаспределения = Мин(СтрокаАванса.Сумма, НоменклатураСчетаНаОплату.Итог("Сумма"));
			НераспределеннаяСумма = СтрокаАванса.Сумма - СуммаРаспределения;
			Если СуммаРаспределения > 0 Тогда
				// Распределяем сумму по строкам пропорционально рублевых сумм счета
				ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СуммаРаспределения, НоменклатураСчетаНаОплату, "Сумма");
				ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(НоменклатураСчетаНаОплату, НоменклатураСчетаФактуры);
			КонецЕсли;
			Если НераспределеннаяСумма > 0 Тогда
				// Строк с указанной при оплате ставкой в счете нет - добавляем строку с значениями по-умолчанию
				СтрокаСчетаФактуры = НоменклатураСчетаФактуры.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаСчетаФактуры, СтрокаАванса);
				СтрокаСчетаФактуры.Сумма = НераспределеннаяСумма;
			КонецЕсли;
			
			НоменклатураСчетаФактуры.ЗаполнитьЗначения(СтрокаАванса.НДСИсчисляетсяНалоговымАгентом, "НДСИсчисляетсяНалоговымАгентом");
			
		Иначе
			СтрокаСчетаФактуры = НоменклатураСчетаФактуры.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСчетаФактуры, СтрокаАванса);
		КонецЕсли;
		
	КонецЦикла;
	
	// Рассчитываем суммы НДС по добавленным строкам счета-фактуры
	Для каждого СтрокаСФ Из НоменклатураСчетаФактуры Цикл
		Если СтрокаСФ.НДСИсчисляетсяНалоговымАгентом Тогда
			СтрокаСФ.СуммаНДС = 0;
		Иначе
			ПроцентНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаСФ.СтавкаНДС);
			СтрокаСФ.СуммаНДС = Окр(СтрокаСФ.Сумма / (100 + ПроцентНДС) * ПроцентНДС, 2);
		КонецЕсли;
	КонецЦикла;
	
	НоменклатураСчетаФактуры.Сортировать("ДатаСчетаНаОплату,СчетНаОплату,НомерТабЧасти,НомерСтроки", Новый СравнениеЗначений);
	
	Возврат НоменклатураСчетаФактуры;

КонецФункции

// Получает товары и услуги комитента из документа "ПоступлениеТоваровУслуг" и распределяет суммы из счета-фактуры
// по номенклатуре.
Функция ТоварыУслугиЗакупленныеКомитентам(СтруктураПараметров) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СтруктураПараметров.Поступление);
	Запрос.УстановитьПараметр("Контрагент", СтруктураПараметров.Комитент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Содержание,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Сумма,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.СтавкаНДС,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.СуммаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслугАгентскиеУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Ссылка = &Ссылка
	|	И ПоступлениеТоваровУслугАгентскиеУслуги.Контрагент = &Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	"""",
	|	ПоступлениеТоваровУслугТовары.Сумма,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
	|	И ПоступлениеТоваровУслугТовары.Контрагент = &Контрагент";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	КоэффициентыСумм = Результат.ВыгрузитьКолонку("Сумма");
	КоэффициентыСуммНДС = Результат.ВыгрузитьКолонку("СуммаНДС");
	
	МассивСумм = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СтруктураПараметров.Сумма, КоэффициентыСумм);
	МассивСуммНДС = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СтруктураПараметров.СуммаНДС, КоэффициентыСуммНДС);
	
	Если МассивСумм = Неопределено 
		ИЛИ МассивСуммНДС = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Для Индекс = 0 По Результат.Количество() - 1 Цикл
		
		Результат[Индекс].Сумма = МассивСумм[Индекс];
		Результат[Индекс].СуммаНДС = МассивСуммНДС[Индекс];
		// Ставка НДС в счете-фактуре налогового агента должна быть расчетная
		Если Результат[Индекс].СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
			Результат[Индекс].СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120;
		ИначеЕсли Результат[Индекс].СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
			Результат[Индекс].СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118;
		ИначеЕсли Результат[Индекс].СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
			Результат[Индекс].СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьКодВидаОперации(СтруктураПараметров, КодВидаОперацииОснования = Неопределено) Экспорт
	
	Перем КодВидаОперации;
	
	Дата                      = СтруктураПараметров.Дата;
	ВидСчетаФактуры           = СтруктураПараметров.ВидСчетаФактуры;
	Исправление               = СтруктураПараметров.Исправление;
	Контрагент                = СтруктураПараметров.Контрагент;
	ДоговорКонтрагента        = СтруктураПараметров.ДоговорКонтрагента;
	Продавец                  = СтруктураПараметров.Продавец;
	СчетФактураНеВыставляется = СтруктураПараметров.СчетФактураНеВыставляется;
	СчетФактураБезНДС         = СтруктураПараметров.СчетФактураБезНДС;
	ДокументОснование         = Неопределено;
	ВерсияКодовВидовОпераций  = УчетНДСКлиентСервер.ВерсияКодовВидовОпераций(Дата);
	
	Если ТипЗнч(СтруктураПараметров) = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		ДокументыОснования = СтруктураПараметров.ДокументыОснования.Выгрузить(,"ДокументОснование");
	Иначе
		ДокументыОснования = СтруктураПараметров.ДокументыОснования;
		СтруктураПараметров.Свойство("ДокументОснование", ДокументОснование);
	КонецЕсли;
	
	Если НЕ УчетНДСПереопределяемый.ИспользуетсяПостановлениеНДС1137(Дата) Тогда
		Возврат СтруктураПараметров.КодВидаОперации;
	КонецЕсли;
	
	Если Исправление ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Если ЗначениеЗаполнено(КодВидаОперацииОснования) Тогда
			Возврат УчетНДС.АктуальныйКодВидаОперации(КодВидаОперацииОснования, ВерсияКодовВидовОпераций);
		КонецЕсли;
	КонецЕсли;
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
		
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда 
			УчетАгентскогоНДСПокупателем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
			Если УчетАгентскогоНДСПокупателем Тогда 
				КодВидаОперации = "33";
				Возврат КодВидаОперации;
			КонецЕсли;
		КонецЕсли;
		
		КодВидаОперации = "02";
		
		Если ЗначениеЗаполнено(ДокументОснование)
			И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			КодВидаОперации = "26"; // авансы по подарочным сертификатам
		КонецЕсли;
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку Тогда
		
		КодВидаОперации = ?(ВерсияКодовВидовОпераций < 3, "05", "02");
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
		Если ЗначениеЗаполнено(ДоговорКонтрагента)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "УчетАгентскогоНДС") Тогда
			КодВидаОперации = "06";
		КонецЕсли;
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу Тогда
		
		КодВидаОперации = ?(ВерсияКодовВидовОпераций < 3, "09", "01");
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда
		
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда 
			УчетАгентскогоНДСПокупателем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
			Если УчетАгентскогоНДСПокупателем Тогда 
				КодВидаОперации = "34";
				Возврат КодВидаОперации;
			КонецЕсли;
		КонецЕсли;
		
		МассивДокументовРеализации = Новый Массив;
		
		Для Каждого СтрокаТабличнойЧасти ИЗ ДокументыОснования Цикл
			
			КодВидаОперации = "";
			ТипОснования = ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование);
			
			Если ТипОснования = Тип("ДокументСсылка.ОтражениеНачисленияНДС")
				И ВерсияКодовВидовОпераций > 1 Тогда
				// Код операции указывается в документе
				КодВидаОперацииИзДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					СтрокаТабличнойЧасти.ДокументОснование, "КодВидаОперации");
				КодВидаОперации = УчетНДС.АктуальныйКодВидаОперации(КодВидаОперацииИзДокумента, ВерсияКодовВидовОпераций);
				Прервать;
			ИначеЕсли ВерсияКодовВидовОпераций >= 3 Тогда
				// По умолчанию код "01", но необходимо проверить случай "смешанной" реализации (код 15),
				// которая может быть оформлена документом "Реализация товаров услуг".
				КодВидаОперации = "01";
				Если ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
					МассивДокументовРеализации.Добавить(СтрокаТабличнойЧасти);
				ИначеЕсли ТипОснования = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
					ДокументОтгрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
						СтрокаТабличнойЧасти.ДокументОснование, "ДокументОтгрузки");
					Если ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
						МассивДокументовРеализации.Добавить(ДокументОтгрузки);
					КонецЕсли;
				Иначе
					МассивДокументовРеализации.Очистить();
					Прервать;
				КонецЕсли;
			// Коды видов операций до 1 июля 2016 года
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
				КодВидаОперации = "03";
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
				КодВидаОперации = "04";
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
				Если ЗначениеЗаполнено(Продавец) И Продавец <> Контрагент Тогда
					КодВидаОперации = "04";
				КонецЕсли;
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				МассивДокументовРеализации.Добавить(СтрокаТабличнойЧасти);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
				ДокументОтгрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					СтрокаТабличнойЧасти.ДокументОснование, "ДокументОтгрузки");
				Если ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
					МассивДокументовРеализации.Добавить(ДокументОтгрузки);
				КонецЕсли;
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.НачислениеНДСпоСМРхозспособом") Тогда
				МассивДокументовРеализации.Очистить();
				КодВидаОперации = "08";
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если МассивДокументовРеализации.Количество() > 0 Тогда
			// Определим какие ценности были реализованы и проверим случай "смешанной" реализации (код 15).
			СчетаУчетаКомиссионногоТовара = Новый Массив;
			СчетаУчетаКомиссионногоТовара.Добавить(ПланыСчетов.Хозрасчетный.ТоварыНаСкладе);
			СчетаУчетаКомиссионногоТовара.Добавить(ПланыСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию);
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ДокументыОснования", МассивДокументовРеализации);
			Запрос.УстановитьПараметр("СчетаУчетаКомиссионногоТовара", СчетаУчетаКомиссионногоТовара);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА РеализацияТоваровУслугТовары.СчетУчета В (&СчетаУчетаКомиссионногоТовара)
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК ЕстьКомиссионныеТоварыИУслуги,
			|	ВЫБОР
			|		КОГДА (НЕ РеализацияТоваровУслугТовары.СчетУчета В (&СчетаУчетаКомиссионногоТовара))
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК ЕстьСобственныеТоварыИУслуги
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка В(&ДокументыОснования)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РеализацияТоваровУслугУслуги.Ссылка,
			|	0,
			|	1
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
			|ГДЕ
			|	РеализацияТоваровУслугУслуги.Ссылка В(&ДокументыОснования)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка,
			|	1,
			|	0
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслугАгентскиеУслуги
			|ГДЕ
			|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка В(&ДокументыОснования)
			|ИТОГИ
			|	СУММА(ЕстьКомиссионныеТоварыИУслуги),
			|	СУММА(ЕстьСобственныеТоварыИУслуги)
			|ПО
			|	Общие";
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Если Выборка.Следующий() Тогда
					Если Выборка.ЕстьСобственныеТоварыИУслуги > 0
						И Выборка.ЕстьКомиссионныеТоварыИУслуги > 0 Тогда
						КодВидаОперации = ?(ВерсияКодовВидовОпераций < 3, "01;04", "15");
					ИначеЕсли ВерсияКодовВидовОпераций < 3 И Выборка.ЕстьКомиссионныеТоварыИУслуги > 0 Тогда
						КодВидаОперации = "04";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КодВидаОперации) Тогда
		КодВидаОперации = "01";
	КонецЕсли;
	
	Возврат КодВидаОперации;
	
КонецФункции

Функция ОпределитьПорядокВыставленияСчетаФактуры(СтруктураПараметров) Экспорт
	
	ПараметрыВыставленияСчетаФактуры = Новый Структура("КодСпособаВыставления,Выставлен,ДатаВыставления");
	
	Если НЕ СтруктураПараметров.СчетФактураНеВыставляется Тогда
		Если УчетНДСПереопределяемый.НаличиеСоглашенияОбменаЭД(СтруктураПараметров.ДокументОснование) Тогда
			ПараметрыВыставленияСчетаФактуры.КодСпособаВыставления = 2;
			ПараметрыВыставленияСчетаФактуры.Выставлен = Ложь;
			ПараметрыВыставленияСчетаФактуры.ДатаВыставления = '00010101';
		Иначе
			ПараметрыВыставленияСчетаФактуры.КодСпособаВыставления = 1;
			ПараметрыВыставленияСчетаФактуры.Выставлен = Истина;
			ПараметрыВыставленияСчетаФактуры.ДатаВыставления = 
				?(СтруктураПараметров.Дата = '00010101', ОбщегоНазначения.ТекущаяДатаПользователя(), СтруктураПараметров.Дата);
		КонецЕсли;	
	КонецЕсли;
	
	Возврат ПараметрыВыставленияСчетаФактуры;

КонецФункции	

// Корректировочный Счет-фактура

Функция ПолучитьРеквизитыИсходногоСчетаФактурыДляКорректировки(
	ДокументОснование, СчетФактура, ПолучатьПредставление = Ложь, ПолучитьИсходный = Ложь, ИсходныйДокумент = Неопределено) Экспорт
	
	Если СчетФактура.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.Корректировочный 
		ИЛИ (ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.КорректировкаРеализации") 
		И ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")) Тогда 
		
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		ДокументРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументРеализации");
		
		Если СчетФактура.Исправление Тогда 
			ИсходныйДокументРеализации = УчетНДСПереопределяемый.ПолучитьИсправляемыйДокументРеализации(ДокументОснование, ПолучитьИсходный);
			Если ТипЗнч(ИсходныйДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				ИсходныйДокументРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИсходныйДокументРеализации, "ДокументРеализации");
			КонецЕсли; 
		Иначе
			ИсходныйДокументРеализации = ДокументРеализации;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда

		ИсходныйДокументРеализации = ИсходныйДокумент;
		
	Иначе
		ИсходныйДокументРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ИсправляемыйДокумент");
	КонецЕсли; 
	
	РеквизитыИсходногоСчетаФактуры = ПолучитьРеквизитыСчетаФактурыВыданного(ИсходныйДокументРеализации, ПолучатьПредставление);
	
	Возврат РеквизитыИсходногоСчетаФактуры;
	
КонецФункции

Функция ПолучитьРеквизитыСчетаФактурыВыданного(ДокументОснование, ПолучатьПредставление = Ложь, ВидСчетаФактуры = Неопределено) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("ДокументОснование", ДокументОснование);
	Запрос.Параметры.Вставить("ВидСчетаФактуры", ВидСчетаФактуры);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента
	|ПОМЕСТИТЬ ВТ_СчетФактураВыданныйДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.ВидСчетаФактуры,
	|	СчетФактураВыданный.ПометкаУдаления КАК ПометкаУдаления,
	|	СчетФактураВыданный.Проведен КАК Проведен,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|				И СчетФактураВыданный.Исправление
	|			ТОГДА СчетФактураВыданный.НомерИсправляемогоКорректировочногоДокумента
	|		КОГДА СчетФактураВыданный.Исправление
	|			ТОГДА СчетФактураВыданный.НомерИсходногоДокумента
	|		ИНАЧЕ ДанныеПервичныхДокументов.Номер
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|				И СчетФактураВыданный.Исправление
	|			ТОГДА СчетФактураВыданный.ДатаИсправляемогоКорректировочногоДокумента
	|		КОГДА СчетФактураВыданный.Исправление
	|			ТОГДА СчетФактураВыданный.ДатаИсходногоДокумента
	|		ИНАЧЕ ДанныеПервичныхДокументов.Дата
	|	КОНЕЦ КАК ДатаСчетаФактуры,
	|	СчетФактураВыданный.Исправление,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Исправление
	|			ТОГДА СчетФактураВыданный.НомерИсправления
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Исправление
	|			ТОГДА СчетФактураВыданный.Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаИсправления,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента
	|		ИНАЧЕ СчетФактураВыданный.НомерИсходногоДокумента
	|	КОНЕЦ КАК НомерИсходногоДокумента,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента
	|		ИНАЧЕ СчетФактураВыданный.ДатаИсходногоДокумента
	|	КОНЕЦ КАК ДатаИсходногоДокумента,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УчитыватьИсправлениеИсходногоДокумента,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерИсправленияИсходногоДокумента,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаИсправленияИсходногоДокумента
	|ИЗ
	|	ВТ_СчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО СчетФактураВыданныйДокументыОснования.Ссылка = СчетФактураВыданный.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (СчетФактураВыданный.Ссылка = ДанныеПервичныхДокументов.Документ)
	|			И (СчетФактураВыданный.Организация = ДанныеПервичныхДокументов.Организация)
	|ГДЕ
	|	НЕ ДанныеПервичныхДокументов.Документ ЕСТЬ NULL
	|	И (СчетФактураВыданный.ВидСчетаФактуры <> ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|			ИЛИ СчетФактураВыданный.ВидСчетаФактуры = &ВидСчетаФактуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления,
	|	Проведен УБЫВ,
	|	СчетФактураВыданный.Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		РеквизитыСчетаФактуры = Новый Структура("СчетФактура,СчетФактураПредставление,СчетФактураКраткоеПредставление,ПометкаУдаления, Проведен, НомерСчетаФактуры, ДатаСчетаФактуры, 
			|Исправление, НомерИсправления, ДатаИсправления, НомерИсходногоДокумента, ДатаИсходногоДокумента,
			|УчитыватьИсправлениеИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
		
		ЗаполнитьЗначенияСвойств(РеквизитыСчетаФактуры, Выборка);
		Если ПолучатьПредставление Тогда
			ПредставлениеДокумента = ПолучитьПредставлениеДокумента(Выборка.СчетФактура, Выборка.ВидСчетаФактуры);
			ЗаполнитьЗначенияСвойств(РеквизитыСчетаФактуры, ПредставлениеДокумента);
		КонецЕсли;
		
		Возврат РеквизитыСчетаФактуры;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// ПОДГОТОВКА ПАРАМЕТРОВ ЗАПОЛНЕНИЯ ДОКУМЕНТА НА ОСНОВАНИИ

// Счет-фактура на аванс

Функция ПодготовитьПараметрыЗаполненияАванс(ДокументОснование, ДокументСсылка, ДоговорКонтрагента = Неопределено, Отказ) Экспорт

	РеквизитыОснования            = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "Организация, Дата");
	ВалютаРеглУчета               = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ПлательщикНДС                 = УчетнаяПолитика.ПлательщикНДС(РеквизитыОснования.Организация, РеквизитыОснования.Дата);
	РасчетнаяСтавкаНДСПоУмолчанию = УчетНДСКлиентСервер.РасчетнаяСтавкаНДСПоУмолчанию(РеквизитыОснования.Дата);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ДокументСсылка", ?(ЗначениеЗаполнено(ДокументСсылка), ДокументСсылка, Неопределено));
	Запрос.УстановитьПараметр("ПлательщикНДС", ПлательщикНДС);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ?(ЗначениеЗаполнено(ДоговорКонтрагента), ДоговорКонтрагента, Неопределено));
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	Запрос.УстановитьПараметр("РасчетнаяСтавкаНДСПоУмолчанию", РасчетнаяСтавкаНДСПоУмолчанию);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = Документы[ДокументОснование.Метаданные().Имя].ТекстЗапросаСчетФактураВыданныйНаАвансРасшифровкаПлатежа(НомераТаблиц)
		+ ТекстЗапросаСуммыАвансовПоВзаиморасчетам(НомераТаблиц)
		+ ТекстЗапросаДанныеПлатежа(НомераТаблиц)
		+ ТекстЗапросаНоменклатураСчетовНаОплату(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если ПроведениеСервер.ИспользуетсяОтложенноеПроведение(РеквизитыОснования.Организация, РеквизитыОснования.Дата) Тогда
		// При использовании отложенного проведения проверим дату актуальности отложенных расчетов.
		ДоговорыПлатежа = Результат[НомераТаблиц.ДоговорыПлатежа].Выгрузить().ВыгрузитьКолонку("ДоговорКонтрагента");
		Если ДоговорыПлатежа.Количество() > 0 Тогда
			МоментАктуальностиОтложенныхРасчетов = УчетВзаиморасчетовОтложенноеПроведение.МоментАктуальностиОтложенныхРасчетов(
				РеквизитыОснования.Организация,
				РеквизитыОснования.Дата,
				,
				ДоговорыПлатежа);
			
			Если МоментАктуальностиОтложенныхРасчетов <> Неопределено
				И МоментАктуальностиОтложенныхРасчетов.Дата <= РеквизитыОснования.Дата Тогда
				
				ПараметрыЗаполнения = Новый Структура();
				ПараметрыЗаполнения.Вставить("МоментАктуальностиОтложенныхРасчетов", МоментАктуальностиОтложенныхРасчетов);
				
				Отказ = Истина;
				Возврат ПараметрыЗаполнения;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	РасшифровкаПлатежа = Результат[НомераТаблиц.РасшифровкаПлатежа].Выгрузить();
	Если РасшифровкаПлатежа.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены авансы по документу %1, на которые не выставлены счета-фактуры на аванс.'"),
			ДокументОснование);
		ПараметрыЗаполнения = Новый Структура();
		ПараметрыЗаполнения.Вставить("ТекстСообщения", ТекстСообщения);
			
		Отказ = Истина;
		Возврат ПараметрыЗаполнения;
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		АвансыПоСчетамНаОплату = РасшифровкаПлатежа.Скопировать();
	Иначе
		СуммыАвансов = Результат[НомераТаблиц.СуммыАвансов].Выгрузить();
		Если СуммыАвансов.Количество() = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не обнаружены данные об авансах по документу %1.'"),
				ДокументОснование);
			Отказ = Истина;
			Возврат ТекстСообщения;
		КонецЕсли;
		
		// Распределяем рублевые суммы авансов по счетам на оплату и ставкам НДС из расшифровки платежа.
		// Очередность распределения определяется указанным в расшифровке платежа способом погашения задолженности.
		АвансыПоСчетамНаОплату = РасшифровкаПлатежа.СкопироватьКолонки();
		ОтборПоДоговору = Новый Структура("Контрагент,ДоговорКонтрагента,СчетАвансов");
		ОтборПоСпособу  = Новый Структура("СпособПогашенияЗадолженности");
		ОчередностьРаспределения = Новый Массив;
		ОчередностьРаспределения.Добавить(Перечисления.СпособыПогашенияЗадолженности.НеПогашать);
		ОчередностьРаспределения.Добавить(Перечисления.СпособыПогашенияЗадолженности.Автоматически);
		ОчередностьРаспределения.Добавить(Перечисления.СпособыПогашенияЗадолженности.ПоДокументу);
		ОчередностьРаспределения.Добавить(Перечисления.СпособыПогашенияЗадолженности.ПустаяСсылка());
		РасшифровкаПлатежа.Индексы.Добавить("Контрагент,ДоговорКонтрагента,СчетАвансов");
		Для каждого СтрокаАванса Из СуммыАвансов Цикл
			НераспределеннаяСумма = СтрокаАванса.Сумма;
			ЗаполнитьЗначенияСвойств(ОтборПоДоговору, СтрокаАванса);
			ПлатежиПоДоговору = РасшифровкаПлатежа.Скопировать(ОтборПоДоговору);
			Если ПлатежиПоДоговору.Итог("Сумма") < НераспределеннаяСумма Тогда
				ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(НераспределеннаяСумма, ПлатежиПоДоговору, "Сумма");
			КонецЕсли;
			Для каждого СпособПогашения Из ОчередностьРаспределения Цикл
				Если НераспределеннаяСумма <= 0 Тогда
					Прервать;
				КонецЕсли;
				ОтборПоСпособу.СпособПогашенияЗадолженности = СпособПогашения;
				ПлатежиПоСпособу = ПлатежиПоДоговору.Скопировать(ОтборПоСпособу);
				Если ПлатежиПоСпособу.Количество() > 0 Тогда
					РаспределяемаяСумма = Мин(НераспределеннаяСумма, ПлатежиПоСпособу.Итог("Сумма"));
					ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(РаспределяемаяСумма, ПлатежиПоСпособу, "Сумма");
					ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ПлатежиПоСпособу, АвансыПоСчетамНаОплату);
					НераспределеннаяСумма = НераспределеннаяСумма - РаспределяемаяСумма;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	АвансыПоСчетамНаОплату.Свернуть(
		"Дата,ДокументОснование,Организация,Контрагент,ДоговорКонтрагента,СчетНаОплату,Номенклатура,ВалютаСчетаНаОплату,
		|СтавкаНДС,ИдентификаторГосКонтракта,НДСИсчисляетсяНалоговымАгентом",
		"Сумма");
	АвансыПоСчетамНаОплату.Индексы.Добавить("СтавкаНДС,НДСИсчисляетсяНалоговымАгентом");
	
	// Удаляем строки со ставками "Без НДС" и 0% - счета-фактуры на такие авансы не выписываются
	// кроме случая получения предварительной оплаты продавцом товаров, указанных в п.8 ст. 161 НК.
	АвансыБезНДС = АвансыПоСчетамНаОплату.НайтиСтроки(Новый Структура("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС));
	Для каждого СтрокаБезНДС Из АвансыБезНДС Цикл
		Если СтрокаБезНДС.НДСИсчисляетсяНалоговымАгентом Тогда
			СтрокаБезНДС.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(СтрокаБезНДС.Дата);
		Иначе
			АвансыПоСчетамНаОплату.Удалить(СтрокаБезНДС);
		КонецЕсли;
	КонецЦикла;
	Авансы0 = АвансыПоСчетамНаОплату.НайтиСтроки(Новый Структура("СтавкаНДС", Перечисления.СтавкиНДС.НДС0));
	Для каждого Строка0 Из Авансы0 Цикл
		АвансыПоСчетамНаОплату.Удалить(Строка0);
	КонецЦикла;
	Если АвансыПоСчетамНаОплату.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены авансы по документу %1 по ставкам НДС, 
			|на которые требуется регистрировать счет-фактуру на аванс.'"),
			ДокументОснование);
		Отказ = Истина;
		Возврат ТекстСообщения;
	КонецЕсли;
	
	// Распределяем суммы авансов по номенклатуре счетов на оплату
	НоменклатураСчетовНаОплату = Результат[НомераТаблиц.НоменклатураСчетовНаОплату].Выгрузить();
	
	НоменклатураСчетаФактуры = РаспределитьСуммыАвансовПоНоменклатуреСчетовНаОплату(
		АвансыПоСчетамНаОплату, НоменклатураСчетовНаОплату, ВалютаРеглУчета);
		
	Реквизиты = Новый Структура();
	Реквизиты.Вставить("Дата");
	Реквизиты.Вставить("ДокументОснование");
	Реквизиты.Вставить("Организация");
	Реквизиты.Вставить("Контрагент");
	Реквизиты.Вставить("ДоговорКонтрагента");
	
	// Идентификатор государственного контракта заполняется автоматически только для КОРП версии.
	Если УчетНДС.ВедетсяУчетНДСПоФЗ56(АвансыПоСчетамНаОплату[0].Дата) 
		И ПолучитьФункциональнуюОпцию("ИспользуетсяГособоронзаказ") Тогда
		Реквизиты.Вставить("ИдентификаторГосКонтракта");
	КонецЕсли;
	
	РеквизитыОплаты = АвансыПоСчетамНаОплату[0];
	Если РеквизитыОплаты.НДСИсчисляетсяНалоговымАгентом Тогда
		// В счете-фактуре продавца товаров, указанных в п.8 ст. 161 НК, необходимо заполнить договор.
		ЗаполнитьЗначенияСвойств(Реквизиты, РеквизитыОплаты);
	Иначе
		ЗаполнитьЗначенияСвойств(Реквизиты, РеквизитыОплаты, , "ДоговорКонтрагента");
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Реквизиты", Реквизиты);
	ПараметрыЗаполнения.Вставить("Авансы",    НоменклатураСчетаФактуры);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ТекстЗапросаСуммыАвансовПоВзаиморасчетам(НомераТаблиц)
	
	// Суммы авансов по документу, а также зачтенные в Кт 62.ОТ суммы авансов в счет отгрузки
	НомераТаблиц.Вставить("ВТ_СуммыАвансовПоДокументу",              НомераТаблиц.Количество());
	// Суммы авансов, по которым необходимо начислить НДС
	НомераТаблиц.Вставить("ВТ_СуммыАвансовПредварительная",          НомераТаблиц.Количество());
	// Временная таблица авансов по результатам проведения документа
	НомераТаблиц.Вставить("ВТ_СуммыАвансов",                         НомераТаблиц.Количество());
	// Уничтожение временных таблиц
	НомераТаблиц.Вставить("Уничтожение_СуммыАвансовПоДокументу",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_СуммыАвансовПредварительная", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Проводки.СубконтоКт1 КАК Контрагент,
	|	Проводки.СубконтоКт2 КАК ДоговорКонтрагента,
	|	Проводки.СчетКт КАК СчетАвансов,
	|	Проводки.Сумма КАК Сумма
	|ПОМЕСТИТЬ СуммыАвансовПоДокументу
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Регистратор = &ДокументОснование
	|				И ВидСубконтоКт1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|				И ВидСубконтоКт2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|				И ВидСубконтоКт3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)
	|				И СубконтоКт3 = &ДокументОснование,
	|			,
	|			) КАК Проводки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Проводки.СубконтоКт1,
	|	Проводки.СубконтоКт2,
	|	Проводки.СчетДт,
	|	-Проводки.Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Регистратор = &ДокументОснование
	|				И СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымУЕВСчетОтгрузки)
	|				И ВидСубконтоДт1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|				И ВидСубконтоДт2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|				И ВидСубконтоДт3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)
	|				И СубконтоДт3 = &ДокументОснование,
	|			,
	|			) КАК Проводки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыАвансовПоДокументу.Контрагент,
	|	СуммыАвансовПоДокументу.ДоговорКонтрагента,
	|	СуммыАвансовПоДокументу.СчетАвансов,
	|	СУММА(СуммыАвансовПоДокументу.Сумма) КАК Сумма
	|ПОМЕСТИТЬ СуммыАвансовПредварительная
	|ИЗ
	|	СуммыАвансовПоДокументу КАК СуммыАвансовПоДокументу
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыАвансовПоДокументу.Контрагент,
	|	СуммыАвансовПоДокументу.ДоговорКонтрагента,
	|	СуммыАвансовПоДокументу.СчетАвансов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыАвансовПредварительная.Контрагент,
	|	СуммыАвансовПредварительная.ДоговорКонтрагента,
	|	СуммыАвансовПредварительная.СчетАвансов,
	|	СуммыАвансовПредварительная.Сумма
	|ПОМЕСТИТЬ СуммыАвансов
	|ИЗ
	|	СуммыАвансовПредварительная КАК СуммыАвансовПредварительная
	|ГДЕ
	|	СуммыАвансовПредварительная.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СуммыАвансовПоДокументу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СуммыАвансовПредварительная";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаДанныеПлатежа(НомераТаблиц)
	
	// Таблица договоров из ВТ РасшифровкаПлатежа для отложенного проведения
	НомераТаблиц.Вставить("ДоговорыПлатежа", НомераТаблиц.Количество());
	
	// Временная таблица контрагентов документа аванса
	НомераТаблиц.Вставить("ВТ_КонтрагентыПлатежа", НомераТаблиц.Количество());
	
	// Временная таблица контрагентов из ВТ_КонтрагентыПлатежа,
	// содержит контрагента, по которому есть аванс, но не был выставлен СФ на аванс
	НомераТаблиц.Вставить("ВТ_КонтрагентыСчетаФактуры", НомераТаблиц.Количество());
	
	// Временная таблица счетов на оплату, выбранных из ВТ РасшифровкаПлатежа 
	// с отбором по контрагенту
	НомераТаблиц.Вставить("ВТ_СчетаНаОплату", НомераТаблиц.Количество());
	
	// Суммы авансов из ВТ СуммыАвансов с отбором по контрагенту
	НомераТаблиц.Вставить("СуммыАвансов", НомераТаблиц.Количество());
	
	// Расшифровка платежа документа из ВТ РасшифровкаПлатежа 
	// с отбором по контрагенту
	НомераТаблиц.Вставить("РасшифровкаПлатежа", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасшифровкаПлатежа.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ КонтрагентыПлатежа
	|ИЗ
	|	РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтрагентыПлатежа.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ КонтрагентыСчетаФактуры
	|ИЗ
	|	КонтрагентыПлатежа КАК КонтрагентыПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммыАвансов КАК СуммыАвансов
	|		ПО КонтрагентыПлатежа.Контрагент = СуммыАвансов.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО (СчетФактура.ДокументОснование = &ДокументОснование)
	|			И (СчетФактура.Ссылка <> &ДокументСсылка)
	|			И (СчетФактура.ПометкаУдаления = ЛОЖЬ)
	|			И КонтрагентыПлатежа.Контрагент = СчетФактура.Контрагент
	|ГДЕ
	|	СчетФактура.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасшифровкаПлатежа.СчетНаОплату КАК СчетНаОплату
	|ПОМЕСТИТЬ СчетаНаОплату
	|ИЗ
	|	РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыСчетаФактуры КАК КонтрагентыСчетаФактуры
	|		ПО (КонтрагентыСчетаФактуры.Контрагент = РасшифровкаПлатежа.Контрагент)
	|ГДЕ
	|	РасшифровкаПлатежа.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыАвансов.Контрагент КАК Контрагент,
	|	СуммыАвансов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СуммыАвансов.СчетАвансов КАК СчетАвансов,
	|	СуммыАвансов.Сумма КАК Сумма
	|ИЗ
	|	СуммыАвансов КАК СуммыАвансов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыСчетаФактуры КАК КонтрагентыСчетаФактуры
	|		ПО (КонтрагентыСчетаФактуры.Контрагент = СуммыАвансов.Контрагент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Дата КАК Дата,
	|	РасшифровкаПлатежа.ДокументОснование КАК ДокументОснование,
	|	РасшифровкаПлатежа.Организация КАК Организация,
	|	РасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РасшифровкаПлатежа.СчетАвансов КАК СчетАвансов,
	|	РасшифровкаПлатежа.СчетНаОплату КАК СчетНаОплату,
	|	РасшифровкаПлатежа.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности,
	|	ЕСТЬNULL(РасшифровкаПлатежа.СчетНаОплату.ВалютаДокумента, &ВалютаРеглУчета) КАК ВалютаСчетаНаОплату,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА РасшифровкаПлатежа.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|		ИНАЧЕ РасшифровкаПлатежа.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	РасшифровкаПлатежа.Сумма КАК Сумма,
	|	РасшифровкаПлатежа.Номенклатура КАК Номенклатура,
	|	РасшифровкаПлатежа.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.Дата < ДАТАВРЕМЯ(2018, 1, 1)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ЕСТЬNULL(ДоговорыСчетаФактуры.УчетАгентскогоНДСПокупателем, ЛОЖЬ)
	|	КОНЕЦ КАК НДСИсчисляетсяНалоговымАгентом
	|ИЗ
	|	РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыСчетаФактуры КАК КонтрагентыСчетаФактуры
	|		ПО РасшифровкаПлатежа.Контрагент = КонтрагентыСчетаФактуры.Контрагент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыСчетаФактуры
	|		ПО РасшифровкаПлатежа.ДоговорКонтрагента = ДоговорыСчетаФактуры.Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаНоменклатураСчетовНаОплату(НомераТаблиц)

	// Табличные части счетов на оплату из ВТ СчетаНаОплату
	НомераТаблиц.Вставить("НоменклатураСчетовНаОплату", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка.Дата КАК ДатаСчетаНаОплату,
	|	ТаблицаТовары.Ссылка КАК СчетНаОплату,
	|	1 КАК НомерТабЧасти,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Содержание КАК СТРОКА(1000)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА ТаблицаТовары.Сумма - ТаблицаТовары.СуммаСкидки
	|		ИНАЧЕ ТаблицаТовары.Сумма - ТаблицаТовары.СуммаСкидки + ТаблицаТовары.СуммаНДС
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаНаОплату КАК СчетаНаОплату
	|		ПО ТаблицаТовары.Ссылка = СчетаНаОплату.СчетНаОплату
	|ГДЕ
	|	ТаблицаТовары.Сумма > ТаблицаТовары.СуммаСкидки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции 

// Счет-фактура на суммовые разницы

Функция ПодготовитьПараметрыЗаполненияСуммовыеРазницы(ДокументОснование, ДокументСсылка, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ДокументСсылка", ?(ЗначениеЗаполнено(ДокументСсылка), ДокументСсылка, Неопределено));
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаСуммовыеРазницы(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	ЗаписиНДСНачисленный = Результат[НомераТаблиц.НДСНачисленныйКоличествоЗаписей].Выгрузить();
	Если ЗаписиНДСНачисленный.Итог("КоличествоЗаписей") = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены данные о суммовых разницах по документу %1.'"),
			ДокументОснование);
		Отказ = Истина;
		Возврат ТекстСообщения;
	КонецЕсли;
	
	СуммовыеРазницы = Результат[НомераТаблиц.СуммовыеРазницы].Выгрузить();
	Если СуммовыеРазницы.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены суммовые разницы по документу %1, на которые не выставлены счета-фактуры.'"),
			ДокументОснование);
		Отказ = Истина;
		Возврат ТекстСообщения;
	КонецЕсли;
	
	Реквизиты = Новый Структура("Дата,ДокументОснование,Организация,Контрагент,ДоговорКонтрагента,Сумма, СуммаНДС, СтавкаНДС");
	ЗаполнитьЗначенияСвойств(Реквизиты, СуммовыеРазницы[0]);
	Реквизиты.Вставить("ВалютаДокумента", Константы.ВалютаРегламентированногоУчета.Получить());
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Реквизиты", Реквизиты);
	ПараметрыЗаполнения.Вставить("СуммовыеРазницы", СуммовыеРазницы);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ТекстЗапросаСуммовыеРазницы(НомераТаблиц)
	
	// Временная таблица суммовых разниц по движениям регистра НДСНачисленный и НДСЗаписиКнигиПродаж документа-основания
	НомераТаблиц.Вставить("ВТ_НДСНачисленный", НомераТаблиц.Количество());
	
	// Временная таблица договоров и ставок НДС из ВТ НДСНачисленный
	//	Если в запрос передана ссылка на существующий счет-фактуру, 
	//	таблица договоров формируется с отбором по договору из счета-фактуры
	НомераТаблиц.Вставить("ВТ_ДоговорыПлатежа", НомераТаблиц.Количество());
	
	// Временная таблица договоров и ставок НДС из ВТ ДоговорыПлатежа, 
	//	содержит 1 договор, по которому есть суммовая разница, но не был выставлен СФ
	НомераТаблиц.Вставить("ВТ_ДоговорыСчетаФактуры", НомераТаблиц.Количество());
	
	// Таблица суммовых разниц из ВТ НДСНачисленный с отбором по договору и ставке НДС из ВТ ДоговорыСчетаФактуры
	НомераТаблиц.Вставить("СуммовыеРазницы", НомераТаблиц.Количество());
	
	// Таблица, содержащая количество записей ВТ НДСНачисленный по всем договорам и ставкам НДС
	НомераТаблиц.Вставить("НДСНачисленныйКоличествоЗаписей", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НДСНачисленный.Период КАК Дата,
	|	НДСНачисленный.Организация КАК Организация,
	|	НДСНачисленный.Покупатель КАК Контрагент,
	|	НДСНачисленный.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСНачисленный.СчетФактура КАК ДокументОснование,
	|	НДСНачисленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСНачисленный.СуммаБезНДС + НДСНачисленный.НДС КАК Сумма,
	|	НДСНачисленный.НДС КАК СуммаНДС
	|ПОМЕСТИТЬ НДСНачисленный
	|ИЗ
	|	РегистрНакопления.НДСНачисленный КАК НДСНачисленный
	|ГДЕ
	|	НДСНачисленный.Регистратор = &ДокументОснование
	|	И НДСНачисленный.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СуммыСвязанныеСРасчетамиПоОплате)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Период,
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	НДСЗаписиКнигиПродаж.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДС + НДСЗаписиКнигиПродаж.НДС,
	|	НДСЗаписиКнигиПродаж.НДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.Регистратор = &ДокументОснование
	|	И НДСЗаписиКнигиПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СуммыСвязанныеСРасчетамиПоОплате)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСНачисленный.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСНачисленный.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ДоговорыПлатежа
	|ИЗ
	|	НДСНачисленный КАК НДСНачисленный
	|ГДЕ
	|	&ДокументСсылка = НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактура.ДоговорКонтрагента,
	|	СчетФактура.СтавкаНДС
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НДСНачисленный КАК НДСНачисленный
	|		ПО (НДСНачисленный.ДоговорКонтрагента = СчетФактура.ДоговорКонтрагента)
	|			И (НДСНачисленный.СтавкаНДС = СчетФактура.СтавкаНДС)
	|ГДЕ
	|	СчетФактура.Ссылка = &ДокументСсылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДоговорыПлатежа.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ДоговорыСчетаФактуры
	|ИЗ
	|	ДоговорыПлатежа КАК ДоговорыПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО (СчетФактура.ДокументОснование = &ДокументОснование)
	|			И (СчетФактура.Ссылка <> &ДокументСсылка)
	|			И (СчетФактура.ПометкаУдаления = ЛОЖЬ)
	|			И ДоговорыПлатежа.ДоговорКонтрагента = СчетФактура.ДоговорКонтрагента
	|			И ДоговорыПлатежа.СтавкаНДС = СчетФактура.СтавкаНДС
	|ГДЕ
	|	(СчетФактура.ДоговорКонтрагента ЕСТЬ NULL 
	|			ИЛИ СчетФактура.СтавкаНДС ЕСТЬ NULL )
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленный.Дата КАК Дата,
	|	НДСНачисленный.Организация КАК Организация,
	|	НДСНачисленный.Контрагент КАК Контрагент,
	|	НДСНачисленный.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСНачисленный.ДокументОснование КАК ДокументОснование,
	|	НДСНачисленный.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(НДСНачисленный.Сумма) КАК Сумма,
	|	СУММА(НДСНачисленный.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	НДСНачисленный КАК НДСНачисленный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыСчетаФактуры КАК ДоговорыСчетаФактуры
	|		ПО (ДоговорыСчетаФактуры.ДоговорКонтрагента = НДСНачисленный.ДоговорКонтрагента)
	|			И (ДоговорыСчетаФактуры.СтавкаНДС = НДСНачисленный.СтавкаНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНачисленный.Дата,
	|	НДСНачисленный.Организация,
	|	НДСНачисленный.Контрагент,
	|	НДСНачисленный.ДоговорКонтрагента,
	|	НДСНачисленный.ДокументОснование,
	|	НДСНачисленный.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоЗаписей
	|ИЗ
	|	НДСНачисленный КАК НДСНачисленный";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Счет-фактура налоговый агент

Функция ПодготовитьПараметрыЗаполненияНалоговыйАгент(ДокументОснование, ДокументСсылка, ДоговорКонтрагента = Неопределено, Отказ) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ДокументСсылка", ?(ЗначениеЗаполнено(ДокументСсылка), ДокументСсылка, Неопределено));
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ?(ЗначениеЗаполнено(ДоговорКонтрагента), ДоговорКонтрагента, Неопределено));
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = Документы[ДокументОснование.Метаданные().Имя].ТекстЗапросаСчетФактураВыданныйНалоговыйАгентРасшифровкаПлатежа(НомераТаблиц)
		+ ТекстЗапросаСуммыОплатПоВзаиморасчетам(НомераТаблиц)
		+ ТекстЗапросаДанныеПлатежаНалоговыйАгент(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	СуммыОплат = Результат[НомераТаблиц.СуммыОплат].Выгрузить();
	Если СуммыОплат.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены данные для заполнения счета-фактуры налогового агента по документу %1.'"),
			ДокументОснование);
		Отказ = Истина;
		Возврат ТекстСообщения;
	КонецЕсли;
	
	РасшифровкаПлатежа = Результат[НомераТаблиц.РасшифровкаПлатежа].Выгрузить();
	Если РасшифровкаПлатежа.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены оплаты по документу %1, на которые не выставлены счета-фактуры налогового агента.'"),
			ДокументОснование);
		Отказ = Истина;
		Возврат ТекстСообщения;
	КонецЕсли;
	
	РасшифровкаПлатежа.Свернуть("Дата,ДокументОснование,Организация,Контрагент,ДоговорКонтрагента,ВидАгентскогоДоговора,Номенклатура", "СуммаБезНДС");
	
	Если РасшифровкаПлатежа.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не обнаружены оплаты по документу %1, на которые не выставлены счета-фактуры налогового агента.'"),
			ДокументОснование);
		Отказ = Истина;
		Возврат ТекстСообщения;
	КонецЕсли;
	
	РасшифровкаПлатежа.Колонки.Добавить("ДокументРасчетовДата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	ОтборСтрокПлатежа = Новый Структура("Контрагент,ДоговорКонтрагента");
	СуммыОплатПоДоговорам = РасшифровкаПлатежа.СкопироватьКолонки();
	СуммыОплатПоДоговорам.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	СуммыОплатПоДоговорам.Колонки.Добавить("СуммаНДС",  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СуммыОплатПоДоговорам.Колонки.Добавить("Сумма",     Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	Для каждого СтрокаОплаты Из СуммыОплат Цикл
		ЗаполнитьЗначенияСвойств(ОтборСтрокПлатежа, СтрокаОплаты);
		ПлатежиПоДоговору = РасшифровкаПлатежа.Скопировать(ОтборСтрокПлатежа);
		ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СтрокаОплаты.Сумма, ПлатежиПоДоговору, "СуммаБезНДС");
		ПлатежиПоДоговору.ЗаполнитьЗначения(СтрокаОплаты.ДокументРасчетовДата, "ДокументРасчетовДата");
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ПлатежиПоДоговору, СуммыОплатПоДоговорам);
	КонецЦикла;
	
	// В документах оплаты поставщикам - налоговым агентам должна быть сумма без НДС и ставка "Без НДС".
	// Поэтому из документов ставку и сумму НДС не берем, а рассчитываем НДС по основной ставке "сверху" от суммы оплаты.
	Для каждого СтрокаПлатежа Из СуммыОплатПоДоговорам Цикл
		СтрокаПлатежа.СтавкаНДС = 
			УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(СтрокаПлатежа.ДокументРасчетовДата);
		СтрокаПлатежа.СуммаНДС  = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтрокаПлатежа.СуммаБезНДС, Ложь, 
			УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаПлатежа.СтавкаНДС));
		СтрокаПлатежа.Сумма = СтрокаПлатежа.СуммаБезНДС + СтрокаПлатежа.СуммаНДС;
	КонецЦикла;
	
	Реквизиты = Новый Структура("Дата,ДокументОснование,Организация,Контрагент,ДоговорКонтрагента,ВидАгентскогоДоговора");
	ЗаполнитьЗначенияСвойств(Реквизиты, РасшифровкаПлатежа[0]);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Реквизиты",    Реквизиты);
	ПараметрыЗаполнения.Вставить("ТаблицаОплат", СуммыОплатПоДоговорам);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ТекстЗапросаСуммыОплатПоВзаиморасчетам(НомераТаблиц)
	
	// Предварительная таблица оплат по результатам проведения документа
	НомераТаблиц.Вставить("ВТ_СуммыОплатПредварительная", НомераТаблиц.Количество());
	
	// Временная таблица оплат по результатам проведения документа
	НомераТаблиц.Вставить("ВТ_СуммыОплат", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Проводки.СубконтоДт1 КАК Контрагент,
	|	Проводки.СубконтоДт2 КАК ДоговорКонтрагента,
	|	Проводки.СубконтоДт3 КАК ДокументРасчетов,
	|	СУММА(Проводки.Сумма) КАК Сумма,
	|	Проводки.Организация КАК Организация
	|ПОМЕСТИТЬ СуммыОплатПредварительная
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Регистратор = &ДокументОснование
	|				И ВидСубконтоДт1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|				И ВидСубконтоДт2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|				И ВидСубконтоДт3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)
	|				И ВЫРАЗИТЬ(СубконтоДт2 КАК Справочник.ДоговорыКонтрагентов).УчетАгентскогоНДС
	|				И (&ДоговорКонтрагента = НЕОПРЕДЕЛЕНО
	|					ИЛИ (ВЫРАЗИТЬ(СубконтоДт2 КАК Справочник.ДоговорыКонтрагентов)) = &ДоговорКонтрагента)
	|				И НЕ СчетКт.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы),
	|			,
	|			) КАК Проводки
	|
	|СГРУППИРОВАТЬ ПО
	|	Проводки.СубконтоДт1,
	|	Проводки.СубконтоДт2,
	|	Проводки.СубконтоДт3,
	|	Проводки.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДокументРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыОплатПредварительная.Контрагент КАК Контрагент,
	|	СуммыОплатПредварительная.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СуммыОплатПредварительная.Сумма КАК Сумма,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДокументРасчетовДата
	|ПОМЕСТИТЬ СуммыОплат
	|ИЗ
	|	СуммыОплатПредварительная КАК СуммыОплатПредварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО СуммыОплатПредварительная.Организация = ДанныеПервичныхДокументов.Организация
	|			И СуммыОплатПредварительная.ДокументРасчетов = ДанныеПервичныхДокументов.Документ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаДанныеПлатежаНалоговыйАгент(НомераТаблиц)

	// Временная таблица договоров из ВТ РасшифровкаПлатежа 
	//	Если в запрос передана ссылка на существующий счет-фактуру, 
	//	таблица договоров формируется с отбором по договору из счета-фактуры
	НомераТаблиц.Вставить("ВТ_ДоговорыПлатежа", НомераТаблиц.Количество());
	
	// Временная таблица договоров из ВТ ДоговорыПлатежа, 
	//	содержит 1 договор, по которому есть оплата по договору налогового агента, но не был выставлен СФ
	НомераТаблиц.Вставить("ВТ_ДоговорыСчетаФактуры", НомераТаблиц.Количество());
	
	// Суммы оплат из ВТ СуммыОплат
	НомераТаблиц.Вставить("СуммыОплат", НомераТаблиц.Количество());
	
	// Расшифровка платежа документа из ВТ РасшифровкаПлатежа 
	//	с отбором по договору из ВТ ДоговорыСчетаФактуры
	НомераТаблиц.Вставить("РасшифровкаПлатежа", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ ДоговорыПлатежа
	|ИЗ
	|	РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ ДоговорыСчетаФактуры
	|ИЗ
	|	ДоговорыПлатежа КАК ДоговорыПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммыОплат КАК СуммыОплат
	|		ПО ДоговорыПлатежа.ДоговорКонтрагента = СуммыОплат.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО (СчетФактура.ДокументОснование = &ДокументОснование)
	|			И (СчетФактура.Ссылка <> &ДокументСсылка)
	|			И (СчетФактура.ПометкаУдаления = ЛОЖЬ)
	|			И ДоговорыПлатежа.ДоговорКонтрагента = СчетФактура.ДоговорКонтрагента
	|ГДЕ
	|	СчетФактура.ДоговорКонтрагента ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыОплат.Контрагент КАК Контрагент,
	|	СуммыОплат.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СуммыОплат.Сумма КАК Сумма,
	|	СуммыОплат.ДокументРасчетовДата КАК ДокументРасчетовДата
	|ИЗ
	|	СуммыОплат КАК СуммыОплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыСчетаФактуры КАК ДоговорыСчетаФактуры
	|		ПО (ДоговорыСчетаФактуры.ДоговорКонтрагента = СуммыОплат.ДоговорКонтрагента)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДанныеДоговораКонтрагента
	|		ПО (ДанныеДоговораКонтрагента.Ссылка = СуммыОплат.ДоговорКонтрагента)
	|ГДЕ
	|	ДанныеДоговораКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Дата КАК Дата,
	|	РасшифровкаПлатежа.ДокументОснование КАК ДокументОснование,
	|	РасшифровкаПлатежа.Организация КАК Организация,
	|	РасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РасшифровкаПлатежа.ДоговорКонтрагента.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	РасшифровкаПлатежа.СуммаБезНДС КАК СуммаБезНДС,
	|	РасшифровкаПлатежа.Номенклатура КАК Номенклатура
	|ИЗ
	|	РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыСчетаФактуры КАК ДоговорыСчетаФактуры
	|		ПО (ДоговорыСчетаФактуры.ДоговорКонтрагента = РасшифровкаПлатежа.ДоговорКонтрагента)
	|ГДЕ
	|	РасшифровкаПлатежа.ДоговорКонтрагента.ВидАгентскогоДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров)";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Корректировочный счет-фактура

Функция ПодготовитьПараметрыЗаполненияКорректировочный(ДокументОснование, Отказ) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		НомераТаблиц = Новый Структура;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КорректировкаРеализации.Ссылка КАК ДокументОснование,
		|	КорректировкаРеализации.Дата,
		|	КорректировкаРеализации.Организация,
		|	КорректировкаРеализации.Контрагент,
		|	КорректировкаРеализации.ДоговорКонтрагента,
		|	КорректировкаРеализации.ВидОперации,
		|	КорректировкаРеализации.ДокументРеализации,
		|	КорректировкаРеализации.ИсправляемыйДокументРеализации,
		|	ВЫБОР
		|		КОГДА КорректировкаРеализации.ИсправляемыйДокументРеализации ССЫЛКА Документ.КорректировкаРеализации
		|				И ВЫРАЗИТЬ(КорректировкаРеализации.ИсправляемыйДокументРеализации КАК Документ.КорректировкаРеализации).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
		|				И КорректировкаРеализации.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИсправлениеКорректировочногоСчетаФактуры
		|ИЗ
		|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|ГДЕ
		|	КорректировкаРеализации.Ссылка = &ДокументОснование";
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			ТекстСообщения = НСтр("ru='Нет данных для корректировочного счета-фактуры.'");
			Отказ = Истина;
			Возврат ТекстСообщения;
		КонецЕсли;
		
		ТаблицаРеквизитовОснований = Результат.Выгрузить();
		
	Иначе
		НомераТаблиц = Новый Структура;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка КАК ДокументОснование,
		|	ВозвратТоваровОтПокупателя.Дата КАК Дата,
		|	ВозвратТоваровОтПокупателя.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение) КАК ВидОперации,
		|	ВозвратТоваровОтПокупателя.Контрагент КАК Контрагент,
		|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВозвратТоваровОтПокупателя.Сделка КАК ДокументРеализации,
		|	ВозвратТоваровОтПокупателяТовары.ИсправляемыйДокумент КАК ИсправляемыйДокументРеализации,
		|	Ложь КАК ИсправлениеКорректировочногоСчетаФактуры
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|		ПО ВозвратТоваровОтПокупателя.Ссылка = ВозвратТоваровОтПокупателяТовары.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.Ссылка = &ДокументОснование
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровОтПокупателяТовары.ИсправляемыйДокумент,
		|	ВозвратТоваровОтПокупателя.Ссылка,
		|	ВозвратТоваровОтПокупателя.Дата,
		|	ВозвратТоваровОтПокупателя.Организация,
		|	ВозвратТоваровОтПокупателя.Контрагент,
		|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента,
		|	ВозвратТоваровОтПокупателя.Сделка,
		|	ВозвратТоваровОтПокупателяТовары.ИсправляемыйДокумент ССЫЛКА Документ.ВозвратТоваровОтПокупателя";
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			ТекстСообщения = НСтр("ru='Нет данных для корректировочного счета-фактуры.'");
			Отказ = Истина;
			Возврат ТекстСообщения;
		КонецЕсли;
		
		ТаблицаРеквизитовОснований = Результат.Выгрузить();

	КонецЕсли; 
	
	ТаблицаРеквизитов = НовыйТаблицаРеквизитовКорректировочногоСчетаФактуры();
	
	Для каждого РеквизитыОснования Из ТаблицаРеквизитовОснований Цикл 
	
		ИсправляемыйДокумент = УчетНДСПереопределяемый.ПолучитьИсправляемыйДокументРеализации(
			РеквизитыОснования.ДокументРеализации, Ложь);
		ИсходныйДокумент = УчетНДСПереопределяемый.ПолучитьИсправляемыйДокументРеализации(
			РеквизитыОснования.ДокументРеализации);
		НомераТаблиц.Очистить();

		Если НЕ ЗначениеЗаполнено(ИсходныйДокумент) Тогда
			ВызватьИсключение НСтр("ru = 'Не определен исправляемый документ'");
		КонецЕсли;
	
		Реквизиты = ТаблицаРеквизитов.Добавить();
		
		ЗаполнитьЗначенияСвойств(Реквизиты, РеквизитыОснования, "Дата,ДокументОснование,Организация,Контрагент,ДоговорКонтрагента");
		
		СтруктураОтбора = Новый Структура("Продавец", Справочники.Контрагенты.ПустаяСсылка());
		Реквизиты.ИсправляемыйСчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(
			РеквизитыОснования.ИсправляемыйДокументРеализации,,,
			СтруктураОтбора); // Исключаем перевыставляемые комитенту счета-фактуры
			
		// ВидСчетаФактуры, Исправление
		
		КорректировкаКорректировочногоСчетаФактуры = Ложь;
		
		Если РеквизитыОснования.ИсправлениеКорректировочногоСчетаФактуры Тогда
			
			// Исправление корректировочного счета-фактуры
			Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			Реквизиты.Исправление     = Истина;
			
		ИначеЕсли РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			
			// Корректировочный счет-фактура
			Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			Реквизиты.Исправление     = Ложь;
			КорректировкаКорректировочногоСчетаФактуры = ТипЗнч(ИсправляемыйДокумент) = Тип("ДокументСсылка.КорректировкаРеализации");
		
		Иначе
			
			СписокТиповНаАванс = УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(
				Перечисления.ВидСчетаФактурыВыставленного.НаАванс);
			СписокТиповНалоговыйАгент = УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(
				Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент);
			СписокТиповКорректировочный = УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(
				Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
			
			Если СписокТиповНалоговыйАгент.Найти(ТипЗнч(ИсходныйДокумент)) <> Неопределено Тогда
				// Исправление счета-фактуры налогового агента
				Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент;
				
			ИначеЕсли СписокТиповНаАванс.Найти(ТипЗнч(ИсходныйДокумент)) <> Неопределено Тогда
				
				Если ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
					ИЛИ ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
					// Исправление счета-фактуры на реализацию
					Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
				Иначе
					// Исправление счета-фактуры на аванс
					Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс;
				КонецЕсли;
				
			ИначеЕсли СписокТиповКорректировочный.Найти(ТипЗнч(ИсходныйДокумент)) <> Неопределено Тогда
				// Исправление корректировочного счета-фактуры
				Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			Иначе
				// По умолчанию: исправление счета-фактуры на реализацию
				Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
			КонецЕсли;
			
			Реквизиты.Исправление = Истина;
			
		КонецЕсли;
		
		Если ТипЗнч(РеквизитыОснования.ДокументРеализации) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			
			// Основание корректировки - счет-фактура
			ИсходныйСчетФактура        = РеквизитыОснования.ДокументРеализации;
			ИсходныйДокументРеализации = Неопределено;
			
		ИначеЕсли ТипЗнч(РеквизитыОснования.ДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			
			// Основание корректировки - другая корректировка
			Если ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				ИсходныйСчетФактура        = ИсходныйДокумент;
				ИсходныйДокументРеализации = Неопределено;
			Иначе
				ИсходныйСчетФактура        = Неопределено;
				ИсходныйДокументРеализации = ИсходныйДокумент;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(РеквизитыОснования.ДокументРеализации) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
			И ЗначениеЗаполнено(Реквизиты.ИсправляемыйСчетФактура) Тогда 
			
			ИсходныйСчетФактура        = Реквизиты.ИсправляемыйСчетФактура;
			ИсходныйДокументРеализации = ИсходныйДокумент;
			
		Иначе
			
			ИсходныйСчетФактура        = Неопределено;
			ИсходныйДокументРеализации = ИсходныйДокумент;
		
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИсходныйСчетФактура",         ИсходныйСчетФактура);
		Запрос.УстановитьПараметр("ИсходныйДокументРеализации",  ИсходныйДокументРеализации);
		Запрос.УстановитьПараметр("КорректировочныйСчетФактура", Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
		Запрос.УстановитьПараметр("ДокументРеализации",          РеквизитыОснования.ДокументРеализации);
		
		Если ЗначениеЗаполнено(ИсходныйСчетФактура) Тогда
			// Реквизиты из исходного счета-фактуры
			Запрос.Текст = ТекстЗапросаРеквизитыИсходногоСчетаФактуры(НомераТаблиц);
		Иначе
			// Реквизиты из счета-фактуры, введенного на основании исходного документа
			Запрос.Текст = ТекстЗапросаРеквизитыСчетаФактурыИсходногоДокумента(НомераТаблиц);
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст + ТекстЗапросаНомерИсправления(НомераТаблиц);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		КодВидаОперацииИсходногоДокумента           = "";
		КодВидаОперацииИсходногоДокументаУменьшение = "";
		НомерИсправленияИсходногоДокумента          = 0;
		
		РеквизитыИсходногоСчетаФактуры = МассивРезультатов[0].Выбрать();
		Если РеквизитыИсходногоСчетаФактуры.Следующий() Тогда
			
			Реквизиты.КППКонтрагента      = РеквизитыИсходногоСчетаФактуры.КППКонтрагента;
			Реквизиты.СводныйКомиссионный = РеквизитыИсходногоСчетаФактуры.СводныйКомиссионный;
			
			Если ИсходныйСчетФактура = Неопределено 
			   И РеквизитыИсходногоСчетаФактуры.КорректировочныйСчетФактура Тогда
				Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			КонецЕсли;
			
			КодВидаОперацииИсходногоДокумента           = РеквизитыИсходногоСчетаФактуры.КодВидаОперации;
			КодВидаОперацииИсходногоДокументаУменьшение = РеквизитыИсходногоСчетаФактуры.КодВидаОперацииНаУменьшение;
			НомерИсправленияИсходногоДокумента          = РеквизитыИсходногоСчетаФактуры.НомерИсправления;
			
			// НомерИсходногоДокумента, ДатаИсходногоДокумента
			
			Если РеквизитыИсходногоСчетаФактуры.КорректировочныйСчетФактура 
			 ИЛИ РеквизитыИсходногоСчетаФактуры.Исправление Тогда
				
				Реквизиты.НомерИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.НомерИсходногоДокумента;
				Реквизиты.ДатаИсходногоДокумента  = РеквизитыИсходногоСчетаФактуры.ДатаИсходногоДокумента;
				
			Иначе
				
				Реквизиты.НомерИсходногоДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
					РеквизитыИсходногоСчетаФактуры.Номер, Истина, Ложь);
				Реквизиты.ДатаИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.Дата;
				
			КонецЕсли;
			
			// НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента
				
			Если РеквизитыИсходногоСчетаФактуры.КорректировочныйСчетФактура 
				И НЕ КорректировкаКорректировочногоСчетаФактуры Тогда
				
				Реквизиты.УчитыватьИсправлениеИсходногоДокумента = 
					РеквизитыИсходногоСчетаФактуры.УчитыватьИсправлениеИсходногоДокумента;
				
				Если Реквизиты.УчитыватьИсправлениеИсходногоДокумента Тогда
					Реквизиты.НомерИсправленияИсходногоДокумента =
						РеквизитыИсходногоСчетаФактуры.НомерИсправленияИсходногоДокумента;
					Реквизиты.ДатаИсправленияИсходногоДокумента =
						РеквизитыИсходногоСчетаФактуры.ДатаИсправленияИсходногоДокумента;
				КонецЕсли;
				
			ИначеЕсли РеквизитыИсходногоСчетаФактуры.Исправление Тогда
					
				Реквизиты.УчитыватьИсправлениеИсходногоДокумента = Истина;
				
				Реквизиты.НомерИсправленияИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.НомерИсправления;
				Реквизиты.ДатаИсправленияИсходногоДокумента  = РеквизитыИсходногоСчетаФактуры.Дата;
					
			КонецЕсли;
			
			// НомерИсправляемогоКорректировочногоДокумента, ДатаИсправляемогоКорректировочногоДокумента
			
			Если РеквизитыИсходногоСчетаФактуры.КорректировочныйСчетФактура Тогда	
				
				Если РеквизитыИсходногоСчетаФактуры.Исправление Тогда
					Реквизиты.НомерИсправляемогоКорректировочногоДокумента = 
						РеквизитыИсходногоСчетаФактуры.НомерИсправляемогоКорректировочногоДокумента;
					Реквизиты.ДатаИсправляемогоКорректировочногоДокумента  = 
						РеквизитыИсходногоСчетаФактуры.ДатаИсправляемогоКорректировочногоДокумента;
				ИначеЕсли Реквизиты.Исправление Тогда
					Реквизиты.НомерИсправляемогоКорректировочногоДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
						РеквизитыИсходногоСчетаФактуры.Номер, Истина, Ложь);
					Реквизиты.ДатаИсправляемогоКорректировочногоДокумента = РеквизитыИсходногоСчетаФактуры.Дата;
				ИначеЕсли КорректировкаКорректировочногоСчетаФактуры Тогда
					Реквизиты.НомерИсходногоДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
						РеквизитыИсходногоСчетаФактуры.Номер, Истина, Ложь);
					Реквизиты.ДатаИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.Дата;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// УчитыватьИсправлениеИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента
		
		ВыборкаНомерИсправления = МассивРезультатов[НомераТаблиц.НомерИсправления].Выбрать();
		Если ВыборкаНомерИсправления.Следующий() Тогда
		
			НомерИсправленияИсходногоДокумента = Макс(
				НомерИсправленияИсходногоДокумента, 
				ВыборкаНомерИсправления.НомерИсправленияИсходногоДокумента);
		
			Если НЕ Реквизиты.УчитыватьИсправлениеИсходногоДокумента
				И Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный
				И ЗначениеЗаполнено(ВыборкаНомерИсправления.НомерИсправленияИсходногоДокумента) Тогда
				
				ЗаполнитьЗначенияСвойств(Реквизиты, ВыборкаНомерИсправления);
				
			ИначеЕсли КорректировкаКорректировочногоСчетаФактуры Тогда
				ЗаполнитьЗначенияСвойств(Реквизиты, ВыборкаНомерИсправления);
			КонецЕсли;
				
		КонецЕсли;
		
		Реквизиты.КодВидаОперацииОснования           = КодВидаОперацииИсходногоДокумента;
		Реквизиты.КодВидаОперацииОснованияУменьшение = КодВидаОперацииИсходногоДокументаУменьшение;
		Если Реквизиты.Исправление Тогда
			Реквизиты.НомерИсправления = НомерИсправленияИсходногоДокумента + 1;
		КонецЕсли;

	КонецЦикла;

	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция ТекстЗапросаРеквизитыИсходногоСчетаФактуры(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыИсходногоСчетаФактуры", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетФактураВыданный.Ссылка.Дата КАК Дата,
	|	СчетФактураВыданный.Ссылка.Номер КАК Номер,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Ссылка.ВидСчетаФактуры = &КорректировочныйСчетФактура
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировочныйСчетФактура,
	|	СчетФактураВыданный.Ссылка.Исправление КАК Исправление,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Ссылка.Исправление
	|			ТОГДА СчетФактураВыданный.Ссылка.НомерИсправления
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерИсправления,
	|	СчетФактураВыданный.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданный.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданный.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданный.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданный.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	СчетФактураВыданный.Ссылка.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.Ссылка.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.Ссылка.КодВидаОперации КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Ссылка.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА СчетФактураВыданный.Ссылка.КодВидаОперацииНаУменьшение
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодВидаОперацииНаУменьшение,
	|	СчетФактураВыданный.Ссылка.КППКонтрагента КАК КППКонтрагента,
	|	СчетФактураВыданный.Ссылка.СводныйКомиссионный КАК СводныйКомиссионный
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка = &ИсходныйСчетФактура
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаРеквизитыСчетаФактурыИсходногоДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыСчетаФактурыИсходногоДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса	=
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Дата КАК Дата,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Номер КАК Номер,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = &КорректировочныйСчетФактура
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировочныйСчетФактура,
	|	ЛОЖЬ КАК Исправление,
	|	0 КАК НомерИсправления,
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.КодВидаОперации КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА СчетФактураВыданныйДокументыОснования.Ссылка.КодВидаОперацииНаУменьшение
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодВидаОперацииНаУменьшение,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.КППКонтрагента КАК КППКонтрагента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.СводныйКомиссионный КАК СводныйКомиссионный
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование = &ИсходныйДокументРеализации
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.Исправление
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураВыданныйДокументыОснования.Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаНомерИсправления(НомераТаблиц)

	НомераТаблиц.Вставить("НомерИсправления", НомераТаблиц.Количество());
	
	ТекстЗапроса	=
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.НомерИсправления КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Дата КАК ДатаИсправленияИсходногоДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование = &ДокументРеализации
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.Исправление
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураВыданныйДокументыОснования.Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиСчетовФактур";
	КомандаПечати.СписокФорм    = ""
			+ "ФормаСписка,"
			+ "ФормаДокументаНаРеализацию,"
			+ "ФормаДокументаНаАванс,"
			+ "ФормаДокументаНаСуммовуюРазницу,"
			+ "ФормаДокументаНалоговыйАгент,"
			+ "ФормаДокументаКорректировочный";
	
	// Универсальный передаточный документ
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьУПД) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "УниверсальныйПередаточныйДокумент";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)'");
		КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиУниверсальныхПередаточныхДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокументаНаРеализацию";
	КонецЕсли;
	
	// Универсальный корректировочный документ
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьУКД) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "УниверсальныйКорректировочныйДокумент";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД)'");
		КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиУниверсальныхКорректировочныхДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокументаКорректировочный";
	КонецЕсли;
	
	// Сводная справка по розничным продажам
	Если ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля") Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СводнаяСправка";
		КомандаПечати.Представление = НСтр("ru = 'Сводная справка'");
		КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиСводныхСправок";
		КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокументаСводнаяСправка";
		КомандаПечати.Порядок       = 80;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "КорректировочнаяСправка";
		КомандаПечати.Представление = НСтр("ru = 'Корректировочная справка'");
		КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиКорректировочныхСправок";
		КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокументаКорректировочнаяСправка";
		КомандаПечати.Порядок       = 85;
		
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		// Печать конвертов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Конверт";
		КомандаПечати.Представление = НСтр("ru = 'Конверт'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиКонверта";
		КомандаПечати.СписокФорм    = ""
			+ "ФормаСписка,"
			+ "ФормаДокументаНаРеализацию,"
			+ "ФормаДокументаНаАванс,"
			+ "ФормаДокументаНаСуммовуюРазницу,"
			+ "ФормаДокументаНалоговыйАгент,"
			+ "ФормаДокументаКорректировочный";
		КомандаПечати.Порядок       = 90;
	КонецЕсли;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Счет-фактура выданный""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка,Обработка.ПеренумерацияДокументов.Форма.Форма";
	КомандаПечати.Порядок       = 100;

	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"СчетФактура", "Счет-фактура",
			УчетНДСБП.ПечатьСчетовФактур(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьСчетовФактур(Ложь,Ложь,Ложь), ПараметрыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура1137") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"СчетФактура1137", "Счет-фактура",
			УчетНДС.ПечатьСчетовФактур1137(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьСчетовФактур(,,Ложь), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_СчетФактура1137");
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура981") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"СчетФактура981", "Счет-фактура",
			УчетНДС.ПечатьСчетовФактур981(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьСчетовФактур(,,Истина), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_СчетФактура981");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировочныйСчетФактура") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"КорректировочныйСчетФактура", "Корректировочный счет-фактура",
			УчетНДСБП.ПечатьКорректировочныхСчетовФактур(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьКорректировочныхСчетовФактур(Ложь, Ложь,,Ложь), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_КорректировочныйСчетФактура");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировочныйСчетФактура1137") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"КорректировочныйСчетФактура1137", "Корректировочный счет-фактура",
			УчетНДС.ПечатьКорректировочныхСчетовФактур1137(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьКорректировочныхСчетовФактур(, Ложь,,Ложь), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_КорректировочныйСчетФактура1137");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировочныйСчетФактура952") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"КорректировочныйСчетФактура952", "Корректировочный счет-фактура",
			УчетНДС.ПечатьКорректировочныхСчетовФактур952(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьКорректировочныхСчетовФактур(,,,Ложь), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_КорректировочныйСчетФактура952");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировочныйСчетФактура981") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"КорректировочныйСчетФактура981", "Корректировочный счет-фактура",
			УчетНДС.ПечатьКорректировочныхСчетовФактур981(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьКорректировочныхСчетовФактур(,,,Истина), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_КорректировочныйСчетФактура981");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировочныйСчетФактура981Аванс") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "КорректировочныйСчетФактура981Аванс", "Корректировочный счет-фактура",
			УчетНДС.ПечатьКорректировочныхСчетовФактур981Аванс(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьКорректировочныхСчетовФактур(,,,Истина), ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_КорректировочныйСчетФактура981Аванс");
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СводнаяСправка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"СводнаяСправка", "Сводная справка по розничным продажам",
			УчетНДСБП.ПечатьСводныхСправок(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьСводныхСправок(), ПараметрыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировочнаяСправка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"КорректировочнаяСправка", "Корректировочная справка по розничным продажам",
			УчетНДСБП.ПечатьКорректировочныхСправок(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПечатьКорректировочныхСправок(), ПараметрыПечати));
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов,
		КоллекцияПечатныхФорм,
		ОбъектыПечати,
		ПараметрыВывода);
	
КонецПроцедуры

Функция ТекстЗапросаПечатьСчетовФактур(ИспользуетсяПостановлениеНДС1137 = Истина, ДляУниверсальногоПередаточногоДокумента = Ложь, ИспользуетсяПостановлениеНДС981 = Неопределено) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА СчетФактура.Исправление
	|			ТОГДА СчетФактура.ДатаИсходногоДокумента
	|		ИНАЧЕ СчетФактура.Дата
	|	КОНЕЦ КАК Дата,
	|	ВЫБОР
	|		КОГДА СчетФактура.Исправление
	|			ТОГДА СчетФактура.НомерИсходногоДокумента
	|		ИНАЧЕ СчетФактура.Номер
	|	КОНЕЦ КАК Номер,
	|	СчетФактура.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	СчетФактура.Руководитель КАК Руководитель,
	|	СчетФактура.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	СчетФактура.СчетФактураБезНДС КАК СчетФактураБезНДС,
	|	СчетФактура.Исправление КАК Исправление,
	|	СчетФактура.НомерИсправления КАК НомерИсправления,
	|	СчетФактура.Дата КАК ДатаИсправления,
	|	СчетФактура.Дата КАК ДатаСведений,
	|	НЕ СчетФактура.Исправление КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	СчетФактураДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураДокументыОснования.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА СчетФактура.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА СчетФактура.Продавец
	|		ИНАЧЕ СчетФактура.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА СчетФактура.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СчетФактура.ДоговорКонтрагента
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	СчетФактура.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	СчетФактура.КППКонтрагента КАК КППСчетаФактуры,
	|	СчетФактура.КПППродавца КАК КПППродавца,
	|	СчетФактура.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА СчетФактура.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА СчетФактура.ДоговорКонтрагента.ВидДоговора
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
	|	КОНЕЦ КАК ВидДоговора,
	|	СчетФактура.Организация.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
	|	ЛОЖЬ КАК НеподтверждениеНулевойСтавки,
	|	СчетФактура.СводныйКомиссионный КАК СводныйКомиссионный,
	|	ЕСТЬNULL(СчетФактура.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ) КАК СчетаФактурыОтИмениОрганизации
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактураДокументыОснования.Ссылка = СчетФактура.Ссылка
	|ГДЕ
	|	СчетФактураДокументыОснования.Ссылка В(&МассивОбъектов)
	|	И ВЫБОР
	|			КОГДА СчетФактура.Исправление
	|				ТОГДА СчетФактура.ДатаИсходногоДокумента > &УсловиеПоДате
	|			ИНАЧЕ СчетФактура.Дата > &УсловиеПоДате
	|		КОНЕЦ
	|	И СчетФактура.ДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|	И ТИПЗНАЧЕНИЯ(СчетФактураДокументыОснования.ДокументОснование) <> ТИП(Документ.ПодтверждениеНулевойСтавкиНДС)";
	
	Если ДляУниверсальногоПередаточногоДокумента Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И ВЫБОР
		|			КОГДА СчетФактура.Исправление
		|				ТОГДА СчетФактура.ДатаИсходногоДокумента >= ДАТАВРЕМЯ(2013, 1, 1)
		|			ИНАЧЕ СчетФактура.Дата >= ДАТАВРЕМЯ(2013, 1, 1)
		|		КОНЕЦ
		|	И ТИПЗНАЧЕНИЯ(СчетФактураДокументыОснования.ДокументОснование) <> ТИП(Документ.ОтчетКомиссионераОПродажах)
		|	И ТИПЗНАЧЕНИЯ(СчетФактураДокументыОснования.ДокументОснование) <> ТИП(Документ.ОтражениеНачисленияНДС)
		|	И ТИПЗНАЧЕНИЯ(СчетФактураДокументыОснования.ДокументОснование) <> ТИП(Документ.НачислениеНДСпоСМРхозспособом)";
	КонецЕсли;
	
	// СФ на аванс
	ТекстЗапроса = ТекстЗапроса + ?(ПустаяСтрока(ТекстЗапроса), "", " ОБЪЕДИНИТЬ ВСЕ ") +
	"ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА СчетФактура.Исправление
	|			ТОГДА СчетФактура.ДатаИсходногоДокумента
	|		ИНАЧЕ СчетФактура.Дата
	|	КОНЕЦ КАК Дата,
	|	ВЫБОР
	|		КОГДА СчетФактура.Исправление
	|			ТОГДА СчетФактура.НомерИсходногоДокумента
	|		ИНАЧЕ СчетФактура.Номер
	|	КОНЕЦ КАК Номер,
	|	СчетФактура.ВидСчетаФактуры,
	|	СчетФактура.Руководитель,
	|	СчетФактура.ГлавныйБухгалтер,
	|	ЛОЖЬ КАК СчетФактураБезНДС,
	|	СчетФактура.Исправление,
	|	СчетФактура.НомерИсправления,
	|	СчетФактура.Дата КАК ДатаИсправления,
	|	СчетФактура.Дата КАК ДатаСведений,
	|	НЕ СчетФактура.Исправление КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ЛОЖЬ КАК ВыводитьСуммуБезНДС,
	|	СчетФактура.Ссылка КАК ДокументОснование,
	|	1 КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА СчетФактура.Контрагент.ОбособленноеПодразделение
	|				И СчетФактура.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА СчетФактура.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ СчетФактура.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	СчетФактура.ДоговорКонтрагента,
	|	СчетФактура.ИдентификаторГосКонтракта,
	|	СчетФактура.КППКонтрагента КАК КППСчетаФактуры,
	|	СчетФактура.КПППродавца КАК КПППродавца,
	|	СчетФактура.Организация,
	|	ВЫБОР
	|		КОГДА СчетФактура.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА СчетФактура.ДоговорКонтрагента.ВидДоговора
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
	|	КОНЕЦ КАК ВидДоговора,
	|	СчетФактура.Организация.ОбособленноеПодразделение,
	|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения,
	|	ЛОЖЬ КАК НеподтверждениеНулевойСтавки,
	|	СчетФактура.СводныйКомиссионный КАК СводныйКомиссионный,
	|	ЕСТЬNULL(СчетФактура.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ) КАК СчетаФактурыОтИмениОрганизации
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.Ссылка В(&МассивОбъектов)
	|	И ВЫБОР
	|			КОГДА СчетФактура.Исправление
	|				ТОГДА СчетФактура.ДатаИсходногоДокумента > &УсловиеПоДате
	|			ИНАЧЕ СчетФактура.Дата > &УсловиеПоДате
	|		КОНЕЦ
	|	И СчетФактура.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку))";
	
	Если НЕ ДляУниверсальногоПередаточногоДокумента Тогда
		
		ТекстЗапроса = ТекстЗапроса + ?(ПустаяСтрока(ТекстЗапроса), "", " ОБЪЕДИНИТЬ ВСЕ ") +
		"ВЫБРАТЬ
		|	СчетФактура.Ссылка,
		|	СчетФактура.Дата,
		|	СчетФактура.Номер,
		|	СчетФактура.ВидСчетаФактуры,
		|	СчетФактура.Руководитель,
		|	СчетФактура.ГлавныйБухгалтер,
		|	ЛОЖЬ,
		|	СчетФактура.Исправление,
		|	СчетФактура.НомерИсправления,
		|	СчетФактура.Дата,
		|	СчетФактура.Дата,
		|	ИСТИНА,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	СчетФактура.Ссылка,
		|	1,
		|	ВЫБОР
		|		КОГДА СчетФактура.Контрагент.ОбособленноеПодразделение
		|				И СчетФактура.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|			ТОГДА СчетФактура.Контрагент.ГоловнойКонтрагент
		|		ИНАЧЕ СчетФактура.Контрагент
		|	КОНЕЦ,
		|	СчетФактура.ДоговорКонтрагента,
		|	"""",
		|	СчетФактура.КППКонтрагента,
		|	СчетФактура.КПППродавца КАК КПППродавца,
		|	СчетФактура.Организация,
		|	ВЫБОР
		|		КОГДА СчетФактура.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|			ТОГДА СчетФактура.ДоговорКонтрагента.ВидДоговора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
		|	КОНЕЦ,
		|	СчетФактура.Организация.ОбособленноеПодразделение,
		|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения,
		|	ЛОЖЬ,
		|	СчетФактура.СводныйКомиссионный,
		|	ЕСТЬNULL(СчетФактура.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ)
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактура
		|ГДЕ
		|	СчетФактура.Ссылка В(&МассивОбъектов)
		|	И ВЫБОР
		|			КОГДА СчетФактура.Исправление
		|				ТОГДА СчетФактура.ДатаИсходногоДокумента > &УсловиеПоДате
		|			ИНАЧЕ СчетФактура.Дата > &УсловиеПоДате
		|		КОНЕЦ
		|	И СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СчетФактура.Ссылка,
		|	ВЫБОР
		|		КОГДА СчетФактура.Исправление
		|			ТОГДА СчетФактура.ДатаИсходногоДокумента
		|		ИНАЧЕ СчетФактура.Дата
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СчетФактура.Исправление
		|			ТОГДА СчетФактура.НомерИсходногоДокумента
		|		ИНАЧЕ СчетФактура.Номер
		|	КОНЕЦ,
		|	СчетФактура.ВидСчетаФактуры,
		|	СчетФактура.Руководитель,
		|	СчетФактура.ГлавныйБухгалтер,
		|	СчетФактура.СчетФактураБезНДС,
		|	СчетФактура.Исправление,
		|	СчетФактура.НомерИсправления,
		|	СчетФактура.Дата,
		|	СчетФактура.Дата,
		|	НЕ СчетФактура.Исправление,
		|	ИСТИНА,
		|	ИСТИНА,
		|	СчетФактура.Ссылка,
		|	1,
		|	ВЫБОР
		|		КОГДА СчетФактура.Контрагент.ОбособленноеПодразделение
		|				И СчетФактура.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|			ТОГДА СчетФактура.Контрагент.ГоловнойКонтрагент
		|		ИНАЧЕ СчетФактура.Контрагент
		|	КОНЕЦ,
		|	СчетФактура.ДоговорКонтрагента,
		|	"""",
		|	СчетФактура.КППКонтрагента,
		|	СчетФактура.КПППродавца КАК КПППродавца,
		|	СчетФактура.Организация,
		|	ВЫБОР
		|		КОГДА СчетФактура.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|			ТОГДА СчетФактура.ДоговорКонтрагента.ВидДоговора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
		|	КОНЕЦ,
		|	СчетФактура.Организация.ОбособленноеПодразделение,
		|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения,
		|	ЛОЖЬ,
		|	СчетФактура.СводныйКомиссионный,
		|	ЕСТЬNULL(СчетФактура.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ)
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактура
		|ГДЕ
		|	СчетФактура.Ссылка В(&МассивОбъектов)
		|	И ВЫБОР
		|			КОГДА СчетФактура.Исправление
		|				ТОГДА СчетФактура.ДатаИсходногоДокумента > &УсловиеПоДате
		|			ИНАЧЕ СчетФактура.Дата > &УсловиеПоДате
		|		КОНЕЦ
		|	И СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СчетФактура.Ссылка,
		|	ВЫБОР
		|		КОГДА СчетФактура.Исправление
		|			ТОГДА СчетФактура.ДатаИсходногоДокумента
		|		ИНАЧЕ СчетФактура.Дата
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СчетФактура.Исправление
		|			ТОГДА СчетФактура.НомерИсходногоДокумента
		|		ИНАЧЕ СчетФактура.Номер
		|	КОНЕЦ,
		|	СчетФактура.ВидСчетаФактуры,
		|	СчетФактура.Руководитель,
		|	СчетФактура.ГлавныйБухгалтер,
		|	СчетФактура.СчетФактураБезНДС,
		|	СчетФактура.Исправление,
		|	СчетФактура.НомерИсправления,
		|	СчетФактура.Дата,
		|	СчетФактура.Дата,
		|	НЕ СчетФактура.Исправление,
		|	ИСТИНА,
		|	ИСТИНА,
		|	ПодтверждениеНулевойСтавкиСостав.ДокументОтгрузки,
		|	ПодтверждениеНулевойСтавкиСостав.НомерСтроки,
		|	ВЫБОР
		|		КОГДА СчетФактура.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА СчетФактура.Продавец
		|		ИНАЧЕ СчетФактура.Контрагент
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СчетФактура.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ СчетФактура.ДоговорКонтрагента
		|	КОНЕЦ,
		|	СчетФактура.ИдентификаторГосКонтракта,
		|	СчетФактура.КППКонтрагента,
		|	СчетФактура.КПППродавца КАК КПППродавца,
		|	СчетФактура.Организация,
		|	ВЫБОР
		|		КОГДА СчетФактура.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|			ТОГДА СчетФактура.ДоговорКонтрагента.ВидДоговора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
		|	КОНЕЦ,
		|	СчетФактура.Организация.ОбособленноеПодразделение,
		|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения,
		|	ИСТИНА,
		|	СчетФактура.СводныйКомиссионный,
		|	ЕСТЬNULL(СчетФактура.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ)
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК ПодтверждениеНулевойСтавкиСостав
		|		ПО (ПодтверждениеНулевойСтавкиСостав.СчетФактураВыданный = СчетФактура.Ссылка)
		|ГДЕ
		|	СчетФактура.Ссылка В(&МассивОбъектов)
		|	И ВЫБОР
		|			КОГДА СчетФактура.Исправление
		|				ТОГДА СчетФактура.ДатаИсходногоДокумента > &УсловиеПоДате
		|			ИНАЧЕ СчетФактура.Дата > &УсловиеПоДате
		|		КОНЕЦ
		|	И СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
		|	И ТИПЗНАЧЕНИЯ(СчетФактура.ДокументОснование) = ТИП(Документ.ПодтверждениеНулевойСтавкиНДС)
		|	И ТИПЗНАЧЕНИЯ(ПодтверждениеНулевойСтавкиСостав.ДокументОтгрузки) <> ТИП(Документ.СчетФактураВыданный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СчетФактураПоСтавке0.Ссылка,
		|	ВЫБОР
		|		КОГДА СчетФактура.Исправление
		|			ТОГДА СчетФактура.ДатаИсходногоДокумента
		|		ИНАЧЕ СчетФактура.Дата
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СчетФактура.Исправление
		|			ТОГДА СчетФактура.НомерИсходногоДокумента
		|		ИНАЧЕ СчетФактура.Номер
		|	КОНЕЦ,
		|	СчетФактура.ВидСчетаФактуры,
		|	СчетФактура.Руководитель,
		|	СчетФактура.ГлавныйБухгалтер,
		|	СчетФактура.СчетФактураБезНДС,
		|	СчетФактура.Исправление,
		|	СчетФактура.НомерИсправления,
		|	СчетФактура.Дата,
		|	СчетФактура.Дата,
		|	НЕ СчетФактура.Исправление,
		|	ИСТИНА,
		|	ИСТИНА,
		|	СчетФактураПоСтавке0.ДокументОснование,
		|	ПодтверждениеНулевойСтавкиСостав.НомерСтроки,
		|	ВЫБОР
		|		КОГДА СчетФактураПоСтавке0.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА СчетФактураПоСтавке0.Продавец
		|		ИНАЧЕ СчетФактураПоСтавке0.Контрагент
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СчетФактураПоСтавке0.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ СчетФактураПоСтавке0.ДоговорКонтрагента
		|	КОНЕЦ,
		|	СчетФактура.ИдентификаторГосКонтракта,
		|	СчетФактура.КППКонтрагента,
		|	СчетФактура.КПППродавца КАК КПППродавца,
		|	СчетФактура.Организация,
		|	ВЫБОР
		|		КОГДА СчетФактураПоСтавке0.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|			ТОГДА СчетФактураПоСтавке0.ДоговорКонтрагента.ВидДоговора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
		|	КОНЕЦ,
		|	СчетФактура.Организация.ОбособленноеПодразделение,
		|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения,
		|	ИСТИНА,
		|	СчетФактура.СводныйКомиссионный,
		|	ЕСТЬNULL(СчетФактура.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ)
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК ПодтверждениеНулевойСтавкиСостав
		|		ПО (ПодтверждениеНулевойСтавкиСостав.СчетФактураВыданный = СчетФактура.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураПоСтавке0
		|		ПО (ПодтверждениеНулевойСтавкиСостав.ДокументОтгрузки = СчетФактураПоСтавке0.Ссылка)
		|ГДЕ
		|	СчетФактура.Ссылка В(&МассивОбъектов)
		|	И ВЫБОР
		|			КОГДА СчетФактура.Исправление
		|				ТОГДА СчетФактура.ДатаИсходногоДокумента > &УсловиеПоДате
		|			ИНАЧЕ СчетФактура.Дата > &УсловиеПоДате
		|		КОНЕЦ
		|	И СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
		|	И ТИПЗНАЧЕНИЯ(СчетФактура.ДокументОснование) = ТИП(Документ.ПодтверждениеНулевойСтавкиНДС)
		|	И ТИПЗНАЧЕНИЯ(ПодтверждениеНулевойСтавкиСостав.ДокументОтгрузки) = ТИП(Документ.СчетФактураВыданный)";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	СчетФактура,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Номер),
	|	МАКСИМУМ(ВидСчетаФактуры),
	|	МАКСИМУМ(СчетФактураБезНДС),
	|	МАКСИМУМ(Исправление),
	|	МАКСИМУМ(НомерИсправления),
	|	МАКСИМУМ(ДатаИсправления),
	|	МАКСИМУМ(УдалитьПрефиксыИзНомера),
	|	МАКСИМУМ(ЭтоСчетФактураВыданный),
	|	МАКСИМУМ(ВыводитьСуммуБезНДС),
	|	МАКСИМУМ(НеподтверждениеНулевойСтавки)
	|ПО
	|	СчетФактура" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка КАК СчетФактура,
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.НомерСтроки КАК НомерСтроки,
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.ДатаДокумента КАК ДатаДокумента,
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.НомерДокумента КАК НомерДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент
	|ИЗ
	|	Документ.СчетФактураВыданный.ПлатежноРасчетныеДокументы КАК СчетФактураВыданныйПлатежноРасчетныеДокументы
	|ГДЕ
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка В(&МассивОбъектов)
	|	И НЕ(СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|				ИЛИ СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|				ИЛИ СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданныйАвансы.Ссылка,
	|	СчетФактураВыданныйАвансы.НомерСтроки,
	|	СчетФактураВыданныйАвансы.ДатаПлатежноРасчетногоДокумента,
	|	СчетФактураВыданныйАвансы.НомерПлатежноРасчетногоДокумента,
	|	СчетФактураВыданныйАвансы.Контрагент
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка В(&МассивОбъектов)
	|	И СчетФактураВыданныйАвансы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку)
	|	И СчетФактураВыданныйАвансы.Ссылка.СводныйКомиссионный";
	
	Если ДляУниверсальногоПередаточногоДокумента Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СчетФактураВыданный.Ссылка,
		|	1,
		|	СчетФактураВыданный.ДатаПлатежноРасчетногоДокумента,
		|	СчетФактураВыданный.НомерПлатежноРасчетногоДокумента,
		|	СчетФактураВыданный.Контрагент
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|ГДЕ
		|	СчетФактураВыданный.Ссылка В(&МассивОбъектов)
		|	И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)";
		
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|" + 
		"ВЫБРАТЬ
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка,
		|	1,
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.ДатаПлатежноРасчетногоДокумента,
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.НомерПлатежноРасчетногоДокумента,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданныйПлатежноРасчетныеДокументы
		|ГДЕ
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка В(&МассивОбъектов)
		|	И (СчетФактураВыданныйПлатежноРасчетныеДокументы.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
		|			ИЛИ СчетФактураВыданныйПлатежноРасчетныеДокументы.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
		|			ИЛИ СчетФактураВыданныйПлатежноРасчетныеДокументы.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку)
		|				И НЕ СчетФактураВыданныйПлатежноРасчетныеДокументы.СводныйКомиссионный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка,
		|	2,
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.ДатаДокументаАвансаКомитента,
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.НомерДокументаАвансаКомитента,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданныйПлатежноРасчетныеДокументы
		|ГДЕ
		|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка В(&МассивОбъектов)
		|	И СчетФактураВыданныйПлатежноРасчетныеДокументы.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку)";

		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Если ИспользуетсяПостановлениеНДС1137 Тогда
		
		Если ИспользуетсяПостановлениеНДС981 = Неопределено Тогда 
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				">= &НачалоПримененияПостановления1137");
		ИначеЕсли ИспользуетсяПостановлениеНДС981 Тогда 
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				">= ДАТАВРЕМЯ(2017,10,1)");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				"МЕЖДУ &НачалоПримененияПостановления1137 И ДАТАВРЕМЯ(2017,09,30,23,59,59)");
		КонецЕсли;
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"< &НачалоПримененияПостановления1137");
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПечатьКорректировочныхСчетовФактур(ИспользуетсяПостановлениеНДС1137 = Истина, ИспользуетсяПостановлениеНДС952 = Истина, ДляУниверсальногоКорректировочногоДокумента = Ложь, ИспользуетсяПостановлениеНДС981 = Неопределено) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыОснования.Ссылка КАК СчетФактура,
	|	ДокументыОснования.Ссылка.Дата КАК Дата,
	|	ДокументыОснования.Ссылка.Номер КАК Номер,
	|	ДокументыОснования.Ссылка.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	ДокументыОснования.Ссылка.Исправление КАК Исправление,
	|	ДокументыОснования.Ссылка.НомерИсправления КАК НомерИсправления,
	|	ДокументыОснования.Ссылка.Дата КАК ДатаИсправления,
	|	ДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	ДокументыОснования.Ссылка.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	ДокументыОснования.Ссылка.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
	|	ДокументыОснования.Ссылка.Дата КАК ДатаСведений,
	|	ДокументыОснования.Ссылка.СчетФактураБезНДС КАК СчетФактураБезНДС,
	|	ВЫБОР
	|		КОГДА ДокументыОснования.Ссылка.Контрагент.ОбособленноеПодразделение
	|				И ДокументыОснования.Ссылка.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА ДокументыОснования.Ссылка.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДокументыОснования.Ссылка.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДокументыОснования.Ссылка.КППКонтрагента КАК КППСчетаФактуры,
	|	ИСТИНА КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.НомерСтроки КАК НомерСтроки,
	|	ДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	ДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	ДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	ДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	ДокументыОснования.Ссылка.Руководитель КАК Руководитель,
	|	ДокументыОснования.Ссылка.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ДокументыОснования.Ссылка.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК ДокументыОснования
	|ГДЕ
	|	ДокументыОснования.Ссылка В(&МассивОбъектов)
	|	И ДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|	И ВЫБОР
	|			КОГДА ДокументыОснования.Ссылка.Исправление
	|				ТОГДА ДокументыОснования.Ссылка.ДатаИсправляемогоКорректировочногоДокумента > &УсловиеПоДате
	|			ИНАЧЕ ДокументыОснования.Ссылка.Дата > &УсловиеПоДате
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданныйАвансы.Ссылка,
	|	СчетФактураВыданныйАвансы.Ссылка.Дата,
	|	СчетФактураВыданныйАвансы.Ссылка.Номер,
	|	СчетФактураВыданныйАвансы.Ссылка.ВидСчетаФактуры,
	|	СчетФактураВыданныйАвансы.Ссылка.Исправление,
	|	СчетФактураВыданныйАвансы.Ссылка.НомерИсправления,
	|	СчетФактураВыданныйАвансы.Ссылка.Дата,
	|	ЛОЖЬ,
	|	"""""""",
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	СчетФактураВыданныйАвансы.Ссылка.Дата,
	|	СчетФактураВыданныйАвансы.Ссылка.СчетФактураБезНДС,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйАвансы.Ссылка.Контрагент.ОбособленноеПодразделение
	|				И СчетФактураВыданныйАвансы.Ссылка.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА СчетФактураВыданныйАвансы.Ссылка.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ СчетФактураВыданныйАвансы.Ссылка.Контрагент
	|	КОНЕЦ,
	|	СчетФактураВыданныйАвансы.Ссылка.КППКонтрагента,
	|	ИСТИНА,
	|	ИСТИНА,
	|	СчетФактураВыданныйАвансы.Ссылка,
	|	СчетФактураВыданныйАвансы.НомерСтроки,
	|	ИсходныйСчетФактураРеквизиты.НомерСчетаФактуры,
	|	ИсходныйСчетФактураРеквизиты.ДатаСчетаФактуры,
	|	"""""""",
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	СчетФактураВыданныйАвансы.Ссылка.Руководитель,
	|	СчетФактураВыданныйАвансы.Ссылка.ГлавныйБухгалтер,
	|	СчетФактураВыданныйАвансы.Ссылка.ИдентификаторГосКонтракта
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналУчетаСчетовФактур КАК ИсходныйСчетФактураРеквизиты
	|		ПО СчетФактураВыданныйАвансы.КорректируемыйСчетФактура = ИсходныйСчетФактураРеквизиты.СчетФактура
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс)
	|	И СчетФактураВыданныйАвансы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	СчетФактура,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Номер),
	|	МАКСИМУМ(ВидСчетаФактуры),
	|	МАКСИМУМ(Исправление),
	|	МАКСИМУМ(НомерИсправления),
	|	МАКСИМУМ(ДатаИсправления),
	|	МАКСИМУМ(УчитыватьИсправлениеИсходногоДокумента),
	|	МАКСИМУМ(НомерИсправляемогоКорректировочногоДокумента),
	|	МАКСИМУМ(ДатаИсправляемогоКорректировочногоДокумента),
	|	МАКСИМУМ(СчетФактураБезНДС),
	|	МАКСИМУМ(УдалитьПрефиксыИзНомера),
	|	МАКСИМУМ(ЭтоСчетФактураВыданный),
	|	МАКСИМУМ(НомерИсходногоДокумента),
	|	МАКСИМУМ(ДатаИсходногоДокумента),
	|	МАКСИМУМ(НомерИсправленияИсходногоДокумента),
	|	МАКСИМУМ(ДатаИсправленияИсходногоДокумента)
	|ПО
	|	СчетФактура";
	
	Если ДляУниверсальногоКорректировочногоДокумента Тогда
		
		Если ИспользуетсяПостановлениеНДС981 = Неопределено Тогда 
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				">= ДАТАВРЕМЯ(2013,1,1)");
		ИначеЕсли ИспользуетсяПостановлениеНДС981 Тогда 
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				">= ДАТАВРЕМЯ(2017,10,1)");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				"МЕЖДУ ДАТАВРЕМЯ(2013,1,1) И ДАТАВРЕМЯ(2017,09,30,23,59,59)");
		КонецЕсли;
	ИначеЕсли ИспользуетсяПостановлениеНДС1137 Тогда
		
		Если ИспользуетсяПостановлениеНДС952 Тогда
			
			Если ИспользуетсяПостановлениеНДС981 = Неопределено Тогда 
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
					">= ДАТАВРЕМЯ(2013,11,6)");
			ИначеЕсли ИспользуетсяПостановлениеНДС981 Тогда 
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
					">= ДАТАВРЕМЯ(2017,10,1)");
			Иначе
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
					"МЕЖДУ ДАТАВРЕМЯ(2013,11,6) И ДАТАВРЕМЯ(2017,09,30,23,59,59) ");
			КонецЕсли;
		Иначе	
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				"МЕЖДУ &НачалоПримененияПостановления1137 И ДАТАВРЕМЯ(2013,11,5,23,59,59)");
		КонецЕсли; 
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"< &НачалоПримененияПостановления1137");
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаДанныеДляПечатиКорректировочныхСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Поставщик,
	|	Реквизиты.Организация.ИНН КАК ИННПоставщика,
	|	Реквизиты.Организация КАК ОбособленноеПодразделениеПоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Покупатель,
	|	Реквизиты.Контрагент.ИНН КАК ИННпокупателя,
	|	Реквизиты.Контрагент КАК ОбособленноеПодразделениеПокупателя,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.Представление КАК Основание,
	|	Реквизиты.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	Реквизиты.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем КАК НДСИсчисляетсяНалоговымАгентом,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	"""" КАК АдресПоставщика,
	|	"""" КАК ИННКПППоставщика,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ИСТИНА КАК СуммаВключаетНДС,
	|	ЛОЖЬ КАК БратьСуммыИзРегистраРублевыеСуммы,
	|	Реквизиты.Исправление КАК Исправление,
	|	Реквизиты.Руководитель КАК ОтветственныйЗаОформление,
	|	"""" КАК СопроводительныеДокументы
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	Авансы.НомерСтроки КАК НомерСтроки,
	|	Авансы.Номенклатура КАК Товар,
	|	Авансы.Номенклатура.Код КАК ТоварКод,
	|	Авансы.Номенклатура.Артикул КАК ТоварАртикул,
	|	Авансы.Содержание КАК ТоварНаименование,
	|	НЕОПРЕДЕЛЕНО КАК СтранаПроисхождения,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеСтраны,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеГТД,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения,
	|	0 КАК КоличествоДоИзменения,
	|	0 КАК КоличествоПослеИзменения,
	|	0 КАК ЦенаДоИзменения,
	|	0 КАК ЦенаПослеИзменения,
	|	0 КАК СтоимостьБезНДСДоИзменения,
	|	0 КАК СтоимостьБезНДСПослеИзменения,
	|	Авансы.СуммаНДС КАК СуммаНДСПослеИзменения,
	|	Авансы.СуммаНДСДоКорректировки КАК СуммаНДСДоИзменения,
	|	Авансы.Сумма КАК СтоимостьСНДСПослеИзменения,
	|	Авансы.СуммаДоКорректировки КАК СтоимостьСНДСДоИзменения,
	|	0 КАК РазницаБезНДСУменьшение,
	|	0 КАК РазницаБезНДСУвеличение,
	|	0 КАК РазницаНДСУменьшение,
	|	Авансы.СуммаНДС - Авансы.СуммаНДСДоКорректировки КАК РазницаНДСУвеличение,
	|	0 КАК РазницаСНДСУменьшение,
	|	Авансы.Сумма - Авансы.СуммаДоКорректировки КАК РазницаСНДСУвеличение,
	|	Авансы.СтавкаНДС КАК СтавкаНДСПослеИзменения,
	|	Авансы.СтавкаНДСДоКорректировки КАК СтавкаНДСДоИзменения,
	|	ИСТИНА КАК ЭтоУслуга,
	|	Авансы.Ссылка КАК Ссылка,
	|	"""" КАК ТоварКодТНВЭД,
	|	"""" КАК ТоварКодТНВЭДДоИзменения
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК Авансы
	|ГДЕ
	|	Авансы.Ссылка = &ДокументОснование";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаДанныеДляПечатиСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("РеквизитыПредварительная",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",  			   	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента", 	НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	&ПустоеПодразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ЦифровойИндексОбособленногоПодразделения <> 0
	|			ТОГДА &ЦифровойИндексОбособленногоПодразделения
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Поставщик,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация.ИНН
	|		ИНАЧЕ Реквизиты.Организация.ИНН
	|	КОНЕЦ КАК ИННпоставщика,
	|	Реквизиты.Организация КАК ОбособленноеПодразделениеПоставщика,
	|	НЕОПРЕДЕЛЕНО КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА ВЫРАЗИТЬ(&ПредставлениеПокупателя КАК СТРОКА(1000))
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Покупатель,
	|	Реквизиты.Контрагент.ИНН КАК ИННпокупателя,
	|	Реквизиты.Контрагент КАК ОбособленноеПодразделениеПокупателя,
	|	НЕОПРЕДЕЛЕНО КАК Грузополучатель,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.Представление КАК Основание,
	|	Реквизиты.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	Реквизиты.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем, ЛОЖЬ) КАК НДСИсчисляетсяНалоговымАгентом,
	|	Реквизиты.Руководитель КАК Исполнитель,
	|	НЕОПРЕДЕЛЕНО КАК ИсполнительНаОсновании,
	|	НЕОПРЕДЕЛЕНО КАК ОтпускПроизвел,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьЧерезКого,
	|	НЕОПРЕДЕЛЕНО КАК ЗаЗаказчикаНаОсновании,
	|	ЛОЖЬ КАК ЕстьТовары,
	|	"""" КАК АдресДоставки,
	|	"""" КАК СведенияОТранспортировкеИГрузе,
	|	НЕОПРЕДЕЛЕНО КАК ОтветственныйЗаОформление,
	|	"""" КАК СопроводительныеДокументы,
	|	НЕОПРЕДЕЛЕНО КАК Перевозчик,
	|	ЛОЖЬ КАК ПеревозкаАвтотранспортом,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА ВЫРАЗИТЬ(&АдресПокупателя КАК СТРОКА(1000))
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК АдресПокупателя,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА ВЫРАЗИТЬ(&ИННКПППокупателя КАК СТРОКА(1000))
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИННКПППокупателя,
	|	&СводныйСФКомиссияПоПродаже КАК СводныйСФКомиссияПоПродаже,
	|	ЛОЖЬ КАК СводныйСФКомиссияПоЗакупке,
	|	"""" КАК АдресПоставщика,
	|	"""" КАК ИННКПППоставщика
	|ПОМЕСТИТЬ РеквизитыПредварительная
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|	И Реквизиты.ВидСчетаФактуры <> ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
	|	И Реквизиты.ВидСчетаФактуры <> ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	&ПустоеПодразделение,
	|	ВЫБОР
	|		КОГДА &ЦифровойИндексОбособленногоПодразделения <> 0
	|			ТОГДА &ЦифровойИндексОбособленногоПодразделения
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА ВЫРАЗИТЬ(&ПредставлениеПоставщика КАК СТРОКА(1000))
	|		КОГДА Реквизиты.Продавец.ОбособленноеПодразделение
	|				И Реквизиты.Продавец.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Продавец.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Продавец
	|	КОНЕЦ,
	|	Реквизиты.Продавец.ИНН,
	|	Реквизиты.Продавец,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА ВЫРАЗИТЬ(&ПредставлениеПокупателя КАК СТРОКА(1000))
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ,
	|	Реквизиты.Контрагент.ИНН,
	|	Реквизиты.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.Представление,
	|	Реквизиты.ДоговорКонтрагента.Дата,
	|	Реквизиты.ДоговорКонтрагента.Номер,
	|	ЛОЖЬ,
	|	Реквизиты.Руководитель,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	"""",
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА ВЫРАЗИТЬ(&АдресПокупателя КАК СТРОКА(1000))
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА ВЫРАЗИТЬ(&ИННКПППокупателя КАК СТРОКА(1000))
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	&СводныйСФКомиссияПоПродаже,
	|	&СводныйСФКомиссияПоЗакупке,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА ВЫРАЗИТЬ(&АдресПоставщика КАК СТРОКА(1000))
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА ВЫРАЗИТЬ(&ИННКПППоставщика КАК СТРОКА(1000))
	|		ИНАЧЕ """"
	|	КОНЕЦ
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|	И Реквизиты.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку))
	|	И Реквизиты.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	&ПустоеПодразделение,
	|	ВЫБОР
	|		КОГДА &ЦифровойИндексОбособленногоПодразделения <> 0
	|			ТОГДА &ЦифровойИндексОбособленногоПодразделения
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ,
	|	Реквизиты.Контрагент.ИНН,
	|	Реквизиты.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА Реквизиты.Комитент
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА Реквизиты.Комитент.ИНН
	|		ИНАЧЕ Реквизиты.Организация.ИНН
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА Реквизиты.Комитент
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.Представление,
	|	Реквизиты.ДоговорКонтрагента.Дата,
	|	Реквизиты.ДоговорКонтрагента.Номер,
	|	ЛОЖЬ,
	|	Реквизиты.Руководитель,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	"""",
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	"""",
	|	"""",
	|	&СводныйСФКомиссияПоПродаже,
	|	ЛОЖЬ,
	|	"""",
	|	""""
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|	И Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыПредварительная.ДатаОснования КАК ДатаОснования,
	|	РеквизитыПредварительная.Организация КАК Организация,
	|	РеквизитыПредварительная.Склад КАК Склад,
	|	РеквизитыПредварительная.Подразделение КАК Подразделение,
	|	РеквизитыПредварительная.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &ПредставлениеПоставщика
	|		ИНАЧЕ РеквизитыПредварительная.Поставщик
	|	КОНЕЦ КАК Поставщик,
	|	РеквизитыПредварительная.ИННпоставщика КАК ИННпоставщика,
	|	РеквизитыПредварительная.ОбособленноеПодразделениеПоставщика КАК ОбособленноеПодразделениеПоставщика,
	|	РеквизитыПредварительная.Грузоотправитель КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА &ПредставлениеПокупателя
	|		ИНАЧЕ РеквизитыПредварительная.Покупатель
	|	КОНЕЦ КАК Покупатель,
	|	РеквизитыПредварительная.ИННпокупателя КАК ИННпокупателя,
	|	РеквизитыПредварительная.ОбособленноеПодразделениеПокупателя КАК ОбособленноеПодразделениеПокупателя,
	|	РеквизитыПредварительная.Грузополучатель КАК Грузополучатель,
	|	РеквизитыПредварительная.Валюта КАК Валюта,
	|	РеквизитыПредварительная.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	РеквизитыПредварительная.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	РеквизитыПредварительная.ВидДоговора КАК ВидДоговора,
	|	РеквизитыПредварительная.Основание КАК Основание,
	|	РеквизитыПредварительная.ОснованиеДата КАК ОснованиеДата,
	|	РеквизитыПредварительная.ОснованиеНомер КАК ОснованиеНомер,
	|	РеквизитыПредварительная.НДСИсчисляетсяНалоговымАгентом КАК НДСИсчисляетсяНалоговымАгентом,
	|	РеквизитыПредварительная.Исполнитель КАК Исполнитель,
	|	РеквизитыПредварительная.ИсполнительНаОсновании КАК ИсполнительНаОсновании,
	|	РеквизитыПредварительная.ОтпускПроизвел КАК ОтпускПроизвел,
	|	РеквизитыПредварительная.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеквизитыПредварительная.ДоверенностьДата КАК ДоверенностьДата,
	|	РеквизитыПредварительная.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеквизитыПредварительная.ДоверенностьЧерезКого КАК ДоверенностьЧерезКого,
	|	РеквизитыПредварительная.ЗаЗаказчикаНаОсновании КАК ЗаЗаказчикаНаОсновании,
	|	РеквизитыПредварительная.ЕстьТовары КАК ЕстьТовары,
	|	РеквизитыПредварительная.АдресДоставки КАК АдресДоставки,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА &АдресПокупателя
	|		ИНАЧЕ РеквизитыПредварительная.АдресПокупателя
	|	КОНЕЦ КАК АдресПокупателя,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоПродаже
	|			ТОГДА &ИННКПППокупателя
	|		ИНАЧЕ РеквизитыПредварительная.ИННКПППокупателя
	|	КОНЕЦ КАК ИННКПППокупателя,
	|	РеквизитыПредварительная.СводныйСФКомиссияПоПродаже КАК СводныйСФКомиссияПоПродаже,
	|	РеквизитыПредварительная.СводныйСФКомиссияПоЗакупке КАК СводныйСФКомиссияПоЗакупке,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &АдресПоставщика
	|		ИНАЧЕ РеквизитыПредварительная.АдресПоставщика
	|	КОНЕЦ КАК АдресПоставщика,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &ИННКПППоставщика
	|		ИНАЧЕ РеквизитыПредварительная.ИННКПППоставщика
	|	КОНЕЦ КАК ИННКПППоставщика
	|ИЗ
	|	РеквизитыПредварительная КАК РеквизитыПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Товар,
	|	ТаблицаТовары.Номенклатура.Код КАК ТоварКод,
	|	ТаблицаТовары.Номенклатура.Артикул КАК ТоварАртикул,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Содержание <> """"
	|			ТОГДА ТаблицаТовары.Содержание
	|		КОГДА ТаблицаТовары.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ""Предварительная оплата""
	|		КОГДА ТаблицаТовары.Номенклатура.НаименованиеПолное = """"
	|			ТОГДА ТаблицаТовары.Номенклатура.Наименование
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НаименованиеПолное
	|	КОНЕЦ КАК ТоварНаименование,
	|	"""" КАК СтранаПроисхождения,
	|	"""" КАК ПредставлениеСтраны,
	|	"""" КАК НомерГТД,
	|	"""" КАК ПредставлениеГТД,
	|	"""" КАК РегистрационныйНомерТД,
	|	"""" КАК ЕдиницаИзмерения,
	|	0 КАК Количество,
	|	0 КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ИСТИНА КАК СуммаВключаетНДС,
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ЭтоКомиссия,
	|	ТаблицаТовары.Сумма КАК ВсегоРуб,
	|	ТаблицаТовары.СуммаНДС КАК НДСРуб,
	|	ТаблицаТовары.Сумма - ТаблицаТовары.СуммаНДС КАК СуммаБезНДСРуб,
	|	ТаблицаТовары.Контрагент КАК КонтрагентСводныйСФ,
	|	НЕОПРЕДЕЛЕНО КАК ПериодичностьУслуги,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка) КАК ТоварКодТНВЭД
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И ТаблицаТовары.Ссылка.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	1,
	|	"""",
	|	"""",
	|	"""",
	|	""Суммы, связанные с расчетами по оплате (ст. 162 НК РФ)"",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	0,
	|	0,
	|	СчетФактура.Сумма,
	|	СчетФактура.СуммаНДС,
	|	СчетФактура.СтавкаНДС,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	СчетФактура.Ссылка,
	|	ЛОЖЬ,
	|	СчетФактура.Сумма,
	|	СчетФактура.СуммаНДС,
	|	СчетФактура.Сумма - СчетФактура.СуммаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.Ссылка = &ДокументОснование
	|	И СчетФактура.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Номенклатура.Код,
	|	ТаблицаТовары.Номенклатура.Артикул,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Содержание <> """"
	|			ТОГДА ТаблицаТовары.Содержание
	|		КОГДА ТаблицаТовары.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Ссылка.ДоговорКонтрагента.ВидАгентскогоДоговора)
	|		КОГДА ТаблицаТовары.Номенклатура.НаименованиеПолное = """"
	|			ТОГДА ТаблицаТовары.Номенклатура.Наименование
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НаименованиеПолное
	|	КОНЕЦ,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	0,
	|	0,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.СуммаНДС,
	|	ТаблицаТовары.СтавкаНДС,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	ТаблицаТовары.Ссылка,
	|	ЛОЖЬ,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.СуммаНДС,
	|	ТаблицаТовары.Сумма - ТаблицаТовары.СуммаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И ТаблицаТовары.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПечатьСводныхСправок() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК СчетФактура,
	|	СчетФактура.Дата КАК Дата,
	|	СчетФактура.Номер КАК Номер,
	|	СчетФактура.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	СчетФактура.Руководитель КАК Руководитель,
	|	СчетФактура.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	СчетФактура.СчетФактураБезНДС КАК СчетФактураБезНДС,
	|	СчетФактура.Исправление КАК Исправление,
	|	СчетФактура.НомерИсправления КАК НомерИсправления,
	|	СчетФактура.Дата КАК ДатаИсправления,
	|	СчетФактура.Дата КАК ДатаСведений,
	|	НЕ СчетФактура.Исправление КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	СчетФактураДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураДокументыОснования.НомерСтроки КАК НомерСтроки,
	|	СчетФактура.Контрагент КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	"""" КАК КППСчетаФактуры,
	|	СчетФактура.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем) КАК ВидДоговора,
	|	СчетФактура.Организация.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
	|	ЛОЖЬ КАК НеподтверждениеНулевойСтавки,
	|	СчетФактура.СводныйКомиссионный КАК СводныйКомиссионный
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактураДокументыОснования.Ссылка = СчетФактура.Ссылка
	|ГДЕ
	|	СчетФактураДокументыОснования.Ссылка В(&МассивОбъектов)
	|	И СчетФактура.ДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.СводнаяСправка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	СчетФактура,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Номер),
	|	МАКСИМУМ(ВидСчетаФактуры),
	|	МАКСИМУМ(СчетФактураБезНДС),
	|	МАКСИМУМ(Исправление),
	|	МАКСИМУМ(НомерИсправления),
	|	МАКСИМУМ(ДатаИсправления),
	|	МАКСИМУМ(УдалитьПрефиксыИзНомера),
	|	МАКСИМУМ(ЭтоСчетФактураВыданный),
	|	МАКСИМУМ(ВыводитьСуммуБезНДС),
	|	МАКСИМУМ(НеподтверждениеНулевойСтавки)
	|ПО
	|	СчетФактура";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПечатьКорректировочныхСправок() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК СчетФактура,
	|	СчетФактура.Дата КАК Дата,
	|	СчетФактура.Номер КАК Номер,
	|	СчетФактура.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	СчетФактура.Руководитель КАК Руководитель,
	|	СчетФактура.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	СчетФактура.СчетФактураБезНДС КАК СчетФактураБезНДС,
	|	СчетФактура.Исправление КАК Исправление,
	|	СчетФактура.НомерИсправления КАК НомерИсправления,
	|	СчетФактура.Дата КАК ДатаИсправления,
	|	СчетФактура.Дата КАК ДатаСведений,
	|	НЕ СчетФактура.Исправление КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	СчетФактураДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураДокументыОснования.НомерСтроки КАК НомерСтроки,
	|	СчетФактура.Контрагент КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	"""" КАК КППСчетаФактуры,
	|	СчетФактура.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем) КАК ВидДоговора,
	|	СчетФактура.Организация.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	СчетФактура.Организация.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
	|	ЛОЖЬ КАК НеподтверждениеНулевойСтавки,
	|	СчетФактура.СводныйКомиссионный КАК СводныйКомиссионный
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактураДокументыОснования.Ссылка = СчетФактура.Ссылка
	|ГДЕ
	|	СчетФактураДокументыОснования.Ссылка В(&МассивОбъектов)
	|	И СчетФактура.ДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	СчетФактура,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Номер),
	|	МАКСИМУМ(ВидСчетаФактуры),
	|	МАКСИМУМ(СчетФактураБезНДС),
	|	МАКСИМУМ(Исправление),
	|	МАКСИМУМ(НомерИсправления),
	|	МАКСИМУМ(ДатаИсправления),
	|	МАКСИМУМ(УдалитьПрефиксыИзНомера),
	|	МАКСИМУМ(ЭтоСчетФактураВыданный),
	|	МАКСИМУМ(ВыводитьСуммуБезНДС),
	|	МАКСИМУМ(НеподтверждениеНулевойСтавки)
	|ПО
	|	СчетФактура";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация, Номер", "Контрагент", "ПредставлениеНомера");
	
	Возврат Результат;
	
КонецФункции

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	ДатаСчетаФактуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дата");
	ВерсияПостановленияНДС1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(ДатаСчетаФактуры);
	Запрос.УстановитьПараметр("ВерсияПостановленияНДС981", ВерсияПостановленияНДС1137 >=4);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРезультат[0]);
		
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Дата, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Реквизиты.Вставить("ИспользуетсяПостановление1137", УчетНДСПереопределяемый.ИспользуетсяПостановлениеНДС1137(Реквизиты.Дата));
	Реквизиты.Вставить("ПравилаПостановления735",       УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Реквизиты.Дата) >= 3);
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер, Истина, Ложь);
	
	ПараметрыДокумента = Новый Структура(
		"Организация,ДокументОснование,ОрганизацияОбособленноеПодразделение,ЦифровойИндексОбособленногоПодразделенияОрганизации", 
		Реквизиты.Организация, 
		Реквизиты.ДокументОснование,
		Реквизиты.ОрганизацияОбособленноеПодразделение,
		Реквизиты.ОрганизацияЦифровойИндексОбособленногоПодразделения);
		
	ИндексОбособленногоПодразделения = ПолучитьИндексОбособленногоПодразделения(ПараметрыДокумента);
	ПостфиксДляНомера = ?(ЗначениеЗаполнено(ИндексОбособленногоПодразделения), "/" + ИндексОбособленногоПодразделения, "");
	
	// Перевыставление счета-фактуры налогового агента комитенту.
	Если ТипЗнч(Реквизиты.ДокументОснование) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ЭтоПеревыставленныйСчетФактураНА = Истина;
	Иначе
		ЭтоПеревыставленныйСчетФактураНА = Ложь;
	КонецЕсли;
	
	Подразделение = Неопределено;
	Если ЭтоПеревыставленныйСчетФактураНА Тогда
		ОснованиеИсходногоСФ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.ДокументОснование, "ДокументОснование");
		МетаданныеОснования = ОснованиеИсходногоСФ.Метаданные();
		ЕстьПодразделение = ОбщегоНазначения.ЕстьРеквизитОбъекта("ПодразделениеОрганизации", МетаданныеОснования);
		Если ЕстьПодразделение Тогда 
			Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеИсходногоСФ, "ПодразделениеОрганизации");
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПостфиксДляНомера", ПостфиксДляНомера);
	Запрос.УстановитьПараметр("НомерСчетаФактуры", Номер + ПостфиксДляНомера);
	Запрос.УстановитьПараметр("ДокументОснование", Реквизиты.ДокументОснование);
	Запрос.УстановитьПараметр("Период",            Реквизиты.Дата);
	Запрос.УстановитьПараметр("Организация",       Реквизиты.Организация);
	Запрос.УстановитьПараметр("Покупатель",        Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("Подразделение",     Подразделение);
	// С 01.10.2017 года счет-фактура регистрируется в журнале учета полученных и выданных счетов-фактур
	// в том налоговом периоде, в котором этот счет-фактура составлен.
	ВерсияПостановленияНДС1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Реквизиты.ДатаВыставления);
	РегистрироватьСФДатойСоставления = ВерсияПостановленияНДС1137 >= 4;
	Реквизиты.Вставить("РегистрироватьСФДатойСоставления", РегистрироватьСФДатойСоставления);
	Запрос.УстановитьПараметр("РегистрироватьСФДатойСоставления", РегистрироватьСФДатойСоставления);
	Запрос.УстановитьПараметр("ИсправляемыйСчетФактура", Реквизиты.ИсправляемыйСчетФактура);
	
	Посредник = Справочники.Контрагенты.ПустаяСсылка();
	Если Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный
		ИЛИ Реквизиты.Исправление Тогда 
		
		Если Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
			РеквизитыИсходногоСчетаФактуры = ПолучитьРеквизитыИсходногоСчетаФактурыДляКорректировки(Реквизиты.ДокументОснование, ДокументСсылка,,Истина);
		Иначе
			ИсходныйДокументРеализации = УчетНДСПереопределяемый.ПолучитьИсправляемыйДокументРеализации(Реквизиты.ДокументОснование, Истина);
			Если ТипЗнч(ИсходныйДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				ИсходныйДокументРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИсходныйДокументРеализации, "ДокументРеализации");
			КонецЕсли; 
			РеквизитыИсходногоСчетаФактуры = ПолучитьРеквизитыСчетаФактурыВыданного(ИсходныйДокументРеализации,,Реквизиты.ВидСчетаФактуры);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыИсходногоСчетаФактуры)
			И ТипЗнч(РеквизитыИсходногоСчетаФактуры.СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")
			И ТипЗнч(РеквизитыИсходногоСчетаФактуры.СчетФактура.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			Посредник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыИсходногоСчетаФактуры.СчетФактура, "ДокументОснование.Контрагент");
		КонецЕсли;
		
	КонецЕсли;
	Запрос.УстановитьПараметр("Посредник",         Посредник);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаНачислениеНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗаписьЖурналаУчетаСчетовФактур(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаНомераДокументовОплаты(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаДанныеРегламентнойОперации(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПеревыставленныйСчетФактура(НомераТаблиц, ПараметрыПроведения, Реквизиты);
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для каждого ИндексТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(ИндексТаблицы.Ключ, Результат[ИндексТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЭтоПеревыставленныйСчетФактураНА
		И (Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		ИЛИ Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент) Тогда
		Если Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент Тогда
			ВидЦенности = УчетНДС.ОпределитьВидЦенностиПоОперации(
				Неопределено, Неопределено, , Истина, Реквизиты.ВидАгентскогоДоговора);
		ИначеЕсли Реквизиты.Под0 Тогда
			ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0;
		Иначе
			ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные;
		КонецЕсли;
		ПараметрыПроведения.ТаблицаАвансов.ЗаполнитьЗначения(ВидЦенности, "ВидЦенности");
	КонецЕсли;

	Возврат ПараметрыПроведения;

КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	Реквизиты.ДокументОснование КАК ДокументОснование,
	|	Реквизиты.СчетНаОплату КАК СчетНаОплату,
	|	Реквизиты.СтавкаНДС КАК СтавкаНДС,
	|	Реквизиты.ДоговорКонтрагента.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ) КАК УчетАгентскогоНДС,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаВзаиморасчетов,
	|	Реквизиты.Под0 КАК Под0,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.КППКонтрагента КАК КППКонтрагента,
	|	Реквизиты.Продавец КАК Продавец,
	|	Реквизиты.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	Реквизиты.ДатаВыставления КАК ДатаВыставления,
	|	Реквизиты.КодСпособаВыставления КАК КодСпособаВыставления,
	|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
	|	Реквизиты.Исправление КАК Исправление,
	|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
	|	Реквизиты.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	Реквизиты.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
	|	Реквизиты.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	Реквизиты.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	Реквизиты.СуммаУвеличение КАК СуммаУвеличение,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
	|			ТОГДА Реквизиты.СуммаДокумента
	|		ИНАЧЕ Реквизиты.СуммаУменьшение
	|	КОНЕЦ КАК СуммаУменьшение,
	|	Реквизиты.СуммаНДСУвеличение КАК СуммаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
	|			ТОГДА Реквизиты.СуммаНДСДокумента
	|		ИНАЧЕ Реквизиты.СуммаНДСУменьшение КОНЕЦ КАК СуммаНДСУменьшение,
	|	Реквизиты.НомерИсправления КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
	|			ТОГДА 0
	|		ИНАЧЕ Реквизиты.СуммаДокумента КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
	|			ТОГДА 0
	|		ИНАЧЕ Реквизиты.СуммаНДСДокумента КОНЕЦ КАК СуммаНДСДокумента,
	|	Реквизиты.СчетФактураБезНДС КАК СчетФактураБезНДС,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|				И Реквизиты.ДоговорКонтрагента = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(Реквизиты.ДокументОснование КАК Документ.ОтчетКомиссионераОПродажах).ДоговорКонтрагента.РасчетыВУсловныхЕдиницах
	|		ИНАЧЕ Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах
	|	КОНЕЦ КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	Реквизиты.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
	|	Реквизиты.СуммаНДСДокументаКомиссия КАК СуммаНДСДокументаКомиссия,
	|	Реквизиты.СуммаУвеличениеКомиссия КАК СуммаУвеличениеКомиссия,
	|	Реквизиты.СуммаУменьшениеКомиссия КАК СуммаУменьшениеКомиссия,
	|	Реквизиты.СуммаНДСУвеличениеКомиссия КАК СуммаНДСУвеличениеКомиссия,
	|	Реквизиты.СуммаНДСУменьшениеКомиссия КАК СуммаНДСУменьшениеКомиссия,
	|	Реквизиты.СводныйКомиссионный КАК СводныйКомиссионный,
	|	Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения КАК ОрганизацияЦифровойИндексОбособленногоПодразделения,
	|	Реквизиты.Организация.ОбособленноеПодразделение КАК ОрганизацияОбособленноеПодразделение,
	|	ЛОЖЬ КАК ИсправлениеСобственнойОшибки,
	|	"""" КАК ИННКонтрагента,
	|	Реквизиты.КПППродавца КАК КПППродавца,
	|	Реквизиты.Комитент КАК Комитент,
	|	Реквизиты.ДоговорКомитента КАК ДоговорКомитента,
	|	Реквизиты.СчетРасчетов КАК СчетРасчетов,
	|	Реквизиты.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.СчетаФактурыОтИмениОрганизации, ЛОЖЬ) КАК СчетаФактурыОтИмениОрганизации,
	|	Реквизиты.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
	|	Реквизиты.ДатаПлатежноРасчетногоДокумента КАК ДатаПлатежноРасчетногоДокумента,
	|	Реквизиты.НомерПлатежноРасчетногоДокумента КАК НомерПлатежноРасчетногоДокумента
	|ПОМЕСТИТЬ РеквизитыДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.СуммаУвеличение КАК СуммаУвеличение,
	|	СчетФактураВыданныйДокументыОснования.СуммаУменьшение КАК СуммаУменьшение,
	|	СчетФактураВыданныйДокументыОснования.СуммаНДСУвеличение КАК СуммаНДСУвеличение,
	|	СчетФактураВыданныйДокументыОснования.СуммаНДСУменьшение КАК СуммаНДСУменьшение,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДокументОснованиеДата,
	|	РеквизитыДокумента.Ссылка КАК Ссылка,
	|	РеквизитыДокумента.Дата КАК Дата,
	|	РеквизитыДокумента.Номер КАК Номер,
	|	РеквизитыДокумента.Организация КАК Организация,
	|	РеквизитыДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	РеквизитыДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	РеквизитыДокумента.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	РеквизитыДокумента.СчетНаОплату КАК СчетНаОплату,
	|	РеквизитыДокумента.СтавкаНДС КАК СтавкаНДС,
	|	РеквизитыДокумента.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	РеквизитыДокумента.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	РеквизитыДокумента.Под0 КАК Под0,
	|	РеквизитыДокумента.Контрагент КАК Контрагент,
	|	РеквизитыДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеквизитыДокумента.КППКонтрагента КАК КППКонтрагента,
	|	РеквизитыДокумента.Продавец КАК Продавец,
	|	РеквизитыДокумента.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	РеквизитыДокумента.ДатаВыставления КАК ДатаВыставления,
	|	РеквизитыДокумента.КодСпособаВыставления КАК КодСпособаВыставления,
	|	РеквизитыДокумента.КодВидаОперации КАК КодВидаОперации,
	|	РеквизитыДокумента.Исправление КАК Исправление,
	|	РеквизитыДокумента.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
	|	РеквизитыДокумента.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	РеквизитыДокумента.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
	|	РеквизитыДокумента.НомерИсправления КАК НомерИсправления,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаДокумента,
	|	РеквизитыДокумента.СуммаНДСДокумента КАК СуммаНДСДокумента,
	|	РеквизитыДокумента.СчетФактураБезНДС КАК СчетФактураБезНДС,
	|	СчетФактураВыданныйДокументыОснования.НомерСтроки - 1 КАК ИндексСтроки,
	|	РеквизитыДокумента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	РеквизитыДокумента.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	РеквизитыДокумента.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
	|	РеквизитыДокумента.СуммаНДСДокументаКомиссия КАК СуммаНДСДокументаКомиссия,
	|	РеквизитыДокумента.СуммаУвеличениеКомиссия КАК СуммаУвеличениеКомиссия,
	|	РеквизитыДокумента.СуммаУменьшениеКомиссия КАК СуммаУменьшениеКомиссия,
	|	РеквизитыДокумента.СуммаНДСУвеличениеКомиссия КАК СуммаНДСУвеличениеКомиссия,
	|	РеквизитыДокумента.СуммаНДСУменьшениеКомиссия КАК СуммаНДСУменьшениеКомиссия,
	|	РеквизитыДокумента.СводныйКомиссионный КАК СводныйКомиссионный,
	|	РеквизитыДокумента.ОрганизацияЦифровойИндексОбособленногоПодразделения КАК ОрганизацияЦифровойИндексОбособленногоПодразделения,
	|	РеквизитыДокумента.ОрганизацияОбособленноеПодразделение КАК ОрганизацияОбособленноеПодразделение,
	|	РеквизитыДокумента.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
	|	РеквизитыДокумента.ИННКонтрагента КАК ИННКонтрагента,
	|	РеквизитыДокумента.КПППродавца КАК КПППродавца,
	|	ВЫБОР
	|		КОГДА РеквизитыДокумента.ДатаСчетаФактурыПродавца <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РеквизитыДокумента.ДатаСчетаФактурыПродавца
	|		КОГДА РеквизитыДокумента.НомерСчетаФактурыПродавца <> """"
	|				И &ВерсияПостановленияНДС981
	|			ТОГДА ВЫБОР
	|					КОГДА РеквизитыДокумента.Исправление
	|						ТОГДА РеквизитыДокумента.ДатаИсходногоДокумента
	|					ИНАЧЕ РеквизитыДокумента.Дата
	|				КОНЕЦ
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаСчетаФактурыПродавца,
	|	НЕОПРЕДЕЛЕНО КАК ПеревыставляемыйСФ,
	|	0 КАК СуммаПеревыставляемогоСФ,
	|	РеквизитыДокумента.Комитент КАК Комитент,
	|	РеквизитыДокумента.ДоговорКомитента КАК ДоговорКомитента,
	|	РеквизитыДокумента.СчетРасчетов КАК СчетРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОплатыНерезиденту,
	|	РеквизитыДокумента.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение,
	|	РеквизитыДокумента.СчетаФактурыОтИмениОрганизации КАК СчетаФактурыОтИмениОрганизации,
	|	РеквизитыДокумента.ДатаПлатежноРасчетногоДокумента КАК ДатаПлатежноРасчетногоДокумента,
	|	РеквизитыДокумента.НомерПлатежноРасчетногоДокумента КАК НомерПлатежноРасчетногоДокумента
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО РеквизитыДокумента.Организация = ДанныеПервичныхДокументов.Организация
	|			И (СчетФактураВыданныйДокументыОснования.ДокументОснование = ДанныеПервичныхДокументов.Документ)
	|ГДЕ
	|	РеквизитыДокумента.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокументов.ДокументОснование,
	|	РеквизитыДокументов.НомерИсходногоДокумента,
	|	РеквизитыДокументов.ДатаИсходногоДокумента,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	РеквизитыДокументов.СуммаУвеличение,
	|	РеквизитыДокументов.СуммаУменьшение,
	|	РеквизитыДокументов.СуммаНДСУвеличение,
	|	РеквизитыДокументов.СуммаНДСУменьшение,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)),
	|	РеквизитыДокументов.Ссылка,
	|	РеквизитыДокументов.Дата,
	|	РеквизитыДокументов.Номер,
	|	РеквизитыДокументов.Организация,
	|	РеквизитыДокументов.ВалютаДокумента,
	|	ЕСТЬNULL(РеквизитыДокументов.ДоговорКонтрагента.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)),
	|	РеквизитыДокументов.ВидСчетаФактуры,
	|	РеквизитыДокументов.СчетНаОплату,
	|	РеквизитыДокументов.СтавкаНДС,
	|	РеквизитыДокументов.ДоговорКонтрагента.ВидАгентскогоДоговора,
	|	ЕСТЬNULL(РеквизитыДокументов.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ),
	|	РеквизитыДокументов.Под0,
	|	РеквизитыДокументов.Контрагент,
	|	РеквизитыДокументов.ДоговорКонтрагента,
	|	РеквизитыДокументов.КППКонтрагента,
	|	РеквизитыДокументов.Продавец,
	|	РеквизитыДокументов.СчетФактураНеВыставляется,
	|	РеквизитыДокументов.ДатаВыставления,
	|	РеквизитыДокументов.КодСпособаВыставления,
	|	РеквизитыДокументов.КодВидаОперации,
	|	РеквизитыДокументов.Исправление,
	|	РеквизитыДокументов.ИсправляемыйСчетФактура,
	|	РеквизитыДокументов.НомерИсправляемогоКорректировочногоДокумента,
	|	РеквизитыДокументов.ДатаИсправляемогоКорректировочногоДокумента,
	|	РеквизитыДокументов.НомерИсправления,
	|	РеквизитыДокументов.СуммаДокумента,
	|	РеквизитыДокументов.СуммаНДСДокумента,
	|	РеквизитыДокументов.СчетФактураБезНДС,
	|	0,
	|	РеквизитыДокументов.РасчетыВУсловныхЕдиницах,
	|	РеквизитыДокументов.НомерСчетаФактурыПродавца,
	|	РеквизитыДокументов.СуммаДокументаКомиссия,
	|	РеквизитыДокументов.СуммаНДСДокументаКомиссия,
	|	РеквизитыДокументов.СуммаУвеличениеКомиссия,
	|	РеквизитыДокументов.СуммаУменьшениеКомиссия,
	|	РеквизитыДокументов.СуммаНДСУвеличениеКомиссия,
	|	РеквизитыДокументов.СуммаНДСУменьшениеКомиссия,
	|	РеквизитыДокументов.СводныйКомиссионный,
	|	РеквизитыДокументов.ОрганизацияЦифровойИндексОбособленногоПодразделения,
	|	РеквизитыДокументов.ОрганизацияОбособленноеПодразделение,
	|	РеквизитыДокументов.ИсправлениеСобственнойОшибки,
	|	РеквизитыДокументов.ИННКонтрагента,
	|	РеквизитыДокументов.КПППродавца,
	|	ВЫБОР
	|		КОГДА РеквизитыДокументов.ДатаСчетаФактурыПродавца <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РеквизитыДокументов.ДатаСчетаФактурыПродавца
	|		КОГДА РеквизитыДокументов.НомерСчетаФактурыПродавца <> """"
	|				И &ВерсияПостановленияНДС981
	|			ТОГДА РеквизитыДокументов.ДатаИсходногоДокумента
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеквизитыДокументов.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.СводнаяСправка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА РеквизитыДокументов.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|					ТОГДА РеквизитыДокументов.ДокументОснование
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеквизитыДокументов.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ВЫРАЗИТЬ(РеквизитыДокументов.ДокументОснование КАК Документ.СчетФактураВыданный).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	РеквизитыДокументов.Комитент,
	|	РеквизитыДокументов.ДоговорКомитента,
	|	РеквизитыДокументов.СчетРасчетов,
	|	ВЫБОР
	|		КОГДА РеквизитыДокументов.ДокументОснование ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ВЫРАЗИТЬ(РеквизитыДокументов.ДокументОснование КАК Документ.СчетФактураВыданный).ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	РеквизитыДокументов.КодВидаОперацииНаУменьшение,
	|	РеквизитыДокументов.СчетаФактурыОтИмениОрганизации,
	|	РеквизитыДокументов.ДатаПлатежноРасчетногоДокумента,
	|	РеквизитыДокументов.НомерПлатежноРасчетногоДокумента
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО РеквизитыДокументов.Организация = ДанныеПервичныхДокументов.Организация
	|			И РеквизитыДокументов.ДокументОснование = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	РеквизитыДокументов.ВидСчетаФактуры <> ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	Реквизиты.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	Реквизиты.Под0 КАК Под0,
	|	Реквизиты.ДокументОснование КАК ДокументОснование,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.СводныйКомиссионный КАК СводныйКомиссионный,
	|	Реквизиты.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	Реквизиты.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	Реквизиты.ОрганизацияЦифровойИндексОбособленногоПодразделения КАК ОрганизацияЦифровойИндексОбособленногоПодразделения,
	|	Реквизиты.ОрганизацияОбособленноеПодразделение КАК ОрганизацияОбособленноеПодразделение,
	|	Реквизиты.Исправление КАК Исправление,
	|	Реквизиты.ДатаВыставления КАК ДатаВыставления,
	|	Реквизиты.СчетаФактурыОтИмениОрганизации КАК СчетаФактурыОтИмениОрганизации,
	|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
	|	Реквизиты.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Реквизиты.Продавец КАК Продавец
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаНачислениеНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		И Реквизиты.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс
		И Реквизиты.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент 
		// Перевыставление счета-фактуры налогового агента комитенту
		ИЛИ ТипЗнч(Реквизиты.ДокументОснование) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ПараметрыПроведения.Вставить("РеквизитыШапки",                 Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаАвансов",                 Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаКорректировочныйНаАванс", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РеквизитыШапки",                 НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаАвансов",                 НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаКорректировочныйНаАванс", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	Реквизиты.ДокументОснование КАК ДокументОснование,
	|	Реквизиты.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Реквизиты.СчетНаОплату КАК СчетНаОплату,
	|	Реквизиты.СтавкаНДС КАК СтавкаНДС,
	|	Реквизиты.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	Реквизиты.Под0 КАК Под0,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.УчетАгентскогоНДСПокупателем, ЛОЖЬ) КАК НДСИсчисляетсяНалоговымАгентом,
	|	Реквизиты.Исправление КАК Исправление,
	|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
	|	Реквизиты.ДатаПлатежноРасчетногоДокумента КАК ДатаПлатежноРасчетногоДокумента,
	|	Реквизиты.НомерПлатежноРасчетногоДокумента КАК НомерПлатежноРасчетногоДокумента
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО (ДоговорыКонтрагентов.Ссылка = Реквизиты.ДоговорКонтрагента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйАвансы.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйАвансы.Сумма КАК Сумма,
	|	СчетФактураВыданныйАвансы.Сумма КАК ВалютнаяСумма,
	|	СчетФактураВыданныйАвансы.СуммаНДС КАК СуммаНДС,
	|	СчетФактураВыданныйАвансы.Сумма - СчетФактураВыданныйАвансы.СуммаНДС КАК СуммаБезНДС,
	|	СчетФактураВыданныйАвансы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАвансамИПредоплатам)
	|	КОНЕЦ КАК СчетУчетаНДС,
	|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.Исправление
	|						ТОГДА Реквизиты.ИсправляемыйСчетФактура
	|					ИНАЧЕ Реквизиты.Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ Реквизиты.ДокументОснование
	|	КОНЕЦ КАК СчетФактура,
	|	Реквизиты.ДокументОснование КАК ДокументОплаты,
	|	СчетФактураВыданныйАвансы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйАвансы.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Контрагент
	|		ИНАЧЕ СчетФактураВыданныйАвансы.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйАвансы.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.ДоговорКонтрагента
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА Реквизиты.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) > КОНЕЦПЕРИОДА(Реквизиты.ДокументОснованиеДата, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаписьДополнительногоЛиста,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) > КОНЕЦПЕРИОДА(Реквизиты.ДокументОснованиеДата, КВАРТАЛ)
	|			ТОГДА НАЧАЛОПЕРИОДА(Реквизиты.ДокументОснованиеДата, КВАРТАЛ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК КорректируемыйПериод,
	|	НЕОПРЕДЕЛЕНО КАК СторнирующаяЗаписьДопЛиста,
	|	Реквизиты.ДокументОснованиеДата КАК ДатаОплаты,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	СчетФактураВыданныйАвансы.Сумма КАК СуммаВВалютеРасчетов,
	|	Реквизиты.Дата КАК ДатаСобытия
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураВыданныйАвансы.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйАвансы.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйАвансы.Сумма - СчетФактураВыданныйАвансы.СуммаДоКорректировки КАК Сумма,
	|	СчетФактураВыданныйАвансы.СуммаНДС - СчетФактураВыданныйАвансы.СуммаНДСДоКорректировки КАК СуммаНДС,
	|	(СчетФактураВыданныйАвансы.Сумма - СчетФактураВыданныйАвансы.СуммаНДС) - (СчетФактураВыданныйАвансы.СуммаДоКорректировки - СчетФактураВыданныйАвансы.СуммаНДСДоКорректировки) КАК СуммаБезНДС,
	|	СчетФактураВыданныйАвансы.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАвансамИПредоплатам) КАК СчетУчетаНДС,
	|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.Исправление
	|						ТОГДА Реквизиты.ИсправляемыйСчетФактура
	|					ИНАЧЕ Реквизиты.Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ Реквизиты.ДокументОснование
	|	КОНЕЦ КАК СчетФактура,
	|	СчетФактураВыданныйАвансы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйАвансы.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Контрагент
	|		ИНАЧЕ СчетФактураВыданныйАвансы.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйАвансы.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.ДоговорКонтрагента
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА Реквизиты.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) > КОНЕЦПЕРИОДА(Реквизиты.ДокументОснованиеДата, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаписьДополнительногоЛиста,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) > КОНЕЦПЕРИОДА(Реквизиты.ДокументОснованиеДата, КВАРТАЛ)
	|			ТОГДА НАЧАЛОПЕРИОДА(Реквизиты.ДокументОснованиеДата, КВАРТАЛ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК КорректируемыйПериод,
	|	НЕОПРЕДЕЛЕНО КАК СторнирующаяЗаписьДопЛиста,
	|	Реквизиты.Дата КАК ДатаСобытия
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураВыданныйАвансы.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаЗаписьЖурналаУчетаСчетовФактур(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ИспользуетсяПостановление1137 Тогда
		ПараметрыПроведения.Вставить("ЗаписьЖурналаУчетаСчетовФактур", Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаНДСЗаписиКнигиПродаж",    Неопределено);
		ПараметрыПроведения.Вставить("СторнирующаяЗаписьЖурнала",      Неопределено);
		Возврат "";
	КонецЕсли;
	
	Если Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс Тогда
		
		НомераТаблиц.Вставить("ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ЗаписьЖурналаУчетаСчетовФактур", НомераТаблиц.Количество());
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТекущийСчетФактура.Ссылка КАК ДокументСсылка,
		|	ТекущийСчетФактура.НомерСтроки КАК ИндексСтроки,
		|	ТекущийСчетФактура.СуммаНДС - ТекущийСчетФактура.СуммаНДСДоКорректировки КАК СуммаНДСРазницаУвеличение,
		|	ТекущийСчетФактура.Сумма - ТекущийСчетФактура.СуммаДоКорректировки КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	ИсходныйСчетФактураРеквизиты.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ИсходныйСчетФактураРеквизиты.ДатаСчетаФактуры КАК ДатаСчетаФактуры
		|ПОМЕСТИТЬ ВТ_ЗаписьЖурналаУчетаСчетовФактур
		|ИЗ
		|	Документ.СчетФактураВыданный.Авансы КАК ТекущийСчетФактура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналУчетаСчетовФактур КАК ИсходныйСчетФактураРеквизиты
		|		ПО ТекущийСчетФактура.КорректируемыйСчетФактура = ИсходныйСчетФактураРеквизиты.СчетФактура
		|ГДЕ
		|	ТекущийСчетФактура.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.ДокументОснование КАК ДокументОснование,
		|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
		|	Реквизиты.Исправление КАК Исправление,
		|	ИСТИНА КАК Корректировочный,
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.КППКонтрагента КАК КППКонтрагента,
		|	Реквизиты.Продавец КАК Продавец,
		|	Реквизиты.Ссылка КАК СчетФактура,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры) КАК ЧастьЖурнала,
		|	Реквизиты.ДатаВыставления КАК ДатаВыставленияПолучения,
		|	Реквизиты.КодСпособаВыставления КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	ВТ_ЗаписьЖурналаУчетаСчетовФактур.ИндексСтроки КАК ИндексСтроки,
		|	ВТ_ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ВТ_ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	&НомерСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
		|	Реквизиты.Дата КАК ДатаКорректировочногоСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
		|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
		|	ЛОЖЬ КАК РасчетыВУсловныхЕдиницах,
		|	Реквизиты.ВалютаДокумента КАК Валюта,
		|	Реквизиты.СчетФактураБезНДС КАК ПоСтавкеБезНДС,
		|	Реквизиты.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
		|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияКорректировочногоСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияКорректировочногоСчетаФактуры,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Посредник,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйПокупателю,
		|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаСделки,
		|	Реквизиты.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
		|	ВТ_ЗаписьЖурналаУчетаСчетовФактур.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
		|	ВТ_ЗаписьЖурналаУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	0 КАК СуммаПоСчетуФактуре,
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	0 КАК СуммаНДСРазницаУменьшение,
		|	0 КАК СуммаНДСКомиссия,
		|	0 КАК СуммаПоСчетуФактуреКомиссия,
		|	0 КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
		|	0 КАК СуммаНДСРазницаУвеличениеКомиссия,
		|	0 КАК СуммаНДСРазницаУменьшениеКомиссия,
		|	0 КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия,
		|	ЛОЖЬ КАК ИсправлениеСобственнойОшибки,
		|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйСчетФактура,
		|	"""" КАК ИННКонтрагента,
		|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактураПолученныйОтПродавца,
		|	ЛОЖЬ КАК Сторно,
		|	НЕОПРЕДЕЛЕНО КАК ИсправленныйСчетФактура,
		|	"""" КАК ИННПродавца,
		|	"""" КАК КПППродавца,
		|	"""" КАК ИННСубкомиссионера,
		|	"""" КАК КППСубкомиссионера,
		|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактурыПродавца,
		|	Реквизиты.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаписьЖурналаУчетаСчетовФактур КАК ВТ_ЗаписьЖурналаУчетаСчетовФактур
		|		ПО Реквизиты.Ссылка = ВТ_ЗаписьЖурналаУчетаСчетовФактур.ДокументСсылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ИначеЕсли НЕ (Реквизиты.СводныйКомиссионный 
		ИЛИ Реквизиты.СчетаФактурыОтИмениОрганизации) Тогда
		
		НомераТаблиц.Вставить("ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур",   НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВременнаяТаблицаДокументыОснования",               НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалютеПредварительная", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалюте",                НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ЗаписьЖурналаУчетаСчетовФактур",                   НомераТаблиц.Количество());
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	ВЫБОР
		|		КОГДА Реквизиты.СчетФактураНеВыставляется
		|				ИЛИ Реквизиты.ДатаВыставления = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА Реквизиты.Дата
		|		ИНАЧЕ Реквизиты.ДатаВыставления
		|	КОНЕЦ КАК Период,
		|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
		|	Реквизиты.Исправление КАК Исправление,
		|	ВЫБОР
		|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
		|		ИЛИ Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Корректировочный,
		|	Реквизиты.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Реквизиты.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ИЛИ Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
		|					И Реквизиты.ПеревыставляемыйСФ = НЕОПРЕДЕЛЕНО
		|			ТОГДА Реквизиты.Организация
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	Реквизиты.КППКонтрагента КАК КППКонтрагента,
		|	ВЫБОР
		|		КОГДА Реквизиты.Продавец = Реквизиты.Контрагент
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Продавец
		|	КОНЕЦ КАК Продавец,
		|	Реквизиты.Ссылка КАК СчетФактура,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры) КАК ЧастьЖурнала,
		|	ВЫБОР
		|		КОГДА Реквизиты.СчетФактураНеВыставляется
		|			ТОГДА Реквизиты.Дата
		|		КОГДА &РегистрироватьСФДатойСоставления
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.Исправление
		|						ТОГДА Реквизиты.ДатаИсходногоДокумента
		|					ИНАЧЕ Реквизиты.Дата
		|				КОНЕЦ
		|		ИНАЧЕ Реквизиты.ДатаВыставления
		|	КОНЕЦ КАК ДатаВыставленияПолучения,
		|	ВЫБОР
		|		КОГДА Реквизиты.КодСпособаВыставления = 0
		|			ТОГДА 1
		|		ИНАЧЕ Реквизиты.КодСпособаВыставления
		|	КОНЕЦ КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	&НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	Реквизиты.Дата КАК ДатаСчетаФактуры,
		|	Реквизиты.НомерИсходногоДокумента + &ПостфиксДляНомера КАК НомерИсходногоДокумента,
		|	Реквизиты.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
		|	Реквизиты.НомерИсправляемогоКорректировочногоДокумента + &ПостфиксДляНомера КАК НомерИсправляемогоКорректировочногоДокумента,
		|	Реквизиты.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
		|	Реквизиты.НомерИсправления КАК НомерИсправления,
		|	Реквизиты.Дата КАК ДатаИсправления,
		|	ЕСТЬNULL(Реквизиты.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
		|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
		|	Реквизиты.СуммаДокумента КАК СуммаПоСчетуФактуре,
		|	Реквизиты.СуммаНДСДокумента КАК СуммаНДС,
		|	Реквизиты.СуммаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	Реквизиты.СуммаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	Реквизиты.СуммаНДСУменьшение КАК СуммаНДСРазницаУменьшение,
		|	Реквизиты.СуммаНДСУвеличение КАК СуммаНДСРазницаУвеличение,
		|	Реквизиты.СчетФактураБезНДС КАК ПоСтавкеБезНДС,
		|	Реквизиты.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
		|	Реквизиты.ДокументОснование КАК ДокументОснование,
		|	Реквизиты.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
		|	Реквизиты.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
		|	Реквизиты.ИндексСтроки КАК ИндексСтроки,
		|	ВЫБОР
		|		КОГДА &Посредник <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА &Посредник
		|		КОГДА ТИПЗНАЧЕНИЯ(Реквизиты.ДокументОснование) = ТИП(Документ.ОтчетКомиссионераОПродажах)
		|				И ((Реквизиты.СчетФактураНеВыставляется
		|						ИЛИ Реквизиты.ДатаВыставления = ДАТАВРЕМЯ(1, 1, 1))
		|						И Реквизиты.Дата >= ДАТАВРЕМЯ(2014, 10, 1)
		|					ИЛИ НЕ Реквизиты.СчетФактураНеВыставляется
		|						И Реквизиты.ДатаВыставления >= ДАТАВРЕМЯ(2014, 10, 1))
		|			ТОГДА ВЫРАЗИТЬ(Реквизиты.ДокументОснование КАК Документ.ОтчетКомиссионераОПродажах).Контрагент
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	КОНЕЦ КАК Посредник,
		|	Реквизиты.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
		|	Реквизиты.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
		|	Реквизиты.СуммаНДСДокументаКомиссия КАК СуммаНДСДокументаКомиссия,
		|	Реквизиты.СуммаУменьшениеКомиссия КАК СуммаУменьшениеКомиссия,
		|	Реквизиты.СуммаУвеличениеКомиссия КАК СуммаУвеличениеКомиссия,
		|	Реквизиты.СуммаНДСУменьшениеКомиссия КАК СуммаНДСУменьшениеКомиссия,
		|	Реквизиты.СуммаНДСУвеличениеКомиссия КАК СуммаНДСУвеличениеКомиссия,
		|	Реквизиты.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
		|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
		|	Реквизиты.ИННКонтрагента КАК ИННКонтрагента,
		|	Реквизиты.КПППродавца КАК КПППродавца,
		|	Реквизиты.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
		|	Реквизиты.СуммаПеревыставляемогоСФ КАК СуммаПеревыставляемогоСФ,
		|	Реквизиты.ПеревыставляемыйСФ КАК ПеревыставляемыйСФ,
		|	Реквизиты.Комитент КАК Комитент,
		|	Реквизиты.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение
		|ПОМЕСТИТЬ ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Корректировочный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.Корректировочный КАК Корректировочный,
		|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснованиеИзТаблицы,
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.ДокументОснование КАК ДокументОснованиеИзШапки
		|ПОМЕСТИТЬ ВременнаяТаблицаДокументыОснования
		|ИЗ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур КАК ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
		|		ПО ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.Регистратор = СчетФактураВыданныйДокументыОснования.Ссылка
		|ГДЕ
		|	НЕ ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.Корректировочный
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.Корректировочный,
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.ДокументОснование,
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.ДокументОснование
		|ИЗ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур КАК ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур
		|ГДЕ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур.Корректировочный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДокументыОснования.Корректировочный КАК Корректировочный,
		|	ВременнаяТаблицаДокументыОснования.ДокументОснованиеИзШапки КАК ДокументОснованиеИзШапки,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК ДокументОснованиеИзТаблицы,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ВсегоРуб,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НДСРуб,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего) КАК РазницаВсего,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС) КАК РазницаНалоговаяБазаНДС,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС) КАК РазницаНДС
		|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная
		|ИЗ
		|	ВременнаяТаблицаДокументыОснования КАК ВременнаяТаблицаДокументыОснования
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО ВременнаяТаблицаДокументыОснования.ДокументОснованиеИзТаблицы = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
		|ГДЕ
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ НЕ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДокументыОснования.Корректировочный,
		|	ВременнаяТаблицаДокументыОснования.ДокументОснованиеИзШапки,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.ДокументОснованиеИзШапки КАК ДокументОснование,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.ВсегоРуб) КАК Всего,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.НДСРуб) КАК НДС,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего < 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего * -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаУменьшения,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаУвеличения,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС < 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС * -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДСУменьшения,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДСУвеличения
		|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
		|ИЗ
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная КАК РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.ДокументОснованиеИзШапки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.ДокументОснованиеИзШапки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.ДокументОснование КАК ДокументОснование,
		|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
		|	Реквизиты.Исправление КАК Исправление,
		|	Реквизиты.Корректировочный КАК Корректировочный,
		|	Реквизиты.Регистратор КАК Регистратор,
		|	Реквизиты.Период КАК Период,
		|	Реквизиты.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Реквизиты.ПеревыставляемыйСФ <> НЕОПРЕДЕЛЕНО
		|			ТОГДА Реквизиты.Комитент
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	Реквизиты.КППКонтрагента КАК КППКонтрагента,
		|	ВЫБОР
		|		КОГДА Реквизиты.ПеревыставляемыйСФ <> НЕОПРЕДЕЛЕНО
		|			ТОГДА Реквизиты.Контрагент
		|		ИНАЧЕ Реквизиты.Продавец
		|	КОНЕЦ КАК Продавец,
		|	Реквизиты.СчетФактура КАК СчетФактура,
		|	Реквизиты.ЧастьЖурнала КАК ЧастьЖурнала,
		|	Реквизиты.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
		|	Реквизиты.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерСчетаФактуры
		|		ИНАЧЕ Реквизиты.НомерИсходногоДокумента
		|	КОНЕЦ КАК НомерСчетаФактуры,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаСчетаФактуры
		|		ИНАЧЕ Реквизиты.ДатаИсходногоДокумента
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерСчетаФактуры
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправляемогоКорректировочногоДокумента
		|	КОНЕЦ КАК НомерКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаСчетаФактуры
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправляемогоКорректировочногоДокумента
		|	КОНЕЦ КАК ДатаКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|				И НЕ Реквизиты.Корректировочный
		|			ТОГДА Реквизиты.НомерИсправления
		|		КОГДА Реквизиты.Исправление
		|				И Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.НомерИсправленияИсходногоДокумента <> 0
		|						ТОГДА Реквизиты.НомерИсправленияИсходногоДокумента
		|					ИНАЧЕ НЕОПРЕДЕЛЕНО
		|				КОНЕЦ
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.НомерИсправленияИсходногоДокумента <> 0
		|						ТОГДА Реквизиты.НомерИсправленияИсходногоДокумента
		|					ИНАЧЕ НЕОПРЕДЕЛЕНО
		|				КОНЕЦ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НомерИсправления,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|				И НЕ Реквизиты.Корректировочный
		|			ТОГДА Реквизиты.ДатаИсправления
		|		КОГДА Реквизиты.Исправление
		|				И Реквизиты.Корректировочный
		|			ТОГДА Реквизиты.ДатаИсправленияИсходногоДокумента
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.НомерИсправленияИсходногоДокумента <> 0
		|						ТОГДА Реквизиты.ДатаИсправленияИсходногоДокумента
		|					ИНАЧЕ НЕОПРЕДЕЛЕНО
		|				КОНЕЦ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДатаИсправления,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА Реквизиты.РасчетыВУсловныхЕдиницах
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РасчетыВУсловныхЕдиницах,
		|	ВЫБОР
		|		КОГДА Реквизиты.РасчетыВУсловныхЕдиницах
		|				И НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА &ВалютаРеглУчета
		|		ИНАЧЕ Реквизиты.ВалютаДокумента
		|	КОНЕЦ КАК Валюта,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаПоСчетуФактуре
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) = 0
		|							ТОГДА Реквизиты.СуммаПоСчетуФактуре
		|						ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуре,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДС
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДС
		|						ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаПоСчетуФактуреРазницаУвеличение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУвеличения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаПоСчетуФактуреРазницаУменьшение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУменьшения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаНДСРазницаУвеличение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУвеличения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДСРазницаУвеличение,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаНДСРазницаУменьшение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУменьшения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДСРазницаУменьшение,
		|	Реквизиты.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.ПеревыставляемыйСФ <> НЕОПРЕДЕЛЕНО
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ Реквизиты.СчетФактураНеВыставляется
		|	КОНЕЦ КАК СчетФактураНеВыставляется,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НомерИсправленияКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДатаИсправленияКорректировочногоСчетаФактуры,
		|	Реквизиты.ИндексСтроки КАК ИндексСтроки,
		|	Реквизиты.Посредник КАК Посредник,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйПокупателю,
		|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаСделки,
		|	Реквизиты.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаНДС = 0
		|				ИЛИ Реквизиты.СуммаНДСДокументаКомиссия = 0
		|					И Реквизиты.ПеревыставляемыйСФ = НЕОПРЕДЕЛЕНО
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА ВЫБОР
		|							КОГДА Реквизиты.ПеревыставляемыйСФ <> НЕОПРЕДЕЛЕНО
		|								ТОГДА Реквизиты.СуммаНДС
		|							ИНАЧЕ Реквизиты.СуммаНДСДокументаКомиссия
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДСДокументаКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС / Реквизиты.СуммаНДС * Реквизиты.СуммаНДСДокументаКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСКомиссия,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаПоСчетуФактуре = 0
		|				ИЛИ Реквизиты.СуммаДокументаКомиссия = 0
		|					И Реквизиты.ПеревыставляемыйСФ = НЕОПРЕДЕЛЕНО
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА ВЫБОР
		|							КОГДА Реквизиты.ПеревыставляемыйСФ <> НЕОПРЕДЕЛЕНО
		|								ТОГДА Реквизиты.СуммаПоСчетуФактуре
		|							ИНАЧЕ Реквизиты.СуммаДокументаКомиссия
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) = 0
		|							ТОГДА Реквизиты.СуммаДокументаКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего / Реквизиты.СуммаПоСчетуФактуре * Реквизиты.СуммаДокументаКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуреКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаУвеличениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаПоСчетуФактуреРазницаУвеличение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаУвеличениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУвеличения, 0) = 0
		|							ТОГДА Реквизиты.СуммаУвеличениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУвеличения / Реквизиты.СуммаПоСчетуФактуреРазницаУвеличение * Реквизиты.СуммаУвеличениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаНДСУвеличениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаНДСРазницаУвеличение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДСУвеличениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУвеличения, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДСУвеличениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУвеличения / Реквизиты.СуммаНДСРазницаУвеличение * Реквизиты.СуммаНДСУвеличениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСРазницаУвеличениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаНДСУменьшениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаНДСРазницаУменьшение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДСУменьшениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУменьшения, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДСУменьшениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУменьшения / Реквизиты.СуммаНДСРазницаУменьшение * Реквизиты.СуммаНДСУменьшениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСРазницаУменьшениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаУменьшениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаПоСчетуФактуреРазницаУменьшение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаУменьшениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУменьшения, 0) = 0
		|							ТОГДА Реквизиты.СуммаУменьшениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУменьшения / Реквизиты.СуммаПоСчетуФактуреРазницаУменьшение * Реквизиты.СуммаУменьшениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия,
		|	Реквизиты.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
		|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
		|	Реквизиты.ИННКонтрагента КАК ИННКонтрагента,
		|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактураПолученныйОтПродавца,
		|	ЛОЖЬ КАК Сторно,
		|	НЕОПРЕДЕЛЕНО КАК ИсправленныйСчетФактура,
		|	"""" КАК ИННПродавца,
		|	Реквизиты.КПППродавца КАК КПППродавца,
		|	"""" КАК ИННСубкомиссионера,
		|	"""" КАК КППСубкомиссионера,
		|	Реквизиты.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
		|	Реквизиты.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение
		|ИЗ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО Реквизиты.ДокументОснование = РегистрСведенийРублевыеСуммыДокументовВВалюте.ДокументОснование
		|			И (Реквизиты.ВалютаДокумента <> &ВалютаРеглУчета)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Реквизиты.ДокументОснование,
		|	Реквизиты.ВидСчетаФактуры,
		|	Реквизиты.Исправление,
		|	Реквизиты.Корректировочный,
		|	Реквизиты.Регистратор,
		|	Реквизиты.Период,
		|	Реквизиты.Организация,
		|	Реквизиты.Контрагент,
		|	Реквизиты.КППКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.ПеревыставляемыйСФ,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры),
		|	Реквизиты.ДатаВыставленияПолучения,
		|	Реквизиты.КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации,
		|	Реквизиты.НомерСчетаФактурыПродавца,
		|	Реквизиты.ДатаСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	Реквизиты.ВалютаДокумента,
		|	Реквизиты.СуммаПеревыставляемогоСФ,
		|	Реквизиты.СуммаНДС,
		|	0,
		|	0,
		|	0,
		|	0,
		|	Реквизиты.ПоСтавкеБезНДС,
		|	Реквизиты.СчетФактураНеВыставляется,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.ИндексСтроки,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	""1"",
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.СуммаНДС,
		|	Реквизиты.СуммаПеревыставляемогоСФ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.ИсправлениеСобственнойОшибки,
		|	Реквизиты.ИсправляемыйСчетФактура,
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка),
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	"""",
		|	"""",
		|	"""",
		|	НЕОПРЕДЕЛЕНО,
		|	""""
		|ИЗ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО Реквизиты.ДокументОснование = РегистрСведенийРублевыеСуммыДокументовВВалюте.ДокументОснование
		|			И (Реквизиты.ВалютаДокумента <> &ВалютаРеглУчета)
		|ГДЕ
		|	Реквизиты.ПеревыставляемыйСФ <> НЕОПРЕДЕЛЕНО"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	Иначе
		
		НомераТаблиц.Вставить("ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур",   НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВременнаяТаблицаДокументыОснования",				  НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_СчетаФактурыКомитенту",				 		  НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_СчетаФактурыКомиссионеру",				 	  НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_Авансы",				 	                      НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалютеПредварительная", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалюте",   			  НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ЗаписьЖурналаУчетаСчетовФактур",					  НомераТаблиц.Количество());
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	ВЫБОР
		|		КОГДА Реквизиты.СчетФактураНеВыставляется
		|				ИЛИ Реквизиты.ДатаВыставления = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА Реквизиты.Дата
		|		ИНАЧЕ Реквизиты.ДатаВыставления
		|	КОНЕЦ КАК Период,
		|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
		|	Реквизиты.Исправление КАК Исправление,
		|	ВЫБОР
		|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Корректировочный,
		|	Реквизиты.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Реквизиты.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ИЛИ Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
		|			ТОГДА Реквизиты.Организация
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	Реквизиты.КППКонтрагента КАК КППКонтрагента,
		|	ВЫБОР
		|		КОГДА Реквизиты.Продавец = Реквизиты.Контрагент
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Продавец
		|	КОНЕЦ КАК Продавец,
		|	Реквизиты.Ссылка КАК СчетФактура,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры) КАК ЧастьЖурнала,
		|	ВЫБОР
		|		КОГДА Реквизиты.СчетФактураНеВыставляется
		|			ТОГДА Реквизиты.Дата
		|		КОГДА &РегистрироватьСФДатойСоставления
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.Исправление
		|						ТОГДА Реквизиты.ДатаИсходногоДокумента
		|					ИНАЧЕ Реквизиты.Дата
		|				КОНЕЦ
		|		ИНАЧЕ Реквизиты.ДатаВыставления
		|	КОНЕЦ КАК ДатаВыставленияПолучения,
		|	ВЫБОР
		|		КОГДА Реквизиты.КодСпособаВыставления = 0
		|			ТОГДА 1
		|		ИНАЧЕ Реквизиты.КодСпособаВыставления
		|	КОНЕЦ КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	&НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	Реквизиты.Дата КАК ДатаСчетаФактуры,
		|	Реквизиты.НомерИсходногоДокумента + &ПостфиксДляНомера КАК НомерИсходногоДокумента,
		|	Реквизиты.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
		|	Реквизиты.НомерИсправляемогоКорректировочногоДокумента + &ПостфиксДляНомера КАК НомерИсправляемогоКорректировочногоДокумента,
		|	Реквизиты.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
		|	Реквизиты.НомерИсправления КАК НомерИсправления,
		|	Реквизиты.Дата КАК ДатаИсправления,
		|	ЕСТЬNULL(Реквизиты.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
		|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
		|	Реквизиты.СуммаДокумента КАК СуммаПоСчетуФактуре,
		|	Реквизиты.СуммаНДСДокумента КАК СуммаНДС,
		|	Реквизиты.СуммаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	Реквизиты.СуммаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	Реквизиты.СуммаНДСУменьшение КАК СуммаНДСРазницаУменьшение,
		|	Реквизиты.СуммаНДСУвеличение КАК СуммаНДСРазницаУвеличение,
		|	Реквизиты.СчетФактураБезНДС КАК ПоСтавкеБезНДС,
		|	Реквизиты.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
		|	Реквизиты.ДокументОснование КАК ДокументОснование,
		|	Реквизиты.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
		|	Реквизиты.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
		|	Реквизиты.ИндексСтроки КАК ИндексСтроки,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Реквизиты.ДокументОснование) = ТИП(Документ.ОтчетКомиссионераОПродажах)
		|				И ((Реквизиты.СчетФактураНеВыставляется
		|						ИЛИ Реквизиты.ДатаВыставления = ДАТАВРЕМЯ(1, 1, 1))
		|						И Реквизиты.Дата >= ДАТАВРЕМЯ(2014, 10, 1)
		|					ИЛИ НЕ Реквизиты.СчетФактураНеВыставляется
		|						И Реквизиты.ДатаВыставления >= ДАТАВРЕМЯ(2014, 10, 1))
		|			ТОГДА ВЫРАЗИТЬ(Реквизиты.ДокументОснование КАК Документ.ОтчетКомиссионераОПродажах).Контрагент
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	КОНЕЦ КАК Посредник,
		|	Реквизиты.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
		|	Реквизиты.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
		|	Реквизиты.СуммаНДСДокументаКомиссия КАК СуммаНДСДокументаКомиссия,
		|	Реквизиты.СуммаУменьшениеКомиссия КАК СуммаУменьшениеКомиссия,
		|	Реквизиты.СуммаУвеличениеКомиссия КАК СуммаУвеличениеКомиссия,
		|	Реквизиты.СуммаНДСУменьшениеКомиссия КАК СуммаНДСУменьшениеКомиссия,
		|	Реквизиты.СуммаНДСУвеличениеКомиссия КАК СуммаНДСУвеличениеКомиссия,
		|	Реквизиты.СводныйКомиссионный КАК СводныйКомиссионный,
		|	Реквизиты.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
		|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
		|	Реквизиты.ИННКонтрагента КАК ИННКонтрагента,
		|	Реквизиты.КПППродавца КАК КПППродавца,
		|	Реквизиты.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
		|	Реквизиты.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение,
		|	Реквизиты.СчетаФактурыОтИмениОрганизации КАК СчетаФактурыОтИмениОрганизации
		|ПОМЕСТИТЬ ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
		|ПОМЕСТИТЬ ВременнаяТаблицаДокументыОснования
		|ИЗ
		|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
		|ГДЕ
		|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтчетКомитентуОПродажахПоставщики.Поставщик КАК Поставщик,
		|	СУММА(ВЫБОР
		|			КОГДА ОтчетКомитентуОПродажахТовары.Ссылка.СуммаВключаетНДС
		|				ТОГДА ОтчетКомитентуОПродажахТовары.Сумма
		|			ИНАЧЕ ОтчетКомитентуОПродажахТовары.Сумма + ОтчетКомитентуОПродажахТовары.СуммаНДС
		|		КОНЕЦ) КАК Сумма,
		|	СУММА(ОтчетКомитентуОПродажахТовары.СуммаНДС) КАК СуммаНДС,
		|	ОтчетКомитентуОПродажахПоставщики.СчетФактура КАК СчетФактура,
		|	ОтчетКомитентуОПродажахПоставщики.НомерСтроки КАК НомерСтроки,
		|	ОтчетКомитентуОПродажахПоставщики.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ОтчетКомитентуОПродажахПоставщики.ДатаСФ КАК ДатаСФ
		|ПОМЕСТИТЬ ВТ_СчетаФактурыКомитенту
		|ИЗ
		|	Документ.ОтчетКомитентуОПродажах.Поставщики КАК ОтчетКомитентуОПродажахПоставщики
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомитентуОПродажах.Товары КАК ОтчетКомитентуОПродажахТовары
		|		ПО ОтчетКомитентуОПродажахПоставщики.Ссылка = ОтчетКомитентуОПродажахТовары.Ссылка
		|			И ОтчетКомитентуОПродажахПоставщики.КлючСтроки = ОтчетКомитентуОПродажахТовары.КлючСтроки
		|ГДЕ
		|	ОтчетКомитентуОПродажахТовары.Ссылка ЕСТЬ НЕ NULL 
		|	И ОтчетКомитентуОПродажахПоставщики.Ссылка.ВыписыватьСчетаФактурыСводно
		|	И ОтчетКомитентуОПродажахПоставщики.СчетФактура = &Ссылка
		|	И ОтчетКомитентуОПродажахПоставщики.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетКомитентуОПродажахПоставщики.Поставщик,
		|	ОтчетКомитентуОПродажахПоставщики.СчетФактура,
		|	ОтчетКомитентуОПродажахПоставщики.НомерСтроки,
		|	ОтчетКомитентуОПродажахПоставщики.НомерСчетаФактуры,
		|	ОтчетКомитентуОПродажахПоставщики.ДатаСФ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтчетКомиссионераОПродажахТоварыУслуги.Покупатель КАК Покупатель,
		|	СУММА(ОтчетКомиссионераОПродажахТоварыУслуги.Сумма) КАК Сумма,
		|	СУММА(ОтчетКомиссионераОПродажахТоварыУслуги.СуммаНДС) КАК СуммаНДС,
		|	ОтчетКомиссионераОПродажахТоварыУслуги.СчетФактура КАК СчетФактура,
		|	ОтчетКомиссионераОПродажахТоварыУслуги.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_СчетаФактурыКомиссионеру
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОтчетКомиссионераОПродажахПокупатели.Покупатель КАК Покупатель,
		|		ВЫБОР
		|			КОГДА ОтчетКомиссионераОПродажахТовары.Ссылка.СуммаВключаетНДС
		|				ТОГДА ОтчетКомиссионераОПродажахТовары.Сумма
		|			ИНАЧЕ ОтчетКомиссионераОПродажахТовары.Сумма + ОтчетКомиссионераОПродажахТовары.СуммаНДС
		|		КОНЕЦ КАК Сумма,
		|		ОтчетКомиссионераОПродажахТовары.СуммаНДС КАК СуммаНДС,
		|		ОтчетКомиссионераОПродажахПокупатели.СчетФактура КАК СчетФактура,
		|		ОтчетКомиссионераОПродажахПокупатели.НомерСтроки КАК НомерСтроки
		|	ИЗ
		|		Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Товары КАК ОтчетКомиссионераОПродажахТовары
		|			ПО ОтчетКомиссионераОПродажахПокупатели.Ссылка = ОтчетКомиссионераОПродажахТовары.Ссылка
		|				И ОтчетКомиссионераОПродажахПокупатели.КлючСтроки = ОтчетКомиссионераОПродажахТовары.КлючСтроки
		|	ГДЕ
		|		ОтчетКомиссионераОПродажахТовары.Ссылка ЕСТЬ НЕ NULL 
		|		И ОтчетКомиссионераОПродажахПокупатели.Ссылка.ВыписыватьСчетаФактурыСводно
		|		И ОтчетКомиссионераОПродажахПокупатели.СчетФактура = &Ссылка
		|		И ОтчетКомиссионераОПродажахПокупатели.Ссылка.Проведен
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОтчетКомиссионераОПродажахПокупатели.Покупатель,
		|		ВЫБОР
		|			КОГДА ОтчетКомиссионераОПродажахУслуги.Ссылка.СуммаВключаетНДС
		|				ТОГДА ОтчетКомиссионераОПродажахУслуги.Сумма
		|			ИНАЧЕ ОтчетКомиссионераОПродажахУслуги.Сумма + ОтчетКомиссионераОПродажахУслуги.СуммаНДС
		|		КОНЕЦ,
		|		ОтчетКомиссионераОПродажахУслуги.СуммаНДС,
		|		ОтчетКомиссионераОПродажахПокупатели.СчетФактура,
		|		ОтчетКомиссионераОПродажахПокупатели.НомерСтроки
		|	ИЗ
		|		Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Услуги КАК ОтчетКомиссионераОПродажахУслуги
		|			ПО ОтчетКомиссионераОПродажахПокупатели.Ссылка = ОтчетКомиссионераОПродажахУслуги.Ссылка
		|				И ОтчетКомиссионераОПродажахПокупатели.КлючСтроки = ОтчетКомиссионераОПродажахУслуги.КлючСтроки
		|	ГДЕ
		|		ОтчетКомиссионераОПродажахУслуги.Ссылка ЕСТЬ НЕ NULL 
		|		И ОтчетКомиссионераОПродажахПокупатели.Ссылка.ВыписыватьСчетаФактурыСводно
		|		И ОтчетКомиссионераОПродажахПокупатели.СчетФактура = &Ссылка
		|		И ОтчетКомиссионераОПродажахПокупатели.Ссылка.Проведен) КАК ОтчетКомиссионераОПродажахТоварыУслуги
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетКомиссионераОПродажахТоварыУслуги.Покупатель,
		|	ОтчетКомиссионераОПродажахТоварыУслуги.СчетФактура,
		|	ОтчетКомиссионераОПродажахТоварыУслуги.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетФактураВыданныйАвансы.Контрагент КАК Контрагент,
		|	"""" КАК КППКонтрагента,
		|	СчетФактураВыданныйАвансы.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
		|	СчетФактураВыданныйАвансы.Сумма КАК Сумма,
		|	СчетФактураВыданныйАвансы.СуммаНДС КАК СуммаНДС,
		|	СчетФактураВыданныйАвансы.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_Авансы
		|ИЗ
		|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
		|ГДЕ
		|	СчетФактураВыданныйАвансы.Ссылка = &Ссылка
		|	И (СчетФактураВыданныйАвансы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
		|			ИЛИ СчетФактураВыданныйАвансы.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ВсегоРуб,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НДСРуб,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего) КАК РазницаВсего,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС) КАК РазницаНалоговаяБазаНДС,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС) КАК РазницаНДС
		|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная
		|ИЗ
		|	ВременнаяТаблицаДокументыОснования КАК ВременнаяТаблицаДокументыОснования
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО ВременнаяТаблицаДокументыОснования.ДокументОснование = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
		|ГДЕ
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ НЕ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.Регистратор КАК Регистратор,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.ВсегоРуб) КАК Всего,
		|	СУММА(РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.НДСРуб) КАК НДС,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего < 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего * -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаУменьшения,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаВсего
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаУвеличения,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС < 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС * -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДСУменьшения,
		|	СУММА(ВЫБОР
		|			КОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС > 0
		|				ТОГДА РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.РазницаНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДСУвеличения
		|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
		|ИЗ
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная КАК РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РегистрСведенийРублевыеСуммыДокументовВВалютеПредварительная.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.ДокументОснование КАК ДокументОснование,
		|	Реквизиты.ВидСчетаФактуры КАК ВидСчетаФактуры,
		|	Реквизиты.Исправление КАК Исправление,
		|	Реквизиты.Корректировочный КАК Корректировочный,
		|	Реквизиты.Регистратор КАК Регистратор,
		|	Реквизиты.Период КАК Период,
		|	Реквизиты.Организация КАК Организация,
		|	ЕСТЬNULL(ВТ_Авансы.Контрагент, ЕСТЬNULL(ВТ_СчетаФактурыКомиссионеру.Покупатель, Реквизиты.Контрагент)) КАК Контрагент,
		|	ЕСТЬNULL(ВТ_Авансы.КППКонтрагента, Реквизиты.КППКонтрагента) КАК КППКонтрагента,
		|	ЕСТЬNULL(ВТ_АвансыЗакупка.Контрагент, ЕСТЬNULL(ВТ_СчетаФактурыКомитенту.Поставщик, Реквизиты.Продавец)) КАК Продавец,
		|	Реквизиты.СчетФактура КАК СчетФактура,
		|	Реквизиты.ЧастьЖурнала КАК ЧастьЖурнала,
		|	Реквизиты.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
		|	Реквизиты.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерСчетаФактуры
		|		ИНАЧЕ Реквизиты.НомерИсходногоДокумента
		|	КОНЕЦ КАК НомерСчетаФактуры,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаСчетаФактуры
		|		ИНАЧЕ Реквизиты.ДатаИсходногоДокумента
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерСчетаФактуры
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправляемогоКорректировочногоДокумента
		|	КОНЕЦ КАК НомерКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА Реквизиты.Корректировочный
		|				И НЕ Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаСчетаФактуры
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправляемогоКорректировочногоДокумента
		|	КОНЕЦ КАК ДатаКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|				И НЕ Реквизиты.Корректировочный
		|			ТОГДА Реквизиты.НомерИсправления
		|		КОГДА Реквизиты.Исправление
		|				И Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.НомерИсправленияИсходногоДокумента <> 0
		|						ТОГДА Реквизиты.НомерИсправленияИсходногоДокумента
		|					ИНАЧЕ НЕОПРЕДЕЛЕНО
		|				КОНЕЦ
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.НомерИсправленияИсходногоДокумента <> 0
		|						ТОГДА Реквизиты.НомерИсправленияИсходногоДокумента
		|					ИНАЧЕ НЕОПРЕДЕЛЕНО
		|				КОНЕЦ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НомерИсправления,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|				И НЕ Реквизиты.Корректировочный
		|			ТОГДА Реквизиты.ДатаИсправления
		|		КОГДА Реквизиты.Исправление
		|				И Реквизиты.Корректировочный
		|			ТОГДА Реквизиты.ДатаИсправленияИсходногоДокумента
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.НомерИсправленияИсходногоДокумента <> 0
		|						ТОГДА Реквизиты.ДатаИсправленияИсходногоДокумента
		|					ИНАЧЕ НЕОПРЕДЕЛЕНО
		|				КОНЕЦ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДатаИсправления,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА Реквизиты.РасчетыВУсловныхЕдиницах
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РасчетыВУсловныхЕдиницах,
		|	ВЫБОР
		|		КОГДА Реквизиты.РасчетыВУсловныхЕдиницах
		|				И НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА &ВалютаРеглУчета
		|		ИНАЧЕ Реквизиты.ВалютаДокумента
		|	КОНЕЦ КАК Валюта,
		|	ВЫБОР
		|		КОГДА ВТ_Авансы.Сумма ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_Авансы.Сумма
		|		КОГДА ВТ_АвансыЗакупка.Сумма ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_АвансыЗакупка.Сумма
		|		КОГДА ВТ_СчетаФактурыКомитенту.Сумма ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомитенту.Сумма
		|		КОГДА ВТ_СчетаФактурыКомиссионеру.Сумма ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомиссионеру.Сумма
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаПоСчетуФактуре
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) = 0
		|							ТОГДА Реквизиты.СуммаПоСчетуФактуре
		|						ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуре,
		|	ВЫБОР
		|		КОГДА ВТ_Авансы.СуммаНДС ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_Авансы.СуммаНДС
		|		КОГДА ВТ_АвансыЗакупка.СуммаНДС ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_АвансыЗакупка.СуммаНДС
		|		КОГДА ВТ_СчетаФактурыКомитенту.СуммаНДС ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомитенту.СуммаНДС
		|		КОГДА ВТ_СчетаФактурыКомиссионеру.СуммаНДС ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомиссионеру.СуммаНДС
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДС
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДС
		|						ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаПоСчетуФактуреРазницаУвеличение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУвеличения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаПоСчетуФактуреРазницаУменьшение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУменьшения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаНДСРазницаУвеличение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУвеличения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДСРазницаУвеличение,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|								И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|						ТОГДА Реквизиты.СуммаНДСРазницаУменьшение
		|					ИНАЧЕ ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУменьшения, 0)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДСРазницаУменьшение,
		|	Реквизиты.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
		|	Реквизиты.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НомерИсправленияКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА Реквизиты.Корректировочный
		|				И Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДатаИсправленияКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА ВТ_Авансы.НомерСтроки ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_Авансы.НомерСтроки
		|		КОГДА ВТ_АвансыЗакупка.НомерСтроки ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_АвансыЗакупка.НомерСтроки
		|		КОГДА ВТ_СчетаФактурыКомитенту.НомерСтроки ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомитенту.НомерСтроки
		|		КОГДА ВТ_СчетаФактурыКомиссионеру.НомерСтроки ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомиссионеру.НомерСтроки
		|		ИНАЧЕ Реквизиты.ИндексСтроки
		|	КОНЕЦ КАК ИндексСтроки,
		|	Реквизиты.Посредник КАК Посредник,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйПокупателю,
		|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаСделки,
		|	ЕСТЬNULL(ВТ_СчетаФактурыКомитенту.НомерСчетаФактуры, ЕСТЬNULL(ВТ_АвансыЗакупка.НомерСчетаФактурыПродавца, Реквизиты.НомерСчетаФактурыПродавца)) КАК НомерСчетаФактурыПродавца,
		|	ВЫБОР
		|		КОГДА ВТ_СчетаФактурыКомитенту.СуммаНДС ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомитенту.СуммаНДС
		|		КОГДА ВТ_АвансыЗакупка.СуммаНДС ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_АвансыЗакупка.СуммаНДС
		|		КОГДА Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаНДС = 0
		|				ИЛИ Реквизиты.СуммаНДСДокументаКомиссия = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДСДокументаКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДСДокументаКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС / Реквизиты.СуммаНДС * Реквизиты.СуммаНДСДокументаКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСКомиссия,
		|	ВЫБОР
		|		КОГДА ВТ_СчетаФактурыКомитенту.Сумма ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_СчетаФактурыКомитенту.Сумма
		|		КОГДА ВТ_АвансыЗакупка.Сумма ЕСТЬ НЕ NULL 
		|			ТОГДА ВТ_АвансыЗакупка.Сумма
		|		КОГДА Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаПоСчетуФактуре = 0
		|				ИЛИ Реквизиты.СуммаДокументаКомиссия = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаДокументаКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) = 0
		|							ТОГДА Реквизиты.СуммаДокументаКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего / Реквизиты.СуммаПоСчетуФактуре * Реквизиты.СуммаДокументаКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуреКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаУвеличениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаПоСчетуФактуреРазницаУвеличение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаУвеличениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУвеличения, 0) = 0
		|							ТОГДА Реквизиты.СуммаУвеличениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУвеличения / Реквизиты.СуммаПоСчетуФактуреРазницаУвеличение * Реквизиты.СуммаУвеличениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаНДСУвеличениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаНДСРазницаУвеличение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДСУвеличениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУвеличения, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДСУвеличениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУвеличения / Реквизиты.СуммаНДСРазницаУвеличение * Реквизиты.СуммаНДСУвеличениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСРазницаУвеличениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаНДСУменьшениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаНДСРазницаУменьшение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаНДСУменьшениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУменьшения, 0) = 0
		|							ТОГДА Реквизиты.СуммаНДСУменьшениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаНДСУменьшения / Реквизиты.СуммаНДСРазницаУменьшение * Реквизиты.СуммаНДСУменьшениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСРазницаУменьшениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Корректировочный
		|				ИЛИ Реквизиты.СуммаУменьшениеКомиссия = 0
		|				ИЛИ Реквизиты.СуммаПоСчетуФактуреРазницаУменьшение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|						ИЛИ НЕ Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|							И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
		|					ТОГДА Реквизиты.СуммаУменьшениеКомиссия
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУменьшения, 0) = 0
		|							ТОГДА Реквизиты.СуммаУменьшениеКомиссия
		|						ИНАЧЕ РегистрСведенийРублевыеСуммыДокументовВВалюте.СуммаУменьшения / Реквизиты.СуммаПоСчетуФактуреРазницаУменьшение * Реквизиты.СуммаУменьшениеКомиссия
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия,
		|	Реквизиты.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
		|	Реквизиты.ИсправляемыйСчетФактура КАК ИсправляемыйСчетФактура,
		|	Реквизиты.ИННКонтрагента КАК ИННКонтрагента,
		|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактураПолученныйОтПродавца,
		|	ЛОЖЬ КАК Сторно,
		|	НЕОПРЕДЕЛЕНО КАК ИсправленныйСчетФактура,
		|	"""" КАК ИННПродавца,
		|	Реквизиты.КПППродавца КАК КПППродавца,
		|	"""" КАК ИННСубкомиссионера,
		|	"""" КАК КППСубкомиссионера,
		|	ВЫБОР
		|		КОГДА Реквизиты.СчетаФактурыОтИмениОрганизации
		|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыКомитенту.ДатаСФ, Реквизиты.ДатаСчетаФактурыПродавца)
		|		ИНАЧЕ Реквизиты.ДатаСчетаФактурыПродавца
		|	КОНЕЦ КАК ДатаСчетаФактурыПродавца,
		|	Реквизиты.КодВидаОперацииНаУменьшение КАК КодВидаОперацииНаУменьшение
		|ИЗ
		|	ВременнаяТаблицаЗаписьЖурналаУчетаСчетовФактур КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО Реквизиты.ДокументОснование = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
		|			И (Реквизиты.ВалютаДокумента <> &ВалютаРеглУчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Авансы КАК ВТ_Авансы
		|		ПО (Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Авансы КАК ВТ_АвансыЗакупка
		|		ПО (Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаФактурыКомитенту КАК ВТ_СчетаФактурыКомитенту
		|		ПО Реквизиты.СчетФактура = ВТ_СчетаФактурыКомитенту.СчетФактура
		|			И (Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаФактурыКомиссионеру КАК ВТ_СчетаФактурыКомиссионеру
		|		ПО Реквизиты.СчетФактура = ВТ_СчетаФактурыКомиссионеру.СчетФактура
		|			И (Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию))"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	Если Реквизиты.РегистрироватьСФДатойСоставления
		И Реквизиты.Исправление Тогда 
		НомераТаблиц.Вставить("СторнирующаяЗаписьЖурнала", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаСторнирующаяЗаписьЖурналаСФ();
	Иначе
		ПараметрыПроведения.Вставить("СторнирующаяЗаписьЖурнала", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСторнирующаяЗаписьЖурналаСФ()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КППКонтрагента КАК КППКонтрагента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Продавец КАК Продавец,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Посредник КАК Посредник,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперации КАК КодВидаОперацииНаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта КАК Валюта,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправленияКорректировочногоСчетаФактуры КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер КАК Субкомиссионер,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю КАК СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаСделки КАК КодВидаСделки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИндексСтроки КАК ИндексСтроки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйСчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИННКонтрагента КАК ИННКонтрагента,
	|	ИСТИНА КАК Сторно,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИННПродавца КАК ИННПродавца,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КПППродавца КАК КПППродавца,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИННСубкомиссионера КАК ИННСубкомиссионера,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КППСубкомиссионера КАК КППСубкомиссионера,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураПолученныйОтПродавца КАК СчетФактураПолученныйОтПродавца,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС КАК СуммаНДС,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия КАК СуммаНДСКомиссия,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшение,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
	|	-ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия КАК СуммаНДСРазницаУвеличениеКомиссия
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			&Период,
	|			СчетФактура = &ИсправляемыйСчетФактура
	|				И Регистратор <> &Ссылка
	|				И НЕ Сторно) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	(ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия > 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия > 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия > 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия > 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия > 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия > 0)"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаНомераДокументовОплаты(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ПравилаПостановления735 
		ИЛИ Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс 
		ИЛИ Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент 
		ИЛИ ЗначениеЗаполнено(Реквизиты.Продавец) Тогда
		ПараметрыПроведения.Вставить("ВТ_ЗаписиКнигиПродажПредварительная", Неопределено);
		ПараметрыПроведения.Вставить("ВТ_ЗаписиКнигиПродаж",    			Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаНомеровДокументовОплаты",    	Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_ЗаписиКнигиПродажПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ЗаписиКнигиПродаж", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаНомеровДокументовОплаты", НомераТаблиц.Количество());
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	&Ссылка КАК СчетФактураДокумент,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.ДатаОплаты,
	|	НДСЗаписиКнигиПродаж.ДокументОплаты,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ДатаСобытия,
	|	НДСЗаписиКнигиПродаж.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.КорректируемыйПериод,
	|	НДСЗаписиКнигиПродаж.СторнирующаяЗаписьДопЛиста,
	|	НДСЗаписиКнигиПродаж.ИсправленныйСчетФактура
	|ПОМЕСТИТЬ ВТ_ЗаписиКнигиПродажПредварительная
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.СчетФактура = &ДокументОснование
	|	И НДСЗаписиКнигиПродаж.Активность
	|	И НДСЗаписиКнигиПродаж.Регистратор = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.ДатаОплаты,
	|	НДСЗаписиКнигиПродаж.ДокументОплаты,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ДатаСобытия,
	|	НДСЗаписиКнигиПродаж.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.КорректируемыйПериод,
	|	НДСЗаписиКнигиПродаж.СторнирующаяЗаписьДопЛиста,
	|	НДСЗаписиКнигиПродаж.ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.СчетФактура = &Ссылка
	|	И НДСЗаписиКнигиПродаж.Активность
	|	И НДСЗаписиКнигиПродаж.Регистратор = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_ЗаписиКнигиПродажПредварительная.Организация,
	|	ВТ_ЗаписиКнигиПродажПредварительная.СчетФактура,
	|	ВТ_ЗаписиКнигиПродажПредварительная.Покупатель,
	|	ВТ_ЗаписиКнигиПродажПредварительная.СчетФактураДокумент,
	|	ВТ_ЗаписиКнигиПродажПредварительная.СтавкаНДС,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ВидЦенности,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ДатаОплаты,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ДокументОплаты,
	|	ВТ_ЗаписиКнигиПродажПредварительная.Событие,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ДатаСобытия,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ДоговорКонтрагента,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ЗаписьДополнительногоЛиста,
	|	ВТ_ЗаписиКнигиПродажПредварительная.КорректируемыйПериод,
	|	ВТ_ЗаписиКнигиПродажПредварительная.СторнирующаяЗаписьДопЛиста,
	|	ВТ_ЗаписиКнигиПродажПредварительная.ИсправленныйСчетФактура
	|ПОМЕСТИТЬ ВТ_ЗаписиКнигиПродаж
	|ИЗ
	|	ВТ_ЗаписиКнигиПродажПредварительная КАК ВТ_ЗаписиКнигиПродажПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.ДатаДокумента КАК ДатаДокументаОплаты,
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.НомерДокумента КАК НомерДокументаОплаты,
	|	ВТ_ЗаписиКнигиПродаж.Организация КАК Организация,
	|	ВТ_ЗаписиКнигиПродаж.СчетФактура КАК СчетФактура,
	|	&Период КАК Период,
	|	ВТ_ЗаписиКнигиПродаж.Покупатель КАК Покупатель,
	|	ВТ_ЗаписиКнигиПродаж.СтавкаНДС,
	|	ВТ_ЗаписиКнигиПродаж.ВидЦенности,
	|	ВТ_ЗаписиКнигиПродаж.ДатаОплаты,
	|	ВТ_ЗаписиКнигиПродаж.ДокументОплаты,
	|	ВТ_ЗаписиКнигиПродаж.Событие,
	|	ВТ_ЗаписиКнигиПродаж.ДатаСобытия,
	|	ВТ_ЗаписиКнигиПродаж.ДоговорКонтрагента,
	|	ВТ_ЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТ_ЗаписиКнигиПродаж.КорректируемыйПериод,
	|	ВТ_ЗаписиКнигиПродаж.СторнирующаяЗаписьДопЛиста,
	|	ВТ_ЗаписиКнигиПродаж.ИсправленныйСчетФактура
	|ИЗ
	|	ВТ_ЗаписиКнигиПродаж КАК ВТ_ЗаписиКнигиПродаж
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ПлатежноРасчетныеДокументы КАК СчетФактураВыданныйПлатежноРасчетныеДокументы
	|		ПО ВТ_ЗаписиКнигиПродаж.СчетФактураДокумент = СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйПлатежноРасчетныеДокументы.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ПодготовитьТаблицуСторноНачисленияНДС(ТаблицаРеквизиты) Экспорт

	Если ТаблицаРеквизиты = Неопределено
		ИЛИ ТаблицаРеквизиты.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	
	ВерсияУчетаНДС = УчетНДСКлиентСервер.Версия(Реквизиты.Период);
	
	Если НЕ ВерсияУчетаНДС = 2 
		ИЛИ НЕ Реквизиты.Исправление
		ИЛИ (Реквизиты.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НаАванс
			И Реквизиты.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат УчетНДС.ТаблицаСторноНачисленияНДС(Реквизиты);
	
КонецФункции

Функция ПолучитьИндексОбособленногоПодразделения(ПараметрыДокумента)
	
	ЦифровойИндексОбособленногоПодразделения = "";
	
	ДокументОснование  = ПараметрыДокумента.ДокументОснование;
	Организация        = ПараметрыДокумента.Организация;
	
	Если ЗначениеЗаполнено(ДокументОснование)
		И ОбщегоНазначения.ЕстьРеквизитОбъекта("ПодразделениеОрганизации", ДокументОснование.Метаданные()) Тогда
		Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ПодразделениеОрганизации");
		Если ЗначениеЗаполнено(Подразделение) Тогда
			РеквизитыПодразделения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Подразделение, "ОбособленноеПодразделение,ЦифровойИндексОбособленногоПодразделения");
			Если РеквизитыПодразделения.ОбособленноеПодразделение Тогда
				ЦифровойИндексОбособленногоПодразделения = РеквизитыПодразделения.ЦифровойИндексОбособленногоПодразделения;
			КонецЕсли;
		КонецЕсли;
 	КонецЕсли;
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Организация, "ОбособленноеПодразделение,ЦифровойИндексОбособленногоПодразделения");
	
	Если ПараметрыДокумента.ОрганизацияОбособленноеПодразделение И НЕ ЗначениеЗаполнено(ЦифровойИндексОбособленногоПодразделения) Тогда
		ЦифровойИндексОбособленногоПодразделения = ПараметрыДокумента.ЦифровойИндексОбособленногоПодразделенияОрганизации;
	КонецЕсли;
	
	Возврат ЦифровойИндексОбособленногоПодразделения;
	
КонецФункции

Функция ТекстЗапросаДанныеРегламентнойОперации(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка Тогда
		ПараметрыПроведения.Вставить("ДанныеРегламентнойОперации", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ДанныеРегламентнойОперации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.ФормированиеСводнойСправки) КАК РегламентнаяОперация
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПеревыставленныйСчетФактура (НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если ТипЗнч(Реквизиты.ДокументОснование) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда 
		ПараметрыПроведения.Вставить("ТаблицаПеревыставленныйСчетФактура", Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовРеквизиты",              Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаАвансов",         Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТаблицаПеревыставленныйСчетФактура", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйАвансы.Ссылка КАК ДокументРасчетовКомитента,
	|	Реквизиты.Организация,
	|	&Подразделение КАК Подразделение,
	|	Реквизиты.Дата КАК Период,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(СчетФактураВыданныйАвансы.Сумма - СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаБезНДС,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.Комитент,
	|	Реквизиты.ДоговорКомитента,
	|	Реквизиты.СчетРасчетов,
	|	1 КАК НомерСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	ИСТИНА КАК ЭтоУслуга,
	|	НЕОПРЕДЕЛЕНО КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК СуммаБУ,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаВзаиморасчетов,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаВзаиморасчетовКомитент,
	|	0 КАК СуммаНДСВзаиморасчетовКомитент,
	|	0 КАК Количество,
	|	"""" КАК Содержание,
	|	Реквизиты.ДокументОплатыНерезиденту
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйАвансы.Ссылка,
	|	Реквизиты.ДокументОплатыНерезиденту,
	|	Реквизиты.Организация,
	|	Реквизиты.Дата,
	|	Реквизиты.СчетРасчетов,
	|	Реквизиты.Ссылка,
	|	Реквизиты.ДоговорКомитента,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.Контрагент,
	|	Реквизиты.Комитент" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты"     , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаАвансов", НомераТаблиц.Количество());
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически) КАК СпособЗачетаАвансов,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСН,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСНПатент,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	ЛОЖЬ КАК ДеятельностьНаТорговомСборе,
	|	""Выбытие"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Комитент КАК Контрагент,
	|	Реквизиты.ДоговорКомитента КАК ДоговорКонтрагента,
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ЛОЖЬ КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.СчетРасчетов КАК СчетРасчетов,
	|	Реквизиты.СчетРасчетов КАК СчетАвансов,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	&Подразделение КАК Подразделение,
	|	&Подразделение КАК ПодразделениеРасчетов,
	|	ВЫБОР
	|		КОГДА ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаВзаиморасчетов,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаРубПоКурсуАванса,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаРуб,
	|	0 КАК СуммаВзаиморасчетовЕНВД,
	|	0 КАК СуммаРуб_ЕНВД,
	|	0 КАК СуммаВзаиморасчетовПатент,
	|	0 КАК СуммаРуб_Патент,
	|	0 КАК СуммаВзаиморасчетовТорговыйСбор,
	|	0 КАК СуммаРуб_ТорговыйСбор,
	|	0 КАК СуммаВзаиморасчетовКомитента,
	|	0 КАК КАКСуммаРуб_Комитента
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО (Реквизиты.ДоговорКомитента = ДоговорыКонтрагентов.Ссылка)
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.Ссылка,
	|	Реквизиты.Комитент,
	|	Реквизиты.ДоговорКомитента,
	|	ДоговорыКонтрагентов.ВидДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов,
	|	Реквизиты.СчетРасчетов,
	|	Реквизиты.СчетРасчетов" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ПодготовитьДанныеДляЗаполненияСводнойСправки(СтруктураПараметров, АдресХранилища) Экспорт
	
	ДанныеДляЗаполнения = Новый Структура;
	
	ДанныеДляЗаполнения.Вставить(
		"ДокументыОснования", ПодготовитьДанныеСводнойСправки(СтруктураПараметров));
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

Функция ПодготовитьДанныеСводнойСправки(СтруктураПараметров)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Организация = &Организация
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.Проведен
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СчетФактураВыданныйДокументыОснования.Ссылка <> &Ссылка
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.СводнаяСправка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСЗаписиКнигиПродаж.Регистратор КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПродаж.Регистратор КАК Документ.ОтчетОРозничныхПродажах).Дата
	|		КОГДА НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПродаж.Регистратор КАК Документ.ПриходныйКассовыйОрдер).Дата
	|		КОГДА НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОплатаПлатежнойКартой
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПродаж.Регистратор КАК Документ.ОплатаПлатежнойКартой).Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаДляСортировки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.Организация = &Организация
	|	И НДСЗаписиКнигиПродаж.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|	И НДСЗаписиКнигиПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И (НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|			ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОплатаПлатежнойКартой)
	|	И НЕ НДСЗаписиКнигиПродаж.Регистратор В
	|				(ВЫБРАТЬ
	|					ВТ_ДокументыОснования.Ссылка
	|				ИЗ
	|					ВТ_ДокументыОснования)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДляСортировки";
	
	Запрос.УстановитьПараметр("Организация",   СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериода",  КонецДня(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("Ссылка",        СтруктураПараметров.Ссылка);
	
	ТаблицаОснования = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОснования;
	
КонецФункции

Функция РеквизитыРегламентнойОперации(ДокументСсылка) Экспорт
	
	РеквизитыОперации = Новый Структура();
	РеквизитыОперации.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	РеквизитыОперации.Вставить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Время));
	РеквизитыОперации.Вставить("РегламентнаяОперация", Перечисления.РегламентныеОперации.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданный.Организация КАК Организация,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, СчетФактураВыданный.Дата) КАК Дата,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.ФормированиеЗаписейКнигиПродаж) КАК РегламентнаяОперация
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО СчетФактураВыданный.Организация = ДанныеПервичныхДокументов.Организация
	|			И СчетФактураВыданный.ДокументОснование = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	СчетФактураВыданный.Ссылка = &ДокументСсылка";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыОперации, Результат);
	КонецЕсли;
	
	Возврат РеквизитыОперации;
	
КонецФункции

Процедура ПодготовитьДанныеДляЗаполненияКорректировочнойСправки(СтруктураПараметров, АдресХранилища) Экспорт
	
	ДанныеДляЗаполнения = Новый Структура;
	
	ДанныеДляЗаполнения.Вставить(
		"ДокументыОснования", ПодготовитьДанныеКорректировочнойСправки(СтруктураПараметров));
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

Функция ПодготовитьДанныеКорректировочнойСправки(СтруктураПараметров)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Организация = &Организация
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.Проведен
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СчетФактураВыданныйДокументыОснования.Ссылка <> &Ссылка
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка)
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ИсправляемыйСчетФактура = &ИсходнаяСправка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ОснованияИсходнойСправки
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ПО СчетФактураВыданныйДокументыОснования.ДокументОснование = ОтчетОРозничныхПродажах.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &ИсходнаяСправка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВозвратыПоОРП
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Сделка В
	|			(ВЫБРАТЬ
	|				ОснованияИсходнойСправки.ДокументОснование
	|			ИЗ
	|				ОснованияИсходнойСправки)
	|	И ВозвратТоваровОтПокупателя.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Регистратор КАК ДокументОснование
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.СчетФактура В
	|			(ВЫБРАТЬ
	|				ОснованияИсходнойСправки.ДокументОснование
	|			ИЗ
	|				ОснованияИсходнойСправки)
	|	И НДСЗаписиКнигиПокупок.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НДСЗаписиКнигиПокупок.Активность
	|	И НЕ НДСЗаписиКнигиПокупок.ВидЦенности В (&ВидыЦенностей_Аванс)
	|	И НЕ НДСЗаписиКнигиПокупок.Регистратор В
	|				(ВЫБРАТЬ
	|					ВТ_ДокументыОснования.Ссылка
	|				ИЗ
	|					ВТ_ДокументыОснования)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупок.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.СчетФактура
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.СчетФактура В
	|			(ВЫБРАТЬ
	|				ВозвратыПоОРП.Ссылка
	|			ИЗ
	|				ВозвратыПоОРП)
	|	И НДСЗаписиКнигиПокупок.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НДСЗаписиКнигиПокупок.Активность
	|	И НЕ НДСЗаписиКнигиПокупок.ВидЦенности В (&ВидыЦенностей_Аванс)
	|	И НЕ НДСЗаписиКнигиПокупок.СчетФактура В
	|				(ВЫБРАТЬ
	|					ВТ_ДокументыОснования.Ссылка
	|				ИЗ
	|					ВТ_ДокументыОснования)";
	
	ВидыЦенностей_Аванс = Новый Массив;
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученныеНалоговыйАгент);
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученныхНалоговыйАгент);
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	ВидыЦенностей_Аванс.Добавить(Перечисления.ВидыЦенностей.АвансыВыданныеНалоговыйАгент);
	
	Запрос.УстановитьПараметр("Организация",     СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("Ссылка",          СтруктураПараметров.Ссылка);
	Запрос.УстановитьПараметр("ИсходнаяСправка", СтруктураПараметров.ИсправляемыйСчетФактура);
	Запрос.УстановитьПараметр("НачалоПериода",   НачалоКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериода",    КонецДня(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("ВидыЦенностей_Аванс", ВидыЦенностей_Аванс);
	
	ТаблицаОснования = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОснования;
	
КонецФункции

#Область ФормированиеДвижений

Процедура СформироватьДвиженияСторноНачисленияНДС(ТаблицаРеквизиты, ТаблицаНДСНачисленный, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНДСНачисленный)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период);
	ИспользуетсяПостановлениеНДС1137 = УчетНДСПереопределяемый.ИспользуетсяПостановлениеНДС1137(Реквизиты.Период);
	
	Если Не ПлательщикНДС Или Не ИспользуетсяПостановлениеНДС1137 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыСторноНачисленияНДС(ТаблицаРеквизиты, ТаблицаНДСНачисленный);
	Реквизиты = Параметры.Реквизиты[0];
	
	Для Каждого СтрокаНДСНачисленный Из Параметры.НДСНачисленный Цикл 
		
		// НДС Продажи
		Движение = Движения.НДСЗаписиКнигиПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		ЗаполнитьЗначенияСвойств(Движение, СтрокаНДСНачисленный);
		Движение.Покупатель = СтрокаНДСНачисленный.Контрагент;
		
		Движение.НомерДокументаОплаты = Реквизиты.НомерПлатежноРасчетногоДокумента;
		Движение.ДатаДокументаОплаты =  Реквизиты.ДатаПлатежноРасчетногоДокумента;
		
		// Хозрасчетный
		Движение = Движения.Хозрасчетный.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		
		Движение.Сумма = СтрокаНДСНачисленный.НДС;
		
		Если Реквизиты.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
			
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатам; // 76.АВ
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, 
				"Контрагенты", СтрокаНДСНачисленный.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт,
				"СФВыданные", СтрокаНДСНачисленный.СчетФактура);

			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДС; // 68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт,
				"ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			Движение.Содержание = "Сторнирование НДС по предоплате";
			
		Иначе
			
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыНДСНалоговогоАгента; // 76.НА
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт,
				"Контрагенты", СтрокаНДСНачисленный.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт,
				"Договоры", СтрокаНДСНачисленный.ДоговорКонтрагента);
			
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДСНалоговогоАгента; // 68.32
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт,
				"Контрагенты", СтрокаНДСНачисленный.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт,
				"Договоры", СтрокаНДСНачисленный.ДоговорКонтрагента);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт,
				"ДокументыРасчетовСКонтрагентами", Реквизиты.ДокументОснование);
			Движение.Содержание = "Сторнирование НДС";
			
		КонецЕсли;

	КонецЦикла;
	
	Движения.НДСЗаписиКнигиПродаж.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыСторноНачисленияНДС(ТаблицаРеквизиты, ТаблицаНДСНачисленный)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.НДСНачисленный
	
	СписокОбязательныхКолонок = ""
	+ "Организация,"					// <СправочникСсылка.Организации> - Организация
	+ "Контрагент,"						// <СправочникСсылка.Контрагенты> - Покупатель
	+ "СчетФактура,"					// <ДокументСсылка.*> - Счет-фактура
	+ "ВидЦенности,"					// <ПеречислениеСсылка.ВидыЦенностей> - Вид ценности
	+ "СтавкаНДС,"						// <ПеречислениеСсылка.СтавкиНДС> - Ставка НДС
	+ "ДатаОплаты,"						// <Дата> - Дата оплаты
	+ "ДокументОплаты,"					// <ДокументСсылка.*> - Документ оплаты
	+ "Событие,"						// <ПеречислениеСсылка.СобытияПоНДСПродажи> - Событие
	+ "ДатаСобытия,"					// <Дата> - Дата события
	+ "ЗаписьДополнительногоЛиста,"		// <Булево> - Запись дополнительного листа
	+ "КорректируемыйПериод,"			// <Дата> - Корректируемый период
	+ "СторнирующаяЗаписьДопЛиста,"		// <Булево> - Сторнирующая запись доп. листа
	+ "ДоговорКонтрагента,"				// <СправочникСсылка.ДоговорыКонтрагентов> - Договор контрагента
	+ "ИсправленныйСчетФактура,"		// <ДокументСсылка.*> - Исправленный счет-фактура
	+ "СуммаБезНДС,"					// <Число,15,2> - Сумма без НДС
	+ "НДС";							// <Число,15,2> - НДС

	Параметры.Вставить("НДСНачисленный", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаНДСНачисленный, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Регистратор,"			// <ДокументСсылка.*> - документ-регистратор движений
	+ "Организация,"			// <СправочникСсылка.Организации> - Организация
	+ "Период,"					// <Дата> - период движений - дата документа
	+ "ВидСчетаФактуры,"		// <ПеречислениеСсылка.ВидСчетаФактурыВыставленного> - вид счета-фактуры
	+ "ДокументОснование,"		// <ДокументСсылка.*> - сторнируемый документ
	+ "ДатаПлатежноРасчетногоДокумента,"
	+ "НомерПлатежноРасчетногоДокумента";
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

// Обработчик обновления 3.0.24.1
//
// Для всех счетов-фактур выданных устанавливается реквизит ПредставлениеНомера
// Для корректировочных счетов-фактур переопределяется СуммаДокумента и СуммаНДСДокумента
Процедура ОбработатьНомераИСуммыСчетаФактуры() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА КорректировкаРеализацииТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА КорректировкаРеализацииТовары.Сумма
	|			ИНАЧЕ КорректировкаРеализацииТовары.Сумма + КорректировкаРеализацииТовары.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС) КАК СуммаНДСДокумента,
	|	КорректировкаРеализацииТовары.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТ_ОснованияКоррСФ
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА КорректировкаРеализацииУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА КорректировкаРеализацииУслуги.Сумма
	|			ИНАЧЕ КорректировкаРеализацииУслуги.Сумма + КорректировкаРеализацииУслуги.СуммаНДС
	|		КОНЕЦ),
	|	СУММА(КорректировкаРеализацииУслуги.СуммаНДС),
	|	КорректировкаРеализацииУслуги.Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации.Услуги КАК КорректировкаРеализацииУслуги
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииУслуги.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА КорректировкаРеализацииАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА КорректировкаРеализацииАгентскиеУслуги.Сумма
	|			ИНАЧЕ КорректировкаРеализацииАгентскиеУслуги.Сумма + КорректировкаРеализацииАгентскиеУслуги.СуммаНДС
	|		КОНЕЦ),
	|	СУММА(КорректировкаРеализацииАгентскиеУслуги.СуммаНДС),
	|	КорректировкаРеализацииАгентскиеУслуги.Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации.АгентскиеУслуги КАК КорректировкаРеализацииАгентскиеУслуги
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииАгентскиеУслуги.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка,
	|	СУММА(ЕСТЬNULL(ВТ_ОснованияКоррСФ.СуммаНДСДокумента, 0)) КАК СуммаНДСДокумента,
	|	СУММА(ЕСТЬNULL(ВТ_ОснованияКоррСФ.СуммаДокумента, 0)) КАК СуммаДокумента
	|ПОМЕСТИТЬ ВТ_СуммыКорректировочныхСФ
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОснованияКоррСФ КАК ВТ_ОснованияКоррСФ
	|		ПО СчетФактураВыданныйДокументыОснования.ДокументОснование = ВТ_ОснованияКоррСФ.Документ
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйДокументыОснования.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ВТ_СуммыКорректировочныхСФ.СуммаНДСДокумента
	|		ИНАЧЕ СчетФактураВыданный.СуммаНДСДокумента
	|	КОНЕЦ КАК СуммаНДСДокументаПосле,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ВТ_СуммыКорректировочныхСФ.СуммаДокумента
	|		ИНАЧЕ СчетФактураВыданный.СуммаДокумента
	|	КОНЕЦ КАК СуммаДокументаПосле,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Корректировочный,
	|	СчетФактураВыданный.СуммаДокумента КАК СуммаДокументаДо,
	|	СчетФактураВыданный.СуммаНДСДокумента КАК СуммаНДСДокументаДо,
	|	СчетФактураВыданный.ПредставлениеНомера
	|ПОМЕСТИТЬ ВТ_СчетаФактурыПредварительная
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыКорректировочныхСФ КАК ВТ_СуммыКорректировочныхСФ
	|		ПО СчетФактураВыданный.Ссылка = ВТ_СуммыКорректировочныхСФ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СчетаФактурыПредварительная.Ссылка,
	|	ВТ_СчетаФактурыПредварительная.СуммаНДСДокументаПосле КАК СуммаНДСДокумента,
	|	ВТ_СчетаФактурыПредварительная.СуммаДокументаПосле КАК СуммаДокумента,
	|	ВТ_СчетаФактурыПредварительная.Корректировочный
	|ИЗ
	|	ВТ_СчетаФактурыПредварительная КАК ВТ_СчетаФактурыПредварительная
	|ГДЕ
	|	(ВТ_СчетаФактурыПредварительная.ПредставлениеНомера = """"
	|			ИЛИ ВТ_СчетаФактурыПредварительная.СуммаДокументаДо <> ВТ_СчетаФактурыПредварительная.СуммаДокументаПосле
	|			ИЛИ ВТ_СчетаФактурыПредварительная.СуммаНДСДокументаДо <> ВТ_СчетаФактурыПредварительная.СуммаНДСДокументаПосле)";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбрабатываемыйСчетФактура = Выборка.Ссылка;
			
			ОбъектСчетФактура = ОбрабатываемыйСчетФактура.ПолучитьОбъект();
			
			Если Выборка.Корректировочный Тогда
				ЗаполнитьЗначенияСвойств(ОбъектСчетФактура, Выборка); 
			КонецЕсли;
			
			ОбъектСчетФактура.ОбменДанными.Загрузка = Истина;
			
			НачатьТранзакцию();
			
			Попытка
				ОбъектСчетФактура.Записать();
			Исключение
				ТекстОшибки = НСтр("ru = 'Не удалось записать документ.
                                    |%1'");
				ОписаниеОшибки = СтрШаблон(ТекстОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка,
					ОбъектСчетФактура.Метаданные(),
					ОбъектСчетФактура.Ссылка, 
					ОписаниеОшибки);
					
				ОтменитьТранзакцию();
			КонецПопытки;
			
			ЗафиксироватьТранзакцию();
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

// Обработчик обновления 3.0.37.38
// В версии 3.0.37 добавлен новый вид счета-фактуры выданного "На аванс комитента на закупку"
// он соответствует прежнему "На аванс комитента" для счетов-фактур комиссионера по закупке
// Обработчик выбирает все счета-фактуры с видом "На аванс комитента" и договором "С комитентом по закупке"
// и устанавливает им вид "На аванс комитента на закупку"
Процедура УстановитьВидОперацииНаАвансКомитентуНаЗакупку() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|	И СчетФактураВыданный.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		СчетФактураНаАвансКомитента = Выборка.Ссылка.ПолучитьОбъект();
		СчетФактураНаАвансКомитента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СчетФактураНаАвансКомитента);
	
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления
//
//Устанавливает новый код вида операции для сводных счетов-фактур по комиссии
Процедура УстановитьКодВидаОперацииСводныйКомиссионный() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетКомитентуОПродажахПоставщики.СчетФактура КАК СчетФактура,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ВТ_СводныеСгенерированныеПредварительная
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Поставщики КАК ОтчетКомитентуОПродажахПоставщики
	|ГДЕ
	|	ОтчетКомитентуОПродажахПоставщики.Ссылка.ВыписыватьСчетаФактурыСводно
	|	И ОтчетКомитентуОПродажахПоставщики.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ОтчетКомитентуОПродажахПоставщики.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомитентуоПродажах.ОтчетОЗакупках)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомитентуОПродажахПоставщики.СчетФактура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура,
	|	СУММА(1)
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|ГДЕ
	|	ОтчетКомиссионераОПродажахПокупатели.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ОтчетКомиссионераОПродажахПокупатели.Ссылка.ВыписыватьСчетаФактурыСводно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СводныеСгенерированныеПредварительная.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ВТ_СводныеСгенерированные
	|ИЗ
	|	ВТ_СводныеСгенерированныеПредварительная КАК ВТ_СводныеСгенерированныеПредварительная
	|ГДЕ
	|	ВТ_СводныеСгенерированныеПредварительная.Количество > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	""27"" КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.КодВидаОперации = ""27""
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КодУстановлен
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	(СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|			ИЛИ СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|	И СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураВыданный.СводныйКомиссионный = ИСТИНА
	|	И СчетФактураВыданный.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_СводныеСгенерированные.СчетФактура
	|			ИЗ
	|				ВТ_СводныеСгенерированные)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка,
	|	""28"",
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.КодВидаОперации = ""28""
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	(СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
	|			ИЛИ СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|			ИЛИ СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗАкупку))
	|	И СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураВыданный.СводныйКомиссионный = ИСТИНА
	|ИТОГИ
	|	СУММА(КодУстановлен)
	|ПО
	|	ОБЩИЕ";
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если ВыборкаИтоги.Следующий()
		И ВыборкаИтоги.КодУстановлен = 0 Тогда
		ВыборкаДокументы = ВыборкаИтоги.Выбрать();
		Пока ВыборкаДокументы.Следующий() Цикл
			СчетФактураДокумент = ВыборкаДокументы.Ссылка.ПолучитьОбъект();
			СчетФактураДокумент.КодВидаОПерации = ВыборкаДокументы.КодВидаОперации;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СчетФактураДокумент);
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

// Обработчик обновления 3.0.42.8
//
//Переопределяет признак СводныйКомиссионный для счетов-фактур, которые сгенерированы сводно, но не являются сводными
Процедура ПереопределитьПризнакСводныйКомиссионный() Экспорт
	
	//комиссия по продаже
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура КАК СчетФактура,
	|	ОтчетКомиссионераОПродажахПокупатели.Покупатель,
	|	1 КАК Количество
	|ПОМЕСТИТЬ СгенерированныеСводно
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажахПокупатели.СчетФактура.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажахПокупатели.СчетФактура.СводныйКомиссионный
	|	И НЕ ОтчетКомиссионераОПродажахПокупатели.Ссылка.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажахПокупатели.ВыставленСФ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СгенерированныеСводно.СчетФактура,
	|	СУММА(СгенерированныеСводно.Количество) КАК Количество
	|ПОМЕСТИТЬ СводныеПредварительная
	|ИЗ
	|	СгенерированныеСводно КАК СгенерированныеСводно
	|
	|СГРУППИРОВАТЬ ПО
	|	СгенерированныеСводно.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СводныеПредварительная.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ СводныеСФ
	|ИЗ
	|	СводныеПредварительная КАК СводныеПредварительная
	|ГДЕ
	|	СводныеПредварительная.Количество > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СгенерированныеСводно.СчетФактура,
	|	СгенерированныеСводно.Покупатель
	|ИЗ
	|	СгенерированныеСводно КАК СгенерированныеСводно
	|ГДЕ
	|	НЕ СгенерированныеСводно.СчетФактура В
	|				(ВЫБРАТЬ
	|					СводныеСФ.СчетФактура
	|				ИЗ
	|					СводныеСФ)";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СчетФактураДокумент = Выборка.СчетФактура.ПолучитьОбъект();
			СчетФактураДокумент.СводныйКомиссионный = Ложь;
			СчетФактураДокумент.Контрагент = Выборка.Покупатель;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СчетФактураДокумент);
			
		КонецЦикла;
		
	КонецЕсли;
	
	//комиссия по закупке
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомитентуОПродажахПоставщики.СчетФактура КАК СчетФактура,
	|	ОтчетКомитентуОПродажахПоставщики.Поставщик,
	|	1 КАК Количество
	|ПОМЕСТИТЬ СгенерированныеСводно
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Поставщики КАК ОтчетКомитентуОПродажахПоставщики
	|ГДЕ
	|	НЕ ОтчетКомитентуОПродажахПоставщики.СчетФактура.ПометкаУдаления
	|	И ОтчетКомитентуОПродажахПоставщики.СчетФактура.СводныйКомиссионный
	|	И НЕ ОтчетКомитентуОПродажахПоставщики.Ссылка.ПометкаУдаления
	|	И ОтчетКомитентуОПродажахПоставщики.ПолученСФ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СгенерированныеСводно.СчетФактура КАК СчетФактура,
	|	СУММА(СгенерированныеСводно.Количество) КАК Количество
	|ПОМЕСТИТЬ СводныеПредварительная
	|ИЗ
	|	СгенерированныеСводно КАК СгенерированныеСводно
	|
	|СГРУППИРОВАТЬ ПО
	|	СгенерированныеСводно.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СводныеПредварительная.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ СводныеСФ
	|ИЗ
	|	СводныеПредварительная КАК СводныеПредварительная
	|ГДЕ
	|	СводныеПредварительная.Количество > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СгенерированныеСводно.СчетФактура,
	|	СгенерированныеСводно.Поставщик
	|ИЗ
	|	СгенерированныеСводно КАК СгенерированныеСводно
	|ГДЕ
	|	НЕ СгенерированныеСводно.СчетФактура В
	|				(ВЫБРАТЬ
	|					СводныеСФ.СчетФактура
	|				ИЗ
	|					СводныеСФ)";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СчетФактураДокумент = Выборка.СчетФактура.ПолучитьОбъект();
			СчетФактураДокумент.СводныйКомиссионный = Ложь;
			СчетФактураДокумент.Продавец = Выборка.Поставщик;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СчетФактураДокумент);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

Функция НовыйТаблицаРеквизитовКорректировочногоСчетаФактуры()
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Дата",                                         ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Реквизиты.Колонки.Добавить("ДокументОснование",                            Новый ОписаниеТипов(Документы.ТипВсеСсылки()));
	Реквизиты.Колонки.Добавить("Организация",                                  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Реквизиты.Колонки.Добавить("Контрагент",                                   Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Реквизиты.Колонки.Добавить("КППКонтрагента",                               Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ДоговорКонтрагента",                           Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	Реквизиты.Колонки.Добавить("Исправление",                                  Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ИсправляемыйСчетФактура",                      Новый ОписаниеТипов("ДокументСсылка.СчетФактураВыданный"));
	Реквизиты.Колонки.Добавить("НомерИсправления",                             Новый ОписаниеТипов("Число"));
	Реквизиты.Колонки.Добавить("КодВидаОперацииОснования",                     Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("КодВидаОперацииОснованияУменьшение",           Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("УчитыватьИсправлениеИсходногоДокумента",       Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("НомерИсходногоДокумента",                      Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ДатаИсходногоДокумента",                       ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Реквизиты.Колонки.Добавить("НомерИсправленияИсходногоДокумента",           Новый ОписаниеТипов("Число"));
	Реквизиты.Колонки.Добавить("ДатаИсправленияИсходногоДокумента",            ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Реквизиты.Колонки.Добавить("НомерИсправляемогоКорректировочногоДокумента", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ДатаИсправляемогоКорректировочногоДокумента",  ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Реквизиты.Колонки.Добавить("СводныйКомиссионный",                          Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ВидСчетаФактуры", Новый ОписаниеТипов("ПеречислениеСсылка.ВидСчетаФактурыВыставленного"));

	Возврат Реквизиты;

КонецФункции

#КонецОбласти

#КонецЕсли