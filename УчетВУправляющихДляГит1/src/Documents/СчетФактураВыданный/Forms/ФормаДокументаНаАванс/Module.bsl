
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОтправкаПочтовыхСообщений.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.СчетФактураВыданный",
		"ФормаДокументаНаАванс",
		НСтр("ru='Новости: Счет-фактура выданный'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// Подсистема "ОбменСКонтрагентами"
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец Подсистема "ОбменСКонтрагентами"
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма")
		И ИсточникВыбора.ИмяФормы = "Обработка.ПодборОснованийСчетаФактуры.Форма.ФормаВыбораОснованияСчетаФактуры" Тогда 
		Модифицированность = Истина;
		Объект.ДокументОснование = ВыбранноеЗначение.Документ;
		ПриИзмененииДокументаОснования();
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		ЗаполнитьНоменклатуруПоСчетуНаСервере(ВыбранноеЗначение);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеСчетФактураВыданныйНаАванс";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПредставлениеДокумента = Документы.СчетФактураВыданный.ПолучитьПредставлениеДокумента(Объект.Ссылка, Объект.ВидСчетаФактуры);
	
	ЗаполнитьДобавленныеКолонкиТаблицыАвансы();
	
	УстановитьЗаголовокФормы(ЭтотОбъект, ПредставлениеДокумента);
	
	УстановитьСостояниеДокумента();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_СчетФактураВыданный", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидСчетаФактурыПриИзменении(Элемент)

	ВидСчетаФактурыПриИзмененииНаСервере();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;
	
	Если Объект.Дата < '20150101'
		И Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку") 
		И Объект.СводныйКомиссионный Тогда
		ТекстСообщения = НСтр("ru='До 2015 года счета-фактуры на авансы комитента на закупку
		|создаются только на основании счета-фактуры полученного.'");
		Объект.Дата = ТекущаяДатаДокумента;
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента);
		
	// Проверка на изменение ответственных лиц.
	Если НЕ ТребуетсяВызовСервера Тогда
		Если ТипЗнч(ДатыИзмененияОтветственныхЛиц) = Тип("ФиксированныйМассив") Тогда
			ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиентСервер.ДатыПринадлежатРазнымИнтервалам(Объект.Дата, 
				ТекущаяДатаДокумента, ДатыИзмененияОтветственныхЛиц);
		КонецЕсли;
	КонецЕсли;
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПриИзмененииНаСервере();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	ДоговорКонтрагентаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыставленПриИзменении(Элемент)
	
	ПриИзмененииПризнакаВыставления();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПризнакаВыставления()
	
	Если НЕ Объект.Выставлен Тогда
		Объект.ДатаВыставления	= '00010101';
	Иначе
		Объект.ДатаВыставления	= Объект.Дата;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийКод = Элемент.СписокВыбора.НайтиПоЗначению(Объект.КодВидаОперации);
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеВыбора, Элемент.СписокВыбора, Элемент, ТекущийКод);
	
КонецПроцедуры

&НаКлиенте
Процедура КодСпособаВыставленияПриИзменении(Элемент)
	
	Если Объект.КодСпособаВыставления = 2 Тогда
		Объект.Выставлен = Ложь;
		Объект.ДатаВыставления = '00010101';
	Иначе
		Объект.Выставлен = Истина;
		ПриИзмененииПризнакаВыставления();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	ОтветственныйПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеЭДОНажатие(Элемент)
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ДекорацияСостояниеЭДОНажатие(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПродавецПриИзменении(Элемент)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура КомитентПриИзменении(Элемент)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторГосКонтрактаРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ВключитьИспользованиеГособоронзаказа" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма.ФормаФункциональностьПрограммы",
			Новый Структура("ТекущаяСтраница", "ГруппаРасчеты"));
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАвансы

&НаКлиенте
Процедура АвансыПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		РасчетнаяСтавкаНДС = УчетНДСКлиентСервер.РасчетнаяСтавкаНДСПоУмолчанию(Объект.Дата);
		Элемент.ТекущиеДанные.СтавкаНДС                = РасчетнаяСтавкаНДС;
		Элемент.ТекущиеДанные.СтавкаНДСДоКорректировки = РасчетнаяСтавкаНДС;
	КонецЕсли;
	
	ЗаполнитьЗаголовкиСтрок(Элемент.ТекущиеДанные);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку") Тогда
		СводноПоПолученным = Объект.Авансы.Количество() = 2;
		Если СводноПоПолученным Тогда 
			Объект.КодВидаОперации = ПолучитьКодВидаОперации();
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные  = Элементы.Авансы.ТекущиеДанные;
	НовыеПараметры = ПолучитьДанныеНоменклатураПриИзменении(
		ТекущиеДанные.Номенклатура, ТекущаяДатаДокумента, ПокупательНалоговыйАгентПоНДС);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, НовыеПараметры);
	РассчитатьСуммуНДСТабЧасти(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыСодержаниеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Авансы.ТекущиеДанные;
	РаботаСНоменклатуройКлиент.СодержаниеПриИзменении(ТекущиеДанные, ТекущаяДатаДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыКорректируемыйСчетФактураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Авансы.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.КорректируемыйСчетФактура)
		И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СуммаДоКорректировки) Тогда
		ДанныеСтроки = Новый Структура("КорректируемыйСчетФактура,Сумма,СтавкаНДС,СуммаНДС");
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТабличнойЧасти);
		ПараметрыДоплаты = ПараметрыДоплатыКАвансу(ДанныеСтроки);
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ПараметрыДоплаты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыКорректируемыйСчетФактураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Объект.Ссылка = ВыбранноеЗначение Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение( , НСтр("ru='Нельзя выбирать в качестве корректируемого документа этот же документ'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыСуммаДоКорректировкиПриИзменении(Элемент)
	
	ПриИзмененииСуммыСтавкиДоКорректировки();
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыСуммаПослеКорректировкиПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Авансы.ТекущиеДанные;
	РассчитатьСуммуНДСТабЧасти(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыСтавкаНДСДоКорректировкиПриИзменении(Элемент)
	
	ПриИзмененииСуммыСтавкиДоКорректировки();
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыСтавкаНДСПослеКорректировкиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Авансы.ТекущиеДанные;
	РассчитатьСуммуНДСТабЧасти(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьНоменклатуруПоСчетуНаОплату(Команда)
	
	Если Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку") Тогда
		Возврат;
	КонецЕсли;
	ПерезаполнятьТоварыУслугиПоСчету = Ложь;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Организация", Объект.Организация);
	Отбор.Вставить("Контрагент", Объект.Контрагент);
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		Отбор.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отбор, Объект);
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуруПоСчетуНаОплату(Команда)
	
	Если Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку") Тогда
		Возврат;
	КонецЕсли;
	ПерезаполнятьТоварыУслугиПоСчету = Истина;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Организация", Объект.Организация);
	Отбор.Вставить("Контрагент", Объект.Контрагент);
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		Отбор.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПолученнымСчетамФактурам(Команда)
	
	Если Объект.ВидСчетаФактуры <> ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку") Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ТекстСообщения = НСтр("ru='Поле ""Контрагент"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Контрагент", , Отказ);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ТекстСообщения = НСтр("ru='Поле ""Договор"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ДоговорКонтрагента", , Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоПолученнымСчетамФактурамНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	ЗаполнитьДобавленныеКолонкиТаблицыАвансы();
	
	ПредставлениеДокумента = Документы.СчетФактураВыданный.ПолучитьПредставлениеДокумента(
		Объект.Ссылка, Объект.ВидСчетаФактуры);
	УстановитьЗаголовокФормы(ЭтотОбъект, ПредставлениеДокумента);
	
	ТекущаяДатаДокумента 			= Объект.Дата;
	ТекущийНомерДокумента			= Объект.Номер;
	
	ЗаполнитьСписокКодовОпераций();
	
	ДатыИзмененияОтветственныхЛиц = ОтветственныеЛицаБППовтИсп.ДатыИзмененияОтветственныхЛицОрганизаций(Объект.Организация);
	
	КомиссияЗакупка = Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку;
	СписокТиповДляВыбора.ЗагрузитьЗначения(УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(
		Объект.ВидСчетаФактуры, Объект.Исправление, КомиссияЗакупка));
	
	УправлениеФормой(ЭтотОбъект);
	
	Если Обработки.ФункциональностьПрограммы.ДоступнаФункциональностьГособоронзаказ() Тогда
		Элементы.ИдентификаторГосКонтракта.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ДатаДляФункциональныхОпций = ?(Объект.Исправление, Объект.ДатаИсходногоДокумента, Объект.Дата);
	
	ИспользуетсяПостановлениеНДС1137 = УчетНДСПереопределяемый.ИспользуетсяПостановлениеНДС1137(ДатаДляФункциональныхОпций);
	ВедетсяУчетНДСПоФЗ81 = УчетНДС.ВедетсяУчетНДСПоФЗ81(ДатаДляФункциональныхОпций);
	ВедетсяУчетНДСПоФЗ56 = УчетНДС.ВедетсяУчетНДСПоФЗ56(ДатаДляФункциональныхОпций);
	ОтражатьСуммыВЖурнале = Объект.ДатаВыставления >= '20150101' 
		И (Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента
		ИЛИ Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку);
	ВерсияКодовВидовОпераций = УчетНДСКлиентСервер.ВерсияКодовВидовОпераций(ДатаДляФункциональныхОпций);
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		УчетАгентскогоНДСПокупателем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
		ПокупательНалоговыйАгентПоНДС = УчетАгентскогоНДСПокупателем
			И УчетНДС.ВедетсяУчетНДСПоФЗ335(Объект.Дата);
	Иначе
		ПокупательНалоговыйАгентПоНДС = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(ЭтотОбъект)
	
	Элементы = ЭтотОбъект.Элементы;
	Объект   = ЭтотОбъект.Объект;
	
	Если Объект.СформированПриВводеНачальныхОстатковНДС Тогда
		ЭтотОбъект.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ПоОтчетуКомиссионера = ЗначениеЗаполнено(Объект.ДокументОснование)
		И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах");
		
	ЭтоИсправлениеПоПостановлению1137 = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 И Объект.Исправление;
	Элементы.НомерИсправленияСистемный.Доступность = ЭтоИсправлениеПоПостановлению1137;
	Элементы.НомерИсходногоДокумента.Доступность   = ЭтоИсправлениеПоПостановлению1137;
	Элементы.ДатаИсходногоДокумента.Доступность    = ЭтоИсправлениеПоПостановлению1137;
	
	Если ЭтоИсправлениеПоПостановлению1137 Тогда
		ЭтотОбъект.РеквизитыИсправляемыйСФНадпись = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 от %2'"),
			Объект.НомерИсходногоДокумента,Формат(Объект.ДатаИсходногоДокумента,"ДЛФ=Д"));
	КонецЕсли;
	
	Элементы.РеквизитыИсправляемыйСФНадпись.Видимость = ЭтоИсправлениеПоПостановлению1137;
	Элементы.ГруппаРеквизитыИсправления.Видимость     = ЭтоИсправлениеПоПостановлению1137;
	Элементы.РеквизитыТекущийСФ.Видимость             = НЕ ЭтоИсправлениеПоПостановлению1137;
	Элементы.ИдентификаторГосКонтракта.Видимость      = ЭтотОбъект.ВедетсяУчетНДСПоФЗ56;
	
	Элементы.ГруппаВидаОперации.Видимость = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137;
	ТекущийКод = Элементы.КодВидаОперации.СписокВыбора.НайтиПоЗначению(Объект.КодВидаОперации);
	Если ТекущийКод <> Неопределено Тогда 
		ЭтотОбъект.НадписьВидОперации = Сред(ТекущийКод.Представление, 5);
	Иначе
		ЭтотОбъект.НадписьВидОперации = "";
	КонецЕсли;
	
	Элементы.Выставлен.Доступность = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 
		И НЕ Объект.СчетФактураНеВыставляется;
	Элементы.ДатаВыставления.Доступность = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 
		И НЕ Объект.СчетФактураНеВыставляется И Объект.Выставлен;
	Элементы.КодСпособаВыставления.Видимость   = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137; 
	Элементы.КодСпособаВыставления.Доступность = НЕ Объект.СчетФактураНеВыставляется;
	
	// Комиссионер получил предварительную оплату в счет последующей отгрузки товаров (работ, услуг) комитента от покупателя. 
	// Покупателю был выставлен счет-фактура (с видом "На аванс комитента").
	НаАвансКомитента = Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента");
	// Комиссионер, приобретающий товары (работы, услуги) для комитента, выдал предварительную оплату поставщику, 
	// комиссионер перевыставляет счет-фактуру комитенту (с видом "На аванс комитента на закупку") по полученному счету-фактуре от поставщика.
	НаАвансКомитентаНаЗакупку = Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку");
	КорректировочныйНаАванс = Объект.ВидСчетаФактуры = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс");
	
	Элементы.ВидСчетаФактуры.Видимость = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 И НЕ НаАвансКомитентаНаЗакупку;
	
	Элементы.Комитент.Видимость = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 И НаАвансКомитента;
		
	Элементы.Продавец.Видимость = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 
		И НаАвансКомитентаНаЗакупку И НЕ Объект.СводныйКомиссионный;
	Элементы.ГруппаДатаНомерДокументаАвансаКомитента.Видимость = ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 
		И НаАвансКомитентаНаЗакупку;
	Элементы.РеквизитыПлатежноРасчетногоДокумента.Видимость = НЕ (ЭтотОбъект.ИспользуетсяПостановлениеНДС1137 
		И НаАвансКомитентаНаЗакупку И Объект.СводныйКомиссионный);

	Элементы.ДоговорКонтрагента.Видимость = НЕ НаАвансКомитентаНаЗакупку;
	Элементы.ДоговорКомитента.Видимость   = НаАвансКомитентаНаЗакупку;
		
	Элементы.ДокументОснование.Видимость = НЕ НаАвансКомитентаНаЗакупку;
	Элементы.ДокументОснованиеВыданныйАвансКомитента.Видимость = НаАвансКомитентаНаЗакупку И НЕ Объект.СводныйКомиссионный;
	
	Элементы.АвансыЗаполнитьНоменклатуруПоСчетуНаОплату.Видимость = НЕ НаАвансКомитентаНаЗакупку;
	Элементы.АвансыДобавитьНоменклатуруПоСчетуНаОплату.Видимость  = НЕ НаАвансКомитентаНаЗакупку;
	Элементы.АвансыЗаполнитьПоПолученнымСчетамФактурам.Видимость  = НаАвансКомитентаНаЗакупку И Объект.СводныйКомиссионный;
	
	Если НаАвансКомитентаНаЗакупку Тогда
		Элементы.АвансыКонтрагент.Заголовок = НСтр("ru='Составлен от имени'");
	КонецЕсли;
		
	Элементы.ГруппаКомиссия.Видимость = ЭтотОбъект.ОтражатьСуммыВЖурнале;
	
	// При реализации товаров, указанных в п.8 ст. 161 НК счет-фактура выставляется без учета сумм налога.
	Элементы.СуммаНДСДокумента.Видимость = НЕ ЭтотОбъект.ПокупательНалоговыйАгентПоНДС;

	ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Объект.Организация);
	
	Элементы.Руководитель.Видимость = Истина;
	Если ЭтоЮрЛицо Тогда
		Элементы.ГлавныйБухгалтер.Видимость = Истина;
		Элементы.Руководитель.Заголовок     = НСтр("ru = 'Руководитель'");
	ИначеЕсли ЭтотОбъект.ВедетсяУчетНДСПоФЗ81 Тогда
		Элементы.ГлавныйБухгалтер.Видимость = Ложь;
		Элементы.Руководитель.Заголовок     = НСтр("ru = 'Предприниматель'");
	Иначе
		Элементы.Руководитель.Видимость = Ложь;
	КонецЕсли;
	
	Если КорректировочныйНаАванс Тогда
		Элементы.АвансыГруппаНоменклатураСодержание.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.АвансыСодержание.ОтображатьВШапке = Ложь;
		Элементы.СуммаДокумента.Заголовок = НСтр("ru='Увеличение суммы'");
	Иначе
		Элементы.АвансыГруппаНоменклатураСодержание.Группировка = ГруппировкаКолонок.Горизонтальная;
		Элементы.АвансыСодержание.ОтображатьВШапке = Истина;
		Элементы.СуммаДокумента.Заголовок = НСтр("ru='Всего'");
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(ЭтотОбъект, ПредставлениеДокумента)
	
	ЭтотОбъект.Заголовок = ПредставлениеДокумента.СчетФактураПредставление;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДокументаОснованияНаСервере()

	ЗаполнитьСписокКодовОпераций();
	
	ЗаполнитьСчетФактуруНаАвансНаСервере(Объект.ДокументОснование);
	
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСчетФактуруНаАвансНаСервере(Знач ДокументОснование)
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьСчетФактуруНаАванс(ДокументОснование, ДокументОбъект.Ссылка, Объект.ДоговорКонтрагента);
	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Функция ДатаПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	Если ИспользуетсяПостановлениеНДС1137 Тогда
		Объект.КодВидаОперации = ПолучитьКодВидаОперации();
	Иначе
		
		Объект.КодВидаОперации = "";
		
		Объект.Исправление             = Ложь;
		Объект.НомерИсправления        = 0;
		Объект.НомерИсходногоДокумента = "";
		Объект.ДатаИсходногоДокумента  = '00010101';
		
		Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс;
		
		Объект.Продавец = Неопределено;
		Объект.НомерДокументаАвансаКомитента = "";
		Объект.ДатаДокументаАвансаКомитента  = '00010101';
		
	КонецЕсли;
	
	РаботаСНоменклатурой.ОбновитьСодержаниеУслуг(Объект.Авансы, Объект.Дата, ТекущаяДатаДокумента);
	
	ЗаполнитьСписокКодовОпераций();
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокКодовОпераций()
	
	УчетНДС.ЗаполнитьСписокКодовВидовОпераций(
		Перечисления.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры,
		Элементы.КодВидаОперации.СписокВыбора,
		Объект.Дата);
		
КонецПроцедуры

&НаСервере
Процедура ВидСчетаФактурыПриИзмененииНаСервере()

	Если Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
		ОчиститьРеквизитыПоКомиссии();
		ОчиститьСуммыДоКорректировки();
	ИначеЕсли Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс Тогда
		ОчиститьРеквизитыПоКомиссии();
	Иначе
		ОчиститьСуммыДоКорректировки();
		Если ОтражатьСуммыВЖурнале Тогда 
			Объект.СуммаДокументаКомиссия    = Объект.Авансы.Итог("Сумма");
			Объект.СуммаНДСДокументаКомиссия = Объект.Авансы.Итог("СуммаНДС");
		Иначе
			Объект.СуммаДокументаКомиссия    = 0;
			Объект.СуммаНДСДокументаКомиссия = 0;
		КонецЕсли;
	КонецЕсли;
	
	КомиссияЗакупка = Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку;
	
	СписокТиповДляВыбора.ЗагрузитьЗначения(
		УчетНДСПереопределяемый.ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(
			Объект.ВидСчетаФактуры, Объект.Исправление, КомиссияЗакупка));
		
	Если ИспользуетсяПостановлениеНДС1137 Тогда
		Объект.КодВидаОперации = ПолучитьКодВидаОперации();
	КонецЕсли;
	
	ПредставлениеДокумента = Документы.СчетФактураВыданный.ПолучитьПредставлениеДокумента(
		Объект.Ссылка, Объект.ВидСчетаФактуры);
	УстановитьЗаголовокФормы(ЭтотОбъект, ПредставлениеДокумента);
	
	ЗаполнитьДобавленныеКолонкиТаблицыАвансы();
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОчиститьСуммыДоКорректировки()
	
	Для Каждого СтрокаАванса Из Объект.Авансы Цикл
		Если СтрокаАванса.СуммаДоКорректировки > 0 Тогда
			СтрокаАванса.Сумма = СтрокаАванса.Сумма - СтрокаАванса.СуммаДоКорректировки;
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(
				СтрокаАванса, Истина);
		КонецЕсли;
		СтрокаАванса.СуммаДоКорректировки            = 0;
		СтрокаАванса.СуммаНДСДоКорректировки         = 0;
		СтрокаАванса.СтавкаНДСДоКорректировки        = Перечисления.СтавкиНДС.ПустаяСсылка();
		СтрокаАванса.КорректируемыйСчетФактура       = Неопределено;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыПоКомиссии()
	
	Объект.Комитент                      = Неопределено;
	Объект.Продавец                      = Неопределено;
	Объект.ДатаДокументаАвансаКомитента  = '00010101';
	Объект.НомерДокументаАвансаКомитента = "";
	Объект.СуммаДокументаКомиссия        = 0;
	Объект.СуммаНДСДокументаКомиссия     = 0;

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	Объект.ДокументОснование	= Неопределено;
	Объект.ДокументыОснования.Очистить();
	Объект.ПлатежноРасчетныеДокументы.Очистить();
	
	УстановитьФункциональныеОпцииФормы();
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
	ДатыИзмененияОтветственныхЛиц = ОтветственныеЛицаБППовтИсп.ДатыИзмененияОтветственныхЛицОрганизаций(Объект.Организация);
	
	Объект.СуммаДокумента		= 0;
	Объект.СуммаНДСДокумента	= 0;
	
	Объект.ДатаПлатежноРасчетногоДокумента	= '00010101';
	Объект.НомерПлатежноРасчетногоДокумента	= "";
	Объект.ПлатежноРасчетныеДокументы.Очистить();
	
	Объект.НомерДокументаАвансаКомитента	= "";
	Объект.ДатаДокументаАвансаКомитента		= '00010101';
	
	ЗаполнитьПорядокВыставленияСчетаФактуры();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()

	Объект.ДокументОснование  = Неопределено;
	Объект.ДоговорКонтрагента = Неопределено;
	Объект.ДокументыОснования.Очистить();
	Объект.ПлатежноРасчетныеДокументы.Очистить();
	
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
	ЗаполнитьПорядокВыставленияСчетаФактуры();
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПорядокВыставленияСчетаФактуры()
	
	СтруктураПараметров = Новый Структура("СчетФактураНеВыставляется, ДокументОснование, ВидСчетаФактуры, Дата");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, Объект);
	
	ПараметрыВыставления = Документы.СчетФактураВыданный.ОпределитьПорядокВыставленияСчетаФактуры(СтруктураПараметров);
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыВыставления);
	
КонецПроцедуры

&НаСервере
Процедура ОтветственныйПриИзмененииНаСервере()
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(Знач Номенклатура, Знач Дата, Знач ПокупательНалоговыйАгентПоНДС)
	
	НовыеПараметры = Новый Структура;
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат НовыеПараметры;
	КонецЕсли;
	
	ПараметрыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Номенклатура, "Услуга,НаименованиеПолное,ВидСтавкиНДС,ПериодичностьУслуги");
	
	Если ПараметрыНоменклатуры.Услуга Тогда
		НовыеПараметры.Вставить("Содержание", РаботаСНоменклатуройКлиентСервер.СодержаниеУслуги(
			ПараметрыНоменклатуры.НаименованиеПолное, ПараметрыНоменклатуры.ПериодичностьУслуги, Дата));
	КонецЕсли;
	
	Если ПокупательНалоговыйАгентПоНДС Тогда
		НовыеПараметры.Вставить("СтавкаНДС", УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(Дата));
	ИначеЕсли ПараметрыНоменклатуры.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.Пониженная
		  ИЛИ ПараметрыНоменклатуры.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.ПониженнаяРасчетная Тогда
		НовыеПараметры.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС10_110);
	Иначе
		НовыеПараметры.Вставить("СтавкаНДС", УчетНДСКлиентСервер.РасчетнаяСтавкаНДСПоУмолчанию(Дата));
	КонецЕсли;
	
	Возврат НовыеПараметры;
		
КонецФункции

&НаКлиенте
Процедура РассчитатьСуммуНДСТабЧасти(ТекущиеДанные)
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(
		ТекущиеДанные, Истина);
		
	Если ПокупательНалоговыйАгентПоНДС Тогда
		ТекущиеДанные.СуммаНДС = 0;
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммыСтавкиДоКорректировки()

	СуффиксПоля = "ДоКорректировки";
	
	СтрокаТабличнойЧасти = Элементы.Авансы.ТекущиеДанные;

	СтрокаТаблицы = Новый Структура("Сумма, СтавкаНДС, СуммаНДС", 0, 0, 0);
	Для Каждого Поле Из СтрокаТаблицы Цикл
		СтрокаТаблицы[Поле.Ключ] = СтрокаТабличнойЧасти[Поле.Ключ + СуффиксПоля];
	КонецЦикла;

	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(
		СтрокаТаблицы, Истина);

	Для Каждого Поле Из СтрокаТаблицы Цикл
		СтрокаТабличнойЧасти[Поле.Ключ + СуффиксПоля] = Поле.Значение;
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруПоСчетуНаСервере(СчетНаОплату)
	
	// Заполняем табличную часть номенклатурой из выбранного счета на оплату
	
	НоменклатураСчетаНаОплату = Документы.СчетФактураВыданный.НоменклатураСчетаНаОплату(СчетНаОплату);
	ВалютаСчетаНаОплату       = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетНаОплату, "ВалютаДокумента");
	ДатаАванса = ?(ЗначениеЗаполнено(Объект.ДатаПлатежноРасчетногоДокумента),
				Объект.ДатаПлатежноРасчетногоДокумента, Объект.Дата);
				
	Если ПерезаполнятьТоварыУслугиПоСчету
		И Объект.Авансы.Количество() <> 0 Тогда
		
		// Очистить табличную часть и заполнить заново номенклатурой из счета на оплату
		// так, чтобы рублевая сумма указанных до очистки авансов не превышала бы рублевую сумму счета.
		
		АвансыПоСтавкамНДС = Объект.Авансы.Выгрузить();
		АвансыПоСтавкамНДС.Свернуть("СтавкаНДС", "Сумма");
		АвансыПоСтавкамНДС.Колонки.Добавить("СчетНаОплату", Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПокупателю"));
		АвансыПоСтавкамНДС.ЗаполнитьЗначения(СчетНаОплату, "СчетНаОплату");
		АвансыПоСтавкамНДС.Колонки.Добавить("ВалютаСчетаНаОплату", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
		АвансыПоСтавкамНДС.ЗаполнитьЗначения(ВалютаСчетаНаОплату, "ВалютаСчетаНаОплату");
		АвансыПоСтавкамНДС.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
		АвансыПоСтавкамНДС.ЗаполнитьЗначения(ДатаАванса, "Дата");
		АвансыПоСтавкамНДС.Колонки.Добавить("НДСИсчисляетсяНалоговымАгентом", Новый ОписаниеТипов("Булево"));
		АвансыПоСтавкамНДС.ЗаполнитьЗначения(ПокупательНалоговыйАгентПоНДС, "НДСИсчисляетсяНалоговымАгентом");
		
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		
		НоменклатураСчетаФактуры = Документы.СчетФактураВыданный.РаспределитьСуммыАвансовПоНоменклатуреСчетовНаОплату(
			АвансыПоСтавкамНДС, НоменклатураСчетаНаОплату, ВалютаРегламентированногоУчета);
		
		Объект.Авансы.Очистить();
		
	Иначе
		
		// Добавить строки номенклатуры из счета на оплату на полную сумму счета
		
		Если ВалютаСчетаНаОплату <> ВалютаРегламентированногоУчета Тогда
			// Пересчитываем суммы счета в рубли
			КурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаСчетаНаОплату, ДатаАванса);
			КурсКратность.Кратность = ?(КурсКратность.Кратность = 0, 1, КурсКратность.Кратность);
			Для каждого СтрокаНоменклатуры Из НоменклатураСчетаНаОплату Цикл
				СтрокаНоменклатуры.Сумма = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаНоменклатуры.Сумма,
					ВалютаСчетаНаОплату, ВалютаРегламентированногоУчета,
					КурсКратность.Курс, 1,
					КурсКратность.Кратность, 1);
				СтрокаНоменклатуры.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					СтрокаНоменклатуры.Сумма, Истина, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаНоменклатуры.СтавкаНДС));
			КонецЦикла;
		КонецЕсли;
		
		НоменклатураСчетаФактуры = НоменклатураСчетаНаОплату;
		
	КонецЕсли;
	
	Для каждого СтрокаНоменклатуры Из НоменклатураСчетаФактуры Цикл
		НоваяСтрока = Объект.Авансы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНоменклатуры);
	КонецЦикла;
	
	ЗаполнитьДобавленныеКолонкиТаблицыАвансы();

КонецПроцедуры

&НаСервере
Функция ПолучитьКодВидаОперации()

	КодВидаОперации = "";
	
	Если Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
	 ИЛИ Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс Тогда
		
		КодВидаОперации = "02";
		
	ИначеЕсли Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента Тогда
		
		КодВидаОперации = ?(ВерсияКодовВидовОпераций < 3, "05", "02");
		
	ИначеЕсли Объект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку Тогда
		
		Если Объект.Авансы.Количество() > 1 Тогда
			КодВидаОперации = "28";
		Иначе
			КодВидаОперации = ?(ВерсияКодовВидовОпераций < 3, "05", "02");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КодВидаОперации;

КонецФункции

&НаКлиенте
Функция ПолучитьСтруктуруПараметровФормы()
	
	СтруктураПараметров = Новый Структура;
	
	ЗначенияЗаполнения = Новый Структура();
	
	ЗначенияЗаполнения.Вставить("ТипСчетаФактуры", "Выданный");
	ЗначенияЗаполнения.Вставить("ВидСчетаФактуры", Объект.ВидСчетаФактуры);
	ЗначенияЗаполнения.Вставить("Исправление", Объект.Исправление);
	ЗначенияЗаполнения.Вставить("СчетФактура", Объект.Ссылка);
	
	ЗначениеОтбора = Новый Структура();
	
	ЗначениеОтбора.Вставить("Организация", Объект.Организация);
	ЗначениеОтбора.Вставить("Контрагент", Объект.Контрагент);
	
	СтруктураПараметров.Вставить("Документ", Объект.ДокументОснование);
	СтруктураПараметров.Вставить("Отбор", ЗначениеОтбора);
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения); 
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// АвансыСодержание

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСодержание");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Авансы.Содержание", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Авансы.Номенклатура", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Предварительная оплата'"));

	// При реализации товаров, указанных в п.8 ст. 161 НК счет-фактура выставляется без учета сумм налога.
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСтавкаНДСДоКорректировки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСуммаНДСДоКорректировки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСтавкаНДСПослеКорректировки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСуммаНДСПослеКорректировки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПокупательНалоговыйАгентПоНДС", ВидСравненияКомпоновкиДанных.Равно, 
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Авансы сводный комиссионный по закупке
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыДокументОснование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыНомерПлатежноРасчетногоДокумента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыДатаПлатежноРасчетногоДокумента");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.СводныйКомиссионный", ВидСравненияКомпоновкиДанных.НеРавно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Авансы сводный комиссионный по продаже
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыДокументОснование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыНомерПлатежноРасчетногоДокумента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыДатаПлатежноРасчетногоДокумента");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ВидСчетаФактуры", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Корректировочный на аванс
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСуммаДоКорректировки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСтавкаНДСДоКорректировки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыСуммаНДСДоКорректировки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыНадписьНадписьДоИзменения");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыНадписьПослеИзменения");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыКорректируемыйСчетФактура");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ВидСчетаФактуры", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСчетФактураНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение( , Объект.ИсправляемыйСчетФактура);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДокументаОснования()
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование)
		И Объект.ВидСчетаФактуры <> ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку") Тогда
		
		ПриИзмененииДокументаОснованияНаСервере();
		
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонкиТаблицыАвансы();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснование()
	
	ЕстьОшибкиЗаполнения = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация", "Объект" , ЕстьОшибкиЗаполнения);
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда 
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Контрагент'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Контрагент", "Объект" , ЕстьОшибкиЗаполнения);
	КонецЕсли;
	
	Если ЕстьОшибкиЗаполнения Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьСтруктуруПараметровФормы();
	
	ОткрытьФорму("Обработка.ПодборОснованийСчетаФактуры.Форма.ФормаВыбораОснованияСчетаФактуры", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры 

&НаКлиенте
Процедура КодВидаОперацииПриИзменении(Элемент)
	
	ТекущийКод = Элемент.СписокВыбора.НайтиПоЗначению(Объект.КодВидаОперации);
	Если ТекущийКод <> Неопределено Тогда
		НадписьВидОперации = Сред(ТекущийКод.Представление, 5);
	Иначе
		НадписьВидОперации = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВыставленияПриИзменении(Элемент)
	
	Если Объект.ДатаВыставления < Объект.Дата Тогда 
		Объект.ДатаВыставления = Объект.Дата;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьОснование();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзСпискаЗавершение(ВыбранныйКод, ДополнительныеПараметры) Экспорт

	Если ВыбранныйКод <> Неопределено Тогда
		Модифицированность = Истина;
		Объект.КодВидаОперации = ВыбранныйКод.Значение;
		НадписьВидОперации = Сред(ВыбранныйКод.Представление, 5);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПолученнымСчетамФактурамНаСервере()

	Объект.Авансы.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданныйАвансы.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ОснованияДругихСчетовФактур
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО СчетФактураВыданныйАвансы.Ссылка = СчетФактураВыданный.Ссылка
	|ГДЕ
	|	СчетФактураВыданный.Дата >= &ДатаСчетаФактуры
	|	И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку)
	|	И СчетФактураВыданный.Контрагент = &Комитент
	|	И СчетФактураВыданный.ДоговорКонтрагента = &ДоговорКомитента
	|	И СчетФактураВыданныйАвансы.Ссылка <> &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Дата КАК Дата,
	|	СчетФактураПолученный.Ссылка КАК Ссылка,
	|	СчетФактураПолученный.Контрагент КАК Контрагент,
	|	СчетФактураПолученный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураПолученный.НомерВходящегоДокумента КАК НомерСчетаФактурыПродавца,
	|	ДанныеПервичныхДокументов.Номер КАК НомерПлатежноРасчетногоДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаПлатежноРасчетногоДокумента,
	|	СчетФактураПолученныйАвансы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(СчетФактураПолученныйАвансы.Сумма) КАК Сумма,
	|	СУММА(СчетФактураПолученныйАвансы.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	Документ.СчетФактураПолученный.Авансы КАК СчетФактураПолученныйАвансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ПО СчетФактураПолученныйАвансы.Ссылка = СчетФактураПолученный.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (СчетФактураПолученный.Организация = ДанныеПервичныхДокументов.Организация)
	|			И (СчетФактураПолученный.ДокументОснование = ДанныеПервичныхДокументов.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОснованияДругихСчетовФактур КАК ОснованияДругихСчетовФактур
	|		ПО (СчетФактураПолученный.ДокументОснование = ОснованияДругихСчетовФактур.ДокументОснование)
	|ГДЕ
	|	СчетФактураПолученный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАвансКомитента)
	|	И СчетФактураПолученный.Дата >= &ДатаСчетаФактуры
	|	И СчетФактураПолученный.ДатаВходящегоДокумента = &ДатаСчетаФактуры
	|	И СчетФактураПолученный.Комитент = &Комитент
	|	И СчетФактураПолученный.ДоговорКомитента = &ДоговорКомитента
	|	И ОснованияДругихСчетовФактур.ДокументОснование ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураПолученный.Дата,
	|	СчетФактураПолученный.Ссылка,
	|	СчетФактураПолученный.Контрагент,
	|	СчетФактураПолученный.ДокументОснование,
	|	СчетФактураПолученный.НомерВходящегоДокумента,
	|	ДанныеПервичныхДокументов.Номер,
	|	ДанныеПервичныхДокументов.Дата,
	|	СчетФактураПолученныйАвансы.СтавкаНДС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",           Объект.Ссылка);
	Запрос.УстановитьПараметр("ДатаСчетаФактуры", НачалоДня(Объект.Дата));
	Запрос.УстановитьПараметр("Комитент",         Объект.Контрагент);
	Запрос.УстановитьПараметр("ДоговорКомитента", Объект.ДоговорКонтрагента);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Авансы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

	СводноПоПолученным = Объект.Авансы.Количество() > 1;
	Объект.КодВидаОперации = ПолучитьКодВидаОперации();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблицыАвансы()

	Для Каждого СтрокаТаблицы Из Объект.Авансы Цикл
		ЗаполнитьЗаголовкиСтрок(СтрокаТаблицы);
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьЗаголовкиСтрок(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.НадписьДоИзменения     = НСтр("ru = 'до изменения:'");
	СтрокаТабличнойЧасти.НадписьПослеИзменения  = НСтр("ru = 'после изменения:'");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Форма.ИтогиВсего    = Форма.Объект.Авансы.Итог("Сумма") - Форма.Объект.Авансы.Итог("СуммаДоКорректировки");
	Форма.ИтогиВсегоНДС = Форма.Объект.Авансы.Итог("СуммаНДС") - Форма.Объект.Авансы.Итог("СуммаНДСДоКорректировки");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыДоплатыКАвансу(ДанныеСтроки)
	
	ПараметрыДоплаты = Новый Структура(
		"СуммаДоКорректировки,СтавкаНДСДоКорректировки,СуммаНДСДоКорректировки,
		|Сумма,СтавкаНДС,СуммаНДС");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КорректируемыйСчетФактура", ДанныеСтроки.КорректируемыйСчетФактура);
	Запрос.УстановитьПараметр("Сумма", ДанныеСтроки.Сумма);
	Запрос.УстановитьПараметр("СтавкаНДС", ДанныеСтроки.СтавкаНДС);
	Запрос.УстановитьПараметр("СуммаНДС", ДанныеСтроки.СуммаНДС);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(СчетФактураВыданныйАвансы.Сумма) КАК СуммаДоКорректировки,
	|	МАКСИМУМ(СчетФактураВыданныйАвансы.СтавкаНДС) КАК СтавкаНДСДоКорректировки,
	|	СУММА(СчетФактураВыданныйАвансы.СуммаНДС) КАК СуммаНДСДоКорректировки,
	|	СчетФактураВыданныйАвансы.Ссылка КАК КорректируемыйСчетФактура,
	|	&Сумма + СУММА(СчетФактураВыданныйАвансы.Сумма) КАК Сумма,
	|	&СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	|ГДЕ
	|	СчетФактураВыданныйАвансы.Ссылка = &КорректируемыйСчетФактура
	|	И (СчетФактураВыданныйАвансы.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ИЛИ СчетФактураВыданныйАвансы.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118))
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйАвансы.Ссылка";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ПараметрыДоплаты;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ПараметрыДоплаты, Выборка);
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(
		ПараметрыДоплаты, Истина);
	
	Возврат ПараметрыДоплаты;
	
КонецФункции

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#КонецОбласти
