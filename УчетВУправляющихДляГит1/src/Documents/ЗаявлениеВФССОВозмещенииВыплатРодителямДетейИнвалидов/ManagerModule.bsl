#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Печать заявления о выплате пособия.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ЗаявлениеВФССДополнительныеДниОтпуска";
	КомандаПечати.Представление = НСтр("ru = 'Заявление о возмещении расходов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// См. УправлениеПечатьюПереопределяемый.ПриПечати.
Процедура Печать(МассивСсылок, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеВФССДополнительныеДниОтпуска") Тогда
		ТабличныйДокумент = ТабличныйДокументЗаявлениеВФССДополнительныеДниОтпуска(МассивСсылок, ОбъектыПечати);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЗаявлениеВФССДополнительныеДниОтпуска",
			НСтр("ru = 'Заявление о возмещении расходов'"),
			ТабличныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#Область ФиксацияВторичныхДанныхВДокументах

Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	ФиксируемыеРеквизиты = ПрямыеВыплатыПособийСоциальногоСтрахования.ОписаниеФиксацииРеквизитовЗаявленияВФССОВозмещенииВыплатРодителямДетейИнвалидов();
	ФиксируемыеТЧ = Новый Структура("Оплаты", СтрРазделить("ДокументОснование", ",", Ложь));
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксацииВторичныхДанных(ФиксируемыеРеквизиты, , ФиксируемыеТЧ);
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТабличныйДокументЗаявлениеВФССДополнительныеДниОтпуска(МассивСсылок, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ЗаявлениеВФССДополнительныеДниОтпуска";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху  = 0;
	ТабличныйДокумент.ПолеСлева   = 0;
	ТабличныйДокумент.ПолеСнизу   = 0;
	ТабличныйДокумент.ПолеСправа  = 0;
	
	ДатаФорм2017 = ПрямыеВыплатыПособийСоциальногоСтрахования.ДатаВступленияВСилуФорм2017Года();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.УстановитьПараметр("ДатаФорм2017", ДатаФорм2017);
	
	СоздатьВТКадровыхДанных(Запрос);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Номер,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата < &ДатаФорм2017
	|			ТОГДА ""ЗаявлениеВФССДополнительныеДниОтпуска_2012""
	|		ИНАЧЕ ""ЗаявлениеВФССДополнительныеДниОтпуска_2017""
	|	КОНЕЦ КАК ИмяМакета,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Проведен,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Организация,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НаименованиеТерриториальногоОрганаФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.РегистрационныйНомерФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДополнительныйКодФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.КодПодчиненностиФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Руководитель,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДолжностьРуководителя,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.АдресОрганизации,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НаименованиеБанка,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НомерЛицевогоСчета,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.КБК,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НомерСчета,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.БИКБанка,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.СтатусДокумента,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ТелефонСоставителя,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.АдресЭлектроннойПочтыОрганизации,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ЗаявлениеСоставил,
	|	ТЧОплаты.Сотрудник,
	|	ТЧОплаты.Статус,
	|	ТЧОплаты.СреднедневнойЗаработок,
	|	ТЧОплаты.ДокументОснование,
	|	ТЧОплаты.КоличествоСтраниц КАК КоличествоСтраниц,
	|	ТЧОплаты.СуммаПособия,
	|	ТЧОплаты.СуммаВзносов,
	|	КадровыеДанныеРуководителей.ФИОПолные КАК ФИОРуководителя,
	|	ВТКадровыеДанныеСотрудников.Фамилия КАК Фамилия,
	|	ВТКадровыеДанныеСотрудников.Имя КАК Имя,
	|	ВТКадровыеДанныеСотрудников.Отчество КАК Отчество,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА Организации.НаименованиеСокращенное
	|		ИНАЧЕ ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ОрганизацияНаименование,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ИНН,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.КПП
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеРуководителей
	|		ПО ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Руководитель = КадровыеДанныеРуководителей.ФизическоеЛицо
	|			И ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата = КадровыеДанныеРуководителей.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК ТЧОплаты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|			ПО ТЧОплаты.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|				И ТЧОплаты.Ссылка.Дата = ВТКадровыеДанныеСотрудников.Период
	|		ПО ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка = ТЧОплаты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Организация = Организации.Ссылка
	|ГДЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ИмяМакета") Цикл
		
		Макет = Неопределено;
		ВыведеноСтрок = 0;
		ИтогоПособия = 0;
		ИтогоСреднедневнойЗаработок = 0;
		ИтогоВзносов = 0;
		ВсегоСтраниц = 0;
		
		Если Выборка.ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2012" Тогда
			КоличествоСтрокВМакете = 4;
		Иначе
			КоличествоСтрокВМакете = 5;
		КонецЕсли;
		
		Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
			Пока Выборка.Следующий() Цикл
				ВыведеноСтрок = (ВыведеноСтрок + 1) % КоличествоСтрокВМакете;
				НомерСтроки = ?(ВыведеноСтрок = 0, КоличествоСтрокВМакете, ВыведеноСтрок);
				
				Если ВыведеноСтрок = 1 Тогда
					Если Макет <> Неопределено Тогда
						Если Выборка.ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2012" Тогда
							ВывестиИтогиЗаявленияОВозмещении_2012(Макет, ИтогоСреднедневнойЗаработок, ИтогоПособия, ВсегоСтраниц);
						Иначе
							ВывестиИтогиЗаявленияОВозмещении_2017(Макет, ИтогоСреднедневнойЗаработок, ИтогоПособия, ИтогоВзносов, ВсегоСтраниц);
						КонецЕсли;
						Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
							ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						КонецЕсли;
						ТабличныйДокумент.Вывести(Макет);
					КонецЕсли;
					ИтогоПособия = 0;
					ВсегоСтраниц = 0;
					
					Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов." + Выборка.ИмяМакета);
					Если Выборка.ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2012" Тогда
						ПрямыеВыплатыПособийСоциальногоСтрахования.ВывестиШапкуИПодвалЗаявленияОВозмещении_2012(Макет, Выборка);
					Иначе
						ПрямыеВыплатыПособийСоциальногоСтрахования.ВывестиШапкуИПодвалЗаявленияОВозмещении_2017(Макет, Выборка);
					КонецЕсли;
				КонецЕсли;
				
				Если Выборка.ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2012" Тогда
					ВывестиСтрокуЗаявленияОВозмещении_2012(Макет, Выборка, НомерСтроки);
				Иначе
					ВывестиСтрокуЗаявленияОВозмещении_2017(Макет, Выборка, НомерСтроки);
				КонецЕсли;
				
				ИтогоСреднедневнойЗаработок = ИтогоСреднедневнойЗаработок + Выборка.СреднедневнойЗаработок;
				ИтогоПособия = ИтогоПособия + Выборка.СуммаПособия;
				ИтогоВзносов = ИтогоВзносов + Выборка.СуммаВзносов;
				ВсегоСтраниц = ВсегоСтраниц + Выборка.КоличествоСтраниц;
			КонецЦикла;
		КонецЦикла;
		
		Если Выборка.ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2012" Тогда
			ВывестиИтогиЗаявленияОВозмещении_2012(Макет, ИтогоСреднедневнойЗаработок, ИтогоПособия, ВсегоСтраниц);
		Иначе
			ВывестиИтогиЗаявленияОВозмещении_2017(Макет, ИтогоСреднедневнойЗаработок, ИтогоПособия, ИтогоВзносов, ВсегоСтраниц);
		КонецЕсли;
		
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ТабличныйДокумент.Вывести(Макет);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ВывестиСтрокуЗаявленияОВозмещении_2012(Макет, Выборка, НомерСтроки)
	
	ПрефиксСтроки = "ФИО_" + Формат(НомерСтроки,"ЧЦ=2; ЧВН=") + "_";
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(Выборка.Фамилия),  Макет, ПрефиксСтроки, 24);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(Выборка.Имя),      Макет, ПрефиксСтроки, 24, 25);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(Выборка.Отчество), Макет, ПрефиксСтроки, 24, 49);
	
	ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(Выборка.СреднедневнойЗаработок, Макет, "РазмерПособия" + НомерСтроки + "_", 8);
	
	Макет.Области["Статус" + НомерСтроки].Текст = Выборка.Статус;
	
КонецПроцедуры

Процедура ВывестиСтрокуЗаявленияОВозмещении_2017(Макет, Выборка, НомерСтроки)
	
	Макет.Параметры["Фамилия" + НомерСтроки]  = Выборка.Фамилия;
	Макет.Параметры["Имя" + НомерСтроки]      = Выборка.Имя;
	Макет.Параметры["Отчество" + НомерСтроки] = Выборка.Отчество;
	
	Макет.Параметры["РазмерПособия" + НомерСтроки] = Выборка.СреднедневнойЗаработок;
	
	Макет.Области["Статус" + НомерСтроки].Текст = Выборка.Статус;
	
КонецПроцедуры

Процедура ВывестиИтогиЗаявленияОВозмещении_2012(Макет, ИтогоСреднедневнойЗаработок, ИтогоПособия, ВсегоСтраниц)
	
	ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоСреднедневнойЗаработок, Макет, "ИтогоРазмерПособия_", 9);
	ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия, Макет, "ИтогоРазмерПособия2_", 9);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(Строка(ВсегоСтраниц), Макет, "КоличествоСтраниц_", 2);
	
КонецПроцедуры

Процедура ВывестиИтогиЗаявленияОВозмещении_2017(Макет, ИтогоСреднедневнойЗаработок, ИтогоПособия, ИтогоВзносов, ВсегоСтраниц)
	
	Макет.Параметры.ИтогоРазмерПособия = ИтогоСреднедневнойЗаработок;
	ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия + ИтогоВзносов, Макет, "ВсегоРасходов_", 11);
	ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоВзносов, Макет, "Взносов_", 11);
	ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия, Макет, "ИтогоРазмерПособия2_", 11);
	Макет.Параметры.Листов = Строка(ВсегоСтраниц);
	
КонецПроцедуры

Процедура СоздатьВТКадровыхДанных(Запрос)
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Оплаты.Сотрудник,
	|	Оплаты.Ссылка.Дата КАК Период
	|ПОМЕСТИТЬ ВТСотрудникиОбщийСписок
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК Оплаты
	|ГДЕ
	|	Оплаты.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Руководитель КАК ФизическоеЛицо,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата КАК Период
	|ПОМЕСТИТЬ ВТФизическиеЛицаОбщийСписок
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов
	|ГДЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка В(&МассивСсылок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ЗаявлениеСоставил,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов
	|ГДЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТСотрудникиОбщийСписок.Сотрудник,
	|	ВТСотрудникиОбщийСписок.Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	ВТСотрудникиОбщийСписок КАК ВТСотрудникиОбщийСписок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТФизическиеЛицаОбщийСписок.ФизическоеЛицо,
	|	ВТФизическиеЛицаОбщийСписок.Период
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТФизическиеЛицаОбщийСписок КАК ВТФизическиеЛицаОбщийСписок";
	
	Запрос.Выполнить();
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(Описатель, Истина, "ФИОПолные,Фамилия,Имя,Отчество");
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛица");
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(Описатель, Истина, "ФИОПолные,ФамилияИО");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
