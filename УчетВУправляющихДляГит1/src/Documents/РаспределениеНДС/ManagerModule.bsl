#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 22, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ

Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(
		СтруктураПараметров.Организация, СтруктураПараметров.Дата);
	
	ОшибкаЗаполнения = Ложь;
	СтрокаСообщения = "";
	ДанныеДляЗаполнения = Новый Структура();
	
	ВыручкаПоВидамДеятельности = ВыручкаПоВидамДеятельности(СтруктураПараметров);

	Если ВыручкаПоВидамДеятельности.ВыручкаЕНВД = 0 И ВыручкаПоВидамДеятельности.ВыручкаБезНДС = 0 
		И ВыручкаПоВидамДеятельности.ВыручкаНДС0 = 0 И ВыручкаПоВидамДеятельности.ВыручкаНДС = 0 Тогда
		ОшибкаЗаполнения = Истина;
		СтрокаСообщения = СтрокаСообщения + Символы.ПС + Нстр("ru = ' - отсутствует выручка от реализации'");
	КонецЕсли;
	
	ДанныеДляЗаполнения.Вставить("РеквизитыДокумента", ВыручкаПоВидамДеятельности);
	
	Если РаздельныйУчетНДСНаСчете19 Тогда

		ЗаполнитьТабличныеЧастиПоНДСКРаспределению(СтруктураПараметров, ДанныеДляЗаполнения);
		
	Иначе
		
		ЗаполнитьТабличныеЧастиПоКосвеннымРасходам(СтруктураПараметров, ДанныеДляЗаполнения);
		
		Если ДанныеДляЗаполнения.СоставКосвенныхРасходов.Количество() = 0 Тогда
			ОшибкаЗаполнения = Истина;
			СтрокаСообщения = СтрокаСообщения + Символы.ПС + Нстр("ru = ' - не обнаружены расходы к распределению'");
		КонецЕсли;
	
	КонецЕсли;
	
	Если ОшибкаЗаполнения Тогда
		СтрокаСообщения = Нстр("ru = 'Документ не заполнен! '") + СтрокаСообщения;
		ДанныеДляЗаполнения.Вставить("ОшибкаЗаполнения",СтрокаСообщения);
	Иначе
		Если РаздельныйУчетНДСНаСчете19 Тогда
			РаспределитьПоВыручкеРаспределяемыйНДС(ВыручкаПоВидамДеятельности, ДанныеДляЗаполнения.РаспределяемыйНДС);
		Иначе
			РаспределитьПоВыручке(ВыручкаПоВидамДеятельности, ДанныеДляЗаполнения.СоставКосвенныхРасходов);
		КонецЕсли;
	КонецЕсли;

	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

// Определяет выручку по видам деятельности с позиции налогобложения НДС.
// Параметры:
//   СтруктураПараметров - Структура - структура с ключами:
//     * Организация - СправочникСсылка.Организации - организация (либо головная организация),
//               для которой производится расчет выручки.
//     * Дата          - Дата - дата документа, служит границей, по которую рассчитывается выручка.
//     * НачалоПериода - Дата - начало квартала, либо начало месяца, дата с которой рассчитывается выручка.
// Возвращаемое значение:
//  Структура -структура с ключами:
//     * ВыручкаЕНВД - Число (15, 2) - выручка по деятельности, освобождаемой от налогообложения НДС
//               в связи с применением единого налога на вмененный доход.
//     * ВыручкаБезНДС - Число (15, 2) - выручка по деятельности, освобождаемой от налогообложения НДС
//               по ст. 145, 149, выручка по деятельности не являющейся объектом налогообложения НДС и пр.
//     * ВыручкаНДС0 - Число (15, 2) - выручка по деятельности, облагаемой по ставке 0%,
//               начиная с 1.07.2016 за исключением экспорта несырьевых товаров.
//     * ВыручкаНДС - Число (15, 2) - выручка по деятельности, облагаемой по ставкам 10 и 18%.
//     * ВыручкаНДС0ТоварыНесырьевые - Число (15, 2) - выручка по деятельности, связанной с экспортом несырьевых товаров,
//               заполняется начиная с 1.07.2016.
Функция ВыручкаПоВидамДеятельности(СтруктураПараметров) Экспорт

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(
		СтруктураПараметров.Организация, СтруктураПараметров.Дата);
		
	ВыручкаПоВидамДеятельности = Новый Структура();
	ВыручкаПоВидамДеятельности.Вставить("ВыручкаЕНВД",                   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВыручкаПоВидамДеятельности.Вставить("ВыручкаБезНДС",                 ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВыручкаПоВидамДеятельности.Вставить("ВыручкаНДС0",                   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВыручкаПоВидамДеятельности.Вставить("ВыручкаНДС",                    ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВыручкаПоВидамДеятельности.Вставить("ВыручкаНДС0ТоварыНесырьевые",   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВыручкаПоВидамДеятельности.Вставить("ВыручкаУслугиНеНаТерриторииРФ", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", 
		БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(СтруктураПараметров.Организация));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	(Организации.ГоловнаяОрганизация = &Организация
	|			ИЛИ Организации.Ссылка = &Организация
	|			ИЛИ ВЫБОР
	|				КОГДА &ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА Организации.Ссылка = &ГоловнаяОрганизация
	|							ИЛИ Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)";
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Запрос.УстановитьПараметр("Организация", МассивОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(СтруктураПараметров.НачалоПериода));
	Если РаздельныйУчетНДСНаСчете19 Тогда
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(СтруктураПараметров.Дата));
	Иначе
		Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецКвартала(СтруктураПараметров.НачалоПериода)));
	КонецЕсли;
	
	Если СтруктураПараметров.НачалоПериода < '20120101' Тогда
	
		ВидыНачисленияРеализация = Новый СписокЗначений;
		ВидыНачисленияРеализация.Добавить(Перечисления.НДСВидНачисления.РеализацияСНДС);
		ВидыНачисленияРеализация.Добавить(Перечисления.НДСВидНачисления.РеализацияБезНДС);
		ВидыНачисленияРеализация.Добавить(Перечисления.НДСВидНачисления.Реализация0);
		ВидыНачисленияРеализация.Добавить(Перечисления.НДСВидНачисления.РеализацияЕНВД);
		
		Запрос.УстановитьПараметр("ВидыНачисленияРеализация", ВидыНачисленияРеализация);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСНачисленныйОбороты.ВидНачисления,
		|	НДСНачисленныйОбороты.СуммаБезНДСПриход КАК СуммаБезНДС
		|ИЗ
		|	РегистрНакопления.НДСНачисленный.Обороты(
		|			НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ),
		|			КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
		|			,
		|			Организация В (&Организация)
		|				И (СчетФактура.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ))
		|				И ВидНачисления В (&ВидыНачисленияРеализация)) КАК НДСНачисленныйОбороты";
		
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		
		Для каждого СтрокаРезультата Из ТаблицаРезультата Цикл
		
			Если СтрокаРезультата.ВидНачисления = Перечисления.НДСВидНачисления.РеализацияСНДС Тогда
				ВыручкаПоВидамДеятельности.ВыручкаНДС = ВыручкаПоВидамДеятельности.ВыручкаНДС + СтрокаРезультата.СуммаБезНДС;
			ИначеЕсли СтрокаРезультата.ВидНачисления = Перечисления.НДСВидНачисления.РеализацияБезНДС Тогда
				ВыручкаПоВидамДеятельности.ВыручкаБезНДС = ВыручкаПоВидамДеятельности.ВыручкаБезНДС + СтрокаРезультата.СуммаБезНДС;
			ИначеЕсли СтрокаРезультата.ВидНачисления = Перечисления.НДСВидНачисления.РеализацияЕНВД Тогда
				ВыручкаПоВидамДеятельности.ВыручкаЕНВД = ВыручкаПоВидамДеятельности.ВыручкаЕНВД + СтрокаРезультата.СуммаБезНДС;
			ИначеЕсли СтрокаРезультата.ВидНачисления = Перечисления.НДСВидНачисления.Реализация0 Тогда
				ВыручкаПоВидамДеятельности.ВыручкаНДС0 = ВыручкаПоВидамДеятельности.ВыручкаНДС0 + СтрокаРезультата.СуммаБезНДС;
			КонецЕсли;
		
		КонецЦикла;
		
	Иначе
		
		СобытияРеализация = Новый Массив;
		СобытияРеализация.Добавить(Перечисления.СобытияПоНДСПродажи.Реализация);
		СобытияРеализация.Добавить(Перечисления.СобытияПоНДСПродажи.НалогИсчисляетПокупатель);
		
		Запрос.УстановитьПараметр("Событие",              СобытияРеализация);
		Запрос.УстановитьПараметр("СтавкаБезНДС",         Перечисления.СтавкиНДС.БезНДС);
		Запрос.УстановитьПараметр("Ставка0",              Перечисления.СтавкиНДС.НДС0);
		Запрос.УстановитьПараметр("СчетаВыручкиЕНВД",     БухгалтерскийУчетПовтИсп.СчетаВыручкиЕНВД());
		Запрос.УстановитьПараметр("СостояниеРеализация0", Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
		Запрос.УстановитьПараметр("ТоварыНесырьевые",     Перечисления.ВидыЦенностей.ТоварыНесырьевые);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК ВыручкаНДС,
		|	0 КАК ВыручкаУслугиНеНаТерриторииРФ,
		|	0 КАК ВыручкаБезНДС,
		|	0 КАК ВыручкаЕНВД,
		|	0 КАК ВыручкаНДС0,
		|	0 КАК ВыручкаНДС0ТоварыНесырьевые
		|ИЗ
		|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Организация В (&Организация)
		|				И СтавкаНДС <> &Ставка0
		|				И СтавкаНДС <> &СтавкаБезНДС
		|				И Событие В (&Событие)
		|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)) КАК НДСЗаписиКнигиПродажОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК ВыручкаНДС,
		|	0,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Организация В (&Организация)
		|				И СтавкаНДС = &СтавкаБезНДС
		|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.УслугиНеНаТерриторииРФ)) КАК НДСЗаписиКнигиПродажОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	0,
		|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Организация В (&Организация)
		|				И СтавкаНДС = &СтавкаБезНДС 
		|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.УслугиНеНаТерриторииРФ)
		|				И Событие В (&Событие)) КАК НДСЗаписиКнигиПродажОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	0,
		|	0,
		|	ХозрасчетныйОбороты.СуммаОборотКт,
		|	0,
		|	0
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаВыручкиЕНВД), , Организация В (&Организация), , ) КАК ХозрасчетныйОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА &НачалоПериода < ДАТАВРЕМЯ(2016, 7, 1)
		|			ТОГДА НДСРеализация0Обороты.СуммаБезНДСПриход
		|		ИНАЧЕ ВЫБОР
		|				КОГДА НДСРеализация0Обороты.ВидЦенности = &ТоварыНесырьевые
		|					ТОГДА 0
		|				ИНАЧЕ НДСРеализация0Обороты.СуммаБезНДСПриход
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &НачалоПериода < ДАТАВРЕМЯ(2016, 7, 1)
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА НДСРеализация0Обороты.ВидЦенности = &ТоварыНесырьевые
		|					ТОГДА НДСРеализация0Обороты.СуммаБезНДСПриход
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.НДСРеализация0.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Организация В (&Организация)
		|				И Состояние = &СостояниеРеализация0) КАК НДСРеализация0Обороты";
		
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаРезультата.Количество() <> 0 Тогда
			
			ВыручкаПоВидамДеятельности.ВыручкаНДС0ТоварыНесырьевые   = ТаблицаРезультата.Итог("ВыручкаНДС0ТоварыНесырьевые");
			ВыручкаПоВидамДеятельности.ВыручкаУслугиНеНаТерриторииРФ = ТаблицаРезультата.Итог("ВыручкаУслугиНеНаТерриторииРФ");
			ВыручкаПоВидамДеятельности.ВыручкаНДС = ТаблицаРезультата.Итог("ВыручкаНДС") 
				+ ТаблицаРезультата.Итог("ВыручкаНДС0ТоварыНесырьевые")
				+ ТаблицаРезультата.Итог("ВыручкаУслугиНеНаТерриторииРФ");
			ВыручкаПоВидамДеятельности.ВыручкаБезНДС = ТаблицаРезультата.Итог("ВыручкаБезНДС");
			ВыручкаПоВидамДеятельности.ВыручкаЕНВД   = ТаблицаРезультата.Итог("ВыручкаЕНВД");
			ВыручкаПоВидамДеятельности.ВыручкаНДС0   = ТаблицаРезультата.Итог("ВыручкаНДС0");
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ВыручкаПоВидамДеятельности;
	
КонецФункции

Процедура ПодготовитьДанныеДляЗаполненияТабличныхЧастей(СтруктураПараметров, АдресХранилища) Экспорт
	
	ДанныеДляЗаполнения = Новый Структура;
	
	ЗаполнитьТабличныеЧастиПоКосвеннымРасходам(СтруктураПараметров, ДанныеДляЗаполнения);
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиПоКосвеннымРасходам(СтруктураПараметров, ДанныеДляЗаполнения) 
	
	ПустаяСсылкаДокумента = Документы.РаспределениеНДС.ПустаяСсылка();
	СоставКосвенныхРасходов = ПустаяСсылкаДокумента.СоставКосвенныхРасходов.ВыгрузитьКолонки();
	СчетаУчетаРасходов      = ПустаяСсылкаДокумента.СчетаУчетаРасходов.ВыгрузитьКолонки();
	
	ДанныеДляЗаполнения.Вставить("СоставКосвенныхРасходов", СоставКосвенныхРасходов);
	ДанныеДляЗаполнения.Вставить("СчетаУчетаРасходов",      СчетаУчетаРасходов);
	
	Результат = ПолучитьВыборкуИзРегистраНДСКосвенныеРасходы(СтруктураПараметров);
		
	КонтрагентПоСчетуФактуре = ПолучитьСписокКонтрагентовПоСчетамФактурам(СтруктураПараметров, Результат.Выгрузить());
	КонтрагентПоСчетуФактуре.Индексы.Добавить("СчетФактура");
	
	Ключ = 1;
	ВыборкаПоСчетуФактуре = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока ВыборкаПоСчетуФактуре.Следующий() Цикл
		
		ВыборкаПоВидуЦенности = ВыборкаПоСчетуФактуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Контрагент = КонтрагентПоСчетуФактуре.Найти(ВыборкаПоСчетуФактуре.СчетФактура,"СчетФактура");
		
		Пока ВыборкаПоВидуЦенности.Следующий() Цикл
			
				
			ВыборкаПоСтавкеНДС = ВыборкаПоВидуЦенности.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			
			Пока ВыборкаПоСтавкеНДС.Следующий() Цикл
				
				ВыборкаПоСчетуУчетаНДС = ВыборкаПоСтавкеНДС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
				
				Пока ВыборкаПоСчетуУчетаНДС.Следующий() Цикл
					
					ВыборкаПоВключениюНДСВСтоимость = ВыборкаПоСчетуУчетаНДС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
					
					Пока ВыборкаПоВключениюНДСВСтоимость.Следующий() Цикл
				
						ВыборкаПоБазисуРаспределения = ВыборкаПоВключениюНДСВСтоимость.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
						
						РассчитатьСуммуКОтражению = (ВыборкаПоБазисуРаспределения.Количество()>1);
						
						Если РассчитатьСуммуКОтражению Тогда
							НомерПервойСтрокиКосвенныхРасходовКзаполнению = СоставКосвенныхРасходов.Количество();
							БазисРаспределенияСНДС = Новый Массив();
							БазисРаспределенияНДС = Новый Массив();
						КонецЕсли; 
						
						Пока ВыборкаПоБазисуРаспределения.Следующий() Цикл
							
							Если РассчитатьСуммуКОтражению Тогда
								БазисРаспределенияСНДС.Добавить(0);
								БазисРаспределенияНДС.Добавить(0);
							КонецЕсли;
							
							ПолучитьСуммуИзВыборки = Истина;
							
							НоваяСтрока = СоставКосвенныхРасходов.Добавить();
							
							НоваяСтрока.ВидЦенности 				   = ВыборкаПоБазисуРаспределения.ВидЦенности;
							НоваяСтрока.Поставщик  	 				   = ?(Контрагент <> Неопределено, Контрагент.Поставщик, 
																		Справочники.Контрагенты.ПустаяСсылка());	
							НоваяСтрока.СчетФактура 				   = ВыборкаПоСчетуФактуре.СчетФактура;
							НоваяСтрока.СтавкаНДС   				   = ВыборкаПоБазисуРаспределения.СтавкаНДС;
							НоваяСтрока.СчетУчетаНДС				   = ВыборкаПоБазисуРаспределения.СчетУчетаНДС;
							НоваяСтрока.НДСВключенВСтоимость 		   = ВыборкаПоБазисуРаспределения.НДСВключенВСтоимость;							
							НоваяСтрока.БазисРаспределенияВключаетЕНВД = ВыборкаПоБазисуРаспределения.БазисРаспределенияВключаетЕНВД;
							НоваяСтрока.КлючСтроки  				   = Ключ;
							
							Выборка = ВыборкаПоБазисуРаспределения.Выбрать();
							
							Пока Выборка.Следующий() Цикл
								
								Если РассчитатьСуммуКОтражению Тогда
									БазисРаспределенияСНДС[БазисРаспределенияСНДС.Количество() - 1] = 
															БазисРаспределенияСНДС[БазисРаспределенияСНДС.Количество() - 1] + 
															Выборка.СуммаБезНДСПроводка + Выборка.НДСПроводка;
									БазисРаспределенияНДС[БазисРаспределенияНДС.Количество() - 1] = 
															БазисРаспределенияНДС[БазисРаспределенияНДС.Количество() - 1] +
															Выборка.НДСПроводка;
									Если ПолучитьСуммуИзВыборки Тогда
										СуммаСНДСкРаспределению	= Выборка.СуммаБезНДС + Выборка.НДС;
										НДСКРаспределению		= Выборка.НДС;
									КонецЕсли;
								ИначеЕсли ПолучитьСуммуИзВыборки Тогда
									НоваяСтрока.СуммаВсего  = Выборка.СуммаБезНДС;
									НоваяСтрока.НДСВсего    = Выборка.НДС;
								КонецЕсли;

								НоваяСтрокаСчетов 					  = СчетаУчетаРасходов.Добавить();
								НоваяСтрокаСчетов.КлючСтроки  		  = Ключ;
								НоваяСтрокаСчетов.СчетЗатрат  		  = Выборка.СчетЗатрат;
								НоваяСтрокаСчетов.ПодразделениеЗатрат = Выборка.ПодразделениеЗатрат;
								НоваяСтрокаСчетов.Субконто1   		  = Выборка.Субконто1;
								НоваяСтрокаСчетов.Субконто2   		  = Выборка.Субконто2;
								НоваяСтрокаСчетов.Субконто3   		  = Выборка.Субконто3;
								НоваяСтрокаСчетов.СуммаБезНДС 		  = Выборка.СуммаБезНДСПроводка;
								НоваяСтрокаСчетов.НДС         		  = Выборка.НДСПроводка;
								
								ПолучитьСуммуИзВыборки = Ложь;
								
							КонецЦикла;
							
							Ключ = Ключ + 1;
						
						КонецЦикла; // Пока ВыборкаПоБазисуРаспределения
						
						Если РассчитатьСуммуКОтражению Тогда
							Если НЕ СуммаСНДСкРаспределению = 0 Тогда
								СуммаСНДСРезультатРаспределения = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(СуммаСНДСкРаспределению,
																												БазисРаспределенияСНДС);
							КонецЕсли; 
							Если НЕ НДСКРаспределению = 0 Тогда
								НДСРезультатРаспределения = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(НДСкРаспределению,БазисРаспределенияНДС);
							КонецЕсли; 
							
							Для ИндексСтрокиТекущейВыборки = 0 По БазисРаспределенияСНДС.Количество() - 1 Цикл
								СоставКосвенныхРасходов[НомерПервойСтрокиКосвенныхРасходовКзаполнению + ИндексСтрокиТекущейВыборки].НДСВсего =
									?(НДСкРаспределению = 0, 0, НДСРезультатРаспределения[ИндексСтрокиТекущейВыборки]);
								СоставКосвенныхРасходов[НомерПервойСтрокиКосвенныхРасходовКзаполнению + ИндексСтрокиТекущейВыборки].СуммаВсего =
									?(СуммаСНДСкРаспределению = 0, 0, СуммаСНДСРезультатРаспределения[ИндексСтрокиТекущейВыборки])
									- СоставКосвенныхРасходов[НомерПервойСтрокиКосвенныхРасходовКзаполнению+ИндексСтрокиТекущейВыборки].НДСВсего;
							КонецЦикла; 
						КонецЕсли;
					КонецЦикла; // Пока ВыборкаПоВключениюНДСВСтоимость
					
				КонецЦикла; // Пока ВыборкаПоСчетуУчетаНДС
				
			КонецЦикла; // Пока ВыборкаПоСтавкеНДС
			
		КонецЦикла; // Пока ВыборкаПоВидуЦенности
		
	КонецЦикла; // Пока ВыборкаПоСчетуФактуре
		
КонецПроцедуры

// Возвращает выборку по регистру НДС по косвенным расходам за период с итогами по измерениям 
//
Функция ПолучитьВыборкуИзРегистраНДСКосвенныеРасходы(СтруктураПараметров)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",	СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоДня(СтруктураПараметров.НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",	КонецДня(СтруктураПараметров.Дата));
	
	РаспределяемыеЗатраты = Новый СписокЗначений;
	РаспределяемыеЗатраты.Добавить(
		Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты);
	РаспределяемыеЗатраты.Добавить(
		Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения);
	
	Запрос.УстановитьПараметр("РаспределяемыеЗатраты", РаспределяемыеЗатраты);
	Запрос.УстановитьПараметр("ВидДеятельностиОпределяетсяПоДоходамПоПолномуБазису", 
		Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСКосвенныеРасходы.Организация КАК Организация,
	|	НДСКосвенныеРасходы.СчетФактура КАК СчетФактура,
	|	НДСКосвенныеРасходы.ВидЦенности КАК ВидЦенности,
	|	НДСКосвенныеРасходы.СтавкаНДС КАК СтавкаНДС,
	|	НДСКосвенныеРасходы.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСКосвенныеРасходы.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	СУММА(НДСКосвенныеРасходы.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(НДСКосвенныеРасходы.НДС) КАК НДС,
	|	НДСКосвенныеРасходы.СтатьяЗатрат КАК СтатьяЗатрат,
	|	НДСКосвенныеРасходы.СчетЗатрат КАК СчетЗатрат,
	|	НДСКосвенныеРасходы.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	НДСКосвенныеРасходы.Субконто1 КАК Субконто1,
	|	НДСКосвенныеРасходы.Субконто2 КАК Субконто2,
	|	НДСКосвенныеРасходы.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ ТаблицаНДСКосвенныеРасходы
	|ИЗ
	|	РегистрНакопления.НДСКосвенныеРасходы КАК НДСКосвенныеРасходы
	|ГДЕ
	|	НДСКосвенныеРасходы.Организация = &Организация
	|	И НДСКосвенныеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НДСКосвенныеРасходы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСКосвенныеРасходы.СтатьяЗатрат,
	|	НДСКосвенныеРасходы.СчетЗатрат,
	|	НДСКосвенныеРасходы.ПодразделениеЗатрат,
	|	НДСКосвенныеРасходы.Субконто1,
	|	НДСКосвенныеРасходы.Субконто2,
	|	НДСКосвенныеРасходы.Субконто3,
	|	НДСКосвенныеРасходы.СчетФактура,
	|	НДСКосвенныеРасходы.ВидЦенности,
	|	НДСКосвенныеРасходы.СтавкаНДС,
	|	НДСКосвенныеРасходы.СчетУчетаНДС,
	|	НДСКосвенныеРасходы.НДСВключенВСтоимость,
	|	НДСКосвенныеРасходы.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСКосвенныеРасходы.Организация,
	|	НДСКосвенныеРасходы.СчетФактура,
	|	НДСКосвенныеРасходы.ВидЦенности,
	|	НДСКосвенныеРасходы.СтавкаНДС,
	|	НДСКосвенныеРасходы.СчетУчетаНДС,
	|	НДСКосвенныеРасходы.НДСВключенВСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСКосвенныеРасходы.Организация КАК Организация,
	|	НДСКосвенныеРасходы.СчетФактура КАК СчетФактура,
	|	НДСКосвенныеРасходы.ВидЦенности КАК ВидЦенности,
	|	НДСКосвенныеРасходы.СтавкаНДС КАК СтавкаНДС,
	|	НДСКосвенныеРасходы.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСКосвенныеРасходы.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	НДСКосвенныеРасходы.СуммаБезНДС,
	|	НДСКосвенныеРасходы.НДС,
	|	НДСКосвенныеРасходы.СтатьяЗатрат,
	|	НДСКосвенныеРасходы.СчетЗатрат,
	|	НДСКосвенныеРасходы.ПодразделениеЗатрат,
	|	НДСКосвенныеРасходы.Субконто1,
	|	НДСКосвенныеРасходы.Субконто2,
	|	НДСКосвенныеРасходы.Субконто3
	|ПОМЕСТИТЬ ВТНДСКосвенныеРасходы
	|ИЗ
	|	ТаблицаНДСКосвенныеРасходы КАК НДСКосвенныеРасходы
	|ГДЕ
	|	НДСКосвенныеРасходы.СуммаБезНДС <> 0
	|	И НДСКосвенныеРасходы.НДС <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС,
	|	НДСВключенВСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НДСКосвенныеРасходыОбороты.Организация,
	|	НДСКосвенныеРасходыОбороты.СчетФактура,
	|	НДСКосвенныеРасходыОбороты.ВидЦенности,
	|	НДСКосвенныеРасходыОбороты.СтавкаНДС,
	|	НДСКосвенныеРасходыОбороты.СчетУчетаНДС,
	|	НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость,
	|	НДСКосвенныеРасходыОбороты.СуммаБезНДСОборот,
	|	НДСКосвенныеРасходыОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТНДСКосвенныеРасходыОбороты
	|ИЗ
	|	РегистрНакопления.НДСКосвенныеРасходы.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК НДСКосвенныеРасходыОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСКосвенныеРасходыОбороты.Организация,
	|	НДСКосвенныеРасходыОбороты.СчетФактура,
	|	НДСКосвенныеРасходыОбороты.ВидЦенности,
	|	НДСКосвенныеРасходыОбороты.СтавкаНДС,
	|	НДСКосвенныеРасходыОбороты.СчетУчетаНДС,
	|	НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НДСКосвенныеРасходыОбороты.СчетФактура КАК СчетФактура,
	|	НДСКосвенныеРасходыОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСКосвенныеРасходыОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСКосвенныеРасходыОбороты.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	НДСКосвенныеРасходы.СтатьяЗатрат КАК СтатьяЗатрат,
	|	НДСКосвенныеРасходы.СчетЗатрат КАК СчетЗатрат,
	|	НДСКосвенныеРасходы.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	НДСКосвенныеРасходы.Субконто1 КАК Субконто1,
	|	НДСКосвенныеРасходы.Субконто2 КАК Субконто2,
	|	НДСКосвенныеРасходы.Субконто3 КАК Субконто3,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА НЕ НДСКосвенныеРасходы.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|					ТОГДА НДСКосвенныеРасходы.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|				КОГДА НЕ НДСКосвенныеРасходы.Субконто2.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|					ТОГДА НДСКосвенныеРасходы.Субконто2.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|				КОГДА НЕ НДСКосвенныеРасходы.Субконто3.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|					ТОГДА НДСКосвенныеРасходы.Субконто3.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ = &ВидДеятельностиОпределяетсяПоДоходамПоПолномуБазису
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БазисРаспределенияВключаетЕНВД,
	|	СУММА(НДСКосвенныеРасходыОбороты.СуммаБезНДСОборот) КАК СуммаБезНДС,
	|	СУММА(НДСКосвенныеРасходыОбороты.НДСОборот) КАК НДС,
	|	СУММА(НДСКосвенныеРасходы.СуммаБезНДС) КАК СуммаБезНДСПроводка,
	|	СУММА(НДСКосвенныеРасходы.НДС) КАК НДСПроводка,
	|	NULL КАК Поставщик
	|ИЗ
	|	ВТНДСКосвенныеРасходыОбороты КАК НДСКосвенныеРасходыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНДСКосвенныеРасходы КАК НДСКосвенныеРасходы
	|		ПО НДСКосвенныеРасходыОбороты.Организация = НДСКосвенныеРасходы.Организация
	|			И НДСКосвенныеРасходыОбороты.СчетФактура = НДСКосвенныеРасходы.СчетФактура
	|			И НДСКосвенныеРасходыОбороты.ВидЦенности = НДСКосвенныеРасходы.ВидЦенности
	|			И НДСКосвенныеРасходыОбороты.СтавкаНДС = НДСКосвенныеРасходы.СтавкаНДС
	|			И НДСКосвенныеРасходыОбороты.СчетУчетаНДС = НДСКосвенныеРасходы.СчетУчетаНДС
	|			И НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость = НДСКосвенныеРасходы.НДСВключенВСтоимость
	|ГДЕ
	|	НЕ(ЕСТЬNULL(НДСКосвенныеРасходыОбороты.СуммаБезНДСОборот, 0) = 0
	|				И ЕСТЬNULL(НДСКосвенныеРасходыОбороты.НДСОборот, 0) = 0)
	|	И ВЫБОР
	|			КОГДА НЕ НДСКосвенныеРасходы.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|				ТОГДА НДСКосвенныеРасходы.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|			КОГДА НЕ НДСКосвенныеРасходы.Субконто2.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|				ТОГДА НДСКосвенныеРасходы.Субконто2.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|			КОГДА НЕ НДСКосвенныеРасходы.Субконто3.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|				ТОГДА НДСКосвенныеРасходы.Субконто3.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|			ИНАЧЕ НДСКосвенныеРасходы.СчетЗатрат
	|		КОНЕЦ В (&РаспределяемыеЗатраты)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСКосвенныеРасходыОбороты.СчетФактура,
	|	НДСКосвенныеРасходыОбороты.ВидЦенности,
	|	НДСКосвенныеРасходыОбороты.СтавкаНДС,
	|	НДСКосвенныеРасходыОбороты.СчетУчетаНДС,
	|	НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА НЕ НДСКосвенныеРасходы.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|					ТОГДА НДСКосвенныеРасходы.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|				КОГДА НЕ НДСКосвенныеРасходы.Субконто2.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|					ТОГДА НДСКосвенныеРасходы.Субконто2.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|				КОГДА НЕ НДСКосвенныеРасходы.Субконто3.ВидДеятельностиДляНалоговогоУчетаЗатрат ЕСТЬ NULL 
	|					ТОГДА НДСКосвенныеРасходы.Субконто3.ВидДеятельностиДляНалоговогоУчетаЗатрат
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ = &ВидДеятельностиОпределяетсяПоДоходамПоПолномуБазису
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	НДСКосвенныеРасходы.СтатьяЗатрат,
	|	НДСКосвенныеРасходы.СчетЗатрат,
	|	НДСКосвенныеРасходы.ПодразделениеЗатрат,
	|	НДСКосвенныеРасходы.Субконто1,
	|	НДСКосвенныеРасходы.Субконто2,
	|	НДСКосвенныеРасходы.Субконто3
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС,
	|	НДСВключенВСтоимость,
	|	БазисРаспределенияВключаетЕНВД,
	|	Поставщик,
	|	СчетЗатрат,
	|	СтатьяЗатрат,
	|	ПодразделениеЗатрат,
	|	Субконто2,
	|	Субконто1,
	|	Субконто3
	|ИТОГИ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС,
	|	НДСВключенВСтоимость,
	|	БазисРаспределенияВключаетЕНВД";
				   
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСписокКонтрагентовПоСчетамФактурам(СтруктураПараметров, СписокСчетовФактур)
	
	// Получение поставщиков по счету-фактуре
	Запрос = Новый Запрос;

	СписокДоступныхОрганизаций = СписокОрганизаций(СтруктураПараметров.Организация);
	
	Запрос.УстановитьПараметр("ДоступныеОрганизации", СписокДоступныхОрганизаций);
	Запрос.УстановитьПараметр("СчетаФактуры", 
		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(
		СписокСчетовФактур.ВыгрузитьКолонку("СчетФактура")));
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленный.Поставщик КАК Поставщик,
	|	НДСПредъявленный.СчетФактура КАК СчетФактура
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	НДСПредъявленный.Организация В(&ДоступныеОрганизации)
	|	И НДСПредъявленный.СчетФактура В(&СчетаФактуры)
	|	И НДСПредъявленный.Поставщик <> &ПустойКонтрагент";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СписокОрганизаций(Организация)

	//Если организация является обособленным подразделением, то добавляем в список все обособленные подразделения
	//головной организации и головную организацию
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоОбособленноеПодразделение(Организация) Тогда

		СписокОрганизацийДляОтбора = 
			ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделений(Организация);
	
	//Иначе добавляем в список все подразделения этой организации и организацию
	Иначе 

		СписокОрганизацийДляОтбора = 
			ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделенийОрганизации(Организация);
		
		ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация);

		СписокОрганизацийДляОтбора.Добавить(ГоловнаяОрганизация);
	
	КонецЕсли;
	
	Возврат СписокОрганизацийДляОтбора;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПоНДСКРаспределению(СтруктураПараметров, ДанныеДляЗаполнения)
	
	ПустаяСсылкаДокумента = Документы.РаспределениеНДС.ПустаяСсылка();
	РаспределяемыйНДС = ПустаяСсылкаДокумента.РаспределяемыйНДС.ВыгрузитьКолонки();
	
	ДанныеДляЗаполнения.Вставить("РаспределяемыйНДС", РаспределяемыйНДС);
	
	НДСКРаспределению = ПолучитьВыборкуИзРегистраНДСРаздельныйУчет(СтруктураПараметров).Выгрузить();
	
	КонтрагентПоСчетуФактуре = ПолучитьСписокКонтрагентовПоСчетамФактурам(СтруктураПараметров, НДСКРаспределению);
	КонтрагентПоСчетуФактуре.Индексы.Добавить("СчетФактура");
	
	Для Каждого СтрокаДокумента Из НДСКРаспределению Цикл
		
		НоваяСтрока = РаспределяемыйНДС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
		
		Контрагент = КонтрагентПоСчетуФактуре.Найти(СтрокаДокумента.СчетФактура,"СчетФактура");
		НоваяСтрока.Поставщик = ?(Контрагент <> Неопределено, Контрагент.Поставщик, Справочники.Контрагенты.ПустаяСсылка());
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьВыборкуИзРегистраНДСРаздельныйУчет(СтруктураПараметров)
	
	ТолькоОСиНМА = КонецМесяца(СтруктураПараметров.Дата) <> КонецКвартала(СтруктураПараметров.Дата);
	
	ПредопределенныеСчетаУчетаОСиНМА = Новый Массив;
	Если ТолькоОСиНМА Тогда
		ПланСчетов = ПланыСчетов.Хозрасчетный;
		ПредопределенныеСчетаУчетаОСиНМА.Добавить(ПланСчетов.ОСвОрганизации);        // 01.01
		ПредопределенныеСчетаУчетаОСиНМА.Добавить(ПланСчетов.АрендованноеИмущество); // 01.03
		ПредопределенныеСчетаУчетаОСиНМА.Добавить(ПланСчетов.ОСБезГосРегистрации);   // 01.08
		ПредопределенныеСчетаУчетаОСиНМА.Добавить(ПланСчетов.ДоходныеВложенияВ_МЦ);  // 03
		ПредопределенныеСчетаУчетаОСиНМА.Добавить(ПланСчетов.НематериальныеАктивы);  // 04
	КонецЕсли;
	СчетаУчетаОСиНМА = БухгалтерскийУчет.СформироватьМассивСубсчетов(ПредопределенныеСчетаУчетаОСиНМА);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",	СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоДня(СтруктураПараметров.НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",	КонецМесяца(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("ТолькоОСиНМА",	ТолькоОСиНМА);
	Запрос.УстановитьПараметр("СчетаУчетаОСиНМА",	СчетаУчетаОСиНМА);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСРаздельныйУчетОбороты.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчетОбороты.АналитикаУчетаНДС КАК АналитикаУчетаНДС,
	|	НДСРаздельныйУчетОбороты.Партия,
	|	СУММА(НДСРаздельныйУчетОбороты.КоличествоОборот) КАК Количество,
	|	СУММА(НДСРаздельныйУчетОбороты.СуммаБезНДСОборот) КАК СуммаБезНДС,
	|	СУММА(НДСРаздельныйУчетОбороты.НДСОборот) КАК НДС
	|ПОМЕСТИТЬ ВТОбороты
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И СпособУчетаНДС = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределяется)) КАК НДСРаздельныйУчетОбороты
	|ГДЕ
	|	НДСРаздельныйУчетОбороты.НДСПриход > 0
	|	И НДСРаздельныйУчетОбороты.НДСОборот > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРаздельныйУчетОбороты.АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчетОбороты.АналитикаУчетаНДС,
	|	НДСРаздельныйУчетОбороты.Партия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаЗатрат,
	|	АналитикаУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСРаздельныйУчет.АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчет.АналитикаУчетаНДС,
	|	НДСРаздельныйУчет.Партия,
	|	СУММА(НДСРаздельныйУчет.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(НДСРаздельныйУчет.НДС) КАК НДС
	|ПОМЕСТИТЬ ВТМодернизации
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет КАК НДСРаздельныйУчет
	|ГДЕ
	|	НДСРаздельныйУчет.Организация = &Организация
	|	И НДСРаздельныйУчет.СпособУчетаНДС = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределяется)
	|	И НДСРаздельныйУчет.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НДСРаздельныйУчет.Регистратор ССЫЛКА Документ.МодернизацияОС
	|	И НДСРаздельныйУчет.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРаздельныйУчет.АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчет.АналитикаУчетаНДС,
	|	НДСРаздельныйУчет.Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбороты.АналитикаУчетаЗатрат,
	|	ВТОбороты.АналитикаУчетаНДС,
	|	ВТОбороты.Партия,
	|	ВТОбороты.Количество,
	|	ВТОбороты.СуммаБезНДС - ЕСТЬNULL(ВТМодернизации.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ВТОбороты.НДС - ЕСТЬNULL(ВТМодернизации.НДС, 0) КАК НДС,
	|	ЛОЖЬ КАК Модернизация
	|ПОМЕСТИТЬ ВТНДСРаздельныйУчет
	|ИЗ
	|	ВТОбороты КАК ВТОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМодернизации КАК ВТМодернизации
	|		ПО ВТОбороты.АналитикаУчетаЗатрат = ВТМодернизации.АналитикаУчетаЗатрат
	|			И ВТОбороты.АналитикаУчетаНДС = ВТМодернизации.АналитикаУчетаНДС
	|			И ВТОбороты.Партия = ВТМодернизации.Партия
	|ГДЕ
	|	ВТОбороты.СуммаБезНДС - ЕСТЬNULL(ВТМодернизации.СуммаБезНДС, 0) >= 0
	|	И ВТОбороты.НДС - ЕСТЬNULL(ВТМодернизации.НДС, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТМодернизации.АналитикаУчетаЗатрат,
	|	ВТМодернизации.АналитикаУчетаНДС,
	|	ВТМодернизации.Партия,
	|	0,
	|	ВТМодернизации.СуммаБезНДС,
	|	ВТМодернизации.НДС,
	|	ИСТИНА
	|ИЗ
	|	ВТМодернизации КАК ВТМодернизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТМодернизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрАналитикаУчетаНДС.СчетФактура КАК СчетФактура,
	|	РегистрАналитикаУчетаНДС.ВидЦенности,
	|	РегистрАналитикаУчетаНДС.СчетУчетаНДС,
	|	РегистрАналитикаУчетаНДС.СтавкаНДС,
	|	ВТНДСРаздельныйУчет.Партия КАК Партия,
	|	ВТНДСРаздельныйУчет.Количество,
	|	ВТНДСРаздельныйУчет.СуммаБезНДС,
	|	ВТНДСРаздельныйУчет.НДС,
	|	РегистрАналитикаУчетаЗатрат.СчетЗатрат,
	|	РегистрАналитикаУчетаЗатрат.Подразделение,
	|	РегистрАналитикаУчетаЗатрат.Субконто1,
	|	РегистрАналитикаУчетаЗатрат.Субконто2,
	|	РегистрАналитикаУчетаЗатрат.Субконто3,
	|	ВТНДСРаздельныйУчет.Модернизация
	|ПОМЕСТИТЬ ВТНДСКРаспределению
	|ИЗ
	|	ВТНДСРаздельныйУчет КАК ВТНДСРаздельныйУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		ПО ВТНДСРаздельныйУчет.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНДС КАК РегистрАналитикаУчетаНДС
	|		ПО ВТНДСРаздельныйУчет.АналитикаУчетаНДС = РегистрАналитикаУчетаНДС.КлючАналитики
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ТолькоОСиНМА
	|				ТОГДА РегистрАналитикаУчетаЗатрат.СчетЗатрат В (&СчетаУчетаОСиНМА)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНДСРаздельныйУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНДСКРаспределению.СчетФактура,
	|	ВТНДСКРаспределению.ВидЦенности,
	|	ВТНДСКРаспределению.СчетУчетаНДС,
	|	ВТНДСКРаспределению.СтавкаНДС,
	|	ВТНДСКРаспределению.Партия,
	|	ВТНДСКРаспределению.Количество,
	|	ВТНДСКРаспределению.СуммаБезНДС,
	|	ВТНДСКРаспределению.НДС,
	|	ВТНДСКРаспределению.СчетЗатрат,
	|	ВТНДСКРаспределению.Подразделение,
	|	ВТНДСКРаспределению.Субконто1,
	|	ВТНДСКРаспределению.Субконто2,
	|	ВТНДСКРаспределению.Субконто3,
	|	ЕСТЬNULL(ДанныеСчетФактура.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСФ,
	|	ЕСТЬNULL(ДанныеПартия.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПартии,
	|	ВТНДСКРаспределению.Модернизация
	|ИЗ
	|	ВТНДСКРаспределению КАК ВТНДСКРаспределению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеСчетФактура
	|		ПО ВТНДСКРаспределению.СчетФактура = ДанныеСчетФактура.Документ
	|			И (ДанныеСчетФактура.Организация = &Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПартия
	|		ПО ВТНДСКРаспределению.Партия = ДанныеПартия.Документ
	|			И (ДанныеПартия.Организация = &Организация)";
	
	Результат = Запрос.Выполнить();
	
	Если ТолькоОСиНМА Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НДСКРаспределению", Результат.Выгрузить());
		Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
		Запрос.УстановитьПараметр("НачалоПериода",	НачалоДня(СтруктураПараметров.НачалоПериода));
		Запрос.УстановитьПараметр("КонецПериода",	КонецМесяца(СтруктураПараметров.Дата));
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСКРаспределению.СчетФактура,
		|	НДСКРаспределению.ВидЦенности,
		|	НДСКРаспределению.СчетУчетаНДС,
		|	НДСКРаспределению.СтавкаНДС,
		|	НДСКРаспределению.Партия,
		|	НДСКРаспределению.Количество,
		|	НДСКРаспределению.СуммаБезНДС,
		|	НДСКРаспределению.НДС,
		|	НДСКРаспределению.СчетЗатрат,
		|	НДСКРаспределению.Подразделение,
		|	НДСКРаспределению.Субконто1,
		|	НДСКРаспределению.Субконто2,
		|	НДСКРаспределению.Субконто3,
		|	НДСКРаспределению.ДатаСФ,
		|	НДСКРаспределению.ДатаПартии,
		|	НДСКРаспределению.Модернизация
		|ПОМЕСТИТЬ НДСКРаспределению
		|ИЗ
		|	&НДСКРаспределению КАК НДСКРаспределению
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДСКРаспределению.СчетФактура,
		|	НДСКРаспределению.ВидЦенности,
		|	НДСКРаспределению.СчетУчетаНДС,
		|	НДСКРаспределению.СтавкаНДС,
		|	НДСКРаспределению.Партия,
		|	НДСКРаспределению.Количество,
		|	НДСКРаспределению.СуммаБезНДС,
		|	НДСКРаспределению.НДС,
		|	НДСКРаспределению.СчетЗатрат,
		|	НДСКРаспределению.Подразделение,
		|	НДСКРаспределению.Субконто1,
		|	НДСКРаспределению.Субконто2,
		|	НДСКРаспределению.Субконто3,
		|	НДСКРаспределению.ДатаСФ,
		|	НДСКРаспределению.ДатаПартии,
		|	НДСКРаспределению.Модернизация
		|ПОМЕСТИТЬ ВТ_ОС
		|ИЗ
		|	НДСКРаспределению КАК НДСКРаспределению
		|ГДЕ
		|	НДСКРаспределению.Субконто1 ССЫЛКА Справочник.ОсновныеСредства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДСКРаспределению.СчетФактура,
		|	НДСКРаспределению.ВидЦенности,
		|	НДСКРаспределению.СчетУчетаНДС,
		|	НДСКРаспределению.СтавкаНДС,
		|	НДСКРаспределению.Партия,
		|	НДСКРаспределению.Количество,
		|	НДСКРаспределению.СуммаБезНДС,
		|	НДСКРаспределению.НДС,
		|	НДСКРаспределению.СчетЗатрат,
		|	НДСКРаспределению.Подразделение,
		|	НДСКРаспределению.Субконто1,
		|	НДСКРаспределению.Субконто2,
		|	НДСКРаспределению.Субконто3,
		|	НДСКРаспределению.ДатаСФ,
		|	НДСКРаспределению.ДатаПартии,
		|	НДСКРаспределению.Модернизация
		|ПОМЕСТИТЬ ВТ_НМА
		|ИЗ
		|	НДСКРаспределению КАК НДСКРаспределению
		|ГДЕ
		|	НДСКРаспределению.Субконто1 ССЫЛКА Справочник.НематериальныеАктивы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НДСКРаспределению
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияОСОрганизаций.ОсновноеСредство
		|ПОМЕСТИТЬ СостоянияОС
		|ИЗ
		|	РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
		|ГДЕ
		|	СостоянияОСОрганизаций.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				ВТ_ОС.Субконто1
		|			ИЗ
		|				ВТ_ОС)
		|	И СостоянияОСОрганизаций.Организация = &Организация
		|	И СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|	И СостоянияОСОрганизаций.ДатаСостояния МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияНМАОрганизаций.НематериальныйАктив
		|ПОМЕСТИТЬ СостоянияНМА
		|ИЗ
		|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
		|ГДЕ
		|	СостоянияНМАОрганизаций.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				ВТ_НМА.Субконто1
		|			ИЗ
		|				ВТ_НМА)
		|	И СостоянияНМАОрганизаций.Организация = &Организация
		|	И СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
		|	И СостоянияНМАОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОС.СчетФактура,
		|	ВТ_ОС.ВидЦенности,
		|	ВТ_ОС.СчетУчетаНДС,
		|	ВТ_ОС.СтавкаНДС,
		|	ВТ_ОС.Партия,
		|	ВТ_ОС.Количество,
		|	ВТ_ОС.СуммаБезНДС,
		|	ВТ_ОС.НДС,
		|	ВТ_ОС.СчетЗатрат,
		|	ВТ_ОС.Подразделение,
		|	ВТ_ОС.Субконто1,
		|	ВТ_ОС.Субконто2,
		|	ВТ_ОС.Субконто3,
		|	ВТ_ОС.ДатаСФ,
		|	ВТ_ОС.ДатаПартии,
		|	ВТ_ОС.Модернизация
		|ИЗ
		|	СостоянияОС КАК СостоянияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОС КАК ВТ_ОС
		|		ПО СостоянияОС.ОсновноеСредство = ВТ_ОС.Субконто1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_НМА.СчетФактура,
		|	ВТ_НМА.ВидЦенности,
		|	ВТ_НМА.СчетУчетаНДС,
		|	ВТ_НМА.СтавкаНДС,
		|	ВТ_НМА.Партия,
		|	ВТ_НМА.Количество,
		|	ВТ_НМА.СуммаБезНДС,
		|	ВТ_НМА.НДС,
		|	ВТ_НМА.СчетЗатрат,
		|	ВТ_НМА.Подразделение,
		|	ВТ_НМА.Субконто1,
		|	ВТ_НМА.Субконто2,
		|	ВТ_НМА.Субконто3,
		|	ВТ_НМА.ДатаСФ,
		|	ВТ_НМА.ДатаПартии,
		|	ВТ_НМА.Модернизация
		|ИЗ
		|	СостоянияНМА КАК СостоянияНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НМА КАК ВТ_НМА
		|		ПО СостоянияНМА.НематериальныйАктив = ВТ_НМА.Субконто1";
		
		Результат = Запрос.Выполнить();
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Процедура вызывается по кнопке "Распределить" в форме диалога документа.
// В процедуре реализуется алгоритм распределения косвенных расходов в табличной
// части "СоставКосвенныхРасходов" на различные виды реализации в текущем периоде.
//
Процедура РаспределитьПоВыручке(РеквизитыДокумента, СоставКосвенныхРасходов) Экспорт

	БазаРаспределенияСЕНВД = РеквизитыДокумента.ВыручкаНДС + РеквизитыДокумента.ВыручкаБезНДС 
							+ РеквизитыДокумента.ВыручкаЕНВД + РеквизитыДокумента.ВыручкаНДС0;
	БазаРаспределенияБезЕНВД = РеквизитыДокумента.ВыручкаНДС + РеквизитыДокумента.ВыручкаБезНДС + РеквизитыДокумента.ВыручкаНДС0;
	
	Для каждого СтрТабЧасти Из СоставКосвенныхРасходов Цикл
		
		Если СтрТабЧасти.БазисРаспределенияВключаетЕНВД Тогда
			БазаРаспределения = БазаРаспределенияСЕНВД;
		Иначе
			БазаРаспределения = БазаРаспределенияБезЕНВД;
		КонецЕсли; 

		УчтеноСуммы   = 0;
		УчтеноНДС     = 0;
		УчтеноВыручки = 0;

		Если РеквизитыДокумента.ВыручкаНДС <> 0 Тогда

			СтрТабЧасти.НДССумма = Окр(СтрТабЧасти.СуммаВсего * (РеквизитыДокумента.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - 
								   УчтеноСуммы;
			СтрТабЧасти.НДС      = Окр(СтрТабЧасти.НДСВсего * (РеквизитыДокумента.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - 
								   УчтеноНДС;

			УчтеноСуммы     = УчтеноСуммы + СтрТабЧасти.НДССумма;
			УчтеноНДС       = УчтеноНДС + СтрТабЧасти.НДС;
			УчтеноВыручки   = УчтеноВыручки + РеквизитыДокумента.ВыручкаНДС;

		Иначе
			СтрТабЧасти.НДССумма = 0;
			СтрТабЧасти.НДС      = 0;
		КонецЕсли;

		Если РеквизитыДокумента.ВыручкаБезНДС <> 0 Тогда

			СтрТабЧасти.БезНДССумма = Окр(СтрТабЧасти.СуммаВсего * (РеквизитыДокумента.ВыручкаБезНДС + УчтеноВыручки)/БазаРаспределения, 2) - 
									  УчтеноСуммы;
			СтрТабЧасти.БезНДС      = Окр(СтрТабЧасти.НДСВсего * (РеквизитыДокумента.ВыручкаБезНДС + УчтеноВыручки)/БазаРаспределения, 2) - 
									  УчтеноНДС;

			УчтеноСуммы   = УчтеноСуммы + СтрТабЧасти.БезНДССумма;
			УчтеноНДС     = УчтеноНДС + СтрТабЧасти.БезНДС;
			УчтеноВыручки = УчтеноВыручки + РеквизитыДокумента.ВыручкаБезНДС;

		Иначе
			СтрТабЧасти.БезНДССумма = 0;
			СтрТабЧасти.БезНДС      = 0;
		КонецЕсли;

		Если РеквизитыДокумента.ВыручкаЕНВД <> 0 И СтрТабЧасти.БазисРаспределенияВключаетЕНВД Тогда

			СтрТабЧасти.ЕНВДСумма = Окр(СтрТабЧасти.СуммаВсего * (РеквизитыДокумента.ВыручкаЕНВД + УчтеноВыручки)/БазаРаспределения, 2) - 
								    УчтеноСуммы;
			СтрТабЧасти.ЕНВДНДС   = Окр(СтрТабЧасти.НДСВсего * (РеквизитыДокумента.ВыручкаЕНВД + УчтеноВыручки)/БазаРаспределения, 2) - 
									УчтеноНДС;

			УчтеноСуммы   = УчтеноСуммы + СтрТабЧасти.ЕНВДСумма;
			УчтеноНДС     = УчтеноНДС + СтрТабЧасти.ЕНВДНДС;
			УчтеноВыручки = УчтеноВыручки + РеквизитыДокумента.ВыручкаЕНВД;

		Иначе
			СтрТабЧасти.ЕНВДСумма = 0;
			СтрТабЧасти.ЕНВДНДС   = 0;
		КонецЕсли;
		
		Если РеквизитыДокумента.ВыручкаНДС0 <> 0 Тогда

			СтрТабЧасти.НДС0Сумма = Окр(СтрТабЧасти.СуммаВсего * (РеквизитыДокумента.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - 
									УчтеноСуммы;
			СтрТабЧасти.НДС0      = Окр(СтрТабЧасти.НДСВсего * (РеквизитыДокумента.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - 
									УчтеноНДС;

			УчтеноСуммы   = УчтеноСуммы + СтрТабЧасти.НДС0Сумма;
			УчтеноНДС     = УчтеноНДС + СтрТабЧасти.НДС0;
			УчтеноВыручки = УчтеноВыручки + РеквизитыДокумента.ВыручкаНДС0;

		Иначе
			СтрТабЧасти.НДС0Сумма = 0;
			СтрТабЧасти.НДС0      = 0;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура РаспределитьПоВыручкеРаспределяемыйНДС(РеквизитыДокумента, РаспределяемыйНДС) Экспорт

	Если РаспределяемыйНДС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	БазаРаспределения = РеквизитыДокумента.ВыручкаНДС + РеквизитыДокумента.ВыручкаБезНДС 
							+ РеквизитыДокумента.ВыручкаЕНВД + РеквизитыДокумента.ВыручкаНДС0;
	
	Для каждого СтрТабЧасти Из РаспределяемыйНДС Цикл
		
		УчтеноСуммы   = 0;
		УчтеноНДС     = 0;
		УчтеноВыручки = 0;

		Если РеквизитыДокумента.ВыручкаНДС <> 0 Тогда

			СтрТабЧасти.СуммаБезНДСПринимаетсяКВычету = Окр(СтрТабЧасти.СуммаБезНДС
				* (РеквизитыДокумента.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
			СтрТабЧасти.НДСПринимаетсяКВычету = Окр(СтрТабЧасти.НДС
				* (РеквизитыДокумента.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

			УчтеноСуммы     = УчтеноСуммы + СтрТабЧасти.СуммаБезНДСПринимаетсяКВычету;
			УчтеноНДС       = УчтеноНДС + СтрТабЧасти.НДСПринимаетсяКВычету;
			УчтеноВыручки   = УчтеноВыручки + РеквизитыДокумента.ВыручкаНДС;

		Иначе
			СтрТабЧасти.СуммаБезНДСПринимаетсяКВычету = 0;
			СтрТабЧасти.НДСПринимаетсяКВычету = 0;
		КонецЕсли;

		Если РеквизитыДокумента.ВыручкаБезНДС <> 0 Тогда

			СтрТабЧасти.СуммаБезНДСУчитываетсяВCтоимости = Окр(СтрТабЧасти.СуммаБезНДС
				* (РеквизитыДокумента.ВыручкаБезНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
			СтрТабЧасти.НДСУчитываетсяВCтоимости = Окр(СтрТабЧасти.НДС
				* (РеквизитыДокумента.ВыручкаБезНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

			УчтеноСуммы   = УчтеноСуммы + СтрТабЧасти.СуммаБезНДСУчитываетсяВCтоимости;
			УчтеноНДС     = УчтеноНДС + СтрТабЧасти.НДСУчитываетсяВCтоимости;
			УчтеноВыручки = УчтеноВыручки + РеквизитыДокумента.ВыручкаБезНДС;

		Иначе
			СтрТабЧасти.СуммаБезНДСУчитываетсяВCтоимости = 0;
			СтрТабЧасти.НДСУчитываетсяВCтоимости = 0;
		КонецЕсли;

		Если РеквизитыДокумента.ВыручкаЕНВД <> 0 Тогда

			СтрТабЧасти.СуммаБезНДСУчитываетсяВCтоимостиЕНВД = Окр(СтрТабЧасти.СуммаБезНДС
				* (РеквизитыДокумента.ВыручкаЕНВД + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
			СтрТабЧасти.НДСУчитываетсяВCтоимостиЕНВД   = Окр(СтрТабЧасти.НДС
				* (РеквизитыДокумента.ВыручкаЕНВД + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

			УчтеноСуммы   = УчтеноСуммы + СтрТабЧасти.СуммаБезНДСУчитываетсяВCтоимостиЕНВД;
			УчтеноНДС     = УчтеноНДС + СтрТабЧасти.НДСУчитываетсяВCтоимостиЕНВД;
			УчтеноВыручки = УчтеноВыручки + РеквизитыДокумента.ВыручкаЕНВД;

		Иначе
			СтрТабЧасти.СуммаБезНДСУчитываетсяВCтоимостиЕНВД = 0;
			СтрТабЧасти.НДСУчитываетсяВCтоимостиЕНВД = 0;
		КонецЕсли;
		
		Если РеквизитыДокумента.ВыручкаНДС0 <> 0 Тогда

			СтрТабЧасти.СуммаБезНДСДляОперацийПо0 = Окр(СтрТабЧасти.СуммаБезНДС
				* (РеквизитыДокумента.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
			СтрТабЧасти.НДСДляОперацийПо0 = Окр(СтрТабЧасти.НДС
				* (РеквизитыДокумента.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

			УчтеноСуммы   = УчтеноСуммы + СтрТабЧасти.СуммаБезНДСДляОперацийПо0;
			УчтеноНДС     = УчтеноНДС + СтрТабЧасти.НДСДляОперацийПо0;
			УчтеноВыручки = УчтеноВыручки + РеквизитыДокумента.ВыручкаНДС0;

		Иначе
			СтрТабЧасти.СуммаБезНДСДляОперацийПо0 = 0;
			СтрТабЧасти.НДСДляОперацийПо0 = 0;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДГОТОВКИ ПАРАМЕТРОВ ПРОВЕДЕНИЯ

Функция ПодготовитьПараметрыПроведения(Ссылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	НомераТаблиц = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Организация, Дата");
	Запрос.УстановитьПараметр("Организация",   РеквизитыДокумента.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(РеквизитыДокумента.Дата));
	Запрос.УстановитьПараметр("КонецПериода",  КонецКвартала(РеквизитыДокумента.Дата));
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицаПоКосвеннымРасходам(НомераТаблиц)
		+ ТекстЗапросаТаблицаРаспределяемыйНДС(НомераТаблиц)
		+ ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц)
		+ ТекстЗапросаТаблицаВключенныйВСтоимостьНДСПоНеоблагаемымОперациям(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, Ссылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Реквизиты.УпрощенныйУчетНДС 		 = УчетнаяПолитика.УпрощенныйУчетНДС(Реквизиты.Организация, Реквизиты.Период);
	Реквизиты.СпособОценкиМПЗ 			 = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период);
	Реквизиты.ПартионныйУчет 			 = ?(Реквизиты.СпособОценкиМПЗ = Перечисления.СпособыОценки.ФИФО, Истина, Ложь);
	Реквизиты.ПрименениеПБУ18 			 = УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период);
	Реквизиты.РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если Реквизиты.РаздельныйУчетНДСНаСчете19 Тогда
		Реквизиты.КонецПериода = КонецМесяца(Реквизиты.Период);
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
		
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаРеквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РаспределениеНДС.Ссылка КАК Регистратор,
	|	РаспределениеНДС.Дата КАК Период,
	|	РаспределениеНДС.Номер,
	|	РаспределениеНДС.Организация,
	|	РаспределениеНДС.СтатьяЗатратНДС,
	|	РаспределениеНДС.СтатьяЗатратНДСприЕНВД,
	|	РаспределениеНДС.СчетСписанияНДС,
	|	РаспределениеНДС.СубконтоСписанияНДС1,
	|	РаспределениеНДС.СубконтоСписанияНДС2,
	|	РаспределениеНДС.СубконтоСписанияНДС3,
	|	РаспределениеНДС.ДляСписанияНДСиспользоватьСчетИАналитикуУчетаЗатрат,
	|	РаспределениеНДС.ПодразделениеЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК УпрощенныйУчетНДС,
	|	НЕОПРЕДЕЛЕНО КАК СпособОценкиМПЗ,
	|	НЕОПРЕДЕЛЕНО КАК ПартионныйУчет,
	|	НЕОПРЕДЕЛЕНО КАК ПрименениеПБУ18,
	|	НЕОПРЕДЕЛЕНО КАК РаздельныйУчетНДСНаСчете19,
	|	РаспределениеНДС.НачалоПериода КАК НачалоПериода,
	|	КОНЕЦПЕРИОДА(РаспределениеНДС.НачалоПериода, КВАРТАЛ) КАК КонецПериода,
	|	РаспределениеНДС.ВыручкаЕНВД,
	|	РаспределениеНДС.ВыручкаБезНДС,
	|	РаспределениеНДС.ВыручкаНДС0,
	|	РаспределениеНДС.ВыручкаНДС,
	|	РаспределениеНДС.СобытиеОС
	|ИЗ
	|	Документ.РаспределениеНДС КАК РаспределениеНДС
	|ГДЕ
	|	РаспределениеНДС.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

	
КонецФункции	

Функция ТекстЗапросаТаблицаПоКосвеннымРасходам(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаПоКосвеннымРасходам", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаСоставРасходов.ВидЦенности,
	|	ТаблицаСоставРасходов.Поставщик,
	|	ТаблицаСоставРасходов.СчетФактура,
	|	ТаблицаСоставРасходов.СтавкаНДС,
	|	ТаблицаСоставРасходов.СуммаВсего,
	|	ТаблицаСоставРасходов.НДСВсего,
	|	ТаблицаСоставРасходов.БезНДССумма,
	|	ТаблицаСоставРасходов.БезНДС,
	|	ТаблицаСоставРасходов.НДС0Сумма,
	|	ТаблицаСоставРасходов.ЕНВДСумма,
	|	ТаблицаСоставРасходов.ЕНВДНДС,
	|	ТаблицаСоставРасходов.НДС0,
	|	ТаблицаСоставРасходов.НДССумма,
	|	ТаблицаСоставРасходов.НДС,
	|	ТаблицаСоставРасходов.КлючСтроки,
	|	ТаблицаСоставРасходов.БазисРаспределенияВключаетЕНВД,
	|	ТаблицаСоставРасходов.НДСВключенВСтоимость,
	|	ТаблицаСоставРасходов.СчетУчетаНДС,
	|	ТаблицаДокумента.СчетаУчетаРасходов.(
	|		КлючСтроки,
	|		СчетЗатрат,
	|		ПодразделениеЗатрат,
	|		Субконто1,
	|		Субконто2,
	|		Субконто3,
	|		СуммаБезНДС,
	|		НДС
	|	),
	|	ТаблицаСоставРасходов.СчетФактура.ДоговорКонтрагента
	|ИЗ
	|	Документ.РаспределениеНДС.СоставКосвенныхРасходов КАК ТаблицаСоставРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеНДС КАК ТаблицаДокумента
	|		ПО ТаблицаСоставРасходов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаСоставРасходов.Ссылка = &Ссылка
	|	И ТаблицаДокумента.СчетаУчетаРасходов.КлючСтроки = ТаблицаСоставРасходов.КлючСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
			
КонецФункции		

Функция ТекстЗапросаТаблицаРаспределяемыйНДС(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаРаспределяемыйНДС", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РаспределяемыйНДС.Ссылка.Организация,
	|	РаспределяемыйНДС.СчетФактура,
	|	РаспределяемыйНДС.Поставщик,
	|	РаспределяемыйНДС.СуммаБезНДС,
	|	РаспределяемыйНДС.НДС,
	|	РаспределяемыйНДС.СуммаБезНДСПринимаетсяКВычету,
	|	РаспределяемыйНДС.НДСПринимаетсяКВычету,
	|	РаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимости,
	|	РаспределяемыйНДС.НДСУчитываетсяВCтоимости,
	|	РаспределяемыйНДС.СуммаБезНДСДляОперацийПо0,
	|	РаспределяемыйНДС.НДСДляОперацийПо0,
	|	РаспределяемыйНДС.ВидЦенности,
	|	РаспределяемыйНДС.СчетУчетаНДС,
	|	РаспределяемыйНДС.СтавкаНДС,
	|	РаспределяемыйНДС.Партия,
	|	РаспределяемыйНДС.СчетЗатрат,
	|	РаспределяемыйНДС.Подразделение,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.НДСУчитываетсяВCтоимости > 0
	|			ТОГДА ВЫБОР
	|					КОГДА РаспределяемыйНДС.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫБОР
	|								КОГДА ВЫРАЗИТЬ(РаспределяемыйНДС.Субконто1 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|									ТОГДА РаспределяемыйНДС.Ссылка.СтатьяЗатратНДС
	|								ИНАЧЕ РаспределяемыйНДС.Субконто1
	|							КОНЕЦ
	|					ИНАЧЕ РаспределяемыйНДС.Субконто1
	|				КОНЕЦ
	|		ИНАЧЕ РаспределяемыйНДС.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.НДСУчитываетсяВCтоимости > 0
	|			ТОГДА ВЫБОР
	|					КОГДА РаспределяемыйНДС.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫБОР
	|								КОГДА ВЫРАЗИТЬ(РаспределяемыйНДС.Субконто2 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|									ТОГДА РаспределяемыйНДС.Ссылка.СтатьяЗатратНДС
	|								ИНАЧЕ РаспределяемыйНДС.Субконто2
	|							КОНЕЦ
	|					ИНАЧЕ РаспределяемыйНДС.Субконто2
	|				КОНЕЦ
	|		ИНАЧЕ РаспределяемыйНДС.Субконто2
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.НДСУчитываетсяВCтоимости > 0
	|			ТОГДА ВЫБОР
	|					КОГДА РаспределяемыйНДС.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫБОР
	|								КОГДА ВЫРАЗИТЬ(РаспределяемыйНДС.Субконто3 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|									ТОГДА РаспределяемыйНДС.Ссылка.СтатьяЗатратНДС
	|								ИНАЧЕ РаспределяемыйНДС.Субконто3
	|							КОНЕЦ
	|					ИНАЧЕ РаспределяемыйНДС.Субконто3
	|				КОНЕЦ
	|		ИНАЧЕ РаспределяемыйНДС.Субконто3
	|	КОНЕЦ КАК Субконто3,
	|	РаспределяемыйНДС.Количество,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределяется) КАК СпособУчетаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределен) КАК НовыйСпособУчетаНДС,
	|	РаспределяемыйНДС.Субконто1 КАК ИсходноеСубконто1,
	|	РаспределяемыйНДС.Субконто2 КАК ИсходноеСубконто2,
	|	РаспределяемыйНДС.Субконто3 КАК ИсходноеСубконто3,
	|	РаспределяемыйНДС.Модернизация
	|ИЗ
	|	Документ.РаспределениеНДС.РаспределяемыйНДС КАК РаспределяемыйНДС
	|ГДЕ
	|	РаспределяемыйНДС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РаспределяемыйНДС.Ссылка.Организация,
	|	РаспределяемыйНДС.СчетФактура,
	|	РаспределяемыйНДС.Поставщик,
	|	0,
	|	0,
	|	0,
	|	0,
	|	РаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимостиЕНВД,
	|	РаспределяемыйНДС.НДСУчитываетсяВCтоимостиЕНВД,
	|	0,
	|	0,
	|	РаспределяемыйНДС.ВидЦенности,
	|	РаспределяемыйНДС.СчетУчетаНДС,
	|	РаспределяемыйНДС.СтавкаНДС,
	|	РаспределяемыйНДС.Партия,
	|	РаспределяемыйНДС.СчетЗатрат,
	|	РаспределяемыйНДС.Подразделение,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(РаспределяемыйНДС.Субконто1 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|						ТОГДА РаспределяемыйНДС.Ссылка.СтатьяЗатратНДСприЕНВД
	|					ИНАЧЕ РаспределяемыйНДС.Субконто1
	|				КОНЕЦ
	|		ИНАЧЕ РаспределяемыйНДС.Субконто1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(РаспределяемыйНДС.Субконто2 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|						ТОГДА РаспределяемыйНДС.Ссылка.СтатьяЗатратНДСприЕНВД
	|					ИНАЧЕ РаспределяемыйНДС.Субконто2
	|				КОНЕЦ
	|		ИНАЧЕ РаспределяемыйНДС.Субконто2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(РаспределяемыйНДС.Субконто3 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|						ТОГДА РаспределяемыйНДС.Ссылка.СтатьяЗатратНДСприЕНВД
	|					ИНАЧЕ РаспределяемыйНДС.Субконто3
	|				КОНЕЦ
	|		ИНАЧЕ РаспределяемыйНДС.Субконто3
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РаспределяемыйНДС.НДСПринимаетсяКВычету + РаспределяемыйНДС.НДСУчитываетсяВCтоимости + РаспределяемыйНДС.НДСДляОперацийПо0 > 0
	|			ТОГДА 0
	|		ИНАЧЕ РаспределяемыйНДС.Количество
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределяется),
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределен),
	|	РаспределяемыйНДС.Субконто1,
	|	РаспределяемыйНДС.Субконто2,
	|	РаспределяемыйНДС.Субконто3,
	|	РаспределяемыйНДС.Модернизация
	|ИЗ
	|	Документ.РаспределениеНДС.РаспределяемыйНДС КАК РаспределяемыйНДС
	|ГДЕ
	|	РаспределяемыйНДС.Ссылка = &Ссылка
	|	И РаспределяемыйНДС.НДСУчитываетсяВCтоимостиЕНВД > 0";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
			
КонецФункции		

Функция ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц)

	НомераТаблиц.Вставить("ДанныеРегламентнойОперации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.РаспределениеНДС) КАК РегламентнаяОперация
	|ИЗ
	|	Документ.РаспределениеНДС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаТаблицаВключенныйВСтоимостьНДСПоНеоблагаемымОперациям(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаВключенныйВСтоимостьНДСПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВключенныйВСтоимостьНДС",        НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НДСНеоблагаемыеОперацииОбороты.СуммаРеализацииБезНДСПриход
	|ПОМЕСТИТЬ НеоблагаемыеОперацииЗаКвартал
	|ИЗ
	|	РегистрНакопления.НДСНеоблагаемыеОперации.Обороты(&НачалоПериода, &КонецПериода, Квартал, Организация = &Организация) КАК НДСНеоблагаемыеОперацииОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимости) КАК СуммаНДСНеПодлежащаяВычету,
	|	РаспределениеНДСРаспределяемыйНДС.Ссылка.Организация,
	|	РаспределениеНДСРаспределяемыйНДС.Ссылка.Дата КАК Период
	|ИЗ
	|	НеоблагаемыеОперацииЗаКвартал КАК НеоблагаемыеОперацииЗаКвартал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеНДС.РаспределяемыйНДС КАК РаспределениеНДСРаспределяемыйНДС
	|		ПО (НеоблагаемыеОперацииЗаКвартал.СуммаРеализацииБезНДСПриход > 0)
	|ГДЕ
	|	РаспределениеНДСРаспределяемыйНДС.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеНДСРаспределяемыйНДС.Ссылка.Организация,
	|	РаспределениеНДСРаспределяемыйНДС.Ссылка.Дата";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	
КонецПроцедуры

#КонецЕсли