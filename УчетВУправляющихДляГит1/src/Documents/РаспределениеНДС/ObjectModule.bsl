#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	УстановитьНачалоПериода();
	
	ДляСписанияНДСИспользоватьСчетИАналитикуУчетаЗатрат = Истина;

	Если НЕ ЗначениеЗаполнено(СобытиеОС) Тогда
		СобытиеОС = УчетОС.ПолучитьСобытиеПоОСИзСправочника(Перечисления.ВидыСобытийОС.РаспределениеНДС);
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата          = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	УстановитьНачалоПериода();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ЕстьВыручкаБезНДС = ВыручкаБезНДС <> 0 
		ИЛИ СоставКосвенныхРасходов.Итог("БезНДС") <> 0;
	ЕстьВыручкаЕНВД = ВыручкаЕНВД <> 0
		ИЛИ СоставКосвенныхРасходов.Итог("ЕНВДНДС") <> 0;
		
	Если НЕ ЕстьВыручкаБезНДС Тогда
		НепроверяемыеРеквизиты.Добавить("СтатьяЗатратНДС");
	КонецЕсли;
	Если НЕ ЕстьВыручкаЕНВД Тогда
		НепроверяемыеРеквизиты.Добавить("СтатьяЗатратНДСприЕНВД");
	КонецЕсли;
	
	// Если организация зарегистрирована в последние 10 рабочих дней квартала
	// налоговый период по НДС является расширенным (с даты регистрации по конец следующего квартала).
	// Распределение НДС должно быть произведено с учетом выручки за налоговый весь период.
	// В периоде регистрации ввод документа запрещен.
	КонецКвартала = КонецДня(КонецКвартала(Дата));
	
	БлижайшийНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		Организация,
		Дата,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал);
		
	Если БлижайшийНалоговыйПериод.Конец <> КонецКвартала Тогда
		
		НачалоНалоговогоПериода = Формат(БлижайшийНалоговыйПериод.Начало, "ДЛФ=D");
		КонецНалоговогоПериода  = Формат(БлижайшийНалоговыйПериод.Конец, "ДЛФ=D");
		КварталРаспределения    = Месяц(БлижайшийНалоговыйПериод.Конец)/3;
		ГодРаспределения        = Формат(Год(БлижайшийНалоговыйПериод.Конец), "ЧГ=0");
		
		ШаблонСообщения = НСтр("ru='Первый налоговый период с %1 по %2. Распределение НДС производится в %3 квартале %4 года.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения,
			НачалоНалоговогоПериода,
			КонецНалоговогоПериода,
			КварталРаспределения,
			ГодРаспределения);
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			,
			"Корректность",
			НСтр("ru = 'Дата'"),,,
			ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
			"Дата", "Объект", Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.РаспределениеНДС.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ

	УчетНДСБП.СформироватьДвиженияРаспределениеНДСКосвенныхРасходов(
		ПараметрыПроведения.ТаблицаРеквизиты, ПараметрыПроведения.ТаблицаПоКосвеннымРасходам, Движения, Отказ);
		
	УчетНДСРаздельный.СформироватьДвиженияРаспределениеНДС(
		ПараметрыПроведения.ТаблицаРеквизиты, 
		ПараметрыПроведения.ТаблицаРаспределяемыйНДС, 
		ПараметрыПроведения.ТаблицаВключенныйВСтоимостьНДС, 
		Движения, 
		Отказ);
		
	РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.СформироватьДвиженияФактВыполненияРегламентнойОперации(
		ПараметрыПроведения.ДанныеРегламентнойОперации, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.СброситьФактВыполненияОперации(Ссылка);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьНачалоПериода()
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	БлижайшийНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		Организация,
		Дата,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал);
	
	Если НачалоКвартала(Дата) < БлижайшийНалоговыйПериод.Начало Тогда
		// Отчетность не представляется, отобразим период стандартно.
		НачалоПериода = НачалоКвартала(Дата);
	ИначеЕсли БлижайшийНалоговыйПериод.Начало <> БлижайшийНалоговыйПериод.Период Тогда
		// Распределение НДС за расширенный налоговый период (п.2 ст.55 НК).
		НачалоПериода = БлижайшийНалоговыйПериод.Начало;
	Иначе
		НачалоПериода = НачалоКвартала(Дата);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#КонецЕсли