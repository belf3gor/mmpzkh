#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 7, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

Функция ТекстЗапросаДанныеДляПечатиСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("Реквизиты",                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалюте", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",         НомераТаблиц.Количество());
	
	// В случае включения НДС в стоимость проводки по НДС не будет в Хозрасчетном, 
	// поэтому для таблицы СуммыПроводок используем НДСПредъявленный.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|					ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Поставщик,
	|	Реквизиты.Контрагент.ИНН КАК ИННпоставщика,
	|	Реквизиты.Контрагент КАК ОбособленноеПодразделениеПоставщика,
	|	НЕОПРЕДЕЛЕНО КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Покупатель,
	|	Реквизиты.Организация.ИНН КАК ИННпокупателя,
	|	Реквизиты.Организация КАК ОбособленноеПодразделениеПокупателя,
	|	НЕОПРЕДЕЛЕНО КАК Грузополучатель,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.Представление КАК Основание,
	|	Реквизиты.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	Реквизиты.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	ЛОЖЬ КАК ЕстьТовары,
	|	"""" КАК АдресДоставки,
	|	"""" КАК СведенияОТранспортировкеИГрузе,
	|	НЕОПРЕДЕЛЕНО КАК ОтветственныйЗаОформление,
	|	"""" КАК СопроводительныеДокументы,
	|	НЕОПРЕДЕЛЕНО КАК Перевозчик,
	|	ЛОЖЬ КАК ПеревозкаАвтотранспортом,
	|	"""" КАК Исполнитель,
	|	"""" КАК ИсполнительНаОсновании,
	|	"""" КАК ОтпускПроизвел,
	|	"""" КАК ДоверенностьНомер,
	|	"""" КАК ДоверенностьДата,
	|	"""" КАК ДоверенностьВыдана,
	|	"""" КАК ДоверенностьЧерезКого,
	|	"""" КАК ЗаЗаказчикаНаОсновании
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура КАК Товар,
	|	ТаблицаУслуги.Номенклатура.Код КАК ТоварКод,
	|	ТаблицаУслуги.Номенклатура.Артикул КАК ТоварАртикул,
	|	ТаблицаУслуги.Содержание КАК ТоварНаименование,
	|	"""" КАК СтранаПроисхождения,
	|	"""" КАК ПредставлениеСтраны,
	|	"""" КАК НомерГТД,
	|	"""" КАК ПредставлениеГТД,
	|	"""" КАК РегистрационныйНомерТД,
	|	ТаблицаУслуги.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаУслуги.Количество КАК Количество,
	|	ТаблицаУслуги.Цена КАК Цена,
	|	ТаблицаУслуги.Сумма КАК Сумма,
	|	ТаблицаУслуги.СуммаНДС КАК СуммаНДС,
	|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ИСТИНА КАК ЭтоУслуга,
	|	ТаблицаУслуги.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ТаблицаУслуги.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ЭтоКомиссия,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0) КАК СуммаБезНДСРуб,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК КонтрагентСводныйСФ,
	|	НЕОПРЕДЕЛЕНО КАК ПериодичностьУслуги,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка) КАК ТоварКодТНВЭД
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Услуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаУслуги.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (ТаблицаУслуги.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|ГДЕ
	|	ТаблицаУслуги.Ссылка = &ДокументОснование";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаДанныеДляОбновленияЦенДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПоступлениеИзПереработкиВозвратнаяТара.Номенклатура,
	|	ПоступлениеИзПереработкиВозвратнаяТара.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	ИСТИНА КАК ЦенаВключаетНДС
	|ПОМЕСТИТЬ ВозвратнаяТара
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.ВозвратнаяТара КАК ПоступлениеИзПереработкиВозвратнаяТара
	|ГДЕ
	|	ПоступлениеИзПереработкиВозвратнаяТара.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеИзПереработкиУслуги.Номенклатура КАК Номенклатура,
	|	ПоступлениеИзПереработкиУслуги.Цена КАК Цена,
	|	&Валюта КАК Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Услуги КАК ПоступлениеИзПереработкиУслуги
	|ГДЕ
	|	ПоступлениеИзПереработкиУслуги.Ссылка = &Ссылка
	|	И НЕ ПоступлениеИзПереработкиУслуги.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВозвратнаяТара.Номенклатура,
	|	ВозвратнаяТара.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	ВозвратнаяТара.ЦенаВключаетНДС
	|ИЗ
	|	ВозвратнаяТара КАК ВозвратнаяТара";
	
	Если Не ЗначениеЗаполнено(Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить()) Тогда
		ТекстЗапроса = ТекстЗапроса 
		+ "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПоступлениеИзПереработкиПродукция.Номенклатура КАК Номенклатура,
		|	ПоступлениеИзПереработкиПродукция.ПлановаяСтоимость КАК Цена,
		|	&Валюта,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияЦен.ПоПлановымЦенам),
		|   &ЦенаВключаетНДС
		|ИЗ
		|	Документ.ПоступлениеИзПереработки.Продукция КАК ПоступлениеИзПереработкиПродукция
		|ГДЕ
		|	ПоступлениеИзПереработкиПродукция.Ссылка = &Ссылка";
	КонецЕсли;
	
	ТекстЗапроса= ТекстЗапроса
	+"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Цена,
	|	Валюта";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьСчетаУчетаРасчетов(Объект) Экспорт
	
	СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(
		Объект.Организация,  Объект.Контрагент, Объект.ДоговорКонтрагента);
		
	Объект.СчетУчетаРасчетовСКонтрагентом = СчетаУчета.СчетРасчетов;
	Объект.СчетУчетаРасчетовПоАвансам     = СчетаУчета.СчетАвансов;
	Объект.СчетУчетаРасчетовПоТаре        = СчетаУчета.СчетУчетаТары;
	
КонецПроцедуры

// Заполняет счета учета номенклатуры в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт

	ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СоответствиеСчетовУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаСпискаНоменклатуры(
		ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, "Номенклатура", Истина), ДанныеОбъекта.Склад, ДанныеОбъекта.Дата);
	
	Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
		СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти.Номенклатура);
		ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СчетаУчета);
	КонецЦикла;

КонецПроцедуры

// Заполняет табличную часть ИспользованныеМатериалы или ВозвращенныеМатериалы на основании данных табличной части Продукция.
// Процедура добавляет строки, не очищая табличную часть перед заполнением.
// Счета учета и СпособУчетаНДС не заполняются. При необходимости, об этом должен позаботиться вызывающий код.
//
// Параметры:
//  Материалы - ДокументТабличнаяЧасть.ПоступлениеИзПереработки.ИспользованныеМатериалы,
//              ДокументТабличнаяЧасть.ПоступлениеИзПереработки.ВозвращенныеМатериалы
//              - заполняемая табличная часть.
//              Допускается передавать соответствующие данные формы 
//              или таблицу значений со совпадающей структурой.
//  Продукция - ТаблицаЗначений - структура таблицы совпадает со структурой одноименной табличной части
// 
Процедура ЗаполнитьМатериалыПоПродукции(Материалы, Продукция) Экспорт
	
	// Получим данные о сырье для заполнения табличной части
	ЭлементыТекстаЗапроса = Новый Массив;
	
	// Исходные данные
	ЭлементыТекстаЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Продукция.НомерСтроки КАК НомерСтрокиВыпуск,
		|	Продукция.Номенклатура КАК НоменклатураПродукции,
		|	Продукция.Спецификация КАК Спецификация,
		|	Продукция.Количество КАК КоличествоПродукции
		|ПОМЕСТИТЬ Выпуск
		|ИЗ
		|	&Продукция КАК Продукция
		|ГДЕ
		|	Продукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Спецификация");
	
	// Получаем данные о расходе сырья
	ЭлементыТекстаЗапроса.Добавить(УправлениеПроизводством.ТекстЗапросаВременнаяТаблицаЗатратыСырья());
	
	// Преобразуем в формат получателя
	ЭлементыТекстаЗапроса.Добавить(
		"ВЫБРАТЬ
		|	МИНИМУМ(ВЫБОР
		|		КОГДА &ПрямыеРасходыУчитываютсяПоПродукции ТОГДА
		|			ЗатратыСырья.НомерСтрокиВыпуск
		|		ИНАЧЕ 
		|			0
		|	КОНЕЦ) КАК НомерСтрокиВыпуск,
		|	МАКСИМУМ(ЗатратыСырья.НомерСтрокиСпецификации) КАК НомерСтрокиСпецификации,
		|	ВЫБОР
		|		КОГДА &ПрямыеРасходыУчитываютсяПоПродукции ТОГДА
		|			ЗатратыСырья.НоменклатураПродукции
		|		ИНАЧЕ 
		|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	КОНЕЦ КАК Продукция,
		|	ЗатратыСырья.Номенклатура КАК Номенклатура,
		|	ЗатратыСырья.Номенклатура.Наименование КАК НоменклатураПредставление,
		|	ЗатратыСырья.СтатьяЗатрат КАК СтатьяЗатрат,
		|	СУММА(ЗатратыСырья.Количество) КАК Количество,
		|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК ОтражениеВУСН
		|ИЗ
		|	ЗатратыСырья КАК ЗатратыСырья
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗатратыСырья.Номенклатура,
		|	ЗатратыСырья.Номенклатура.Наименование,
		|	ВЫБОР
		|		КОГДА &ПрямыеРасходыУчитываютсяПоПродукции ТОГДА
		|			ЗатратыСырья.НоменклатураПродукции
		|		ИНАЧЕ 
		|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	КОНЕЦ,
		|	ЗатратыСырья.ЕдиницаИзмерения,
		|	ЗатратыСырья.СтатьяЗатрат
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтрокиВыпуск,
		|	НомерСтрокиСпецификации,
		|	НоменклатураПредставление");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Продукция", Продукция);
	Запрос.УстановитьПараметр("ПрямыеРасходыУчитываютсяПоПродукции",
		ЗначениеЗаполнено(ПланыСчетов.Хозрасчетный.ПрямыеРасходыУчитываютсяПоПродукции()));
	Запрос.Текст = СтрСоединить(ЭлементыТекстаЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Материалы.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СведенияОНоменклатуре) Экспорт

	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОНоменклатуре.Свойство("СчетаУчета") Тогда
		// СведенияОНоменклатуре - структура сведений номенклатуры
		СчетаУчета = СведенияОНоменклатуре.СчетаУчета;
	ИначеЕсли СведенияОНоменклатуре.Свойство("СчетУчета") Тогда
		// СведенияОНоменклатуре - структура счетов учета номенклатуры
		СчетаУчета = СведенияОНоменклатуре;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ИмяТабличнойЧасти = "Продукция" Тогда
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетУчета) Тогда
			СтрокаТабличнойЧасти.Счет = СчетаУчета.СчетУчета;
		КонецЕсли;
		
	ИначеЕсли ИмяТабличнойЧасти = "Услуги" Тогда
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетУчетаНДС) Тогда
			СтрокаТабличнойЧасти.СчетУчетаНДС = СчетаУчета.СчетУчетаНДС;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетаУчета.СпособУчетаНДС) Тогда
			СтрокаТабличнойЧасти.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
		КонецЕсли;
		
	ИначеЕсли ИмяТабличнойЧасти = "ИспользованныеМатериалы" Тогда
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетПередачи) Тогда
			СтрокаТабличнойЧасти.СчетУчета =
				БухгалтерскийУчет.СчетУчетаМатериалыПереданныеВПереработку(СчетаУчета.СчетПередачи);
		КонецЕсли;
		
	ИначеЕсли ИмяТабличнойЧасти = "ВозвращенныеМатериалы" Тогда
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетПередачи) Тогда
			СтрокаТабличнойЧасти.СчетУчета =
				БухгалтерскийУчет.СчетУчетаМатериалыПереданныеВПереработку(СчетаУчета.СчетПередачи);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетУчета) Тогда
			СтрокаТабличнойЧасти.СчетПередачи = СчетаУчета.СчетУчета;
		КонецЕсли;
		
	ИначеЕсли ИмяТабличнойЧасти = "ВозвратнаяТара" Тогда
		
		Если ЗначениеЗаполнено(СчетаУчета.СчетУчета) Тогда
			СтрокаТабличнойЧасти.СчетУчета = СчетаУчета.СчетУчета;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("УдалитьПредъявленСчетФактура");
	МассивРеквизитов.Добавить("УдалитьНомерВходящегоСчетаФактуры");
	МассивРеквизитов.Добавить("УдалитьДатаВходящегоСчетаФактуры");
	МассивРеквизитов.Добавить("УдалитьНДСПредъявленКВычету");
	МассивРеквизитов.Добавить("УдалитьКодСпособаПолучения");
	МассивРеквизитов.Добавить("УдалитьКодВидаОперации");
	МассивРеквизитов.Добавить("УдалитьУчитыватьНДС");
	МассивРеквизитов.Добавить("УдалитьСделка");
	
	Возврат МассивРеквизитов;
	
КонецФункции

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ, ДоговорДляОтложенногоПроведения = Неопределено) Экспорт

	ПараметрыПроведения = Новый Структура;

	ЭтоОтложенноеПроведение = ЗначениеЗаполнено(ДоговорДляОтложенногоПроведения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВидыСубконтоЗатрат.НомерСтроки = 1
	|				ТОГДА ВидыСубконтоЗатрат.ВидСубконто
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОНЕЦ) КАК ВидСубконто1,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВидыСубконтоЗатрат.НомерСтроки = 2
	|				ТОГДА ВидыСубконтоЗатрат.ВидСубконто
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОНЕЦ) КАК ВидСубконто2,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВидыСубконтоЗатрат.НомерСтроки = 3
	|				ТОГДА ВидыСубконтоЗатрат.ВидСубконто
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОНЕЦ) КАК ВидСубконто3
	|ПОМЕСТИТЬ ВТ_ВидыСубконтоЗатрат
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконтоЗатрат
	|ГДЕ
	|	ВидыСубконтоЗатрат.Ссылка В
	|			(ВЫБРАТЬ
	|				Реквизиты.СчетЗатрат
	|			ИЗ
	|				Документ.ПоступлениеИзПереработки КАК Реквизиты
	|			ГДЕ
	|				Реквизиты.Ссылка = &Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ЛОЖЬ КАК ЭлектронныеУслуги,
	|	Реквизиты.ДоговорКонтрагента.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.СчетУчетаРасчетовПоТаре КАК СчетУчетаРасчетовПоТаре,
	|	Реквизиты.СчетЗатрат КАК СчетЗатрат,
	|	ЕСТЬNULL(ВидыСубконтоЗатрат.ВидСубконто1, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)) КАК ВидСубконтоЗатрат1,
	|	ЕСТЬNULL(ВидыСубконтоЗатрат.ВидСубконто2, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)) КАК ВидСубконтоЗатрат2,
	|	ЕСТЬNULL(ВидыСубконтоЗатрат.ВидСубконто3, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)) КАК ВидСубконтоЗатрат3,
	|	Реквизиты.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	Реквизиты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.Склад.ТипСклада КАК ТипСклада
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыСубконтоЗатрат КАК ВидыСубконтоЗатрат
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	Реквизиты.ЭлектронныеУслуги КАК ЭлектронныеУслуги,
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Запрос.Выполнить());
	
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	ИспользоватьПлановуюСебестоимость = УчетнаяПолитика.ПлановаяСебестоимость(Реквизиты.Организация, Реквизиты.Период);	
	
	ИспользоватьСчет40 = (УчетнаяПолитика.СпособУчетаВыпускаГотовойПродукции(Реквизиты.Организация, Реквизиты.Период) =
		Перечисления.СпособыУчетаВыпускаГотовойПродукции.СИспользованиемСчета40);
	
	СпособОценкиМПЗ = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период);
	ВедетсяУчетПоПартиям = СпособОценкиМПЗ <> Перечисления.СпособыОценки.ПоСредней;
	
	// Определяем необходимость движений по регистрам учета УСН:
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период);
	
	// Коэффициенты пересчета сумм
	// - из валюты документа в валюту взаиморасчетов 
	// - из валюты взаиморасчетов в рубли
	Реквизиты.Вставить("ОрганизацияПрименяетУСН", УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период));
	Реквизиты.Вставить("НачислятьНДСПоОтгрузке",  УчетнаяПолитика.НачислятьНДСПоОтгрузке(Реквизиты.Организация, Реквизиты.Период));
	Реквизиты.Вставить("ВалютаРеглУчета",         ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Реквизиты.Вставить("ЭтоОтложенноеПроведение", ЭтоОтложенноеПроведение);
	
	Если Реквизиты.ВалютаВзаиморасчетов = Реквизиты.ВалютаРеглУчета Тогда
		Реквизиты.Вставить("КурсДокумента", 1);
		Реквизиты.Вставить("КратностьДокумента", 1);
		КоэффициентРуб = 1;
	Иначе
		Реквизиты.Вставить("КурсДокумента", Реквизиты.КурсВзаиморасчетов);
		Реквизиты.Вставить("КратностьДокумента", Реквизиты.КратностьВзаиморасчетов);
		КоэффициентРуб = Реквизиты.КурсВзаиморасчетов / Реквизиты.КратностьВзаиморасчетов;
	КонецЕсли;
	
	Если Реквизиты.ВалютаВзаиморасчетов = Реквизиты.ВалютаДокумента Тогда
		Реквизиты.Вставить("КурсВзаиморасчетов", 1);
		Реквизиты.Вставить("КратностьВзаиморасчетов", 1);
		КоэффициентВзаиморасчетов = 1;
	Иначе
		Реквизиты.Вставить("КурсВзаиморасчетов", Реквизиты.КурсВзаиморасчетов); 
		Реквизиты.Вставить("КратностьВзаиморасчетов", Реквизиты.КратностьВзаиморасчетов);
		КоэффициентВзаиморасчетов = (Реквизиты.КурсВзаиморасчетов / Реквизиты.КратностьВзаиморасчетов) / КоэффициентРуб;
	КонецЕсли;

	Запрос.УстановитьПараметр("КоэффициентВзаиморасчетов",      КоэффициентВзаиморасчетов);
	Запрос.УстановитьПараметр("КоэффициентРуб",                 КоэффициентРуб);
	Запрос.УстановитьПараметр("ВалютаРеглУчета",                Реквизиты.ВалютаРеглУчета);
	Запрос.УстановитьПараметр("СинонимПродукция",               НСтр("ru = 'Продукция'"));
	Запрос.УстановитьПараметр("ИспользоватьПлановуюСебестоимость", 
																ИспользоватьПлановуюСебестоимость);
	Запрос.УстановитьПараметр("ИспользоватьСчет40",             ИспользоватьСчет40);
	Запрос.УстановитьПараметр("ВедетсяУчетПоПартиям",           ВедетсяУчетПоПартиям);
	Запрос.УстановитьПараметр("УчестьРасходыУСНУслуги",         ПрименяетсяУСНДоходыМинусРасходы);
	Запрос.УстановитьПараметр("УчетАгентскогоНДС",              Реквизиты.УчетАгентскогоНДС);
	Запрос.УстановитьПараметр("СинонимИспользованныеМатериалы", НСтр("ru = 'Использованные материалы'"));
	Запрос.УстановитьПараметр("СинонимВозвращенныеМатериалы",   НСтр("ru = 'Возвращенные материалы'"));
	Запрос.УстановитьПараметр("СинонимУслуги",					НСтр("ru = 'Услуги'"));
	
	Запрос.УстановитьПараметр("ВестиУчетПоВидамДеятельностиИП",	УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",	УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПустоеПодразделение", 			БухгалтерскийУчетПереопределяемый.ПустоеПодразделение());
	
	ПодготовитьТаблицыДокументаДляЦелейПриобретенияРеализации(Запрос, Реквизиты);

	НомераТаблиц = Новый Структура;

	Запрос.Текст = ТекстЗапросаТаблицыДокумента(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПлановаяСтоимостьПродукции(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаВыпускПродукции(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗачетАвансов(НомераТаблиц)
		+ ТекстЗапросаПоступлениеУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаОценкаСтоимости(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаСписаниеМатериалов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаВозвратМатериалов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеТары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПереоценкаВалютныхОстатков(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеМПЗИП(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

Процедура ПодготовитьТаблицыДокументаДляЦелейПриобретенияРеализации(Запрос, Реквизиты)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Услуги.Ссылка,
	|	Услуги.НомерСтроки,
	|	Услуги.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Услуги.Номенклатура,
	|	Услуги.Количество,
	|	Услуги.Цена,
	|	Услуги.Сумма,
	|	Услуги.СтавкаНДС,
	|	Услуги.СуммаНДС КАК НДС,
	|	Услуги.СтатьяЗатрат,
	|	Услуги.СчетУчетаНДС,
	|	Услуги.СпособУчетаНДС,
	|	Услуги.Содержание,
	|	Услуги.Ссылка.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Услуги.Ссылка.СчетЗатрат КАК СчетЗатрат,
	|	Услуги.ОтражениеВУСН
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Услуги КАК Услуги
	|ГДЕ
	|	Услуги.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Услуги = РезультатЗапроса[0].Выгрузить();
	УчетВзаиморасчетов.ПодготовкаТаблицыЗначенийДляЦелейПриобретенияИРеализации(Услуги, Реквизиты);
	Запрос.УстановитьПараметр("Услуги",Услуги);

КонецПроцедуры

Функция ТекстЗапросаТаблицыДокумента(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	НомераТаблиц.Вставить("ВременнаяТаблицаУслуги", НомераТаблиц.Количество());
	
	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		
		ПараметрыПроведения.Вставить("ВременнаяТаблицаПродукция", Неопределено);
		ПараметрыПроведения.Вставить("ВременнаяТаблицаМатериалы", Неопределено);
		ПараметрыПроведения.Вставить("ВременнаяТаблицаВозвращенныеМатериалы", Неопределено);
		ПараметрыПроведения.Вставить("ВременнаяТаблицаТара", Неопределено);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаУслуги.Ссылка,
		|	ТаблицаУслуги.НомерСтроки,
		|	ТаблицаУслуги.Номенклатура,
		|	ТаблицаУслуги.Содержание,
		|	ТаблицаУслуги.СтавкаНДС,
		|	ТаблицаУслуги.СчетУчетаНДС,
		|	ТаблицаУслуги.СпособУчетаНДС,
		|	ТаблицаУслуги.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаУслуги.СуммаВал КАК СуммаВзаиморасчетов,
		|	ТаблицаУслуги.НДСВал КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаУслуги.Сумма КАК СуммаРуб,
		|	ТаблицаУслуги.НДС КАК СуммаНДСРуб,
		|	ТаблицаУслуги.Количество,
		|	ТаблицаУслуги.ОтражениеВУСН КАК ОтражениеВУСН,
		|	ТаблицаУслуги.СтатьяЗатрат,
		|	ТаблицаУслуги.ВалютаВзаиморасчетов,
		|	&УчестьРасходыУСНУслуги КАК УчестьРасходыУСНУслуги
		|ПОМЕСТИТЬ ТаблицаУслуги
		|ИЗ
		|	&Услуги КАК ТаблицаУслуги";
		
		Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаПродукция", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаМатериалы", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаВозвращенныеМатериалы", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаТара", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПродукция.Ссылка,
	|	ТаблицаПродукция.НомерСтроки,
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Счет КАК СчетУчета,
	|	ТаблицаПродукция.Количество,
	|	ТаблицаПродукция.СуммаПлановая
	|ПОМЕСТИТЬ ТаблицаПродукция
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Продукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаПродукция.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаУслуги.Ссылка,
	|	ТаблицаУслуги.НомерСтроки,
	|	ТаблицаУслуги.Номенклатура,
	|	ТаблицаУслуги.Содержание,
	|	ТаблицаУслуги.СтавкаНДС,
	|	ТаблицаУслуги.СчетУчетаНДС,
	|	ТаблицаУслуги.СпособУчетаНДС,
	|	ТаблицаУслуги.СчетЗатрат КАК СчетЗатрат,
	|	ТаблицаУслуги.СуммаВал КАК СуммаВзаиморасчетов,
	|	ТаблицаУслуги.НДСВал КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаУслуги.Сумма КАК СуммаРуб,
	|	ТаблицаУслуги.НДС КАК СуммаНДСРуб,
	|	ТаблицаУслуги.Количество,
	|	ТаблицаУслуги.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ТаблицаУслуги.СтатьяЗатрат,
	|	ТаблицаУслуги.ВалютаВзаиморасчетов,
	|	&УчестьРасходыУСНУслуги КАК УчестьРасходыУСНУслуги
	|ПОМЕСТИТЬ ТаблицаУслуги
	|ИЗ
	|	&Услуги КАК ТаблицаУслуги
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаМатериалы.Ссылка,
	|	ТаблицаМатериалы.НомерСтроки,
	|	ТаблицаМатериалы.Продукция,
	|	ТаблицаМатериалы.Номенклатура,
	|	ТаблицаМатериалы.Количество,
	|	ТаблицаМатериалы.СчетУчета,
	|	ТаблицаМатериалы.СтатьяЗатрат,
	|	ТаблицаМатериалы.СпособУчетаНДС
	|ПОМЕСТИТЬ ТаблицаМатериалы
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.ИспользованныеМатериалы КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВозвращенныеМатериалы.Ссылка,
	|	ТаблицаВозвращенныеМатериалы.НомерСтроки,
	|	ТаблицаВозвращенныеМатериалы.Номенклатура,
	|	ТаблицаВозвращенныеМатериалы.Количество,
	|	ТаблицаВозвращенныеМатериалы.СчетУчета,
	|	ТаблицаВозвращенныеМатериалы.СчетПередачи
	|ПОМЕСТИТЬ ТаблицаВозвращенныеМатериалы
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.ВозвращенныеМатериалы КАК ТаблицаВозвращенныеМатериалы
	|ГДЕ
	|	ТаблицаВозвращенныеМатериалы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТара.Ссылка,
	|	ТаблицаТара.НомерСтроки,
	|	ТаблицаТара.Номенклатура,
	|	ТаблицаТара.Количество,
	|	ТаблицаТара.Сумма,
	|	ВЫРАЗИТЬ(ТаблицаТара.Сумма * &КоэффициентВзаиморасчетов КАК ЧИСЛО(15, 2)) КАК СуммаВзаиморасчетов,
	|	ВЫРАЗИТЬ((ВЫРАЗИТЬ(ТаблицаТара.Сумма * &КоэффициентВзаиморасчетов КАК ЧИСЛО(15, 2))) * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб,
	|	ТаблицаТара.СчетУчета
	|ПОМЕСТИТЬ ТаблицаТара
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.ВозвратнаяТара КАК ТаблицаТара
	|ГДЕ
	|	ТаблицаТара.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПлановаяСтоимостьПродукции(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ПлановаяСтоимостьПродукцииРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПлановаяСтоимостьПродукцииТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПлановаяСтоимостьПродукцииРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПлановаяСтоимостьПродукцииТаблица"  , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ИспользоватьСчет40 КАК ИспользоватьСчет40
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Продукция"" КАК ИмяСписка,
	|	&СинонимПродукция КАК СинонимСписка,
	|	ТаблицаПродукция.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСчет40
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыпускПродукции)
	|		ИНАЧЕ ТаблицаПродукция.Ссылка.СчетЗатрат
	|	КОНЕЦ КАК СчетЗатрат,
	|	ТаблицаПродукция.Ссылка.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	ТаблицаПродукция.Ссылка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаЗатрат,
	|	ТаблицаПродукция.СчетУчета КАК СчетУчета,
	|	ТаблицаПродукция.Ссылка.ПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаПродукция.Ссылка.Склад КАК Склад,
	|	ТаблицаПродукция.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПлановуюСебестоимость 
	|			ТОГДА ТаблицаПродукция.СуммаПлановая
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаПлановая,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПлановуюСебестоимость 
	|			ТОГДА ТаблицаПродукция.СуммаПлановая
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаПлановаяНУ
	|ИЗ
	|	ТаблицаПродукция КАК ТаблицаПродукция
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаПродукция.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаВыпускПродукции(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ВыпускПродукцииРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ВыпускПродукцииТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВыпускПродукцииРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВыпускПродукцииТаблица"  , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Продукция"" КАК ИмяСписка,
	|	&СинонимПродукция КАК СинонимСписка,
	|	ТаблицаПродукция.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Ссылка.СчетЗатрат КАК СчетЗатрат,
	|	ТаблицаПродукция.Ссылка.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	ТаблицаПродукция.Ссылка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаЗатрат,
	|	ТаблицаПродукция.СчетУчета КАК СчетСписания,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Хозрасчетный.УчетПоПодразделениям, ЛОЖЬ)
	|			ТОГДА ТаблицаПродукция.Ссылка.ПодразделениеОрганизации
	|		ИНАЧЕ &ПустоеПодразделение
	|	КОНЕЦ КАК ПодразделениеСписания,
	|	ВЫБОР
	|		КОГДА ВидыСубконто1.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|			ТОГДА ТаблицаПродукция.Номенклатура
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВидыСубконто1.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады)
	|					ТОГДА ТаблицаПродукция.Ссылка.Склад
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВидыСубконто1.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии)
	|								И &ВедетсяУчетПоПартиям
	|							ТОГДА ТаблицаПродукция.Ссылка
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоСписания1,
	|	ВЫБОР
	|		КОГДА ВидыСубконто2.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|			ТОГДА ТаблицаПродукция.Номенклатура
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВидыСубконто2.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады)
	|					ТОГДА ТаблицаПродукция.Ссылка.Склад
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВидыСубконто2.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии)
	|								И &ВедетсяУчетПоПартиям
	|							ТОГДА ТаблицаПродукция.Ссылка
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоСписания2,
	|	ВЫБОР
	|		КОГДА ВидыСубконто3.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|			ТОГДА ТаблицаПродукция.Номенклатура
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВидыСубконто3.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады)
	|					ТОГДА ТаблицаПродукция.Ссылка.Склад
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВидыСубконто3.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии)
	|								И &ВедетсяУчетПоПартиям
	|							ТОГДА ТаблицаПродукция.Ссылка
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоСписания3,
	|	ТаблицаПродукция.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПлановуюСебестоимость 
	|			ТОГДА ТаблицаПродукция.СуммаПлановая
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПлановаяСтоимость,
	|	ЛОЖЬ КАК ПрямыеРасходыРаспределятьПоКоличеству
	|ИЗ
	|	ТаблицаПродукция КАК ТаблицаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто1
	|		ПО ТаблицаПродукция.СчетУчета = ВидыСубконто1.Ссылка
	|			И (ВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто2
	|		ПО ТаблицаПродукция.СчетУчета = ВидыСубконто2.Ссылка
	|			И (ВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто3
	|		ПО ТаблицаПродукция.СчетУчета = ВидыСубконто3.Ссылка
	|			И (ВидыСубконто3.НомерСтроки = 3)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО ТаблицаПродукция.СчетУчета = Хозрасчетный.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаПродукция.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаЗачетАвансов(НомераТаблиц)

	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаАвансов",   НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	""Поступление из переработки"" КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСН,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСНПатент,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	ЛОЖЬ КАК ДеятельностьНаТорговомСборе,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	&УчетАгентскогоНДС КАК УчетАгентскогоНДС
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетов,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	ТаблицаУслуги.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	СУММА(ТаблицаУслуги.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаУслуги.СуммаРуб) КАК СуммаРуб,
	|	0 КАК СуммаВзаиморасчетовКомитента,
	|	0 КАК СуммаВзаиморасчетовЕНВД,
	|	0 КАК СуммаВзаиморасчетовПатент,
	|	0 КАК СуммаВзаиморасчетовТорговыйСбор
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.Ссылка,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора,
	|	ТаблицаУслуги.ВалютаВзаиморасчетов,
	|	Реквизиты.РасчетыВУсловныхЕдиницах,
	|	Реквизиты.УчетАгентскогоНДС,
	|	Реквизиты.ПодразделениеОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗачетАвансов.НомерСтроки,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	ТаблицаЗачетАвансов.ДокументАванса,
	|	ТаблицаЗачетАвансов.СуммаЗачета
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.ЗачетАвансов КАК ТаблицаЗачетАвансов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаЗачетАвансов.Ссылка = &Ссылка
	|	И Реквизиты.СпособЗачетаАвансов = ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.ПоДокументу)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаЗачетАвансов.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаПоступлениеУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ПоступлениеУслугРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПоступлениеУслугТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеУслугРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеУслугТаблица"  , НомераТаблиц.Количество());
	
	// Для счетов Дт порядок субконто совпадает с порядком следования субконто на счете.
	// Для счетов Кт виды субконто передаются явно.
	// Такой интерфейс предусмотрен в СформироватьДвиженияПоступлениеУслуг().
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Услуги"" КАК ИмяСписка,
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
	|	ТаблицаУслуги.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаУслуги.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаУслуги.СуммаРуб КАК СуммаБУ,
	|	ТаблицаУслуги.СуммаРуб КАК СуммаНУ,
	|	ТаблицаУслуги.СуммаРуб КАК СуммаРуб,
	|	ТаблицаУслуги.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаУслуги.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ТаблицаУслуги.СпособУчетаНДС КАК СпособУчетаНДС,
	|	Реквизиты.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	ТаблицаУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|			ТОГДА ТаблицаУслуги.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|	КОНЕЦ КАК Продукция,
	|	Реквизиты.СчетЗатрат КАК СчетЗатрат,
	|	Реквизиты.СчетЗатрат КАК СчетЗатратНУ,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСубконтоЗатрат1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы)
	|			ТОГДА Реквизиты.НоменклатурнаяГруппа
	|		КОГДА Реквизиты.ВидСубконтоЗатрат1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ТаблицаУслуги.СтатьяЗатрат
	|		КОГДА Реквизиты.ВидСубконтоЗатрат1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|						ТОГДА ТаблицаУслуги.Номенклатура
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСубконтоЗатрат2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы)
	|			ТОГДА Реквизиты.НоменклатурнаяГруппа
	|		КОГДА Реквизиты.ВидСубконтоЗатрат2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ТаблицаУслуги.СтатьяЗатрат
	|		КОГДА Реквизиты.ВидСубконтоЗатрат2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|						ТОГДА ТаблицаУслуги.Номенклатура
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСубконтоЗатрат3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы)
	|			ТОГДА Реквизиты.НоменклатурнаяГруппа
	|		КОГДА Реквизиты.ВидСубконтоЗатрат3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ТаблицаУслуги.СтатьяЗатрат
	|		КОГДА Реквизиты.ВидСубконтоЗатрат3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|						ТОГДА ТаблицаУслуги.Номенклатура
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто3,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСубконтоЗатрат1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы)
	|			ТОГДА Реквизиты.НоменклатурнаяГруппа
	|		КОГДА Реквизиты.ВидСубконтоЗатрат1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ТаблицаУслуги.СтатьяЗатрат
	|		КОГДА Реквизиты.ВидСубконтоЗатрат1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|						ТОГДА ТаблицаУслуги.Номенклатура
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоНУ1,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСубконтоЗатрат2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы)
	|			ТОГДА Реквизиты.НоменклатурнаяГруппа
	|		КОГДА Реквизиты.ВидСубконтоЗатрат2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ТаблицаУслуги.СтатьяЗатрат
	|		КОГДА Реквизиты.ВидСубконтоЗатрат2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|						ТОГДА ТаблицаУслуги.Номенклатура
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоНУ2,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидСубконтоЗатрат3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы)
	|			ТОГДА Реквизиты.НоменклатурнаяГруппа
	|		КОГДА Реквизиты.ВидСубконтоЗатрат3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ТаблицаУслуги.СтатьяЗатрат
	|		КОГДА Реквизиты.ВидСубконтоЗатрат3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаУслуги.Номенклатура.Услуга
	|						ТОГДА ТаблицаУслуги.Номенклатура
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоНУ3,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
	|	""Контрагенты"" КАК ВидКорСубконто1,
	|	""Договоры"" КАК ВидКорСубконто2,
	|	""ДокументыРасчетовСКонтрагентами"" КАК ВидКорСубконто3,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	ТаблицаУслуги.Ссылка КАК КорСубконто3,
	|	ТаблицаУслуги.Содержание КАК Содержание
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаУслуги.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("РеквизитыНДС", Неопределено);
		ПараметрыПроведения.Вставить("ПродукцияНДС", Неопределено);
		ПараметрыПроведения.Вставить("ИспользованныеМатериалыНДС", Неопределено);
		ПараметрыПроведения.Вставить("ВозвращенныеМатериалыНДС", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РеквизитыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПродукцияНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ИспользованныеМатериалыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВозвращенныеМатериалыНДС",   НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеПолучатель,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	Реквизиты.ТипСклада КАК ТипСкладаОтправителя,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	Реквизиты.ЭлектронныеУслуги КАК ЭлектронныеУслуги,
	|	Реквизиты.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	НЕОПРЕДЕЛЕНО КАК НДСвСтоимостиТоваров,
	|	НЕОПРЕДЕЛЕНО КАК СчетСписанияНДС,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоСписанияНДС1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоСписанияНДС2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоСписанияНДС3,
	|	ИСТИНА КАК СписыватьНДСнаКорСчетИАналитикуТовара,
	|	Реквизиты.Склад КАК СкладПолучатель,
	|	Реквизиты.ТипСклада КАК ТипСкладаПолучателя
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродукция.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.СчетУчета КАК СчетУчета,
	|	ТаблицаПродукция.Количество КАК Количество
	|ИЗ
	|	ТаблицаПродукция КАК ТаблицаПродукция
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ИспользованныеМатериалы"" КАК ИмяСписка,
	|	&СинонимИспользованныеМатериалы КАК СинонимСписка,
	|	ТаблицаМатериалы.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалы.СчетУчета КАК СчетУчета,
	|	ТаблицаМатериалы.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка) КАК СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаМатериалы.СпособУчетаНДС КАК НовыйСпособУчетаНДС
	|ИЗ
	|	ТаблицаМатериалы КАК ТаблицаМатериалы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаМатериалы.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ВозвращенныеМатериалы"" КАК ИмяСписка,
	|	ТаблицаВозвращенныеМатериалы.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаВозвращенныеМатериалы.Номенклатура КАК Номенклатура,
	|	ТаблицаВозвращенныеМатериалы.СчетУчета КАК СчетУчета,
	|	ТаблицаВозвращенныеМатериалы.Количество КАК Количество,
	|	ТаблицаВозвращенныеМатериалы.СчетПередачи КАК СчетУчетаПолучатель,
	|	Реквизиты.Склад КАК СкладПолучатель,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПустаяСсылка) КАК НовыйСпособУчетаНДС
	|ИЗ
	|	ТаблицаВозвращенныеМатериалы КАК ТаблицаВозвращенныеМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВозвращенныеМатериалы.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаОценкаСтоимости(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ОценкаСтоимостиРеквизиты", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ОценкаСтоимостиРеквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	"""" КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСписаниеМатериалов(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("СписаниеМатериаловРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("СписаниеМатериаловТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("СписаниеМатериаловРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеМатериаловТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	""Списание"" КАК ТипСписания,
	|	Реквизиты.Ссылка КАК ДокументРеализации,
	|	""ПоступлениеИзПереработки"" КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""Списание запасов на затраты производства"" КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ИспользованныеМатериалы"" КАК ИмяСписка,
	|	&СинонимИспользованныеМатериалы КАК СинонимСписка,
	|	Реквизиты.Период КАК Период,
	|	ТаблицаМатериалы.НомерСтроки КАК НомерСтроки,
	|	ТаблицаМатериалы.СчетУчета КАК СчетУчета,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	0 КАК Себестоимость,
	|	ТаблицаМатериалы.Количество КАК Количество,
	|	Реквизиты.ПодразделениеЗатрат КАК КорПодразделение,
	|	Реквизиты.СчетЗатрат КАК КорСчетСписания,
	|	Реквизиты.НоменклатурнаяГруппа КАК КорСубконто1,
	|	ТаблицаМатериалы.СтатьяЗатрат КАК КорСубконто2,
	|	ТаблицаМатериалы.Продукция КАК КорСубконто3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция) КАК ВидКорСубконто3,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетАвансовСКомитентом,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом
	|ИЗ
	|	ТаблицаМатериалы КАК ТаблицаМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВозвратМатериалов(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ВозвратМатериаловРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ВозвратМатериаловТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВозвратМатериаловРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВозвратМатериаловТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""Возврат материалов из переработки на стороне"" КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ВозвращенныеМатериалы"" КАК ИмяСписка,
	|	&СинонимВозвращенныеМатериалы КАК СинонимСписка,
	|	Реквизиты.Период КАК Период,
	|	ТаблицаВозвращенныеМатериалы.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВозвращенныеМатериалы.СчетУчета,
	|	ТаблицаВозвращенныеМатериалы.Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	0 КАК Себестоимость,
	|	ТаблицаВозвращенныеМатериалы.Количество,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	ТаблицаВозвращенныеМатериалы.СчетПередачи КАК КорСчетСписания,
	|	ТаблицаВозвращенныеМатериалы.Номенклатура КАК КорСубконто1,
	|	Реквизиты.Склад КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады) КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетАвансовСКомитентом,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом
	|ИЗ
	|	ТаблицаВозвращенныеМатериалы КАК ТаблицаВозвращенныеМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеТары(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ПоступлениеТарыРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПоступлениеТарыТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеТарыРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеТарыТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Склад,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.СчетУчетаРасчетовПоТаре,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ВозвратнаяТара"" КАК ИмяСписка,
	|	ТаблицаТара.Ссылка,
	|	ТаблицаТара.НомерСтроки,
	|	ТаблицаТара.Номенклатура,
	|	ТаблицаТара.СуммаВзаиморасчетов,
	|	ТаблицаТара.СуммаРуб,
	|	ТаблицаТара.Количество,
	|	ТаблицаТара.СчетУчета,
	|	""Передача тары"" КАК Содержание
	|ИЗ
	|	ТаблицаТара КАК ТаблицаТара
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТара.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПереоценкаВалютныхОстатков(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ПереоценкаВалютныхОстатковРеквизиты", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПереоценкаВалютныхОстатковРеквизиты", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ВалютаДокумента
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ПоступлениеРасходовУСНРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПоступлениеРасходовУСНТаблицаРасходов", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеРасходовУСНРеквизиты",       НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеРасходовУСНТаблицаРасходов", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК Валюта,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ЛОЖЬ КАК РасходыПредпринимателя
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Услуги) КАК ВидРасхода,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
	|	ТаблицаУслуги.Номенклатура КАК ЭлементРасхода,
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоМатериал,
	|	ТаблицаУслуги.СчетЗатрат КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК Партия,
	|	ТаблицаУслуги.Количество КАК Количество,
	|	ТаблицаУслуги.СуммаВзаиморасчетов КАК Сумма,
	|	ТаблицаУслуги.СуммаНДСВзаиморасчетов КАК НДС,
	|	ТаблицаУслуги.ОтражениеВУСН КАК ОтражениеВУСН
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|ГДЕ
	|	ТаблицаУслуги.УчестьРасходыУСНУслуги";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПоступлениеМПЗИП(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("ПоступлениеМПЗИПРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПоступлениеМПЗИПТаблицаУслуг", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеМПЗИПРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоступлениеМПЗИПТаблицаУслуг", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""ПоступлениеИзПереработки"" КАК ТипПоступления,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	""ПоступлениеИзПереработки"" КАК ВидОперации
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Услуги"" КАК ИмяСписка,
	|	&СинонимУслуги КАК СинонимСписка,
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
	|	ТаблицаУслуги.СуммаРуб КАК Сумма,
	|	ТаблицаУслуги.СуммаНДСРуб КАК НДС,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Количество <> 0
	|			ТОГДА ТаблицаУслуги.Количество
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество,
	|	Реквизиты.СчетЗатрат КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ Реквизиты.НоменклатурнаяГруппа
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ТаблицаУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ТаблицаУслуги.СтатьяЗатрат.ВидРасходовНУ КАК ВидРасходовНУ,
	|	ИСТИНА КАК ПринятиеКналоговомуУчету,
	|	ТаблицаУслуги.СтатьяЗатрат.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК Партия
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаУслуги.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПодготовитьТаблицыМатериаловПродукцииИП(ТаблицаСписанныеМПЗ, ТаблицаВозвращенныеМатериалы, ВыпускПродукцииТаблица, ТаблицаРеквизиты, Движения) Экспорт
	Перем СтруктураТаблиц;
	
	СтруктураТаблиц	= Новый Структура("ТаблицаМатериалов, ПолученоПродукции, ПолученныеУслуги, УслугиОтнесенныеНаИМР");
	
	Реквизиты	= ТаблицаРеквизиты[0];
	
	Если НЕ УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	ИспользоватьПлановуюСебестоимость = УчетнаяПолитика.ПлановаяСебестоимость(Реквизиты.Организация, Реквизиты.Период);
	
	ТипНоменклатурнойГруппы	= Тип("СправочникСсылка.НоменклатурныеГруппы");
	
	МассивТиповЗатрат = Новый Массив;
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.СтатьиЗатрат"));
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.РасходыБудущихПериодов"));
	
	ВремТаблицаСписанныеМПЗ	= ТаблицаСписанныеМПЗ.Скопировать();
	ВремТаблицаСписанныеМПЗ.Колонки.Добавить("СчетЗатрат",				Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ВремТаблицаСписанныеМПЗ.Колонки.Добавить("НоменклатурнаяГруппа",	Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ВремТаблицаСписанныеМПЗ.Колонки.Добавить("СтатьяЗатрат",			Новый ОписаниеТипов(МассивТиповЗатрат));
	
	ВремТаблицаСписанныеМПЗ.ЗагрузитьКолонку(ВремТаблицаСписанныеМПЗ.ВыгрузитьКолонку("КорСчетСписания"),	"СчетЗатрат");
	ВремТаблицаСписанныеМПЗ.ЗагрузитьКолонку(ВремТаблицаСписанныеМПЗ.ВыгрузитьКолонку("КорСубконто1"),		"НоменклатурнаяГруппа");
	ВремТаблицаСписанныеМПЗ.ЗагрузитьКолонку(ВремТаблицаСписанныеМПЗ.ВыгрузитьКолонку("КорСубконто2"),		"СтатьяЗатрат");
	
	// Подготовка таблиц для распределения материалов и услуг на готовую продукцию.
	ТаблицаМатериалов	= Новый ТаблицаЗначений;
	ТаблицаМатериалов.Колонки.Добавить("ИмяСписка",				ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаМатериалов.Колонки.Добавить("СинонимСписка",			ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаМатериалов.Колонки.Добавить("НомерСтроки",			ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаМатериалов.Колонки.Добавить("Продукция",				Справочники.ТипВсеСсылки());
	ТаблицаМатериалов.Колонки.Добавить("НоменклатурнаяГруппа",	Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ТаблицаМатериалов.Колонки.Добавить("Номенклатура",			Справочники.ТипВсеСсылки());
	ТаблицаМатериалов.Колонки.Добавить("Партия",				Документы.ТипВсеСсылки());
	ТаблицаМатериалов.Колонки.Добавить("СчетУчета",				Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаМатериалов.Колонки.Добавить("СчетЗатрат",			Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаМатериалов.Колонки.Добавить("СтатьяЗатрат",			Справочники.ТипВсеСсылки());
	ТаблицаМатериалов.Колонки.Добавить("СчетДоходов",			Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаМатериалов.Колонки.Добавить("Количество",			ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаМатериалов.Колонки.Добавить("КоличествоПродукции",	ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	ПолученныеУслуги	= Новый ТаблицаЗначений;
	ПолученныеУслуги.Колонки.Добавить("НоменклатурнаяГруппа",	Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ПолученныеУслуги.Колонки.Добавить("ХарактерДеятельности",	Новый ОписаниеТипов("ПеречислениеСсылка.ХарактерДеятельности"));
	ПолученныеУслуги.Колонки.Добавить("ВидМПЗ",					Новый ОписаниеТипов("ПеречислениеСсылка.ВидыМПЗ"));
	ПолученныеУслуги.Колонки.Добавить("Номенклатура",			Справочники.ТипВсеСсылки());
	ПолученныеУслуги.Колонки.Добавить("Партия",					Документы.ТипВсеСсылки());
	ПолученныеУслуги.Колонки.Добавить("ДокументОплаты",			Документы.ТипВсеСсылки());
	ПолученныеУслуги.Колонки.Добавить("Количество",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ПолученныеУслуги.Колонки.Добавить("Сумма",					ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ПолученныеУслуги.Колонки.Добавить("НДС",					ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ПолученныеУслуги.Колонки.Добавить("СчетУчетаПродукции",		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПолученныеУслуги.Колонки.Добавить("Продукция",				Справочники.ТипВсеСсылки());
	
	УслугиОтнесенныеНаИМР	= Новый ТаблицаЗначений;
	УслугиОтнесенныеНаИМР.Колонки.Добавить("НоменклатурнаяГруппа",	Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	УслугиОтнесенныеНаИМР.Колонки.Добавить("ХарактерДеятельности",	Новый ОписаниеТипов("ПеречислениеСсылка.ХарактерДеятельности"));
	УслугиОтнесенныеНаИМР.Колонки.Добавить("СтатьяЗатрат",			Справочники.ТипВсеСсылки());
	УслугиОтнесенныеНаИМР.Колонки.Добавить("Партия",				Документы.ТипВсеСсылки());
	УслугиОтнесенныеНаИМР.Колонки.Добавить("ДокументОплаты",		Документы.ТипВсеСсылки());
	УслугиОтнесенныеНаИМР.Колонки.Добавить("МПЗ",					Справочники.ТипВсеСсылки());
	УслугиОтнесенныеНаИМР.Колонки.Добавить("ПартияМПЗ",				Документы.ТипВсеСсылки());
	УслугиОтнесенныеНаИМР.Колонки.Добавить("Количество",			ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	УслугиОтнесенныеНаИМР.Колонки.Добавить("Сумма",					ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	УслугиОтнесенныеНаИМР.Колонки.Добавить("НДС",					ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	ПолученоПродукции =Новый ТаблицаЗначений;
	ПолученоПродукции.Колонки.Добавить("СчетЗатрат",			Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПолученоПродукции.Колонки.Добавить("НоменклатурнаяГруппа",	Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ПолученоПродукции.Колонки.Добавить("Продукция",				Справочники.ТипВсеСсылки());
	ПолученоПродукции.Колонки.Добавить("СчетУчетаПродукции",	Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПолученоПродукции.Колонки.Добавить("КоличествоПродукции",	ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ПолученоПродукции.Колонки.Добавить("ПлановаяСтоимость",		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	КолонкаБазаРаспределения = ?(ИспользоватьПлановуюСебестоимость, "ПлановаяСтоимость", "Количество");
	
	// Заполняем таблицу "ПолученоПродукции"
	Для Каждого Продукция Из ВыпускПродукцииТаблица Цикл
		
		Если Продукция[КолонкаБазаРаспределения] = 0 Тогда
			Продолжить;
		КонецЕсли;
				
		НоваяСтрока	= ПолученоПродукции.Добавить();
		НоваяСтрока.СчетЗатрат				= Продукция.СчетЗатрат;
		НоваяСтрока.НоменклатурнаяГруппа	= Продукция.НоменклатурнаяГруппаЗатрат;
		НоваяСтрока.Продукция				= Продукция.Номенклатура;
		НоваяСтрока.СчетУчетаПродукции		= Продукция.СчетСписания;
		НоваяСтрока.КоличествоПродукции		= Продукция.Количество;
		НоваяСтрока.ПлановаяСтоимость		= Продукция.ПлановаяСтоимость;
		
	КонецЦикла;
	
	ПолученоПродукции.Свернуть("СчетЗатрат, НоменклатурнаяГруппа, Продукция, СчетУчетаПродукции", "КоличествоПродукции, ПлановаяСтоимость");
	
	КолонкаБазаРаспределения = ?(ИспользоватьПлановуюСебестоимость, "ПлановаяСтоимость", "КоличествоПродукции");
	
	// Распределение материалов по готовой продукции.
	КешБазыРаспределения = Новый Соответствие;
		
	Для Каждого Материал Из ВремТаблицаСписанныеМПЗ Цикл
		
		Если Материал.Количество = 0 Тогда
			// Распределение невозможно
			Продолжить;
		КонецЕсли;
		
		// Определим продукцию, на которую потрачен материал
		МатериалПродукция = УчетТоваров.ЗначениеКорСубконто(Материал, ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция);
		
		МассивКоэффициентов = КоэффициентыРаспределенияНаПродукцию(ПолученоПродукции, МатериалПродукция, КешБазыРаспределения, КолонкаБазаРаспределения);
		
		Если МассивКоэффициентов.Количество() > 0 Тогда
			
			МассивКоличество = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(Материал.Количество, МассивКоэффициентов, 3);
			
			Для Индекс = 0 По МассивКоэффициентов.ВГраница() Цикл
				
				Если МассивКоличество[Индекс] = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				МатериалПоПродукции = ТаблицаМатериалов.Добавить();
				ЗаполнитьЗначенияСвойств(МатериалПоПродукции, Материал);
				ЗаполнитьЗначенияСвойств(МатериалПоПродукции, ПолученоПродукции[Индекс]);
				
				МатериалПоПродукции.СчетЗатрат	= ПолученоПродукции[Индекс].СчетУчетаПродукции;
				МатериалПоПродукции.Количество	= МассивКоличество[Индекс];
				
			КонецЦикла;
			
		Иначе
			
			МатериалПоПродукции	= ТаблицаМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(МатериалПоПродукции, Материал);
			МатериалПоПродукции.Продукция	= Материал.СтатьяЗатрат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Распределение услуг по готовой продукции.
	Для Каждого Услуга Из Движения.ИПМПЗ Цикл
		
		Если Услуга.Количество = 0 Тогда
			// Распределение невозможно
			Продолжить;
		КонецЕсли;
		
		МассивКоэффициентов = КоэффициентыРаспределенияНаПродукцию(ПолученоПродукции, Услуга.Номенклатура, КешБазыРаспределения, КолонкаБазаРаспределения);
		
		Если МассивКоэффициентов.Количество() > 0 Тогда
			
			МассивКоличество	= ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(Услуга.Количество, МассивКоэффициентов, 3);
			МассивСумма			= ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(Услуга.Сумма, МассивКоэффициентов, 2);
			МассивНДС			= ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(Услуга.НДС, МассивКоэффициентов, 2);
			
			Для Индекс = 0 По МассивКоэффициентов.ВГраница() Цикл
				
				Если МассивКоличество[Индекс] = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				УслугаПоПродукции	= ПолученныеУслуги.Добавить();
				ЗаполнитьЗначенияСвойств(УслугаПоПродукции, Услуга);
				ЗаполнитьЗначенияСвойств(УслугаПоПродукции, ПолученоПродукции[Индекс]);
				
				УслугаПоПродукции.Количество	= МассивКоличество[Индекс];
				УслугаПоПродукции.Сумма			= ?(МассивСумма <> Неопределено, МассивСумма[Индекс], 0);
				УслугаПоПродукции.НДС			= ?(МассивНДС <> Неопределено, МассивНДС[Индекс], 0);
				
			КонецЦикла;
			
		Иначе
			
			// отнесение услуг на ИМР
			ИМР	= УслугиОтнесенныеНаИМР.Добавить();
			ЗаполнитьЗначенияСвойств(ИМР, Услуга);
			ИМР.СтатьяЗатрат = Услуга.Номенклатура;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// После распределения полученных услуг по готовой продукции движения регистра ИПМПЗ можно очистить,
	// чтобы не пришлось их лишний раз сторнировать из-за возможного несовпадения характера деятельности.
	Движения.ИПМПЗ.Очистить();
	
	ТаблицаМатериалов	= УчетДоходовИРасходовПредпринимателя.ДополнитьТаблицуСписанияМатериалов(ТаблицаМатериалов, ТаблицаРеквизиты);
	
	СтруктураТаблиц.ТаблицаМатериалов		= ТаблицаМатериалов;
	СтруктураТаблиц.ПолученоПродукции		= ПолученоПродукции;
	СтруктураТаблиц.ПолученныеУслуги		= ПолученныеУслуги;
	СтруктураТаблиц.УслугиОтнесенныеНаИМР	= УслугиОтнесенныеНаИМР;
	
	Возврат СтруктураТаблиц;
	
КонецФункции

// Определяет коэффициенты распределения затрат между строками списка Продукция.
// Если для затраты указано, на какую продукцию она относится, то распределяем только на выпуск этой продукции.
// Если не указано (или выпуска указанной продукции нет) - то на весь выпуск.
// База распределения - ПлановаяСтоимость.
//
// Параметры:
//  ВесьВыпуск		 - ТаблицаЗначений - данные о выпуске;
//                     Должна содержать поля
//                     * Номенклатура (означает выпущенную продукцию)
//                     * ПлановаяСтоимость (Число, отличное от нуля - база распределения).
//  Продукция		 - СправочникСсылка.Номенклатура - продукция, к которой относится затрата.
//                     Может быть пустой ссылкой, что означает "вся выпущенная продукция"
//  КешКоэффициентов - Соответствие - если функция вызывается в цикле, то следует вне цикла создать пустое соответствие и передавать его.
// 
// Возвращаемое значение:
//  Массив - числовые значения базы распределения. Количество и позиции коэффициентов соответствуют списку ВесьВыпуск.
//
Функция КоэффициентыРаспределенияНаПродукцию(ВесьВыпуск, Продукция, КешКоэффициентов, КолонкаБазаРаспределения)
	
	Коэффициенты = КешКоэффициентов[Продукция];
	Если Коэффициенты <> Неопределено Тогда
		Возврат Коэффициенты;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Продукция) Тогда
		
		// Распределим на всю продукцию
		Коэффициенты = ВесьВыпуск.ВыгрузитьКолонку(КолонкаБазаРаспределения);
		
	Иначе
		
		// Попробуем ограничить распределение только строками с этой же продукцией
		СтрокиПродукции = ВесьВыпуск.НайтиСтроки(Новый Структура("Продукция", Продукция));
			
		Если Не ЗначениеЗаполнено(СтрокиПродукции) Тогда
			
			// Продукция в списке материалов указана ошибочно, игнорируем ее.
			Коэффициенты = КоэффициентыРаспределенияНаПродукцию(ВесьВыпуск, Справочники.Номенклатура.ПустаяСсылка(), КешКоэффициентов, КолонкаБазаРаспределения);
				
		Иначе
			
			// Элементы массива коэффициентов должны соответствовать строкам списка ПолученоПродукции.
			// Поэтому массив будет содержать и элементы, относящиеся к другой продукции (с коэффициентом 0).
			Коэффициенты = Новый Массив(ВесьВыпуск.Количество());
			
			Для Индекс = 0 По Коэффициенты.ВГраница() Цикл
				Коэффициенты[Индекс] = 0;
			КонецЦикла;
			
			Для Каждого СтрокаПродукции Из СтрокиПродукции Цикл
				Коэффициенты[ВесьВыпуск.Индекс(СтрокаПродукции)] = СтрокаПродукции[КолонкаБазаРаспределения];
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;	
		
	КешКоэффициентов.Вставить(Продукция, Коэффициенты);
	
	Возврат Коэффициенты;
			
КонецФункции

Процедура ДобавитьКолонкуСодержаниеПродукция(ТаблицаПродукция) Экспорт
	
	Если ТаблицаПродукция.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаПродукция.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ТаблицаПродукция Цикл
		СтрокаТаблицы.Содержание = "Выпуск " + БухгалтерскийУчетПовтИсп.НазваниеОбъектаПоСчетуУчета(СтрокаТаблицы.СчетУчета);
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьТаблицыНДС(ТаблицыДокумента) Экспорт
	
	Товары = ТаблицыДокумента.ТаблицаТовары.СкопироватьКолонки(
		"НомерСтроки,Содержание,СуммаРуб,СуммаНДСРуб,СчетУчета,СчетУчетаНДС,СуммаВзаиморасчетов,СуммаНДСВзаиморасчетов,СтавкаНДС,Количество");
	Товары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов(
		"СправочникСсылка.Номенклатура,СправочникСсылка.ОбъектыСтроительства"));
	
	Для каждого СтрокаДокумента Из ТаблицыДокумента.ТаблицаТовары Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
	КонецЦикла;
	Для каждого СтрокаДокумента Из ТаблицыДокумента.ТаблицаОборудование Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
	КонецЦикла;
	Для каждого СтрокаДокумента Из ТаблицыДокумента.ТаблицаОбъектыСтроительства Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
	КонецЦикла;
	
	Услуги = ТаблицыДокумента.ТаблицаУслуги.Скопировать(,
		"НомерСтроки,Содержание,СуммаРуб,СуммаНДСРуб,СчетЗатрат,СчетУчетаНДС,СуммаВзаиморасчетов,СуммаНДСВзаиморасчетов,СтавкаНДС,
		|ПодразделениеЗатрат,Субконто1,Субконто2,Субконто3");
	Услуги.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	
	Для каждого СтрокаУслуги Из Услуги Цикл
		Если ТипЗнч(СтрокаУслуги.Субконто1) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
			СтрокаУслуги.СтатьяЗатрат = СтрокаУслуги.Субконто1;
		ИначеЕсли ТипЗнч(СтрокаУслуги.Субконто2) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
			СтрокаУслуги.СтатьяЗатрат = СтрокаУслуги.Субконто2;
		ИначеЕсли ТипЗнч(СтрокаУслуги.Субконто3) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
			СтрокаУслуги.СтатьяЗатрат = СтрокаУслуги.Субконто3;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("Товары,Услуги", Товары, Услуги);
		
КонецФункции	


// ОТЛОЖЕННОЕ ПРОВЕДЕНИЕ

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение
	 	ИЛИ НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период) Тогда
		ПараметрыПроведения.Вставить("ТаблицаРегистрации", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТаблицаРегистрации", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""Продукция"" КАК ИмяСписка,
	|	ТаблицаПродукция.СчетУчета КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчетСписания
	|ИЗ
	|	ТаблицаПродукция КАК ТаблицаПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ВозвратнаяТара"",
	|	ТаблицаТара.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|ИЗ
	|	ТаблицаТара КАК ТаблицаТара";
	
	// Счета табличных частей Продукция и ВозвратнаяТара будут получены из МатериалыСОценкойСтоимости в модуле объекта.
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение
		ИЛИ НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период) Тогда
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.Период КАК Дата
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаУслуги КАК ТаблицаУслуги
	|		ПО Реквизиты.Ссылка = ТаблицаУслуги.Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ОбработкаОтложенногоПроведения(Параметры, Отказ) Экспорт
	
	ПараметрыПроведения = ПодготовитьПараметрыПроведения(
		Параметры.Регистратор,
		Отказ,
		Параметры.ДоговорКонтрагента);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	

	ТаблицаВзаиморасчеты = УчетВзаиморасчетовОтложенноеПроведение.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		Параметры,
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ЗачетАвансовТаблицаАвансов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияЗачетАвансов(
		Параметры,
		ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Накладная на передачу готовой продукции (МХ-18)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МХ18";
	КомандаПечати.Представление = НСтр("ru = 'Накладная на передачу готовой продукции (МХ-18)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 50;
	
	// Приходный ордер (М-4)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М4";
	КомандаПечати.Представление = НСтр("ru = 'Приходный ордер (М-4)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 60;
	
	// Отчет об использовании материалов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МатериалыЗаказчика";
	КомандаПечати.Представление = ПредставлениеПечатнойФормыМатериалыЗаказчика();
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 70;
	
	// Справка-расчет "Рублевые суммы документа в валюте"
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьРублевыхСуммДокументовВВалюте";
	КомандаПечати.Идентификатор = "РублевыеСуммыДокументаВВалюте";
	КомандаПечати.Представление = НСтр("ru = 'Справка-расчет ""Рублевые суммы документа в валюте""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ФункциональныеОпции = "ИспользоватьВалютныйУчет";
	КомандаПечати.Порядок       = 80;
	
	// Акт об оказании услуг за поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг за поставщика'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 90;
	
	// Счет-фактура от поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактураПолученный";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура за поставщика'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиСчетовФактурПолученных";
	КомандаПечати.Порядок       = 100;
	
	// Универсальный передаточный документ за поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "УниверсальныйПередаточныйДокумент";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД) за поставщика'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиУниверсальныхПередаточныхДокументов";
	КомандаПечати.Порядок       = 110;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Поступление из переработки""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 200;
	
КонецПроцедуры

// Функция формирует табличный документ с печатной формой накладной,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьМХ18(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент  = Новый ТабличныйДокумент;
	// Зададим параметры макета по умолчанию
	ТабДокумент.ПолеСверху              = 0;
	ТабДокумент.ПолеСлева               = 0;
	ТабДокумент.ПолеСнизу               = 0;
	ТабДокумент.ПолеСправа              = 0;
	ТабДокумент.РазмерКолонтитулаСверху = 0;
	ТабДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабДокумент.АвтоМасштаб             = Истина;
	ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;

	// Загрузим настройки пользователя
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеИзПереработки_МХ18";

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_МХ18");

	Шапка                        = Макет.ПолучитьОбласть("Шапка");
	ЗаголовокТаблицы             = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	Строка                       = Макет.ПолучитьОбласть("Строка");
	Итого                        = Макет.ПолучитьОбласть("Итого");
	ИтогоПоНакладнойОднаСтраница = Макет.ПолучитьОбласть("ИтогоПоНакладнойОднаСтраница");
	ИтогоПоНакладнойМногоСтраниц = Макет.ПолучитьОбласть("ИтогоПоНакладнойМногоСтраниц");
	ПодвалТаблицы                = Макет.ПолучитьОбласть("ПодвалТаблицы");
	Подвал                       = Макет.ПолучитьОбласть("Подвал");

	ТоварКод = "Код";
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
 	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка КАК Ссылка,
	|	ПоступлениеИзПереработки.Номер КАК НомерДокумента,
	|	ПоступлениеИзПереработки.Дата КАК ДатаДокумента,
	|	ПоступлениеИзПереработки.Организация КАК Организация,
	|	ПоступлениеИзПереработки.ПодразделениеОрганизации КАК Подразделение,
	|	ПоступлениеИзПереработки.ПодразделениеОрганизации.НаименованиеПолное КАК ПредставлениеПодразделения,
	|	ПоступлениеИзПереработки.Склад КАК Склад,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура КАК Номенклатура,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеИзПереработкиПродукция.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеИзПереработкиПродукция.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	1 КАК Коэффициент,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	СУММА(ПоступлениеИзПереработкиПродукция.Количество) КАК Количество,
	|	СУММА(ПоступлениеИзПереработкиПродукция.СуммаПлановая) КАК СуммаПлановая,
	|	СУММА(ПоступлениеИзПереработкиПродукция.Количество) КАК КоличествоМест,
	|	МИНИМУМ(ПоступлениеИзПереработкиПродукция.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеИзПереработки.Продукция КАК ПоступлениеИзПереработкиПродукция
	|		ПО ПоступлениеИзПереработки.Ссылка = ПоступлениеИзПереработкиПродукция.Ссылка
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В(&СписокОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеИзПереработки.Ссылка,
	|	ПоступлениеИзПереработки.Номер,
	|	ПоступлениеИзПереработки.Дата,
	|	ПоступлениеИзПереработки.Организация,
	|	ПоступлениеИзПереработки.ПодразделениеОрганизации,
	|	ПоступлениеИзПереработки.ПодразделениеОрганизации.НаименованиеПолное,
	|	ПоступлениеИзПереработки.Склад,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеИзПереработкиПродукция.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеИзПереработкиПродукция.Номенклатура.Код
	|	КОНЕЦ,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения.Код
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка,
	|	НомерСтроки";

	ШапкаДокумента = Запрос.Выполнить().Выбрать();

	ПервыйДокумент = Истина;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	// Вычисление количества строк
	СоответствиеКоличествоСтрок = Новый Соответствие;
	Пока ШапкаДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл
		СчетчикСтрок = 0;
		Пока ШапкаДокумента.Следующий() Цикл
			СчетчикСтрок = СчетчикСтрок + 1;
		КонецЦикла;
		СоответствиеКоличествоСтрок.Вставить(ШапкаДокумента.Ссылка, СчетчикСтрок);
	КонецЦикла;	
	ШапкаДокумента.Сбросить();

	Пока ШапкаДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		// Выводим шапку
		Шапка.Параметры.Заполнить(ШапкаДокумента);
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ШапкаДокумента.НомерДокумента, Истина, Ложь);
		Шапка.Параметры.ДатаДокумента  = ШапкаДокумента.ДатаДокумента;

		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ШапкаДокумента.Организация,
			ШапкаДокумента.ДатаДокумента);

		Шапка.Параметры.ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации);
		Шапка.Параметры.ПредставлениеПолучателя  = ШапкаДокумента.Склад;
		Шапка.Параметры.ОрганизацияПоОКПО        = СведенияОбОрганизации.КодПоОКПО;

		ТабДокумент.Вывести(Шапка);

		// Выводим заголовок таблицы
		ТабДокумент.Вывести(ЗаголовокТаблицы);

		// Инициализация счетчика строк
		НомерСтроки = 0;
		КоличествоСтрок = СоответствиеКоличествоСтрок.Получить(ШапкаДокумента.Ссылка);
		
		// Инициализация счетчика страниц
		НомерСтраницы = 1;

		// Инициализация итогов по странице
		ИтогоМассаБруттоНаСтранице = 0;
		ИтогоМестНаСтранице        = 0;
		ИтогоКоличествоНаСтранице  = 0;
		ИтогоСуммаНаСтранице       = 0;

		// Инициализация итогов по документу
		ИтогоМассаБрутто = 0;
		ИтогоМест        = 0;
		ИтогоКоличество  = 0;
		ИтогСумма        = 0;
		
		// Выводим многострочную часть документа
		Пока ШапкаДокумента.Следующий() Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			// Проверим, помещается ли строка с итогами на страницу, если нет, будем
			// выводить итоги по странице, а строку перенесем на следующую страницу
			СтрокаСИтогами = Новый Массив;
			СтрокаСИтогами.Добавить(Строка);

			// Если строка - последняя в таблице, проверим, поместятся ли
			// итоги по накладной и подвал.
			Если НомерСтроки = КоличествоСтрок Тогда

				Если НомерСтраницы > 1 Тогда
					СтрокаСИтогами.Добавить(Итого);
					СтрокаСИтогами.Добавить(ИтогоПоНакладнойМногоСтраниц);
					СтрокаСИтогами.Добавить(Подвал);
				Иначе
					СтрокаСИтогами.Добавить(ИтогоПоНакладнойОднаСтраница);
					СтрокаСИтогами.Добавить(Подвал);
				КонецЕсли;

			Иначе // не последняя строка, достаточно проверить, поместятся ли итоги по странице

				СтрокаСИтогами.Добавить(Итого);

			КонецЕсли;

			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСИтогами) Тогда

				// Выводим итоги по странице
				Итого.Параметры.ИтогМассаБруттоНаСтранице = ИтогоМассаБруттоНаСтранице;
				Итого.Параметры.ИтогМестНаСтранице        = ИтогоМестНаСтранице;
				Итого.Параметры.ИтогКоличествоНаСтранице  = ИтогоКоличествоНаСтранице;
				Итого.Параметры.ИтогСуммаНаСтранице       = ИтогоСуммаНаСтранице;

				ТабДокумент.Вывести(Итого);

				// Выводим подвал таблицы
				ТабДокумент.Вывести(ПодвалТаблицы);

				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

				// Очистим итоги по странице
				ИтогоМассаБруттоНаСтранице = 0;
				ИтогоМестНаСтранице        = 0;
				ИтогоКоличествоНаСтранице  = 0;
				ИтогоСуммаНаСтранице       = 0;

				// Установим новый номер
				НомерСтраницы = НомерСтраницы + 1;
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;

				ТабДокумент.Вывести(ЗаголовокТаблицы);

			КонецЕсли;

			// Выводим строку
			Строка.Параметры.Заполнить(ШапкаДокумента);
			Строка.Параметры.Цена              = ?(ШапкаДокумента.Количество = 0, 0, ШапкаДокумента.СуммаПлановая
												 / ШапкаДокумента.Количество);
			Строка.Параметры.Сумма             = ШапкаДокумента.СуммаПлановая;

			Мест       = ШапкаДокумента.КоличествоМест;
			Мест       = ?(Мест <> Неопределено, Мест, 0);
			Количество = ШапкаДокумента.Количество;

			ТабДокумент.Вывести(Строка);

			// Увеличим итоги по странице
			ИтогоМассаБруттоНаСтранице = 0;
			ИтогоМестНаСтранице        = ИтогоМестНаСтранице       + Мест;
			ИтогоКоличествоНаСтранице  = ИтогоКоличествоНаСтранице + Количество;
			ИтогоСуммаНаСтранице       = ИтогоСуммаНаСтранице      + ШапкаДокумента.СуммаПлановая;

			// Увеличим итоги по документу
			ИтогоМассаБрутто = 0;
			ИтогоМест        = ИтогоМест + Мест;
			ИтогоКоличество  = ИтогоКоличество + Количество;
			ИтогСумма        = ИтогСумма + ШапкаДокумента.СуммаПлановая;

		КонецЦикла;

		// Если страниц много, выводим промежуточные итоги по последней странице
		// перед итогами по накладной
		Если НомерСтраницы > 1 Тогда

			// Выводим итоги по странице
			Итого.Параметры.ИтогМассаБруттоНаСтранице = ИтогоМассаБруттоНаСтранице;
			Итого.Параметры.ИтогМестНаСтранице        = ИтогоМестНаСтранице;
			Итого.Параметры.ИтогКоличествоНаСтранице  = ИтогоКоличествоНаСтранице;
			Итого.Параметры.ИтогСуммаНаСтранице       = ИтогоСуммаНаСтранице;

			ТабДокумент.Вывести(Итого);

			// Выводим итоги по накладной
			ИтогоПоНакладнойМногоСтраниц.Параметры.ИтогМассаБрутто = ИтогоМассаБрутто;
			ИтогоПоНакладнойМногоСтраниц.Параметры.ИтогМест        = ИтогоМест;
			ИтогоПоНакладнойМногоСтраниц.Параметры.ИтогКоличество  = ИтогоКоличество;
			ИтогоПоНакладнойМногоСтраниц.Параметры.ИтогСумма       = ИтогСумма;

			ТабДокумент.Вывести(ИтогоПоНакладнойМногоСтраниц);

		Иначе // только итоги по накладной

			ИтогоПоНакладнойОднаСтраница.Параметры.ИтогМассаБрутто = ИтогоМассаБрутто;
			ИтогоПоНакладнойОднаСтраница.Параметры.ИтогМест        = ИтогоМест;
			ИтогоПоНакладнойОднаСтраница.Параметры.ИтогКоличество  = ИтогоКоличество;
			ИтогоПоНакладнойОднаСтраница.Параметры.ИтогСумма       = ИтогСумма;

			ТабДокумент.Вывести(ИтогоПоНакладнойОднаСтраница);

		КонецЕсли;

		// Выводим подвал таблицы
		ТабДокумент.Вывести(ПодвалТаблицы);

		// Выводим подвал документа
		Подвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, , ",,,,,,,,0");
		Подвал.Параметры.ИтогСуммаПрописью	= ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(ИтогСумма, ВалютаРегламентированногоУчета);
		
		ТабДокумент.Вывести(Подвал);

		// В табличном документе зададим имя области, в которую был
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент,
			НомерСтрокиНачало, ОбъектыПечати, ШапкаДокумента.Ссылка);
	КонецЦикла;

	Возврат ТабДокумент;

КонецФункции

Функция ПечатьМ4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_М4");

	// Зададим параметры макета
	ТабДокумент.ПолеСверху         = 0;
	ТабДокумент.ПолеСлева          = 0;
	ТабДокумент.ПолеСнизу          = 0;
	ТабДокумент.ПолеСправа         = 0;
	ТабДокумент.АвтоМасштаб        = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_М4";

	ОбластьМакетаШапка              = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакетаЗаголовокДокумента = Макет.ПолучитьОбласть("ЗаголовокДокумента");
	ОбластьМакетаЗаголовокТаблицы   = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьМакетаСтрока             = Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаПодвалСтрок        = Макет.ПолучитьОбласть("ПодвалСтрок");
	ОбластьМакетаИтого              = Макет.ПолучитьОбласть("Итого");
	ОбластьМакетаПодвал             = Макет.ПолучитьОбласть("Подвал");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеИзПереработкиНоменклатура.Номер,
	|	ПоступлениеИзПереработкиНоменклатура.Ссылка,
	|	ПоступлениеИзПереработкиНоменклатура.ДатаСоставления КАК ДатаДокумента,
	|	ПоступлениеИзПереработкиНоменклатура.ДатаСоставления,
	|	ПоступлениеИзПереработкиНоменклатура.Организация,
	|	ПоступлениеИзПереработкиНоменклатура.Организация КАК ЮридическоеФизическоеЛицо,
	|	ПоступлениеИзПереработкиНоменклатура.МестоПриемки,
	|	ПоступлениеИзПереработкиНоменклатура.СкладНаименование,
	|	ПоступлениеИзПереработкиНоменклатура.ПоставщикКод,
	|	ПоступлениеИзПереработкиНоменклатура.Поставщик,
	|	ПоступлениеИзПереработкиНоменклатура.РасчетыВУсловныхЕдиницах,
	|	ПоступлениеИзПереработкиНоменклатура.ВалютаДокумента,
	|	ПоступлениеИзПереработкиНоменклатура.Курс,
	|	ПоступлениеИзПереработкиНоменклатура.Кратность,
	|	ПоступлениеИзПереработкиНоменклатура.СуммаВключаетНДС,
	|	ПоступлениеИзПереработкиНоменклатура.СубСчет,
	|	ПоступлениеИзПереработкиНоменклатура.ПодразделениеЗатрат,
	|	ПоступлениеИзПереработкиНоменклатура.Номенклатура,
	|	ПоступлениеИзПереработкиНоменклатура.ТоварНаименование,
	|	ПоступлениеИзПереработкиНоменклатура.ТоварКод,
	|	ПоступлениеИзПереработкиНоменклатура.ЕдиницаИзмеренияНаименование,
	|	ПоступлениеИзПереработкиНоменклатура.ЕдиницаИзмеренияКод,
	|	ПоступлениеИзПереработкиНоменклатура.НомерТЧ,
	|	СУММА(ПоступлениеИзПереработкиНоменклатура.КоличествоПринято) КАК КоличествоПринято,
	|	СУММА(ПоступлениеИзПереработкиНоменклатура.Стоимость) КАК Стоимость,
	|	МИНИМУМ(ПоступлениеИзПереработкиНоменклатура.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоступлениеИзПереработки.Номер КАК Номер,
	|		ПоступлениеИзПереработки.Ссылка КАК Ссылка,
	|		ПоступлениеИзПереработки.Дата КАК ДатаДокумента,
	|		ПоступлениеИзПереработки.Дата КАК ДатаСоставления,
	|		ПоступлениеИзПереработки.Организация КАК Организация,
	|		ПоступлениеИзПереработки.Организация КАК ЮридическоеФизическоеЛицо,
	|		ПоступлениеИзПереработки.Склад КАК МестоПриемки,
	|		ПоступлениеИзПереработки.Склад.Представление КАК СкладНаименование,
	|		ПоступлениеИзПереработки.Контрагент.Код КАК ПоставщикКод,
	|		ПоступлениеИзПереработки.Контрагент КАК Поставщик,
	|		ПоступлениеИзПереработки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|		ПоступлениеИзПереработки.ВалютаДокумента КАК ВалютаДокумента,
	|		ПоступлениеИзПереработки.КурсВзаиморасчетов КАК Курс,
	|		ПоступлениеИзПереработки.КратностьВзаиморасчетов КАК Кратность,
	|		ПоступлениеИзПереработки.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|		ПоступлениеИзПереработки.СчетЗатрат КАК СубСчет,
	|		ПоступлениеИзПереработки.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|		ПоступлениеИзПереработкиПродукция.Номенклатура КАК Номенклатура,
	|		ПоступлениеИзПереработкиПродукция.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|		ВЫБОР
	|			КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|				ТОГДА ПоступлениеИзПереработкиПродукция.Номенклатура.Артикул
	|			ИНАЧЕ ПоступлениеИзПереработкиПродукция.Номенклатура.Код
	|		КОНЕЦ КАК ТоварКод,
	|		ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|		ПоступлениеИзПереработкиПродукция.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|		ПоступлениеИзПереработкиПродукция.Количество КАК КоличествоПринято,
	|		ПоступлениеИзПереработкиПродукция.СуммаПлановая КАК Стоимость,
	|		1 КАК НомерТЧ,
	|		ПоступлениеИзПереработкиПродукция.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеИзПереработки.Продукция КАК ПоступлениеИзПереработкиПродукция
	|			ПО ПоступлениеИзПереработки.Ссылка = ПоступлениеИзПереработкиПродукция.Ссылка
	|	ГДЕ
	|		ПоступлениеИзПереработки.Ссылка В(&СписокОбъектов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПоступлениеИзПереработки.Номер,
	|		ПоступлениеИзПереработки.Ссылка,
	|		ПоступлениеИзПереработки.Дата,
	|		ПоступлениеИзПереработки.Дата,
	|		ПоступлениеИзПереработки.Организация,
	|		ПоступлениеИзПереработки.Организация,
	|		ПоступлениеИзПереработки.Склад,
	|		ПоступлениеИзПереработки.Склад.Представление,
	|		ПоступлениеИзПереработки.Контрагент.Код,
	|		ПоступлениеИзПереработки.Контрагент,
	|		ПоступлениеИзПереработки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах,
	|		ПоступлениеИзПереработки.ВалютаДокумента,
	|		ПоступлениеИзПереработки.КурсВзаиморасчетов,
	|		ПоступлениеИзПереработки.КратностьВзаиморасчетов,
	|		ПоступлениеИзПереработки.СуммаВключаетНДС,
	|		ПоступлениеИзПереработки.СчетУчетаРасчетовСКонтрагентом,
	|		ПоступлениеИзПереработки.ПодразделениеЗатрат,
	|		ПоступлениеИзПереработкиВозвращенныеМатериалы.Номенклатура,
	|		ПоступлениеИзПереработкиВозвращенныеМатериалы.Номенклатура.НаименованиеПолное,
	|		ВЫБОР
	|			КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|				ТОГДА ПоступлениеИзПереработкиВозвращенныеМатериалы.Номенклатура.Артикул
	|			ИНАЧЕ ПоступлениеИзПереработкиВозвращенныеМатериалы.Номенклатура.Код
	|		КОНЕЦ,
	|		ПоступлениеИзПереработкиВозвращенныеМатериалы.Номенклатура.ЕдиницаИзмерения.Наименование,
	|		ПоступлениеИзПереработкиВозвращенныеМатериалы.Номенклатура.ЕдиницаИзмерения.Код,
	|		ПоступлениеИзПереработкиВозвращенныеМатериалы.Количество,
	|		0,
	|		2,
	|		ПоступлениеИзПереработкиВозвращенныеМатериалы.НомерСтроки
	|	ИЗ
	|		Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеИзПереработки.ВозвращенныеМатериалы КАК ПоступлениеИзПереработкиВозвращенныеМатериалы
	|			ПО ПоступлениеИзПереработки.Ссылка = ПоступлениеИзПереработкиВозвращенныеМатериалы.Ссылка
	|	ГДЕ
	|		ПоступлениеИзПереработки.Ссылка В(&СписокОбъектов)) КАК ПоступлениеИзПереработкиНоменклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеИзПереработкиНоменклатура.Ссылка,
	|	ПоступлениеИзПереработкиНоменклатура.Номер,
	|	ПоступлениеИзПереработкиНоменклатура.ДатаСоставления,
	|	ПоступлениеИзПереработкиНоменклатура.Организация,
	|	ПоступлениеИзПереработкиНоменклатура.МестоПриемки,
	|	ПоступлениеИзПереработкиНоменклатура.Поставщик,
	|	ПоступлениеИзПереработкиНоменклатура.РасчетыВУсловныхЕдиницах,
	|	ПоступлениеИзПереработкиНоменклатура.ВалютаДокумента,
	|	ПоступлениеИзПереработкиНоменклатура.Курс,
	|	ПоступлениеИзПереработкиНоменклатура.Кратность,
	|	ПоступлениеИзПереработкиНоменклатура.СуммаВключаетНДС,
	|	ПоступлениеИзПереработкиНоменклатура.СубСчет,
	|	ПоступлениеИзПереработкиНоменклатура.ПодразделениеЗатрат,
	|	ПоступлениеИзПереработкиНоменклатура.Номенклатура,
	|	ПоступлениеИзПереработкиНоменклатура.ТоварКод,
	|	ПоступлениеИзПереработкиНоменклатура.ЕдиницаИзмеренияНаименование,
	|	ПоступлениеИзПереработкиНоменклатура.ЕдиницаИзмеренияКод,
	|	ПоступлениеИзПереработкиНоменклатура.СкладНаименование,
	|	ПоступлениеИзПереработкиНоменклатура.ПоставщикКод,
	|	ПоступлениеИзПереработкиНоменклатура.НомерТЧ,
	|	ПоступлениеИзПереработкиНоменклатура.ТоварНаименование,
	|	ПоступлениеИзПереработкиНоменклатура.ДатаСоставления,
	|	ПоступлениеИзПереработкиНоменклатура.Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеИзПереработкиНоменклатура.Ссылка,
	|	ДатаДокумента,
	|	ПоступлениеИзПереработкиНоменклатура.НомерТЧ,
	|	НомерСтроки УБЫВ";

	ПервыйДокумент = Истина;

	Шапка = Запрос.Выполнить().Выбрать();

	// Вычисление количества строк
	СоответствиеКоличествоСтрок = Новый Соответствие;
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		СчетчикСтрок = 0;
		Пока Шапка.Следующий() Цикл
			СчетчикСтрок = СчетчикСтрок + 1;
		КонецЦикла;
		СоответствиеКоличествоСтрок.Вставить(Шапка.Ссылка, СчетчикСтрок);
	КонецЦикла;	
	Шапка.Сбросить();

	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		// Выводим общие реквизиты шапки
		СведенияОПокупателе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.ЮридическоеФизическоеЛицо, Шапка.ДатаСоставления);

		ОбластьМакетаШапка.Параметры.Заполнить(Шапка);
		ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации   = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПокупателе);
		ОбластьМакетаШапка.Параметры.ОрганизацияПоОКПО          = СведенияОПокупателе.КодПоОКПО;
		ОбластьМакетаШапка.Параметры.НомерДокумента             = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Ложь);
		ОбластьМакетаШапка.Параметры.ПредставлениеПодразделения = ?(ЗначениеЗаполнено(Шапка.ПодразделениеЗатрат),Шапка.ПодразделениеЗатрат.Наименование,"");

		ТабДокумент.Вывести(ОбластьМакетаШапка);

		// Выводим заголовок докмента
		ОбластьМакетаЗаголовокДокумента.Параметры.Заполнить(Шапка);
		ОбластьМакетаЗаголовокДокумента.Параметры.ДатаСоставления = Шапка.ДатаСоставления;
		ПредставлениеКонтрагента = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Поставщик, Шапка.ДатаСоставления),
			"НаименованиеДляПечатныхФорм,");
		ОбластьМакетаЗаголовокДокумента.Параметры.ПоставщикНаименование = ПредставлениеКонтрагента;
		ТабДокумент.Вывести(ОбластьМакетаЗаголовокДокумента);

		// Выводим заголовок таблицы
		ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);

		// Инициализация итогов в документе
		ИтогоКоличествоПринято = 0;
		ИтогоСуммаБезНДС       = 0;
		ИтогоСуммаНДС          = 0;
		ИтогоВсегоСНДС         = 0;
		Ном                    = 0;

		// Инициализация счетчиков страниц и строк
		НомерСтраницы   = 1;
		НомерСтроки     = 0;
		КоличествоСтрок = СоответствиеКоличествоСтрок.Получить(Шапка.Ссылка);

		// Выводим многострочную часть докмента
		Пока Шапка.Следующий() Цикл

			Если НЕ ЗначениеЗаполнено(Шапка.Номенклатура) 
				ИЛИ (НЕ ЗначениеЗаполнено(Шапка.Стоимость) И Шапка.НомерТЧ = 1) Тогда 
				Продолжить;
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;

			ОбластьМакетаСтрока.Параметры.Заполнить(Шапка);

			Кратность = ?(Шапка.Кратность = 0, 1, Шапка.Кратность);
			ВсегоСНДС = Шапка.Стоимость;

			КоличествоПринято = ?(ЗначениеЗаполнено(Шапка.КоличествоПринято), Шапка.КоличествоПринято, 0);
			СуммаНДС          = 0;
			Цена              = (ВсегоСНДС - СуммаНДС) / ?(ЗначениеЗаполнено(КоличествоПринято), КоличествоПринято, 1);

			ОбластьМакетаСтрока.Параметры.КоличествоПринято = КоличествоПринято;
			ОбластьМакетаСтрока.Параметры.ВсегоСНДС         = ВсегоСНДС;
			ОбластьМакетаСтрока.Параметры.СуммаБезНДС       = ВсегоСНДС - СуммаНДС;
			ОбластьМакетаСтрока.Параметры.СуммаНДС          = СуммаНДС;
			ОбластьМакетаСтрока.Параметры.Цена              = Цена;
			ОбластьМакетаСтрока.Параметры.ТоварНаименование = СокрЛП(Шапка.ТоварНаименование);

			// Проверим вывод
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьМакетаСтрока);
			Если НомерСтроки = КоличествоСтрок Тогда           // если последняя строка, должен
				СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);  // помещаться и подвал документа
				СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
			Иначе                                              // иначе - только подвал строк
				СтрокаСПодвалом.Добавить(ОбластьМакетаПодвалСтрок);
			КонецЕсли;

			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда

				ТабДокумент.Вывести(ОбластьМакетаПодвалСтрок);
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();


				НомерСтраницы = НомерСтраницы + 1;
				ОбластьМакетаЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;

				ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);

			КонецЕсли;

			ТабДокумент.Вывести(ОбластьМакетаСтрока);

			ИтогоКоличествоПринято = ИтогоКоличествоПринято + КоличествоПринято;
			ИтогоСуммаБезНДС       = ИтогоСуммаБезНДС + ВсегоСНДС - СуммаНДС;
			ИтогоСуммаНДС          = ИтогоСуммаНДС + СуммаНДС;
			ИтогоВсегоСНДС         = ИтогоВсегоСНДС + ВсегоСНДС;

		КонецЦикла;

		// Выводим итоги по документу
		ОбластьМакетаИтого.Параметры.ИтогоКоличествоПринято = ИтогоКоличествоПринято;
		ОбластьМакетаИтого.Параметры.ИтогоСуммаБезНДС       = ИтогоСуммаБезНДС;
		ОбластьМакетаИтого.Параметры.ИтогоСуммаНДС          = ИтогоСуммаНДС;
		ОбластьМакетаИтого.Параметры.ИтогоВсегоСНДС         = ИтогоВсегоСНДС;
		ТабДокумент.Вывести(ОбластьМакетаИтого);

		// Выводим итоги по документу
		ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
		ДанныеПодвал = Новый Структура;
		Если ЗначениеЗаполнено(Шапка.МестоПриемки) Тогда 
			МОЛ = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(Шапка.МестоПриемки, Шапка.ДатаСоставления);
			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Шапка.Организация, МОЛ, Шапка.ДатаСоставления);
			ДанныеПодвал.Вставить("ДолжностьПоставщика", ДанныеФизЛица.Должность);
			ДанныеПодвал.Вставить("ФИОПоставщика",       ДанныеФизЛица.Представление);
		КонецЕсли;
		ОбластьМакетаПодвал.Параметры.Заполнить(Шапка);
		ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеПодвал);
		ТабДокумент.Вывести(ОбластьМакетаПодвал);

		// В табличном документе зададим имя области, в которую был
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент,
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;

	Возврат ТабДокумент;

КонецФункции

Функция ПечатьМатериалыЗаказчика(Знач ДокументыДляПечати, ОбъектыПечати, ПараметрыПечати)
	
	ДокументыДляПечати = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ДокументыДляПечати);
	
	УправлениеДоступомБП.УдалитьНедоступныеЭлементыИзМассива(ДокументыДляПечати);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеИзПереработки_МатериалыЗаказчика";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыДляПечати", ДокументыДляПечати);
	// Порядок записей в выборке должен совпадать в обоих запросах.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка КАК Ссылка,
	|	ПоступлениеИзПереработки.Номер КАК Номер,
	|	ПоступлениеИзПереработки.Дата КАК Дата,
	|	ПоступлениеИзПереработки.Организация КАК Организация,
	|	ПоступлениеИзПереработки.Контрагент КАК Контрагент,
	|	ПоступлениеИзПереработки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеИзПереработки.ДоговорКонтрагента) КАК ДоговорКонтрагентаПредставление,
	|	ПоступлениеИзПереработки.ВалютаДокумента КАК ВалютаДокумента,
	|	ПоступлениеИзПереработки.ДоговорКонтрагента.ГосударственныйКонтракт КАК ГосударственныйКонтракт
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В(&ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеИзПереработки.Дата,
	|	ПоступлениеИзПереработки.Номер,
	|	ПоступлениеИзПереработки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Материалы.Ссылка КАК Ссылка,
	|	Материалы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Материалы.Номенклатура.НаименованиеПолное = """"
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(Материалы.Номенклатура)
	|		ИНАЧЕ Материалы.Номенклатура.НаименованиеПолное
	|	КОНЕЦ КАК Товар,
	|	ВЫБОР
	|		КОГДА ДополнительнаяКолонка.Значение = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА Материалы.Номенклатура.Артикул
	|		КОГДА ДополнительнаяКолонка.Значение = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА Материалы.Номенклатура.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодАртикул,
	|	СУММА(Материалы.Количество) КАК Количество,
	|	Материалы.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.ИспользованныеМатериалы КАК Материалы,
	|	Константа.ДополнительнаяКолонкаПечатныхФормДокументов КАК ДополнительнаяКолонка
	|ГДЕ
	|	Материалы.Ссылка В(&ДокументыДляПечати)
	|
	|СГРУППИРОВАТЬ ПО
	|	Материалы.Ссылка,
	|	Материалы.Номенклатура,
	|	Материалы.Номенклатура.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ДополнительнаяКолонка.Значение = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА Материалы.Номенклатура.Артикул
	|		КОГДА ДополнительнаяКолонка.Значение = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА Материалы.Номенклатура.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Материалы.Номенклатура.НаименованиеПолное = """"
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(Материалы.Номенклатура)
	|		ИНАЧЕ Материалы.Номенклатура.НаименованиеПолное
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Материалы.Ссылка.Дата,
	|	Материалы.Ссылка.Номер,
	|	Материалы.Ссылка,
	|	МИНИМУМ(Материалы.НомерСтроки)
	|ИТОГИ ПО
	|	Ссылка";
	
	Результат = Запрос.ВыполнитьПакет();
	ЗаголовкиДокументов = Результат[0].Выбрать();
	ДанныеДокументов    = Результат[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Описание макета
	ПредставлениеФормы = ПредставлениеПечатнойФормыМатериалыЗаказчика();
	ИменаОбластей = СтрРазделить("Заголовок,Поставщик,Получатель,ИтогоМатериалы,Подписи", ",");
	
	ПараметрыВыводаСписка = ПечатьТорговыхДокументов.ПараметрыВыводаСпискаКодАртикул(
		"ШапкаТаблицыМатериалыЗаказчика",
		"СтрокаМатериалыЗаказчика",
		"СКодом");
	ИменаОбластей.Добавить(ПараметрыВыводаСписка.ИмяОбластиШапка);
	ИменаОбластей.Добавить(ПараметрыВыводаСписка.ИмяОбластиСтрока);
	
	ОбластиМакета = ПечатьТорговыхДокументов.ОбластиМакета("Документ.ПоступлениеИзПереработки.ПФ_MXL_Акт", ИменаОбластей);
	
	МакетШапкиСписка = ОбластиМакета[ПараметрыВыводаСписка.ИмяОбластиШапка];
	ДанныеОбласти = Новый Структура;
	ДанныеОбласти.Вставить("ИмяКодАртикул", ПараметрыВыводаСписка.ЗаголовокКолонкиКодАртикул);
	МакетШапкиСписка.Параметры.Заполнить(ДанныеОбласти);
	
	// Вывод данных
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Ссылка = ДанныеДокументов.Ссылка;
		
		СтрокиДокумента = ДанныеДокументов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если Не ЗаголовкиДокументов.НайтиСледующий(ДанныеДокументов.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// ШАПКА
		
		// Заголовок
		
		ПоляЗаголовка = Новый Структура("Номер, Дата, ГосударственныйКонтракт");
		ЗаполнитьЗначенияСвойств(ПоляЗаголовка, ЗаголовкиДокументов);
		
		ДанныеОбласти = Новый Структура;
		ДанныеОбласти.Вставить(
			"ТекстЗаголовка",
			ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(ПоляЗаголовка, ПредставлениеФормы, Истина));
			
		ОбластиМакета.Заголовок.Параметры.Заполнить(ДанныеОбласти);
		
		// Исполнитель
		
		СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ЗаголовкиДокументов.Контрагент,
			ЗаголовкиДокументов.Дата);
			
		ДанныеОбласти = Новый Структура;
		ДанныеОбласти.Вставить(
			"Поставщик",
			ЗаголовкиДокументов.Контрагент);
		ДанныеОбласти.Вставить(
			"ПредставлениеПоставщика",
			ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОКонтрагенте, "НаименованиеДляПечатныхФорм"));
			
		ОбластиМакета.Поставщик.Параметры.Заполнить(ДанныеОбласти);
			
		// Заказчик
		
		ДанныеОбласти = Новый Структура;
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ЗаголовкиДокументов.Организация,
			ЗаголовкиДокументов.Дата);
		ДанныеОбласти.Вставить(
			"Получатель",
			ЗаголовкиДокументов.Организация);
		ДанныеОбласти.Вставить(
			"ПредставлениеПолучателя",
			ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм"));
		ДанныеОбласти.Вставить(
			"Основание",
			ЗаголовкиДокументов.ДоговорКонтрагентаПредставление);
		
		ОбластиМакета.Получатель.Параметры.Заполнить(ДанныеОбласти);
		
		// Выводим макеты шапки
		
		ТабличныйДокумент.Вывести(ОбластиМакета.Заголовок);
		ТабличныйДокумент.Вывести(ОбластиМакета.Поставщик);
		ТабличныйДокумент.Вывести(ОбластиМакета.Получатель);
		
		// СПИСОК МАТЕРИАЛОВ
		ТабличныйДокумент.Вывести(МакетШапкиСписка);
		
		МакетСтроки = ОбластиМакета[ПараметрыВыводаСписка.ИмяОбластиСтрока];
		
		ДанныеОбласти = Новый Структура("НомерСтроки, КодАртикул, Товар, Количество, ЕдиницаИзмерения");
		ДанныеОбласти.НомерСтроки = 0;
		
		Пока СтрокиДокумента.Следующий() Цикл
			
			ДанныеОбласти.НомерСтроки = ДанныеОбласти.НомерСтроки + 1;
			
			ЗаполнитьЗначенияСвойств(ДанныеОбласти, СтрокиДокумента);
			
			МакетСтроки.Параметры.Заполнить(ДанныеОбласти);
			
			ТабличныйДокумент.Вывести(МакетСтроки);
			
		КонецЦикла;
		
		// ПОДВАЛ
		ТабличныйДокумент.Вывести(ОбластиМакета.ИтогоМатериалы);
		ТабличныйДокумент.Вывести(ОбластиМакета.Подписи);
		
		// В табличном документе зададим имя области, в которую был
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПредставлениеПечатнойФормыМатериалыЗаказчика()
	Возврат НСтр("ru='Отчет о переработанном сырье'");
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ18") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ18", "Накладная МХ-18",
			ПечатьМХ18(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "ОбщийМакет.ПФ_MXL_МХ18");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М4") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М4", "М-4 (Приходный ордер)",
			ПечатьМ4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "ОбщийМакет.ПФ_MXL_М4");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Акт") Тогда
		ТаблицаСведенийАктаОбОказанииУслуг = ПолучитьТаблицуСведенийАктаОбОказанииУслуг(МассивОбъектов);
		ПараметрыПечати.Вставить("ВидДокументаПечати", "ПоступлениеИзПереработки");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеИзПереработки_Акт");
		ПараметрыПечати.Вставить("ИмяМакетаПечати", "Документ.ПоступлениеИзПереработки.ПФ_MXL_Акт");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Акт", "Акт об оказании услуг за поставщика", 
			ПечатьТорговыхДокументов.ПечатьАктаОбОказанииУслуг(ТаблицаСведенийАктаОбОказанииУслуг, ОбъектыПечати, ПараметрыПечати),,
					"Документ.ПоступлениеИзПереработки.ПФ_MXL_Акт");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МатериалыЗаказчика") Тогда
		ПечатныеФормы = ПечатьМатериалыЗаказчика(МассивОбъектов, ОбъектыПечати, ПараметрыПечати);		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"МатериалыЗаказчика",
			ПредставлениеПечатнойФормыМатериалыЗаказчика(),
			ПечатныеФормы,
			, // Картинка
			"Документ.ПоступлениеИзПереработки.ПФ_MXL_Акт");
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	

КонецПроцедуры

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаОбОказанииУслуг()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка КАК Ссылка,
	|	ПоступлениеИзПереработки.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПоступлениеИзПереработки.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ДокументыДляПечати
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка КАК Документ,
	|	ПоступлениеИзПереработки.Дата КАК ДатаДляПолученияСведений,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ПоступлениеИзПереработки.Контрагент КАК Поставщик,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеИзПереработки.ДоговорКонтрагента) КАК ПредставлениеДоговора,
	|	ПоступлениеИзПереработки.Организация КАК Получатель,
	|	ПоступлениеИзПереработки.Организация КАК Организация,
	|	ПоступлениеИзПереработки.ПодразделениеОрганизации КАК Подразделение,
	|	"""" КАК БанковскийСчетПродавца,
	|	ПоступлениеИзПереработки.ВалютаДокумента КАК Валюта,
	|	ПоступлениеИзПереработки.КурсВзаиморасчетов КАК Курс,
	|	ПоступлениеИзПереработки.КратностьВзаиморасчетов КАК Кратность,
	|	ЕСТЬNULL(ПоступлениеИзПереработки.ВалютаДокумента.Код, """") КАК ВалютаКод,
	|	ЕСТЬNULL(ПоступлениеИзПереработки.ВалютаДокумента.Наименование, """") КАК ВалютаНаименование,
	|	ЕСТЬNULL(ПоступлениеИзПереработки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ПоступлениеИзПереработки.СуммаВключаетНДС,
	|	"""" КАК Руководитель,
	|	"""" КАК ЗаРуководителяНаОсновании
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПоступлениеИзПереработки.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПоступлениеИзПереработки.Ссылка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляПечати.Ссылка
	|			ИЗ
	|				ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка КАК Документ,
	|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги) КАК ТабличнаяЧасть,
	|	ПоступлениеИзПереработки.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеИзПереработки.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеИзПереработки.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеИзПереработки.Содержание
	|		КОГДА НЕ ПоступлениеИзПереработки.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеИзПереработки.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеИзПереработки.Номенклатура.Наименование
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеИзПереработки.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеИзПереработки.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК НоменклатураКод,
	|	ЕСТЬNULL(ПоступлениеИзПереработки.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ПоступлениеИзПереработки.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(ПоступлениеИзПереработки.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеИзПереработки.Количество,
	|	ПоступлениеИзПереработки.Цена КАК Цена,
	|	ПоступлениеИзПереработки.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеИзПереработки.Сумма - ПоступлениеИзПереработки.СуммаНДС
	|		ИНАЧЕ ПоступлениеИзПереработки.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ПоступлениеИзПереработки.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеИзПереработки.СтавкаНДС КАК СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеИзПереработки.Услуги КАК ПоступлениеИзПереработки
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеИзПереработки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеИзПереработки.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеИзПереработки.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ,
	|	НомерСтроки";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийАктаОбОказанииУслуг(Знач МассивОбъектов) Экспорт
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеАктаОбОказанииУслуг();
	
	Запрос = Новый Запрос();
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаОбОказанииУслуг();
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		
		Если Выборка.РасчетыВУсловныхЕдиницах Тогда
			СведенияОДокументе.Валюта             = ВалютаРегУчета;
			СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
			СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		КонецЕсли;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыАктаОбОказанииУслуг();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = Ложь;
		Если Выборка.Валюта <> ВалютаРегУчета
			И Выборка.РасчетыВУсловныхЕдиницах Тогда
			НуженПересчетВРубли = Истина;
		КонецЕсли;
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС = Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС = Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность = 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС = Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС = (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
				СтрокаТаблицыДокумента.Сумма = СтрокаТаблицыДокумента.СуммаБезНДС + ?(Выборка.СуммаВключаетНДС, СтрокаТаблицыДокумента.СуммаНДС, 0);
				СтрокаТаблицыДокумента.Цена  = ?(СтрокаТаблицыДокумента.Количество=0, СтрокаТаблицыДокумента.Сумма, СтрокаТаблицыДокумента.Сумма / СтрокаТаблицыДокумента.Количество);
			КонецЕсли;
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
		ПечатьТорговыхДокументов.ЗаполнитьДанныеОтветственныхЛиц(СведенияОДокументе, Новый Структура("ЗаполнятьРуководителя"));
		
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляПечатиСправкиРасчетаРублевыеСуммыДокументовВВалюте(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("ВТ_ТаблицаПоШапкеДокумента",                                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаРеквизитов",                                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РегистрСведенийРублевыеСуммыДокументовВВалюте",             НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПоДокументамЗачетнныхАвансов",                           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПредоплат",                                          НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ПоДокументамЗачетнныхАвансов",               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ТаблицаПоШапкеДокумента",                    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаСумм",                                               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_РегистрСведенийРублевыеСуммыДокументовВВалюте", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеОбрабатываемогоДокумента.Ссылка КАК Ссылка,
	|	ДанныеОбрабатываемогоДокумента.Дата КАК Дата,
	|	ДанныеОбрабатываемогоДокумента.Проведен КАК Проведен,
	|	ДанныеОбрабатываемогоДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеОбрабатываемогоДокумента.Организация КАК Организация,
	|	ДанныеОбрабатываемогоДокумента.Контрагент КАК Контрагент,
	|	ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	ДанныеОбрабатываемогоДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ДанныеОбрабатываемогоДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	"""" КАК НомерВходящегоДокумента,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ) КАК УчетАгентскогоНДС,
	|	ДанныеОбрабатываемогоДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ПОМЕСТИТЬ ВТ_ТаблицаПоШапкеДокумента
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ДанныеОбрабатываемогоДокумента
	|ГДЕ
	|	ДанныеОбрабатываемогоДокумента.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПоШапкеДокумента.Ссылка КАК Ссылка,
	|	ВТ_ТаблицаПоШапкеДокумента.Дата КАК Дата,
	|	ВТ_ТаблицаПоШапкеДокумента.Проведен КАК Проведен,
	|	ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация,
	|	ВТ_ТаблицаПоШапкеДокумента.Контрагент,
	|	ВТ_ТаблицаПоШапкеДокумента.ДоговорКонтрагента,
	|	ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.НомерВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.ДатаВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.РасчетыВУсловныхЕдиницах,
	|	ВТ_ТаблицаПоШапкеДокумента.УчетАгентскогоНДС,
	|	ВТ_ТаблицаПоШапкеДокумента.СуммаВключаетНДС,
	|	0 КАК ВсегоВал,
	|	0 КАК НДСВал
	|ИЗ
	|	ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РублевыеСуммыДокументовВВалюте.Всего,
	|	РублевыеСуммыДокументовВВалюте.НДС,
	|	РублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО РублевыеСуммыДокументовВВалюте.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|			И (РублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Регистратор КАК Ссылка,
	|	Хозрасчетный.Сумма КАК СуммаПредоплатыРуб,
	|	Хозрасчетный.ВалютнаяСуммаДт КАК СуммаПредоплатыВал,
	|	ХозрасчетныйСубконто.Значение КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(ХозрасчетныйСубконто.Значение) КАК ДокументПредоплатыПредставление,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация
	|ПОМЕСТИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО Хозрасчетный.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|			И Хозрасчетный.СчетКт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовПоАвансам
	|			И Хозрасчетный.СчетДт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовСКонтрагентом
	|			И (ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор
	|			И Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит))
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка КАК Ссылка,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыРуб КАК СуммаПредоплатыРуб,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыВал КАК СуммаПредоплатыВал,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.ДокументПредоплатыПредставление,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВходящегоДокумента
	|ИЗ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента КАК ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ = ДанныеПервичныхДокументов.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка
	|ИТОГИ
	|	СУММА(СуммаПредоплатыРуб),
	|	СУММА(СуммаПредоплатыВал)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаПоШапкеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработкиУслуги.Ссылка КАК Ссылка,
	|	ПоступлениеИзПереработкиУслуги.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеИзПереработкиУслуги.Номенклатура КАК Товар,
	|	ПоступлениеИзПереработкиУслуги.Номенклатура.Наименование КАК ТоварНаименование,
	|	ПоступлениеИзПереработкиУслуги.Сумма КАК ВсегоВал,
	|	ПоступлениеИзПереработкиУслуги.СуммаНДС КАК НДСВал,
	|	ПоступлениеИзПереработкиУслуги.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК ВсегоРуб,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДСРуб,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДСРуб
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Услуги КАК ПоступлениеИзПереработкиУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ПоступлениеИзПереработкиУслуги.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ПоступлениеИзПереработкиУслуги.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|ГДЕ
	|	ПоступлениеИзПереработкиУслуги.Сумма <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(ВсегоВал),
	|	СУММА(НДСВал),
	|	СУММА(ВсегоРуб),
	|	СУММА(НДСРуб),
	|	СУММА(НалоговаяБазаНДСРуб)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

// Подготоваливает необходимые таблицы для счет фактуры полученный.
// Данные таблиц необходимы для формирования движений по регистру НДС Предъявленный.
//
// Параметры:
// ТекущийДокумент - документ по которому нужно получить движения
// Отказ - флаг отказа.
//
// Возвращаемое значение:
// Структура - содержатся таблицы по документу.
Функция ПодготовитьПараметрыПроведенияДляСчетФактуры(ТекущийДокумент, Отказ = Ложь) Экспорт

	ПараметрыПроведения = Документы.ПоступлениеИзПереработки.ПодготовитьПараметрыПроведения(ТекущийДокумент, Отказ);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ

	// Таблица взаиморасчетов
	ТаблицаВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ЗачетАвансовТаблицаАвансов, ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);
	// Таблицы документа с корректировкой сумм по курсу авансов
	СтруктураТаблицДокумента = Новый Структура("ПоступлениеУслугТаблица", ПараметрыПроведения.ПоступлениеУслугТаблица);
	СтруктураТаблицДокумента = УчетДоходовРасходов.ПодготовитьТаблицыПоступленияПоКурсуАвансов(СтруктураТаблицДокумента,
		ТаблицаВзаиморасчеты, ПараметрыПроведения.ЗачетАвансовРеквизиты);

	ПараметрыПроведения.Вставить("СтруктураТаблицДокумента", СтруктураТаблицДокумента);
	ПараметрыПроведения.Вставить("ТаблицаВзаиморасчеты",     ТаблицаВзаиморасчеты);
	
	Возврат ПараметрыПроведения;
	
КонецФункции

// Определяет предъявлена ли сумма НДС по документу
//
// Параметры:
// ТекущийДокумент - документ, сумму НДС которого нужно вычислить
//
// Возвращаемое значение:
// Булево - если сумма НДС в документе не нулевая
Функция НаличиеСуммыНДС(ТекущийДокумент) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ПоступлениеИзПереработкиУслуги.СуммаНДС) КАК СуммаНДС,
	|	ПоступлениеИзПереработкиУслуги.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Услуги КАК ПоступлениеИзПереработкиУслуги
	|ГДЕ
	|	ПоступлениеИзПереработкиУслуги.Ссылка = &СсылкаНаДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеИзПереработкиУслуги.Ссылка";

	Запрос.УстановитьПараметр("СсылкаНаДокумент", ТекущийДокумент);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.СуммаНДС > 0;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ПечатьУПД

// Определяет какие из переданных документов, на основании которых формируется печатная форма
// универсального передаточного документа на печать, можно распечатать без выписки счета-фактуры.
// В конфигурации не поддерживается печать универсального передаточного документа в статусе "2" 
// (только первичный документ), если в документе выделена сумма НДС и сумма налога не относится
// к посреднической деятельности.
//
// Параметры:
//   МассивДокументов - Массив - массив документов, по которым формируется печатная форма.
//
// Возвращаемое значение:
//   ДокументыСчетФактураНеТребуются - Массив - документы, для печати которых не требуется выписка счета-фактуры.
Функция ПолучитьДокументыСчетФактураНеТребуются(МассивДокументов) Экспорт
	
	ДокументыСчетФактураНеТребуются = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(МассивДокументов) Тогда
		Возврат ДокументыСчетФактураНеТребуются;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеИзПереработки.Ссылка КАК Ссылка,
	|	ИСТИНА КАК ЕстьНДС
	|ПОМЕСТИТЬ ВТ_ДокументыСНДС
	|ИЗ
	|	Документ.ПоступлениеИзПереработки.Услуги КАК ПоступлениеИзПереработки
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В(&МассивДокументов)
	|	И ПоступлениеИзПереработки.СуммаНДС > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеИзПереработки.Ссылка КАК Ссылка,
	|	ПоступлениеИзПереработки.Дата КАК Дата,
	|	ПоступлениеИзПереработки.Организация КАК Организация,
	|	ЕСТЬNULL(ВТ_ДокументыСНДС.ЕстьНДС, ЛОЖЬ) КАК ЕстьНДС
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыСНДС КАК ВТ_ДокументыСНДС
	|		ПО (ВТ_ДокументыСНДС.Ссылка = ПоступлениеИзПереработки.Ссылка)
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В(&МассивДокументов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если НЕ Выборка.ЕстьНДС Тогда
			ДокументыСчетФактураНеТребуются.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыСчетФактураНеТребуются;
	
КонецФункции

// Возвращает текст запроса для формирования печатной формы универсального передаточного документа
// Параметры:
//   ИспользуетсяПостановлениеНДС981 - Булево - признак применения постановления Правительства РФ №981
//
// Возвращаемое значение:
//   ТекстЗапроса - Строка - текст запроса для формирования печатной формы универсального передаточного документа.
Функция ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(ИспользуетсяПостановлениеНДС981 = Неопределено) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактура,
	|	ПоступлениеИзПереработки.Дата КАК Дата,
	|	"""" КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаФактуры,
	|	"""" КАК Руководитель,
	|	"""" КАК ГлавныйБухгалтер,
	|	ИСТИНА КАК СчетФактураБезНДС,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК УдалитьПрефиксыИзНомера,
	|	ЛОЖЬ КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	ПоступлениеИзПереработки.Ссылка КАК ДокументОснование,
	|	1 КАК НомерСтроки,
	|	ПоступлениеИзПереработки.Контрагент КАК Контрагент,
	|	ПоступлениеИзПереработки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	"""" КАК КППСчетаФактуры,
	|	"""" КАК КПППродавца,
	|	ПоступлениеИзПереработки.Дата КАК ДатаСведений
	|ИЗ
	|	Документ.ПоступлениеИзПереработки КАК ПоступлениеИзПереработки
	|ГДЕ
	|	ПоступлениеИзПереработки.Ссылка В(&МассивОбъектов)
	|	И &УсловиеПоДате";
	
	Если ИспользуетсяПостановлениеНДС981 = Неопределено Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДате", "ИСТИНА");
	ИначеЕсли ИспользуетсяПостановлениеНДС981 Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДате",
			"ПоступлениеИзПереработки.Дата >= ДАТАВРЕМЯ(2017,10,1)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДате",
			"ПоступлениеИзПереработки.Дата < ДАТАВРЕМЯ(2017,10,1)");
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#КонецЕсли