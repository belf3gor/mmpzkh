#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 17, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область СчетаУчета

Процедура УстановитьПравилаЗаполненияСчетовУчета(Правила) Экспорт
	
	// Для следующих операций правила заполнения пока не используются:
	// ПрочийПриход (кроме режима Интеграция с банком)
	// РозничнаяВыручка
	
	// учет денег
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетКасса", "НаличныеДеньги");
	// ВалютаДокумента
	
	// учет расчетов
	// - расчеты с покупателем (разрешено погашать задолженность)
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовСКонтрагентом", "РасчетыСПокупателем");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента",           "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СпособПогашенияЗадолженности", "СпособПогашенияЗадолженности");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОплатаПокупателя");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПогашатьЗадолженность");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "РасчетыПоПеням");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПокупателя", Ложь);
	
	// - расчеты по пеням
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовСКонтрагентом", "РасчетыПоПретензиям");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента",          "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СпособПогашенияЗадолженности", "СпособПогашенияЗадолженности");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Сделка",                       "Сделка");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОплатаПокупателя");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПогашатьЗадолженность");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "РасчетыПоПеням");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПокупателя", Ложь);
	
	// - аванс покупателя
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовПоАвансам", "АвансыПокупателя", Ложь);
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента",           "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СпособПогашенияЗадолженности", "СпособПогашенияЗадолженности");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПогашатьЗадолженность");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОплатаПокупателя");
	
	// - расчеты с поставщиком
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовСКонтрагентом", "РасчетыСПоставщиком");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента",           "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ВозвратОтПоставщика");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПоставщику", Ложь);
	
	// - получение займа
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовСКонтрагентом", "ПолученныеЗаймы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента",          "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПолучениеЗайма");
	
	// - получение кредита
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовСКонтрагентом", "ПолученныеКредиты");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента",          "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПолучениеКредита");
	
	// - возврат займа контрагентом
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "РасшифровкаПлатежа", "СчетУчетаРасчетовСКонтрагентом", "ПредоставленныеЗаймы");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ВозвратЗаймаКонтрагентом");
	
	// - получение с банковского счета
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "БезналичныеДеньги");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПолучениеНаличныхВБанке");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ИспользоватьПереводыВПути");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "ПереводыВПути");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПолучениеНаличныхВБанке");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ИспользоватьПереводыВПути");
	
	// прочие доходы
	Если ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "ПрочиеДоходы");
		// Вызов с параметром "СубконтоЗатрат1" используется в виде исключения.
		СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоКт1", "СубконтоЗатрат1");
	КонецЕсли;
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Дата");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Организация");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ВалютаДокумента");
	Для каждого ИмяВидаОперации Из ВидыОперацийСчетаУчета() Цикл
		СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, ИмяВидаОперации, "ВидОперации");
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьДанныеЗаполненияСчетовУчета(ДанныеЗаполнения) Экспорт
	
	Для каждого ИмяВидаОперации Из ВидыОперацийСчетаУчета() Цикл
		Если ДанныеЗаполнения.Свойство(ИмяВидаОперации) Тогда
			ДанныеЗаполнения[ИмяВидаОперации] = (ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПКО[ИмяВидаОперации]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыОперацийСчетаУчета()
	
	ВидыОпераций = Новый Массив;
	ВидыОпераций.Добавить("ОплатаПокупателя");        // счет расчетов и счет авансов в списке
	ВидыОпераций.Добавить("ВозвратОтПоставщика");     // счет расчетов и счет авансов в списке
	ВидыОпераций.Добавить("ПолучениеЗайма");
	ВидыОпераций.Добавить("ПолучениеКредита");
	ВидыОпераций.Добавить("ВозвратЗаймаКонтрагентом");
	ВидыОпераций.Добавить("ПолучениеНаличныхВБанке"); // один счет в шапке
	ВидыОпераций.Добавить("ПрочийПриход"); // один счет в шапке
	
	Возврат Новый ФиксированныйМассив(ВидыОпераций);
	
КонецФункции

#КонецОбласти

Функция СчетКассыИзОРП(Основание) Экспорт
	
	СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	
	// Поле "Счет операционной кассы" заполняем из отчета о розничных продажах при условии, что он там был один
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетОРозничныхПродажахТовары.СчетДоходов КАК СчетДоходов
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
	|ГДЕ
	|	ОтчетОРозничныхПродажахТовары.Ссылка = &Основание
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетОРозничныхПродажахАгентскиеУслуги.СчетРасчетов
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.АгентскиеУслуги КАК ОтчетОРозничныхПродажахАгентскиеУслуги
	|ГДЕ
	|	ОтчетОРозничныхПродажахАгентскиеУслуги.Ссылка = &Основание";
	
	Запрос.УстановитьПараметр("Основание", Основание);
	ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	Если ТаблицаСчетов.Количество() = 1 Тогда
		СчетУчетаРасчетовСКонтрагентом = ТаблицаСчетов[0].СчетДоходов;
	КонецЕсли;
	
	Возврат СчетУчетаРасчетовСКонтрагентом;
	
КонецФункции

// Вызывается документом СчетФактураВыданный на аванс при вводе на основании
//
Функция ТекстЗапросаСчетФактураВыданныйНаАвансРасшифровкаПлатежа(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("ВТ_РасшифровкаПлатежа", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка КАК ДокументОснование,
	|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	ТаблицаДокумента.ДоговорКонтрагента.НаименованиеДляСчетаФактурыНаАванс КАК Номенклатура,
	|	ТаблицаДокумента.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
	|	ТаблицаДокумента.СчетНаОплату КАК СчетНаОплату,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДокумента.СуммаПлатежа) КАК Сумма,
	|	ТаблицаДокумента.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности
	|ПОМЕСТИТЬ РасшифровкаПлатежа
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка.Дата,
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Ссылка.Организация,
	|	ТаблицаДокумента.Ссылка.Контрагент,
	|	ТаблицаДокумента.ДоговорКонтрагента,
	|	ТаблицаДокумента.СчетУчетаРасчетовПоАвансам,
	|	ТаблицаДокумента.СчетНаОплату,
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.СпособПогашенияЗадолженности,
	|	ТаблицаДокумента.ДоговорКонтрагента.НаименованиеДляСчетаФактурыНаАванс";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПолучитьСписокВидовОперацийСРасшифровкойПлатежа() Экспорт
	
	СписокОпераций = Новый СписокЗначений();
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.ОплатаПокупателя);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.ВозвратЗаймаКонтрагентом);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.ПолучениеЗайма);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.ПолучениеКредита);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПКО.РозничнаяВыручка);
	
	Возврат(СписокОпераций);
	
КонецФункции

Функция ЕстьРасшифровкаПлатежа(Знач ВидОперации) Экспорт
	
	СписокВидовСРасшифровкойПлатежа = ПолучитьСписокВидовОперацийСРасшифровкойПлатежа();
	
	Возврат СписокВидовСРасшифровкойПлатежа.НайтиПоЗначению(ВидОперации) <> Неопределено;
	
КонецФункции

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("ВидОперации");
	МассивРеквизитов.Добавить("Организация");
	
	Возврат МассивРеквизитов;
	
КонецФункции

Функция СчетаБанка() Экспорт
	
	СчетаОтбора = Новый Массив;
	СчетаОтбора.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	СчетаОтбора.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);
	
	Возврат БухгалтерскийУчет.ПолучитьМассивСчетовДенежныхСредств(СчетаОтбора, Ложь);
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ВидОперации)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ТекстЗапросаПараметрыПечатиЧека(ДокументСсылка, ИменаТаблиц) Экспорт
	
	ТекстЗапроса = ТекстЗапросаРеквизитыПечатиЧека(ИменаТаблиц) 
		+ Документы.РеализацияТоваровУслуг.ТекстЗапросаРеализацииПоСчету(ИменаТаблиц)
		+ Документы.СчетНаОплатуПокупателю.ТекстЗапросаДанныеДляПечатиЧека(ИменаТаблиц)
		+ Документы.РеализацияТоваровУслуг.ТекстЗапросаДанныеДляПечатиЧека(ИменаТаблиц);
		
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаДанныеДляОбновленияЦенДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриходныйКассовыйОрдер.УслугаНПД КАК Номенклатура,
	|	ПриходныйКассовыйОрдер.СуммаДокумента КАК Цена,
	|	&Валюта КАК Валюта,
	|	&СпособЗаполненияЦены КАК СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС КАК ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Ссылка = &Ссылка";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаЗаполнения

Процедура ОбработкаПроверкиЗаполненияРасшифровкаПлатежа(Объект, ФормаРасшифровкаПлатежа = Неопределено, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	СообщенияПроверки = ПодготовитьСообщенияПроверкиЗаполненияРасшифровкаПлатежа(
		Объект,
		ФормаРасшифровкаПлатежа,
		Отказ,
		ПроверяемыеРеквизиты);
		
	// Вывод сообщений проверки.
	СообщитьРезультатПроверки(
		Объект,
		Отказ,
		СообщенияПроверки,
		Метаданные.Документы.ПриходныйКассовыйОрдер.ТабличныеЧасти.РасшифровкаПлатежа,
		ФормаРасшифровкаПлатежа);
	
КонецПроцедуры

Функция ПодготовитьСообщенияПроверкиЗаполненияРасшифровкаПлатежа(Объект, ФормаРасшифровкаПлатежа = Неопределено, Отказ, ПроверяемыеРеквизиты, ПроверятьСчетаУчета = Истина) Экспорт

	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ВидОперации           = Объект.ВидОперации;
	РасшифровкаПлатежа    = ДанныеТабличнойЧасти(Объект, ФормаРасшифровкаПлатежа);
	ЭтоПроверкаВТаблице   = ДанныеОтображаютсяВТаблицеНаФорме(Объект, ФормаРасшифровкаПлатежа);
	
	// Отключаем стандартную проверку реквизитов ТЧ РасшифровкаПлатежа,
	// выполняем ее в зависимости от того, из какой формы она вызвана.
	Для каждого Реквизит Из ПроверяемыеРеквизиты Цикл
		Если СтрНайти(Реквизит, "РасшифровкаПлатежа") = 1 Тогда

			Если НЕ ПроверятьСчетаУчета Тогда
				Если СтрНайти(Реквизит, "СчетУчетаРасчетовСКонтрагентом") > 0
					ИЛИ СтрНайти(Реквизит, "СчетУчетаРасчетовПоАвансам") > 0 Тогда
					// Счета проверять будем не в этой процедуре, поэтому их не исключаем из проверяемых реквизитов.
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			МассивНепроверяемыхРеквизитов.Добавить(Реквизит);
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьРасшифровкаПлатежа(ВидОперации) Тогда
	
		// Построчная проверка заполнения отдельных реквизитов ТЧ РасшифровкаПлатежа
		
		ЕстьРасчетыСПоставщикамиПокупателями = (ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика)
			И НЕ Объект.БезЗакрывающихДокументов;
		
		ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация, Объект.Дата);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Объект.Организация, Объект.Дата);
		ЕстьАвансыУСН = УчетУСН.ЕстьАвансыПокупателей(Объект.ВидОперации);
		ВестиУчетПоДоговорам     = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам")
			И НЕ Объект.БезЗакрывающихДокументов;
		ЭтоРасшифровкаВыручки = (ВидОперации = Перечисления.ВидыОперацийПКО.РозничнаяВыручка);
		
		СообщенияПроверки = ПроверкаЗаполненияДокументов.НовыйРезультатПроверки();
		
		ПредыдущийПатент = Неопределено;
		
		Для каждого СтрокаПлатежа Из РасшифровкаПлатежа Цикл
			
			// ДоговорКонтрагента
			Если Не ЭтоРасшифровкаВыручки И ВестиУчетПоДоговорам И НЕ ЗначениеЗаполнено(СтрокаПлатежа.ДоговорКонтрагента) Тогда
				Сообщение = СообщенияПроверки.Добавить();
				Сообщение.ПолноеИмяРеквизита = "РасшифровкаПлатежа.ДоговорКонтрагента";
				Сообщение.НомерСтроки        = СтрокаПлатежа.НомерСтроки;
			КонецЕсли;
			
			// СпособПогашенияЗадолженности И Сделка
			Если ЕстьРасчетыСПоставщикамиПокупателями Тогда
				
				Если НЕ ЗначениеЗаполнено(СтрокаПлатежа.СпособПогашенияЗадолженности) Тогда
					Сообщение = СообщенияПроверки.Добавить();
					Сообщение.ПолноеИмяРеквизита = "РасшифровкаПлатежа.СпособПогашенияЗадолженности";
					Сообщение.НомерСтроки        = СтрокаПлатежа.НомерСтроки;
				КонецЕсли;
				
				Если СтрокаПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.ПоДокументу
					И НЕ ЗначениеЗаполнено(СтрокаПлатежа.Сделка) Тогда
					
					Сообщение = СообщенияПроверки.Добавить();
					Сообщение.ПолноеИмяРеквизита = "РасшифровкаПлатежа.Сделка";
					Сообщение.НомерСтроки        = СтрокаПлатежа.НомерСтроки;
					
				КонецЕсли;
				
			КонецЕсли;
		
			// СуммаПлатежа
			Если НЕ ЗначениеЗаполнено(СтрокаПлатежа.СуммаПлатежа) Тогда
				// Проверку суммы платежа не в таблице не осуществляем,
				// т.к. в этом случае она совпадает с реквизитом шапки СуммаДокумента,
				// и будет проверяться он.
				Если ЭтоПроверкаВТаблице Тогда
					Сообщение = СообщенияПроверки.Добавить();
					Сообщение.ПолноеИмяРеквизита = "РасшифровкаПлатежа.СуммаПлатежа";
					Сообщение.НомерСтроки        = СтрокаПлатежа.НомерСтроки;
				КонецЕсли;
			КонецЕсли;
			
			// Сумма взаиморасчетов
			Если Не ЭтоРасшифровкаВыручки И Не ЗначениеЗаполнено(СтрокаПлатежа.ДоговорКонтрагента)
				И СтрокаПлатежа.СуммаПлатежа > 0
				И СтрокаПлатежа.СуммаВзаиморасчетов = 0 Тогда
				
				Сообщение = СообщенияПроверки.Добавить();
				Сообщение.ПолноеИмяРеквизита = "РасшифровкаПлатежа.СуммаВзаиморасчетов";
				Сообщение.НомерСтроки        = СтрокаПлатежа.НомерСтроки;
				
			КонецЕсли;
			
			Если ЕстьАвансыУСН И (ПрименяетсяУСН ИЛИ ПрименяетсяУСНПатент) Тогда
				
				// допустимо отражение аванса только по одному патенту
				Если ПрименяетсяУСНПатент И ТипЗнч(СтрокаПлатежа.ПорядокОтраженияАванса) = Тип("СправочникСсылка.Патенты") Тогда
					Если НЕ ЗначениеЗаполнено(ПредыдущийПатент) Тогда
						ПредыдущийПатент = СтрокаПлатежа.ПорядокОтраженияАванса;
					Иначе
						Если СтрокаПлатежа.ПорядокОтраженияАванса <> ПредыдущийПатент Тогда
							
							Сообщение = СообщенияПроверки.Добавить();
							Сообщение.ПолноеИмяРеквизита = "РасшифровкаПлатежа.ПолеОтражениеАванса";
							Сообщение.НомерСтроки        = СтрокаПлатежа.НомерСтроки;
							
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Счета учета в строках проверяем с помощью общего механизма для всех счетов.
		Если ПроверятьСчетаУчета 
			И НЕ Объект.БезЗакрывающихДокументов Тогда
			ПроверяемыеРеквизитыРасшифровкаПлатежа = Новый Массив;
			Если ВидОперации <> Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам Тогда
				ПроверяемыеРеквизитыРасшифровкаПлатежа.Добавить("РасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом");
			КонецЕсли;
			
			ПроверяемыеРеквизитыРасшифровкаПлатежа.Добавить("РасшифровкаПлатежа.СчетУчетаРасчетовПоАвансам");
			
			Если ФормаРасшифровкаПлатежа <> Неопределено Тогда
				// Общие механизмы проверки счетов учета обращаются к табличной части внутри самого объекта.
				Объект.Вставить("РасшифровкаПлатежа", РасшифровкаПлатежа);
			КонецЕсли;
			
			СчетаУчетаВДокументах.ПроверитьЗаполнение(Объект, Отказ, ПроверяемыеРеквизитыРасшифровкаПлатежа,, СообщенияПроверки, Ложь);
			
			Если ФормаРасшифровкаПлатежа <> Неопределено Тогда
				// Более табличная часть внутри Объекта не требуется.
				Объект.Удалить("РасшифровкаПлатежа");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Возврат СообщенияПроверки;

КонецФункции

Процедура СообщитьРезультатПроверки(Объект, Отказ, СообщенияПроверки, МетаданныеТабличнойЧасти, ФормаСТаблицей = Неопределено) Экспорт
	
	Если СообщенияПроверки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	// Для сохранения порядка сортировки пронумеруем строки.
	СообщенияПроверки.Колонки.Добавить("НомерППДляСортировки", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	
	НомерПП = 1;
	Для каждого СтрокаСообщения Из СообщенияПроверки Цикл
		СтрокаСообщения.НомерППДляСортировки = НомерПП;
		НомерПП = НомерПП + 1;
	КонецЦикла;
	
	СообщенияПроверки.Сортировать("НомерСтроки, НомерППДляСортировки");

	КэшСинонимовРеквизитов = Новый Соответствие;
	
	ИспользоватьТабличныеФормыБанковскихДокументов = Константы.ИспользоватьТабличныеФормыБанковскихДокументов.Получить();
	ЭтоПроверкаВТаблице = ДанныеОтображаютсяВТаблицеНаФорме(Объект, ФормаСТаблицей);
	
	Для каждого СтрокаСообщения Из СообщенияПроверки Цикл
		ИмяРеквизитаДетально = ОбщегоНазначенияБПКлиентСервер.РазложитьПолноеИмяРеквизита(СтрокаСообщения.ПолноеИмяРеквизита);
		СинонимРеквизита     = КэшСинонимовРеквизитов[ИмяРеквизитаДетально.Реквизит];
		Если СинонимРеквизита = Неопределено Тогда
			Если ИмяРеквизитаДетально.Реквизит = "ПолеОтражениеАванса" Тогда
				СинонимРеквизита = НСтр("ru = 'Поле отражения аванса'");
				КэшСинонимовРеквизитов.Вставить(ИмяРеквизитаДетально.Реквизит, СинонимРеквизита);
			Иначе
				СинонимРеквизита = МетаданныеТабличнойЧасти.Реквизиты.Найти(ИмяРеквизитаДетально.Реквизит).Синоним;
				КэшСинонимовРеквизитов.Вставить(ИмяРеквизитаДетально.Реквизит, СинонимРеквизита);
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоПроверкаВТаблице Тогда
			Если ПустаяСтрока(СтрокаСообщения.Ошибка) Тогда
				ШаблонТекстаСообщенияКолонка = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка",
					?(ЗначениеЗаполнено(СтрокаСообщения.Ошибка), "Корректность", "Заполнение"),
					"%1",
					"%2",
					МетаданныеТабличнойЧасти.Синоним,
					СтрокаСообщения.Ошибка);
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонТекстаСообщенияКолонка, СинонимРеквизита, СтрокаСообщения.НомерСтроки);
			Иначе
				ТекстСообщения = СокрЛП(СтрокаСообщения.Ошибка);
			КонецЕсли;
			
			Если ИспользоватьТабличныеФормыБанковскихДокументов Тогда
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяРеквизитаДетально.ТабличнаяЧасть,
					СтрокаСообщения.НомерСтроки,
					ИмяРеквизитаДетально.Реквизит);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, Поле, "Объект", Отказ);
			ИначеЕсли ФормаСТаблицей <> Неопределено Тогда
				// При вызове из доп. формы не указываем КлючДанных,
				// иначе при двойном щелчке по сообщению будет переход в основную форму документа.
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяРеквизитаДетально.ТабличнаяЧасть,
					СтрокаСообщения.НомерСтроки,
					ИмяРеквизитаДетально.Реквизит);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "", Отказ);
			Иначе
				// При проверке в таблице в момент вызова из основной формы документа
				// сообщение показываем возле гиперссылки.
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "НадписьСуммаДокумента",, Отказ);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(СтрокаСообщения.Ошибка) Тогда
				ШаблонТекстаСообщенияПоле = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле",
					?(ЗначениеЗаполнено(СтрокаСообщения.Ошибка), "Корректность", "Заполнение"),
					"%1",
					СтрокаСообщения.Ошибка);
				
				// Сообщения показываем возле полей ввода на основной форме,
				// связанных с первой строкой табличной части.
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаСообщенияПоле, СинонимРеквизита);
			Иначе
				ТекстСообщения = СокрЛП(СтрокаСообщения.Ошибка);
			КонецЕсли;
			
			Поле = СтрЗаменить(СтрокаСообщения.ПолноеИмяРеквизита, ".", "");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, Поле,, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеТабличнойЧасти(Объект, ФормаРасшифровкаПлатежа)

	Если ФормаРасшифровкаПлатежа <> Неопределено Тогда
		РасшифровкаПлатежа  = ФормаРасшифровкаПлатежа.РасшифровкаПлатежа;
	Иначе
		РасшифровкаПлатежа  = Объект.РасшифровкаПлатежа;
	КонецЕсли;

	Возврат РасшифровкаПлатежа;

КонецФункции

Функция ДанныеОтображаютсяВТаблицеНаФорме(Объект, ФормаРасшифровкаПлатежа)

	РасшифровкаПлатежа = ДанныеТабличнойЧасти(Объект, ФормаРасшифровкаПлатежа);

	Если ФормаРасшифровкаПлатежа <> Неопределено Тогда
		Результат = Истина;
	Иначе
		// Если в табличной части 1 строка, то сообщение о проверках ее полей
		// выводятся как для полей ввода на основной форме документа.
		Результат = Константы.ИспользоватьТабличныеФормыБанковскихДокументов.Получить()
			ИЛИ РасшифровкаПлатежа.Количество() > 1;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Константы.ИспользоватьТабличныеФормыБанковскихДокументов.Получить()
		И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		ВыбраннаяФорма = "ФормаДокумента";
	Иначе
		ВыбраннаяФорма = "ФормаДокументаОднострочная";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Процедура ПодготовитьПараметрыРеквизитыДокумента(Запрос, ПараметрыПроведения, Отказ)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.БезЗакрывающихДокументов КАК БезЗакрывающихДокументов,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.СчетКасса КАК СчетКасса,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СубконтоКт1 КАК СубконтоКт1,
	|	Реквизиты.СубконтоКт2 КАК СубконтоКт2,
	|	Реквизиты.СубконтоКт3 КАК СубконтоКт3,
	|	Реквизиты.ПодразделениеКт КАК ПодразделениеКт,
	|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Реквизиты.ВыручкаСНТТ КАК ВыручкаСНТТ,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ОплатаПокупателя)
	|		И НЕ Реквизиты.БезЗакрывающихДокументов КАК РасчетыПоРеализации,
	|	Реквизиты.Основание КАК Содержание,
	|	Реквизиты.Приложение КАК Приложение,
	|	ВЫРАЗИТЬ(Реквизиты.Контрагент КАК Справочник.Склады).НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Реквизиты.ВалютаДокумента = &ВалютаРеглУчета КАК ДокументВРублях,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ВозвратОтПоставщика)
	|		И НЕ Реквизиты.БезЗакрывающихДокументов КАК ЭтоВозврат
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.БезЗакрывающихДокументов КАК БезЗакрывающихДокументов,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.СчетКасса КАК СчетКасса,
	|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Реквизиты.ВыручкаСНТТ КАК ВыручкаСНТТ,
	|	Реквизиты.РасчетыПоРеализации КАК РасчетыПоРеализации,
	|	Реквизиты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Реквизиты.ДокументВРублях КАК ДокументВРублях,
	|	1 КАК КоэффициентРуб,
	|	Реквизиты.Содержание КАК Содержание,
	|	Реквизиты.Приложение КАК Приложение,
	|	Реквизиты.ЭтоВозврат КАК ЭтоВозврат,
	|	&ЭтоОтложенноеПроведение КАК ЭтоОтложенноеПроведение
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	ТаблицаРеквизиты = Запрос.Выполнить().Выгрузить();
	Реквизиты        = ТаблицаРеквизиты[0];
	
	// Коэффициент пересчета сумм из валюты документа в рубли
	Если Реквизиты.ДокументВРублях Тогда
		КоэффициентРуб = 1;
	Иначе
		СтруктураКурсаДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Реквизиты.ВалютаДокумента, Реквизиты.Дата);
		
		Если СтруктураКурсаДокумента.Кратность = 0 Тогда
			Отказ           = Истина;
			ШаблонСообщения = НСтр("ru = 'Документ %1 не может быть проведен.
				|Не задана кратность валюты %2 на дату %3'");
			ТекстСообщения  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения, Реквизиты.Ссылка, Реквизиты.ВалютаДокумента, Формат(Реквизиты.Дата, "ДФ=dd.MM.yy"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ПараметрыПроведения.ДокументСсылка);
			ПараметрыПроведения.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);
			
			Возврат;
		КонецЕсли;
		
		КоэффициентРуб           = СтруктураКурсаДокумента.Курс / СтруктураКурсаДокумента.Кратность;
		Реквизиты.КоэффициентРуб = КоэффициентРуб;
	КонецЕсли;
	
	ПараметрыПроведения.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);
	
КонецПроцедуры

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ, ДоговорДляОтложенногоПроведения = Неопределено) Экспорт
	
	ВалютаРеглУчета     = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ЭтоОтложенноеПроведение = ЗначениеЗаполнено(ДоговорДляОтложенногоПроведения);
	
	ПараметрыПроведения = Новый Структура;
	ПараметрыПроведения.Вставить("ВалютаРеглУчета", ВалютаРеглУчета);
	ПараметрыПроведения.Вставить("ДокументСсылка",  ДокументСсылка);
	ПараметрыПроведения.Вставить("ЭтоОтложенноеПроведение",  ЭтоОтложенноеПроведение);
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц  = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	Запрос.УстановитьПараметр("ЭтоОтложенноеПроведение", 			ЭтоОтложенноеПроведение);
	Запрос.УстановитьПараметр("ДоговорДляОтложенногоПроведения", 	ДоговорДляОтложенногоПроведения);
	
	ПодготовитьПараметрыРеквизитыДокумента(Запрос, ПараметрыПроведения, Отказ);
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	Если Отказ ИЛИ НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Дата, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	ПлательщикНДС               = УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация,               Реквизиты.Дата);
	СпособОценкиТоваровВРознице = УчетнаяПолитика.СпособОценкиТоваровВРознице(Реквизиты.Организация, Реквизиты.Дата);
	
	ПараметрыПроведения.Вставить("ПлательщикНДС", ПлательщикНДС);
	
	Запрос.УстановитьПараметр("ВалютаДокумента",                 Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТорговаяТочка",                   Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ВалютаРеглУчета",                 ВалютаРеглУчета);
	Запрос.УстановитьПараметр("ПлательщикНДС",                   ПлательщикНДС);
	Запрос.УстановитьПараметр("ОрганизацияПрименяетУСН",         УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация,                  Реквизиты.Дата));
	Запрос.УстановитьПараметр("ОрганизацияПрименяетУСНПатент",   УчетнаяПолитика.ПрименяетсяУСНПатент(Реквизиты.Организация,            Реквизиты.Дата));
	Запрос.УстановитьПараметр("РозничнаяТорговляОблагаетсяЕНВД", УчетнаяПолитика.РозничнаяТорговляОблагаетсяЕНВД(Реквизиты.Организация, Реквизиты.Дата));
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",    УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация,    Реквизиты.Дата));
	Запрос.УстановитьПараметр("МассивСчетовВыручкиЕНВД",         БухгалтерскийУчетПовтИсп.СчетаВыручкиЕНВД());
	Запрос.УстановитьПараметр("УчетПоПродажнойСтоимости",        СпособОценкиТоваровВРознице = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости);
	Запрос.УстановитьПараметр("ИспользоватьПереводыВПутиПриПеремещенияДенежныхСредств", УчетнаяПолитика.ИспользоватьПереводыВПутиПриПеремещенияДенежныхСредств(
		Реквизиты.Организация, Реквизиты.Дата));
	Если Реквизиты.БезЗакрывающихДокументов Тогда
		Запрос.УстановитьПараметр("ПрочиеВнереализационныеДоходыРасходы",
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.ПрочиеВнереализационныеДоходыРасходы"));
	КонецЕсли;
	
	Для каждого Колонка Из ПараметрыПроведения.ТаблицаРеквизиты.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицыДокумента(НомераТаблиц,       ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПогашениеЗадолженности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРозничнаяВыручка(НомераТаблиц,       ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРозничнаяВыручкаНДС(НомераТаблиц,    ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПрочееПоступление(НомераТаблиц,      ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаОплатаСчетов(НомераТаблиц,           ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Реквизиты = ПараметрыПроведения.Реквизиты[0];
	Реквизиты.Содержание = ТекстСодержанияПроводокДокумента(Реквизиты);
	Если ПараметрыПроведения.РасшифровкаПлатежа <> Неопределено Тогда
		ПараметрыПроведения.РасшифровкаПлатежа.ЗаполнитьЗначения(Реквизиты.Содержание,  "Содержание");
	КонецЕсли;
	
	Если ПараметрыПроведения.РозничнаяВыручкаНДС <> Неопределено Тогда
		ПараметрыПроведения.РозничнаяВыручкаНДС.ЗаполнитьЗначения(Реквизиты.Содержание, "Содержание");
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	&УчетПоПродажнойСтоимости КАК УчетПоПродажнойСтоимости,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ОплатаПокупателя)
	|		И НЕ Реквизиты.БезЗакрывающихДокументов КАК РасчетыПоРеализации,
	|	&ОрганизацияПрименяетУСН КАК УчитыватьЗадолженностьУСН,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ОплатаПокупателя)
	|		И НЕ Реквизиты.БезЗакрывающихДокументов
	|		И &ОрганизацияПрименяетУСНПатент КАК УчитыватьЗадолженностьУСНПатент,
	|	Реквизиты.Основание КАК Содержание,
	|	Реквизиты.Приложение КАК Приложение,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ВозвратОтПоставщика)
	|		И НЕ Реквизиты.БезЗакрывающихДокументов КАК ЭтоВозврат
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаТаблицыДокумента(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСчетаУчетаВНТТ", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПорядокНалогообложенияТорговыхТочек.Период КАК Период,
	|	ПорядокНалогообложенияТорговыхТочек.Склад КАК ТорговаяТочка,
	|	ВЫБОР
	|		КОГДА ПорядокНалогообложенияТорговыхТочек.ОсобыйПорядокНалогообложения
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД)
	|	КОНЕЦ КАК СчетДоходовОтРеализации,
	|	ВЫБОР
	|		КОГДА ПорядокНалогообложенияТорговыхТочек.ОсобыйПорядокНалогообложения
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажЕНВД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД)
	|	КОНЕЦ КАК СчетРасходовОтРеализации
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетаУчетаВНТТ
	|ИЗ
	|	РегистрСведений.ПорядокНалогообложенияТорговыхТочек.СрезПоследних(&Дата, Склад = &ТорговаяТочка) КАК ПорядокНалогообложенияТорговыхТочек";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	// в данных видах операций табличная часть Расшифровка платежа не используется
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеНаличныхВБанке
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ПрочийПриход
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ЛичныеСредстваПредпринимателя
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаРаботником Тогда 
		
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ТаблицаРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	&Дата КАК РеквизитыДата,
	|	&Организация КАК РеквизитыОрганизация,
	|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасшифровкаПлатежа.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности,
	|	&Контрагент КАК РеквизитыКонтрагент,
	|	ТаблицаРасшифровкаПлатежа.СчетНаОплату КАК СчетНаОплату,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.Сделка КАК Сделка,
	|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	ТаблицаРасшифровкаПлатежа.ПорядокОтраженияАванса КАК ПорядокОтраженияАванса,
	|	ТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК СуммаПлатежа,
	|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДС КАК СуммаНДС,
	|	&ВыручкаСНТТ КАК РеквизитыВыручкаСНТТ,
	|	&СчетКасса КАК РеквизитыСчетКасса,
	|	&ПодразделениеОрганизации КАК РеквизитыПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента.ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ДокументВРублях
	|				ТОГДА ТаблицаРасшифровкаПлатежа.СуммаПлатежа
	|			ИНАЧЕ ТаблицаРасшифровкаПлатежа.СуммаПлатежа * &КоэффициентРуб
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРуб,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ДокументВРублях
	|				ТОГДА ТаблицаРасшифровкаПлатежа.СуммаНДС
	|			ИНАЧЕ ТаблицаРасшифровкаПлатежа.СуммаНДС * &КоэффициентРуб
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСРуб,
	|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ВЫРАЗИТЬ(&Содержание КАК СТРОКА(150)) КАК Содержание
	|ПОМЕСТИТЬ ТаблицаРасшифровкаПлатежа
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И (НЕ &ЭтоОтложенноеПроведение
	|			ИЛИ ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорДляОтложенногоПроведения)";
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРасшифровкаПлатежа", НомераТаблиц.Количество());
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПогашениеЗадолженности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	// Процедура погашения задолженности выполняется для операций:
	// - ОплатаПокупателя (в режиме БезЗакрывающихДокументов = Ложь)
	// - ВозвратОтПоставщика (в режиме БезЗакрывающихДокументов = Ложь)
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
		И НЕ Реквизиты.БезЗакрывающихДокументов Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыКонтрагент КАК Контрагент,
		|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаРасшифровкаПлатежа.ВидДоговора КАК ВидДоговора,
		|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаРасшифровкаПлатежа.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
		|	ТаблицаРасшифровкаПлатежа.РасчетыВВалюте КАК РасчетыВВалюте,
		|	ТаблицаРасшифровкаПлатежа.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности,
		|	ТаблицаРасшифровкаПлатежа.Сделка КАК ДокументРасчетов,
		|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетов,
		|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
		|	ТаблицаРасшифровкаПлатежа.ПорядокОтраженияАванса КАК ПорядокОтраженияАванса,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыСчетКасса КАК КорСчет,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК КорСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК КорПодразделение,
		|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаРасшифровкаПлатежа.СуммаРуб КАК СуммаРуб,
		|	ВЫРАЗИТЬ(""Поступление наличных"" КАК СТРОКА(150)) КАК Содержание
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика
		И НЕ Реквизиты.БезЗакрывающихДокументов Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыКонтрагент КАК Контрагент,
		|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаРасшифровкаПлатежа.ВидДоговора КАК ВидДоговора,
		|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаРасшифровкаПлатежа.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
		|	ТаблицаРасшифровкаПлатежа.РасчетыВВалюте КАК РасчетыВВалюте,
		|	ТаблицаРасшифровкаПлатежа.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности,
		|	ТаблицаРасшифровкаПлатежа.Сделка КАК ДокументРасчетов,
		|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовПоАвансам КАК СчетРасчетов,
		|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом КАК СчетАвансов,
		|	ТаблицаРасшифровкаПлатежа.ПорядокОтраженияАванса КАК ПорядокОтраженияАванса,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыСчетКасса КАК КорСчет,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК КорСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК КорПодразделение,
		|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаРасшифровкаПлатежа.СуммаРуб КАК СуммаРуб,
		|	ВЫРАЗИТЬ(""Поступление наличных"" КАК СТРОКА(150)) КАК Содержание
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки";
	Иначе
		ПараметрыПроведения.Вставить("РасшифровкаПлатежа", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РасшифровкаПлатежа", НомераТаблиц.Количество());
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаРозничнаяВыручка(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПКО.РозничнаяВыручка
		Или НЕ Реквизиты.ВыручкаСНТТ
		Или Реквизиты.БезЗакрывающихДокументов Тогда
		ПараметрыПроведения.Вставить("РозничнаяВыручка", Неопределено);
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ТаблицаРасшифровкаПлатежа.РеквизитыДата КАК Период,
	|	ТаблицаРасшифровкаПлатежа.РеквизитыОрганизация КАК Организация,
	|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаРасшифровкаПлатежа.РеквизитыКонтрагент КАК Склад,
	|	ТаблицаРасшифровкаПлатежа.РеквизитыСчетКасса КАК СчетУчетаДенежныхСредств,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК СуммаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.СуммаРуб КАК СуммаРуб,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ВЫБОР
	|		КОГДА СчетаУчетаВНТТ.СчетДоходовОтРеализации ЕСТЬ НЕ NULL 
	|				И СчетаУчетаВНТТ.СчетДоходовОтРеализации <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА СчетаУчетаВНТТ.СчетДоходовОтРеализации
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &РозничнаяТорговляОблагаетсяЕНВД
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД)
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетДоходов,
	|	ВЫБОР
	|		КОГДА СчетаУчетаВНТТ.СчетРасходовОтРеализации ЕСТЬ НЕ NULL 
	|				И СчетаУчетаВНТТ.СчетРасходовОтРеализации <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА СчетаУчетаВНТТ.СчетРасходовОтРеализации
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &РозничнаяТорговляОблагаетсяЕНВД
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажЕНВД)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД)
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетРасходов,
	|	&НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ РозничнаяВыручка
	|ИЗ
	|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетаУчетаВНТТ КАК СчетаУчетаВНТТ
	|		ПО СчетаУчетаВНТТ.ТорговаяТочка = &ТорговаяТочка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РозничнаяВыручка.НомерСтроки КАК НомерСтроки,
	|	РозничнаяВыручка.Подразделение КАК Подразделение,
	|	РозничнаяВыручка.Склад КАК Склад,
	|	РозничнаяВыручка.СчетУчетаДенежныхСредств КАК СчетУчетаДенежныхСредств,
	|	РозничнаяВыручка.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	РозничнаяВыручка.СтавкаНДС КАК СтавкаНДС,
	|	РозничнаяВыручка.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	РозничнаяВыручка.СуммаРуб КАК СуммаРуб,
	|	РозничнаяВыручка.СуммаНДСРуб КАК СуммаНДСРуб,
	|	РозничнаяВыручка.СчетДоходов КАК СчетДоходов,
	|	РозничнаяВыручка.СчетРасходов КАК СчетРасходов,
	|	РозничнаяВыручка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ИЗ
	|	РозничнаяВыручка КАК РозничнаяВыручка
	|
	|УПОРЯДОЧИТЬ ПО
	|	РозничнаяВыручка.НомерСтроки";
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРозничнаяВыручка", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РозничнаяВыручка", НомераТаблиц.Количество());
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаРозничнаяВыручкаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПКО.РозничнаяВыручка
		ИЛИ НЕ Реквизиты.ВыручкаСНТТ ИЛИ НЕ ПараметрыПроведения.ПлательщикНДС Тогда
		ПараметрыПроведения.Вставить("РозничнаяВыручкаНДС", Неопределено);
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС) КАК СчетУчетаНДС,
	|	СУММА(ТаблицаДокумента.СуммаРуб - ТаблицаДокумента.СуммаНДСРуб) КАК СуммаБезНДС,
	|	СУММА(ТаблицаДокумента.СуммаНДСРуб) КАК НДС,
	|	НЕОПРЕДЕЛЕНО КАК Содержание,
	|	ТаблицаДокумента.НоменклатурнаяГруппа КАК Субконто,
	|	ТаблицаДокумента.СчетДоходов КАК СчетДоходов,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	РозничнаяВыручка КАК ТаблицаДокумента
	|ГДЕ
	|	(НЕ ТаблицаДокумента.СчетДоходов В (&МассивСчетовВыручкиЕНВД)
	|			ИЛИ ТаблицаДокумента.СуммаНДСРуб <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.СчетДоходов,
	|	ТаблицаДокумента.НоменклатурнаяГруппа";
	
	НомераТаблиц.Вставить("РозничнаяВыручкаНДС", НомераТаблиц.Количество());
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПрочееПоступление(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	// Процедура прочего поступления вызывается для операций:
	// - ОплатаПокупателя (в режиме БезЗакрывающихДокументов = Истина)
	// - РозничнаяВыручка (в режиме БезЗакрывающихДокументов = Истина)
	// - ВозвратОтПоставщика (в режиме БезЗакрывающихДокументов = Истина)
	// - ПрочийПриход
	// - ЛичныеСредстваПредпринимателя
	// - ПолучениеНаличныхВБанке
	// - ВозвратОтПодотчетногоЛица
	// - РасчетыПоКредитамИЗаймам
	// - ВозвратЗаймаРаботником
	
	Если (Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
		Или Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.РозничнаяВыручка)
		И Реквизиты.БезЗакрывающихДокументов Тогда 
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаРасшифровкаПлатежа.ПорядокОтраженияАванса = ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияАвансов.ДоходЕНВД)
		|			ИЛИ ТаблицаРасшифровкаПлатежа.ПорядокОтраженияАванса ССЫЛКА Справочник.Патенты
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД)
		|	КОНЕЦ КАК СчетКт,
		|	&ОсновнаяНоменклатурнаяГруппа КАК СубконтоКт1,
		|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК ПодразделениеКт,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыСчетКасса КАК СчетДт,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК ПодразделениеДт,
		|	ТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСумма,
		|	ТаблицаРасшифровкаПлатежа.СуммаРуб КАК СуммаРуб
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика
		И Реквизиты.БезЗакрывающихДокументов Тогда 
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходы) КАК СчетКт,
		|	&ПрочиеВнереализационныеДоходыРасходы КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК ПодразделениеКт,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыСчетКасса КАК СчетДт,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК ПодразделениеДт,
		|	ТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСумма,
		|	ТаблицаРасшифровкаПлатежа.СуммаРуб КАК СуммаРуб
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ПрочийПриход Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетКт,
		|	Реквизиты.СубконтоКт1 КАК СубконтоКт1,
		|	Реквизиты.СубконтоКт2 КАК СубконтоКт2,
		|	Реквизиты.СубконтоКт3 КАК СубконтоКт3,
		|	Реквизиты.ПодразделениеКт КАК ПодразделениеКт,
		|	Реквизиты.СчетКасса КАК СчетДт,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДт,
		|	Реквизиты.СуммаДокумента КАК ВалютнаяСумма,
		|	ВЫРАЗИТЬ(Реквизиты.СуммаДокумента * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб
		|ИЗ
		|	Реквизиты КАК Реквизиты";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ЛичныеСредстваПредпринимателя Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению) КАК СчетКт,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
		|	Реквизиты.СчетКасса КАК СчетДт,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДт,
		|	Реквизиты.СуммаДокумента КАК ВалютнаяСумма,
		|	ВЫРАЗИТЬ(Реквизиты.СуммаДокумента * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб
		|ИЗ
		|	Реквизиты КАК Реквизиты";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеНаличныхВБанке Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетКт,
		|	Реквизиты.Контрагент КАК СубконтоКт1,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
		|	Реквизиты.СчетКасса КАК СчетДт,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДт,
		|	Реквизиты.СуммаДокумента КАК ВалютнаяСумма,
		|	ВЫРАЗИТЬ(Реквизиты.СуммаДокумента * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|ГДЕ
		|	НЕ &ИспользоватьПереводыВПутиПриПеремещенияДенежныхСредств
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1,
		|	ВЫБОР
		|		КОГДА Реквизиты.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПереводыВПути)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПереводыВПутиВал)
		|	КОНЕЦ,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.ПодразделениеОрганизации,
		|	Реквизиты.СчетКасса,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.ПодразделениеОрганизации,
		|	Реквизиты.СуммаДокумента,
		|	ВЫРАЗИТЬ(Реквизиты.СуммаДокумента * &КоэффициентРуб КАК ЧИСЛО(15, 2))
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|ГДЕ
		|	&ИспользоватьПереводыВПутиПриПеремещенияДенежныхСредств"
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА Реквизиты.ДокументВРублях
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
		|	КОНЕЦ КАК СчетКт,
		|	Реквизиты.Контрагент КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
		|	Реквизиты.СчетКасса КАК СчетДт,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДт,
		|	Реквизиты.СуммаДокумента КАК ВалютнаяСумма,
		|	ВЫРАЗИТЬ(Реквизиты.СуммаДокумента * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб
		|ИЗ
		|	Реквизиты КАК Реквизиты";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам 
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаКонтрагентом
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеЗайма
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеКредита Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
		|	ТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом КАК СчетКт,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыКонтрагент КАК СубконтоКт1,
		|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК ПодразделениеКт,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыСчетКасса КАК СчетДт,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыПодразделениеОрганизации КАК ПодразделениеДт,
		|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК ВалютнаяСумма,
		|	ТаблицаРасшифровкаПлатежа.СуммаРуб КАК СуммаРуб
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаРаботником Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоПредоставленнымЗаймам) КАК СчетКт,
		|	Реквизиты.Контрагент КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
		|	Реквизиты.СчетКасса КАК СчетДт,
		|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДт,
		|	Реквизиты.СуммаДокумента КАК ВалютнаяСумма,
		|	ВЫРАЗИТЬ(Реквизиты.СуммаДокумента * &КоэффициентРуб КАК ЧИСЛО(15, 2)) КАК СуммаРуб
		|ИЗ
		|	Реквизиты КАК Реквизиты";
	Иначе
		ПараметрыПроведения.Вставить("РасшифровкаПлатежаПрочее", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РасшифровкаПлатежаПрочее", НомераТаблиц.Количество());
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстСодержанияПроводокДокумента(СтруктураПараметров)
	
	Содержание = СтруктураПараметров.Содержание
		+ ?(ПустаяСтрока(СтруктураПараметров.Приложение), "", ", " + СтруктураПараметров.Приложение);
	
	Возврат Содержание;
	
КонецФункции

Функция ТекстЗапросаОплатаСчетов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
		И НЕ Реквизиты.ЭтоОтложенноеПроведение Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
		|	ТаблицаРасшифровкаПлатежа.СчетНаОплату КАК СчетНаОплату,
		|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА СчетНаОплатуПокупателю.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаРуб
		|		ИНАЧЕ ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА СчетНаОплатуПокупателю.ВалютаДокумента = &ВалютаРеглУчета
		|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаНДСРуб
		|		КОГДА СчетНаОплатуПокупателю.ВалютаДокумента = &ВалютаДокумента
		|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаНДС
		|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.СуммаНДСРуб * ТаблицаРасшифровкаПлатежа.КратностьВзаиморасчетов / ТаблицаРасшифровкаПлатежа.КурсВзаиморасчетов КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК СуммаНДС,
		|	СчетНаОплатуПокупателю.ВалютаДокумента КАК Валюта
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
		|		ПО ТаблицаРасшифровкаПлатежа.СчетНаОплату = СчетНаОплатуПокупателю.Ссылка
		|ГДЕ
		|	ТаблицаРасшифровкаПлатежа.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки";
	Иначе
		ПараметрыПроведения.Вставить("ОплатаСчетов", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ОплатаСчетов", НомераТаблиц.Количество());
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#Область ОтложенноеПроведение

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтложенноеПроведение
		ИЛИ НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Дата) Тогда
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
		Возврат "";
	КонецЕсли;

	Если (Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика)
		И НЕ Реквизиты.БезЗакрывающихДокументов Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаРасшифровкаПлатежа.РеквизитыКонтрагент КАК Контрагент,
		|	ТаблицаРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаРасшифровкаПлатежа.ВидДоговора КАК ВидДоговора,
		|	ТаблицаРасшифровкаПлатежа.РеквизитыДата КАК Дата
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа";
	Иначе
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", НомераТаблиц.Количество());
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ОбработкаОтложенногоПроведения(Параметры, Отказ) Экспорт
	
	ПараметрыПроведения = ПодготовитьПараметрыПроведения(
		Параметры.Регистратор,
		Отказ,
		Параметры.ДоговорКонтрагента);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	

	ТаблицаВзаиморасчетов = УчетВзаиморасчетовОтложенноеПроведение.ПодготовитьТаблицуВзаиморасчетовПогашениеЗадолженности(
		Параметры, 
		ПараметрыПроведения.РасшифровкаПлатежа,
		ПараметрыПроведения.Реквизиты,
		Отказ);

	ТаблицаНДСПоРеализациямНеплательщика = УчетУСН.ПодготовитьТаблицуНДСПоРеализацииНеплательщиком(ТаблицаВзаиморасчетов,
		ПараметрыПроведения.Реквизиты);

	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура("ТаблицаРасчетов, ТаблицаНДСПродажи",
		ТаблицаВзаиморасчетов, ТаблицаНДСПоРеализациямНеплательщика);

	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияПогашениеЗадолженности(
		Параметры,
		ТаблицаВзаиморасчетов,
		ПараметрыПроведения.Реквизиты,
		Отказ);

	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияУСН(
		Параметры,
		СтруктураТаблицУСН);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновленияИБ

// Внешний интерфейс обновления ИБ

Процедура ЗаполнитьРеквизитыНастройкиУСНДляАктуальныхДокументов() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ГраницаАктуальногоПериода",
		УчетУСН.ДатаНачалаАктуальногоПериодаДляПереходаНаУпрощеннуюНастройкуКУДиР());
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", КонецГода(ДобавитьМесяц(ТекущаяДатаСеанса(), 12)));
	
	ВидыОперацийАвансаУСН = Новый Массив;
	ВидыОперацийАвансаУСН.Добавить(Перечисления.ВидыОперацийПКО.ОплатаПокупателя);
	ВидыОперацийАвансаУСН.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика);
	Запрос.УстановитьПараметр("ВидыОперацийАванса", ВидыОперацийАвансаУСН);
	
	ВидыОперацийПрочиеПоступления = Новый Массив;
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ВозвратЗаймаРаботником);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ПолучениеНаличныхВБанке);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ПрочийПриход);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.РозничнаяВыручка);
	Запрос.УстановитьПараметр("ВидыОперацийПрочиеПоступления", ВидыОперацийПрочиеПоступления);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент
	|ПОМЕСТИТЬ ВТ_РегУП
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегУПО1.Организация КАК Организация,
	|	РегУПО1.Период КАК ПериодТекущий,
	|	МИНИМУМ(РегУПО2.Период) КАК ПериодСледующий
	|ПОМЕСТИТЬ ВТ_ПериодыУчетнойПолитики
	|ИЗ
	|	ВТ_РегУП КАК РегУПО1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегУП КАК РегУПО2
	|		ПО РегУПО1.Организация = РегУПО2.Организация
	|			И РегУПО1.Период < РегУПО2.Период
	|ГДЕ
	|	(РегУПО1.ПрименяетсяУСН
	|			ИЛИ РегУПО1.ПрименяетсяУСНПатент)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегУПО1.Период,
	|	РегУПО1.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодТекущий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.Период КАК ПериодНач,
	|	ВЫБОР
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период <= &КонецПериодаВыборки
	|			ТОГДА &КонецПериодаВыборки
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период > &КонецПериодаВыборки
	|			ТОГДА КОНЕЦПЕРИОДА(НастройкиСистемыНалогообложения.Период, ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПериодыУчетнойПолитики.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|	КОНЕЦ КАК ПериодКон
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_РегУП КАК НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|		ПО НастройкиСистемыНалогообложения.Организация = ПериодыУчетнойПолитики.Организация
	|			И НастройкиСистемыНалогообложения.Период = ПериодыУчетнойПолитики.ПериодТекущий
	|ГДЕ
	|	(НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|			ИЛИ НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодНач,
	|	ПериодКон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.ДатаДокумента КАК ДатаДокумента,
	|	ВложенныйЗапрос.ВидОбработкиОбновления
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПКОРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|		ПКОРасшифровкаПлатежа.Ссылка.Дата КАК ДатаДокумента,
	|		""ЗаполнитьПорядокОтраженияАванса"" КАК ВидОбработкиОбновления
	|	ИЗ
	|		Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПКОРасшифровкаПлатежа
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|			ПО ПКОРасшифровкаПлатежа.Ссылка.Организация = ВТ_УчетнаяПолитика.Организация
	|				И ПКОРасшифровкаПлатежа.Ссылка.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|				И ПКОРасшифровкаПлатежа.Ссылка.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|	ГДЕ
	|		ПКОРасшифровкаПлатежа.ПорядокОтраженияАванса В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияАвансов.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.Патенты.ПустаяСсылка))
	|		И ПКОРасшифровкаПлатежа.Ссылка.Дата >= &ГраницаАктуальногоПериода
	|		И ПКОРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОперацийАванса)
	|		И ПКОРасшифровкаПлатежа.Ссылка.УдалитьПорядокОтраженияАванса <> ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияАвансов.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПКОРасшифровкаПлатежа.Ссылка,
	|		ПКОРасшифровкаПлатежа.Ссылка.Дата,
	|		""ЗаполнитьДоходыУСНПрочиеРасчеты""
	|	ИЗ
	|		Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПКОРасшифровкаПлатежа
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|			ПО ПКОРасшифровкаПлатежа.Ссылка.Организация = ВТ_УчетнаяПолитика.Организация
	|				И ПКОРасшифровкаПлатежа.Ссылка.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|				И ПКОРасшифровкаПлатежа.Ссылка.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|	ГДЕ
	|		ПКОРасшифровкаПлатежа.Ссылка.Дата >= &ГраницаАктуальногоПериода
	|		И ПКОРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам)
	|		И ПКОРасшифровкаПлатежа.Ссылка.Графа5_УСН <> 0
	|		И НЕ ПКОРасшифровкаПлатежа.Ссылка.УдалитьРучнаяНастройка_УСН
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПКОРасшифровкаПлатежа.Ссылка,
	|		ПКОРасшифровкаПлатежа.Ссылка.Дата
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(ПКОРасшифровкаПлатежа.ДоходыУСН) = 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПриходныйКассовыйОрдер.Ссылка,
	|		ПриходныйКассовыйОрдер.Ссылка.Дата,
	|		""ОбработатьРучнуюНастройкуУСНПрочиеРасчеты""
	|	ИЗ
	|		Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|			ПО ПриходныйКассовыйОрдер.Организация = ВТ_УчетнаяПолитика.Организация
	|				И ПриходныйКассовыйОрдер.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|				И ПриходныйКассовыйОрдер.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|	ГДЕ
	|		ПриходныйКассовыйОрдер.Дата >= &ГраницаАктуальногоПериода
	|		И ПриходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам)
	|		И ПриходныйКассовыйОрдер.УдалитьРучнаяНастройка_УСН
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПриходныйКассовыйОрдер.Ссылка,
	|		ПриходныйКассовыйОрдер.Ссылка.Дата,
	|		""ОбработатьРучнуюНастройкуУСНПрочиеПоступления""
	|	ИЗ
	|		Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|			ПО ПриходныйКассовыйОрдер.Организация = ВТ_УчетнаяПолитика.Организация
	|				И ПриходныйКассовыйОрдер.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|				И ПриходныйКассовыйОрдер.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|	ГДЕ
	|		ПриходныйКассовыйОрдер.Дата >= &ГраницаАктуальногоПериода
	|		И ПриходныйКассовыйОрдер.ВидОперации В(&ВидыОперацийПрочиеПоступления)
	|		И ПриходныйКассовыйОрдер.УдалитьРучнаяНастройка_УСН) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаПоДокументу Из РезультатЗапроса Цикл
		
		Попытка
			
			Если СтрокаПоДокументу.ВидОбработкиОбновления = "ЗаполнитьПорядокОтраженияАванса" Тогда
				ЗаполнитьПорядокОтраженияАвансаВРасшифровкеПлатежа(СтрокаПоДокументу);
			ИначеЕсли СтрокаПоДокументу.ВидОбработкиОбновления = "ЗаполнитьДоходыУСНПрочиеРасчеты" Тогда
				ЗаполнитьДоходыУСНПрочиеРасчетыВРасшифровкеПлатежа(СтрокаПоДокументу);
			ИначеЕсли СтрокаПоДокументу.ВидОбработкиОбновления = "ОбработатьРучнуюНастройкуУСНПрочиеРасчеты" Тогда
				ОбработатьРучнуюНастройкуУСНПрочиеРасчеты(СтрокаПоДокументу);
			ИначеЕсли СтрокаПоДокументу.ВидОбработкиОбновления = "ОбработатьРучнуюНастройкуУСНПрочиеПоступления" Тогда
				ОбработатьРучнуюНастройкуУСНПрочиеПоступления(СтрокаПоДокументу);
			КонецЕсли; 
			
		Исключение
			// Если не удалось обработать какой-либо документ, пропускаем и обрабатываем позднее (отложенно).
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"), 
					СтрокаПоДокументу.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПриходныйКассовыйОрдер, СтрокаПоДокументу.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПорядокОтраженияАвансаВРасшифровкеПлатежаОтложенно(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", КонецГода(ДобавитьМесяц(ТекущаяДатаСеанса(), 12)));
	
	ВидыОперацийАвансаУСН = Новый Массив;
	ВидыОперацийАвансаУСН.Добавить(Перечисления.ВидыОперацийПКО.ОплатаПокупателя);
	ВидыОперацийАвансаУСН.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика);
	Запрос.УстановитьПараметр("ВидыОперацийАванса", ВидыОперацийАвансаУСН);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент
	|ПОМЕСТИТЬ ВТ_РегУП
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегУПО1.Организация КАК Организация,
	|	РегУПО1.Период КАК ПериодТекущий,
	|	МИНИМУМ(РегУПО2.Период) КАК ПериодСледующий
	|ПОМЕСТИТЬ ВТ_ПериодыУчетнойПолитики
	|ИЗ
	|	ВТ_РегУП КАК РегУПО1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегУП КАК РегУПО2
	|		ПО РегУПО1.Организация = РегУПО2.Организация
	|			И РегУПО1.Период < РегУПО2.Период
	|ГДЕ
	|	(РегУПО1.ПрименяетсяУСН
	|			ИЛИ РегУПО1.ПрименяетсяУСНПатент)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегУПО1.Период,
	|	РегУПО1.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодТекущий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.Период КАК ПериодНач,
	|	ВЫБОР
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период <= &КонецПериодаВыборки
	|			ТОГДА &КонецПериодаВыборки
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период > &КонецПериодаВыборки
	|			ТОГДА КОНЕЦПЕРИОДА(НастройкиСистемыНалогообложения.Период, ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПериодыУчетнойПолитики.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|	КОНЕЦ КАК ПериодКон
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_РегУП КАК НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|		ПО НастройкиСистемыНалогообложения.Организация = ПериодыУчетнойПолитики.Организация
	|			И НастройкиСистемыНалогообложения.Период = ПериодыУчетнойПолитики.ПериодТекущий
	|ГДЕ
	|	(НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|			ИЛИ НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодНач,
	|	ПериодКон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ПКОРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ПКОРасшифровкаПлатежа.Ссылка.Дата КАК ДатаДокумента
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПКОРасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|		ПО ПКОРасшифровкаПлатежа.Ссылка.Организация = ВТ_УчетнаяПолитика.Организация
	|			И ПКОРасшифровкаПлатежа.Ссылка.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|			И ПКОРасшифровкаПлатежа.Ссылка.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|ГДЕ
	|	ПКОРасшифровкаПлатежа.ПорядокОтраженияАванса В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияАвансов.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.Патенты.ПустаяСсылка))
	|	И ПКОРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОперацийАванса)
	|	И ПКОРасшифровкаПлатежа.Ссылка.УдалитьПорядокОтраженияАванса <> ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияАвансов.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		Попытка
			
			ЗаполнитьПорядокОтраженияАвансаВРасшифровкеПлатежа(СтрокаРезультата);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПриходныйКассовыйОрдер, СтрокаРезультата.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаполнитьПорядокОтраженияАвансаВРасшифровкеПлатежаОтложенно
				|не удалось обработать некоторые документы Поступление наличных (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПриходныйКассовыйОрдер,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнитьПорядокОтраженияАвансаВРасшифровкеПлатежаОтложенно
					|обработала очередную порцию документов Поступление наличных: %1'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДоходыУСНПрочиеРасчетыВРасшифровкеПлатежаОтложенно(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", КонецГода(ДобавитьМесяц(ТекущаяДатаСеанса(), 12)));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|ПОМЕСТИТЬ ВТ_РегУП
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегУПО1.Организация КАК Организация,
	|	РегУПО1.Период КАК ПериодТекущий,
	|	МИНИМУМ(РегУПО2.Период) КАК ПериодСледующий
	|ПОМЕСТИТЬ ВТ_ПериодыУчетнойПолитики
	|ИЗ
	|	ВТ_РегУП КАК РегУПО1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегУП КАК РегУПО2
	|		ПО РегУПО1.Организация = РегУПО2.Организация
	|			И РегУПО1.Период < РегУПО2.Период
	|ГДЕ
	|	РегУПО1.ПрименяетсяУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РегУПО1.Период,
	|	РегУПО1.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодТекущий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.Период КАК ПериодНач,
	|	ВЫБОР
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период <= &КонецПериодаВыборки
	|			ТОГДА &КонецПериодаВыборки
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период > &КонецПериодаВыборки
	|			ТОГДА КОНЕЦПЕРИОДА(НастройкиСистемыНалогообложения.Период, ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПериодыУчетнойПолитики.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|	КОНЕЦ КАК ПериодКон
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_РегУП КАК НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|		ПО НастройкиСистемыНалогообложения.Организация = ПериодыУчетнойПолитики.Организация
	|			И НастройкиСистемыНалогообложения.Период = ПериодыУчетнойПолитики.ПериодТекущий
	|ГДЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодНач,
	|	ПериодКон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ПКОРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ПКОРасшифровкаПлатежа.Ссылка.Дата КАК ДатаДокумента
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПКОРасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|		ПО ПКОРасшифровкаПлатежа.Ссылка.Организация = ВТ_УчетнаяПолитика.Организация
	|			И ПКОРасшифровкаПлатежа.Ссылка.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|			И ПКОРасшифровкаПлатежа.Ссылка.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|ГДЕ
	|	ПКОРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам)
	|	И ПКОРасшифровкаПлатежа.Ссылка.Графа5_УСН <> 0
	|	И НЕ ПКОРасшифровкаПлатежа.Ссылка.УдалитьРучнаяНастройка_УСН
	|
	|СГРУППИРОВАТЬ ПО
	|	ПКОРасшифровкаПлатежа.Ссылка,
	|	ПКОРасшифровкаПлатежа.Ссылка.Дата
	|
	|ИМЕЮЩИЕ
	|	СУММА(ПКОРасшифровкаПлатежа.ДоходыУСН) = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		Попытка
			
			ЗаполнитьДоходыУСНПрочиеРасчетыВРасшифровкеПлатежа(СтрокаРезультата);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПриходныйКассовыйОрдер, СтрокаРезультата.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаполнитьДоходыУСНПрочиеРасчетыВРасшифровкеПлатежаОтложенно
				|не удалось обработать некоторые документы Поступление наличных (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПриходныйКассовыйОрдер,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнитьДоходыУСНПрочиеРасчетыВРасшифровкеПлатежаОтложенно
					|обработала очередную порцию документов Поступление наличных: %1'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРучнуюНастройкуУСНПрочиеРасчетыОтложенно(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", КонецГода(ДобавитьМесяц(ТекущаяДатаСеанса(), 12)));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|ПОМЕСТИТЬ ВТ_РегУП
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегУПО1.Организация КАК Организация,
	|	РегУПО1.Период КАК ПериодТекущий,
	|	МИНИМУМ(РегУПО2.Период) КАК ПериодСледующий
	|ПОМЕСТИТЬ ВТ_ПериодыУчетнойПолитики
	|ИЗ
	|	ВТ_РегУП КАК РегУПО1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегУП КАК РегУПО2
	|		ПО РегУПО1.Организация = РегУПО2.Организация
	|			И РегУПО1.Период < РегУПО2.Период
	|ГДЕ
	|	РегУПО1.ПрименяетсяУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РегУПО1.Период,
	|	РегУПО1.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодТекущий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.Период КАК ПериодНач,
	|	ВЫБОР
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период <= &КонецПериодаВыборки
	|			ТОГДА &КонецПериодаВыборки
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период > &КонецПериодаВыборки
	|			ТОГДА КОНЕЦПЕРИОДА(НастройкиСистемыНалогообложения.Период, ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПериодыУчетнойПолитики.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|	КОНЕЦ КАК ПериодКон
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_РегУП КАК НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|		ПО НастройкиСистемыНалогообложения.Организация = ПериодыУчетнойПолитики.Организация
	|			И НастройкиСистемыНалогообложения.Период = ПериодыУчетнойПолитики.ПериодТекущий
	|ГДЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодНач,
	|	ПериодКон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ПриходныйКассовыйОрдер.Ссылка КАК Ссылка,
	|	ПриходныйКассовыйОрдер.Ссылка.Дата КАК ДатаДокумента
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|		ПО ПриходныйКассовыйОрдер.Организация = ВТ_УчетнаяПолитика.Организация
	|			И ПриходныйКассовыйОрдер.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|			И ПриходныйКассовыйОрдер.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|ГДЕ
	|	ПриходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам)
	|	И ПриходныйКассовыйОрдер.УдалитьРучнаяНастройка_УСН
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		Попытка
			
			ОбработатьРучнуюНастройкуУСНПрочиеРасчеты(СтрокаРезультата);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"), 
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПриходныйКассовыйОрдер, СтрокаРезультата.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ОбработатьРучнуюНастройкуУСНПрочиеРасчетыОтложенно
				|не удалось обработать некоторые документы Поступление наличных (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПриходныйКассовыйОрдер,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ОбработатьРучнуюНастройкуУСНПрочиеРасчетыОтложенно
					|обработала очередную порцию документов Поступление наличных: %1'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРучнуюНастройкуУСНПрочиеПоступленияОтложенно(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", КонецГода(ДобавитьМесяц(ТекущаяДатаСеанса(), 12)));
	
	ВидыОперацийПрочиеПоступления = Новый Массив;
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ВозвратЗаймаРаботником);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ПолучениеНаличныхВБанке);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.ПрочийПриход);
	ВидыОперацийПрочиеПоступления.Добавить(Перечисления.ВидыОперацийПКО.РозничнаяВыручка);
	Запрос.УстановитьПараметр("ВидыОперацийПрочиеПоступления", ВидыОперацийПрочиеПоступления);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|ПОМЕСТИТЬ ВТ_РегУП
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегУПО1.Организация КАК Организация,
	|	РегУПО1.Период КАК ПериодТекущий,
	|	МИНИМУМ(РегУПО2.Период) КАК ПериодСледующий
	|ПОМЕСТИТЬ ВТ_ПериодыУчетнойПолитики
	|ИЗ
	|	ВТ_РегУП КАК РегУПО1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегУП КАК РегУПО2
	|		ПО РегУПО1.Организация = РегУПО2.Организация
	|			И РегУПО1.Период < РегУПО2.Период
	|ГДЕ
	|	РегУПО1.ПрименяетсяУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РегУПО1.Период,
	|	РегУПО1.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодТекущий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	НастройкиСистемыНалогообложения.Период КАК ПериодНач,
	|	ВЫБОР
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период <= &КонецПериодаВыборки
	|			ТОГДА &КонецПериодаВыборки
	|		КОГДА ПериодыУчетнойПолитики.ПериодСледующий ЕСТЬ NULL 
	|				И НастройкиСистемыНалогообложения.Период > &КонецПериодаВыборки
	|			ТОГДА КОНЕЦПЕРИОДА(НастройкиСистемыНалогообложения.Период, ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПериодыУчетнойПолитики.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|	КОНЕЦ КАК ПериодКон
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_РегУП КАК НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|		ПО НастройкиСистемыНалогообложения.Организация = ПериодыУчетнойПолитики.Организация
	|			И НастройкиСистемыНалогообложения.Период = ПериодыУчетнойПолитики.ПериодТекущий
	|ГДЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодНач,
	|	ПериодКон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ПриходныйКассовыйОрдер.Ссылка КАК Ссылка,
	|	ПриходныйКассовыйОрдер.Ссылка.Дата КАК ДатаДокумента
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|		ПО ПриходныйКассовыйОрдер.Организация = ВТ_УчетнаяПолитика.Организация
	|			И ПриходныйКассовыйОрдер.Дата >= ВТ_УчетнаяПолитика.ПериодНач
	|			И ПриходныйКассовыйОрдер.Дата <= ВТ_УчетнаяПолитика.ПериодКон
	|ГДЕ
	|	ПриходныйКассовыйОрдер.ВидОперации В(&ВидыОперацийПрочиеПоступления)
	|	И ПриходныйКассовыйОрдер.УдалитьРучнаяНастройка_УСН
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		Попытка
			
			ОбработатьРучнуюНастройкуУСНПрочиеПоступления(СтрокаРезультата);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"), 
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПриходныйКассовыйОрдер, СтрокаРезультата.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ОбработатьРучнуюНастройкуУСНПрочиеПоступленияОтложенно
				|не удалось обработать некоторые документы Поступление наличных (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПриходныйКассовыйОрдер,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ОбработатьРучнуюНастройкуУСНПрочиеПоступленияОтложенно
					|обработала очередную порцию документов Поступление наличных: %1'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьКредитыИЗаймы(Параметры) Экспорт
	
	СчетаРасчетовПоВыданнымЗаймам = Новый Массив();
	СчетаРасчетовПоВыданнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ПредоставленныеЗаймы);
	
	СчетаРасчетовПоПолученнымЗаймам = Новый Массив();
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.КраткосрочныеЗаймы);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.КраткосрочныеЗаймыВал);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймам);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймамВал);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеЗаймы);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеЗаймыВал);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймам);
	СчетаРасчетовПоПолученнымЗаймам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймамВал);
	
	СчетаРасчетовПоПолученнымКредитам = Новый Массив();
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.КраткосрочныеКредиты);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.КраткосрочныеКредитыВал);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымКредитам);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымКредитамВал);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеКредиты);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеКредитыВал);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымКредитам);
	СчетаРасчетовПоПолученнымКредитам.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымКредитамВал);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВыданныеЗаймы", 		СчетаРасчетовПоВыданнымЗаймам); 
	Запрос.УстановитьПараметр("ПолученныеЗаймы", 	СчетаРасчетовПоПолученнымЗаймам);
	Запрос.УстановитьПараметр("ПолученныеКредиты", 	СчетаРасчетовПоПолученнымКредитам);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка КАК ПриходныйКассовыйОрдер,
	|	СРЕДНЕЕ(ВЫБОР
	|			КОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом В (&ВыданныеЗаймы)
	|				ТОГДА 1
	|			КОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом В (&ПолученныеЗаймы)
	|				ТОГДА 2
	|			КОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом В (&ПолученныеКредиты)
	|				ТОГДА 3
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КодВидаОперации,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом В (&ВыданныеЗаймы)
	|				ТОГДА 1
	|			КОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом В (&ПолученныеЗаймы)
	|				ТОГДА 2
	|			КОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом В (&ПолученныеКредиты)
	|				ТОГДА 3
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КодВидаОперацииМинимум
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РасчетыПоКредитамИЗаймам)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			Если Не ЗначениеЗаполнено(Выборка.ПриходныйКассовыйОрдер) Тогда
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект = Выборка.ПриходныйКассовыйОрдер.ПолучитьОбъект();
			
			Если Выборка.КодВидаОперации <> Выборка.КодВидаОперацииМинимум 
				ИЛИ Выборка.КодВидаОперации = 0 Тогда
				ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеЗайма;
			ИначеЕсли Выборка.КодВидаОперации = 1 Тогда
				ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаКонтрагентом;
			ИначеЕсли Выборка.КодВидаОперации = 2 Тогда
				ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеЗайма;
			ИначеЕсли Выборка.КодВидаОперации = 3 Тогда
				ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеКредита;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось опеределить вид расчетов по кредитам и займам в Поступление наличных ""%1"" по причине:
				|%2'"), 
				Выборка.ПриходныйКассовыйОрдер,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,, 
				Выборка.ПриходныйКассовыйОрдер, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		
		Параметры.ОбработкаЗавершена = Истина;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Ложь;
		
		Если ОбъектовОбработано = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедуре ПриходныйКассовыйОрдер.ОбработатьКредитыИЗаймы
				|не удалось опеределить вид расчетов по кредитам и займам в %1 документах Поступление наличных.'"), 
				ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
		Иначе
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ПриходныйКассовыйОрдер.ОбработатьКредитыИЗаймы
				|обработала очередную порцию документов Поступление наличных: %1 элементов'"), 
				ОбъектовОбработано));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Служебные обработчики обновления ИБ

// Заполняет новый реквизит "ПорядокОтраженияАванса" в ТЧ "РасшифровкаПлатежа" у переданного документа.
//
Процедура ЗаполнитьПорядокОтраженияАвансаВРасшифровкеПлатежа(СтрокаПоДокументу)
	
	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПриходныйКассовыйОрдер");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаПоДокументу.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = СтрокаПоДокументу.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ДокументОбъект.УдалитьПорядокОтраженияАванса) Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли; 
		
		// Обработка объекта.
		// 1. Переносим порядок отражения авансов из шапки в ТЧ
		ПорядокОтраженияАвансаДляТЧ = Неопределено;
		Если ДокументОбъект.УдалитьПорядокОтраженияАванса = Перечисления.ПорядокОтраженияАвансов.ДоходПатент Тогда
			ПорядокОтраженияАвансаДляТЧ = ДокументОбъект.Патент;
		Иначе
			ПорядокОтраженияАвансаДляТЧ = ДокументОбъект.УдалитьПорядокОтраженияАванса;
		КонецЕсли;
		
		Для каждого СтрокаПлатеж Из ДокументОбъект.РасшифровкаПлатежа Цикл
			СтрокаПлатеж.ПорядокОтраженияАванса = ПорядокОтраженияАвансаДляТЧ;
		КонецЦикла;
		
		ДокументОбъект.УдалитьПорядокОтраженияАванса = Перечисления.ПорядокОтраженияАвансов.ПустаяСсылка();
		
		// 2. Если отражение в КУДиР настроено вручную (допускалось при УСН-доходы) - установим признак ручной корректировки движений
		Если ДокументОбъект.УдалитьРучнаяНастройка_УСН Тогда
			ДокументОбъект.РучнаяКорректировка = Истина;
			ДокументОбъект.УдалитьРучнаяНастройка_УСН = Ложь;
		КонецЕсли; 
		
		// Запись обработанного объекта.
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Заполняет новый реквизит "ДоходыУСН" в ТЧ "РасшифровкаПлатежа" у переданного документа
// (для ситуации, когда настройка УСН не редактировалась пользователем).
//
Процедура ЗаполнитьДоходыУСНПрочиеРасчетыВРасшифровкеПлатежа(СтрокаПоДокументу)

	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПриходныйКассовыйОрдер");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаПоДокументу.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = СтрокаПоДокументу.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		Если ДокументОбъект.Графа5_УСН = ДокументОбъект.РасшифровкаПлатежа.Итог("ДоходыУСН")
			И НЕ ДокументОбъект.УдалитьРучнаяНастройка_УСН Тогда
			// документ уже обработан
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		// Обработка объекта.
		КоэффициентыРаспределения = ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("СуммаПлатежа");
		МассивРаспределенныхДоходов = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(
			ДокументОбъект.Графа5_УСН, КоэффициентыРаспределения);
		Если МассивРаспределенныхДоходов <> Неопределено Тогда
			ДокументОбъект.РасшифровкаПлатежа.ЗагрузитьКолонку(МассивРаспределенныхДоходов, "ДоходыУСН");
		КонецЕсли;
		
		// Запись обработанного объекта.
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Для поступлений по прочим расчетам, в которых настройка УСН отредактирована пользователем вручную:
//    если отредактированы только доходы УСН - распределяет сумму доходов по строкам ТЧ "РасшифровкаПлатежа";
//    если иные настройки КУДиР отличаются от умолчаний - выставляется признак ручной корректировки движений
Процедура ОбработатьРучнуюНастройкуУСНПрочиеРасчеты(СтрокаПоДокументу)
	
	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПриходныйКассовыйОрдер");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаПоДокументу.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = СтрокаПоДокументу.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		Если НЕ ДокументОбъект.УдалитьРучнаяНастройка_УСН Тогда
			// документ уже обработан
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		// Обработка объекта.
		Если ДокументОбъект.Графа5_УСН <> 0 И ДокументОбъект.РасшифровкаПлатежа.Количество() <> 0 Тогда
			КоэффициентыРаспределения = ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("СуммаПлатежа");
			МассивРаспределенныхДоходов = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(
				ДокументОбъект.Графа5_УСН, КоэффициентыРаспределения);
			ДокументОбъект.РасшифровкаПлатежа.ЗагрузитьКолонку(МассивРаспределенныхДоходов, "ДоходыУСН");
		КонецЕсли;
		
		Если ДокументОбъект.Графа6_УСН <> 0 ИЛИ ДокументОбъект.Графа7_УСН <> 0 ИЛИ ДокументОбъект.НДС_УСН <> 0
			ИЛИ ДокументОбъект.ДоходыЕНВД_УСН ИЛИ ДокументОбъект.РасходыЕНВД_УСН Тогда
				ДокументОбъект.РучнаяКорректировка = Истина;
		КонецЕсли;
		
		ДокументОбъект.УдалитьРучнаяНастройка_УСН = Ложь;
		
		// Запись обработанного объекта.
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Для прочих поступлений, в которых настройка УСН отредактирована пользователем вручную:
//    если отредактированы только доходы УСН, и вид операции "ПрочееПоступление" - ручная настройка УСН сбрасывается;
//    для остальных видов операций - выставляется признак ручной корректировки движений.
Процедура ОбработатьРучнуюНастройкуУСНПрочиеПоступления(СтрокаПоДокументу)
	
	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПриходныйКассовыйОрдер");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаПоДокументу.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = СтрокаПоДокументу.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		Если НЕ ДокументОбъект.УдалитьРучнаяНастройка_УСН Тогда
			// документ уже обработан
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПКО.ПрочийПриход Тогда
			Если НЕ ДокументОбъект.РучнаяКорректировка И (ДокументОбъект.Графа6_УСН <> 0 ИЛИ ДокументОбъект.Графа7_УСН <> 0 ИЛИ ДокументОбъект.НДС_УСН <> 0
				ИЛИ ДокументОбъект.ДоходыЕНВД_УСН ИЛИ ДокументОбъект.РасходыЕНВД_УСН) Тогда
				ДокументОбъект.РучнаяКорректировка = Истина;
				// Иначе: если заданы только ДоходыУСН (Графа 5) - признак ручной корректировки не изменяем
			КонецЕсли;
		Иначе
			ДокументОбъект.РучнаяКорректировка = Истина;
		КонецЕсли;
		
		ДокументОбъект.УдалитьРучнаяНастройка_УСН = Ложь;
		
		// Запись обработанного объекта.
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приходный кассовый ордер (КО-1)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПКО";
	КомандаПечати.Представление = НСтр("ru = 'Приходный кассовый ордер (КО-1)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ФункциональныеОпции = "ИспользуетсяКассоваяКнига";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Поступление наличных""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка,Обработка.ПеренумерацияДокументов.Форма.Форма,Форма";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

// Определяет коды счетов дебета и кредита для вывода в печатную форму.
//
Функция ОпределитьКодыСчетСубсчет(ВалютаРегламентированногоУчета, Шапка)
	
	ЕстьРасчетыСКонтрагентами = УчетДенежныхСредствКлиентСервер.ЕстьРасчетыСКонтрагентами(Шапка.ВидОперации);
	ЕстьРасчетыПоКредитам     = УчетДенежныхСредствКлиентСервер.ЕстьРасчетыПоКредитам(Шапка.ВидОперации);
	
	КодыСчетов   = Новый Структура("Дебет, Кредит");
	
	// Определяем код счета дебета
	КассаВВалюте = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Шапка.СчетКасса).Валютный;
	ВалютаРег    = ВалютаРегламентированногоУчета;
	
	СчетДт = Шапка.СчетКасса;
	КодыСчетов.Дебет = СчетДт;
	
	Если Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица Тогда
		Если КассаВВалюте Тогда
			СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал).Код;
		Иначе
			СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами).Код;
		КонецЕсли;
	ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.ПрочийПриход Тогда
		СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Шапка.СчетУчетаРасчетовСКонтрагентом).Код;
	ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеНаличныхВБанке Тогда
		СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Шапка.СчетУчетаРасчетовСКонтрагентом).Код;
	ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.РозничнаяВыручка Тогда
		СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Шапка.СчетУчетаРасчетовСКонтрагентом).Код;
		// При проведении розничной выручки данные из реквизита СчетУчетаРасчетовСКонтрагентом в проводках не используются.
		// Проводок в этом случае в документе может вообще не быть - они делаются в другом документе - ОРП,
		// а ПКО нужен только для отражения в Кассовой книге.
		// Из-за этого может быть рассинхронизация, когда в проводках проходит субсчет 90.01.2, а при печати выходит 90.01.1 (и наоборот).
		// Бывают и более сложные случаи, когда в исходном документе (ОРП), проводки проходят сразу по обоим субсчетам счета 90.01.
		// Более корректно в таком случае выводить головной счет 90.01, чем пытаться "угадать" нужный субсчет.
		Если СтрНачинаетсяС(СтрокаКредит, "90.01") Тогда
			СтрокаКредит = "90.01";
		КонецЕсли;
	ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаРаботником Тогда
		СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыПоПредоставленнымЗаймам).Код;
	ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.ЛичныеСредстваПредпринимателя Тогда
		СтрокаКредит = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению).Код;
	ИначеЕсли ЕстьРасчетыСКонтрагентами ИЛИ ЕстьРасчетыПоКредитам Тогда
		ТабСчет = Шапка.РасшифровкаПлатежа.Выгрузить();
		ТабСчет.Свернуть("СчетУчетаРасчетовСКонтрагентом, СчетУчетаРасчетовПоАвансам", "СуммаПлатежа");
		СтрокаКредит = "";
		Для каждого Строка Из ТабСчет Цикл
			СчетУчетаРасчетовСКонтрагентомКод = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Строка.СчетУчетаРасчетовСКонтрагентом).Код;
			СчетУчетаРасчетовПоАвансамКод     = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Строка.СчетУчетаРасчетовПоАвансам).Код;
			Если НЕ Строка.СчетУчетаРасчетовСКонтрагентом.Пустая()
				И СтрНайти(СтрокаКредит, СчетУчетаРасчетовСКонтрагентомКод) = 0 Тогда
				СтрокаКредит = СтрокаКредит + ", " + СчетУчетаРасчетовСКонтрагентомКод;
			КонецЕсли;
			Если НЕ Строка.СчетУчетаРасчетовПоАвансам.Пустая()
				И СтрНайти(СтрокаКредит, СчетУчетаРасчетовПоАвансамКод) = 0 Тогда
				СтрокаКредит = СтрокаКредит + ", " + СчетУчетаРасчетовПоАвансамКод;
			КонецЕсли;
		КонецЦикла;
		
		СтрокаКредит = Сред(СтрокаКредит, 2);
	КонецЕсли;
	
	КодыСчетов.Кредит = СтрокаКредит;
	
	Возврат КодыСчетов;
	
КонецФункции

Функция РубКоп(Сумма)
	
	Руб = Цел(Сумма);
	Коп = Окр(100 *(Сумма - Руб), 0, 1);
	СуммаРубКоп = "" + Руб + " руб. " + Цел(Коп / 10) + (Коп - 10 * Цел(Коп / 10)) + " коп.";
	
	Возврат СуммаРубКоп;
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
//
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыПКО()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Номер,
	|	ПриходныйКассовыйОрдер.Дата КАК ДатаДокумента,
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.Организация,
	|	ПриходныйКассовыйОрдер.СчетКасса,
	|	ПриходныйКассовыйОрдер.СчетУчетаРасчетовСКонтрагентом,
	|	ПриходныйКассовыйОрдер.Контрагент,
	|	ПриходныйКассовыйОрдер.ВидОперации,
	|	ПриходныйКассовыйОрдер.Контрагент.Представление КАК Контрагент,
	|	ПриходныйКассовыйОрдер.ПринятоОт КАК ПринятоОт,
	|	ПриходныйКассовыйОрдер.Основание КАК Основание,
	|	ПриходныйКассовыйОрдер.Приложение КАК Приложение,
	|	ПриходныйКассовыйОрдер.СуммаДокумента КАК Сумма,
	|	ПриходныйКассовыйОрдер.ВалютаДокумента,
	|	ПриходныйКассовыйОрдер.ВалютаДокумента.Представление КАК ВалютаПредставление,
	|	ПриходныйКассовыйОрдер.ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ПриходныйКассовыйОрдер.ПодразделениеОрганизации.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПриходныйКассовыйОрдер.ПодразделениеОрганизации.Наименование
	|		ИНАЧЕ ПриходныйКассовыйОрдер.ПодразделениеОрганизации.НаименованиеПолное
	|	КОНЕЦ КАК ПредставлениеПодразделения,
	|	ПриходныйКассовыйОрдер.СтавкаНДС,
	|	ПриходныйКассовыйОрдер.РасшифровкаПлатежа.(
	|		Ссылка,
	|		НомерСтроки,
	|		ДоговорКонтрагента,
	|		Сделка,
	|		КурсВзаиморасчетов,
	|		СуммаПлатежа,
	|		КратностьВзаиморасчетов,
	|		СуммаВзаиморасчетов,
	|		СтавкаНДС,
	|		СуммаНДС,
	|		СтатьяДвиженияДенежныхСредств,
	|		СчетУчетаРасчетовСКонтрагентом,
	|		СчетУчетаРасчетовПоАвансам,
	|		СчетНаОплату,
	|		СпособПогашенияЗадолженности
	|	)
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПриходныйКассовыйОрдер.Дата,
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.РасшифровкаПлатежа.НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ с печатной формой ПКО
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьПКО(МассивОбъектов, ОбъектыПечати)
	Перем ПодразделениеОтветственныхЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходныйКассовыйОрдер_КО1";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КО1");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыПКО();
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ЕстьРасчетыСКонтрагентами = УчетДенежныхСредствКлиентСервер.ЕстьРасчетыСКонтрагентами(Шапка.ВидОперации);
		ЕстьРасчетыПоКредитам     = УчетДенежныхСредствКлиентСервер.ЕстьРасчетыПоКредитам(Шапка.ВидОперации);
		
		Валютный = Шапка.ВалютаДокумента <> ВалютаРегламентированногоУчета;
		
		// Выводим шапку ПКО
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
		
		ОбластьМакета.Параметры.СуммаРубКоп       = ?(Валютный, Формат(Шапка.Сумма, "ЧЦ=15; ЧДЦ=2") + " " + СокрЛП(Шапка.ВалютаДокумента), РубКоп(Шапка.Сумма));
		ОбластьМакета.Параметры.Сумма             = Формат(Шапка.Сумма, "ЧЦ=15; ЧДЦ=2") + ?(Валютный, " " + СокрЛП(Шапка.ВалютаДокумента), "");
		ОбластьМакета.Параметры.СуммаПрописью     = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(Шапка.Сумма, Шапка.ВалютаДокумента);
		
		ОбластьМакета.Параметры.ОрганизацияПоОКПО = СведенияОбОрганизации.КодПоОКПО;
		ОбластьМакета.Параметры.ДатаДокумента     = Шапка.ДатаДокумента;
		ОбластьМакета.Параметры.НомерДокумента    = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Ложь);
		
		КодыСчетСубсчет = ОпределитьКодыСчетСубсчет(ВалютаРегламентированногоУчета, Шапка);
		
		ОбластьМакета.Параметры.КодДебета = КодыСчетСубсчет.Дебет;
		ОбластьМакета.Параметры.СубСчет   = КодыСчетСубсчет.Кредит;
		
		ТабНДС = Шапка.РасшифровкаПлатежа.Выгрузить();
		ТабНДС.Свернуть("СтавкаНДС", "СуммаНДС");
		
		ТекстСуммаНДС = "";
		
		Если ЕстьРасчетыСКонтрагентами ИЛИ ЕстьРасчетыПоКредитам 
			ИЛИ Шапка.ВидОперации = Перечисления.ВидыОперацийПКО.РозничнаяВыручка Тогда
			
			Для каждого СтрокаНДС Из ТабНДС Цикл
				ТекстСуммаНДС = ТекстСуммаНДС + Символы.ПС + "НДС ("
					+ ?(НЕ ЗначениеЗаполнено(СтрокаНДС.СтавкаНДС), "без налога", СтрокаНДС.СтавкаНДС) + ") "
					+ Формат(СтрокаНДС.СуммаНДС, "ЧЦ=15;ЧДЦ=2;ЧРД=-;ЧН=0-00") + " " + Шапка.ВалютаПредставление;
			КонецЦикла;
			
		Иначе
			ТекстСуммаНДС = ТекстСуммаНДС + Символы.ПС + "НДС (без налога) "
				+ Формат(0, "ЧЦ=15;ЧДЦ=2;ЧРД=-;ЧН=0-00") + " " + Шапка.ВалютаПредставление;
		КонецЕсли;
		
		ТекстСуммаНДС = Сред(ТекстСуммаНДС, 2);
		
		ОбластьМакета.Параметры.ВТомЧисле = ТекстСуммаНДС;
		
		ПодразделениеОтветственныхЛиц = Шапка.ПодразделениеОрганизации;
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.ДатаДокумента, ПодразделениеОтветственныхЛиц);
		
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		ОбластьМакета.Параметры.ФИОКассира            = ОтветственныеЛица.КассирПредставление;
		
		ОбластьМакета.Параметры.ПринятоОт  = Шапка.ПринятоОт;
		ОбластьМакета.Параметры.Основание  = Шапка.Основание;
		ОбластьМакета.Параметры.Приложение = Шапка.Приложение;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// В табличном документе зададим имя области, в которую был
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПКО") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"ПКО", "Приходный кассовый ордер", ПечатьПКО(МассивОбъектов, ОбъектыПечати),,
			"ОбщийМакет.ПФ_MXL_КО1");
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

Функция НаименованиеВСтрокеЧека(СтруктураШапки) Экспорт
	
	Если ЗначениеЗаполнено(СтруктураШапки.ПринятоОт) Тогда
		
		ШаблонНаименования = НСтр("ru = 'Оплата от: %1%2Основание: %3'");
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНаименования, СтруктураШапки.ПринятоОт, " ", СтруктураШапки.Основание);
		
	Иначе
		
		ШаблонНаименования = НСтр("ru = 'Основание: %1'");
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНаименования, СтруктураШапки.Основание);
		
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаРеквизитыПечатиЧека(ИменаТаблиц)
	
	ИменаТаблиц.Добавить("ВТ_РасшифровкаПлатежа");
	ИменаТаблиц.Добавить("ВТ_ДоговораПлатежногоАгента");
	ИменаТаблиц.Добавить("ВТ_РеквизитыШапки");
	ИменаТаблиц.Добавить("ВТ_РеализацииПоСчету");
	ИменаТаблиц.Добавить("РеквизитыПечатиЧека");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента.Владелец КАК Контрагент,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ПриходныйКассовыйОрдерРасшифровкаПлатежа.Сделка КАК Документ.РеализацияТоваровУслуг)) <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.Сделка
	|		ИНАЧЕ ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетНаОплату
	|	КОНЕЦ КАК Сделка,
	|	СУММА(ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ВТ_РасшифровкаПлатежа
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ПриходныйКассовыйОрдерРасшифровкаПлатежа.Сделка КАК Документ.РеализацияТоваровУслуг)) <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПриходныйКассовыйОрдерРасшифровкаПлатежа.Сделка
	|		ИНАЧЕ ПриходныйКассовыйОрдерРасшифровкаПлатежа.СчетНаОплату
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорПлатежногоАгента,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента.Владелец КАК ПлатежныйАгент
	|ПОМЕСТИТЬ ВТ_ДоговораПлатежногоАгента
	|ИЗ
	|	ВТ_РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента.ПлатежныйАгент
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.СубконтоКт2,
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.СубконтоКт1
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ВЫРАЗИТЬ(ПриходныйКассовыйОрдер.СубконтоКт2 КАК Справочник.ДоговорыКонтрагентов).ПлатежныйАгент
	|	И ПриходныйКассовыйОрдер.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Ссылка КАК Ссылка,
	|	ПриходныйКассовыйОрдер.Дата КАК Дата,
	|	ПриходныйКассовыйОрдер.Номер КАК Номер,
	|	ПриходныйКассовыйОрдер.Организация КАК Организация,
	|	ПриходныйКассовыйОрдер.Патент КАК Патент,
	|	ПриходныйКассовыйОрдер.Основание КАК Основание,
	|	ПриходныйКассовыйОрдер.ВидОперации КАК ВидОперации,
	|	ПриходныйКассовыйОрдер.Контрагент КАК Контрагент,
	|	ПриходныйКассовыйОрдер.ПринятоОт КАК ПринятоОт,
	|	ПриходныйКассовыйОрдер.СтавкаНДС КАК СтавкаНДС,
	|	ПриходныйКассовыйОрдер.СуммаДокумента КАК СуммаДокумента,
	|	ПриходныйКассовыйОрдер.ВалютаДокумента КАК ВалютаДокумента,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ЕСТЬNULL(ВТ_ДоговораПлатежногоАгента.ДоговорПлатежногоАгента, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК ДоговорПлатежногоАгента,
	|	ЕСТЬNULL(ВТ_ДоговораПлатежногоАгента.ПлатежныйАгент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК ПлатежныйАгент,
	|	ЕСТЬNULL(ВТ_ДоговораПлатежногоАгента.КоличествоДоговоров, 0) > 1 КАК НесколькоДоговоровПлатежногоАгента
	|ПОМЕСТИТЬ ВТ_РеквизитыШапки
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТ_ДоговораПлатежногоАгента.Ссылка КАК Ссылка,
	|			МАКСИМУМ(ВТ_ДоговораПлатежногоАгента.ДоговорПлатежногоАгента) КАК ДоговорПлатежногоАгента,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ДоговораПлатежногоАгента.ДоговорПлатежногоАгента) КАК КоличествоДоговоров,
	|			ВТ_ДоговораПлатежногоАгента.ПлатежныйАгент КАК ПлатежныйАгент
	|		ИЗ
	|			ВТ_ДоговораПлатежногоАгента КАК ВТ_ДоговораПлатежногоАгента
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВТ_ДоговораПлатежногоАгента.Ссылка,
	|			ВТ_ДоговораПлатежногоАгента.ПлатежныйАгент) КАК ВТ_ДоговораПлатежногоАгента
	|		ПО ПриходныйКассовыйОрдер.Ссылка = ВТ_ДоговораПлатежногоАгента.Ссылка
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Ссылка КАК РеализацияТоваров,
	|	ВТ_РасшифровкаПлатежа.Сделка КАК СчетНаОплату
	|ПОМЕСТИТЬ ВТ_РеализацииПоСчету
	|ИЗ
	|	ВТ_РасшифровкаПлатежа КАК ВТ_РасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_РасшифровкаПлатежа.Сделка = РеализацияТоваровУслуг.СчетНаОплатуПокупателю
	|ГДЕ
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Проведен, ЛОЖЬ)
	|	И ВТ_РасшифровкаПлатежа.Сделка <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РеквизитыШапки.Ссылка КАК Ссылка,
	|	ВТ_РеквизитыШапки.Организация КАК Организация,
	|	ВТ_РеквизитыШапки.СуммаДокумента КАК СуммаДокумента,
	|	ВТ_РеквизитыШапки.Контрагент КАК Контрагент,
	|	ИСТИНА КАК ВыводитьКонтрагентаВЧеке,
	|	ВТ_РеквизитыШапки.ПринятоОт КАК ПринятоОт,
	|	ВТ_РеквизитыШапки.Основание КАК Основание,
	|	ВТ_РеквизитыШапки.Дата КАК Дата,
	|	ВТ_РеквизитыШапки.Номер КАК Номер,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОплатыККТ.Наличные) КАК ТипОплаты,
	|	ВЫБОР ВТ_РеквизитыШапки.ВидОперации
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ВозвратОтПоставщика)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств)
	|	КОНЕЦ КАК ТипРасчета,
	|	ВТ_РеквизитыШапки.Патент КАК Патент,
	|	ВЫБОР
	|		КОГДА ВТ_РеквизитыШапки.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ВозвратОтПоставщика)
	|				ИЛИ ВТ_РеквизитыШапки.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ОплатаПокупателя)
	|			ТОГДА ВТ_РеквизитыШапки.СтавкаНДС
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	КОНЕЦ КАК СтавкаНДС,
	|	ВТ_РеквизитыШапки.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ВТ_РеквизитыШапки.ПлатежныйАгент КАК ПлатежныйАгент,
	|	ВТ_РеквизитыШапки.НесколькоДоговоровПлатежногоАгента КАК НесколькоДоговоровПлатежногоАгента,
	|	ВТ_РеквизитыШапки.ВалютаДокумента КАК ВалютаДокумента
	|ИЗ
	|	ВТ_РеквизитыШапки КАК ВТ_РеквизитыШапки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

#КонецОбласти

Процедура ЗаполнитьПоОтчетуОРозничныхПродажах(Объект, Основание) Экспорт
	
	СтрокаПлатежа = Объект.РасшифровкаПлатежа.Добавить();
	Объект.ВидОперации   = Перечисления.ВидыОперацийПКО.РозничнаяВыручка;
	Объект.Контрагент    = Основание.Склад;
	Объект.СчетКасса     = Основание.СчетКасса;
	Объект.СтатьяДвиженияДенежныхСредств = Основание.СтатьяДвиженияДенежныхСредств;
	Если Основание.ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах Тогда
		Объект.ВыручкаСНТТ = Истина;
	КонецЕсли;
	
	// Поле "Счет операционной кассы" заполняем из отчета о розничных продажах при условии, что он там был один
	СчетОперационнойКассы = Документы.ПриходныйКассовыйОрдер.СчетКассыИзОРП(Основание);
	Если ЗначениеЗаполнено(СчетОперационнойКассы) Тогда
		Объект.СчетУчетаРасчетовСКонтрагентом = СчетОперационнойКассы;
	Иначе
		РозничнаяТорговляОблагаетсяЕНВД = УчетнаяПолитика.РозничнаяТорговляОблагаетсяЕНВД(Объект.Организация, Объект.Дата);
		СчетаДоходовРасходов = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаВНТТ(Объект.Организация, Объект.Контрагент, Объект.Дата,
			Новый Структура("РозничнаяТорговляОблагаетсяЕНВД", РозничнаяТорговляОблагаетсяЕНВД));
		Объект.СчетУчетаРасчетовСКонтрагентом = СчетаДоходовРасходов.СчетДоходовОтРеализации;
	КонецЕсли;
	
	ТаблицаПлатежей = Объект.РасшифровкаПлатежа.ВыгрузитьКолонки();
	
	ТаблицаСуммОснования = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДСВРазрезеСтавокНДС(Основание);
	ТаблицаСуммОснования.Колонки.Сумма.Имя = "СуммаПлатежа";
	
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаСуммОснования, ТаблицаПлатежей);
	Если ТаблицаПлатежей.Количество() = 0 Тогда
		ТаблицаПлатежей.Добавить();
	КонецЕсли;
	
	// Вычитаем оплату по безналичному расчету
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СУММА(ОтчетОРозничныхПродажахОплата.СуммаОплаты) КАК СуммаБезналичнойОплаты
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Оплата КАК ОтчетОРозничныхПродажахОплата
	|ГДЕ
	|	ОтчетОРозничныхПродажахОплата.Ссылка = &Основание
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОтчетОРозничныхПродажахОплата.СуммаОплаты) ЕСТЬ НЕ NULL  И
	|	СУММА(ОтчетОРозничныхПродажахОплата.СуммаОплаты) > 0");
	
	Запрос.УстановитьПараметр("Основание", Основание);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		СуммаНаличнойОплаты = Макс(ТаблицаПлатежей.Итог("СуммаПлатежа") - Выборка.СуммаБезналичнойОплаты, 0);
		
		Если СуммаНаличнойОплаты > 0 Тогда
			СуммыПлатежей = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаНаличнойОплаты, ТаблицаПлатежей.ВыгрузитьКолонку("СуммаПлатежа"));
			ТаблицаПлатежей.ЗагрузитьКолонку(СуммыПлатежей, "СуммаПлатежа");
		Иначе
			ТаблицаПлатежей.ЗаполнитьЗначения(0, "СуммаПлатежа");
		КонецЕсли; 
		
		Для каждого СтрокаПлатежа Из ТаблицаПлатежей Цикл
			ПриходныйКассовыйОрдерФормыКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатежа);
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаПлатежей.ЗаполнитьЗначения(Основание.СтатьяДвиженияДенежныхСредств, "СтатьяДвиженияДенежныхСредств");
	Объект.РасшифровкаПлатежа.Загрузить(ТаблицаПлатежей);
	Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
КонецПроцедуры

// Заполняет реквизит "ДеятельностьНаТорговомСборе" по данным учета и данным документа.
//
Процедура УстановитьДеятельностьНаТорговомСборе(Объект) Экспорт
	
	Если Объект.ДеятельностьНаПатенте 
		Или Объект.ВидОперации <> Перечисления.ВидыОперацийПКО.РозничнаяВыручка
		Или Не УчетнаяПолитика.РаздельныйУчетТорговыйСборПриУСН(Объект.Организация, Объект.Дата) Тогда
		ВозможнаДеятельностьНаТорговомСборе = Ложь;
	Иначе
		УчетВПродажныхЦенах = (УчетнаяПолитика.СпособОценкиТоваровВРознице(Объект.Организация, Объект.Дата) = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости);
		РозничнаяТорговляОблагаетсяЕНВД = УчетнаяПолитика.РозничнаяТорговляОблагаетсяЕНВД(Объект.Организация, Объект.Дата);
		ТипСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ТипСклада");
		СчетаДоходовРасходов = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаВНТТ(
			Объект.Организация,
			Объект.Контрагент, 
			Объект.Дата,
			Новый Структура("РозничнаяТорговляОблагаетсяЕНВД", РозничнаяТорговляОблагаетсяЕНВД));
		
		НТТнаЕНВД = УчетнаяПолитика.ПлательщикЕНВД(Объект.Организация, Объект.Дата)
			И УчетВПродажныхЦенах
			И БухгалтерскийУчетПовтИсп.СчетОтноситсяКДеятельностиЕНВД(СчетаДоходовРасходов.СчетДоходовОтРеализации) 
			И (ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);
		
		ВозможнаДеятельностьНаТорговомСборе = Не НТТнаЕНВД;
	КонецЕсли;
	
	Если ВозможнаДеятельностьНаТорговомСборе Тогда
		Объект.ДеятельностьНаТорговомСборе = ТорговыйСбор.ДеятельностьНаТорговомСбореПриУСНДоходы(
			Объект.Организация, Объект.Контрагент, Объект.Дата);
	ИначеЕсли Объект.ДеятельностьНаТорговомСборе Тогда
		Объект.ДеятельностьНаТорговомСборе = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
