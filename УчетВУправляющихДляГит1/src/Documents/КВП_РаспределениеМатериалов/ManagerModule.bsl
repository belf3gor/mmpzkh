#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение

// Функция возвращает параметры проведения.
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка,
	|	Реквизиты.Номер,
	|	Реквизиты.Дата,
	|	Реквизиты.Ответственный,
	|	Реквизиты.Организация,
	|	Реквизиты.ВидОперации,
	|	Реквизиты.СпособРаспределения,
	|	Реквизиты.СуммаДокумента,
	|	Реквизиты.ТипПлощади,
	|	Реквизиты.Услуга
	|ИЗ
	|	Документ.КВП_РаспределениеМатериалов КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Реквизиты = Новый Структура("Ссылка,Номер,Дата,Ответственный,Организация,ВидОперации,
								|СпособРаспределения,СуммаДокумента,ТипПлощади,Услуга");
	
	ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	
	ПараметрыПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если ДокументСсылка.ВидОперации = 
				Перечисления.КВП_ВидыОперацийРаспределениеМатериалов.РаспределениеПоДокументам Тогда
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		ТаблицаМатериалов = ДокументОбъект.СформироватьТаблицуМатериалов(Истина);
		ТаблицаМатериалов.Колонки.Добавить("НомерСтроки");
		Для Каждого СтрокаТаблицы Из ТаблицаМатериалов Цикл
			СтрокаТабличнойЧасти = ДокументОбъект.ДокументыСписанияМатериалов.Найти(СтрокаТаблицы.Документ, "Документ");
			Если Не СтрокаТабличнойЧасти = Неопределено Тогда
				СтрокаТаблицы.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
			КонецЕсли;
		КонецЦикла;
		
		ПараметрыПроведения.Вставить("ТаблицаМатериалов", ТаблицаМатериалов);
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Материалы.Ссылка,
		|	Материалы.НомерСтроки,
		|	Материалы.Номенклатура,
		|	Материалы.ЕдиницаИзмерения,
		|	Материалы.Коэффициент,
		|	Материалы.Количество,
		|	Материалы.Сумма,
		|	Материалы.Номенклатура.Услуга КАК Услуга,
		|	Материалы.Номенклатура.НомерГТД КАК НомерГТД,
		|	Материалы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения
		|ИЗ
		|	Документ.КВП_РаспределениеМатериалов.Материалы КАК Материалы
		|ГДЕ
		|	Материалы.Ссылка = &Ссылка";
		
		ТаблицаМатериалов = Запрос.Выполнить().Выгрузить();
		
		ПараметрыПроведения.Вставить("ТаблицаМатериалов", ТаблицаМатериалов);
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаРаспределения.Ссылка,
	|	ТаблицаРаспределения.НомерСтроки,
	|	ТаблицаРаспределения.Объект,
	|	ТаблицаРаспределения.Номенклатура,
	|	ТаблицаРаспределения.Сумма,
	|	ТаблицаРаспределения.ЕдиницаИзмерения,
	|	ТаблицаРаспределения.Количество
	|ИЗ
	|	Документ.КВП_РаспределениеМатериалов.ТаблицаРаспределения КАК ТаблицаРаспределения
	|ГДЕ
	|	ТаблицаРаспределения.Ссылка = &Ссылка";
	
	ТаблицаПоТаблицеРаспределения = Запрос.Выполнить().Выгрузить();
	
	ПараметрыПроведения.Вставить("ТаблицаПоТаблицеРаспределения", ТаблицаПоТаблицеРаспределения);
	
	Возврат ПараметрыПроведения;
	
КонецФункции

#КонецОбласти 

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли