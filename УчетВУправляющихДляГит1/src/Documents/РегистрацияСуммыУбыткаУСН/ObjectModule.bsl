#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроверитьВозможностьПроведения(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СдвинутьГраницуАктуальностиРегламентныхОпераций();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	СдвинутьГраницуАктуальностиРегламентныхОпераций();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Дата")
		И ЗначениеЗаполнено(ДанныеЗаполнения.Дата) Тогда
		ЭтотОбъект.Дата = ДанныеЗаполнения.Дата;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Документы.РегистрацияСуммыУбыткаУСН.ОбработатьИзменениеГодаНаСервере(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьВозможностьПроведения(Отказ)

	Если НЕ УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата) Тогда
		ТекстСообщения = НСтр("ru = 'Указанная организация не применяет УСН с объектом налогообложения ""Доходы, уменьшенные на величину расходов""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Организация", "Объект", Отказ);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегистрацияСуммыУбыткаУСН.Ссылка
	               |ИЗ
	               |	Документ.РегистрацияСуммыУбыткаУСН КАК РегистрацияСуммыУбыткаУСН
	               |ГДЕ
	               |	РегистрацияСуммыУбыткаУСН.Ссылка <> &Ссылка
	               |	И ГОД(РегистрацияСуммыУбыткаУСН.Дата) = ГОД(&Дата)
	               |	И РегистрацияСуммыУбыткаУСН.Организация = &Организация";
				   
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Шаблон = НСтр("ru = 'По организации ""%1"" в этом периоде уже введен документ регистрации убытков для УСН'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Организация);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , "Объект", Отказ);		
	КонецЕсли;
	
КонецПроцедуры

Процедура СдвинутьГраницуАктуальностиРегламентныхОпераций()

	// Отметим как неактуальные регламентные операции, входящие в группу, куда входит РасчетНалогаУСН.
	ВидРегламетнойОперации = Перечисления.ВидыРегламентныхОпераций.РасчетНалогаУСН;
	РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
		Организация, 
		НачалоМесяца(КонецГода(Дата)), 
		ЗакрытиеМесяца.ГруппаПоВидуОперации(ВидРегламетнойОперации) - 1, 
		Неопределено);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
