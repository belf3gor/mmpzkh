#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает время документа по умолчанию.
// Требуется если пользователь используется опцию:
// "Время документов устанавливать автоматически".
//
// Возвращаемое значение:
//    Структура - Структура с ключами "Часы" и "Минуты".
//
Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 20, 0);
	
КонецФункции

// Процедура подготавливает данные для табличных частей
// ПоДаннымОрганизации и ПоДаннымКонтрагента.
// 
// Параметры:
//    СтруктураПараметров - Структура - Структура с параметрами для заполнения.
//    АдресХранилища - Строка - Адрес во временном хранилище, куда будет помещен результат выполнения процедуры.
//
Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СтруктураДанныхЗаполнения = Новый Структура();
	СтруктураДанныхЗаполнения.Вставить("Успешно", Ложь);
	
	Если НЕ ПроверитьОтложенныеРасчеты(СтруктураПараметров) Тогда
		ПоместитьВоВременноеХранилище(СтруктураДанныхЗаполнения, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	// Подготовим переменные
	СчетаИсключения             = СчетаИсключения();
	ТаблицаПоДаннымОрганизации = НовыйТаблицаДокументов();
	КэшМетаданных              = Новый Соответствие; // Используется для получения представления документов
	Запрос = ПодготовитьЗапрос(СтруктураПараметров, СчетаИсключения);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Остаток на начало
	ВыборкаОстатокНаНачало = РезультатЗапроса[1].Выбрать();
	ВыборкаОстатокНаНачало.Следующий();
	СтруктураДанныхЗаполнения.Вставить("ОстатокНаНачало", ВыборкаОстатокНаНачало.ОстатокНаНачало);
	
	// Первоначальное заполнение таблицы по данным организации
	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Дебет = 0 И Выборка.Кредит = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаПоДаннымОрганизации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Представление = ПредставлениеДокумента(
			СтруктураПараметров,
			Выборка,
			КэшМетаданных,
			СчетаИсключения);
			
	КонецЦикла;
	
	// Дополним таблицу валютной суммой
	ДополнитьТаблицуВалютнойСуммой(ТаблицаПоДаннымОрганизации, СтруктураПараметров);
	
	// Дополним таблицу счетами-фактурами
	ДополнитьТаблицуСчетамиФактурами(ТаблицаПоДаннымОрганизации, СтруктураПараметров);
	
	// Сортируем таблицу
	КолонкиДляСортировки = ?(СтруктураПараметров.РазбитьПоДоговорам, 
		"Договор, Дата, Документ, Представление",
		"Дата, Документ, Представление, Договор");
	ТаблицаПоДаннымОрганизации.Сортировать(КолонкиДляСортировки, Новый СравнениеЗначений);
	
	// Заполним возвращаемую структуру
	СтруктураДанныхЗаполнения.Вставить("ПоДаннымОрганизации", ТаблицаПоДаннымОрганизации);
	Если СтруктураПараметров.ЗаполнятьДанныеКонтрагента Тогда
		СтруктураДанныхЗаполнения.Вставить("ПоДаннымКонтрагента", ТаблицаПоДаннымКонтрагента(ТаблицаПоДаннымОрганизации));
	КонецЕсли;
	СтруктураДанныхЗаполнения.Успешно = Истина;
	ПоместитьВоВременноеХранилище(СтруктураДанныхЗаполнения, АдресХранилища);
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Функция возвращает представителя организации
// 
// Параметры:
//    Организация - Справочник.Организации - Организация, по которой требуется получить представителя.
//    Дата - Дата - Дата, на которую требуется получить представителя.
//
// Возвращаемое значение:
//    ПредставительОрганизации - Справочник.ФизическоеЛицо - Представитель организации.
//
Функция ПредставительОрганизации(Организация, Дата) Экспорт
	
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
	ДанныеУполномоченногоЛица = ОтветственныеЛицаБП.ДанныеУполномоченногоЛица(
		Организация, Пользователи.ТекущийПользователь());
	
	Если НЕ ЗначениеЗаполнено(ДанныеУполномоченногоЛица.Руководитель)
		ИЛИ ДанныеУполномоченногоЛица.Руководитель = ОтветственныеЛица.Руководитель Тогда
		ПредставительОрганизации = ОтветственныеЛица.Руководитель;
	Иначе
		ПредставительОрганизации = ДанныеУполномоченногоЛица.Руководитель;
	КонецЕсли;
	
	Возврат ПредставительОрганизации;
	
КонецФункции

// Функция возвращает соответствие представлений по данным контрагентам
// Используется для формирования таблицы ПоДаннымКонтрагента.
// 
// Возвращаемое значение:
//    СоответствиеПредставлений - Соответствие.
//
Функция СоответствиеПредставленийПоДаннымКонтрагента() Экспорт
	
	СоответствиеПредставлений = Новый Соответствие;
	СоответствиеПредставлений.Вставить(НСтр("ru='Передано'"),              НСтр("ru='Принято'"));
	СоответствиеПредставлений.Вставить(НСтр("ru='Принято'"),               НСтр("ru='Передано'"));
	СоответствиеПредставлений.Вставить(НСтр("ru='Продажа'"),               НСтр("ru='Приход'"));
	СоответствиеПредставлений.Вставить(НСтр("ru='Приход'"),                НСтр("ru='Продажа'"));
	СоответствиеПредставлений.Вставить(НСтр("ru='Корректировка продажи'"), НСтр("ru='Корректировка прихода'"));
	СоответствиеПредставлений.Вставить(НСтр("ru='Корректировка прихода'"), НСтр("ru='Корректировка продажи'"));
	
	Возврат СоответствиеПредставлений;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//   КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки.
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "АктСверки";
	КомандаОтправки.Представление = НСтр("ru = 'Акт сверки'");
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Печать акта сверки
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктСверки";
	КомандаПечати.Представление = НСтр("ru = 'Акт сверки'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 10;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		// Печать конвертов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Конверт";
		КомандаПечати.Представление = НСтр("ru = 'Конверт'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиКонверта";
		КомандаПечати.Порядок       = 30;
	КонецЕсли;
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСверки") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктСверки", НСтр("ru = 'Акт сверки взаиморасчетов'"),
			ПечатьАктаСверки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.АктСверкиВзаиморасчетов.ПФ_MXL_АктСверки");
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДокумента

Функция ПроверитьОтложенныеРасчеты(СтруктураПараметров)

	ДатаОкончания = СтруктураПараметров.ДатаОкончания;
	Если ТипЗнч(ДатаОкончания) = Тип("Граница") Тогда
		ДатаОкончания = ДатаОкончания.Значение;
	КонецЕсли;
	
	ПараметрыРасчета = УчетВзаиморасчетовОтложенноеПроведение.НовыеПараметрыРасчета();
	ЗаполнитьЗначенияСвойств(ПараметрыРасчета, СтруктураПараметров);
	ПараметрыРасчета.ДатаОкончания = ДатаОкончания;
	
	Результат = УчетВзаиморасчетовОтложенноеПроведение.ВыполнитьОтложенныеРасчеты(ПараметрыРасчета);
	
	Возврат (Результат.КоличествоДоговоровСОшибками = 0);

КонецФункции

Функция СчетаИсключения()
	
	СчетаИсключения = Новый Массив;
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.НДСПоАренднымОбязательствам);
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.НДСПоАренднымОбязательствамУЕ);
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.РасчетыНДСНалоговогоАгента);
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.КорректировкаРасчетовПрошлогоПериода);
	
	Возврат СчетаИсключения;
	
КонецФункции

Функция НовыйТаблицаДокументов()
	
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Договор",       Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТаблицаДокументов.Колонки.Добавить("Дата",          ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаДокументов.Колонки.Добавить("Документ",      Документы.ТипВсеСсылки());
	ТаблицаДокументов.Колонки.Добавить("Представление", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаДокументов.Колонки.Добавить("Дебет",         ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДокументов.Колонки.Добавить("Кредит",        ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДокументов.Колонки.Добавить("Валюта",        Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаДокументов.Колонки.Добавить("ВалютнаяСумма", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДокументов.Колонки.Добавить("ДатаВходящегоДокумента", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	Возврат ТаблицаДокументов;
	
КонецФункции

Функция ПредставлениеДокумента(СтруктураПараметров, Выборка, КэшМетаданных, СчетаИсключения)
	
	ШаблонПредставлениеСтроки = "[Представление] ([НомерДокумента][ДатаДокумента])";
	ПараметрыПредставления = Новый Структура;
	ПараметрыПредставления.Вставить("Представление", "");
	ПараметрыПредставления.Вставить("НомерДокумента", "");
	ПараметрыПредставления.Вставить("ДатаДокумента", "");
	
	// Представление документа
	Если СтруктураПараметров.ВыводитьПолныеНазванияДокументов Тогда
		МетаданныеДокумента = ПолучитьМетаданныеДокумента(Выборка.Документ, КэшМетаданных);
		ПараметрыПредставления.Представление = МетаданныеДокумента.Представление();
	Иначе
		ПараметрыПредставления.Представление = 
			ПредставлениеХозяйственнойОперации(Выборка, СчетаИсключения);
	КонецЕсли;
		
	Если Выборка.НомерВходящегоДокумента = Неопределено Тогда
		Если ПараметрыПредставления.Представление = НСтр("ru='Продажа'")
			И ТипЗнч(Выборка.Документ) <> Тип("ДокументСсылка.ОперацияБух") Тогда
			ПараметрыПредставления.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер, Истина, Ложь);
		Иначе
			ПараметрыПредставления.НомерДокумента = "";
		КонецЕсли;
	Иначе
		Если (Выборка.НомерВходящегоДокумента = "" И ПараметрыПредставления.Представление = НСтр("ru='Оплата'"))
			ИЛИ СтрНайти(ПараметрыПредставления.Представление, НСтр("ru='разницы'")) > 0 Тогда
			ПараметрыПредставления.НомерДокумента = "";
		Иначе
			ПараметрыПредставления.НомерДокумента = Формат(Выборка.НомерВходящегоДокумента, "ЧН=_______");
		КонецЕсли;
	КонецЕсли;
	
	// Дата документа
	Если Выборка.ДатаВходящегоДокумента = Неопределено ИЛИ Выборка.ДатаВходящегоДокумента = '00010101' Тогда
		ДатаДокумента = Выборка.Дата;
	Иначе
		ДатаДокумента = Выборка.ДатаВходящегоДокумента;
	КонецЕсли;
	
	ТекстПередДатой = ?(ПараметрыПредставления.НомерДокумента = "", "", " " + НСтр("ru='от'") + " ");
	ПараметрыПредставления.ДатаДокумента = ТекстПередДатой + Формат(ДатаДокумента, "ДЛФ=D; ДП='  .  .    '");
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонПредставлениеСтроки, ПараметрыПредставления);
	
КонецФункции

Функция ПредставлениеХозяйственнойОперации(Выборка, СчетаИсключения)
	
	Представление = "";
	Если БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы) Тогда
		
		Если НЕ ЗначениеЗаполнено(Выборка.ВидПрочихДоходовИРасходов) Тогда
			
		ИначеЕсли Выборка.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.КурсовыеРазницы Тогда
			Представление = НСтр("ru='Курсовые разницы'");
		ИначеЕсли Выборка.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ШтрафыПениНеустойкиКПолучениюУплате Тогда
			Представление = НСтр("ru='Санкции'");
		ИначеЕсли Выборка.КорСубконто1 = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.СуммовыеРазницы") Тогда
			Представление = НСтр("ru='Суммовые разницы'");
		ИначеЕсли Выборка.КорСубконто1 = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.КурсовыеРазницыПоРасчетамВУЕ") Тогда
			Представление = НСтр("ru='Курсовые разницы по расчетам в у.е.'");
		КонецЕсли;
		
	ИначеЕсли СчетаИсключения.Найти(Выборка.КорСчет) <> Неопределено
		И Выборка.Дебет > 0 Тогда
		
		Представление = НСтр("ru='Зачет НДС'");
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Представление) Тогда
		// Строка уже отработана
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам) Тогда
		
		Если Выборка.Дебет > 0 Тогда
			Представление = НСтр("ru='Возврат займа'");
		ИначеЕсли Выборка.Кредит > 0 Тогда
			Представление = НСтр("ru='Займ'");
		Иначе
			Представление = НСтр("ru='Операции по займам'");
		КонецЕсли;
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоПретензиям) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоПретензиямВал) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоПретензиямУЕ) Тогда
		
		Представление = НСтр("ru='Претензия'");
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.ДенежныеДокументы)
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.ДенежныеДокументыВал) Тогда
		
		Если (Выборка.Кредит > 0) Тогда
			Представление = НСтр("ru='Поступление денежных документов'");
			
		ИначеЕсли (Выборка.Дебет > 0) Тогда
			Представление = НСтр("ru='Выдача денежных документов'");
			
		КонецЕсли;
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.Касса) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.РасчетныеСчета) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.ВалютныеСчета) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.СпециальныеСчета) Тогда
		
		Если (Выборка.Кредит >0) Тогда
			
			Если БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками) Тогда
				Представление = НСтр("ru='Возврат средств'");
				
			ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками) Тогда
				Представление = НСтр("ru='Оплата'");
				
			Иначе
				Представление = НСтр("ru='Перевод средств'");
			КонецЕсли;
			
		ИначеЕсли (Выборка.Дебет >0) Тогда
			Если БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками)
				ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами) Тогда
				
				Представление = НСтр("ru='Оплата'");
				
			ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками) Тогда
				Представление = НСтр("ru='Возврат средств'");
				
			Иначе
				Представление = НСтр("ru='Перевод средств'");
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Выборка.Документ) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		Представление = НСтр("ru='Корректировка прихода'");
	ИначеЕсли ТипЗнч(Выборка.Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		Представление = НСтр("ru='Корректировка продажи'");
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.Продажи) И (Выборка.Дебет > 0) Тогда
		Представление = НСтр("ru='Продажа'");
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.КорСчет, ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы) 
		И НЕ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками) 
		И (Выборка.Дебет > 0) Тогда
		Представление = НСтр("ru='Продажа'");
	ИначеЕсли (БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами))
		И (Выборка.Кредит <> 0) Тогда
		Представление = НСтр("ru='Приход'");
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(Выборка.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками) Тогда
		Если ТипЗнч(Выборка.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И Выборка.Дебет <> 0 Тогда
			Представление = НСтр("ru='Продажа'");
		ИначеЕсли (Выборка.Кредит <> 0) Тогда
			Представление = НСтр("ru='Оплата'");
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(Представление)Тогда
		Если Выборка.Дебет > 0 Тогда
			Представление = НСтр("ru='Принято'");
		ИначеЕсли Выборка.Дебет < 0 Тогда
			Представление = НСтр("ru='Сторно: принято'");
		ИначеЕсли Выборка.Кредит > 0 Тогда
			Представление = НСтр("ru='Передано'");
		Иначе
			Представление = НСтр("ru='Сторно: передано'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ПодготовитьЗапрос(СтруктураПараметров, СчетаИсключения)

	МассивКонтрагентов = МассивКонтрагентов(СтруктураПараметров.Контрагент, СтруктураПараметров.СОбособленнымиПодразделениями);
	МассивДоговоров = МассивДоговоров(СтруктураПараметров.ДоговорКонтрагента);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачала",                            СтруктураПараметров.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",                         СтруктураПараметров.ДатаОкончания);
	Запрос.УстановитьПараметр("Организация",                           СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("МассивКонтрагентов",                    МассивКонтрагентов);
	Запрос.УстановитьПараметр("МассивДоговоров",                       МассивДоговоров);
	Запрос.УстановитьПараметр("Валюта",                                СтруктураПараметров.Валюта);
	Запрос.УстановитьПараметр("ФильтрСписокСчетов",                    СтруктураПараметров.ФильтрСписокСчетов);
	Запрос.УстановитьПараметр("АналитикаРасчетов",                     СтруктураПараметров.АналитикаРасчетов);
	Запрос.УстановитьПараметр("ДоговорКонтрагента",                    СтруктураПараметров.ДоговорКонтрагента);
	Запрос.УстановитьПараметр("РазбитьПоДоговорам",                    СтруктураПараметров.РазбитьПоДоговорам);
	Запрос.УстановитьПараметр("СчетаИсключения",                       СчетаИсключения);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ФильтрСписокСчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ФильтрСписокСчетов)
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|			ТОГДА ХозрасчетныйОстаткиНаНачало.СуммаОстаток
	|		ИНАЧЕ ХозрасчетныйОстаткиНаНачало.ВалютнаяСуммаОстаток
	|	КОНЕЦ КАК ОстатокНаНачало
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаНачала,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ФильтрСписокСчетов.Счет
	|				ИЗ
	|					ФильтрСписокСчетов),
	|			&АналитикаРасчетов,
	|			Организация = &Организация
	|				И ВЫБОР
	|					КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Валюта = &Валюта
	|				КОНЕЦ
	|				И Субконто1 В (&МассивКонтрагентов)
	|				И ВЫБОР
	|					КОГДА &ДоговорКонтрагента = НЕОПРЕДЕЛЕНО
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Субконто2 В (&МассивДоговоров)
	|				КОНЕЦ) КАК ХозрасчетныйОстаткиНаНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Период КАК Дата,
	|	ДанныеПервичныхДокументов.НомерРегистратора КАК Номер,
	|	ХозрасчетныйОбороты.Регистратор КАК Документ,
	|	ХозрасчетныйОбороты.Субконто2 КАК Договор,
	|	ВЫБОР
	|		КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	|		ИНАЧЕ ХозрасчетныйОбороты.ВалютнаяСуммаОборотДт
	|	КОНЕЦ КАК Дебет,
	|	ВЫБОР
	|		КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|		ИНАЧЕ ХозрасчетныйОбороты.ВалютнаяСуммаОборотКт
	|	КОНЕЦ КАК Кредит,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, НЕОПРЕДЕЛЕНО) КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаВходящегоДокумента,
	|	ХозрасчетныйОбороты.КорСчет,
	|	ХозрасчетныйОбороты.КорСубконто1,
	|	ХозрасчетныйОбороты.КорСубконто2,
	|	ХозрасчетныйОбороты.КорСубконто3,
	|	ХозрасчетныйОбороты.Валюта,
	|	ХозрасчетныйОбороты.ВалютнаяСуммаОборот КАК ВалютнаяСумма,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.ПрочиеДоходыИРасходы).ВидПрочихДоходовИРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидПрочихДоходовИРасходов,
	|	ХозрасчетныйОбороты.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ФильтрСписокСчетов.Счет
	|				ИЗ
	|					ФильтрСписокСчетов),
	|			&АналитикаРасчетов,
	|			Организация = &Организация
	|				И ВЫБОР
	|					КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Валюта = &Валюта
	|				КОНЕЦ
	|				И Субконто1 В (&МассивКонтрагентов)
	|				И ВЫБОР
	|					КОГДА &ДоговорКонтрагента = НЕОПРЕДЕЛЕНО
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Субконто2 В (&МассивДоговоров)
	|				КОНЕЦ,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация = &Организация)
	|			И ХозрасчетныйОбороты.Регистратор = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В
	|					(ВЫБРАТЬ
	|						ФильтрСписокСчетов.Счет
	|					ИЗ
	|						ФильтрСписокСчетов)
	|				ТОГДА ВЫБОР
	|						КОГДА &ДоговорКонтрагента = НЕОПРЕДЕЛЕНО
	|							ТОГДА ВЫБОР
	|									КОГДА &РазбитьПоДоговорам
	|										ТОГДА НЕ ХозрасчетныйОбороты.КорСубконто2 = ХозрасчетныйОбороты.Субконто2
	|									ИНАЧЕ НЕ ХозрасчетныйОбороты.КорСубконто1 В (&МассивКонтрагентов)
	|								КОНЕЦ
	|						ИНАЧЕ НЕ(ХозрасчетныйОбороты.КорСубконто1 В (&МассивКонтрагентов)
	|									И ХозрасчетныйОбороты.КорСубконто2 = ХозрасчетныйОбороты.Субконто2)
	|					КОНЕЦ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ(ВЫБОР
	|					КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|						ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	|					ИНАЧЕ ХозрасчетныйОбороты.ВалютнаяСуммаОборотДт
	|				КОНЕЦ = 0
	|				И ВЫБОР
	|					КОГДА &Валюта = НЕОПРЕДЕЛЕНО
	|						ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|					ИНАЧЕ ХозрасчетныйОбороты.ВалютнаяСуммаОборотКт
	|				КОНЕЦ = 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Документ";
	
	Возврат Запрос;
	
КонецФункции

Функция МассивКонтрагентов(Контрагент, СОбособленнымиПодразделениями)

	Если НЕ СОбособленнымиПодразделениями Тогда
		МассивКонтрагентов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контрагент);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ГоловнойКонтрагент = &Контрагент";
		
		ТаблицаКонтрагентов = Запрос.Выполнить().Выгрузить();
		МассивКонтрагентов = ТаблицаКонтрагентов.ВыгрузитьКолонку("Контрагент");
		
	КонецЕсли;
	
	Возврат МассивКонтрагентов;
	
КонецФункции

Функция МассивДоговоров(Договор)

	Если НЕ ЗначениеЗаполнено(Договор) Тогда
		МассивДоговоров = Новый Массив;
	ИначеЕсли НЕ Договор.ЭтоГруппа Тогда
		МассивДоговоров = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Договор);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДоговорКонтрагента", Договор);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка В ИЕРАРХИИ(&ДоговорКонтрагента)";
		ТаблицаДоговоров = Запрос.Выполнить().Выгрузить();
		МассивДоговоров = ТаблицаДоговоров.ВыгрузитьКолонку("Договор");
	КонецЕсли;
	
	Возврат МассивДоговоров;
	
КонецФункции

Функция ПолучитьМетаданныеДокумента(Знач ДокументСсылка, КэшМетаданных)
	
	МетаданныеДокумента = КэшМетаданных[ТипЗнч(ДокументСсылка)];
	
	Если МетаданныеДокумента = Неопределено Тогда
		МетаданныеДокумента = ДокументСсылка.Метаданные();
		КэшМетаданных.Вставить(ТипЗнч(ДокументСсылка), МетаданныеДокумента);
	КонецЕсли;
	
	Возврат МетаданныеДокумента;
	
КонецФункции

Процедура ДополнитьТаблицуСчетамиФактурами(ТаблицаПоДаннымОрганизации, СтруктураПараметров)
	
	Если НЕ СтруктураПараметров.ВыводитьСчетаФактуры Тогда
		Возврат;
	КонецЕсли;
	
	ВидыСчетовФактурВыставленный = Новый Массив;
	ВидыСчетовФактурВыставленный.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
	ВидыСчетовФактурВыставленный.Добавить(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	
	ВидыСчетовФактурПолученный = Новый Массив;
	ВидыСчетовФактурПолученный.Добавить(Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
	ВидыСчетовФактурПолученный.Добавить(Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", ТаблицаПоДаннымОрганизации.ВыгрузитьКолонку("Документ"));
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	
	Запрос.УстановитьПараметр("ВидыСчетовФактурВыставленный", ВидыСчетовФактурВыставленный);
	Запрос.УстановитьПараметр("ВидыСчетовФактурПолученный", ВидыСчетовФактурПолученный);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданный.Исправление КАК Исправление,
	|	СчетФактураВыданный.ВидСчетаФактуры КАК ВидСчетаФактуры
	|ПОМЕСТИТЬ СписокСчетовФактурВыданных
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО СчетФактураВыданныйДокументыОснования.Ссылка = СчетФактураВыданный.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование В(&СписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетФактураПолученныйДокументыОснования.Ссылка КАК Ссылка,
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ СписокСчетовФактурПолученных
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование В(&СписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетовФактурВыданных.Ссылка КАК Ссылка,
	|	""Выданный"" КАК Вид,
	|	СписокСчетовФактурВыданных.ДокументОснование КАК ДокументОснование,
	|	СписокСчетовФактурВыданных.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	СписокСчетовФактурВыданных.Исправление КАК Исправление,
	|	СписокСчетовФактурВыданных.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный) КАК Корректировочный,
	|	ВЫБОР
	|		КОГДА СписокСчетовФактурВыданных.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправленияКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления
	|	КОНЕЦ КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА СписокСчетовФактурВыданных.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления
	|	КОНЕЦ КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры КАК НомерИсходногоДокумента,
	|	ВЫБОР
	|		КОГДА СписокСчетовФактурВыданных.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА СписокСчетовФактурВыданных.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА СписокСчетовФактурВыданных.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|						ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры
	|					ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокСчетовФактурВыданных.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|					ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры
	|				ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры
	|			КОНЕЦ
	|	КОНЕЦ КАК Дата,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры КАК НомерИсправляемогоКорректировочногоДокумента,
	|	NULL КАК НомерВходящегоДокумента,
	|	NULL КАК ДатаВходящегоДокумента,
	|	NULL КАК БланкСтрогойОтчетности
	|ИЗ
	|	СписокСчетовФактурВыданных КАК СписокСчетовФактурВыданных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(, Организация = &Организация) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|		ПО СписокСчетовФактурВыданных.Ссылка = ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Ссылка,
	|	""Полученный"",
	|	СписокСчетовФактурПолученных.ДокументОснование,
	|	СчетФактураПолученный.ВидСчетаФактуры,
	|	СчетФактураПолученный.Исправление,
	|	СчетФактураПолученный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный),
	|	СчетФактураПолученный.НомерИсправления,
	|	СчетФактураПолученный.ДатаИсправления,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	СчетФактураПолученный.НомерВходящегоДокумента,
	|	СчетФактураПолученный.ДатаВходящегоДокумента,
	|	ЛОЖЬ
	|ИЗ
	|	СписокСчетовФактурПолученных КАК СписокСчетовФактурПолученных
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ПО СписокСчетовФактурПолученных.Ссылка = СчетФактураПолученный.Ссылка
	|ГДЕ
	|	СчетФактураПолученный.Организация = &Организация
	|	И СчетФактураПолученный.Проведен = ИСТИНА
	|	И СчетФактураПолученный.ВидСчетаФактуры В(&ВидыСчетовФактурПолученный)";
	ТаблицаСчетовФактур = Запрос.Выполнить().Выгрузить();
	ТаблицаСчетовФактур.Индексы.Добавить("ДокументОснование");
	
	ШаблонСчетФактуры = НСтр("ru='Счет-фактура (%1)'");

	Для Каждого СтрокаТаблицы Из ТаблицаПоДаннымОрганизации Цикл
		
		СтрокиСчетовФактур = ТаблицаСчетовФактур.НайтиСтроки(Новый Структура("ДокументОснование", СтрокаТаблицы.Документ));
		ПредставлениеСчетаФактуры = "";
		Для Каждого СтрокаСчетаФактуры Из СтрокиСчетовФактур Цикл
			
			Если СтрокаСчетаФактуры.Вид = "Выданный" Тогда
				СтруктураПредставленияПоРеквизитам = Документы.СчетФактураВыданный.ПолучитьПредставлениеПоРеквизитам(
					СтрокаСчетаФактуры);
			Иначе
				СтруктураПредставленияПоРеквизитам = Документы.СчетФактураПолученный.ПолучитьПредставлениеПоРеквизитам(
					СтрокаСчетаФактуры);
			КонецЕсли;
			
			ПредставлениеСчетаФактуры = ПредставлениеСчетаФактуры 
				+ Символы.ПС 
				+ СтрШаблон(ШаблонСчетФактуры, СтруктураПредставленияПоРеквизитам.ТекстПредставлениеКраткое);
			
		КонецЦикла;
		
		СтрокаТаблицы.Представление = СтрокаТаблицы.Представление + ПредставлениеСчетаФактуры;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьТаблицуВалютнойСуммой(ТаблицаПоДаннымОрганизации, СтруктураПараметров)
	
	Если СтруктураПараметров.ВалютаДокумента = СтруктураПараметров.ВалютаРегламентированногоУчета 
		И НЕ СтруктураПараметров.ВыводитьПолныеНазванияДокументов Тогда
		
		ТаблицаПоДаннымОрганизации.Свернуть(
			"Дата,ДатаВходящегоДокумента,Документ,Представление,Договор,Валюта",
			"Дебет, Кредит, ВалютнаяСумма");
		
		Для Каждого СтрокаТаблицы Из ТаблицаПоДаннымОрганизации Цикл
			Если СтрокаТаблицы.ВалютнаяСумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ШаблонСтрокиСВалютнойСуммой = "[Представление] ([ВалютнаяСумма] [Валюта])";
			ПараметрыСтроки = Новый Структура;
			ПараметрыСтроки.Вставить("Представление", СтрокаТаблицы.Представление);
			ПараметрыСтроки.Вставить("ВалютнаяСумма", 0);
			ПараметрыСтроки.Вставить("Валюта",        СтрокаТаблицы.Валюта);
			
			Если СтрокаТаблицы.ВалютнаяСумма > 0 Тогда
				ПараметрыСтроки.ВалютнаяСумма = Формат(СтрокаТаблицы.ВалютнаяСумма, "ЧЦ=15; ЧДЦ=2");
			Иначе
				Множитель = ?((СтрокаТаблицы.Дебет + СтрокаТаблицы.Кредит) > 0, -1, 1);
				ПараметрыСтроки.ВалютнаяСумма = Формат(Множитель * СтрокаТаблицы.ВалютнаяСумма, "ЧЦ=15; ЧДЦ=2");
			КонецЕсли;
			СтрокаТаблицы.Представление = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
				ШаблонСтрокиСВалютнойСуммой,
				ПараметрыСтроки);
		КонецЦикла;
		
	Иначе
		ТаблицаПоДаннымОрганизации.Свернуть("Дата,ДатаВходящегоДокумента,Документ,Представление,Договор","Дебет, Кредит");
	КонецЕсли;
	
КонецПроцедуры

Функция ТаблицаПоДаннымКонтрагента(ТаблицаПоДаннымОрганизации)
	
	ТаблицаПоДаннымКонтрагента = НовыйТаблицаДокументов();
	
	Для Каждого СтрокаПоДаннымОрганизации Из ТаблицаПоДаннымОрганизации Цикл
		СтрокаПоДаннымКонтрагента = ТаблицаПоДаннымКонтрагента.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПоДаннымКонтрагента, СтрокаПоДаннымОрганизации);
		СтрокаПоДаннымКонтрагента.Представление =
			ПредставлениеДокументаПоДаннымКонтрагента(СтрокаПоДаннымОрганизации.Представление);
		СтрокаПоДаннымКонтрагента.Дебет  = СтрокаПоДаннымОрганизации.Кредит;
		СтрокаПоДаннымКонтрагента.Кредит = СтрокаПоДаннымОрганизации.Дебет;
		СтрокаПоДаннымКонтрагента.Дата   = ?(ЗначениеЗаполнено(СтрокаПоДаннымОрганизации.ДатаВходящегоДокумента),
			СтрокаПоДаннымОрганизации.ДатаВходящегоДокумента,
			СтрокаПоДаннымОрганизации.Дата);
	КонецЦикла;
	
	Возврат ТаблицаПоДаннымКонтрагента;
	
КонецФункции

Функция ПредставлениеДокументаПоДаннымКонтрагента(ПредставлениеДокумента) Экспорт
	
	Представление = СокрЛП(ПредставлениеДокумента);
	Для Каждого Соответствие Из СоответствиеПредставленийПоДаннымКонтрагента() Цикл
		Если СтрНачинаетсяС(Представление, Соответствие.Ключ) Тогда
			Представление = СтрЗаменить(Представление, Соответствие.Ключ, Соответствие.Значение);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы.
//
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыАктСверки()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТЧПоДаннымОрганизации.Ссылка КАК Документ,
	|	0 КАК НомерТабЧасти,
	|	ВЫБОР
	|		КОГДА ТЧПоДаннымОрганизации.Ссылка.РазбитьПоДоговорам
	|			ТОГДА ТЧПоДаннымОрганизации.Договор
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Договор,
	|	ТЧПоДаннымОрганизации.НомерСтроки КАК НомерСтроки,
	|	ТЧПоДаннымОрганизации.Дата КАК Дата,
	|	ТЧПоДаннымОрганизации.Документ КАК Регистратор,
	|	ТЧПоДаннымОрганизации.Представление КАК Представление,
	|	ТЧПоДаннымОрганизации.Дебет КАК Дебет,
	|	ТЧПоДаннымОрганизации.Кредит КАК Кредит,
	|	NULL КАК ПредставлениеКонтр,
	|	NULL КАК ДатаКонтр,
	|	0 КАК ДебетКонтр,
	|	0 КАК КредитКонтр
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.АктСверкиВзаиморасчетов.ПоДаннымОрганизации КАК ТЧПоДаннымОрганизации
	|ГДЕ
	|	ТЧПоДаннымОрганизации.Ссылка В(&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧПоДаннымКонтрагента.Ссылка,
	|	1,
	|	ВЫБОР
	|		КОГДА ТЧПоДаннымКонтрагента.Ссылка.РазбитьПоДоговорам
	|			ТОГДА ТЧПоДаннымКонтрагента.Договор
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ТЧПоДаннымКонтрагента.НомерСтроки,
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	0,
	|	ТЧПоДаннымКонтрагента.Представление,
	|	ТЧПоДаннымКонтрагента.Дата,
	|	ТЧПоДаннымКонтрагента.Дебет,
	|	ТЧПоДаннымКонтрагента.Кредит
	|ИЗ
	|	Документ.АктСверкиВзаиморасчетов.ПоДаннымКонтрагента КАК ТЧПоДаннымКонтрагента
	|ГДЕ
	|	ТЧПоДаннымКонтрагента.Ссылка В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктСверкиВзаиморасчетов.Ссылка КАК Документ,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Дата КАК МинДата,
	|	ДанныеДокумента.Регистратор КАК Регистратор,
	|	ДанныеДокумента.Представление КАК Представление,
	|	ДанныеДокумента.ДатаКонтр КАК ДатаКонтр,
	|	ДанныеДокумента.ПредставлениеКонтр КАК ПредставлениеКонтр,
	|	ЕСТЬNULL(ДанныеДокумента.Дебет, 0) КАК СуммаОборотДт,
	|	ЕСТЬNULL(ДанныеДокумента.Кредит, 0) КАК СуммаОборотКт,
	|	ЕСТЬNULL(ДанныеДокумента.ДебетКонтр, 0) КАК СуммаОборотДтКонтр,
	|	ЕСТЬNULL(ДанныеДокумента.КредитКонтр, 0) КАК СуммаОборотКтКонтр,
	|	АктСверкиВзаиморасчетов.Организация КАК Организация,
	|	АктСверкиВзаиморасчетов.Контрагент КАК Контрагент,
	|	АктСверкиВзаиморасчетов.Дата КАК ДатаАктаСверки,
	|	АктСверкиВзаиморасчетов.ПредставительОрганизации КАК ПредставительОрганизации,
	|	АктСверкиВзаиморасчетов.ПредставительКонтрагента КАК ПредставительКонтрагента,
	|	АктСверкиВзаиморасчетов.ДатаНачала КАК ДатаНачала,
	|	АктСверкиВзаиморасчетов.ДатаОкончания КАК ДатаОкончания,
	|	АктСверкиВзаиморасчетов.ОстатокНаНачало КАК ОстатокНаНачало,
	|	АктСверкиВзаиморасчетов.Расхождение КАК Расхождение,
	|	АктСверкиВзаиморасчетов.ДоговорКонтрагента КАК ДоговорКонтрагентаВШапке,
	|	ЕСТЬNULL(АктСверкиВзаиморасчетов.ДоговорКонтрагента.ЭтоГруппа, ЛОЖЬ) КАК ДоговорКонтрагентаВШапкеЭтоГруппа,
	|	АктСверкиВзаиморасчетов.ВалютаДокумента КАК ВалютаДокумента,
	|	АктСверкиВзаиморасчетов.РазбитьПоДоговорам КАК РазбитьПоДоговорам,
	|	АктСверкиВзаиморасчетов.СверкаСогласована КАК СверкаСогласована,
	|	ДанныеДокумента.НомерТабЧасти КАК НомерТабЧасти,
	|	АктСверкиВзаиморасчетов.СОбособленнымиПодразделениями КАК СОбособленнымиПодразделениями
	|ИЗ
	|	Документ.АктСверкиВзаиморасчетов КАК АктСверкиВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокумента КАК ДанныеДокумента
	|		ПО (ДанныеДокумента.Документ = АктСверкиВзаиморасчетов.Ссылка)
	|ГДЕ
	|	АктСверкиВзаиморасчетов.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерТабЧасти,
	|	ДанныеДокумента.НомерСтроки
	|ИТОГИ
	|	МИНИМУМ(МинДата),
	|	СУММА(СуммаОборотДт),
	|	СУММА(СуммаОборотКт),
	|	СУММА(СуммаОборотДтКонтр),
	|	СУММА(СуммаОборотКтКонтр),
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(Контрагент),
	|	МАКСИМУМ(ДатаАктаСверки),
	|	МАКСИМУМ(ПредставительОрганизации),
	|	МАКСИМУМ(ПредставительКонтрагента),
	|	МАКСИМУМ(ДатаНачала),
	|	МАКСИМУМ(ДатаОкончания),
	|	МАКСИМУМ(ОстатокНаНачало),
	|	МАКСИМУМ(Расхождение),
	|	МАКСИМУМ(ДоговорКонтрагентаВШапке),
	|	МАКСИМУМ(ДоговорКонтрагентаВШапкеЭтоГруппа),
	|	МАКСИМУМ(ВалютаДокумента),
	|	МАКСИМУМ(РазбитьПоДоговорам),
	|	МАКСИМУМ(СверкаСогласована),
	|	МАКСИМУМ(СОбособленнымиПодразделениями)
	|ПО
	|	Документ,
	|	Договор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ с печатной формой акта сверки.
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной.
//
Функция ПечатьАктаСверки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб         = Истина;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктСверкиВзаиморасчетов";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АктСверкиВзаиморасчетов.ПФ_MXL_АктСверки");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыАктСверки();
	
	Шапка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Документ");
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Получаем области
		ОбластьЗаголовок    = Макет.ПолучитьОбласть("Заголовок");
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьНачОстатки   = Макет.ПолучитьОбласть("НачОстатки");
		ОбластьДоговор      = Макет.ПолучитьОбласть("Договор");
		ОбластьДоговорИтоги = Макет.ПолучитьОбласть("ДоговорИтоги");
		ОбластьОбороты      = Макет.ПолучитьОбласть("Обороты");
		ОбластьОборотыИтог  = Макет.ПолучитьОбласть("ОборотыИтог");
		ОбластьКонОстатки   = Макет.ПолучитьОбласть("КонОстатки");
		ОбластьПодвал   = Макет.ПолучитьОбласть("Подвал");
		
		// Заполняем шапку
		ПараметрыЗаголовок = Новый Структура;
		
		// Название организации
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаАктаСверки);
		НазваниеОрганизации   = СведенияОбОрганизации.НаименованиеДляПечатныхФорм;
		ПараметрыЗаголовок.Вставить("НазваниеОрганизации", НазваниеОрганизации);
		
		// Наименование контрагента
		СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Контрагент, Шапка.ДатаАктаСверки);
		НаименованиеКонтрагента = СведенияОКонтрагенте.НаименованиеДляПечатныхФорм;
		Если Шапка.СОбособленнымиПодразделениями Тогда
			НаименованиеКонтрагента = НаименованиеКонтрагента + " " + НСтр("ru='с обособленными подразделениями'");
		КонецЕсли;
		ПараметрыЗаголовок.Вставить("НаименованиеКонтрагента", НаименованиеКонтрагента);
		
		// За период
		ДатаНачалаОтчета = ?(ЗначениеЗаполнено(Шапка.ДатаНачала), Шапка.ДатаНачала, Шапка.МинДата);
		Если ЗначениеЗаполнено(ДатаНачалаОтчета) Тогда
			ЗаПериод = ПредставлениеПериода(НачалоДня(ДатаНачалаОтчета), КонецДня(Шапка.ДатаОкончания), "ФП = Истина");
			ПараметрыЗаголовок.Вставить("ЗаПериод", СтрШаблон(НСтр("ru='за период: %1'"), ЗаПериод));
		КонецЕсли;
		
		// По договору
		Если ЗначениеЗаполнено(Шапка.ДоговорКонтрагентаВШапке) И НЕ Шапка.ДоговорКонтрагентаВШапкеЭтоГруппа Тогда
			ПараметрыЗаголовок.Вставить("ПоДоговору",
				Символы.ПС + СтрШаблон(НСтр("ru='по договору %1'"), Шапка.ДоговорКонтрагентаВШапке));
		КонецЕсли;
		
		// Валюта документа
		ПараметрыЗаголовок.Вставить("ВалютаДокумента", Шапка.ВалютаДокумента);
		
		// Представитель организации
		ДанныеПредставителяОрганизации = ДанныеПредставителяОрганизации(Шапка);
		Если СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			// Для ИП выводим только информацию об организации
			ПредставительОрганизации = НазваниеОрганизации;
		Иначе
			ПредставительОрганизации = СтрШаблон("%1 %2 %3",
				ДанныеПредставителяОрганизации.Должность,
				НазваниеОрганизации, 
				ДанныеПредставителяОрганизации.ФИОПолные);
		КонецЕсли;
		ПараметрыЗаголовок.Вставить("ПредставительОрганизации", ПредставительОрганизации);
		
		// Представитель контрагента
		ДанныеПредставителяКонтрагента = ДанныеПредставителяКонтрагента(Шапка);
		Если СведенияОКонтрагенте.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			// Для физического лица выводим только информацию об организации
			ПредставительКонтрагента = НаименованиеКонтрагента;
		Иначе
			ПредставительКонтрагента = СтрШаблон("%1 %2 %3", 
				ДанныеПредставителяКонтрагента.Должность, 
				НаименованиеКонтрагента,
				ДанныеПредставителяКонтрагента.ФИОПолные);
		КонецЕсли;
		ПараметрыЗаголовок.Вставить("ПредставительКонтрагента", ПредставительКонтрагента);
		
		// Выводим шапку
		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		// Строки таблицы
		ПараметрыНачальныеОстатки = Новый Структура;
		ПараметрыНачальныеОстатки.Вставить("СуммаНачальныйОстатокДт", ?(Шапка.ОстатокНаНачало > 0, Шапка.ОстатокНаНачало, 0));
		ПараметрыНачальныеОстатки.Вставить("СуммаНачальныйОстатокКт", ?(Шапка.ОстатокНаНачало < 0, -Шапка.ОстатокНаНачало, 0));
		ОбластьНачОстатки.Параметры.Заполнить(ПараметрыНачальныеОстатки);
		ТабличныйДокумент.Вывести(ОбластьНачОстатки);
		
		Если Шапка.РазбитьПоДоговорам Тогда
			СальдоПоДоговорам = НачальноеСальдоПоДоговорам(Шапка);
			НачальноеСальдоПоДоговорамАкта      = СальдоПоДоговорам.ПоДоговорамАкта;
			НачальноеСальдоПоОстальнымДоговорам = СальдоПоДоговорам.ПоОстальнымДоговорам;
			
			ВывестиСальдоПоОстальнымДоговорам(
				НачальноеСальдоПоОстальнымДоговорам,
				ОбластьДоговор,
				ОбластьДоговорИтоги,
				ТабличныйДокумент,
				Шапка);
		КонецЕсли;
		
		ОбходПоДоговорам = Шапка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Договор");
		Пока ОбходПоДоговорам.Следующий() Цикл
			
			// Собираем данные
			ОбходПоДокументам = ОбходПоДоговорам.Выбрать();
			
			// Создадим список документов по организации и контрагенту.
			СписокДокументов  = Новый Массив;
			СписокДокументовК = Новый Массив;
			
			Пока ОбходПоДокументам.Следующий() Цикл
				Если ОбходПоДокументам.Дата <> NULL Тогда
					СтруктураДокумента = НовыйСтруктураДокумента();
					ЗаполнитьЗначенияСвойств(СтруктураДокумента, ОбходПоДокументам);
					СписокДокументов.Добавить(СтруктураДокумента);
				КонецЕсли;
				Если ОбходПоДокументам.ДатаКонтр <> NULL Тогда
					СтруктураДокумента = НовыйСтруктураДокумента(Истина);
					ЗаполнитьЗначенияСвойств(СтруктураДокумента, ОбходПоДокументам);
					СписокДокументовК.Добавить(СтруктураДокумента);
				КонецЕсли;
			КонецЦикла;
			
			// Не отображаем информацию по контрагенту, если не введены документы и сверка не согласована.
			// В этом случае будут пустые строки и итоги со стороны контрагента.
			ЗаполнятьДанныеКонтрагента = (Шапка.СверкаСогласована ИЛИ (СписокДокументовК.Количество() <> 0));
			
			// Выравняем таблицы по количеству строк
			РазницаСтрок = СписокДокументов.Количество() - СписокДокументовК.Количество();
			Если РазницаСтрок > 0 Тогда
				ДополняемаяТаблица = СписокДокументовК;
				НоваяСтрока = НовыйСтруктураДокумента(Истина);
			ИначеЕсли РазницаСтрок < 0 Тогда
				РазницаСтрок = -РазницаСтрок;
				ДополняемаяТаблица = СписокДокументов;
				НоваяСтрока = НовыйСтруктураДокумента();
			КонецЕсли;
			Для Счетчик = 1 По РазницаСтрок Цикл
				ДополняемаяТаблица.Добавить(НоваяСтрока);
			КонецЦикла;
			
			ЕстьСтрокиДокументов = (СписокДокументов.Количество() <> 0);
			
			// Выводим данные в табличный документ
			Если Шапка.РазбитьПоДоговорам И ЕстьСтрокиДокументов Тогда
				ОбластьДоговор.Параметры.Заполнить(ОбходПоДоговорам);
				НачальноеСальдоПоДоговору = НачальноеСальдоПоДоговорамАкта.Скопировать(
					Новый Структура("Договор", ОбходПоДоговорам.Договор));
				СтруктураСальдоПоДоговору = Новый Структура;
				СтруктураСальдоПоДоговору.Вставить("НачОстатокПоДоговоруДт",
					НачальноеСальдоПоДоговору.Итог("НачОстатокПоДоговоруДт"));
				СтруктураСальдоПоДоговору.Вставить("НачОстатокПоДоговоруКт",
					НачальноеСальдоПоДоговору.Итог("НачОстатокПоДоговоруКт"));
				ОбластьДоговор.Параметры.Заполнить(СтруктураСальдоПоДоговору);
				
				ТабличныйДокумент.Вывести(ОбластьДоговор);
			КонецЕсли;
			
			Для Счетчик = 0 По СписокДокументов.Количество() - 1 Цикл
				// Выводим обороты
				ОбластьОбороты.Параметры.Заполнить(СписокДокументов[Счетчик]);
				ОбластьОбороты.Параметры.Заполнить(СписокДокументовК[Счетчик]);
				ТабличныйДокумент.Вывести(ОбластьОбороты);
			КонецЦикла;
			
			Если Шапка.РазбитьПоДоговорам И ЕстьСтрокиДокументов Тогда
				ОбластьДоговорИтоги.Параметры.Заполнить(ОбходПоДоговорам);
				// Конечное сальдо по договору
				СтруктураКонСальдоПоДоговору = Новый Структура;
				КонОстатокПоДоговоруДт = СтруктураСальдоПоДоговору.НачОстатокПоДоговоруДт + ОбходПоДоговорам.СуммаОборотДт;
				КонОстатокПоДоговоруКт = СтруктураСальдоПоДоговору.НачОстатокПоДоговоруКт + ОбходПоДоговорам.СуммаОборотКт;
				КонОстатокПоДоговору   = КонОстатокПоДоговоруДт - КонОстатокПоДоговоруКт;
				Если КонОстатокПоДоговору < 0 Тогда
					СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДт", 0);
					СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКт",-КонОстатокПоДоговору);
				Иначе
					СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДт", КонОстатокПоДоговору);
					СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКт", 0);
				КонецЕсли;
				
				Если НЕ ЗаполнятьДанныеКонтрагента Тогда
					СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДтКонтр", 0);
					СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКтКонтр", 0);
				Иначе
					КонОстатокПоДоговоруДтКонтр = СтруктураСальдоПоДоговору.НачОстатокПоДоговоруКт + ОбходПоДоговорам.СуммаОборотДтКонтр;
					КонОстатокПоДоговоруКтКонтр = СтруктураСальдоПоДоговору.НачОстатокПоДоговоруДт + ОбходПоДоговорам.СуммаОборотКтКонтр;
					КонОстатокПоДоговоруКонтр = КонОстатокПоДоговоруДтКонтр - КонОстатокПоДоговоруКтКонтр;
					Если КонОстатокПоДоговоруКонтр < 0 Тогда
						СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДтКонтр", 0);
						СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКтКонтр",-КонОстатокПоДоговоруКонтр);
					Иначе
						СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДтКонтр", КонОстатокПоДоговоруКонтр);
						СтруктураКонСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКтКонтр", 0);
					КонецЕсли;
				КонецЕсли;
				
				ОбластьДоговорИтоги.Параметры.Заполнить(СтруктураКонСальдоПоДоговору);
				ТабличныйДокумент.Вывести(ОбластьДоговорИтоги);
			КонецЕсли;
			
		КонецЦикла;
		
		// Итоги по документам
		ОбластьОборотыИтог.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьОборотыИтог);
		
		// Итоги по контрагенту
		ОстатокНаКонец = Шапка.ОстатокНаНачало + Шапка.СуммаОборотДт - Шапка.СуммаОборотКт;
		Если ЗаполнятьДанныеКонтрагента Тогда
			ОстатокНаКонецКонтр = Шапка.ОстатокНаНачало - Шапка.СуммаОборотДтКонтр + Шапка.СуммаОборотКтКонтр;
		Иначе
			ОстатокНаКонецКонтр = 0;
		КонецЕсли;
		ПараметрыИтогиПоКонтрагенту = Новый Структура;
		ПараметрыИтогиПоКонтрагенту.Вставить("СуммаКонечныйОстатокДт",      ?(ОстатокНаКонец > 0, ОстатокНаКонец, 0));
		ПараметрыИтогиПоКонтрагенту.Вставить("СуммаКонечныйОстатокКт",      ?(ОстатокНаКонец < 0, -ОстатокНаКонец, 0));
		ПараметрыИтогиПоКонтрагенту.Вставить("СуммаКонечныйОстатокКтКонтр", ?(ОстатокНаКонецКонтр > 0, ОстатокНаКонецКонтр, 0));
		ПараметрыИтогиПоКонтрагенту.Вставить("СуммаКонечныйОстатокДтКонтр", ?(ОстатокНаКонецКонтр < 0, -ОстатокНаКонецКонтр, 0));
		ОбластьКонОстатки.Параметры.Заполнить(ПараметрыИтогиПоКонтрагенту);
		ТабличныйДокумент.Вывести(ОбластьКонОстатки);
		
		// Результаты сверки
		ПараметрыПодвала = Новый Структура;
		ПараметрыПодвала.Вставить("НаименованиеКонтрагента", НаименованиеКонтрагента);
		ПараметрыПодвала.Вставить("НазваниеОрганизации",     НазваниеОрганизации);
		ПараметрыПодвала.Вставить("Должность",               ДанныеПредставителяОрганизации.Должность);
		ПараметрыПодвала.Вставить("ДолжностьК",              ДанныеПредставителяКонтрагента.Должность);
		ПараметрыПодвала.Вставить("ФИОПредставителя",        СтрШаблон("(%1)", ДанныеПредставителяОрганизации.Представление));
		ПараметрыПодвала.Вставить("ФИОПредставителяК",       СтрШаблон("(%1)", ДанныеПредставителяКонтрагента.Представление));
		ПараметрыПодвала.Вставить("РезультатыСверки",
			РезультатСверки(Шапка, ОстатокНаКонец, НазваниеОрганизации, НаименованиеКонтрагента));
		Если ЗаполнятьДанныеКонтрагента Тогда
			ПараметрыПодвала.Вставить("РезультатыСверкиК",
				РезультатСверки(Шапка, ОстатокНаКонецКонтр, НазваниеОрганизации, НаименованиеКонтрагента));
		КонецЕсли;
		
		// Итог сверки выводим если нашли расхождение и сверка согласована.
		Если Шапка.Расхождение <> 0 И Шапка.СверкаСогласована Тогда
			СуммаРасхождение = ?(Шапка.Расхождение > 0, Шапка.Расхождение, -Шапка.Расхождение);
			ИтогСверки = СтрШаблон(НСтр("ru='В результате сверки выявлено расхождение информации о состоянии расчетов в размере %1 %2 %3'"),
				Формат(СуммаРасхождение, "ЧЦ=21; ЧДЦ=2"),
				Строка(Шапка.ВалютаДокумента),
				УчетДенежныхСредствБП.ФорматироватьСуммуПрописьюПлатежногоДокумента(СуммаРасхождение, Шапка.ВалютаДокумента, Ложь));
			ПараметрыПодвала.Вставить("ИтогСверки", Символы.ПС + ИтогСверки + Символы.ПС + " ");
		КонецЕсли;
		
		ОбластьПодвал.Параметры.Заполнить(ПараметрыПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		ТабличныйДокумент.ТолькоПросмотр = Истина;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Документ);
			
		УправлениеПечатьюБП.ДополнитьДокументПодписьюИПечатью(ТабличныйДокумент, Шапка, ОбъектыПечати, ПараметрыПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

Процедура ВывестиСальдоПоОстальнымДоговорам(НачальноеСальдоПоДоговорам, ОбластьДоговор, ОбластьДоговорИтоги, ТабличныйДокумент, Шапка)
	
	Для Каждого СтрокаДоговора Из НачальноеСальдоПоДоговорам Цикл
		// Выводим начальное сальдо по договору.
		ОбластьДоговор.Параметры.Заполнить(СтрокаДоговора);
		ТабличныйДокумент.Вывести(ОбластьДоговор);
		
		// Конечное сальдо равно начальному сальдо,
		// потому что по договору нет движений.
		КонечноеСальдоПоДоговору = Новый Структура;
		КонечноеСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДт",      СтрокаДоговора.НачОстатокПоДоговоруДт);
		КонечноеСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКт",      СтрокаДоговора.НачОстатокПоДоговоруКт);
		КонечноеСальдоПоДоговору.Вставить("КонОстатокПоДоговоруДтКонтр", СтрокаДоговора.НачОстатокПоДоговоруКт);
		КонечноеСальдоПоДоговору.Вставить("КонОстатокПоДоговоруКтКонтр", СтрокаДоговора.НачОстатокПоДоговоруДт);
		
		// Выводим конечное сальдо по договору.
		ОбластьДоговорИтоги.Параметры.Заполнить(КонечноеСальдоПоДоговору);
		ТабличныйДокумент.Вывести(ОбластьДоговорИтоги);
	КонецЦикла;
	
КонецПроцедуры

Функция НачальноеСальдоПоДоговорам(Шапка)
	
	ЗапросОстаткиДоговоров = Новый Запрос;
	
	ФильтрСписокСчетов = Новый Массив();
	Для Каждого СтрокаСчета Из Шапка.Документ.СписокСчетов Цикл
		Если ЗначениеЗаполнено(СтрокаСчета.Счет) И СтрокаСчета.УчаствуетВРасчетах Тогда
			ФильтрСписокСчетов.Добавить(СтрокаСчета.Счет);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДоговоров = Шапка.Документ.ПоДаннымОрганизации.Выгрузить(,"Договор");
	ТаблицаДоговоров.Свернуть("Договор");
	МассивДоговоров = ТаблицаДоговоров.ВыгрузитьКолонку("Договор");
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	// Дополним массив договорами из табличной части контрагента.
	ТаблицаДоговоровКонтрагента = Шапка.Документ.ПоДаннымКонтрагента.Выгрузить(,"Договор");
	ТаблицаДоговоровКонтрагента.Свернуть("Договор");
	МассивДоговоровКонтрагента = ТаблицаДоговоровКонтрагента.ВыгрузитьКолонку("Договор");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивДоговоров, МассивДоговоровКонтрагента, Истина);
	
	ЗапросОстаткиДоговоров.УстановитьПараметр("Организация",   Шапка.Организация);
	ЗапросОстаткиДоговоров.УстановитьПараметр("НачалоПериода", Шапка.ДатаНачала);
	ЗапросОстаткиДоговоров.УстановитьПараметр("СчетаРасчета",  ФильтрСписокСчетов);
	ЗапросОстаткиДоговоров.УстановитьПараметр("Контрагент",    Шапка.Контрагент);
	ЗапросОстаткиДоговоров.УстановитьПараметр("Договоры",      МассивДоговоров);
	ЗапросОстаткиДоговоров.УстановитьПараметр("Валюта",
		?(Шапка.ВалютаДокумента = ВалютаРегламентированногоУчета, Неопределено, Шапка.ВалютаДокумента));
	Если Шапка.ДоговорКонтрагентаВШапкеЭтоГруппа Тогда
		ЗапросОстаткиДоговоров.УстановитьПараметр("ГруппаДоговоров", Шапка.ДоговорКонтрагентаВШапке);
	Иначе
		ЗапросОстаткиДоговоров.УстановитьПараметр("ГруппаДоговоров", Неопределено);
	КонецЕсли;
	ЗапросОстаткиДоговоров.УстановитьПараметр("Договоры",      МассивДоговоров);
	ЗапросОстаткиДоговоров.УстановитьПараметр("ГоловнаяОрганизация",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Шапка.Организация));
	ЗапросОстаткиДоговоров.УстановитьПараметр("АналитикаРасчетов",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры));
	ЗапросОстаткиДоговоров.УстановитьПараметр("АктСверкиВВалюте",
		Шапка.ВалютаДокумента <> ВалютаРегламентированногоУчета);
		
	ЗапросОстаткиДоговоров.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СчетаРасчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетаРасчета)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОстальныеДоговоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Владелец = &Контрагент
	|	И НЕ ДоговорыКонтрагентов.Ссылка В (&Договоры)
	|	И (&ГруппаДоговоров = НЕОПРЕДЕЛЕНО
	|			ИЛИ ДоговорыКонтрагентов.Ссылка В ИЕРАРХИИ (&ГруппаДоговоров))
	|	И ДоговорыКонтрагентов.ЭтоГруппа = ЛОЖЬ
	|	И ДоговорыКонтрагентов.Организация = &ГоловнаяОрганизация
	|	И (&Валюта = Неопределено ИЛИ ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &Валюта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Договор,
	|	ВЫБОР
	|		КОГДА &АктСверкиВВалюте
	|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстатокДт
	|	КОНЕЦ КАК НачОстатокПоДоговоруДт,
	|	ВЫБОР
	|		КОГДА &АктСверкиВВалюте
	|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстатокКт
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстатокКт
	|	КОНЕЦ КАК НачОстатокПоДоговоруКт
	|ПОМЕСТИТЬ ОстаткиПоДоговорамАкта
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоПериода,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаРасчета.Ссылка
	|				ИЗ
	|					СчетаРасчета КАК СчетаРасчета),
	|			&АналитикаРасчетов,
	|			Организация = &Организация
	|				И Субконто1 В (&Договоры)) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Договор,
	|	ВЫБОР
	|		КОГДА &АктСверкиВВалюте
	|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстатокДт
	|	КОНЕЦ КАК НачОстатокПоДоговоруДт,
	|	ВЫБОР
	|		КОГДА &АктСверкиВВалюте
	|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстатокКт
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстатокКт
	|	КОНЕЦ КАК НачОстатокПоДоговоруКт
	|ПОМЕСТИТЬ ОстаткиПоОстальнымДоговорам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоПериода,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаРасчета.Ссылка
	|				ИЗ
	|					СчетаРасчета КАК СчетаРасчета),
	|			&АналитикаРасчетов,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ОстальныеДоговоры.Ссылка
	|					ИЗ
	|						ОстальныеДоговоры КАК ОстальныеДоговоры)) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоДоговорамАкта.Договор КАК Договор,
	|	СУММА(ОстаткиПоДоговорамАкта.НачОстатокПоДоговоруДт) КАК НачОстатокПоДоговоруДт,
	|	СУММА(ОстаткиПоДоговорамАкта.НачОстатокПоДоговоруКт) КАК НачОстатокПоДоговоруКт
	|ИЗ
	|	ОстаткиПоДоговорамАкта КАК ОстаткиПоДоговорамАкта
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоДоговорамАкта.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоОстальнымДоговорам.Договор КАК Договор,
	|	СУММА(ОстаткиПоОстальнымДоговорам.НачОстатокПоДоговоруДт) КАК НачОстатокПоДоговоруДт,
	|	СУММА(ОстаткиПоОстальнымДоговорам.НачОстатокПоДоговоруКт) КАК НачОстатокПоДоговоруКт
	|ИЗ
	|	ОстаткиПоОстальнымДоговорам КАК ОстаткиПоОстальнымДоговорам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ОстаткиПоОстальнымДоговорам.Договор = ДоговорыКонтрагентов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоОстальнымДоговорам.Договор,
	|	ДоговорыКонтрагентов.Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорыКонтрагентов.Наименование";
	РезультатЗапроса = ЗапросОстаткиДоговоров.ВыполнитьПакет();
	
	НачальноеСальдоПоДоговорам = Новый Структура;
	НачальноеСальдоПоДоговорам.Вставить("ПоДоговорамАкта", РезультатЗапроса[4].Выгрузить());
	НачальноеСальдоПоДоговорам.Вставить("ПоОстальнымДоговорам", РезультатЗапроса[5].Выгрузить());

	Возврат НачальноеСальдоПоДоговорам;
	
КонецФункции

Функция НовыйСтруктураДокумента(ПоДаннымКонтрагента = Ложь)

	СтруктураДокумента = Новый Структура;
	Если ПоДаннымКонтрагента Тогда
		Суффикс = "Контр";
	Иначе
		Суффикс = "";
	КонецЕсли;
	СтруктураДокумента.Вставить("Дата" + Суффикс);
	СтруктураДокумента.Вставить("Представление" + Суффикс);
	СтруктураДокумента.Вставить("Регистратор"   + Суффикс);
	СтруктураДокумента.Вставить("СуммаОборотДт" + Суффикс);
	СтруктураДокумента.Вставить("СуммаОборотКт" + Суффикс);
	
	Возврат СтруктураДокумента;
	
КонецФункции

Функция РезультатСверки(Шапка, ОстатокНаКонец, Организация, Контрагент)
	
	ШаблонРезультатСверки = НСтр("ru='на [ДатаСверки] задолженность [Заключение].'");
	ПараметрыИтогСверки = Новый Структура;
	ПараметрыИтогСверки.Вставить("ДатаСверки", Формат(Шапка.ДатаОкончания, "ДЛФ=D"));
	ПараметрыИтогСверки.Вставить("Заключение", НСтр("ru='отсутствует'"));
	
	Если ОстатокНаКонец <> 0 Тогда
		ШаблонЗаключение = НСтр("ru='в пользу [Представитель] [СуммаЦифрами] [Валюта] ([СуммаПрописью])'");
		ПараметрыЗаключение = Новый Структура("Представитель, СуммаЦифрами, Валюта, СуммаПрописью");
		ПараметрыЗаключение.Валюта = Шапка.ВалютаДокумента;
		Если ОстатокНаКонец > 0 Тогда
			ПараметрыЗаключение.Представитель = Организация;
			Множитель = 1;
		Иначе
			ПараметрыЗаключение.Представитель = Контрагент;
			Множитель = -1;
		КонецЕсли;
		
		ПараметрыЗаключение.СуммаЦифрами = Формат(Множитель * ОстатокНаКонец, "ЧЦ=21; ЧДЦ=2");
		ПараметрыЗаключение.СуммаПрописью = УчетДенежныхСредствБП.ФорматироватьСуммуПрописьюПлатежногоДокумента(
			Множитель * ОстатокНаКонец, Шапка.ВалютаДокумента, Ложь);
		
		ПараметрыИтогСверки.Заключение = 
			СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонЗаключение, ПараметрыЗаключение);
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонРезультатСверки, ПараметрыИтогСверки);
	
КонецФункции

Функция ДанныеПредставителяКонтрагента(Шапка)
	
	ДанныеПредставителяКонтрагента = Новый Структура("Должность, ФИОПолные, Представление");
	
	РеквизитыПредставителяКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Шапка.ПредставительКонтрагента, "Должность, Фамилия, Имя, Отчество");
	
	ДанныеПредставителяКонтрагента.Должность = РеквизитыПредставителяКонтрагента.Должность;
	ДанныеПредставителяКонтрагента.ФИОПолные = ОбщегоНазначенияБПВызовСервера.ПолучитьФамилиюИмяОтчество(
		РеквизитыПредставителяКонтрагента.Фамилия,
		РеквизитыПредставителяКонтрагента.Имя,
		РеквизитыПредставителяКонтрагента.Отчество,
		Ложь);
	
	ДанныеПредставителяКонтрагента.Представление = 
		ФизическиеЛицаКлиентСервер.ФамилияИнициалы(РеквизитыПредставителяКонтрагента);
	
	ЗаполнитьПустыеЗначенияДанныхПредставителя(ДанныеПредставителяКонтрагента);
	
	Возврат ДанныеПредставителяКонтрагента;
	
КонецФункции

Функция ДанныеПредставителяОрганизации(Шапка)
	
	ДанныеПредставителяОрганизации = Новый Структура("Должность, ФИОПолные, Представление");
	
	ОтветственныеЛицаОрганизации = ОтветственныеЛицаБППовтИсп.ОтветственныеЛица(Шапка.Организация, Шапка.ДатаАктаСверки);
	
	Если ОтветственныеЛицаОрганизации.Руководитель = Шапка.ПредставительОрганизации Тогда
		ДанныеФизЛица = ОтветственныеЛицаОрганизации.РуководительФИО;
		ДанныеПредставителяОрганизации.Должность = ОтветственныеЛицаОрганизации.РуководительДолжность;
		
	ИначеЕсли ОтветственныеЛицаОрганизации.ГлавныйБухгалтер = Шапка.ПредставительОрганизации Тогда
		ДанныеФизЛица = ОтветственныеЛицаОрганизации.ГлавныйБухгалтерФИО;
		ДанныеПредставителяОрганизации.Должность = ОтветственныеЛицаОрганизации.ГлавныйБухгалтерДолжность;
		
	Иначе
		// Если это не ответственное лицо, тогда получим его кадровые данные
		ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(
			Шапка.Организация,
			Шапка.ПредставительОрганизации,
			Шапка.ДатаАктаСверки);
		ДанныеПредставителяОрганизации.Должность = ДанныеФизЛица.Должность;

	КонецЕсли;
	
	ДанныеПредставителяОрганизации.Представление = ДанныеФизЛица.Представление;
	ДанныеПредставителяОрганизации.ФИОПолные = ОбщегоНазначенияБПВызовСервера.ПолучитьФамилиюИмяОтчество(
		ДанныеФизЛица.Фамилия,
		ДанныеФизЛица.Имя,
		ДанныеФизЛица.Отчество,
		Ложь);
	
	ЗаполнитьПустыеЗначенияДанныхПредставителя(ДанныеПредставителяОрганизации);
	
	Возврат ДанныеПредставителяОрганизации;
	
КонецФункции

Процедура ЗаполнитьПустыеЗначенияДанныхПредставителя(ДанныеПредставителя)

	Если НЕ ЗначениеЗаполнено(ДанныеПредставителя.Должность) Тогда
		ДанныеПредставителя.Должность = "________________";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДанныеПредставителя.ФИОПолные) Тогда
		ДанныеПредставителя.ФИОПолные = "_______________________";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДанныеПредставителя.Представление) Тогда
		ДанныеПредставителя.Представление = "_______________________";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
