
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы документа.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	УПЖКХ_ТиповыеМетодыСервер.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Устанавливаем значение по умолчанию.
	Если Объект.Ссылка.Пустая() Тогда
		УПЖКХ_ЗаполнениеДокументовСервер.ЗаполнитьШапкуДокумента(Объект, 
																	УПЖКХ_ТиповыеМетодыКлиентСервер.ТекущийПользователь());
		УПЖКХ_ТиповыеМетодыСервер.УстановитьСостояниеДокумента(ЭтотОбъект);
	КонецЕсли;
	
	// Установить доступность формы с учетом ключа СЛК.
	СЗК_МодульЗащитыКлиентСервер.УстановитьДоступностьФормыДляРедактирования(ЭтаФорма);
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаСервере
// Обработчик события "ПриЧтенииНаСервере" формы документа.
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УПЖКХ_ТиповыеМетодыСервер.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	УПЖКХ_ТиповыеМетодыСервер.УстановитьСостояниеДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
// Обработчик события "ПослеЗаписиНаСервере" формы документа.
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УПЖКХ_ТиповыеМетодыСервер.УстановитьСостояниеДокумента(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" поля "Дата".
Процедура ДатаПриИзменении(Элемент)
	
	УПЖКХ_РаботаСДиалогамиКлиентСервер.ПроверитьНомерДокумента(Объект, Объект.Дата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "ЗаполнитьЧленовТСЖ".
Процедура КомандаЗаполнитьЧленовТСЖ(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Дата) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не указана дата документа");
		Возврат;
	КонецЕсли;

	Если Объект.Организация.Пустая() Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не указана организация.");
		Возврат;
	КонецЕсли;
	
	Если Не Объект.СоставПравления.Количество() = 0 Тогда
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Продолжить?";
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьРезультатВопросаКомандаЗаполнить", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	КомандаЗаполнитьПродожение();
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "УстановитьДатуНачала".
Процедура УстановитьДатуНачала(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьДатуНачалаЗавершение", ЭтотОбъект);
	ПоказатьВводЗначения(ОписаниеОповещения, Объект.Дата, "Укажите дату начала");
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "УстановитьДатуНачала".
Процедура УстановитьДатуКонца(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьДатуОкончанияЗавершение", ЭтотОбъект);
	ПоказатьВводЗначения(ОписаниеОповещения, Объект.Дата, "Укажите дату окончания");
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
// Заполняет табличную часть документа.
Процедура ЗаполнитьТабличнуюЧастьНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьТабличнуюЧасть();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

КонецПроцедуры

&НаКлиенте
// Процедура-обработчик результата вопроса, вызванного в процедуре "ОбработатьРезультатВопросаКомандаЗаполнить()".
Процедура ОбработатьРезультатВопросаКомандаЗаполнить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СоставПравления.Очистить();
	
	КомандаЗаполнитьПродожение();

КонецПроцедуры // ОбработатьРезультатВопросаКомандаЗаполнить()

&НаКлиенте
// Процедуре-продолжение процедуры-обработчика команды "КомандаЗаполнитьЧленовТСЖ()".
Процедура КомандаЗаполнитьПродожение()
	
	ЗаполнитьТабличнуюЧастьНаСервере();
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры // КомандаЗаполнитьПродожение()

&НаКлиенте
// Обработчик результата выбор, вызванного в процедуре "УстановитьДатуНачала()".
Процедура УстановитьДатуНачалаЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	Если Не Значение = Неопределено Тогда
		Для Каждого СтрокаТабличнойЧасти Из Объект.СоставПравления Цикл
			СтрокаТабличнойЧасти.ДатаНачала = Значение;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДатуНачалаЗавершение()

&НаКлиенте
// обработчик результата выбор, вызванного в процедуре "УстановитьДатуНачала()".
Процедура УстановитьДатуОкончанияЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	Если Не Значение = Неопределено Тогда
		Для Каждого СтрокаТабличнойЧасти Из Объект.СоставПравления Цикл
			СтрокаТабличнойЧасти.ДатаОкончания = Значение;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДатуНачалаЗавершение()

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	
	УПЖКХ_ТиповыеМетодыСервер.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти
