
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	// Не все регламентные операции допустимо выбирать в формах
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Исключения", ВыполняютсяСпециальнымиДокументами());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыРегламентныхОпераций.Ссылка КАК Значение,
	|	ПРЕДСТАВЛЕНИЕ(ВидыРегламентныхОпераций.Ссылка) КАК Представление,
	|	ВидыРегламентныхОпераций.Порядок КАК Порядок
	|ИЗ
	|	Перечисление.ВидыРегламентныхОпераций КАК ВидыРегламентныхОпераций
	|ГДЕ
	|	НЕ ВидыРегламентныхОпераций.Ссылка В (&Исключения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Значение, Выборка.Представление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
	
#Область ПрограммныйИнтерфейс

Функция ВыполняютсяСпециальнымиДокументами() Экспорт
	
	Операции = Новый Массив;
	Операции.Добавить(Перечисления.ВидыРегламентныхОпераций.НачислениеЗарплаты);
	Операции.Добавить(Перечисления.ВидыРегламентныхОпераций.НачислениеРезерваОтпусков);
	Операции.Добавить(Перечисления.ВидыРегламентныхОпераций.НачислениеНДС);
	Возврат Операции;
	
КонецФункции

#КонецОбласти
