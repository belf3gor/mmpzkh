#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

// Определяет порядок уплаты налога в зависимости от переданного вида агентского договора.
// Анализируется только налоговый агент по приобретению, налоговый агент при реализации комиссионных товаров
// или услуг в данной функции не рассматривается.
// Параметры:
//     ВидАгентскогоДоговора - ПеречислениеСсылка.ВидыАгентскихДоговоров - вид договора налогового агента, 
//                             для которого необходимо определить порядок уплаты.
// Возвращаемое значение:
//     Булево - Истина - если налог уплачивается сразу при начислении,
//              Ложь - если налог уплачивается по 1/3 в течение квартала, следующего за кварталом начисления.
Функция НалогУплачиваетсяПриНачислении(ВидАгентскогоДоговора) Экспорт
	
	Возврат ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.Нерезидент
	    ИЛИ ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги;
	
КонецФункции

// Определяет, относится ли переданный вид агентского договора договором с нерезидентом.
// Анализируется только налоговый агент по приобретению, налоговый агент при реализации комиссионных товаров
// или услуг в данной функции не рассматривается.
// Параметры:
//     ВидАгентскогоДоговора - ПеречислениеСсылка.ВидыАгентскихДоговоров - вид договора налогового агента, 
//                             для которого необходимо определить признак.
// Возвращаемое значение:
//     Булево - Истина - договор с нерезидентом,
//              Ложь - договор с резидентом.
Функция ЭтоНерезидент(ВидАгентскогоДоговора) Экспорт
	
	Возврат НалогУплачиваетсяПриНачислении(ВидАгентскогоДоговора);
	
КонецФункции

// Возвращает массив видов договоров налогового агента по которым требуется округление налога.
// Возвращаемое значение:
//     ВидыДоговоров - Массив - массив значений виды ПеречислениеСсылка.ВидыАгентскихДоговоров.
Функция МассивВидовДоговоровТребующихОкругленияНалога() Экспорт
	
	ВидыДоговоров = Новый Массив;
	ВидыДоговоров.Добавить(Перечисления.ВидыАгентскихДоговоров.Нерезидент);
	ВидыДоговоров.Добавить(Перечисления.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги);
	ВидыДоговоров.Добавить(Перечисления.ВидыАгентскихДоговоров.РеализацияИмущества);
	
	Возврат ВидыДоговоров;
	
КонецФункции

// Возвращает массив видов договоров налогового агента с нерезидентом.
// Возвращаемое значение:
//     ВидыДоговоров - Массив - массив значений виды ПеречислениеСсылка.ВидыАгентскихДоговоров.
Функция МассивВидовДоговоровСНерезидентом() Экспорт
	
	ВидыДоговоров = Новый Массив;
	ВидыДоговоров.Добавить(Перечисления.ВидыАгентскихДоговоров.Нерезидент);
	ВидыДоговоров.Добавить(Перечисления.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги);
	
	Возврат ВидыДоговоров;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора         = ПолучитьДоступныеЗначения(Параметры.Отбор, Параметры.СтрокаПоиска);
	
КонецПроцедуры

#КонецОбласти

Функция ПолучитьДоступныеЗначения(Отбор, СтрокаПоиска)
	
	Исключения = Новый Массив;
	
	Если НЕ БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС() Тогда
		Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИсполняютсяОбязанностиНалоговогоАгентаПоНДС") Тогда
		Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.Аренда);
		Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.РеализацияИмущества);
		Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.Нерезидент);
		Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги);
	ИначеЕсли ТипЗнч(Отбор) = Тип("Структура") 
		И Отбор.Свойство("ИностранныйКонтрагент") Тогда 
		
		// Для списков с отбором не показываем для выбора вид агентского договора "Электронные услуги"
		Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги);
		Если НЕ Отбор.ИностранныйКонтрагент Тогда
			Исключения.Добавить(Перечисления.ВидыАгентскихДоговоров.Нерезидент);
		КонецЕсли;
	
	КонецЕсли;
	
	ДоступныеЗначения = Новый СписокЗначений;
	
	Для каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыАгентскихДоговоров.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(СтрокаПоиска)) <> 1 Тогда
			Продолжить;
		КонецЕсли;
		Ссылка = Перечисления.ВидыАгентскихДоговоров[ЗначениеПеречисления.Имя];
		Если ТипЗнч(Отбор) = Тип("ПеречислениеСсылка.ВидыАгентскихДоговоров")
			И Отбор <> Ссылка Тогда
			Продолжить;
		ИначеЕсли (ТипЗнч(Отбор) = Тип("ФиксированныйМассив") ИЛИ ТипЗнч(Отбор) = Тип("Массив"))
			И Отбор.Найти(Ссылка) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Исключения.Найти(Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДоступныеЗначения.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;
	
	Возврат ДоступныеЗначения;
	
КонецФункции

#КонецОбласти

#КонецЕсли