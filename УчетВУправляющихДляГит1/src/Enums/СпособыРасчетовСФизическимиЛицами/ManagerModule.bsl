
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор <> Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка,
		|	Таблица.Синоним КАК Синоним,
		|	Таблица.Порядок КАК Порядок
		|ПОМЕСТИТЬ ВТТаблицаСпособовРасчетов
		|ИЗ
		|	&ТаблицаСпособовРасчетов КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка,
		|	Таблица.Порядок КАК Порядок
		|ИЗ
		|	ВТТаблицаСпособовРасчетов КАК Таблица
		|ГДЕ
		|	(Таблица.Ссылка В (&ДоступныеСпособыРасчетов)
		|			ИЛИ &ПоВсемСпособамРасчетов)
		|	И Таблица.Синоним ПОДОБНО &СтрокаПоиска
		|
		|УПОРЯДОЧИТЬ ПО
		|	Таблица.Порядок";
		
		Если Параметры.СтрокаПоиска = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.Синоним ПОДОБНО &СтрокаПоиска", "");
		КонецЕсли;
		
		// Составляем таблицу способов расчетов.
		ТаблицаСпособовРасчетов = Новый ТаблицаЗначений;
		ТаблицаСпособовРасчетов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыРасчетовСФизическимиЛицами"));
		ТаблицаСпособовРасчетов.Колонки.Добавить("Синоним", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(256)));
		ТаблицаСпособовРасчетов.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
		
		ДоступныеСпособыРасчетов = Неопределено;
		Если Параметры.Отбор.Свойство("ГруппаСпособов") Тогда
			ДоступныеСпособыРасчетов = Перечисления.СпособыРасчетовСФизическимиЛицами.ДоступныеСпособыРасчетов(Параметры.Отбор.ГруппаСпособов);
		КонецЕсли;
		
		Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.СпособыРасчетовСФизическимиЛицами.ЗначенияПеречисления Цикл
			НоваяСтрока = ТаблицаСпособовРасчетов.Добавить();
			НоваяСтрока.Ссылка = Перечисления.СпособыРасчетовСФизическимиЛицами[ЗначениеПеречисления.Имя];
			НоваяСтрока.Синоним = ЗначениеПеречисления.Синоним;
			// Расставляем заданный порядок.
			Если ДоступныеСпособыРасчетов <> Неопределено Тогда
				НоваяСтрока.Порядок = ДоступныеСпособыРасчетов.Найти(НоваяСтрока.Ссылка);
			КонецЕсли;
		КонецЦикла;
		
		// Отбор только по доступным способам с учетом введенной строки.
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ТаблицаСпособовРасчетов", ТаблицаСпособовРасчетов);
		Запрос.УстановитьПараметр("ДоступныеСпособыРасчетов", ДоступныеСпособыРасчетов);
		Запрос.УстановитьПараметр("ПоВсемСпособамРасчетов", ДоступныеСпособыРасчетов = Неопределено);
		Запрос.УстановитьПараметр("СтрокаПоиска", Строка(Параметры.СтрокаПоиска) + "%");
		РезультатЗапроса = Запрос.Выполнить();
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формируется массив доступных способов расчетов с учетом указанной ГруппыСпособов.
//	Параметры
//		ГруппыСпособов - тип Строка
//
Функция ДоступныеСпособыРасчетов(ГруппаСпособов) Экспорт
	
	СпособыРасчетов = Новый Массив;
	
	Если ГруппаСпособов = "ДоговорыГПХ" Тогда
		
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда);
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами);
		
	ИначеЕсли ГруппаСпособов = "ПрочиеРасчетыСПерсоналом" Тогда	
		
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда);
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.ПрочиеРасчетыСПерсоналом);
		
	ИначеЕсли ГруппаСпособов = "Ведомости" Тогда
		
		ИмяОпции = "ИспользоватьНачисленияПоДоговорам";
		ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
		
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда);
		Если ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции) Тогда
			СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами);
		КонецЕсли;
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.ПрочиеРасчетыСПерсоналом);	
		
	ИначеЕсли ГруппаСпособов = "ПрочиеДоходы" Тогда
		
		СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами);
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.Дивиденды") Тогда
			СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.Дивиденды);
			СпособыРасчетов.Добавить(Перечисления.СпособыРасчетовСФизическимиЛицами.ДивидендыСотрудникам);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СпособыРасчетов;
	
КонецФункции

#КонецОбласти

#КонецЕсли