#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив разделов монитора руководителя по умолчанию
// Возвращаемое значение:
// Массив - Список разделов монитора руководителя доступных по умолчанию
Функция РазделыПерсонализированныхДанныхПоУмолчанию() Экспорт
	
	РазделыПоУмолчанию = Новый Массив;
	РазделыПоУмолчанию.Добавить(КоличествоПоступлений);
	РазделыПоУмолчанию.Добавить(КоличествоРеализаций);
	РазделыПоУмолчанию.Добавить(НДСКВычету);
	РазделыПоУмолчанию.Добавить(СреднесписочнаяЧисленность);
	РазделыПоУмолчанию.Добавить(КоличествоКонтрагентов);
	
	Возврат РазделыПоУмолчанию;
	
КонецФункции

// Возвращает массив разделов монитора руководителя по умолчанию
// Возвращаемое значение:
// Массив - Список разделов монитора руководителя доступных по умолчанию
Функция РазделыПерсонализированныхДанныхПодключенныеСервисы() Экспорт
	
	РазделыПодключенныеСервисы = Новый Массив;
	РазделыПодключенныеСервисы.Добавить(Сервис1СОтчетностьПодключен);
	РазделыПодключенныеСервисы.Добавить(Сервис1СКонтрагентПодключен);
	РазделыПодключенныеСервисы.Добавить(Сервис1СПАРКПодключен);
	РазделыПодключенныеСервисы.Добавить(Сервис1СЭДОПодключен);
	
	Возврат РазделыПодключенныеСервисы;
	
КонецФункции

// Определяет перечень разделов, к данным которых у пользователя есть доступ.
// 
// Возвращаемое значение:
//  ФиксированныйМассив - содержит элементы перечисления
//
Функция РазрешенныеПользователюРазделыПерсонализированныхДанных() Экспорт
	
	РазрешенныеРазделы = Новый Массив;
	
	ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь( , , Ложь);
	
	Для Каждого МетаданныеЗначения Из Метаданные.Перечисления.РазделыПерсонализированныхДанных.ЗначенияПеречисления Цикл
		
		Раздел = Перечисления.РазделыПерсонализированныхДанных[МетаданныеЗначения.Имя];
		
		Если Не ПолноправныйПользователь И Не ДанныеРазделаДоступны(Раздел) Тогда
			Продолжить;
		КонецЕсли;
		
		РазрешенныеРазделы.Добавить(Раздел);
		
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(РазрешенныеРазделы);
	
КонецФункции

// Проверяет достаточность прав пользователя для просмотра разделов персонализированных данных.
// Эти права определяются правами пользователя на просмотр исходных данных,
// на основании которых рассчитаны персонализированных данных.
//
// Может быть вызвана в привилегированном режиме.
//
// Параметры:
//  Раздел	 - ПеречислениеСсылка.РазделыПерсонализированныхДанных - раздел данных,
//             доступ к данным раздела которого проверяется
// 
// Возвращаемое значение:
//  Булево - Истина, если данные раздела доступны пользователю.
//
Функция ДанныеРазделаДоступны(Раздел) Экспорт
	
	// Может быть вызвана в привилегированном режиме.
	// В этом случае в ПравоДоступа() следует указывать параметр Пользователь.
	// Но может быть вызвана и вне привилегированного режима.
	// В этом случае параметр Пользователь указывать не следует.
	// Чтобы не делать две ветки, устанавливаем привилегированный режим.
	УстановитьПривилегированныйРежим(Истина);
	
	Если Раздел = КоличествоПоступлений Тогда
		
		Возврат ПравоДоступа(
			"Просмотр",
			Метаданные.Документы.ПоступлениеТоваровУслуг,
			ПользователиИнформационнойБазы.ТекущийПользователь());
			
	ИначеЕсли Раздел = КоличествоРеализаций Тогда
		
		Возврат ПравоДоступа(
			"Просмотр",
			Метаданные.Документы.РеализацияТоваровУслуг,
			ПользователиИнформационнойБазы.ТекущийПользователь());
			
	ИначеЕсли Раздел = КоличествоКонтрагентов Тогда
		
		Возврат ПравоДоступа(
			"Просмотр",
			Метаданные.Справочники.Контрагенты,
			ПользователиИнформационнойБазы.ТекущийПользователь());

	ИначеЕсли Раздел = СреднесписочнаяЧисленность Тогда
		
		Возврат УправлениеДоступомБПВызовСервера.ПравоПросмотраВсехКадровыхДанных();
		
	ИначеЕсли Раздел = Сервис1СКонтрагентПодключен Тогда
		
		Возврат ПравоДоступа(
			"Просмотр",
			Метаданные.Справочники.Контрагенты,
			ПользователиИнформационнойБазы.ТекущийПользователь());
			
	ИначеЕсли Раздел = Сервис1СПАРКПодключен Тогда
		
		Возврат Пользователи.РолиДоступны("ЧтениеДанныхСПАРКРиски");
		
	ИначеЕсли Раздел = Сервис1СОтчетностьПодключен Тогда
		
		Возврат ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.РегламентированнаяОтчетность);
		
	ИначеЕсли Раздел = Сервис1СЭДОПодключен Тогда
		
		Возврат ПравоДоступа(
			"Просмотр",
			Метаданные.Справочники.НастройкиЭДО,
			ПользователиИнформационнойБазы.ТекущийПользователь());
			
	Иначе
		
		Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии();
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли