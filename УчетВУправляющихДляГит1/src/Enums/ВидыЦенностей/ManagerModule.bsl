
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора         = ПолучитьДоступныеЗначения(Параметры.Отбор, Параметры.СтрокаПоиска).ДоступныеЗначения;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает вид ценности, соответствующий виду переданного договора налогового агента.
// Параметры
//     ВидАгентскогоДоговора - ПеречислениеСсылка.ВидыАгентскихДоговоров.
// Возвращаемое значение:
//     ВидЦенности - ПеречислениеСсылка.ВидыЦенностей.
Функция ВидЦенностиПоВидуАгентскогоДоговора(ВидАгентскогоДоговора) Экспорт
	
	Если ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.Аренда Тогда
		Возврат Перечисления.ВидыЦенностей.НалоговыйАгентАренда;
	ИначеЕсли ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияИмущества Тогда
		Возврат Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества;
	ИначеЕсли ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.Нерезидент Тогда 
		Возврат Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы;
	ИначеЕсли ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги Тогда
		Возврат Перечисления.ВидыЦенностей.НалоговыйАгентЭлектронныеУслуги;
	ИначеЕсли ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров Тогда
		Возврат Перечисления.ВидыЦенностей.ТоварыНалоговыйАгент;
	КонецЕсли;
	
КонецФункции

// Определяет, относится ли переданный вид ценности к операциям налогового агента по НДС.
// Параметры
//     ВидЦенности - ПеречислениеСсылка.ВидыЦенностей.
// Возвращаемое значение: 
//     Булево - Истина - вид ценности относится к операциям налогового агента по НДС,
//              Ложь - вид ценности не относится к операциям налогового агента по НДС.
Функция ЭтоНалоговыйАгент(ВидЦенности) Экспорт
	
	ВидыЦенностейНалоговогоАгента = МассивВидовЦенностиНалоговыйАгент();
	Возврат ВидыЦенностейНалоговогоАгента.Найти(ВидЦенности) <> Неопределено;
	
КонецФункции

// Определяет, относится ли переданный вид ценности к операциям налогового агента по НДС за нерезидента.
// Анализируется только налоговый агент по приобретению, налоговый агент при реализации комиссионных товаров
// или услуг в данной функции не рассматривается.
// Параметры
//     ВидЦенности - ПеречислениеСсылка.ВидыЦенностей.
// Возвращаемое значение: 
//     Булево - Истина - вид ценности относится к операциям налогового агента по НДС за нерезидента,
//              Ложь - вид ценности не относится к операциям налогового агента по НДС за нерезидента.
Функция ЭтоНалоговыйАгентЗаНерезидента(ВидЦенности) Экспорт
	
	Если ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы
		ИЛИ ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентЭлектронныеУслуги Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает массив видов ценностей, относящихся к операциям налогового агента по НДС.
// Параметры
//     ВключаяКомиссионныеОперации - Булево - признак необходимости включения в состав возвращаемого массива
//                                            операций реализации ценностей комитента-нерезидента.
//     ВключаяОбратноеНачисление   - Булево - признак необходимости включения в состав возвращаемого массива
//                                            операций реализации товаров п.8 ст. 161 НК.
// Возвращаемое значение: 
//     Массив - массив значений вида ПеречислениеСсылка.ВидыЦенностей.
Функция МассивВидовЦенностиНалоговыйАгент(
		ВключаяКомиссионныеОперации = Истина, ВключаяОбратноеНачисление = Ложь) Экспорт
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентЭлектронныеУслуги);
	
	Если ВключаяКомиссионныеОперации Тогда
		ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
	КонецЕсли;
	
	Если ВключаяОбратноеНачисление Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ВидыЦенностей,
			МассивВидовЦенностиНалоговыйАгентРеализацияТоваров());
	КонецЕсли;
	
	Возврат ВидыЦенностей;
	
КонецФункции

// Возвращает массив видов ценностей, по которым производится поиск счета-фактуры с учетом договора.
// Возвращаемое значение: 
//     Массив - массив значений вида ПеречислениеСсылка.ВидыЦенностей.
Функция МассивВидовЦенностейУчитывающихсяПоДоговорам() Экспорт
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		ВидыЦенностей,
		МассивВидовЦенностиНалоговыйАгент());
		
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		ВидыЦенностей,
		МассивВидовЦенностиНалоговыйАгентРеализацияТоваров());
		
	Возврат ВидыЦенностей;
	
КонецФункции
	
// Определяет, производится ли поиск счета-фактуры по переданному виду ценности с учетом договора.
// Параметры
//     ВидЦенности - ПеречислениеСсылка.ВидыЦенностей.
// Возвращаемое значение: 
//     Булево - Истина - вид ценности учитывается по договорам,
//              Ложь - вид ценности не учитывается по договорам.
Функция УчитываетсяПоДоговорам(ВидЦенности) Экспорт
	
	ВидыЦенностейУчитывающиесяПоДоговорам = МассивВидовЦенностейУчитывающихсяПоДоговорам();
	Возврат ВидыЦенностейУчитывающиесяПоДоговорам.Найти(ВидЦенности) <> Неопределено;
	
КонецФункции

// Возвращает массив видов ценностей, относящихся к операциям налогового агента при покупке товаров,
// указанных в п.8 ст. 161 НК РФ.
// Возвращаемое значение: 
//     Массив - массив значений вида ПеречислениеСсылка.ВидыЦенностей.
Функция МассивВидовЦенностиНалоговыйАгентРеализацияТоваров() Экспорт
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыВыданныеНалоговыйАгент);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученныеНалоговыйАгент);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученныхНалоговыйАгент);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТоварыНалоговыйАгент);
	
	Возврат ВидыЦенностей;
	
КонецФункции

// Функция выполняет заполнение списка значений 
// данных выбора с учетом настроек параметров учета.
// Поддерживается параметр отбора.
// Обрабатывается также строка поиска.
//
Функция ПолучитьДоступныеЗначения(Отбор, СтрокаПоиска)
	
	Исключения = Новый Массив;
	//Служебный из интерактивного выбора исключен всегда
	Исключения.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	Если ТипЗнч(Отбор) = Тип("Структура") И Отбор.Свойство("ВидУчетаНДС") И ЗначениеЗаполнено(Отбор.ВидУчетаНДС) Тогда
		// Настройка исключений, все что включено в массив Исключения удаляется из списка выбора
		Если Отбор.ВидУчетаНДС = "НДСПолученный" Тогда
			ВидыЦенностейНДСАвансыПолученные(Исключения);
			ВидыЦенностейНДСАвансыВыданные(Исключения);
		ИначеЕсли Отбор.ВидУчетаНДС = "НДСНачисленный" Тогда
			ВидыЦенностейНДСАвансыПолученные(Исключения);
			ВидыЦенностейНДСАвансыВыданные(Исключения);
			ВидыЦенностейНДСВозврат(Исключения);
		ИначеЕсли Отбор.ВидУчетаНДС = "НДСАвансы" Тогда
			ВидыЦенностейНДСПолученный(Исключения);
			ВидыЦенностейНДСНачисленный(Исключения);
			ВидыЦенностейНДСВозврат(Исключения);
			ВидыЦенностейНДСНалоговыйАгент(Исключения);
			ВидыЦенностейНДСТаможенныеПлатежи(Исключения);
		КонецЕсли;
		
	КонецЕсли;
	
	ДоступныеЗначения = Новый СписокЗначений;
	ЕстьНедоступные   = Ложь;
	
	Для каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыЦенностей.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(СтрокаПоиска)) <> 1 Тогда
			Продолжить;
		КонецЕсли;
		Ссылка = Перечисления.ВидыЦенностей[ЗначениеПеречисления.Имя];
		Если ТипЗнч(Отбор) = Тип("ПеречислениеСсылка.ВидыЦенностей")
			И Отбор <> Ссылка Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(Отбор) = Тип("ФиксированныйМассив")
			И Отбор.Найти(Ссылка) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Исключения.Найти(Ссылка) <> Неопределено Тогда
			ЕстьНедоступные = Истина;
			Продолжить;
		КонецЕсли;
		ДоступныеЗначения.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;
	
	Возврат Новый Структура("ДоступныеЗначения, ЕстьНедоступные", ДоступныеЗначения, ЕстьНедоступные);
	
КонецФункции

#Область ОграниченияПоВидамЦенностей

Процедура ВидыЦенностейНДСПолученный(МассивЗначений)
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.Материалы);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.НМА);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.Товары);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ОС);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.Оборудование);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ОбъектыНезавершенногоСтроительства);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ПосредническиеУслуги);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.СМРПодрядные);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.КомандировочныеРасходы);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ПредставительскиеРасходы);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ТоварыУслугиКомитентов);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ТаможенныйСоюз);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ТоварыНесырьевые);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ВнутреннееПотребление);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ИмуществоСУчетомНДС);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.СуммыСвязанныеСРасчетамиПоОплате);
КонецПроцедуры

Процедура ВидыЦенностейНДСНалоговыйАгент(МассивЗначений)
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентЭлектронныеУслуги);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ТоварыНалоговыйАгент);
КонецПроцедуры

Процедура ВидыЦенностейНДСТаможенныеПлатежи(МассивЗначений)
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежи);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС);
КонецПроцедуры

Процедура ВидыЦенностейНДСАвансыПолученные(МассивЗначений)
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.АвансыПолученныеНалоговыйАгент);
КонецПроцедуры

Процедура ВидыЦенностейНДСАвансыВыданные(МассивЗначений)
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.АвансыВыданныеНалоговыйАгент);
КонецПроцедуры

Процедура ВидыЦенностейНДСВозврат(МассивЗначений)
	МассивЗначений.Добавить(Перечисления.ВидыЦенностей.Возврат);
КонецПроцедуры

Процедура ВидыЦенностейНДСНачисленный(МассивЗначений)
	ВидыЦенностейНДСПолученный(МассивЗначений);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти