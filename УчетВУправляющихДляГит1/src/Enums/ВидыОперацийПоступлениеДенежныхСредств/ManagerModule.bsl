#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора         = ПолучитьДоступныеЗначения(Параметры.Отбор, Параметры.СтрокаПоиска).ДоступныеЗначения;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Отбор") Тогда
		Параметры.Вставить("Отбор", Новый Структура);
	КонецЕсли;
	
	ДанныеВыбора = ПолучитьДоступныеЗначения(Параметры.Отбор, Неопределено).ДоступныеЗначения;
	Параметры.Отбор.Очистить();
	Параметры.Отбор.Вставить("Ссылка", ДанныеВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ПолучитьСписокДоступныхЗначений(Отбор, ЕстьНедоступные = Ложь) Экспорт
	
	СтруктураДоступныхЗначений = ПолучитьДоступныеЗначения(Отбор, Неопределено);
	
	ЕстьНедоступные = СтруктураДоступныхЗначений.ЕстьНедоступные;
	
	Возврат СтруктураДоступныхЗначений.ДоступныеЗначения;
	
КонецФункции

// Возвращает виды операции доступные для использования при включенной функциональной опции ИнтеграцияСБанком.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица операций, доступных при интеграции с банком.
//    * ВидОперации       - ПеречислениеСсылка.ВидыОперацийПоступлениеДенежныхСредств - вид операции.
//    * ОблагаетсяНалогом - Булево, Строка(0) - если Истина, то доход по этому виду операции облагается налогом УСН (доходы).
//                                              если пустая строка, то решение о включении в налоговую базу должен принять пользователь.
//
Функция ВидыОперацийДоступныеПриИнтеграцииСБанком() Экспорт
	
	Менеджер = Перечисления.ВидыОперацийПоступлениеДенежныхСредств;
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ВидОперации",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоступлениеДенежныхСредств"));
	Результат.Колонки.Добавить("ОблагаетсяНалогом",
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ОплатаПокупателя;
	НовыйРезультат.ОблагаетсяНалогом = "Да";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ВозвратОтПоставщика;
	НовыйРезультат.ОблагаетсяНалогом = "Нет";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.РасчетыПоКредитамИЗаймам;
	НовыйРезультат.ОблагаетсяНалогом = "Да";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ПолучениеЗайма;
	НовыйРезультат.ОблагаетсяНалогом = "Нет";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ПолучениеКредита;
	НовыйРезультат.ОблагаетсяНалогом = "Да";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ВозвратЗаймаКонтрагентом;
	НовыйРезультат.ОблагаетсяНалогом = "";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ПрочиеРасчетыСКонтрагентами;
	НовыйРезультат.ОблагаетсяНалогом = "";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ПереводСДругогоСчета;
	НовыйРезультат.ОблагаетсяНалогом = "Нет";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ПоступленияОтПродажПоПлатежнымКартамИБанковскимКредитам;
	НовыйРезультат.ОблагаетсяНалогом = "Да";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ЛичныеСредстваПредпринимателя;
	НовыйРезультат.ОблагаетсяНалогом = "Нет";
	
	НовыйРезультат = Результат.Добавить();
	НовыйРезультат.ВидОперации = Менеджер.ПрочееПоступление;
	НовыйРезультат.ОблагаетсяНалогом = "";
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция выполняет заполнение списка значений 
// данных выбора с учетом настроек параметров учета.
// Поддерживается параметр отбора.
// Обрабатывается также строка поиска.
//
Функция ПолучитьДоступныеЗначения(Отбор, СтрокаПоиска)
	
	Исключения = Новый Массив;
	
	Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.РасчетыПоКредитамИЗаймам);
	
	Организация = Неопределено;
	БанковскийСчет = Справочники.БанковскиеСчета.ПустаяСсылка();
	СчетБанк = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		Если Отбор.Свойство("Организация") И ЗначениеЗаполнено(Отбор.Организация) Тогда
			Организация = Отбор.Организация;
		КонецЕсли;
		
		Если Отбор.Свойство("СчетОрганизации") И ЗначениеЗаполнено(Отбор.СчетОрганизации) Тогда
			БанковскийСчет = Отбор.СчетОрганизации;
		КонецЕсли;
		
		Если Отбор.Свойство("СчетБанк") И ЗначениеЗаполнено(Отбор.СчетБанк) Тогда
			СчетБанк = Отбор.СчетБанк;
		КонецЕсли;
	КонецЕсли;
	
	// Если отбор не передан, получим значение "по умолчанию", либо "единственную" организацию в ИБ.
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	Иначе
		ЭтоЮрЛицо = Истина; // ИП обычно ведет в базе только себя, а в этом случае организация будет получена.
	КонецЕсли;
	
	Валютный = Ложь;
	Если ЗначениеЗаполнено(Организация) Тогда
		Если Не Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Организация)
			И Не БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
			Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПереводСДругогоСчета);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетБанк) Тогда
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетБанк);
			Валютный = СвойстваСчета.Валютный;
		КонецЕсли;
		
		Если Валютный Тогда
			Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты);
			Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаОтФакторинговойКомпании);
		Иначе
			Если НЕ ЗначениеЗаполнено(БанковскийСчет) Тогда
				УчетДенежныхСредствБП.УстановитьБанковскийСчет(БанковскийСчет, Организация, Неопределено, Ложь);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(БанковскийСчет) Тогда
				Валютный = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Валютный");
				Если Валютный Тогда
					Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты);
					Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаОтФакторинговойКомпании);
				Иначе
					Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПриобретениеИностраннойВалюты);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты);
		Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПриобретениеИностраннойВалюты);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьФакторинг") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаОтФакторинговойКомпании);
	КонецЕсли;
	
	Если НЕ Константы.ИспользоватьИнкассацию.Получить() Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.Инкассация);
	КонецЕсли;
	
	Если ЭтоЮрЛицо ИЛИ НЕ ПолучитьФункциональнуюОпцию("ВестиУчетИндивидуальногоПредпринимателя") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ЛичныеСредстваПредпринимателя);
	КонецЕсли;
	
	Если (НЕ УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров()
		ИЛИ (НЕ ЭтоЮрЛицо И НЕ УчетЗарплаты.ИПИспользуетТрудНаемныхРаботников(Организация)))
		ИЛИ Валютный Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратЗаймаРаботником);
	КонецЕсли;
	
	ДоступныеЗначения = Новый СписокЗначений;
	ЕстьНедоступные   = Ложь;
	
	Для каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(СтрокаПоиска)) <> 1 Тогда
			Продолжить;
		КонецЕсли;
		Ссылка = Перечисления.ВидыОперацийПоступлениеДенежныхСредств[ЗначениеПеречисления.Имя];
		Если ТипЗнч(Отбор) = Тип("ПеречислениеСсылка.ВидыОперацийПоступлениеДенежныхСредств")
			И Отбор <> Ссылка Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(Отбор) = Тип("ФиксированныйМассив")
			И Отбор.Найти(Ссылка) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Исключения.Найти(Ссылка) <> Неопределено Тогда
			ЕстьНедоступные = Истина;
			Продолжить;
		КонецЕсли;
		ДоступныеЗначения.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;
	
	Возврат Новый Структура("ДоступныеЗначения, ЕстьНедоступные", ДоступныеЗначения, ЕстьНедоступные);
	
КонецФункции

#КонецОбласти

#КонецЕсли