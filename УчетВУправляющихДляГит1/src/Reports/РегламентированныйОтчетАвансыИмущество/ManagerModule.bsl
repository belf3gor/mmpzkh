#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НаДату >= '20080101' Тогда
		Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	КонецЕсли;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт

	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(254));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2008Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение №3 к приказу Минфина РФ от 20.02.2008 г. №27н.";
	НоваяФорма.РедакцияФормы      = "от 20.02.2008 г. №27н.";
	НоваяФорма.ДатаНачалоДействия = '2008-01-01';
	НоваяФорма.ДатаКонецДействия  = '2011-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2011Кв3";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 4 к приказу ФНС России от 24.11.2011 № ММВ-7-11/895.";
	НоваяФорма.РедакцияФормы      = "от 24.11.2011 № ММВ-7-11/895.";
	НоваяФорма.ДатаНачалоДействия = '2012-01-01';
	НоваяФорма.ДатаКонецДействия  = '2013-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2013Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 4 к приказу ФНС России от 05.11.2013 № ММВ-7-11/478@.";
	НоваяФорма.РедакцияФормы      = "от 05.11.2013 № ММВ-7-11/478@.";
	НоваяФорма.ДатаНачалоДействия = '2014-01-01';
	НоваяФорма.ДатаКонецДействия  = '2015-09-30';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2017Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 4 к приказу ФНС России от 31.03.2017 № ММВ-7-21/271@.";
	НоваяФорма.РедакцияФормы      = "от 31.03.2017 № ММВ-7-21/271@.";
	НоваяФорма.ДатаНачалоДействия = '2017-01-01';
	НоваяФорма.ДатаКонецДействия  = '2018-09-30';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2013Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 4 к приказу ФНС России от 05.11.2013 № ММВ-7-11/478@ (в редакции письма ФНС России от 25.03.2016 № БС-4-11/5197@).";
	НоваяФорма.РедакцияФормы      = "от 25.03.2016 № БС-4-11/5197@.";
	НоваяФорма.ДатаНачалоДействия = '2016-01-01';
	НоваяФорма.ДатаКонецДействия  = '2017-09-30';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2019Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 4 к приказу ФНС России от 31.03.2017 № ММВ-7-21/271@ (в редакции приказа ФНС России от 04.10.2018 № ММВ-7-21/575@).";
	НоваяФорма.РедакцияФормы      = "от 04.10.2018 № ММВ-7-21/575@.";
	НоваяФорма.ДатаНачалоДействия = '2019-01-01';
	НоваяФорма.ДатаКонецДействия  = '2019-09-30';
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	Если ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2017Кв1" Тогда
		
		ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат ТаблицаДанныхРеглОтчета;
		КонецЕсли;
		
		Если ДанныеРеглОтчета.Свойство("ДанныеМногоуровневыхРазделов") Тогда
			
			ДанныеМногострочнойЧастиРаздела1
			= ДанныеРеглОтчета.ДанныеМногоуровневыхРазделов["Раздел1"].Строки[0].ДанныеМногострочныхЧастей["П0000100"];
			
			Для каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
				Сумма = ТаблицаДанныхРеглОтчета.Добавить();
				Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО  = СтрокаМнЧ.Данные.П000010001003;
				Сумма.КБК    = СтрокаМнЧ.Данные.П000010002003;
				Сумма.Сумма  = СтрокаМнЧ.Данные.П000010003003;
			КонецЦикла;
			
		ИначеЕсли ДанныеРеглОтчета.Свойство("ДанныеМногострочныхРазделов") Тогда // отчет сохранен в 2.0
			
			ДанныеМногострочнойЧастиРаздела1 = ДанныеРеглОтчета.ДанныеМногострочныхРазделов.П00001;
			
			Для каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1 Цикл
				Сумма = ТаблицаДанныхРеглОтчета.Добавить();
				Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО  = СтрокаМнЧ.П000010001003;
				Сумма.КБК    = СтрокаМнЧ.П000010002003;
				Сумма.Сумма  = СтрокаМнЧ.П000010003003;
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2013Кв4" Тогда
		
		ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат ТаблицаДанныхРеглОтчета;
		КонецЕсли;
		
		Если ДанныеРеглОтчета.ДанныеМногострочныхРазделов.Свойство("П00001000") Тогда
			
			Для Каждого СтрТаблицы Из ДанныеРеглОтчета.ДанныеМногострочныхРазделов.П00001000 Цикл
				Сумма = ТаблицаДанныхРеглОтчета.Добавить();
				Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО  = СтрТаблицы.П000010001003;
				Сумма.КБК    = СтрТаблицы.П000010002003;
				Сумма.Сумма  = СтрТаблицы.П000010003003;
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2011Кв3" Тогда
		
		ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат ТаблицаДанныхРеглОтчета;
		КонецЕсли;
		
		// Раздел 1 (многостраничный)
		// 010 - ОКАТО налога по 7 штук на странице
		// 020 -  Код КБК
		// 030 - сумма
		Если ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел1") Тогда
			
			Раздел1 = ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Раздел1;
			
			Период = ЭкземплярРеглОтчета.ДатаОкончания;
			КодСтрокиОКАТО                   = "П000010001000_";
			КодСтрокиКБК                     = "П000010002000_";
			КодСтрокиСумма                   = "П000010003000_";
			
			Для Каждого Страница Из Раздел1 Цикл
				
				НалогКУплате = Страница.Данные;
				
				Для НомерПоказателя = 1 По 7 Цикл
					Сумма = ТаблицаДанныхРеглОтчета.Добавить();
					Сумма.Период = Период;
					Сумма.ОКАТО  = НалогКУплате[КодСтрокиОКАТО + НомерПоказателя];
					Сумма.КБК    = НалогКУплате[КодСтрокиКБК + НомерПоказателя];
					Сумма.Сумма  = НалогКУплате[КодСтрокиСумма+ НомерПоказателя];
					Сумма.Аванс  = Истина;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаДанныхРеглОтчета;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма20080101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152028", '2008-02-20', "27н", "ФормаОтчета2008Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20080101, "5.01");
	
	Форма20120101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152028", '2011-11-24', "ММВ-7-11/895", "ФормаОтчета2011Кв3");
	ОпределитьФорматВДеревеФормИФорматов(Форма20120101, "5.02");
	
	Форма20140101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152028", '2013-11-05', "ММВ-7-11/478@", "ФормаОтчета2013Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма20140101, "5.03");
	
	Форма20170101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152028", '2017-03-31', "ММВ-7-21/271@", "ФормаОтчета2017Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20170101, "5.04");
	
	Форма20190101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152028", '2018-10-04', "ММВ-7-21/575@", "ФормаОтчета2019Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20190101, "5.05");
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "",
			ИмяОбъекта = "", ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

#КонецОбласти

#КонецЕсли