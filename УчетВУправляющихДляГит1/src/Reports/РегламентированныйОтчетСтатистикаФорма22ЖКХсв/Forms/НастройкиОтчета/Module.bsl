////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Процедура заполняет неизменяемые значения в табличных частях настройки соответствия услуг.
Процедура ЗаполнитьНастройкиСоответствияУслуг()
	
	СтруктураПараметров = УПЖКХ_ТиповыеМетодыВызовСервера.ХранилищеОбщихНастроекЗагрузить("СтатистикаФорма22Сводная_НастройкиОтчета");
	
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		мВидыУслуг = Новый ТаблицаЗначений;
		мВидыУслугЖилищные = Новый ТаблицаЗначений;
		СтруктураПараметров.Свойство("ТаблицаСоответствияУслуг",          мВидыУслуг);
		СтруктураПараметров.Свойство("ТаблицаСоответствияУслугЖилищные",  мВидыУслугЖилищные);
		СтруктураПараметров.Свойство("СоциальнаяНормаНаЧеловека",         СоциальнаяНормаНаЧеловека);
		СтруктураПараметров.Свойство("мВидОбслуживаемойПлощадиПомещений", ВидПлощадиОбслуживаемогоЖилищногоФонда);
	Иначе
		мВидыУслуг = Новый СписокЗначений;
		мВидыУслуг.Добавить("ХолоднаяВода",   "Холодная вода");
		мВидыУслуг.Добавить("ГорячаяВода",    "Горячая вода");
		мВидыУслуг.Добавить("Водоотведение",  "Сточные воды (водоотведение)");
		мВидыУслуг.Добавить("Отопление",      "Отопление");
		мВидыУслуг.Добавить("Электроэнергия", "Электроэнергия");
		мВидыУслуг.Добавить("СетевойГаз",     "Сетевой газ");
		мВидыУслуг.Добавить("СжиженныйГаз",   "Сжиженный газ");
		мВидыУслуг.Добавить("ЗахоронениеТБО", "Захоронение твердых бытовых отходов");
		мВидыУслуг.Добавить("ПрочиеУслуги",   "Прочие услуги");
		
		мВидыУслугЖилищные = Новый СписокЗначений;
		мВидыУслугЖилищные.Добавить("Наем",              "Наем");
		мВидыУслугЖилищные.Добавить("СодержаниеИРемонт", "Содержание и ремонт");
		мВидыУслугЖилищные.Добавить("ВывозТО",           "Вывоз твердых отходов");
		мВидыУслугЖилищные.Добавить("КапитальныйРемонт", "Капитальный ремонт");
	КонецЕсли;
		
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.КВП_Услуги"));
	
	ТаблицаСоответствияУслуг.Очистить();
	Для Каждого ВидУслуги ИЗ мВидыУслуг Цикл
		НоваяСтрока = ТаблицаСоответствияУслуг.Добавить();
		Попытка
			НоваяСтрока.ВидУслуги              = ВидУслуги.Значение;
			НоваяСтрока.ВидУслугиПредставление = ВидУслуги.Представление;
		Исключение
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВидУслуги);
		КонецПопытки;
		НоваяСтрока.СписокУслуг.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
		Если НоваяСтрока.СписокУслуг.Количество() > 0 Тогда
			НоваяСтрока.СписокУслугЗаполнен = Истина;
		Иначе
			НоваяСтрока.СписокУслугЗаполнен = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСоответствияУслугЖилищные.Очистить();
	Для Каждого ВидУслуги ИЗ мВидыУслугЖилищные Цикл
		НоваяСтрока = ТаблицаСоответствияУслугЖилищные.Добавить();
		Попытка
			НоваяСтрока.ВидУслуги              = ВидУслуги.Значение;
			НоваяСтрока.ВидУслугиПредставление = ВидУслуги.Представление;
		Исключение
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВидУслуги);
		КонецПопытки;
		НоваяСтрока.СписокУслуг.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
		Если НоваяСтрока.СписокУслуг.Количество() > 0 Тогда
			НоваяСтрока.СписокУслугЗаполнен = Истина;
		Иначе
			НоваяСтрока.СписокУслугЗаполнен = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройкиСоответствияУслуг()

&НаСервере
// Процедура сохраняет настройки отчета в хранилище.
Процедура СохранитьНастройки()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТаблицаСоответствияУслуг",               ТаблицаСоответствияУслуг.Выгрузить());
	СтруктураПараметров.Вставить("ТаблицаСоответствияУслугЖилищные",       ТаблицаСоответствияУслугЖилищные.Выгрузить());
	СтруктураПараметров.Вставить("СоциальнаяНормаНаЧеловека",              СоциальнаяНормаНаЧеловека);
	СтруктураПараметров.Вставить("мВидОбслуживаемойПлощадиПомещений",      ВидПлощадиОбслуживаемогоЖилищногоФонда);
	
	УПЖКХ_ТиповыеМетодыВызовСервера.ХранилищеОбщихНастроекСохранить("СтатистикаФорма22Сводная_НастройкиОтчета",,
																	СтруктураПараметров);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьНастройкиСоответствияУслуг();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗакрытием" формы.
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Модифицированность Тогда
		
		Ответ = Вопрос("Настройки были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			СохранитьНастройки();
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
// Обработчик команды "Сохранить".
Процедура Сохранить(Команда)
	
	СохранитьНастройки();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередНачаломДобавления" ТЧ "ТаблицаСоответствияУслуг"
//
Процедура ТаблицаСоответствияУслугПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередУдалением" ТЧ "ТаблицаСоответствияУслуг".
//
Процедура ТаблицаСоответствияУслугПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередНачаломДобавления" ТЧ "ТаблицаСоответствияУслугЖилищные".
//
Процедура ТаблицаСоответствияУслугЖилищныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередУдалением" ТЧ "ТаблицаСоответствияУслугЖилищные".
//
Процедура ТаблицаСоответствияУслугЖилищныеПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события при изменении поля "СписокУслуг" ТЧ "ТаблицаСоответствияУслуг".
//
Процедура ТаблицаСоответствияУслугСписокУслугПриИзменении(Элемент)
	
	Если Элементы.ТаблицаСоответствияУслуг.ТекущиеДанные.СписокУслуг.Количество() = 0 Тогда
		Элементы.ТаблицаСоответствияУслуг.ТекущиеДанные.СписокУслугЗаполнен = Ложь;
	Иначе
		Элементы.ТаблицаСоответствияУслуг.ТекущиеДанные.СписокУслугЗаполнен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события при изменении поля "СписокУслуг" ТЧ "ТаблицаСоответствияУслугЖилищные".
//
Процедура ТаблицаСоответствияУслугЖилищныеСписокУслугПриИзменении(Элемент)
	
	Если Элементы.ТаблицаСоответствияУслугЖилищные.ТекущиеДанные.СписокУслуг.Количество() = 0 Тогда
		Элементы.ТаблицаСоответствияУслугЖилищные.ТекущиеДанные.СписокУслугЗаполнен = Ложь;
	Иначе
		Элементы.ТаблицаСоответствияУслугЖилищные.ТекущиеДанные.СписокУслугЗаполнен = Истина;
	КонецЕсли;

КонецПроцедуры
