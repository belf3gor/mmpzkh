&НаСервере
Перем мОбъектОтчета;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания,
      ФормаДлительнойОперации;

&НаСервере
Перем СохраненныеДанныеОтчета;

&НаКлиенте
Перем ОбработкаКС Экспорт;

&НаСервере
Перем СписокРасшифровкиПоказателей;

&НаСервере
Перем СписокРасшифровкиформулы;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обход ошибки платформы Параметр сеанса отсутствует или удален.
	ТабличныйДокумент.Очистить();
	
	СформироватьСтруктуруРеквизитовФормы();
	
	СтруктураРеквизитовФормы.мСохраненныйДок = Параметры.мСохраненныйДок;
	
	СтруктураРеквизитовФормы.мВерсияФормы = "01/01/2018";
	
	СтруктураРеквизитовФормы.мВыбраннаяФорма          = Параметры.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета  = Параметры.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = Параметры.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мПериодичность           = Параметры.мПериодичность;
	СтруктураРеквизитовФормы.мСкопированаФорма        = Параметры.мСкопированаФорма;
	СтруктураРеквизитовФормы.Организация              = Параметры.Организация;
	Если ТипЗнч(Параметры.НалоговыйОрган) = Тип("СправочникСсылка.РегистрацииВНалоговомОргане") Тогда
		Если НЕ Параметры.НалоговыйОрган.Пустая() Тогда
			СтруктураРеквизитовФормы.РегистрацияВНалОргане = Параметры.НалоговыйОрган;
		КонецЕсли;
	КонецЕсли;
	СтруктураРеквизитовФормы.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417
	= Параметры.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417;
	СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически
	= Параметры.СформироватьФормуОтчетаАвтоматически;
	
	мСтруктураЦветовРаскраски = Новый Структура;
	мСтруктураЦветовРаскраски.Вставить("_0", Новый Цвет(255, 255, 192));
	мСтруктураЦветовРаскраски.Вставить("_1", Новый Цвет(192, 192, 192));
	мСтруктураЦветовРаскраски.Вставить("_3", Новый Цвет(230, 240, 220));
	мСтруктураЦветовРаскраски.Вставить("_4", Новый Цвет(192, 220, 192));
	мСтруктураЦветовРаскраски.Вставить("_5", Новый Цвет(255, 255, 255));
	
	ПорядкиОкругленияОтчетности = Новый Структура;
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1",
		Перечисления.ПорядкиОкругленияОтчетности.Окр1);
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1000",
		Перечисления.ПорядкиОкругленияОтчетности.Окр1000);
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1000000",
		Перечисления.ПорядкиОкругленияОтчетности.Окр1000000);
	
	СоответствиеЕдиницыИзмеренияДелителю = Новый Структура;
	СоответствиеЕдиницыИзмеренияДелителю.Вставить("Окр1", 1);
	СоответствиеЕдиницыИзмеренияДелителю.Вставить("Окр1000", 1000);
	СоответствиеЕдиницыИзмеренияДелителю.Вставить("Окр1000000", 1000000);
	
	ДатаПодписи = ТекущаяДатаСеанса();
	
	Инициализация(Параметры.БезОткрытияФормы);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = Новый Структура;
	СтруктураРеквизитовФормы.Вставить("ВыводитьСтраницуВТаблДокНаСервере");
	СтруктураРеквизитовФормы.Вставить("НаимТекущегоРаздела");
	СтруктураРеквизитовФормы.Вставить("мАвтоВыборКодов");
	СтруктураРеквизитовФормы.Вставить("мБезОткрытияФормы");
	СтруктураРеквизитовФормы.Вставить("мВерсияФормы");
	СтруктураРеквизитовФормы.Вставить("мЗаписываетсяНовыйДокумент");
	СтруктураРеквизитовФормы.Вставить("СписокПечатаемыхЛистов");
	СтруктураРеквизитовФормы.Вставить("мПечатныеформы");
	СтруктураРеквизитовФормы.Вставить("мРежимПечати");
	СтруктураРеквизитовФормы.Вставить("мСчетчикСтраниц");
	СтруктураРеквизитовФормы.Вставить("мИдГруппы1");
	СтруктураРеквизитовФормы.Вставить("ДлинаИмениГруппы");
	СтруктураРеквизитовФормы.Вставить("ДлинаИмениГрафы");
	СтруктураРеквизитовФормы.Вставить("ТочностьЕдиницыИзмерения");
	СтруктураРеквизитовФормы.Вставить("ФлажокОтклАвтоРасчет", Ложь);
	СтруктураРеквизитовФормы.Вставить("ВидДокумента");
	СтруктураРеквизитовФормы.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитовФормы.Вставить("мВариант");
	СтруктураРеквизитовФормы.Вставить("мСтрокаФормата");
	СтруктураРеквизитовФормы.Вставить("мВыбраннаяФорма");
	СтруктураРеквизитовФормы.Вставить("мДатаКонцаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мДатаНачалаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мПериодичность");
	СтруктураРеквизитовФормы.Вставить("мСкопированаФорма");
	СтруктураРеквизитовФормы.Вставить("мСохраненныйДок");
	СтруктураРеквизитовФормы.Вставить("мПечатьБезШтрихкодаРазрешена");
	СтруктураРеквизитовФормы.Вставить("Организация");
	СтруктураРеквизитовФормы.Вставить("НаимОрганизации");
	СтруктураРеквизитовФормы.Вставить("НачальноеЗначениеНаимОрганизации", "");
	СтруктураРеквизитовФормы.Вставить("СтрПериодОтчета");
	СтруктураРеквизитовФормы.Вставить("ТекущаяСтрокаРазделовОтчета");
	СтруктураРеквизитовФормы.Вставить("НомераСтрокМногоуровнегоРаздела");
	СтруктураРеквизитовФормы.Вставить("СтруктураМногострочныхЧастей");
	СтруктураРеквизитовФормы.Вставить("ОтображатьКнопкуЗаполнить", Ложь);
	СтруктураРеквизитовФормы.Вставить("ОтображатьКнопкуРасшифровать", Ложь);
	СтруктураРеквизитовФормы.Вставить("АвтозаполняемыеПоказатели");
	СтруктураРеквизитовФормы.Вставить("ИдентификаторЗадания");
	СтруктураРеквизитовФормы.Вставить("АдресВоВременномХранилище");
	СтруктураРеквизитовФормы.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417");
	СтруктураРеквизитовФормы.Вставить("СформироватьФормуОтчетаАвтоматически");
	СтруктураРеквизитовФормы.Вставить("мСохранятьРасшифровку", Истина);
	СтруктураРеквизитовФормы.Вставить("АдресВременногоХранилищаРасшифровки");
	СтруктураРеквизитовФормы.Вставить("РегистрацияВНалОргане");
	СтруктураРеквизитовФормы.Вставить("ТекстДляЗаголовочнойЧасти");
	СтруктураРеквизитовФормы.Вставить("мПроверятьСоотношенияПриПечатиИВыгрузки");
	
КонецПроцедуры

#Область Инициализация

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный";
	
	// Очистка для случая повторного запуска инициализации,
	// например, при загрузке отчета из файла выгрузки.
	ТабличныйДокумент.Очистить();
	
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
	ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + СтруктураРеквизитовФормы.НаимТекущегоРаздела));
	
	ИнициализироватьМногострочныеЧасти();
	
	ИнициализироватьМногоуровневыеРазделы();
	
	ИнициализироватьОбычныеРазделы();
	
	ЗаполнитьСтруктуруЗависимыхЯчеекДляАвторасчета();
	
	ФормироватьСтруктуруСтраницОтчета();
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;
	СтруктураРеквизитовФормы.мПечатныеформы = Новый СписокЗначений;
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
	СтруктураРеквизитовФормы.ВидДокумента = 0;
	НомерКорректировки = 1;
	
	ЗаполнитьЗначенияПоУмолчанию = Ложь;
	ОтчетСкопированОрганизацияНеИзменилась = Ложь;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			ВосстановитьСохраненныеДанные();
		Иначе
			СтруктураРеквизитовФормы.ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 0;
			ЗаполнитьЗначенияПоУмолчанию = Истина;
			КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
			СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки = Ложь;
		КонецЕсли;
		Модифицированность = Истина;
	Иначе
		ВосстановитьСохраненныеДанные();
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			Если СтруктураРеквизитовФормы.мСохраненныйДок.Организация
				= СтруктураРеквизитовФормы.Организация Тогда
				ОтчетСкопированОрганизацияНеИзменилась = Истина;
			КонецЕсли;
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
			Модифицированность = Истина;
		КонецЕсли;
		Если НЕ БезОткрытияФормы И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
			ЗаблокироватьДанныеДляРедактирования(
				СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнитьЗначенияПоУмолчанию Тогда
		
		ТабличныйДокумент.Области.ПоМесту.Значение = "270";
		
	КонецЕсли;
	
	ПолучитьСведенияОПоказателяхОтчета();
	
	Если НЕ СтруктураРеквизитовФормы.ОтображатьКнопкуЗаполнить Тогда
		Элементы.Заполнить.Видимость = Ложь;
		СтруктураРеквизитовФормы.мСохранятьРасшифровку = Ложь;
	КонецЕсли;
	
	Если НЕ СтруктураРеквизитовФормы.ОтображатьКнопкуРасшифровать Тогда
		Элементы.Расшифровать.Видимость = Ложь;
		Элементы.ТабличныйДокументКонтекстноеМенюРасшифровать.Видимость = Ложь;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.НачальноеЗначениеНаимОрганизации = НачальноеЗначениеНаименованияОрганизации();
	
	ПоказатьПериод();
	
	ЗаполнитьПериодОтчета(ЭтаФорма);
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено
		ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено
		ИЛИ СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически Тогда
		
		Если ОтчетСкопированОрганизацияНеИзменилась Тогда
			
			Если ЗначениеЗаполнено(ТабличныйДокумент.Области.КодФормыРеорганизации.Значение) Тогда
				НаимОргВосстановленное = ТабличныйДокумент.Области.НаимОрг.Значение;
				ЗаполнитьСведенияОбОрганизацииНаСервере();
				ТабличныйДокумент.Области.НаимОрг.Значение = НаимОргВосстановленное;
			КонецЕсли;
			
		Иначе
			
			ЗаполнитьСведенияОбОрганизацииНаСервере();
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьДоступностьОбластей(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	СтруктураРеквизитовФормы.НаимОрганизации = СтруктураРеквизитовФормы.Организация.Наименование;
	
	СформироватьСпискиВыбора();
	
	Если НЕ БезОткрытияФормы Тогда
		РегламентированнаяОтчетностьКлиентСервер.ПоказатьКнопкиПечати(ЭтаФорма);
	КонецЕсли;
	
	ОтобразитьНомерКорректировки(ЭтаФорма);
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	НомерКорректировки = ?(СтруктураРеквизитовФормы.ВидДокумента = 0, 0, НомерКорректировки);
	ТабличныйДокумент.Области.НомерКорректировки.Значение = Строка(НомерКорректировки);
	ЗаполнитьДатуВЯчейках(ЭтаФорма);
	
	УстановитьФорматВыводаНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтаФорма);
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма);
	
	Если СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически Тогда
		ЗаданиеВыполнено = ЗаполнитьАвтоНаСервере();
		Если ЗаданиеВыполнено Тогда
			Если Элементы.Расшифровать.Видимость Тогда
				Элементы.Расшифровать.Доступность = Истина;
				Элементы.ТабличныйДокументКонтекстноеМенюРасшифровать.Доступность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьМногострочныеЧасти()
	
	// Идентификаторы многострочных групп.
	СтруктураРеквизитовФормы.мИдГруппы1 = "П0000100";
	
	// Свойства, общие для всех многострочных групп.
	СтруктураРеквизитовФормы.ДлинаИмениГруппы = 8;
	СтруктураРеквизитовФормы.ДлинаИмениГрафы  = 13;
	
	// Структура с описанием свойств каждой группы в отдельности.
	СтруктураМногострочныхЧастей = Новый Структура;
	
	// Группа П0000100.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П000010001003");
	ИменаГраф.Добавить("П000010002003");
	ИменаГраф.Добавить("П000010002103");
	ИменаГраф.Добавить("П000010002303");
	ИменаГраф.Добавить("П000010002503");
	ИменаГраф.Добавить("П000010002703");
	ИменаГраф.Добавить("П000010003003");
	ИменаГраф.Добавить("П000010004003");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураМногострочныхЧастей.Вставить(
		СтруктураРеквизитовФормы.мИдГруппы1, Новый Структура(
		"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп",
		ИменаГраф, 1, 0, ИменаПодчиненныхГрупп));
	
	СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей = СтруктураМногострочныхЧастей;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьМногоуровневыеРазделы()
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела = Новый Массив;
	
	// Формирование структуры свойств многоуровневых разделов.
	//
	мСтруктураМногоуровневыхРазделов = Новый Структура;
	
	СтруктураРаздела = Новый Структура;
	ИменаГруппМнЧ = Новый Массив;
	ИменаГруппМнЧ.Добавить(СтруктураРеквизитовФормы.мИдГруппы1);
	СтруктураРаздела.Вставить("ИменаГруппМнЧ", ИменаГруппМнЧ);
	мСтруктураМногоуровневыхРазделов.Вставить("Раздел1", СтруктураРаздела);
	
	СтруктураРаздела = Новый Структура;
	СтруктураРаздела.Вставить("Многостраничность");
	мСтруктураМногоуровневыхРазделов.Вставить("Раздел2", СтруктураРаздела);
	
	// Формирование начальной структуры дерева разделов
	// с заполнением каждого уровня пустыми данными.
	//
	ДеревоДанных = Новый ДеревоЗначений;
	ДеревоДанных.Колонки.Добавить("Данные");
	ДеревоДанных.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	СохрТаблДокумент = Новый ТабличныйДокумент;
	СохрТаблДокумент.Вывести(ТабличныйДокумент);
	
	Для каждого Элем Из мСтруктураМногоуровневыхРазделов Цикл
		Если Элем.Значение.Свойство("Подчиненность") Тогда
			Продолжить;
		КонецЕсли;
		
		ДеревоДанныхРаздела = ДеревоДанных.Скопировать();
		ТекВетка = ДеревоДанныхРаздела.Строки.Добавить();
		
		ТабличныйДокумент.Очистить();
		ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
		ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + Элем.Ключ));
		
		ДанныеТаблДокумента = ДанныеТабличногоДокумента(Элем.Ключ);
		
		ТекВетка.Данные = ДанныеТаблДокумента.Данные;
		ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
		
		ИнициализироватьПодчиненныеРазделы(Элем.Ключ, ТекВетка);
		
		СтруктураРеквизитовФормы.Вставить("АдресВоВрХранилищеДеревоДанных" + Элем.Ключ,
		ПоместитьВоВременноеХранилище(ДеревоДанныхРаздела, УникальныйИдентификатор));
		
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(СохрТаблДокумент);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьПодчиненныеРазделы(Знач ИмяРаздела, Ветка)
	
	Если мСтруктураМногоуровневыхРазделов[ИмяРаздела].Свойство("ИменаПодчиненныхРазделов") Тогда
		
		ИменаПодчРазделов = мСтруктураМногоуровневыхРазделов[ИмяРаздела]["ИменаПодчиненныхРазделов"];
		Для каждого ИмяПодчРаздела Из ИменаПодчРазделов Цикл
			ПодчВетка = Ветка.Строки.Добавить().Строки.Добавить();
			
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
			ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяПодчРаздела));
			
			ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяПодчРаздела);
			
			ПодчВетка.Данные = ДанныеТаблДокумента.Данные;
			ПодчВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
			
			ИнициализироватьПодчиненныеРазделы(ИмяПодчРаздела, ПодчВетка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОбычныеРазделы(МассивИменРазделов = Неопределено)
	
	Если МассивИменРазделов = Неопределено Тогда
		
		МассивИменРазделов = Новый Массив;
		МассивИменРазделов.Добавить("Титульный");
		
	КонецЕсли;
	
	СохрТабличныйДокумент = Новый ТабличныйДокумент;
	СохрТабличныйДокумент.Вывести(ТабличныйДокумент);
	
	Для каждого ИмяРаздела Из МассивИменРазделов Цикл
		
		ЭтаФорма["СтруктураДанных" + ИмяРаздела] = Новый Структура;
		
		ТабличныйДокумент.Очистить();
		ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
		ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяРаздела));
		
		СохранитьДанныеРаздела(ИмяРаздела);
		
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(СохрТабличныйДокумент);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСведенияОПоказателяхОтчета()
	
	СтруктураРеквизитовФормы.Вставить("АвтозаполняемыеПоказатели", Новый Структура);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",              СтруктураРеквизитовФормы.Организация);
	ПараметрыОтчета.Вставить("мДатаНачалаПериодаОтчета", СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыОтчета.Вставить("мДатаКонцаПериодаОтчета",  СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	
	АвтозаполняемыеПоказатели = РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОПоказателяхОтчета(
	ИДОтчета(ЭтаФорма.ИмяФормы), ИДРедакцииОтчета(ЭтаФорма.ИмяФормы), ПараметрыОтчета);
	
	Если АвтозаполняемыеПоказатели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Показатель Из АвтозаполняемыеПоказатели Цикл
		
		Если Показатель.Значение[0] Тогда
			СтруктураРеквизитовФормы.ОтображатьКнопкуЗаполнить = Истина;
			Если СтруктураРеквизитовФормы.ОтображатьКнопкуРасшифровать Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
		Если Показатель.Значение[1] Тогда
			СтруктураРеквизитовФормы.ОтображатьКнопкуРасшифровать = Истина;
			Если СтруктураРеквизитовФормы.ОтображатьКнопкуЗаполнить Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураРеквизитовФормы.Вставить("АвтозаполняемыеПоказатели", АвтозаполняемыеПоказатели);
	
КонецПроцедуры

&НаСервере
Функция НачальноеЗначениеНаименованияОрганизации()
	
	СтрокаСведений = "НаимЮЛПол";
	
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		СтруктураРеквизитовФормы.Организация, ДатаПодписи, СтрокаСведений);
	
	НаимОрганизации = "";
	СведенияОбОрганизации.Свойство("НаимЮлПол", НаимОрганизации);
	
	Возврат СокрЛП(НаимОрганизации);
	
КонецФункции

&НаСервере
Процедура ФормироватьСтруктуруСтраницОтчета() Экспорт
	
	мДеревоСтраницОтчета.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы        = "Титульный";
	СтрокаУровня1.Представление      = "Титульный лист";
	СтрокаУровня1.ОриентацияСтраницы = "Портрет";
	СтрокаУровня1.ВыводНаПечать      = 1;
	СтрокаУровня1.ПоказатьСтраницу   = 1;
	СтрокаУровня1.ВыгрузитьСтраницу  = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы        = "Раздел1";
	СтрокаУровня1.Представление      = "Раздел 1";
	СтрокаУровня1.ОриентацияСтраницы = "Портрет";
	СтрокаУровня1.ВыводНаПечать      = 1;
	СтрокаУровня1.ПоказатьСтраницу   = 1;
	СтрокаУровня1.ВыгрузитьСтраницу  = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы        = "Раздел2";
	СтрокаУровня1.Представление      = "Раздел 2";
	СтрокаУровня1.ОриентацияСтраницы = "Портрет";
	СтрокаУровня1.ВыводНаПечать      = 1;
	СтрокаУровня1.ПоказатьСтраницу   = 1;
	СтрокаУровня1.ВыгрузитьСтраницу  = 2;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСпискиВыбора()
	
	МакетСоставаПоказателей = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет("СпискиВыбора2018Кв1");
	
	КоллекцияСписковВыбора = Новый Соответствие;
	Для Каждого Область Из МакетСоставаПоказателей.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			ВерхОбласти = Область.Верх;
			НизОбласти = Область.Низ;
			ТаблСписка = Новый ТаблицаЗначений;
			ТаблСписка.Колонки.Добавить("Код",,,
				МакетСоставаПоказателей.Область(ВерхОбласти, 1, ВерхОбласти, 1).ШиринаКолонки);
			ТаблСписка.Колонки.Добавить("Название",,,
				МакетСоставаПоказателей.Область(ВерхОбласти, 2, ВерхОбласти, 2).ШиринаКолонки);
			Для НомСтр = ВерхОбласти По НизОбласти Цикл
				КодПоказателя = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
				Если КодПоказателя <> "###" Тогда
					НовСтрока = ТаблСписка.Добавить();
					НовСтрока.Код = КодПоказателя;
					НовСтрока.Название = СокрП(МакетСоставаПоказателей.Область(НомСтр, 2).Текст);
				КонецЕсли;
			КонецЦикла;
			КоллекцияСписковВыбора.Вставить(Область.Имя, ТаблСписка);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваПоказателей.Очистить();
	
	//Титульный.
	//
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ПоМесту", 3, ,
	"Выбор места представления", КоллекцияСписковВыбора["МестаПредставления"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "КодФормыРеорганизации", 1, ,
	"Выбор кода формы реорганизации (ликвидации)", КоллекцияСписковВыбора["ФормыРеорганизации"]);
	
	//Раздел 1.
	//
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010001003", 0, ,
	"Выбор кода бюджетной классификации", КоллекцияСписковВыбора["КБК"]);
	
	//Раздел 2.
	//
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020001003", 0, ,
	"Выбор кода бюджетной классификации", КоллекцияСписковВыбора["КБК"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020003003", 12, ,
	"Выбор кода категории земель", КоллекцияСписковВыбора["КодыКатегорийЗемель"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020004003", 1, ,
	"Выбор периода строительства", КоллекцияСписковВыбора["ПериодыСтроительства"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020007003_КодЛьготы", 7, ,
	"Выбор кода налоговой льготы", КоллекцияСписковВыбора["КодыНалоговыхЛьгот_Стр070"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020009003_КодЛьготы", 7, ,
	"Выбор кода налоговой льготы", КоллекцияСписковВыбора["КодыНалоговыхЛьгот_Стр090"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020018003_КодЛьготы", 7, ,
	"Выбор кода налоговой льготы", КоллекцияСписковВыбора["КодыНалоговыхЛьгот_Стр180"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020020003", 7, ,
	"Выбор кода налоговой льготы", КоллекцияСписковВыбора["КодыНалоговыхЛьгот_Стр200"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020022003_КодЛьготы", 7, ,
	"Выбор кода налоговой льготы", КоллекцияСписковВыбора["КодыНалоговыхЛьгот_Стр220"]);
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000020024003_КодЛьготы", 7, ,
	"Выбор кода налоговой льготы", КоллекцияСписковВыбора["КодыНалоговыхЛьгот_Стр240"]);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПериод()
	
	СтруктураРеквизитовФормы.СтрПериодОтчета = ПредставлениеПериода(
		НачалоДня(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета),
		КонецДня(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета), "ФП = Истина");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПериодОтчета(Форма)
	
	ЗаполненКодФормыРеорганизации
	= ЗначениеЗаполнено(Форма.ТабличныйДокумент.Области.КодФормыРеорганизации.Значение);
	
	КодПериода = "34";
	Если ЗаполненКодФормыРеорганизации Тогда
		КодПериода = "50";
	КонецЕсли;
	
	Форма.ТабличныйДокумент.Области.Период.Значение = КодПериода;
	
	Форма.ТабличныйДокумент.Области.ОтчетГод.Значение
	= Формат(Год(Форма.СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета), "ЧГ=");
	
КонецПроцедуры

#КонецОбласти

#Область СохранениеДанныхОтчета

&НаКлиенте
Процедура СохранитьОтчет(Команда)
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение = Неопределено) Экспорт
	
	Вариант = СтруктураРеквизитовФормы.ВидДокумента * НомерКорректировки;
	
	Если СтруктураДанныхТитульный.Количество() = 0 
	 ИЛИ СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		
		СтруктураДанныхТитульный.Вставить("НалоговыйОрган", ТабличныйДокумент.Области.НалоговыйОрган.Значение);
		СтруктураДанныхТитульный.Вставить("КПП", ТабличныйДокумент.Области.КПП.Значение);
		
	КонецЕсли;
	
	КодИФНС = СтруктураДанныхТитульный.НалоговыйОрган;
	КПП = СтруктураДанныхТитульный.КПП;
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПриЗаписиРегламентированногоОтчетаНаКлиенте(ЭтаФорма, КодИФНС, Автосохранение, Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		НаименованиеТекущегоРаздела = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	Иначе
		НаименованиеТекущегоРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	КонецЕсли;
	
	РезультатСохранения = ПередСохранением(КодИФНС, КПП, ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаименованиеТекущегоРаздела);
	
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением = (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	Если НуженВопросПередСохранением Тогда
		
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС, КПП);
		
	Иначе
		
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС, КПП)
	
	Если ПодобныйОтчетСуществует Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчет с видом %1 уже существует.
			|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП", Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,КодИФНС, КПП);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,КодИФНС, КПП);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, КодИФНС, КПП", Автосохранение, Вариант, ВыполняемоеОповещение,КодИФНС, КПП );
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.Открыть();
	Иначе
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
		РезультатСохранения = Сохранить(Автосохранение, КодИФНС, КПП, Вариант, Неопределено, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
	Иначе
		Возврат;
	КонецЕсли;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
	РезультатСохранения = Сохранить(Автосохранение, КодИФНС, КПП, Вариант, ВидДокументаНомерКорректировкиИзменен, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения)
	
	КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если РезультатСохранения Тогда
		
		РегламентированнаяОтчетностьКлиент.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПередСохранением(КодИФНС, КПП, ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаимТекРаздела)
	
	Если РегламентированнаяОтчетность.БылиИзмененыКлючевыеРеквизитыОтчета(ЭтаФорма, КодИФНС, КПП)
	   И РегламентированнаяОтчетность.СуществуетДокументСАналогичнымиРеквизитами(ЭтаФорма, КодИФНС, КПП) Тогда
		
		ПодобныйОтчетСуществует = Истина;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мЗаписываетсяНовыйДокумент = (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	Если Вариант <> Неопределено И ((СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) ИЛИ (СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено) ИЛИ (Вариант <> СтруктураРеквизитовФормы.мВариант)) Тогда
		
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			
		ИначеЕсли Вариант <> СтруктураРеквизитовФормы.мВариант Тогда
			
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Истина;
			
		КонецЕсли;

	КонецЕсли;
	
	Если ПодобныйОтчетСуществует
	 ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
	 
	 	Возврат Ложь;
		
 	КонецЕсли;
		
	Возврат Сохранить(Автосохранение, КодИФНС, КПП, Вариант, , НаимТекРаздела);
		
КонецФункции

&НаСервере
Функция Сохранить(Автосохранение = Ложь, КодИФНС, КПП, Вариант, ВидДокументаНомерКорректировкиИзменен = Неопределено, НаимТекРаздела) Экспорт
	
	ДанныеДляРазблокирования = Неопределено;
	
	Если НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
			ДанныеДляРазблокирования = Новый Структура("Ключ,ИдФормы",
			СтруктураРеквизитовФормы.мСохраненныйДок, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчетаНаСервере(
		ЭтаФорма, КодИФНС, Автосохранение, Вариант, ВидДокументаНомерКорректировкиИзменен,
		СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мСохраненныйДок.ИсточникОтчета           = Метаданные.Отчеты[ИДОтчета(ЭтаФорма.ИмяФормы)].Имя;
	СтруктураРеквизитовФормы.мСохраненныйДок.НаименованиеОтчета       = Метаданные.Отчеты[ИДОтчета(ЭтаФорма.ИмяФормы)].ОсновнаяФорма.Синоним;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаНачала               = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания            = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВыбраннаяФорма           = СтруктураРеквизитовФормы.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок.Организация              = СтруктураРеквизитовФормы.Организация;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи              = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий              = Комментарий;
	СтруктураРеквизитовФормы.мСохраненныйДок.КодНалоговогоОргана      = КодИФНС;
	СтруктураРеквизитовФормы.мСохраненныйДок.КПП                      = КПП;
	СтруктураРеквизитовФормы.мСохраненныйДок.Вид                      = Вариант;
	СтруктураРеквизитовФормы.мСохраненныйДок.ПредставлениеВида        = РегламентированнаяОтчетность.ПредставлениеВидаДокумента(СтруктураРеквизитовФормы.мСохраненныйДок.Вид);
	СтруктураРеквизитовФормы.мВариант                                 = Вариант;
	
	СтруктураРеквизитовФормы.мСохраненныйДок.ФорматВыгрузки = "Автоматически";
	
	СохранитьДанныеРаздела(НаимТекРаздела);
	
	ПоказателиОтчета = Новый Структура();
	ПоказателиОтчета.Вставить("ПолеТабличногоДокументаТитульный", СтруктураДанныхТитульный);
	
	СписокСохранения = Новый Структура();
	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	// Вставляем данные многоуровневых разделов.
	ДанныеМногоуровневыхРазделов = Новый Структура;
	Для каждого Элем Из мСтруктураМногоуровневыхРазделов Цикл
		СтруктураРаздела = Элем.Значение;
		Если НЕ СтруктураРаздела.Свойство("Подчиненность") Тогда
			ДанныеМногоуровневыхРазделов.Вставить(Элем.Ключ, ПолучитьИзВременногоХранилища(
				СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + Элем.Ключ]));
		КонецЕсли;
	КонецЦикла;
	СписокСохранения.Вставить("ДанныеМногоуровневыхРазделов", ДанныеМногоуровневыхРазделов);
	
	СписокСохранения.Вставить("ВерсияФормы", СтруктураРеквизитовФормы.мВерсияФормы);
	
	СписокСохранения.Вставить("СохранятьРасшифровку", СтруктураРеквизитовФормы.мСохранятьРасшифровку);
	
	Если СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки <> Неопределено Тогда
		
		ТаблицаРасшифровки = ПолучитьИзВременногоХранилища(СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки);
		
		Если СтруктураРеквизитовФормы.мСохранятьРасшифровку = Истина Тогда
			СписокСохранения.Вставить("Расшифровка", ТаблицаРасшифровки);
		КонецЕсли;
		
	КонецЕсли;
	
	СписокСохранения.Вставить("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	СписокСохранения.Вставить("Автозаполнение", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	СписокСохранения.Вставить("ПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена);
	СписокСохранения.Вставить("ФлПодсчетСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	СписокСохранения.Вставить("ФлПроверятьСоотношенияПриПечатиИВыгрузки", 
		СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки);
	
	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;
	
	Попытка
		СтруктураРеквизитовФормы.мСохраненныйДок.Записать();
		РегламентированнаяОтчетность.СохранитьСтатусОтправкиРеглОтчета(ЭтаФорма);
	Исключение
		Если НЕ Автосохранение Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"),
				РегламентированнаяОтчетностьКлиентСервер.СформироватьТекстСообщения(ОписаниеОшибки()));
			Сообщение.Сообщить();
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецПопытки;
	
	СохранитьДеревоСтраницОтчета(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	Модифицированность = Ложь;
	
	СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
	
	Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы
		И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	
	Если ДанныеДляРазблокирования <> Неопределено Тогда
		РазблокироватьДанныеДляРедактирования(ДанныеДляРазблокирования.Ключ, ДанныеДляРазблокирования.ИдФормы);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура СохранитьДеревоСтраницОтчета(Знач мСохраненныйДок)
	
	Если мСохраненныйДок <> Неопределено Тогда
		Если ТипЗнч(мСохраненныйДок) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			мСохраненныйДок = мСохраненныйДок.ПолучитьОбъект();
		КонецЕсли;
		Если Не мСохраненныйДок.ЭтоНовый() Тогда
			// Сохраняем выбранные листы для печати в хранилище.
			ХранилищеДанных = Новый ХранилищеЗначения(РеквизитФормыВЗначение("мДеревоСтраницОтчета"));
			мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
			Попытка
				мСохраненныйДок.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВосстановлениеСохраненныхДанныхОтчета

&НаСервере
Функция СохраненныеДанныеОтчета()
	
	Если СохраненныеДанныеОтчета = Неопределено Тогда
		
		СохраненныеДанныеОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
		
		Если СохраненныеДанныеОтчета.Свойство("ОкружениеСохранения") Тогда
			ВосстановитьСохраненныеДанныеОтчетаСтарогоОбразца(СохраненныеДанныеОтчета);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СохраненныеДанныеОтчета;
	
КонецФункции

&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногоуровневыхРазделов;
	Перем ПоказателиОтчета;
	Перем Автозаполнение;
	Перем ТаблицаРасшифровки;
	Перем ТаблицаМногостраничногоДокумента;
	
	// В случае, если формы была скопирована по F9, то проверим, не изменилась ли организация.
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		СтруктураРеквизитовФормы.Организация = СтруктураРеквизитовФормы.мСохраненныйДок.Организация;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения;
	ДатаПодписи = СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи;
	Комментарий = СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий;
	
	СписокСохранения = СохраненныеДанныеОтчета();
	
	ДеревоНастройкиСтраниц = СтруктураРеквизитовФормы.мСохраненныйДок.ДеревоНастройкиСтраниц.Получить();
	ЗначениеВРеквизитФормы(ДеревоНастройкиСтраниц.Скопировать(), "мДеревоСтраницОтчета");
	
	СписокСохранения.Свойство("ПоказателиОтчета", ПоказателиОтчета);
	
	Если СписокСохранения.Свойство("ДанныеМногоуровневыхРазделов", ДанныеМногоуровневыхРазделов) Тогда
		Для Каждого ЭлементСтруктуры Из ДанныеМногоуровневыхРазделов Цикл
			СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ЭлементСтруктуры.Ключ]
				= ПоместитьВоВременноеХранилище(ЭлементСтруктуры.Значение, УникальныйИдентификатор);
		КонецЦикла;
	КонецЕсли;
	
	СписокСохранения.Свойство("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	Если СтруктураРеквизитовФормы.мАвтоВыборКодов = Неопределено Тогда
		СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	КонецЕсли;
	
	СписокСохранения.Свойство("ФлПроверятьСоотношенияПриПечатиИВыгрузки", 
		СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки);
	СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки = ?(
		СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки = Неопределено, Ложь, 
		СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки);
	
	СписокСохранения.Свойство("Автозаполнение", Автозаполнение);
	СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Автозаполнение;
	
	Если НЕ СписокСохранения.Свойство("ПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена) Тогда
		СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена = Ложь;
	КонецЕсли;
	
	Если НЕ СписокСохранения.Свойство("ФлПодсчетСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц) Тогда
		СтруктураРеквизитовФормы.мСчетчикСтраниц = Ложь;
	КонецЕсли;
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	СписокСохранения.Свойство("СохранятьРасшифровку", СтруктураРеквизитовФормы.мСохранятьРасшифровку);
	Если СтруктураРеквизитовФормы.мСохранятьРасшифровку = Неопределено Тогда
		СтруктураРеквизитовФормы.мСохранятьРасшифровку = Истина;
	КонецЕсли;
	
	СписокСохранения.Свойство("Расшифровка", ТаблицаРасшифровки);
	
	Если ТипЗнч(ТаблицаРасшифровки) = Тип("ТаблицаЗначений") Тогда
		
		Если ТаблицаРасшифровки.Колонки.Найти("НомСтроки") <> Неопределено Тогда
			
			ИмяПоказателя     = ТаблицаРасшифровки.Колонки.Найти("ИмяПоказателя");
			ИмяПоказателя.Имя = "НаименованиеПоказателя";
			
			ИмяЯчейки     = ТаблицаРасшифровки.Колонки.Найти("ИмяЯчейки");
			ИмяЯчейки.Имя = "ИмяПоказателя";
			
			Раздел     = ТаблицаРасшифровки.Колонки.Найти("Раздел");
			Раздел.Имя = "ИмяРаздела";
			
			ИмяСлагаемого     = ТаблицаРасшифровки.Колонки.Найти("ИмяСлагаемого");
			ИмяСлагаемого.Имя = "НаименованиеСлагаемого";
			
			ТаблицаРасшифровки.Колонки.Добавить("ДополнительныеПараметры");
			
			Для Каждого Элемент Из ТаблицаРасшифровки Цикл
				
				Если ЗначениеЗаполнено(Элемент.КодСчета) Тогда
					Элемент.НаименованиеСлагаемого = Элемент.НаименованиеСлагаемого + Элемент.КодСчета.Код;
				КонецЕсли;
				
				ДопПараметры = Новый Структура;
				
				Если ЗначениеЗаполнено(Элемент.Субконто1) Тогда
					ДопПараметры.Вставить("Субконто1", Новый Структура("ВидСубконто,ЗначениеСубконто", Элемент.КодСчета.ВидыСубконто[0].ВидСубконто, Элемент.Субконто1));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Элемент.Субконто2) Тогда
					ДопПараметры.Вставить("Субконто2", Новый Структура("ВидСубконто,ЗначениеСубконто", Элемент.КодСчета.ВидыСубконто[1].ВидСубконто, Элемент.Субконто2));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Элемент.Субконто3) Тогда
					ДопПараметры.Вставить("Субконто3", Новый Структура("ВидСубконто,ЗначениеСубконто", Элемент.КодСчета.ВидыСубконто[2].ВидСубконто, Элемент.Субконто3));
				КонецЕсли;
				
				Элемент.ДополнительныеПараметры = ДопПараметры;
				
			КонецЦикла;
			
			ТаблицаРасшифровки = ТаблицаРасшифровки.Скопировать(, "ИмяПоказателя, НаименованиеПоказателя, ЗнакОперации, НаименованиеСлагаемого, Сумма, ИмяРаздела, ДополнительныеПараметры");
			
		КонецЕсли;
		
		СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки = ПоместитьВоВременноеХранилище(ТаблицаРасшифровки, УникальныйИдентификатор);
		Элементы.Расшифровать.Доступность = Истина;
		Элементы.ТабличныйДокументКонтекстноеМенюРасшифровать.Доступность = Истина;
		
	КонецЕсли;
	
	СтруктураДанныхТитульный = ПоказателиОтчета["ПолеТабличногоДокументаТитульный"];
	
	ИмяТекРаздела = "Титульный";
	Для Каждого ЭлСтруктуры Из ЭтаФорма["СтруктураДанных" + ИмяТекРаздела] Цикл
		Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
		Если Обл <> Неопределено Тогда
			Обл.Значение = ЭлСтруктуры.Значение;
		КонецЕсли;
	КонецЦикла;
	
	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтаФорма);
	
	КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеДанныеОтчетаСтарогоОбразца(СписокСохранения)
	
	ПрефиксТД = "ПолеТабличногоДокумента";
	ДанныеМногоуровневыхРазделов = Новый Структура;
	
	// Данные вариантов автозаполнения.
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения") Тогда
		СписокСохранения.Удалить("ДанныеВариантовАвтоЗаполнения");
	КонецЕсли;
	
	// Сохранение с последующим восстановлением номеров строк многоуровнего раздела.
	// (На случай выгрузки отчета, сохраненного в 2.0, без сохранения в 3.0.)
	СохрНомераСтрокМногоуровнегоРаздела = СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела;
	
	ИнициализироватьМногоуровневыеРазделы();
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела = СохрНомераСтрокМногоуровнегоРаздела;
	
	// Раздел 1.
	ИмяРаздела = "Раздел1";
	СтарТаблица = СписокСохранения.ДанныеМногостраничныхРазделов[ИмяРаздела];
	ДеревоРаздела = ПолучитьИзВременногоХранилища(
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	ИмяГруппы = СтруктураРеквизитовФормы.мИдГруппы1;
	СтруктураМнЧ = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы];
	ИменаГраф = СтруктураМнЧ.ИменаГраф;
	Для ИндСтарСтраницы = 0 По СтарТаблица.Количество() - 1 Цикл
		СтарДанные = СтарТаблица[ИндСтарСтраницы].Данные;
		Для СтарНомСтроки = 1 По 2 Цикл
			_НомСтр = "_" + Формат(СтарНомСтроки, "ЧГ=");
			Если ИндСтарСтраницы = 0 И СтарНомСтроки = 1 Тогда
				СтрокаМнЧ = ДеревоРаздела.Строки[0].ДанныеМногострочныхЧастей[ИмяГруппы].Строки[0];
			Иначе
				СтрокаМнЧ = ДеревоРаздела.Строки[0].ДанныеМногострочныхЧастей[ИмяГруппы].Строки.Добавить();
			КонецЕсли;
			СтрокаМнЧ.Данные = Новый Структура;
			СтрокаМнЧ.ДанныеМногострочныхЧастей = Новый Структура;
			Для каждого ИмяГрафы Из ИменаГраф Цикл
				СтрокаМнЧ.Данные.Вставить(ИмяГрафы, СтарДанные[ИмяГрафы + _НомСтр]);
			КонецЦикла;
		КонецЦикла;
		Если ИндСтарСтраницы = 0 Тогда
			ДеревоРаздела.Строки[0].Данные.Вставить("П000010000100", СтарДанные["П000010000100"]);
		КонецЕсли;
	КонецЦикла;
	ДанныеМногоуровневыхРазделов.Вставить(ИмяРаздела, ДеревоРаздела);
	
	// Раздел 2.
	ИмяРаздела = "Раздел2";
	СтарТаблица = СписокСохранения.ДанныеМногостраничныхРазделов[ИмяРаздела];
	ДеревоРаздела = ПолучитьИзВременногоХранилища(
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	Для Инд = 0 По СтарТаблица.Количество() - 1 Цикл
		СтарСтраница = СтарТаблица[Инд];
		СтарДанные = СтарСтраница.Данные;
		Если Инд > 0 Тогда
			ДеревоРаздела.Строки.Добавить();
			ДеревоРаздела.Строки[Инд].Данные = Новый Структура;
			ДеревоРаздела.Строки[Инд].ДанныеМногострочныхЧастей = Новый Структура;
		КонецЕсли;
		Для каждого Пок Из СтарДанные Цикл
			ДеревоРаздела.Строки[Инд].Данные.Вставить(Пок.Ключ, Пок.Значение);
		КонецЦикла;
	КонецЦикла;
	ДанныеМногоуровневыхРазделов.Вставить(ИмяРаздела, ДеревоРаздела);
	
	СписокСохранения.Вставить("ДанныеМногоуровневыхРазделов", ДанныеМногоуровневыхРазделов);
	
	СписокСохранения.Удалить("ДанныеМногострочныхРазделов");
	СписокСохранения.Удалить("ДанныеМногостраничныхРазделов");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаСобытийВТабличномДокументе

&НаКлиенте
Процедура ТабличныйДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если СтрЧислоВхождений(Область.Имя, "ДобавитьСтроку") = 1 И Область.Текст <> "" Тогда
		ДобавитьСтроку("");
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "УдалитьСтроку") = 1 И Область.Текст <> "" Тогда
		УдалитьСтроку("");
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "ДобавитьСтраницу") > 0 И Область.Текст <> "" Тогда
		ДобавитьСтраницу("");
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "УдалитьСтраницу") > 0 И Область.Текст <> "" Тогда
		УдалитьСтраницу("");
	ИначеЕсли Область.Имя = "СобратьДанные" Тогда
		СобратьДанные();
	КонецЕсли;
	
	Если РегламентированнаяОтчетностьКлиент.ВыбраннаяОбластьРегламентированногоОтчетаНеВерна(
		ЭтаФорма, Область, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	Если Область.Имя = "НалоговыйОрган" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОбработкаВыбораИФНС();
		
	ИначеЕсли СтрЗаканчиваетсяНа(Область.Имя, "_Основание") Тогда
		
		СтандартнаяОбработка = Ложь;
		ВвестиОснованиеЛьготы(Область);
		Возврат;
		
	Иначе
		
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтаФорма, Область.Имя);
		Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений.Количество() > 0 Тогда
			КодПоказателя = ПараметрВводаПоля.Показатель;
			КопироватьДанныеФормы(ПараметрВводаПоля.ТаблицаЗначений, ТЗВыбора);
			ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	ИмяПоказателя = Область.Имя;
	ЗначПоказателя = Область.Значение;
	
	Если ИмяПоказателя = "ДатаПодписи" Тогда
		ДатаПодписи = Область.Значение;
		ЗаполнитьСведенияОбОрганизацииНаСервере(Истина);
		Модифицированность = Истина;
	ИначеЕсли ИмяПоказателя = "НомерКорректировки" Тогда
		ОписаниеТипа = Новый ОписаниеТипов("Число");
		НомерКорректировки = ОписаниеТипа.ПривестиЗначение(Область.Значение);
		СтруктураРеквизитовФормы.ВидДокумента = ?(НомерКорректировки = 0, 0, 1);
		Область.Значение = Строка(НомерКорректировки);
		Модифицированность = Истина;
	КонецЕсли;
	
	Если СтрНачинаетсяС(ИмяПоказателя, "П000010002003")
		ИЛИ ИмяПоказателя = "П000020002003" Тогда
		ОКТМО = СтрЗаменить(ЗначПоказателя, " ", "");
		ДлинаОКТМО = СтрДлина(ОКТМО);
		Если ДлинаОКТМО = 11 ИЛИ ДлинаОКТМО = 8 ИЛИ ДлинаОКТМО = 0 Тогда
			Область.Значение = ОКТМО;
		Иначе
			Область.Значение = "";
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр(
			"ru='Код по ОКТМО должен состоять из 8 или 11 цифр. Значение очищено.'");
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	
	Если СтрНачинаетсяС(ИмяПоказателя, "П000010001003")
		ИЛИ ИмяПоказателя = "П000020001003" Тогда
		КБК = СтрЗаменить(ЗначПоказателя, " ", "");
		ДлинаКБК = СтрДлина(КБК);
		Если ДлинаКБК = 20 ИЛИ ДлинаКБК = 0 Тогда
			Область.Значение = КБК;
		Иначе
			Область.Значение = "";
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр(
			"ru='Код бюджетной классификации должен состоять из 20 цифр. Значение очищено.'");
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	
	Если СтрНачинаетсяС(ИмяПоказателя, "П000020006003") ИЛИ СтрНачинаетсяС(ИмяПоказателя, "П000020010003") Тогда
		ОблЧисл = ТабличныйДокумент.Области.Найти(Лев(ИмяПоказателя, 13) + "_Числ");
		ОблЗнам = ТабличныйДокумент.Области.Найти(Лев(ИмяПоказателя, 13) + "_Знам");
		Если ОблЧисл.Значение > 0 И ОблЗнам.Значение > 0 И ОблЧисл.Значение > ОблЗнам.Значение Тогда
			Область.Очистить();
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(НСтр(
			"ru='В строке %1 числитель не должен быть больше знаменателя. Значение очищено.'"), Сред(ИмяПоказателя, 9, 3));
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяПоказателя = "П000020013003" ИЛИ ИмяПоказателя = "П000020016003" Тогда
		Если ЗначПоказателя > 12 Тогда
			Область.Очистить();
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(НСтр(
			"ru='Значение в строке %1 не должно быть больше 12. Значение очищено.'"), Сред(ИмяПоказателя, 9, 3));
			Сообщение.Сообщить();
		Иначе
			ОблКолМесСобств = ТабличныйДокумент.Области.Найти("П000020013003");
			ОблКолМесЛьготы = ТабличныйДокумент.Области.Найти("П000020016003");
			Если ОблКолМесСобств.Значение > 0 И ОблКолМесЛьготы.Значение > ОблКолМесСобств.Значение Тогда
				Область.Очистить();
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru='Значение в строке 160 не должно быть больше значения в строке 130. Значение очищено.'");
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяПоказателя = "П000020007003_КодЛьготы" ИЛИ ИмяПоказателя = "П000020009003_КодЛьготы" Тогда
		
		Если ЗначениеЗаполнено(ЗначПоказателя) Тогда
			
			ОблСтр070_КодЛьготы = ТабличныйДокумент.Области.Найти("П000020007003_КодЛьготы");
			ОблСтр090_КодЛьготы = ТабличныйДокумент.Области.Найти("П000020009003_КодЛьготы");
			
			Стр070_КодЛьготы = СокрЛП(ОблСтр070_КодЛьготы.Значение);
			Стр090_КодЛьготы = СокрЛП(ОблСтр090_КодЛьготы.Значение);
			
			МассивОчищаемыхСтрок = Новый Массив;
			
			Если ИмяПоказателя = "П000020009003_КодЛьготы" Тогда
				Если ЗначениеЗаполнено(Стр070_КодЛьготы) Тогда
					ОблСтр070_КодЛьготы.Очистить();
					МассивОчищаемыхСтрок.Добавить("070");
					Если ТабличныйДокумент.Области.Найти("П000020008003").Значение > 0 Тогда
						МассивОчищаемыхСтрок.Добавить("080");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если ИмяПоказателя = "П000020007003_КодЛьготы" Тогда
				Если ЗначениеЗаполнено(Стр090_КодЛьготы) Тогда
					ОблСтр090_КодЛьготы.Очистить();
					МассивОчищаемыхСтрок.Добавить("090");
					Если ТабличныйДокумент.Области.Найти("П000020010003_Числ").Значение > 0
						ИЛИ ТабличныйДокумент.Области.Найти("П000020010003_Знам").Значение > 0 Тогда
						МассивОчищаемыхСтрок.Добавить("100");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если МассивОчищаемыхСтрок.Количество() > 0 Тогда
				ТекстНачало = ?(МассивОчищаемыхСтрок.Количество() > 1, "Значения в строках", "Значение в строке");
				ТекстСередина = СтрСоединить(МассивОчищаемыхСтрок, ", ");
				ТекстОкончание = ?(МассивОчищаемыхСтрок.Количество() > 1, "очищены", "очищено");
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон(НСтр(
				"ru='Не допускается одновременное заполнение групп строк: 070, 080; 090, 100. %1 %2 %3.'"),
				ТекстНачало, ТекстСередина, ТекстОкончание);
				Сообщение.Сообщить();
			КонецЕсли;
			
		КонецЕсли;
		
	Конецесли;
	
	Если ИмяПоказателя = "П000020018003_КодЛьготы" ИЛИ ИмяПоказателя = "П000020020003"
		ИЛИ ИмяПоказателя = "П000020022003_КодЛьготы" Тогда
		
		Если ЗначениеЗаполнено(ЗначПоказателя) Тогда
			
			ОблСтр180_КодЛьготы = ТабличныйДокумент.Области.Найти("П000020018003_КодЛьготы");
			ОблСтр200_КодЛьготы = ТабличныйДокумент.Области.Найти("П000020020003");
			ОблСтр220_КодЛьготы = ТабличныйДокумент.Области.Найти("П000020022003_КодЛьготы");
			
			Стр180_КодЛьготы = СокрЛП(ОблСтр180_КодЛьготы.Значение);
			Стр200_КодЛьготы = СокрЛП(ОблСтр200_КодЛьготы.Значение);
			Стр220_КодЛьготы = СокрЛП(ОблСтр220_КодЛьготы.Значение);
			
			МассивОчищаемыхСтрок = Новый Массив;
			
			Если ИмяПоказателя = "П000020020003" ИЛИ ИмяПоказателя = "П000020022003_КодЛьготы" Тогда
				Если ЗначениеЗаполнено(Стр180_КодЛьготы) Тогда
					ОблСтр180_КодЛьготы.Очистить();
					МассивОчищаемыхСтрок.Добавить("180");
					Если ТабличныйДокумент.Области.Найти("П000020019003").Значение > 0 Тогда
						МассивОчищаемыхСтрок.Добавить("190");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если ИмяПоказателя = "П000020018003_КодЛьготы" ИЛИ ИмяПоказателя = "П000020022003_КодЛьготы" Тогда
				Если ЗначениеЗаполнено(Стр200_КодЛьготы) Тогда
					ОблСтр200_КодЛьготы.Очистить();
					МассивОчищаемыхСтрок.Добавить("200");
					Если ТабличныйДокумент.Области.Найти("П000020021003").Значение > 0 Тогда
						МассивОчищаемыхСтрок.Добавить("210");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если ИмяПоказателя = "П000020018003_КодЛьготы" ИЛИ ИмяПоказателя = "П000020020003" Тогда
				Если ЗначениеЗаполнено(Стр220_КодЛьготы) Тогда
					ОблСтр220_КодЛьготы.Очистить();
					МассивОчищаемыхСтрок.Добавить("220");
					Если ТабличныйДокумент.Области.Найти("П000020022003_Процент").Значение > 0 Тогда
						МассивОчищаемыхСтрок.Добавить("220а");
					КонецЕсли;
					Если ТабличныйДокумент.Области.Найти("П000020023003").Значение > 0 Тогда
						МассивОчищаемыхСтрок.Добавить("230");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если МассивОчищаемыхСтрок.Количество() > 0 Тогда
				ТекстНачало = ?(МассивОчищаемыхСтрок.Количество() > 1, "Значения в строках", "Значение в строке");
				ТекстСередина = СтрСоединить(МассивОчищаемыхСтрок, ", ");
				ТекстОкончание = ?(МассивОчищаемыхСтрок.Количество() > 1, "очищены", "очищено");
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон(НСтр(
				"ru='Не допускается одновременное заполнение групп строк: 180, 190; 200, 210; 220, 220а, 230. %1 %2 %3.'"),
				ТекстНачало, ТекстСередина, ТекстОкончание);
				Сообщение.Сообщить();
			КонецЕсли;
			
		КонецЕсли;
		
	Конецесли;
	
	Если ИмяПоказателя = "П000020000100" Тогда
		
		СтруктПарам = Новый Структура;
		СтруктПарам.Вставить("КадастрНомер", ЗначПоказателя);
		СтруктПарам.Вставить("НомерСтраницы", Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
		
		Если Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
			Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьЭлементы()[0].КолонкаРазделыОтчета
			= ПредставлениеСтраницыРаздела2(СтруктПарам);
		Иначе
			Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчета
			= ПредставлениеСтраницыРаздела2(СтруктПарам);
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтаФорма, ИмяПоказателя);
	
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений.Количество() > 0 Тогда
		
		КопироватьДанныеФормы(ПараметрВводаПоля.ТаблицаЗначений, ТЗВыбора);
		ЭлТЗВыбора = ТЗВыбора.НайтиСтроки(Новый Структура("Код", ЗначПоказателя));
		
	КонецЕсли;
	
	ИмяОбласти = Область.Имя;
	Если ИмяРаздела <> "Титульный" Тогда
		ПозицияПодстроки = СтрНайти(ИмяОбласти, "_");
		Если ПозицияПодстроки > 0 И Сред(ИмяОбласти, ПозицияПодстроки) <> "_КодЛьготы" Тогда
			ИмяОбласти = Лев(ИмяОбласти, ПозицияПодстроки - 1);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВводаПоля = СвойстваПоказателей.НайтиСтроки(Новый Структура("Показатель", ИмяОбласти));
	Если ПараметрыВводаПоля.Количество() > 0 Тогда
		ПараметрВводаПоля = ПараметрыВводаПоля[0];
		Если СтрДлина(СокрЛП(ЗначПоказателя)) <> 0 И ПараметрВводаПоля.Длина <> 0 И СтрДлина(СокрЛП(ЗначПоказателя)) <> ПараметрВводаПоля.Длина Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Длина значения показателя должна быть равна " + ПараметрВводаПоля.Длина + "!'");
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если ИмяРаздела = "Титульный" Тогда
		ЗаполнитьПериодОтчета(ЭтаФорма);
	КонецЕсли;
	
	УстановитьДоступностьОбластей(ЭтаФорма, ИмяРаздела);
	
	Если СтруктураЗависимыхЯчеекДляАвторасчета.Свойство(ИмяРаздела) Тогда
		РасчетНаКлиенте(ИмяРаздела, Область.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриАктивизацииОбласти(Элемент)
	
	Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Доступность = Ложь;
	Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Доступность = Ложь;
	
	ИмяГруппы = МногострочнаяЧастьТекущейОбласти(ЭтаФорма, Элемент.ТекущаяОбласть.Имя);
	
	Если ЗначениеЗаполнено(ИмяГруппы) Тогда
		
		Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Доступность = Истина;
		
		ИмяПервойГрафы = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"][0];
		ИсхКолСтрок = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИсхКолСтрок"];
		Координаты = СтрокаСКоординатамиПоказателя(Элемент.ТекущаяОбласть.Имя);
		
		ТестИмя = ИмяПервойГрафы + Координаты + СтрокаСИндексомПоказателя(ИсхКолСтрок + 1);
		Если СтрЧислоВхождений(Элемент.ТекущаяОбласть.Имя, "ДобавитьСтроку") = 0
			И ТабличныйДокумент.Области.Найти(ТестИмя) <> Неопределено Тогда
			Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ДопТекст = ДополнительныйТекстПриДобавленииУдаленииСтрок(ИмяГруппы);
	Если ДопТекст <> Неопределено Тогда
		Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Заголовок
		= ДопТекст.ЗаголовокКонтекстногоМенюДобавитьСтроку;
		Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Заголовок
		= ДопТекст.ЗаголовокКонтекстногоМенюУдалитьСтроку;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораИФНС()
	
	КодИМНС = СокрЛП(ТабличныйДокумент.Области.НалоговыйОрган.Значение);
	КПП     = СокрЛП(ТабличныйДокумент.Области.КПП.Значение); 
	ЗначенияДляОтбора = Новый Массив;
	ЗначенияДляОтбора.Добавить(Новый Структура("КодНО, КПП", КодИМНС, КПП));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыФормы.Вставить("ЗначенияДляОтбора", ЗначенияДляОтбора);
	
	ФормаВыбораНалоговогоОргана = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени(
	"ФормаВыбораНалоговогоОргана", ПараметрыФормы, ЭтаФорма);
	
	Если ФормаВыбораНалоговогоОргана.ТаблицаНО.Количество() <> 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораИФНСЗавершение", ЭтотОбъект);
		ФормаВыбораНалоговогоОргана.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВыбораНалоговогоОргана.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВыбораНалоговогоОргана.Открыть();
		
	ИначеЕсли НЕ ЗначениеЗаполнено(КодИМНС) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Код налогового органа необходимо задать в справочнике ""Организации"".'");
		Сообщение.Сообщить();
		Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
			УстановитьДоступностьОбластей(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
		КонецЕсли;
		
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Для выбора инспекции, необходимо завести соответствующую запись в справочнике ""Регистрации в налоговом органе"".'");
		Сообщение.Сообщить();
		Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
			УстановитьДоступностьОбластей(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораИФНСЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ВыбранныйКодНО = РезультатВыбора.КодНО;
		ДлинаКодаНО = СтрДлина(ВыбранныйКодНО);
		
		Для Инд = ДлинаКодаНО + 1 По 4 Цикл
			ВыбранныйКодНО = "0" + ВыбранныйКодНО;
		КонецЦикла;
		
		ТабличныйДокумент.Области["НалоговыйОрган"].Значение = ВыбранныйКодНО;
		ТабличныйДокумент.Области["КПП"].Значение = РезультатВыбора.КПП;
		
		ЗаполнитьСведенияОПодписантах();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		УстановитьДоступностьОбластей(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиОснованиеЛьготы(Область)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОснованиеЛьготы", Область.Значение);
	
	ФормаВвода = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ФормаВводаОснованияЛьготы", ПараметрыФормы, ЭтаФорма);
	
	ДополнительныеПараметры = Новый Структура("Область", Область);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиОснованиеЛьготыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ФормаВвода.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаВвода.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВвода.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиОснованиеЛьготыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	
	Если Результат <> Неопределено Тогда
		Область.Значение = Результат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МногострочнаяЧастьТекущейОбласти(Форма, Знач ИмяОбласти)
	
	Если НЕ ЗначениеЗаполнено(ИмяОбласти) Тогда
		Возврат Неопределено;
	Иначе
		НомПозПодчеркивания = Форма.СтруктураРеквизитовФормы.ДлинаИмениГрафы + 1;
		Если НЕ (СтрДлина(ИмяОбласти) > НомПозПодчеркивания
			И Сред(ИмяОбласти, НомПозПодчеркивания, 1) = "_") Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ИмяОбласти = СтрЗаменить(ИмяОбласти, "ДобавитьСтрокуЗначок", "");
	ИмяОбласти = СтрЗаменить(ИмяОбласти, "ДобавитьСтроку", "");
	ИмяОбласти = СтрЗаменить(ИмяОбласти, "УдалитьСтроку", "");
	
	ИмяГруппы = Лев(ИмяОбласти, Форма.СтруктураРеквизитовФормы.ДлинаИмениГруппы);
	
	Для каждого ЭлемСтруктуры Из Форма.СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей Цикл
		Если ЭлемСтруктуры.Ключ = ИмяГруппы Тогда
			Возврат ИмяГруппы;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция ДополнительныйТекстПриДобавленииУдаленииСтрок(Знач ИмяГруппы)
	
	Если НЕ ЗначениеЗаполнено(ИмяГруппы) Тогда
		Возврат Неопределено;
	ИначеЕсли НЕ СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей.Свойство(ИмяГруппы) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Подстрока = "строку";
	ТекстВопроса = НСтр("ru='Удалить выбранную строку?'");
	
	ДопТекст = Новый Структура;
	ДопТекст.Вставить("ЗаголовокКонтекстногоМенюДобавитьСтроку", "Добавить " + Подстрока);
	ДопТекст.Вставить("ЗаголовокКонтекстногоМенюУдалитьСтроку", "Удалить " + Подстрока);
	ДопТекст.Вставить("ТекстВопросаПередУдалениемСтроки", ТекстВопроса);
	
	Возврат ДопТекст;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьОбластей(Форма, ИмяРаздела)
	
	АвтоРасчПоказатели = Новый Массив;
	
	Если ИмяРаздела = "Титульный" Тогда
		
		ОблНаимОрг     = Форма.ТабличныйДокумент.Области.НаимОрг;
		ОблКодИННРеорг = Форма.ТабличныйДокумент.Области.ИННРеорганизованнойОрганизации;
		ОблКодКППРеорг = Форма.ТабличныйДокумент.Области.КППРеорганизованнойОрганизации;
		
		ЗначПоМесту = СокрЛП(Форма.ТабличныйДокумент.Области.ПоМесту.Значение);
		
		ЗначКодФормыРеорг = СокрЛП(Форма.ТабличныйДокумент.Области.КодФормыРеорганизации.Значение);
		
		Если ЗначениеЗаполнено(ЗначКодФормыРеорг)
			И ЗначКодФормыРеорг <> "0" Тогда
			
			Если ЗначПоМесту = "270" ИЛИ ЗначПоМесту = "216" Тогда
				РазрешитьВводДанныхВОбласть(Форма, ОблНаимОрг);
			Иначе
				ЗапретитьВводДанныхВОбласть(Форма, ОблНаимОрг);
				Форма.ТабличныйДокумент.Области.НаимОрг.Значение
				= Форма.СтруктураРеквизитовФормы.НачальноеЗначениеНаимОрганизации;
			КонецЕсли;
			
			РазрешитьВводДанныхВОбласть(Форма, ОблКодИННРеорг);
			РазрешитьВводДанныхВОбласть(Форма, ОблКодКППРеорг);
			
		Иначе
			
			ЗапретитьВводДанныхВОбласть(Форма, ОблНаимОрг);
			Форма.ТабличныйДокумент.Области.НаимОрг.Значение
			= Форма.СтруктураРеквизитовФормы.НачальноеЗначениеНаимОрганизации;
			
			ЗапретитьВводДанныхВОбласть(Форма, ОблКодИННРеорг);
			ЗапретитьВводДанныхВОбласть(Форма, ОблКодКППРеорг);
			
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Раздел1" Тогда
		
		СтруктЗависимыхЯчеек = Форма.СтруктураЗависимыхЯчеекДляАвторасчета[ИмяРаздела];
		
		Инд = 1;
		Инд_Строка = "_1";
		Обл = Форма.ТабличныйДокумент.Области.Найти("П000010002103" + Инд_Строка);
		Пока Обл <> Неопределено Цикл
			Для каждого Пок Из СтруктЗависимыхЯчеек Цикл
				АвтоРасчПоказатели.Добавить(Пок.Ключ + Инд_Строка);
			КонецЦикла;
			Инд = Инд + 1;
			Инд_Строка = СтрокаСИндексомПоказателя(Инд);
			Обл = Форма.ТабличныйДокумент.Области.Найти("П000010002103" + Инд_Строка);
		КонецЦикла;
		
	ИначеЕсли ИмяРаздела = "Раздел2" Тогда
		
		СтруктЗависимыхЯчеек = Форма.СтруктураЗависимыхЯчеекДляАвторасчета[ИмяРаздела];
		
		Для каждого Пок Из СтруктЗависимыхЯчеек Цикл
			АвтоРасчПоказатели.Добавить(Пок.Ключ);
		КонецЦикла;
		
		ОблСтр070_КодЛьготы = Форма.ТабличныйДокумент.Области["П000020007003_КодЛьготы"];
		ОблСтр070_Основание = Форма.ТабличныйДокумент.Области["П000020007003_Основание"];
		ОблСтр080 = Форма.ТабличныйДокумент.Области["П000020008003"];
		КодЛьготы = СокрЛП(ОблСтр070_КодЛьготы.Значение);
		Если КодЛьготы = "3022100" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр070_Основание);
			УстановитьОснованиеЛьготыПоУмолчанию(ОблСтр070_Основание);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр070_Основание);
		КонецЕсли;
		Если ЗначениеЗаполнено(КодЛьготы) Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр080);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр080);
		КонецЕсли;
		
		ОблСтр090_КодЛьготы = Форма.ТабличныйДокумент.Области["П000020009003_КодЛьготы"];
		ОблСтр090_Основание = Форма.ТабличныйДокумент.Области["П000020009003_Основание"];
		ОблСтр100_Числ = Форма.ТабличныйДокумент.Области["П000020010003_Числ"];
		ОблСтр100_Знам = Форма.ТабличныйДокумент.Области["П000020010003_Знам"];
		КодЛьготы = СокрЛП(ОблСтр090_КодЛьготы.Значение);
		Если КодЛьготы = "3022300" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр090_Основание);
			УстановитьОснованиеЛьготыПоУмолчанию(ОблСтр090_Основание);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр090_Основание);
		КонецЕсли;
		Если ЗначениеЗаполнено(КодЛьготы) Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр100_Числ);
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр100_Знам);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр100_Числ);
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр100_Знам);
		КонецЕсли;
		
		ОблСтр130 = Форма.ТабличныйДокумент.Области["П000020013003"];
		ОблСтр160 = Форма.ТабличныйДокумент.Области["П000020016003"];
		Если ОблСтр130.Значение > 0 Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр160);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр160);
		КонецЕсли;
		
		ОблСтр180_КодЛьготы = Форма.ТабличныйДокумент.Области["П000020018003_КодЛьготы"];
		ОблСтр180_Основание = Форма.ТабличныйДокумент.Области["П000020018003_Основание"];
		ОблСтр190 = Форма.ТабличныйДокумент.Области["П000020019003"];
		КодЛьготы = СокрЛП(ОблСтр180_КодЛьготы.Значение);
		Если КодЛьготы = "3022400" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр180_Основание);
			УстановитьОснованиеЛьготыПоУмолчанию(ОблСтр180_Основание);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр180_Основание);
		КонецЕсли;
		Если ЗначениеЗаполнено(КодЛьготы) Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр190);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр190);
			ИндексАвтоРасчПоказателя = АвтоРасчПоказатели.Найти("П000020019003");
			Если ИндексАвтоРасчПоказателя <> Неопределено Тогда
				АвтоРасчПоказатели.Удалить(ИндексАвтоРасчПоказателя);
			КонецЕсли;
		КонецЕсли;
		
		ОблСтр200 = Форма.ТабличныйДокумент.Области["П000020020003"];
		ОблСтр210 = Форма.ТабличныйДокумент.Области["П000020021003"];
		КодЛьготы = СокрЛП(ОблСтр200.Значение);
		Если ЗначениеЗаполнено(КодЛьготы) Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр210);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр210);
			ИндексАвтоРасчПоказателя = АвтоРасчПоказатели.Найти("П000020021003");
			Если ИндексАвтоРасчПоказателя <> Неопределено Тогда
				АвтоРасчПоказатели.Удалить(ИндексАвтоРасчПоказателя);
			КонецЕсли;
		КонецЕсли;
		
		ОблСтр220_КодЛьготы = Форма.ТабличныйДокумент.Области["П000020022003_КодЛьготы"];
		ОблСтр220_Основание = Форма.ТабличныйДокумент.Области["П000020022003_Основание"];
		ОблСтр220_Процент = Форма.ТабличныйДокумент.Области["П000020022003_Процент"];
		ОблСтр230 = Форма.ТабличныйДокумент.Области["П000020023003"];
		КодЛьготы = СокрЛП(ОблСтр220_КодЛьготы.Значение);
		Если КодЛьготы = "3022200" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр220_Основание);
			УстановитьОснованиеЛьготыПоУмолчанию(ОблСтр220_Основание);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр220_Основание);
		КонецЕсли;
		Если ЗначениеЗаполнено(КодЛьготы) Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр220_Процент);
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр230);
			
			Если ОблСтр220_Процент.Значение = 0 Тогда
				ИндексАвтоРасчПоказателя = АвтоРасчПоказатели.Найти("П000020023003");
				Если ИндексАвтоРасчПоказателя <> Неопределено Тогда
					АвтоРасчПоказатели.Удалить(ИндексАвтоРасчПоказателя);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр220_Процент);
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр230);
		КонецЕсли;
		
		ОблСтр240_КодЛьготы = Форма.ТабличныйДокумент.Области["П000020024003_КодЛьготы"];
		ОблСтр240_Основание = Форма.ТабличныйДокумент.Области["П000020024003_Основание"];
		КодЛьготы = СокрЛП(ОблСтр240_КодЛьготы.Значение);
		Если КодЛьготы = "3022500" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОблСтр240_Основание);
			УстановитьОснованиеЛьготыПоУмолчанию(ОблСтр240_Основание);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОблСтр240_Основание);
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого ИмяПок Из АвтоРасчПоказатели Цикл
		Обл = Форма.ТабличныйДокумент.Область(ИмяПок);
		Если Обл.ЦветФона = Форма.мСтруктураЦветовРаскраски["_5"] Тогда
			Продолжить;
		КонецЕсли;
		Если Форма.СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
			Обл.Защита = Ложь;
			Обл.ЦветФона = Форма.мСтруктураЦветовРаскраски["_0"];
		Иначе
			Обл.Защита = Истина;
			Обл.ЦветФона = Форма.мСтруктураЦветовРаскраски["_4"];
		КонецЕсли;
	КонецЦикла;
	
	ПереопределитьЗначенияСвойствЯчеекТабличногоДокумента(Форма, ИмяРаздела);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗапретитьВводДанныхВОбласть(Форма, ОбрабатываемаяОбласть, ОчищатьЗначение = Истина)
	
	ЦветНедоступнойОбласти = Форма.мСтруктураЦветовРаскраски["_5"];
	
	ОбрабатываемаяОбласть.Защита   = Истина;
	ОбрабатываемаяОбласть.ЦветФона = ЦветНедоступнойОбласти;
	
	Если ОчищатьЗначение Тогда
		ОбрабатываемаяОбласть.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РазрешитьВводДанныхВОбласть(Форма, ОбрабатываемаяОбласть)
	
	ЦветОбластиРучногоВвода = Форма.мСтруктураЦветовРаскраски["_0"];
	
	ОбрабатываемаяОбласть.Защита   = Ложь;
	ОбрабатываемаяОбласть.ЦветФона = ЦветОбластиРучногоВвода;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПереопределитьЗначенияСвойствЯчеекТабличногоДокумента(Форма, ИмяРаздела)
	
	Для каждого Обл Из Форма.ТабличныйДокумент.Области Цикл
		
		Если НЕ (СтрЧислоВхождений(Тип(Обл), "Рисунок") = 0
			И Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Обл.СодержитЗначение = Истина
			И НЕ Обл.Защита) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Обл.ЦветФона = Форма.мСтруктураЦветовРаскраски["_4"]
		 ИЛИ Обл.ЦветФона = Форма.мСтруктураЦветовРаскраски["_5"] Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяГруппы = МногострочнаяЧастьТекущейОбласти(Форма, Обл.Имя);
		Если ИмяГруппы = Неопределено Тогда
			ИмяПоиска = Обл.Имя;
		Иначе
			ИмяПоиска = Лев(Обл.Имя, Форма.СтруктураРеквизитовФормы.ДлинаИмениГрафы);
		КонецЕсли;
		
		Если Форма.СтруктураРеквизитовФормы.АвтозаполняемыеПоказатели.Свойство(ИмяПоиска) Тогда
			Если Форма.СтруктураРеквизитовФормы.АвтозаполняемыеПоказатели[ИмяПоиска][0] Тогда
				Обл.ЦветФона = Форма.мСтруктураЦветовРаскраски["_3"];
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОснованиеЛьготыПоУмолчанию(ОбластьОснованияЛьготы)
	
	Если НЕ ЗначениеЗаполнено(ОбластьОснованияЛьготы.Значение) Тогда
		ОбластьОснованияЛьготы.Значение = "000000000000";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПомещениеДанныхИзТабличногоДокументаВМестаХранения

&НаСервере
Процедура СохранитьДанныеРаздела(НаимРаздела, ВеткаРаздела = Неопределено)
	
	// Многоуровневые разделы.
	Если мСтруктураМногоуровневыхРазделов.Свойство(НаимРаздела) Тогда
		
		Если ВеткаРаздела = Неопределено Тогда
			
			СтруктураРаздела = мСтруктураМногоуровневыхРазделов[НаимРаздела];
			
			ИмяГлРаздела = Неопределено;
			СтруктураРаздела.Свойство("ИмяГлавногоРаздела", ИмяГлРаздела);
			ИмяГлРаздела = ?(ИмяГлРаздела = Неопределено, НаимРаздела, ИмяГлРаздела);
			
			ДеревоДанныхРаздела = ПолучитьИзВременногоХранилища(
				СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]);
			
			ТекВетка = ДеревоДанныхРаздела;
			
			Для каждого НомерСтроки Из СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела Цикл
				ТекВетка = ТекВетка.Строки[НомерСтроки - 1];
			КонецЦикла;
			
		Иначе
			
			ТекВетка = ВеткаРаздела;
			
		КонецЕсли;
		
		ДанныеТаблДокумента = ДанныеТабличногоДокумента(НаимРаздела);
		
		ТекВетка.Данные = ДанныеТаблДокумента.Данные;
		ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
		
		СтруктураРеквизитовФормы.Вставить("АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела,
			ПоместитьВоВременноеХранилище(ДеревоДанныхРаздела, УникальныйИдентификатор));
		
		ДеревоДанныхРаздела = Неопределено;
		
	// Обычные разделы.
	Иначе
		
		ЭтаФорма["СтруктураДанных" + НаимРаздела] = ДанныеТабличногоДокумента(НаимРаздела);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеТабличногоДокумента(НаимРаздела)
	
	ДанныеТаблДокумента = Новый Структура;
	
	// Многоуровневые разделы.
	//
	Если мСтруктураМногоуровневыхРазделов.Свойство(НаимРаздела) Тогда
		
		СтруктураРаздела = мСтруктураМногоуровневыхРазделов[НаимРаздела];
		
		ИменаГруппМнЧ = Неопределено;
		СтруктураРаздела.Свойство("ИменаГруппМнЧ", ИменаГруппМнЧ);
		РазделСодержитМнЧ = (ИменаГруппМнЧ <> Неопределено);
		
		СтруктураДанных = Новый Структура();
		Для Каждого ТекущаяОбласть Из ТабличныйДокумент.Области Цикл
			Если СтрЧислоВхождений(Тип(ТекущаяОбласть), "Рисунок") = 0
				И ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
				И ТекущаяОбласть.СодержитЗначение = Истина Тогда
				Если РазделСодержитМнЧ Тогда
					Если МногострочнаяЧастьТекущейОбласти(ЭтаФорма, ТекущаяОбласть.Имя) = Неопределено Тогда
						СтруктураДанных.Вставить(ТекущаяОбласть.Имя, ТекущаяОбласть.Значение);
					КонецЕсли;
				Иначе
					СтруктураДанных.Вставить(ТекущаяОбласть.Имя, ТекущаяОбласть.Значение);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ДанныеТаблДокумента.Вставить("Данные", СтруктураДанных);
		
		СтруктураДанныхМногострочныхЧастей = Новый Структура;
		Если РазделСодержитМнЧ Тогда
			Для каждого ИмяГруппыМнЧ Из СтруктураРаздела["ИменаГруппМнЧ"] Цикл
				ЭлемСтруктуры = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппыМнЧ];
				Если ЭлемСтруктуры["УровеньПодчинения"] = 0 Тогда
					ДеревоДанных = Новый ДеревоЗначений;
					ДеревоДанных.Колонки.Добавить("Данные");
					ДеревоДанных.Колонки.Добавить("ДанныеМногострочныхЧастей");
					СформироватьДеревоМногострочнойЧастиПоДаннымТабличногоДокумента(ИмяГруппыМнЧ, ДеревоДанных);
					СтруктураДанныхМногострочныхЧастей.Вставить(ИмяГруппыМнЧ, ДеревоДанных);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ДанныеТаблДокумента.Вставить("ДанныеМногострочныхЧастей", СтруктураДанныхМногострочныхЧастей);
		
	// Обычные разделы.
	//
	Иначе
		
		Для Инд = 0 По ТабличныйДокумент.Области.Количество() - 1 Цикл
			
			ТекОбласть = ТабличныйДокумент.Области[Инд];
			
			Если СтрЧислоВхождений(Тип(ТекОбласть), "Рисунок") = 0
				И ТекОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
				И ТекОбласть.СодержитЗначение = Истина Тогда
				
				ДанныеТаблДокумента.Вставить(ТекОбласть.Имя, ТекОбласть.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДанныеТаблДокумента;
	
КонецФункции

&НаСервере
Процедура СформироватьДеревоМногострочнойЧастиПоДаннымТабличногоДокумента(ИмяГруппы, ДеревоДанных, Координаты = "")
	
	Если НЕ СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей.Свойство(ИмяГруппы) Тогда
		Возврат;
	КонецЕсли;
	
	МассивИменГраф = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"];
	ИмяПервойГрафы = МассивИменГраф[0];
	
	Инд_Число = 1;
	Пока ТабличныйДокумент.Области.Найти(ИмяПервойГрафы + Координаты + СтрокаСИндексомПоказателя(Инд_Число)) <> Неопределено Цикл
		НовВетка = ДеревоДанных.Строки.Добавить();
		
		// Заполнение данными из текущей строки многострочной части.
		Данные = Новый Структура;
		Инд = СтрокаСИндексомПоказателя(Инд_Число);
		Для каждого ИмяГрафы Из МассивИменГраф Цикл
			Обл = ТабличныйДокумент.Области.Найти(ИмяГрафы + Координаты + Инд);
			Если Обл <> Неопределено Тогда
				Данные.Вставить(ИмяГрафы, Обл.Значение);
			КонецЕсли;
		КонецЦикла;
		НовВетка.Данные = Данные;
		
		// Заполнение данными из строк многострочных частей, подчиненных текущей строке многострочной части.
		ДанныеМногострочныхЧастей = Новый Структура;
		ИменаПодчГрупп = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаПодчиненныхГрупп"];
		Для каждого ИмяПодчГруппы Из ИменаПодчГрупп Цикл
			ДеревоПодчСтрок = Новый ДеревоЗначений;
			ДеревоПодчСтрок.Колонки.Добавить("Данные");
			ДеревоПодчСтрок.Колонки.Добавить("ДанныеМногострочныхЧастей");
			ПодчКоординаты = Координаты + Инд;
			СформироватьДеревоМногострочнойЧастиПоДаннымТабличногоДокумента(
				ИмяПодчГруппы, ДеревоПодчСтрок, ПодчКоординаты);
			ДанныеМногострочныхЧастей.Вставить(ИмяПодчГруппы, ДеревоПодчСтрок);
		КонецЦикла;
		НовВетка.ДанныеМногострочныхЧастей = ДанныеМногострочныхЧастей;
		
		Инд_Число = Инд_Число + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПомещениеДанныхИзМестХраненияВТабличныйДокумент

&НаСервере
Процедура ВывестиДанныеВТабличныйДокументНаСервере(ИмяТекРаздела, НовыеНомераСтрокМногоуровнегоРаздела = Неопределено)
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	ВывестиДанныеВТабличныйДокумент(ИмяТекРаздела, НовыеНомераСтрокМногоуровнегоРаздела);
	
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяТекРаздела;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеВТабличныйДокумент(ИмяТекРаздела, НовыеНомераСтрокМногоуровнегоРаздела = Неопределено)
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
	ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяТекРаздела));
	
	// Многоуровневые разделы.
	//
	Если мСтруктураМногоуровневыхРазделов.Свойство(ИмяТекРаздела) Тогда
		
		СтруктураРаздела = мСтруктураМногоуровневыхРазделов[ИмяТекРаздела];
		
		ИмяГлРаздела = Неопределено;
		СтруктураРаздела.Свойство("ИмяГлавногоРаздела", ИмяГлРаздела);
		ИмяГлРаздела = ?(ИмяГлРаздела = Неопределено, ИмяТекРаздела, ИмяГлРаздела);
		
		ДеревоДанныхРаздела = ПолучитьИзВременногоХранилища(
			СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]);
		
		ТекВетка = ДеревоДанныхРаздела;
		
		Для каждого НомерСтроки Из НовыеНомераСтрокМногоуровнегоРаздела Цикл
			ТекВетка = ТекВетка.Строки[НомерСтроки - 1];
		КонецЦикла;
		
		// Вывод немногострочных данных.
		Для Каждого ЭлСтруктуры Из ТекВетка.Данные Цикл
			Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
			Если Обл <> Неопределено Тогда
				Обл.Значение = ЭлСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
		// Добавление в многострочную часть необходимого количества
		// дополнительных строк, вывод многострочных данных.
		ВывестиМногострочныеДанныеВТабличныйДокумент(ТекВетка);
		
		ДеревоДанныхРаздела = Неопределено;
		
		СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела = НовыеНомераСтрокМногоуровнегоРаздела;
		
	// Обычные разделы.
	//
	Иначе
		
		Для Каждого ЭлСтруктуры Из ЭтаФорма["СтруктураДанных" + ИмяТекРаздела] Цикл
			Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
			Если Обл <> Неопределено Тогда
				Обл.Значение = ЭлСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	УстановитьФорматВыводаНаСервере(ИмяТекРаздела);
	
	УстановитьДоступностьОбластей(ЭтаФорма, ИмяТекРаздела);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМногострочныеДанныеВТабличныйДокумент(Ветка, Координаты = "")
	
	ДанныеМнЧ = Ветка.ДанныеМногострочныхЧастей;
	
	Для каждого ЭлемСтруктуры Из ДанныеМнЧ Цикл
		
		ДеревоМнЧ = ЭлемСтруктуры.Значение;
		
		Для Инд_Число = 0 По ДеревоМнЧ.Строки.Количество() - 1 Цикл
			
			// Добавление необходимого количества доп. строк.
			Если Инд_Число > 0 Тогда
				Инд = СтрокаСИндексомПоказателя(Инд_Число);
				ИмяОбластиПреемника = "ДобавлениеСтроки" + ЭлемСтруктуры.Ключ + Координаты + Инд;
				ВставитьДопСтроку(ЭлемСтруктуры.Ключ, ИмяОбластиПреемника);
			КонецЕсли;
			
			// Обработка гиперссылок удаления строки, если эта строка единственная.
			Если ДеревоМнЧ.Строки.Количество() = 1 Тогда
				Инд = СтрокаСИндексомПоказателя(1);
				ИмяИскомойОбласти = "УдалитьСтроку" + ЭлемСтруктуры.Ключ + Координаты + Инд;
				Обл = ТабличныйДокумент.Области.Найти(ИмяИскомойОбласти);
				Если Обл <> Неопределено Тогда
					Обл.Гиперссылка = Ложь;
					Обл.Текст = "";
				КонецЕсли;
			КонецЕсли;
			
			// Вывод многострочных данных.
			Данные = ДеревоМнЧ.Строки[Инд_Число].Данные;
			Инд = СтрокаСИндексомПоказателя(Инд_Число + 1);
			Для Каждого ЭлСтруктуры Из Данные Цикл
				ИмяИскомойОбласти = ЭлСтруктуры.Ключ + Координаты + Инд;
				Обл = ТабличныйДокумент.Области.Найти(ИмяИскомойОбласти);
				Если Обл <> Неопределено Тогда
					Обл.Значение = ЭлСтруктуры.Значение;
				КонецЕсли;
			КонецЦикла;
			
			// Добавление необходимого количества подчиненных доп. строк.
			ПодчВетка = ДеревоМнЧ.Строки[Инд_Число];
			ПодчКоординаты = Координаты + Инд;
			ВывестиМногострочныеДанныеВТабличныйДокумент(ПодчВетка, ПодчКоординаты);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ДобавлениеУдалениеСтраницМногоуровневыхРазделов

&НаКлиенте
Процедура ДобавитьСтраницу(Команда)
	
	ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	Если ИмяРаздела = "Раздел2" Тогда
		ДобавитьСтраницуУровня0(ИмяРаздела);
	Иначе
		Возврат;
	КонецЕсли;
	
	Обл = ТабличныйДокумент.Области["УдалитьСтраницуЗначок"];
	Обл.Гиперссылка = Истина;
	Обл.Текст = "х";
	Обл = ТабличныйДокумент.Области["УдалитьСтраницу"];
	Обл.Гиперссылка = Истина;
	Обл.Текст = "Удалить страницу";
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтраницуУровня0(ИмяРаздела)
	
	ДеревоРаздела = ПолучитьИзВременногоХранилища(
		СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	
	// Нахождение строки в дереве значений, соответствующей текущей странице.
	ТекВетка = ДеревоРаздела.Строки[
		СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0] - 1];
	// Помещение данных текущей страницы в структуры найденной строки.
	ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяРаздела);
	ТекВетка.Данные = ДанныеТаблДокумента.Данные;
	ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
	
	// Добавление новой страницы.
	// (Добавляется строка в конец коллекции строк дерева значений.)
	НовВетка = ДеревоРаздела.Строки.Добавить();
	// Добавление разделов, подчиненных новой странице.
	ИнициализироватьПодчиненныеРазделы(ИмяРаздела, НовВетка);
	
	НомерНовВетки = ДеревоРаздела.Строки.Количество();
	
	// Вывод и помещение начальных данных новой страницы
	// в структуры вновь добавленной строки дерева значений.
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
	ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяРаздела));
	ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяРаздела);
	НовВетка.Данные = ДанныеТаблДокумента.Данные;
	НовВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
	
	// Сохранение изменений в дереве значений.
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]
		= ПоместитьВоВременноеХранилище(ДеревоРаздела, УникальныйИдентификатор);
	
	ДеревоРаздела = Неопределено;
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0] = НомерНовВетки;
	
	Для каждого ИскомыйРаздел Из РазделыОтчета.ПолучитьЭлементы() Цикл
		Если ИскомыйРаздел.КолонкаРазделыОтчетаСокрНаим = ИмяРаздела Тогда
			Для каждого НайденнаяСтраница Из ИскомыйРаздел.ПолучитьЭлементы() Цикл
				Если НайденнаяСтраница.КолонкаНомерСтраницыРазделаОтчета
					= СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0] Тогда
					Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтраница.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницу(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтраницуЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Удалить текущую страницу?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	Если ИмяРаздела = "Раздел2" Тогда
		УдалитьСтраницуУровня0(ИмяРаздела);
	Иначе
		Возврат;
	КонецЕсли;
	
	СпрятатьСсылкуУдалитьСтраницу();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтраницуУровня0(ИмяРаздела)
	
	ДеревоДанных = ПолучитьИзВременногоХранилища(
		СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	
	НомПоследнейСтраницы = ДеревоДанных.Строки.Количество();
	
	// Номер строки дерева значений, соответствующий номеру удаляемой страницы.
	НомУдаляемойСтраницы = СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0];
	
	// Номер строки дерева значений, соответствующий номеру выводимой после удаления страницы.
	НомСтраницыПослеУдаления = ?(НомУдаляемойСтраницы = НомПоследнейСтраницы,
		НомУдаляемойСтраницы - 1, НомУдаляемойСтраницы);
	
	// Удаление страницы.
	ДеревоДанных.Строки.Удалить(НомУдаляемойСтраницы - 1);
	
	// Сохранение изменений в дереве значений.
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]
		= ПоместитьВоВременноеХранилище(ДеревоДанных, УникальныйИдентификатор);
	
	ДеревоДанных = Неопределено;
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0] = НомСтраницыПослеУдаления;
	
	// Вывод данных строки дерева значений после удаления страницы.
	ВывестиДанныеВТабличныйДокумент(ИмяРаздела, СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела);
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	Для каждого ИскомыйРаздел Из РазделыОтчета.ПолучитьЭлементы() Цикл
		Если ИскомыйРаздел.КолонкаРазделыОтчетаСокрНаим = ИмяРаздела Тогда
			Для каждого НайденнаяСтраница Из ИскомыйРаздел.ПолучитьЭлементы() Цикл
				Если НайденнаяСтраница.КолонкаНомерСтраницыРазделаОтчета = НомСтраницыПослеУдаления Тогда
					Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтраница.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СпрятатьСсылкуУдалитьСтраницу()
	
	Если Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя() <> Неопределено Тогда
		ЭлементыРодителя = Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
		Если (ЭлементыРодителя.Количество() > 1)
			И (ЭлементыРодителя[0].КолонкаРазделыОтчетаСокрНаим = ЭлементыРодителя[1].КолонкаРазделыОтчетаСокрНаим) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОблУдалитьСтраницу = ТабличныйДокумент.Области.Найти("УдалитьСтраницуЗначок");
	Если ОблУдалитьСтраницу <> Неопределено Тогда
		ОблУдалитьСтраницу.Текст = "";
		ОблУдалитьСтраницу.Гиперссылка = Ложь;
	КонецЕсли;
	ОблУдалитьСтраницу = ТабличныйДокумент.Области.Найти("УдалитьСтраницу");
	Если ОблУдалитьСтраницу <> Неопределено Тогда
		ОблУдалитьСтраницу.Текст = "";
		ОблУдалитьСтраницу.Гиперссылка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДобавлениеУдалениеДополнительныхСтрокМногострочныхЧастейМногоуровневыхРазделов

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	ВставитьДополнительнуюСтроку();
	
КонецПроцедуры

&НаСервере
Процедура ВставитьДополнительнуюСтроку()
	
	ИмяТекОбласти = Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя;
	
	Если СтрНайти(ИмяТекОбласти, "ДобавитьСтроку") Тогда
		ИмяОбластиПреемника = СтрЗаменить(ИмяТекОбласти, "ДобавитьСтроку", "");
		ИмяОбластиПреемника = СтрЗаменить(ИмяОбластиПреемника, "Значок", "");
		ИмяГруппы = Лев(ИмяОбластиПреемника, СтруктураРеквизитовФормы.ДлинаИмениГруппы);
		ИмяОбластиПреемника = "ДобавлениеСтроки" + ИмяОбластиПреемника;
	Иначе
		ИмяГруппы = Лев(ИмяТекОбласти, СтруктураРеквизитовФормы.ДлинаИмениГруппы);
		ИмяГрафы = Лев(ИмяТекОбласти, СтруктураРеквизитовФормы.ДлинаИмениГрафы);
		МаксИнд = ИндексПоказателя(ИмяТекОбласти);
		Координаты = СтрокаСКоординатамиПоказателя(ИмяТекОбласти);
		Пока ТабличныйДокумент.Области.Найти(ИмяГрафы + Координаты + СтрокаСИндексомПоказателя(МаксИнд + 1)) <> Неопределено Цикл
			МаксИнд = МаксИнд + 1;
		КонецЦикла;
		ИмяОбластиПреемника = "ДобавлениеСтроки" + ИмяГруппы + Координаты + СтрокаСИндексомПоказателя(МаксИнд);
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей.Свойство(ИмяГруппы) Тогда
		ВставитьДопСтроку(ИмяГруппы, ИмяОбластиПреемника);
		СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВставитьДопСтроку(Знач ИмяГруппы, Знач ИмяОбластиПреемника, ЭтоАвтоВставкаПодчиненнойМнЧ = Ложь)
	
	НачИндекс = "_1";
	
	Если СтрНайти(ИмяОбластиПреемника, "ДобавлениеСтроки" + ИмяГруппы) > 0 Тогда
		
		ИскомаяОбласть = ТабличныйДокумент.Области.Найти("МногострочнаяЧасть" + ИмяГруппы);
		
		ВремТаблДок = ТабличныйДокумент.ПолучитьОбласть(ИскомаяОбласть.Имя);
		ВставляемаяОбласть = ВремТаблДок.Область(ИскомаяОбласть.Имя);
		ВставляемаяОбласть.Имя = "";
		
		НовКоординаты = СтрокаСКоординатамиПоказателя(ИмяОбластиПреемника);
		
		КолУровнейПодчиненности = СтрЧислоВхождений(НовКоординаты, "_");
		НачКоординаты = "";
		Для НомУр = 1 По КолУровнейПодчиненности Цикл
			НачКоординаты = НачКоординаты + НачИндекс;
		КонецЦикла;
		
		ПреИндекс_Число = ИндексПоказателя(ИмяОбластиПреемника);
		НовИндекс_Число = ?(ЭтоАвтоВставкаПодчиненнойМнЧ, 1, ПреИндекс_Число + 1);
		
		ПреИндекс = СтрокаСИндексомПоказателя(ПреИндекс_Число);
		НовИндекс = СтрокаСИндексомПоказателя(НовИндекс_Число);
		
		НачПостфикс = ИмяГруппы + НачКоординаты + НачИндекс;
		ПреПостфикс = ИмяГруппы + НовКоординаты + ПреИндекс;
		НовПостфикс = ИмяГруппы + НовКоординаты + НовИндекс;
		
		МассивИменГраф = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"];
		
		Для каждого ИмяГрафы Из МассивИменГраф Цикл
			
			НачИмяОбл = ИмяГрафы + НачКоординаты + НачИндекс;
			НовИмяОбл = ИмяГрафы + НовКоординаты + НовИндекс;
			
			ОбластьЯчейки = ВремТаблДок.Области[НачИмяОбл];
			ОбластьЯчейки.Имя = НовИмяОбл;
			
			Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
				ОбластьЯчейки.Очистить();
			КонецЕсли;
			
		КонецЦикла;
		
		Обл = ВремТаблДок.Области.Найти("УдалитьСтроку" + НачПостфикс);
		Если Обл <> Неопределено Тогда
			Обл.Имя = "УдалитьСтроку" + НовПостфикс;
		КонецЕсли;
		
		Если ЭтоАвтоВставкаПодчиненнойМнЧ Тогда
			Обл = ВремТаблДок.Области.Найти("УдалитьСтроку" + НовПостфикс);
			Если Обл <> Неопределено Тогда
				Обл.Гиперссылка = Ложь;
				Обл.Текст = "";
			КонецЕсли;
		Иначе
			Обл = ВремТаблДок.Области.Найти("УдалитьСтроку" + НовПостфикс);
			Если Обл <> Неопределено Тогда
				Обл.Гиперссылка = Истина;
				Обл.Текст = "х";
				Если НовИндекс_Число = 2 Тогда
					Если Обл <> Неопределено Тогда
						Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + ПреПостфикс);
						Обл.Гиперссылка = Истина;
						Обл.Текст = "х";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтроку" + ПреПостфикс);
		Если Обл <> Неопределено Тогда
			Обл.Имя = "ДобавитьСтроку" + НовПостфикс;
		КонецЕсли;
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтрокуЗначок" + ПреПостфикс);
		Если Обл <> Неопределено Тогда
			Обл.Имя = "ДобавитьСтрокуЗначок" + НовПостфикс;
		КонецЕсли;
		
		ОблПреемник = ТабличныйДокумент.Области.Найти(ИмяОбластиПреемника);
		ОблПреемник.Имя = "ДобавлениеСтроки" + НовПостфикс;
		
		// Изменения в оформлении ячеек: установка значения "НетЛинии"/"Сплошная" для
		// свойства "ГраницаСнизу" (в случае задания номеров специальных колонок):
		НомераСпецКолонок = Неопределено;
		СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы].Свойство(
			"КолонкиСИзмНижнГраницей", НомераСпецКолонок);
		Если НомераСпецКолонок <> Неопределено Тогда
			Для каждого НомКолонки Из НомераСпецКолонок Цикл
				// Удаление линии в определенных колонках последней (до добавления) строки.
				Если НЕ ЭтоАвтоВставкаПодчиненнойМнЧ Тогда
					Обл = ТабличныйДокумент.Область(
						ОблПреемник.Верх - 1, НомКолонки, ОблПреемник.Верх - 1, НомКолонки);
					Если Обл <> Неопределено Тогда
						Обл.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
					КонецЕсли;
				КонецЕсли;
				// Установка линии в определенных колонках добавляемой строки.
				Обл = ВремТаблДок.Область(
					ВставляемаяОбласть.Низ, НомКолонки, ВставляемаяОбласть.Низ, НомКолонки);
				Если Обл <> Неопределено Тогда
					Обл.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ОблПреемник_ = ТабличныйДокумент.Область(
			ОблПреемник.Верх, , ОблПреемник.Верх + ИскомаяОбласть.Низ - ИскомаяОбласть.Верх, );
		ТабличныйДокумент.ВставитьОбласть(
			ВставляемаяОбласть, ОблПреемник_, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		Если НЕ ЭтоАвтоВставкаПодчиненнойМнЧ Тогда
			// Установка текущей области после добавления строки.
			НовИмяТекОбл = МассивИменГраф[0] + НовКоординаты + НовИндекс;
			НовТекОбл = ТабличныйДокумент.Области[НовИмяТекОбл];
			Элементы.ТабличныйДокумент.ТекущаяОбласть = НовТекОбл;
		КонецЕсли;
		
	КонецЕсли;
	
	ИменаПодчиненныхГрупп = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[
		ИмяГруппы]["ИменаПодчиненныхГрупп"];
	Для каждого ИмяПодчГруппы Из ИменаПодчиненныхГрупп Цикл
		НовКоординатыДляПодчОбласти = НовКоординаты + НовИндекс;
		
		КолУровнейПодчиненности = СтрЧислоВхождений(НовКоординатыДляПодчОбласти, "_");
		НачКоординатыДляПодчОбласти = "";
		Для НомУр = 1 По КолУровнейПодчиненности Цикл
			НачКоординатыДляПодчОбласти = НачКоординатыДляПодчОбласти + НачИндекс;
		КонецЦикла;
		
		НачИндВставляемойОбласти = НачИндекс;
		
		ИскомаяОбласть = ТабличныйДокумент.Области.Найти(
			"ДобавлениеСтроки" + ИмяПодчГруппы + НачКоординатыДляПодчОбласти + НачИндВставляемойОбласти);
		
		Если ИскомаяОбласть = Неопределено Тогда
			НачИндВставляемойОбласти = 1;
			Пока ТабличныйДокумент.Области.Найти("ДобавлениеСтроки" + ИмяПодчГруппы
				+ НачКоординатыДляПодчОбласти + "_" + Формат(НачИндВставляемойОбласти, "ЧГ=")) = Неопределено Цикл
				НачИндВставляемойОбласти = НачИндВставляемойОбласти + 1;
			КонецЦикла;
			НачИндВставляемойОбласти = "_" + Формат(НачИндВставляемойОбласти, "ЧГ=");
			ИскомаяОбласть = ТабличныйДокумент.Области.Найти(
				"ДобавлениеСтроки" + ИмяПодчГруппы + НачКоординатыДляПодчОбласти + НачИндВставляемойОбласти);
		КонецЕсли;
		
		ВремТабличныйДокумент = ТабличныйДокумент.ПолучитьОбласть(ИскомаяОбласть.Имя);
		ВставляемаяОбласть = ВремТабличныйДокумент.Область(ИскомаяОбласть.Имя);
		Для каждого ОбластьЯчейки Из ВремТабличныйДокумент.Области Цикл
			ОбластьЯчейки.Имя = СтрЗаменить(ОбластьЯчейки.Имя,
				ИмяПодчГруппы + НачКоординатыДляПодчОбласти + НачИндВставляемойОбласти,
				ИмяПодчГруппы + НовКоординатыДляПодчОбласти + НачИндекс);
		КонецЦикла;
		ИмяОбластиПреемникаДляПодчОбласти = СтрЗаменить(ИмяОбластиПреемника, ПреПостфикс, НовПостфикс);
		ОбластьПреемник = ТабличныйДокумент.Области.Найти(ИмяОбластиПреемникаДляПодчОбласти);
		
		ТабличныйДокумент.ВставитьОбласть(ВставляемаяОбласть, ОбластьПреемник, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		ИмяОбластиПреемникаДляПодчОбласти = "ДобавлениеСтроки" + ИмяПодчГруппы + НовКоординатыДляПодчОбласти + НачИндекс;
		
		ВставитьДопСтроку(ИмяПодчГруппы, ИмяОбластиПреемникаДляПодчОбласти, Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	ИмяТекОбласти = Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя;
	ИмяТекОбласти = СтрЗаменить(ИмяТекОбласти, "УдалитьСтроку", "");
	
	ИмяГруппы = Лев(ИмяТекОбласти, СтруктураРеквизитовФормы.ДлинаИмениГруппы);
	
	Если НЕ СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей.Свойство(ИмяГруппы) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не выбрана удаляемая строка многострочного раздела.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтрокуЗавершение", ЭтотОбъект);
	ДопТекст = ДополнительныйТекстПриДобавленииУдаленииСтрок(ИмяГруппы);
	Если ДопТекст <> Неопределено Тогда
		ТекстВопроса = ДопТекст.ТекстВопросаПередУдалениемСтроки;
	Иначе
		ТекстВопроса = НСтр("ru='Удалить выбранную строку?'");
	КонецЕсли;
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьДопСтрокуНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДопСтрокуНаСервере()
	
	ИмяТекОбласти = Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя;
	
	УдалитьДопСтроку(ИмяТекОбласти);
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	Расчет(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДопСтроку(ИмяОбласти)
	
	ИмяГруппы = СтрЗаменить(ИмяОбласти, "УдалитьСтроку", "");
	ИмяГруппы = Лев(ИмяГруппы, СтруктураРеквизитовФормы.ДлинаИмениГруппы);
	
	ТекКоординаты = СтрокаСКоординатамиПоказателя(ИмяОбласти);
	
	ТекИндекс_Число = ИндексПоказателя(ИмяОбласти);
	ТекИндекс = СтрокаСИндексомПоказателя(ТекИндекс_Число);
	ТекПостфикс = ИмяГруппы + ТекКоординаты + ТекИндекс;
	
	МассивИменГраф = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"];
	ИмяПервойГрафы = МассивИменГраф[0];
	
	ОблПервУдаляемогоПоказателя = ТабличныйДокумент.Область(ИмяПервойГрафы + ТекКоординаты + ТекИндекс);
	ОблУдалитьСтроку = ТабличныйДокумент.Область("УдалитьСтроку" + ИмяГруппы + ТекКоординаты + ТекИндекс);
	СмещениеОтВерхаМнЧ = ОблПервУдаляемогоПоказателя.Верх - ОблУдалитьСтроку.Верх;
	
	ОблДобавлениеСтроки = ТабличныйДокумент.Области.Найти("ДобавлениеСтроки" + ТекПостфикс);
	
	// Удаляется последняя строка.
	//
	Если ОблДобавлениеСтроки <> Неопределено Тогда
		
		ПреИндекс_Число = ТекИндекс_Число - 1;
		ПреИндекс = СтрокаСИндексомПоказателя(ПреИндекс_Число);
		ПреПостфикс = ИмяГруппы + ТекКоординаты + ПреИндекс;
		
		ВерхУдОбласти = ОблПервУдаляемогоПоказателя.Верх - СмещениеОтВерхаМнЧ;
		НизУдОбласти  = ОблДобавлениеСтроки.Верх - 1;
		
		УдаляемаяОбласть = ТабличныйДокумент.Область(ВерхУдОбласти, , НизУдОбласти, );
		ТабличныйДокумент.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		Обл = ТабличныйДокумент.Области.Найти("ДобавлениеСтроки" + ТекПостфикс);
		Обл.Имя = "ДобавлениеСтроки" + ПреПостфикс;
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтроку" + ТекПостфикс);
		Обл.Имя = "ДобавитьСтроку" + ПреПостфикс;
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтрокуЗначок" + ТекПостфикс);
		Обл.Имя = "ДобавитьСтрокуЗначок" + ПреПостфикс;
		
		Если ПреИндекс_Число = 1 Тогда
			Обл = ТабличныйДокумент.Область("УдалитьСтроку" + ПреПостфикс);
			Обл.Гиперссылка = Ложь;
			Обл.Текст = "";
		КонецЕсли;
		
		// Изменения в оформлении ячеек в случае задания номеров специальных колонок.
		НомераСпецКолонок = Неопределено;
		СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы].Свойство(
			"КолонкиСИзмНижнГраницей", НомераСпецКолонок);
		Если НомераСпецКолонок <> Неопределено Тогда
			Для каждого НомКолонки Из НомераСпецКолонок Цикл
				// Установка линии в определенных колонках последней (после удаления) строки.
				Обл = ТабличныйДокумент.Область(
					ВерхУдОбласти - 1, НомКолонки, ВерхУдОбласти - 1, НомКолонки);
				Если Обл <> Неопределено Тогда
					Обл.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		НовИмяТекОбл = ИмяПервойГрафы + ТекКоординаты + ПреИндекс;
		Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области[НовИмяТекОбл];
		
	// Удаляемая строка не последняя.
	//
	Иначе
		
		НовИндекс_Число = ТекИндекс_Число + 1;
		НовИндекс = СтрокаСИндексомПоказателя(НовИндекс_Число);
		
		ОблСледующегоПоказателя = ТабличныйДокумент.Область(ИмяПервойГрафы + ТекКоординаты + НовИндекс);
		
		ВерхУдОбласти = ОблПервУдаляемогоПоказателя.Верх - СмещениеОтВерхаМнЧ;
		НизУдОбласти  = ОблСледующегоПоказателя.Верх - СмещениеОтВерхаМнЧ - 1;
		
		УдаляемаяОбласть = ТабличныйДокумент.Область(ВерхУдОбласти, , НизУдОбласти, );
		
		// Сохранение свойств "эталонных" областей для последующего восстановления.
		СпОбластейДляВосстановленияПослеУдаления = Новый СписокЗначений;
		ВремТаблДок = ТабличныйДокумент.ПолучитьОбласть(УдаляемаяОбласть.Имя);
		Для каждого ЭлемСтруктуры Из СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей Цикл
			ИмяИскомойобласти = "МногострочнаяЧасть" + ЭлемСтруктуры.Ключ;
			Искомаяобласть = ВремТаблДок.Области.Найти(ИмяИскомойобласти);
			Если Искомаяобласть <> Неопределено Тогда
				ВысотаОбл = Искомаяобласть.Низ - Искомаяобласть.Верх + 1;
				СпОбластейДляВосстановленияПослеУдаления.Добавить(ВысотаОбл, ЭлемСтруктуры.Ключ);
			КонецЕсли;
		КонецЦикла;
		
		ТабличныйДокумент.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		НовИндекс_Число = ТекИндекс_Число;
		
		// Реиндексация имен областей текущей многострочной части после удаления строки.
		Пока ТабличныйДокумент.Области.Найти(ИмяПервойГрафы + ТекКоординаты
			+ СтрокаСИндексомПоказателя(НовИндекс_Число + 1)) <> Неопределено Цикл
			
			ПреИндекс = СтрокаСИндексомПоказателя(НовИндекс_Число + 1);
			НовИндекс = СтрокаСИндексомПоказателя(НовИндекс_Число);
			
			Для каждого ИмяГрафы Из МассивИменГраф Цикл
				ПреИмяОбл = ИмяГрафы + ТекКоординаты + ПреИндекс;
				НовИмяОбл = ИмяГрафы + ТекКоординаты + НовИндекс;
				
				ОбластьЯчейки = ТабличныйДокумент.Области[ПреИмяОбл];
				ОбластьЯчейки.Имя = НовИмяОбл;
			КонецЦикла;
			
			ПреПостфикс = ИмяГруппы + ТекКоординаты + ПреИндекс;
			НовПостфикс = ИмяГруппы + ТекКоординаты + НовИндекс;
			
			Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + ПреПостфикс);
			Обл.Имя = "УдалитьСтроку" + НовПостфикс;
			
			// Обновление координат в именах областей подчиненных многострочных частей.
			ИменаПодчиненныхГрупп = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[
				ИмяГруппы]["ИменаПодчиненныхГрупп"];
			Для каждого ИмяПодчГруппы Из ИменаПодчиненныхГрупп Цикл
				ПарамРеинд = Новый Структура("ИмяГруппы,ПреКоординаты,НовКоординаты",
					ИмяПодчГруппы, ТекКоординаты + ПреИндекс, ТекКоординаты + НовИндекс);
				РеиндексироватьПодчиненныеДопСтроки(ПарамРеинд);
			КонецЦикла;
			
			НовИндекс_Число = НовИндекс_Число + 1;
			
		КонецЦикла;
		
		ПреИндекс = СтрокаСИндексомПоказателя(НовИндекс_Число);
		НовИндекс = СтрокаСИндексомПоказателя(НовИндекс_Число - 1);
		
		ПреПостфикс = ИмяГруппы + ТекКоординаты + ПреИндекс;
		НовПостфикс = ИмяГруппы + ТекКоординаты + НовИндекс;
		
		Обл = ТабличныйДокумент.Области.Найти("ДобавлениеСтроки" + ПреПостфикс);
		Обл.Имя = "ДобавлениеСтроки" + НовПостфикс;
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтроку" + ПреПостфикс);
		Обл.Имя = "ДобавитьСтроку" + НовПостфикс;
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтрокуЗначок" + ПреПостфикс);
		Обл.Имя = "ДобавитьСтрокуЗначок" + НовПостфикс;
		
		// Восстановление удаленных "эталонных" областей по предварительно сохраненным свойствам.
		НачИндекс = "_1";
		Для каждого ЭлемСписка Из СпОбластейДляВосстановленияПослеУдаления Цикл
			НачКоординаты = "";
			УровеньПодчиненияГруппы = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[
				ЭлемСписка.Представление]["УровеньПодчинения"];
			
			Для НомУр = 1 По УровеньПодчиненияГруппы Цикл
				НачКоординаты = НачКоординаты + НачИндекс;
			КонецЦикла;
			
			ИмяГрафыДляПозиционирования = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[
				ЭлемСписка.Представление]["ИменаГраф"][0];
			
			ИмяОбластиДляПозиционирования = ИмяГрафыДляПозиционирования + НачКоординаты + НачИндекс;
			ОбластьДляПозиционирования = ТабличныйДокумент.Область(ИмяОбластиДляПозиционирования);
			
			Если ЭлемСписка.Представление = ИмяГруппы Тогда
				ВосстОбластьВерх = ОбластьДляПозиционирования.Верх - СмещениеОтВерхаМнЧ;
			Иначе
				ВосстОбластьВерх = ОбластьДляПозиционирования.Верх;
			КонецЕсли;
			ВосстОбластьНиз  = ВосстОбластьВерх + ЭлемСписка.Значение - 1;
			
			ВосстОбласть = ТабличныйДокумент.Область(ВосстОбластьВерх, , ВосстОбластьНиз, );
			ВосстОбласть.Имя = "МногострочнаяЧасть" + ЭлемСписка.Представление;
		КонецЦикла;
		
		Если НовИндекс = НачИндекс Тогда // после удаления осталась единственная строка
			Обл = ТабличныйДокумент.Область("УдалитьСтроку" + НовПостфикс);
			Обл.Гиперссылка = Ложь;
			Обл.Текст = "";
		КонецЕсли;
		
		НовИмяТекОбл = ИмяПервойГрафы + ТекКоординаты + ТекИндекс;
		Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области[НовИмяТекОбл];
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РеиндексироватьПодчиненныеДопСтроки(ПараметрыРеиндексации)
	
	ИмяГруппы = ПараметрыРеиндексации["ИмяГруппы"];
	ПреКоординаты = ПараметрыРеиндексации["ПреКоординаты"];
	НовКоординаты = ПараметрыРеиндексации["НовКоординаты"];
	
	МассивИменГраф = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"];
	ИмяПервойГрафы = МассивИменГраф[0];
	
	Инд_Число = 1;
	Пока ТабличныйДокумент.Области.Найти(ИмяПервойГрафы
		+ ПреКоординаты + СтрокаСИндексомПоказателя(Инд_Число)) <> Неопределено Цикл
		
		Инд = СтрокаСИндексомПоказателя(Инд_Число);
		
		Для каждого ИмяГрафы Из МассивИменГраф Цикл
			ПреИмяОбл = ИмяГрафы + ПреКоординаты + Инд;
			НовИмяОбл = ИмяГрафы + НовКоординаты + Инд;
			
			ОбластьЯчейки = ТабличныйДокумент.Области[ПреИмяОбл];
			ОбластьЯчейки.Имя = НовИмяОбл;
		КонецЦикла;
		
		ПреПостфикс = ИмяГруппы + ПреКоординаты + Инд;
		НовПостфикс = ИмяГруппы + НовКоординаты + Инд;
		
		Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + ПреПостфикс);
		Обл.Имя = "УдалитьСтроку" + НовПостфикс;
		
		ИменаПодчиненныхГрупп = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаПодчиненныхГрупп"];
		Для каждого ИмяПодчГруппы Из ИменаПодчиненныхГрупп Цикл
			ПарамРеинд = Новый Структура("ИмяГруппы,ПреКоординаты,НовКоординаты",
				ИмяПодчГруппы, ПреКоординаты + Инд, НовКоординаты + Инд);
			РеиндексироватьПодчиненныеДопСтроки(ПарамРеинд);
		КонецЦикла;
		
		Инд_Число = Инд_Число + 1;
		
	КонецЦикла;
	
	Инд = СтрокаСИндексомПоказателя(Инд_Число - 1);
	
	ПреПостфикс = ИмяГруппы + ПреКоординаты + Инд;
	НовПостфикс = ИмяГруппы + НовКоординаты + Инд;
	
	Обл = ТабличныйДокумент.Области.Найти("ДобавлениеСтроки" + ПреПостфикс);
	Обл.Имя = "ДобавлениеСтроки" + НовПостфикс;
	Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтроку" + ПреПостфикс);
	Обл.Имя = "ДобавитьСтроку" + НовПостфикс;
	Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтрокуЗначок" + ПреПостфикс);
	Обл.Имя = "ДобавитьСтрокуЗначок" + НовПостфикс;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИндексПоказателя(Знач ИмяОбласти)
	
	МассивСтрок = СтрРазделить(ИмяОбласти, "_");
	
	КолСтрок = МассивСтрок.Количество();
	
	Если КолСтрок = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИндексСтрока = МассивСтрок[КолСтрок - 1];
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ИндексСтрока) Тогда
		Возврат Число(ИндексСтрока);
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаСИндексомПоказателя(Знач Индекс)
	
	Если ТипЗнч(Индекс) <> Тип("Число") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат "_" + Формат(Индекс, "ЧГ=");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаСКоординатамиПоказателя(Знач Координаты)
	
	СтрокаСКоординатами = "";
	
	Если ТипЗнч(Координаты) = Тип("Массив") Тогда
		Для каждого КоординатаЧисло Из Координаты Цикл
			Если ТипЗнч(КоординатаЧисло) <> Тип("Число") Тогда
				Возврат Неопределено;
			КонецЕсли;
			СтрокаСКоординатами = СтрокаСКоординатами + "_" + Формат(КоординатаЧисло, "ЧГ=");
		КонецЦикла;
	ИначеЕсли ТипЗнч(Координаты) = Тип("Строка") Тогда
		МассивСтрок = СтрРазделить(Координаты, "_");
		КолСтрок = МассивСтрок.Количество();
		Если КолСтрок = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		МассивСтрок.Удалить(КолСтрок - 1);
		МассивСтрок.Удалить(0);
		Для каждого КоординатаСтрока Из МассивСтрок Цикл
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КоординатаСтрока) Тогда
				СтрокаСКоординатами = СтрокаСКоординатами + "_" + Формат(КоординатаСтрока, "ЧГ=");
			Иначе
				Возврат Неопределено;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтрокаСКоординатами;
	
КонецФункции

#КонецОбласти

#Область Авторасчет

&НаКлиенте
Процедура РасчетНаКлиенте(ИмяТекущейСтраницыПанели = "", ОбластьИмя = "") Экспорт
	
	Расчет(ЭтаФорма, ИмяТекущейСтраницыПанели, ОбластьИмя);
	
КонецПроцедуры

&НаСервере
Процедура РасчетНаСервере(ИмяТекущейСтраницыПанели = "", ОбластьИмя = "") Экспорт
	
	Расчет(ЭтаФорма, ИмяТекущейСтраницыПанели, ОбластьИмя);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура Расчет(Форма, ИмяРаздела = "", ОбластьИмя = "") Экспорт
	
	Перем ТаблицаБлока;
	
	Если Форма.СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяРаздела = "Раздел1" Тогда
		
		Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела Тогда
			
			Если ЗначениеЗаполнено(ОбластьИмя) Тогда
				
				СтруктЗависимыхЯчеек = Форма.СтруктураЗависимыхЯчеекДляАвторасчета[ИмяРаздела];
				
				ОбластьИмяБезИнд = Лев(ОбластьИмя, Форма.СтруктураРеквизитовФормы.ДлинаИмениГрафы);
				_Инд = Сред(ОбластьИмя, Форма.СтруктураРеквизитовФормы.ДлинаИмениГрафы + 1);
				
				// Стр. 030, 040.
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000010003003, ОбластьИмяБезИнд) > 0
					ИЛИ СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000010004003, ОбластьИмяБезИнд) > 0 Тогда
					
					СуммаНалога
					= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма,
					Форма.ТабличныйДокумент.Области.Найти("П000010002103" + _Инд).Значение)
					- РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма,
					Форма.ТабличныйДокумент.Области.Найти("П000010002303" + _Инд).Значение)
					- РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма,
					Форма.ТабличныйДокумент.Области.Найти("П000010002503" + _Инд).Значение)
					- РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма,
					Форма.ТабличныйДокумент.Области.Найти("П000010002703" + _Инд).Значение);
					
					Форма.ТабличныйДокумент.Области.Найти("П000010003003" + _Инд).Значение = Макс(СуммаНалога, 0);
					Форма.ТабличныйДокумент.Области.Найти("П000010004003" + _Инд).Значение = -Мин(СуммаНалога, 0);
					
				КонецЕсли;
				
			ИначеЕсли НЕ ЗначениеЗаполнено(ОбластьИмя) Тогда
				РасчетВДеревеЗначений(Форма, ИмяРаздела);
			КонецЕсли;
		Иначе
			РасчетВДеревеЗначений(Форма, ИмяРаздела);
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Раздел2" Тогда
		
		Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела Тогда
			
			Если ЗначениеЗаполнено(ОбластьИмя) Тогда
				
				СтруктЗависимыхЯчеек = Форма.СтруктураЗависимыхЯчеекДляАвторасчета[ИмяРаздела];
				
				// Стр. 170.
				//
				Стр170 = 0;
				Для Каждого ТекОбласть Из Форма.ТабличныйДокумент.Области Цикл
					Если СтруктЗависимыхЯчеек.Свойство(ТекОбласть.Имя) Тогда
						Продолжить;
					КонецЕсли;
					Если СтрЧислоВхождений(Тип(ТекОбласть), "Рисунок") = 0
						И ТекОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
						И ТекОбласть.СодержитЗначение = Истина Тогда
						Если ЗначениеЗаполнено(ТекОбласть.Значение) Тогда
							Стр170 = 1;
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Стр130 = Форма.ТабличныйДокумент.Области["П000020013003"].Значение;
				Стр160 = Форма.ТабличныйДокумент.Области["П000020016003"].Значение;
				Если Стр160 > 0 Тогда
					Стр170 = Окр(Макс(Стр130 - Стр160, 0) / Стр130, 4);
				КонецЕсли;
				
				Форма.ТабличныйДокумент.Области["П000020017003"].Значение = Стр170;
				
				// Стр. 110.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020011003, ОбластьИмя) > 0 Тогда
					
					Стр050 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020005003"].Значение);
					
					Строка060_Числ = 1;
					Строка060_Знам = 1;
					Если ЗначениеЗаполнено(Форма.ТабличныйДокумент.Области["П000020006003_Числ"].Значение)
						И ЗначениеЗаполнено(Форма.ТабличныйДокумент.Области["П000020006003_Знам"].Значение) Тогда
						Строка060_Числ = Форма.ТабличныйДокумент.Области["П000020006003_Числ"].Значение;
						Строка060_Знам = Форма.ТабличныйДокумент.Области["П000020006003_Знам"].Значение;
					КонецЕсли;
					
					Стр080 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020008003"].Значение);
					
					ЗаполненаСтр100 = Ложь;
					Строка100_Числ = 1;
					Строка100_Знам = 1;
					Если ЗначениеЗаполнено(Форма.ТабличныйДокумент.Области["П000020010003_Числ"].Значение)
						И ЗначениеЗаполнено(Форма.ТабличныйДокумент.Области["П000020010003_Знам"].Значение) Тогда
						Строка100_Числ = Форма.ТабличныйДокумент.Области["П000020010003_Числ"].Значение;
						Строка100_Знам = Форма.ТабличныйДокумент.Области["П000020010003_Знам"].Значение;
						ЗаполненаСтр100 = Истина;
					КонецЕсли;
					
					Стр170 = Форма.ТабличныйДокумент.Области["П000020017003"].Значение;
					
					ОсноваДляРасчетаСтр110 = Стр050 * Строка060_Числ / Строка060_Знам;
					
					ОсноваДляРасчетаСтр110_Окр
					= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, ОсноваДляРасчетаСтр110);
					
					Стр110 = ОсноваДляРасчетаСтр110;
					Если Стр080 > 0 Тогда
						Стр110 = Макс(ОсноваДляРасчетаСтр110_Окр - Стр080, 0);
					ИначеЕсли ЗаполненаСтр100 Тогда
						Стр110 = Макс(ОсноваДляРасчетаСтр110_Окр - РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
						Форма, ОсноваДляРасчетаСтр110 * (Строка100_Числ * (1 - Стр170) / Строка100_Знам)), 0);
					КонецЕсли;
					
					Форма.ТабличныйДокумент.Области["П000020011003"].Значение
					= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, Стр110);
					
				КонецЕсли;
				
				// Стр. 140.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020014003, ОбластьИмя) > 0 Тогда
					
					Форма.ТабличныйДокумент.Области["П000020014003"].Значение = Окр(Стр130 / 12, 4);
					
				КонецЕсли;
				
				// Стр. 150.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020015003, ОбластьИмя) > 0 Тогда
					
					Стр040 = СокрЛП(Форма.ТабличныйДокумент.Области["П000020004003"].Значение);
					
					Стр110 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020011003"].Значение);
					
					Стр120 = Форма.ТабличныйДокумент.Области["П000020012003"].Значение;
					
					Стр140 = Форма.ТабличныйДокумент.Области["П000020014003"].Значение;
					
					Стр145 = Форма.ТабличныйДокумент.Области["П000020014503"].Значение;
					Стр145 = ?(Стр145 = 0, 1, Стр145);
					
					ОсноваДляРасчетаСтр150 = Стр110 * Стр120 * Стр140 * Стр145 / 100;
					
					Если Стр040 = "1" Тогда
						Стр150 = ОсноваДляРасчетаСтр150 * 2;
					ИначеЕсли Стр040 = "2" Тогда
						Стр150 = ОсноваДляРасчетаСтр150 * 4;
					Иначе
						Стр150 = ОсноваДляРасчетаСтр150;
					КонецЕсли;
					
					Форма.ТабличныйДокумент.Области["П000020015003"].Значение
					= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, Стр150);
					
				КонецЕсли;
				
				// Стр. 190.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020019003, ОбластьИмя) > 0 Тогда
					
					Стр190 = 0;
					
					КодЛьготы = СокрЛП(Форма.ТабличныйДокумент.Области["П000020018003_КодЛьготы"].Значение);
					
					Если ЗначениеЗаполнено(КодЛьготы) Тогда
						Стр150 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
						Форма, Форма.ТабличныйДокумент.Области["П000020015003"].Значение);
						Стр170 = Форма.ТабличныйДокумент.Области["П000020017003"].Значение;
						Стр190 = Стр150 * (1 - Стр170);
					КонецЕсли;
					
					Форма.ТабличныйДокумент.Области["П000020019003"].Значение
					= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, Стр190);
					
				КонецЕсли;
				
				// Стр. 210.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020021003, ОбластьИмя) > 0 Тогда
					
					Стр210 = 0;
					
					КодЛьготы = СокрЛП(Форма.ТабличныйДокумент.Области["П000020020003"].Значение);
					
					Если ЗначениеЗаполнено(КодЛьготы) Тогда
						Стр150 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
						Форма, Форма.ТабличныйДокумент.Области["П000020015003"].Значение);
						Стр170 = Форма.ТабличныйДокумент.Области["П000020017003"].Значение;
						Стр210 = Стр150 * (1 - Стр170);
					КонецЕсли;
					
					Форма.ТабличныйДокумент.Области["П000020021003"].Значение
					= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, Стр210);
					
				КонецЕсли;
				
				// Стр. 230.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020023003, ОбластьИмя) > 0 Тогда
					
					КодЛьготы = СокрЛП(Форма.ТабличныйДокумент.Области["П000020022003_КодЛьготы"].Значение);
					Стр220_Процент = Форма.ТабличныйДокумент.Области["П000020022003_Процент"].Значение;
					
					Если ЗначениеЗаполнено(КодЛьготы) Тогда
						
						Если Стр220_Процент > 0 Тогда
							
							Стр150 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
							Форма, Форма.ТабличныйДокумент.Области["П000020015003"].Значение);
							
							Стр230 = Стр150 * Стр220_Процент / 100;
							
							Форма.ТабличныйДокумент.Области["П000020023003"].Значение
							= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, Стр230);
							
						Иначе
							// Для ячейки с показателем стр. 230 установлен режим ввода значения вручную.
						КонецЕсли;
						
					Иначе
						
						Форма.ТабличныйДокумент.Области["П000020023003"].Значение = 0;
						
					КонецЕсли;
					
				КонецЕсли;
				
				// Стр. 250.
				//
				Если СтрЧислоВхождений(СтруктЗависимыхЯчеек.П000020025003, ОбластьИмя) > 0 Тогда
					
					Стр110 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020011003"].Значение);
					
					Стр120 = Форма.ТабличныйДокумент.Области["П000020012003"].Значение;
					
					Стр140 = Форма.ТабличныйДокумент.Области["П000020014003"].Значение;
					
					Стр150 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020015003"].Значение);
					
					Стр190 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020019003"].Значение);
					
					Стр210 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020021003"].Значение);
					
					Стр230 = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
					Форма, Форма.ТабличныйДокумент.Области["П000020023003"].Значение);
					
					Стр240 = СокрЛП(Форма.ТабличныйДокумент.Области["П000020024003_КодЛьготы"].Значение);
					
					Стр250 = Стр150;
					Если Стр190 > 0 Тогда
						Стр250 = Стр150 - Стр190;
					ИначеЕсли Стр210 > 0 Тогда
						Стр250 = Стр150 - Стр210;
					ИначеЕсли Стр230 > 0 Тогда
						Стр250 = Стр150 - Стр230;
					ИначеЕсли ЗначениеЗаполнено(Стр240) Тогда
						Стр250 = Стр110 * Стр120 * Стр140 / 100;
					КонецЕсли;
					
					Форма.ТабличныйДокумент.Области["П000020025003"].Значение = Макс(Стр250, 0);
					
				КонецЕсли;
				
			ИначеЕсли НЕ ЗначениеЗаполнено(ОбластьИмя) Тогда
				РасчетВДеревеЗначений(Форма, ИмяРаздела);
			КонецЕсли;
			
		Иначе
			РасчетВДеревеЗначений(Форма, ИмяРаздела);
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "" Тогда
		
		Для каждого Раздел Из Форма.СтруктураЗависимыхЯчеекДляАвторасчета Цикл
			Расчет(Форма, Раздел.Ключ);
		КонецЦикла;
		
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РасчетВДеревеЗначений(Форма, ИмяРаздела)
	
	ИмяГлРаздела = ИмяРаздела;
	СтруктураРаздела = Форма.мСтруктураМногоуровневыхРазделов[ИмяРаздела];
	Если СтруктураРаздела.Свойство("ИмяГлавногоРаздела") Тогда
		ИмяГлРаздела = СтруктураРаздела["ИмяГлавногоРаздела"];
	КонецЕсли;
	
	ДеревоДанных = Неопределено;
	
	Если ИмяРаздела = "Раздел1" Тогда
		
		ДеревоДанных = ПолучитьИзВременногоХранилища(
		Форма.СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]);
		
		ДанныеМнЧР1 = ДеревоДанных.Строки[0].ДанныеМногострочныхЧастей[Форма.СтруктураРеквизитовФормы.мИдГруппы1];
		
		Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела Тогда
			
			Для каждого СтрокаМчЧ Из ДанныеМнЧР1.Строки Цикл
				
				_НомСтрокиМнЧ = "_" + Формат(ДанныеМнЧР1.Строки.Индекс(СтрокаМчЧ) + 1, "ЧГ=");
				Для каждого Пок Из СтрокаМчЧ.Данные Цикл
					СтрокаМчЧ.Данные.Вставить(Пок.Ключ, Форма.ТабличныйДокумент.Области[Пок.Ключ + _НомСтрокиМнЧ].Значение);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Для каждого СтрокаМчЧ Из ДанныеМнЧР1.Строки Цикл
			
			// Стр. 030, 040.
			//
			СуммаНалога
			= РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, СтрокаМчЧ.Данные["П000010002103"])
			- РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, СтрокаМчЧ.Данные["П000010002303"])
			- РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, СтрокаМчЧ.Данные["П000010002503"])
			- РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(Форма, СтрокаМчЧ.Данные["П000010002703"]);
			
			СтрокаМчЧ.Данные.Вставить("П000010003003", Макс(СуммаНалога, 0));
			СтрокаМчЧ.Данные.Вставить("П000010004003", -Мин(СуммаНалога, 0));
			
		КонецЦикла;
		
		Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела Тогда
			
			Для каждого СтрокаМчЧ Из ДанныеМнЧР1.Строки Цикл
				
				_НомСтрокиМнЧ = "_" + Формат(ДанныеМнЧР1.Строки.Индекс(СтрокаМчЧ) + 1, "ЧГ=");
				Для каждого Пок Из СтрокаМчЧ.Данные Цикл
					Форма.ТабличныйДокумент.Области[Пок.Ключ + _НомСтрокиМнЧ].Значение = СтрокаМчЧ.Данные[Пок.Ключ];
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Раздел2" Тогда
		
		ОбъектОтчета = Форма.ОбъектОтчета(Форма.ИмяФормы);
		
		ПараметрыРасчета = Новый Структура;
		ПараметрыРасчета.Вставить("ИмяОтчета",   ИДОтчета(Форма.ИмяФормы));
		ПараметрыРасчета.Вставить("ИмяФормы",    ИДРедакцииОтчета(Форма.ИмяФормы));
		ПараметрыРасчета.Вставить("ИмяРаздела",  ИмяРаздела);
		ПараметрыРасчета.Вставить("Разрядность", Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения);
		ПараметрыРасчета.Вставить("Класс",       Форма.СоответствиеЕдиницыИзмеренияДелителю.Свойство(
		"Окр" + СтрЗаменить(Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения, " ", "")));
		
		ДеревоДанных = ПолучитьИзВременногоХранилища(
		Форма.СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]);
		
		АктивнаяВетка = Неопределено;
		Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела Тогда
			АктивнаяВетка = ДеревоДанных.Строки[
			Форма.СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0] - 1];
		КонецЕсли;
		
		Для каждого ВеткаР2 Из ДеревоДанных.Строки Цикл
			
			Если ВеткаР2 = АктивнаяВетка Тогда
				Для каждого Пок Из ВеткаР2.Данные Цикл
					ВеткаР2.Данные.Вставить(Пок.Ключ, Форма.ТабличныйДокумент.Области[Пок.Ключ].Значение);
				КонецЦикла;
			КонецЕсли;
			
			ОбъектОтчета.РассчитатьПоказателиРеглОтчета(ПараметрыРасчета, ВеткаР2.Данные);
			
			Если ВеткаР2 = АктивнаяВетка Тогда
				Для каждого Пок Из ВеткаР2.Данные Цикл
					Форма.ТабличныйДокумент.Области[Пок.Ключ].Значение = Пок.Значение;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДеревоДанных <> Неопределено Тогда
		Форма.СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]
		= ПоместитьВоВременноеХранилище(ДеревоДанных, Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьДанные()
	
	СформироватьРаздел1();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьРаздел1()
	
	// Предварительное сохранение наименования СРП (для последующего восстановления).
	//
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
		СохранитьДанныеРаздела("Раздел1");
	КонецЕсли;
	
	ДеревоДанныхРаздел1 = ПолучитьИзВременногоХранилища(
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанныхРаздел1"]);
	
	СохрНаименованиеСРП = ДеревоДанныхРаздел1.Строки[0].Данные["П000010000100"];
	
	// Предварительное сохранение сумм авансовых платежей.
	//
	ДанныеМногострочнойЧастиРаздела1
	= ДеревоДанныхРаздел1.Строки[0].ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппы1];
	
	АвансовыеПлатежи = Новый ТаблицаЗначений;
	АвансовыеПлатежи.Колонки.Добавить("КБК");
	АвансовыеПлатежи.Колонки.Добавить("ОКТМО");
	АвансовыеПлатежи.Колонки.Добавить("СуммаАвансовыхПлатежей1Кв");
	АвансовыеПлатежи.Колонки.Добавить("СуммаАвансовыхПлатежей2Кв");
	АвансовыеПлатежи.Колонки.Добавить("СуммаАвансовыхПлатежей3Кв");
	
	Для каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
		
		СуммаАвансовыхПлатежей1Кв = СтрокаМнЧ.Данные["П000010002303"];
		СуммаАвансовыхПлатежей2Кв = СтрокаМнЧ.Данные["П000010002503"];
		СуммаАвансовыхПлатежей3Кв = СтрокаМнЧ.Данные["П000010002703"];
		
		Если ЗначениеЗаполнено(СуммаАвансовыхПлатежей1Кв)
			ИЛИ ЗначениеЗаполнено(СуммаАвансовыхПлатежей2Кв)
			ИЛИ ЗначениеЗаполнено(СуммаАвансовыхПлатежей3Кв) Тогда
			
			АвансовыйПлатеж = АвансовыеПлатежи.Добавить();
			АвансовыйПлатеж.КБК   = СокрЛП(СтрокаМнЧ.Данные["П000010001003"]);
			АвансовыйПлатеж.ОКТМО = СокрЛП(СтрокаМнЧ.Данные["П000010002003"]);
			АвансовыйПлатеж.СуммаАвансовыхПлатежей1Кв = СуммаАвансовыхПлатежей1Кв;
			АвансовыйПлатеж.СуммаАвансовыхПлатежей2Кв = СуммаАвансовыхПлатежей2Кв;
			АвансовыйПлатеж.СуммаАвансовыхПлатежей3Кв = СуммаАвансовыхПлатежей3Кв;
			
		КонецЕсли;
		
	КонецЦикла;
	
	АвансовыеПлатежи.Свернуть(
	"КБК,ОКТМО", "СуммаАвансовыхПлатежей1Кв,СуммаАвансовыхПлатежей2Кв,СуммаАвансовыхПлатежей3Кв");
	
	// Очистка раздела 1.
	//
	ИнициализироватьМногоуровневыйРаздел("Раздел1");
	
	// Заполнение промежуточной таблицы значений данными из Раздела 2.
	//
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("КБК");
	ТЗ.Колонки.Добавить("ОКТМО");
	ТЗ.Колонки.Добавить("Сумма");
	
	ДеревоДанныхРаздел2 = ПолучитьИзВременногоХранилища(
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанныхРаздел2"]);
	
	Для каждого ВеткаРаздел2 Из ДеревоДанныхРаздел2.Строки Цикл
		ДанныеВеткиРаздел2 = ВеткаРаздел2.Данные;
		НоваяСтрокаТЗ = ТЗ.Добавить();
		НоваяСтрокаТЗ.КБК   = СокрЛП(ДанныеВеткиРаздел2["П000020001003"]);
		НоваяСтрокаТЗ.ОКТМО = СокрЛП(ДанныеВеткиРаздел2["П000020002003"]);
		НоваяСтрокаТЗ.Сумма = ДанныеВеткиРаздел2["П000020025003"];
	КонецЦикла;
	
	ТЗ.Свернуть("КБК,ОКТМО", "Сумма");
	ТЗ.Сортировать("КБК Возр, ОКТМО Возр");
	
	ТЗКоличествоСтрок = ТЗ.Количество();
	
	Для Инд = 0 По ТЗКоличествоСтрок - 1 Цикл
		ОбрИнд = ТЗКоличествоСтрок - 1 - Инд;
		СтрокаТЗ = ТЗ[ОбрИнд];
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.ОКТМО)
		   И НЕ ЗначениеЗаполнено(СтрокаТЗ.КБК)
		   И НЕ ЗначениеЗаполнено(СтрокаТЗ.Сумма) Тогда
			ТЗ.Удалить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	// Формирование Раздела 1.
	//
	ДеревоДанныхРаздел1
	= ПолучитьИзВременногоХранилища(СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанныхРаздел1"]);
	
	ДанныеМногострочнойЧастиРаздела1
	= ДеревоДанныхРаздел1.Строки[0].ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппы1];
	
	Для Инд = 0 По ТЗ.Количество() - 1 Цикл
		
		СтрокаТЗ = ТЗ[Инд];
		
		Если Инд = 0 Тогда
			СтрокаМнЧ = ДанныеМногострочнойЧастиРаздела1.Строки[0];
		Иначе
			СтрокаМнЧ = ДанныеМногострочнойЧастиРаздела1.Строки.Добавить();
			СтрокаМнЧ.Данные = Новый Структура;
			СтрокаМнЧ.ДанныеМногострочныхЧастей = Новый Структура;
		КонецЕсли;
		
		СтрокаМнЧ.Данные.Вставить("П000010001003", СтрокаТЗ.КБК);
		СтрокаМнЧ.Данные.Вставить("П000010002003", СтрокаТЗ.ОКТМО);
		СтрокаМнЧ.Данные.Вставить("П000010002103", СтрокаТЗ.Сумма);
		
		СтрокиАвансовыхПлатежей = АвансовыеПлатежи.НайтиСтроки(Новый Структура("КБК,ОКТМО", СтрокаТЗ.КБК, СтрокаТЗ.ОКТМО));
		Если СтрокиАвансовыхПлатежей.Количество() > 0 Тогда
			СтрокаМнЧ.Данные.Вставить("П000010002303", СтрокиАвансовыхПлатежей[0].СуммаАвансовыхПлатежей1Кв);
			СтрокаМнЧ.Данные.Вставить("П000010002503", СтрокиАвансовыхПлатежей[0].СуммаАвансовыхПлатежей2Кв);
			СтрокаМнЧ.Данные.Вставить("П000010002703", СтрокиАвансовыхПлатежей[0].СуммаАвансовыхПлатежей3Кв);
		Иначе
			СтрокаМнЧ.Данные.Вставить("П000010002303", 0);
			СтрокаМнЧ.Данные.Вставить("П000010002503", 0);
			СтрокаМнЧ.Данные.Вставить("П000010002703", 0);
		КонецЕсли;
		
		СтрокаМнЧ.Данные.Вставить("П000010003003", 0);
		СтрокаМнЧ.Данные.Вставить("П000010004003", 0);
		
	КонецЦикла;
	
	// Восстановление предварительно сохраненного наименования СРП.
	//
	ДеревоДанныхРаздел1.Строки[0].Данные.Вставить("П000010000100", СохрНаименованиеСРП);
	
	ПоместитьВоВременноеХранилище(ДеревоДанныхРаздел1,
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанныхРаздел1"]);
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
		НовыеНомераСтрокМногоуровнегоРаздела = Новый Массив;
		НовыеНомераСтрокМногоуровнегоРаздела.Добавить(1);
		ВывестиДанныеВТабличныйДокумент("Раздел1", НовыеНомераСтрокМногоуровнегоРаздела);
	КонецЕсли;
	
	// Запуск авторасчета значений в строках 030 и 040.
	//
	РасчетВДеревеЗначений(ЭтаФорма, "Раздел1");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтруктуруЗависимыхЯчеекДляАвторасчета()
	
	СтруктураЗависимыхЯчеекДляАвторасчета = Новый Структура;
	
	// Раздел 2.
	Структ = Новый Структура;
	Структ.Вставить("П000020011003", "П000020005003;П000020006003_Числ;П000020006003_Знам;П000020007003_КодЛьготы;П000020008003;П000020009003_КодЛьготы;П000020010003_Числ;П000020010003_Знам;П000020013003;П000020016003;П000020017003");
	Структ.Вставить("П000020014003", "П000020013003");
	Структ.Вставить("П000020015003", "П000020004003;П000020005003;П000020006003_Числ;П000020006003_Знам;П000020007003_КодЛьготы;П000020008003;П000020009003_КодЛьготы;П000020010003_Числ;П000020010003_Знам;П000020011003;П000020012003;П000020013003;П000020014003;П000020014503;П000020016003;П000020017003");
	Структ.Вставить("П000020017003");
	Структ.Вставить("П000020019003", "П000020004003;П000020005003;П000020006003_Числ;П000020006003_Знам;П000020007003_КодЛьготы;П000020008003;П000020009003_КодЛьготы;П000020010003_Числ;П000020010003_Знам;П000020011003;П000020012003;П000020013003;П000020014003;П000020014503;П000020015003;П000020016003;П000020017003;П000020018003_КодЛьготы");
	Структ.Вставить("П000020021003", "П000020004003;П000020005003;П000020006003_Числ;П000020006003_Знам;П000020007003_КодЛьготы;П000020008003;П000020009003_КодЛьготы;П000020010003_Числ;П000020010003_Знам;П000020011003;П000020012003;П000020013003;П000020014003;П000020014503;П000020015003;П000020016003;П000020017003;П000020020003");
	Структ.Вставить("П000020023003", "П000020004003;П000020005003;П000020006003_Числ;П000020006003_Знам;П000020007003_КодЛьготы;П000020008003;П000020009003_КодЛьготы;П000020010003_Числ;П000020010003_Знам;П000020011003;П000020012003;П000020013003;П000020014003;П000020014503;П000020015003;П000020016003;П000020017003;П000020022003_КодЛьготы;П000020022003_Процент");
	Структ.Вставить("П000020025003", "П000020004003;П000020005003;П000020006003_Числ;П000020006003_Знам;П000020007003_КодЛьготы;П000020008003;П000020009003_КодЛьготы;П000020010003_Числ;П000020010003_Знам;П000020011003;П000020012003;П000020013003;П000020014003;П000020014503;П000020015003;П000020016003;П000020017003;П000020018003_КодЛьготы;П000020019003;П000020020003;П000020021003;П000020022003_КодЛьготы;П000020022003_Процент;П000020023003;П000020024003_КодЛьготы");
	СтруктураЗависимыхЯчеекДляАвторасчета.Вставить("Раздел2", Структ);
	
	// Раздел 1.
	Структ = Новый Структура;
	Структ.Вставить("П000010003003", "П000010002103;П000010002303;П000010002503;П000010002703");
	Структ.Вставить("П000010004003", "П000010002103;П000010002303;П000010002503;П000010002703");
	СтруктураЗависимыхЯчеекДляАвторасчета.Вставить("Раздел1", Структ);
	
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаИПроверкаВыгрузкиЭлектронногоПредставленияИЗагрузкаОтчетаИзФайла

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗагрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузкаОтчетаИзФайла(ДанныеДляЗагрузки) Экспорт
	
	Если Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(НСтр("ru='Отчет ""%1"" не сохранен.'"), ЭтотОбъект.Заголовок);
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки)
	
	СформироватьСтруктуруПоказателей();
	
	ПараметрыОтчета = СформироватьСтруктуруПараметров();
	
	ДеревоДляЗагрузки = ИзвлечьСтруктуруXML("СхемаВыгрузки505");
	
	Если НЕ РегламентированнаяОтчетностьЗагрузка.ЗагрузкаОтчетаИзФайла(
		ЭтотОбъект, ПараметрыОтчета, ДанныеДляЗагрузки, ДеревоДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = Неопределено;
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму(
		"Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтаФорма);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.СформироватьИЗаписать(СпДокОсн);
	
КонецПроцедуры

&НаКлиенте
Функция УниверсальнаяВыгрузка(ТекстВыгрузки = Неопределено, ПроверятьБлокировки = Истина) Экспорт
	
	Перем ПараметрыВыгрузки;
	
	Если Модифицированность Тогда
		ТекстЗаголовка = ЗаголовокРегламентированногоОтчета(
			СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета, СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
		
		ОргСтр = " (" + СтруктураРеквизитовФормы.Организация + ")";
		Если СтрНайти(ТекстЗаголовка, ОргСтр) = 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + ОргСтр;
		КонецЕсли;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Отчет """ + ТекстЗаголовка + """ не сохранен.";
		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Открыта() Тогда
		Если СтруктураРеквизитовФормы.ЕдиницаИзмерения
			<> ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Перед выгрузкой необходимо установить единицу измерения ""в рублях"".'");
			Сообщение.Сообщить();
			
			Возврат Ложь;
			
		КонецЕсли;
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения <> 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Перед выгрузкой необходимо установить точность ""0"".'");
			Сообщение.Сообщить();
			
			Возврат Ложь;
			
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки = Истина Тогда // возможны 3 состояния
		РегламентированнаяОтчетностьКлиент.ПроверитьКонтрольныеСоотношения(ЭтаФорма, ОбработкаКС, Истина);
	КонецЕсли;
	
	Если НЕ УниверсальнаяВыгрузкаXMLНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ЗаголовокРегламентированногоОтчета(ДатаНач, ДатаКон, Периодичность = Неопределено) Экспорт
	
	СтрПериодОтчета = ПредставлениеПериода(НачалоДня(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета),
		КонецДня(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета), "ФП = Истина" );
	
	Если СтрНайти(ЭтаФорма.Заголовок, СтрПериодОтчета) = 0 Тогда
		Возврат ЭтаФорма.Заголовок + " за " + СтрПериодОтчета;
	Иначе
		Возврат ЭтаФорма.Заголовок;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLНаСервере(ТекстВыгрузки, ПараметрыВыгрузки)
	
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML("СхемаВыгрузки505");
	
	Если НЕ ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗаполнитьДанными(ПараметрыВыгрузки, ДеревоВыгрузки);
	
	ТекстВыгрузки = Новый Массив;
	
	ТекстВыгр = ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки);
	ТекстВыгрузки.Добавить(ВыгрузкаОтчета(ТекстВыгр, ПараметрыВыгрузки));
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СформироватьСтруктуруПоказателей()
	
	мДанныеОтчета = Новый Структура;
	
	СохрСтруктура = СохраненныеДанныеОтчета();
	ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		мДанныеОтчета.Вставить(СтрЗаменить(Раздел.Ключ, "ПолеТабличногоДокумента", ""),
			РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
	КонецЦикла;
	
	Для Каждого Раздел Из мСтруктураМногоуровневыхРазделов Цикл
		мДанныеОтчета.Вставить(Раздел.Ключ);
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция СформироватьСтруктуруПараметров()
	
	Титульный = мДанныеОтчета.Титульный;
	
	ПараметрыВыгрузки = Новый Структура;
	
	ПараметрыВыгрузки.Вставить("ПоМесту",  СокрЛП(Титульный.ПоМесту));
	
	ПараметрыВыгрузки.Вставить("ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	
	ПараметрыВыгрузки.Вставить("ДатаПодписи",  Титульный.ДатаПодписи);
	
	ПараметрыВыгрузки.Вставить("ДатаДок",  Формат(ТекущаяДатаСеанса(), "ДФ=дд.ММ.гггг"));
	ПараметрыВыгрузки.Вставить("Период",   СокрЛП(Титульный.Период));
	ПараметрыВыгрузки.Вставить("ОтчетГод", Формат(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания, "ДФ=гггг"));
	ПараметрыВыгрузки.Вставить("КодНО",    СокрЛП(Титульный.НалоговыйОрган));
	ПараметрыВыгрузки.Вставить("НомКорр",  Строка(СтруктураРеквизитовФормы.мСохраненныйДок.Вид));
	
	ПараметрыВыгрузки.Вставить("Тлф", СокрЛП(Титульный.ТелОрганизации));
	
	ПараметрыВыгрузки.Вставить("НаимОрг", СокрЛП(Титульный.НаимОрг));
	ПараметрыВыгрузки.Вставить("ИННОрг",  СокрЛП(СтруктураРеквизитовФормы.Организация.ИНН));
	ПараметрыВыгрузки.Вставить("КПП",     СокрЛП(Титульный.КПП));
	
	ПараметрыВыгрузки.Вставить("КодИФНСПосредника", ?(СтруктураРеквизитовФормы.Организация.КрупнейшийНалогоплательщик,
	СокрЛП(СтруктураРеквизитовФормы.Организация.КодНалоговогоОрганаПолучателя), ""));
	
	ПараметрыВыгрузки.Вставить("ФормРеорг", СокрЛП(Титульный.КодФормыРеорганизации));
	ПараметрыВыгрузки.Вставить("ИННРеорг",  СокрЛП(Титульный.ИННРеорганизованнойОрганизации));
	ПараметрыВыгрузки.Вставить("КППРеорг",  СокрЛП(Титульный.КППРеорганизованнойОрганизации));
	
	ПараметрыВыгрузки.Вставить("ПрПодп",      СокрЛП(Титульный.ПрПодп));
	ПараметрыВыгрузки.Вставить("НаимОргПред", СокрЛП(Титульный.ОргУп));
	ПараметрыВыгрузки.Вставить("НаимДокПред", СокрЛП(Титульный.ДокУпПред));
	ФИОПодписанта = РегламентированнаяОтчетность.РазложитьФИО(Титульный.ОргПодписант);
	ПараметрыВыгрузки.Вставить("ФамилияПодписанта",  ФИОПодписанта.Фамилия);
	ПараметрыВыгрузки.Вставить("ИмяПодписанта",      ФИОПодписанта.Имя);
	ПараметрыВыгрузки.Вставить("ОтчествоПодписанта", ФИОПодписанта.Отчество);
	
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "ЗемНалНД");
	
	ПараметрыВыгрузки.Вставить("ИдФайл", ИдентификаторФайлаВыгрузки(ПараметрыВыгрузки, "NO_ZEMND"));
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

&НаСервере
Функция ИзвлечьСтруктуруXML(ИмяМакета)
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(ИмяМакета);
	
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

&НаСервере
Функция СимволыДляПроверкиКПП()
	
	Структ = Новый Структура;
	
	Структ.Вставить("Символы0_5", СтрРазделить("0,1,2,3,4,5", ",", Ложь));
	Структ.Вставить("Символы0_7", СтрРазделить("0,1,2,3,4,5,6,7", ",", Ложь));
	Структ.Вставить("Символы0_9", СтрРазделить("0,1,2,3,4,5,6,7,8,9", ",", Ложь));
	
	Структ.Вставить("Символы1_9", СтрРазделить("1,2,3,4,5,6,7,8,9", ",", Ложь));
	
	Структ.Вставить("Символы6_9", СтрРазделить("6,7,8,9", ",", Ложь));
	
	Структ.Вставить("Символы8_9", СтрРазделить("8,9", ",", Ложь));
	
	Структ.Вставить("СимволыA_I", СтрРазделить("A,B,C,D,E,F,G,H,I", ",", Ложь));
	Структ.Вставить("СимволыA_Q", СтрРазделить("A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q", ",", Ложь));
	Структ.Вставить("СимволыA_Y", СтрРазделить("A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y", ",", Ложь));
	Структ.Вставить("СимволыA_Z", СтрРазделить("A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z", ",", Ложь));
	
	Структ.Вставить("СимволыB_Z", СтрРазделить("B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z", ",", Ложь));
	
	Возврат Структ;
	
КонецФункции

&НаСервере
Процедура ПроверитьКПП(ИмяОбласти, ЗначКПП, СимволыДляПроверкиКПП, СтатусПроверки)
	
	Если ИмяОбласти = "КПП" Тогда
		ПодстрокаВТексте = "налогоплательщика";
	ИначеЕсли ИмяОбласти = "КППРеорганизованнойОрганизации" Тогда
		ПодстрокаВТексте = "реорганизованной организации";
	Иначе
		Возврат;
	КонецЕсли;
	
	// Символы 1, 2.
	Символ1 = Лев(ЗначКПП, 1);
	Символ2 = Сред(ЗначКПП, 2, 1);
	КорректныСимволы12
	= (СимволыДляПроверкиКПП.Символы0_9.Найти(Символ1) <> Неопределено
	И СимволыДляПроверкиКПП.Символы1_9.Найти(Символ2) <> Неопределено)
	ИЛИ (СимволыДляПроверкиКПП.Символы1_9.Найти(Символ1) <> Неопределено
	И СимволыДляПроверкиКПП.Символы0_9.Найти(Символ2) <> Неопределено);
	Если НЕ КорректныСимволы12 Тогда
		СообщитьВТС(
		"Задано некорректное сочетание значений 1-го и 2-го символов КПП " + ПодстрокаВТексте + ". Допустимы сочетания значений ""01"" - ""99"".",
		"Титульный", ИмяОбласти);
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Символы 3, 4.
	Для НомПоз = 3 По 4 Цикл
		ТекСимвол = Сред(ЗначКПП, НомПоз, 1);
		КорректенТекСимвол = СимволыДляПроверкиКПП.Символы0_9.Найти(ТекСимвол) <> Неопределено;
		Если НЕ КорректенТекСимвол Тогда
			СообщитьВТС(
			"Задано некорректное значение " + НомПоз + "-го символа КПП " + ПодстрокаВТексте + ". Допустимы значения ""0"" - ""9"".",
			"Титульный", ИмяОбласти);
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Символы 5, 6.
	Символ5 = Сред(ЗначКПП, 5, 1);
	Символ6 = Сред(ЗначКПП, 6, 1);
	ЭтоСимволы56Вариант1 = Символ5 = "0"
	И (СимволыДляПроверкиКПП.Символы0_7.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Z.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант2 = Символ5 = "1"
	И (СимволыДляПроверкиКПП.Символы0_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Q.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант3 = Символ5 = "2"
	И (СимволыДляПроверкиКПП.Символы0_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_I.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант4 = Символ5 = "3"
	И (СимволыДляПроверкиКПП.Символы0_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Y.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант5 = Символ5 = "4"
	И (СимволыДляПроверкиКПП.Символы0_5.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.Символы8_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Z.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант6 = Символ5 = "5"
	И (СимволыДляПроверкиКПП.Символы1_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Z.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант7 = СимволыДляПроверкиКПП.СимволыB_Z.Найти(Символ5) <> Неопределено
	И (СимволыДляПроверкиКПП.Символы0_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Z.Найти(Символ6) <> Неопределено);
	ЭтоСимволы56Вариант8 = СимволыДляПроверкиКПП.Символы6_9.Найти(Символ5) <> Неопределено
	И (СимволыДляПроверкиКПП.Символы0_9.Найти(Символ6) <> Неопределено
	ИЛИ СимволыДляПроверкиКПП.СимволыA_Z.Найти(Символ6) <> Неопределено);
	Если НЕ (ЭтоСимволы56Вариант1 ИЛИ ЭтоСимволы56Вариант2 ИЛИ ЭтоСимволы56Вариант3 ИЛИ ЭтоСимволы56Вариант4
		ИЛИ ЭтоСимволы56Вариант5 ИЛИ ЭтоСимволы56Вариант6 ИЛИ ЭтоСимволы56Вариант7 ИЛИ ЭтоСимволы56Вариант8) Тогда
		СообщитьВТС(
		"Задано некорректное сочетание значений 5-го и 6-го символов КПП " + ПодстрокаВТексте
		+ ". Допустимы значения ""0"" - ""9"", ""A"" - ""Z"", за исключением сочетаний: ""08"", ""09"", ""1R"" - ""1Z"", ""2J"" - ""2Z"", ""3Z"", ""46"", ""47"", ""50"", ""A0"" - ""A9"", ""AA"" - ""AZ"".",
		"Титульный", ИмяОбласти);
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Символы 7 - 9.
	Для НомПоз = 7 По 9 Цикл
		ТекСимвол = Сред(ЗначКПП, НомПоз, 1);
		КорректенТекСимвол = СимволыДляПроверкиКПП.Символы0_9.Найти(ТекСимвол) <> Неопределено;
		Если НЕ КорректенТекСимвол Тогда
			СообщитьВТС(
			"Задано некорректное значение " + НомПоз + "-го символа КПП " + ПодстрокаВТексте
			+ ". Допустимы значения ""0"" - ""9"".", "Титульный", ИмяОбласти);
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки)
	
	СтатусПроверки = Истина;
	
	ДанныеМногоуровневыхРазделов = Новый Структура;
	СохрСтруктура = СохраненныеДанныеОтчета();
	Для каждого ЭлементСтруктуры Из СохрСтруктура.ДанныеМногоуровневыхРазделов Цикл
		ДанныеМногоуровневыхРазделов.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;
	
	СимволыДляПроверкиКПП = СимволыДляПроверкиКПП();
	
	ВозможныеКодыПоМесту = Новый Массив;
	ВозможныеКодыПоМесту.Добавить("213");
	ВозможныеКодыПоМесту.Добавить("216");
	ВозможныеКодыПоМесту.Добавить("270");
	ВозможныеКодыПоМесту.Добавить("250");
	ВозможныеКодыПоМесту.Добавить("251");
	
	ВозможныеКодыПериода = Новый Массив;
	ВозможныеКодыПериода.Добавить("34");
	ВозможныеКодыПериода.Добавить("50");
	
	ВозможныеКодыФормыРеорганизции = Новый Массив;
	ВозможныеКодыФормыРеорганизции.Добавить("0");
	ВозможныеКодыФормыРеорганизции.Добавить("1");
	ВозможныеКодыФормыРеорганизции.Добавить("2");
	ВозможныеКодыФормыРеорганизции.Добавить("3");
	ВозможныеКодыФормыРеорганизции.Добавить("5");
	ВозможныеКодыФормыРеорганизции.Добавить("6");
	
	// Титульный лист.
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ИННОрг) Тогда
		СообщитьВТС("Не задан ИНН налогоплательщика.",
		"Титульный", "ИНН");
		СтатусПроверки = Ложь;
	ИначеЕсли СтрДлина(ПараметрыВыгрузки.ИННОрг) <> 10 Тогда
		СообщитьВТС("Задан ИНН налогоплательщика, не состоящий из 10 цифр.",
		"Титульный", "ИНН");
		СтатусПроверки = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.КПП) Тогда
		СообщитьВТС("Не задан КПП налогоплательщика.",
		"Титульный", "КПП");
		СтатусПроверки = Ложь;
	ИначеЕсли СтрДлина(ПараметрыВыгрузки.КПП) <> 9 Тогда
		СообщитьВТС("Задан КПП налогоплательщика, не состоящий из 9 символов.",
		"Титульный", "КПП");
		СтатусПроверки = Ложь;
	Иначе
		ПроверитьКПП("КПП", ПараметрыВыгрузки.КПП, СимволыДляПроверкиКПП, СтатусПроверки);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.НаимОрг) Тогда
		СообщитьВТС("Не задано наименование налогоплательщика.",
		"Титульный", "НаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.КодНО) Тогда
		СообщитьВТС("Не задан код налогового органа.",
		"Титульный", "НалоговыйОрган");
		СтатусПроверки = Ложь;
	ИначеЕсли СтрДлина(ПараметрыВыгрузки.КодНО) <> 4 Тогда
		СообщитьВТС("Задан код налогового органа, не состоящий из 4 цифр.",
		"Титульный", "НалоговыйОрган");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ПоМесту) Тогда
		СообщитьВТС("Не задан код места, по которому представляется налоговый расчет.",
		"Титульный", "ПоМесту");
		СтатусПроверки = Ложь;
	ИначеЕсли ВозможныеКодыПоМесту.Найти(ПараметрыВыгрузки.ПоМесту) = Неопределено Тогда
		СообщитьВТС("Задан некорректный код места, по которому представляется налоговый расчет.",
		"Титульный", "ПоМесту");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Период) Тогда
		СообщитьВТС("Не задан код налогового периода.",
		"Титульный", "Период");
		СтатусПроверки = Ложь;
	ИначеЕсли ВозможныеКодыПериода.Найти(ПараметрыВыгрузки.Период) = Неопределено Тогда
		СообщитьВТС("Задан некорректный код налогового периода.",
		"Титульный", "Период");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ФамилияПодписанта) Тогда
		СообщитьВТС("Не задана фамилия подписанта.", "Титульный", "ОргПодписант");
		СтатусПроверки = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ИмяПодписанта) Тогда
		СообщитьВТС("Не задано имя подписанта.", "Титульный", "ОргПодписант");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ПараметрыВыгрузки.ПрПодп = "2" И НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.НаимДокПред) Тогда
		СообщитьВТС("Не заданы наименование и реквизиты документа, подтверждающего полномочия представителя.",
		"Титульный", "ДокУпПред");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВыгрузки.ИННРеорг)
	 ИЛИ ЗначениеЗаполнено(ПараметрыВыгрузки.КППРеорг)
	 ИЛИ ЗначениеЗаполнено(ПараметрыВыгрузки.ФормРеорг) Тогда
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ФормРеорг) Тогда
			СообщитьВТС("Не задан код формы реорганизации.",
			"Титульный", "КодФормыРеорганизации");
			СтатусПроверки = Ложь;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПараметрыВыгрузки.ФормРеорг) Тогда
			Если ВозможныеКодыФормыРеорганизции.Найти(ПараметрыВыгрузки.ФормРеорг) = Неопределено Тогда
				СообщитьВТС("Задано некорректное значение кода формы реорганизации.",
				"Титульный", "КодФормыРеорганизации");
				СтатусПроверки = Ложь;
			ИначеЕсли ПараметрыВыгрузки.ФормРеорг <> "0" Тогда
				ДлинаИННРеорг = СтрДлина(ПараметрыВыгрузки.ИННРеорг);
				Если ДлинаИННРеорг <> 10 Тогда
					СообщитьВТС("Не" + ?(ДлинаИННРеорг = 0, "", "верно")
					+ " задан ИНН реорганизованной организации.",
					"Титульный", "ИННРеорганизованнойОрганизации");
					СтатусПроверки = Ложь;
				КонецЕсли;
				ДлинаКППРеорг = СтрДлина(ПараметрыВыгрузки.КППРеорг);
				Если ДлинаКППРеорг <> 9 Тогда
					СообщитьВТС("Не" + ?(ДлинаКППРеорг = 0, "", "верно")
					+ " задан КПП реорганизованной организации.",
					"Титульный", "КППРеорганизованнойОрганизации");
					СтатусПроверки = Ложь;
				Иначе
					ПроверитьКПП("КППРеорганизованнойОрганизации", ПараметрыВыгрузки.КППРеорг, СимволыДляПроверкиКПП, СтатусПроверки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Таблица для проверки сочетаний КБК и ОКТМО раздела 1 с разделом 2.
	СочетаниеРазделов = Новый ТаблицаЗначений;
	СочетаниеРазделов.Колонки.Добавить("КБК");
	СочетаниеРазделов.Колонки.Добавить("ОКТМО");
	СочетаниеРазделов.Колонки.Добавить("Раздел", Новый ОписаниеТипов("Число"));
	СочетаниеРазделов.Колонки.Добавить("Страница", Новый ОписаниеТипов("Число"));
	СочетаниеРазделов.Колонки.Добавить("ИмяОбласти");
	СочетаниеРазделов.Колонки.Добавить("Счетчик1", Новый ОписаниеТипов("Число"));
	СочетаниеРазделов.Колонки.Добавить("Счетчик2", Новый ОписаниеТипов("Число"));
	
	СочетаниеРазделов.Индексы.Добавить("Раздел");
	
	// Раздел 1.
	ДеревоРаздела = ДанныеМногоуровневыхРазделов.Раздел1;
	ДанныеМногострочнойЧастиРаздела1
	= ДеревоРаздела.Строки[0].ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппы1];
	
	НомерСтраницы = 1;
	
	Если ПараметрыВыгрузки.ПоМесту = "250" ИЛИ ПараметрыВыгрузки.ПоМесту = "251" Тогда
		
		НаимСРП = СокрЛП(ДеревоРаздела.Строки[0].Данные.П000010000100);
		Если НЕ ЗначениеЗаполнено(НаимСРП) Тогда
			СообщитьВТС("В разделе 1 не задано наименование соглашения о разделе продукции (для участков недр, предоставленных в пользование на условиях СРП).",
			"Раздел1", "П000010000100", НомерСтраницы);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого СтрокаМнЧРаздела1 Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
		
		ЗаполненаСтрокаМнЧРаздела1 = Ложь;
		Для каждого Эл Из СтрокаМнЧРаздела1.Данные Цикл
			Если ЗначениеЗаполнено(Эл.Значение) Тогда
				ЗаполненаСтрокаМнЧРаздела1 = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаполненаСтрокаМнЧРаздела1 Тогда
			
			НомСтроки = Формат(ДанныеМногострочнойЧастиРаздела1.Строки.Индекс(СтрокаМнЧРаздела1) + 1, "ЧГ=");
			
			ИмяПокКБК   = "П000010001003";
			ИмяПокОКТМО = "П000010002003";
			
			ИмяОблКБК   = ИмяПокКБК   + "_" + НомСтроки;
			ИмяОблОКТМО = ИмяПокОКТМО + "_" + НомСтроки;
			
			КБК   = СокрЛП(СтрокаМнЧРаздела1.Данные[ИмяПокКБК]);
			ОКТМО = СокрЛП(СтрокаМнЧРаздела1.Данные[ИмяПокОКТМО]);
			
			ДлинаКБК = СтрДлина(КБК);
			Если ДлинаКБК <> 20 Тогда
				СообщитьВТС("В строке " + НомСтроки + " раздела 1 не" + ?(ДлинаКБК = 0,
				"", "верно") + " задан КБК.", "Раздел1", ИмяОблКБК, НомерСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ДлинаОКТМО = СтрДлина(ОКТМО);
			Если ДлинаОКТМО <> 11 И ДлинаОКТМО <> 8 Тогда
				СообщитьВТС("В строке " + НомСтроки + " раздела 1 не" + ?(ДлинаОКТМО = 0,
				"", "верно") + " задан код по ОКТМО.", "Раздел1", ИмяОблОКТМО, НомерСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаМнЧРаздела1.Данные["П000010003003"])
				И ЗначениеЗаполнено(СтрокаМнЧРаздела1.Данные["П000010004003"]) Тогда
				СообщитьВТС("В строке " + НомСтроки + " раздела 1 одновременно заданы строки 030 и 040.",
				"Раздел1", "П000010003003", НомерСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Сочетание = СочетаниеРазделов.Добавить();
			Сочетание.КБК        = КБК;
			Сочетание.ОКТМО      = ОКТМО;
			Сочетание.Раздел     = 1;
			Сочетание.Страница   = 1;
			Сочетание.ИмяОбласти = ИмяОблКБК;
			Сочетание.Счетчик1   = 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Раздел 2.
	ДеревоРаздела = ДанныеМногоуровневыхРазделов.Раздел2;
	
	Для каждого СтраницаРаздела Из ДеревоРаздела.Строки Цикл
		
		ЗаполненаСтраницаРаздела = Ложь;
		Для каждого Эл Из СтраницаРаздела.Данные Цикл
			Если ЗначениеЗаполнено(Эл.Значение) Тогда
				ЗаполненаСтраницаРаздела = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаполненаСтраницаРаздела Тогда
			
			НомСтраницы = Формат(ДеревоРаздела.Строки.Индекс(СтраницаРаздела) + 1, "ЧГ=");
			
			НомКадастрЗУ = СокрЛП(СтраницаРаздела.Данные.П000020000100);
			Если НЕ ЗначениеЗаполнено(НомКадастрЗУ) Тогда
				СообщитьВТС("На странице " + НомСтраницы + " раздела 2 не задан кадастровый номер земельного участка.",
				"Раздел2", "П000020000100", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			КБК = СокрЛП(СтраницаРаздела.Данные.П000020001003);
			ДлинаКБК = СтрДлина(КБК);
			Если ДлинаКБК <> 20 Тогда
				СообщитьВТС("На странице " + НомСтраницы + " раздела 2 не" + ?(ДлинаКБК = 0,
				"", "верно") + " задан КБК.", "Раздел2", "П000020001003", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ОКТМО = СокрЛП(СтраницаРаздела.Данные.П000020002003);
			ДлинаОКТМО = СтрДлина(ОКТМО);
			Если ДлинаОКТМО <> 11 И ДлинаОКТМО <> 8 Тогда
				СообщитьВТС("На странице " + НомСтраницы + " раздела 2 не" + ?(ДлинаОКТМО = 0,
				"", "верно") + " задан код по ОКТМО.", "Раздел2", "П000020002003", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			КатегорЗем = СокрЛП(СтраницаРаздела.Данные.П000020003003);
			Если НЕ ЗначениеЗаполнено(КатегорЗем) Тогда
				СообщитьВТС("На странице " + НомСтраницы + " раздела 2 не задан код категории земель.",
				"Раздел2", "П000020003003", НомСтраницы);
				СтатусПроверки = Ложь;
			ИначеЕсли СтрДлина(КатегорЗем) <> 12 Тогда
				СообщитьВТС("На странице " + НомСтраницы + " раздела 2 задан код категории земель, не состоящий из 12 цифр.",
				"Раздел2", "П000020003003", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ПерСтр = СокрЛП(СтраницаРаздела.Данные.П000020004003);
			Если ЗначениеЗаполнено(ПерСтр) Тогда
				ВозможныеЗначения = ВозможныеЗначенияПоДаннымСпискаВыбора("П000020004003");
				Если ВозможныеЗначения.Количество() > 0 И ВозможныеЗначения.Найти(ПерСтр) = Неопределено Тогда
					ВозможныеЗначенияДляСообщения = СтрСоединить(ВозможныеЗначения, ", ");
					СообщитьВТС("На странице " + НомСтраницы + " раздела 2 период строительства, если заполнен, должен принимать одно из значений: """
					+ СтрСоединить(ВозможныеЗначения, """, """) + """.", "Раздел2", "П000020004003", НомСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020006003_Числ)
				ИЛИ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020006003_Знам) Тогда
				Если НЕ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020006003_Числ) Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 не задан числитель в доле налогоплательщика в праве на земельный участок.",
					"Раздел2", "П000020006003_Числ", НомСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли НЕ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020006003_Знам) Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 не задан знаменатель в доле налогоплательщика в праве на земельный участок.",
					"Раздел2", "П000020006003_Знам", НомСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли СтраницаРаздела.Данные.П000020006003_Числ > СтраницаРаздела.Данные.П000020006003_Знам Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 числитель больше знаменателя в доле налогоплательщика в праве на земельный участок.",
					"Раздел2", "П000020006003_Числ", НомСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные.П000020007003_КодЛьготы);
			Если ЗначениеЗаполнено(КодЛьготы) И КодЛьготы <> "3022100" Тогда
				СообщитьВТС("На странице " + НомСтраницы
				+ " раздела 2 код налоговой льготы в виде не облагаемой налогом суммы, если заполнен, должен принимать значение ""3022100"".",
				"Раздел2", "П000020007003_КодЛьготы", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			КодЛьготыСтр090 = СокрЛП(СтраницаРаздела.Данные.П000020009003_КодЛьготы);
			Если ЗначениеЗаполнено(КодЛьготыСтр090) И КодЛьготыСтр090 <> "3022300" Тогда
				СообщитьВТС("На странице " + НомСтраницы
				+ " раздела 2 код налоговой льготы в виде доли необлагаемой площади земельного участка, если заполнен, должен принимать значение ""3022300"".",
				"Раздел2", "П000020009003_КодЛьготы", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если КодЛьготыСтр090 = "3022300"
				ИЛИ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020010003_Числ)
				ИЛИ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020010003_Знам) Тогда
				Если НЕ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020010003_Числ)
					И НЕ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020010003_Знам) Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 не задана доля необлагаемой площади земельного участка (пункт 2 статьи 387 Налогового кодекса Российской Федерации).",
					"Раздел2", "П000020010003_Числ", НомСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли НЕ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020010003_Числ) Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 не задан числитель в доле необлагаемой площади земельного участка (пункт 2 статьи 387 Налогового кодекса Российской Федерации).",
					"Раздел2", "П000020010003_Числ", НомСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли НЕ ЗначениеЗаполнено(СтраницаРаздела.Данные.П000020010003_Знам) Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 не задан знаменатель в доле необлагаемой площади земельного участка (пункт 2 статьи 387 Налогового кодекса Российской Федерации).",
					"Раздел2", "П000020010003_Знам", НомСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли СтраницаРаздела.Данные.П000020010003_Числ > СтраницаРаздела.Данные.П000020010003_Знам Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 числитель больше знаменателя в доле необлагаемой площади земельного участка (пункт 2 статьи 387 Налогового кодекса Российской Федерации).",
					"Раздел2", "П000020010003_Числ", НомСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные.П000020018003_КодЛьготы);
			Если ЗначениеЗаполнено(КодЛьготы) И КодЛьготы <> "3022400" Тогда
				СообщитьВТС("На странице " + НомСтраницы
				+ " раздела 2 код налоговой льготы в виде освобождения от налогообложения (строка 180), если заполнен, должен принимать значение ""3022400"".",
				"Раздел2", "П000020018003_КодЛьготы", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные.П000020020003);
			Если ЗначениеЗаполнено(КодЛьготы) Тогда
				ВозможныеЗначения = ВозможныеЗначенияПоДаннымСпискаВыбора("П000020020003");
				Если ВозможныеЗначения.Количество() > 0 И ВозможныеЗначения.Найти(КодЛьготы) = Неопределено Тогда
					СообщитьВТС("На странице " + НомСтраницы
					+ " раздела 2 код налоговой льготы в виде освобождения от налогообложения (строка 200), если заполнен, должен принимать одно из значений: """
					+ СтрСоединить(ВозможныеЗначения, """, """) + """.", "Раздел2", "П000020020003", НомСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные.П000020022003_КодЛьготы);
			Если ЗначениеЗаполнено(КодЛьготы) И КодЛьготы <> "3022200" Тогда
				СообщитьВТС("На странице " + НомСтраницы
				+ " раздела 2 код налоговой льготы в виде уменьшения суммы налога, если заполнен, должен принимать значение ""3022200"".",
				"Раздел2", "П000020022003_КодЛьготы", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные.П000020024003_КодЛьготы);
			Если ЗначениеЗаполнено(КодЛьготы) И КодЛьготы <> "3022500" Тогда
				СообщитьВТС("На странице " + НомСтраницы
				+ " раздела 2 код налоговой льготы в виде снижения налоговой ставки, если заполнен, должен принимать значение ""3022500"".",
				"Раздел2", "П000020024003_КодЛьготы", НомСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Сочетание = СочетаниеРазделов.Добавить();
			Сочетание.КБК        = КБК;
			Сочетание.ОКТМО      = ОКТМО;
			Сочетание.Раздел     = 2;
			Сочетание.Страница   = ДеревоРаздела.Строки.Индекс(СтраницаРаздела) + 1;
			Сочетание.ИмяОбласти = "П000020000100";
			Сочетание.Счетчик2   = 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Сочетания разделов.
	СочетанияРаздела1 = СочетаниеРазделов.НайтиСтроки(Новый Структура("Раздел", 1));
	СочетанияРаздела2 = СочетаниеРазделов.НайтиСтроки(Новый Структура("Раздел", 2));
	
	СокращеннаяПроверка = Ложь;
	
	Если СочетанияРаздела1.Количество() = 0 Тогда
		СообщитьВТС("В разделе 1 не заполнена ни одна группа строк 010-040.",
		"Раздел1", "П000010001003_1", 1);
		СтатусПроверки = Ложь;
		СокращеннаяПроверка = Истина;
	КонецЕсли;
	
	Если СочетанияРаздела2.Количество() = 0 Тогда
		СообщитьВТС("В налоговой декларации отсутствуют заполненные листы раздела 2.",
		"Раздел2", "П000020000100", 1);
		СтатусПроверки = Ложь;
		СокращеннаяПроверка = Истина;
	КонецЕсли;
	
	Если СокращеннаяПроверка Тогда
		Возврат СтатусПроверки;
	КонецЕсли;
	
	ПовторенияРаздела1 = СочетаниеРазделов.Скопировать(СочетанияРаздела1);
	ПовторенияРаздела1.Свернуть("КБК, ОКТМО", "Счетчик1");
	
	Для Каждого СтрокаПроверки Из ПовторенияРаздела1 Цикл
		Если СтрокаПроверки.Счетчик1 > 1 И ЗначениеЗаполнено(СтрокаПроверки.ОКТМО) И ЗначениеЗаполнено(СтрокаПроверки.КБК) Тогда
			УсловияПоиска = Новый Структура("КБК, ОКТМО, Раздел", СтрокаПроверки.КБК, СтрокаПроверки.ОКТМО, 1);
			СтрокиДубликатов = СочетаниеРазделов.НайтиСтроки(УсловияПоиска);
			ТаблицаДубликатов = СочетаниеРазделов.Скопировать(СтрокиДубликатов);
			ТаблицаДубликатов.Сортировать("Страница, ИмяОбласти");
			
			ПозицияПерехода = ТаблицаДубликатов[0].ИмяОбласти;
			
			ТаблицаДубликатов.Свернуть("Страница");
			ТаблицаДубликатов.Сортировать("Страница");
			
			СообщитьВТС("В разделе 1 обнаружены дублирующиеся сочетания КБК """ + СтрокаПроверки.КБК
			+ """ и кода по ОКТМО """ + СтрокаПроверки.ОКТМО + """.",
			"Раздел1", ПозицияПерехода, 1);
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	СочетаниеРазделовСвернутое = СочетаниеРазделов.Скопировать();
	СочетаниеРазделовСвернутое.Свернуть("КБК, ОКТМО", "Счетчик1, Счетчик2");
	
	Для Каждого Сочетание Из СочетаниеРазделовСвернутое Цикл
		
		Если Сочетание.Счетчик1 <> 0 И Сочетание.Счетчик2 = 0
			И ЗначениеЗаполнено(Сочетание.КБК) И ЗначениеЗаполнено(Сочетание.ОКТМО) Тогда
			
			// По КБК и ОКТМО группы строк 010-040 раздела 1 не найдены листы раздела 2.
			
			УсловияПоиска = Новый Структура("КБК, ОКТМО, Раздел", Сочетание.КБК, Сочетание.ОКТМО, 1);
			НесвязанныеСтроки = СочетаниеРазделов.НайтиСтроки(УсловияПоиска);
			
			ПозицияПерехода = НесвязанныеСтроки[0].ИмяОбласти;
			
			СообщитьВТС("По сочетанию КБК """ + Сочетание.КБК + """ и кода по ОКТМО """
			+ Сочетание.ОКТМО + """ раздела 1 не найдены соответствующие листы раздела 2.",
			"Раздел1", ПозицияПерехода, 1);
			СтатусПроверки = Ложь;
			
		ИначеЕсли Сочетание.Счетчик1 = 0 Тогда
			
			Если Сочетание.Счетчик2 <> 0
				И ЗначениеЗаполнено(Сочетание.КБК) И ЗначениеЗаполнено(Сочетание.ОКТМО) Тогда
				
				// По КБК и ОКТМО раздела 2 не найдена группа строк 010-040 раздела 1.
				
				УсловияПоиска = Новый Структура("КБК, ОКТМО, Раздел", Сочетание.КБК, Сочетание.ОКТМО, 2);
				НесвязанныеСтроки = СочетаниеРазделов.НайтиСтроки(УсловияПоиска);
				
				СтраницаПерехода = НесвязанныеСтроки[0].Страница;
				ПозицияПерехода = "П000020001003";
				
				СообщитьВТС("По сочетанию КБК """ + Сочетание.КБК + """ и кода по ОКТМО """
				+ Сочетание.ОКТМО + """ на странице " + Формат(СтраницаПерехода, "ЧГ=")
				+ " раздела 2 не найдена соответствующая группа строк 010-040 в разделе 1.",
				"Раздел2", ПозицияПерехода, СтраницаПерехода);
				СтатусПроверки = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Функция ВозможныеЗначенияПоДаннымСпискаВыбора(ИмяПоказателя)
	
	ЗначенияПоДаннымСпискаВыбора = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтаФорма, ИмяПоказателя);
	
	ВозможныеЗначения = Новый Массив;
	
	Если ЗначенияПоДаннымСпискаВыбора = Неопределено Тогда
		Возврат ВозможныеЗначения;
	КонецЕсли;
	
	Для каждого Эл Из ЗначенияПоДаннымСпискаВыбора.ТаблицаЗначений Цикл
		ЗначКод = СокрЛП(Эл.Код);
		Если ЗначениеЗаполнено(ЗначКод) Тогда
			ВозможныеЗначения.Добавить(ЗначКод);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозможныеЗначения;
	
КонецФункции

&НаСервере
Процедура СообщитьВТС(Описание, Раздел, ИмяЯчейки, ФИО = "")
	
	РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, Описание, Раздел, ИмяЯчейки, ФИО);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанными(ПараметрыВыгрузки, ДеревоВыгрузки)
	
	ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ДеревоВыгрузки); // формируем точную структуру XML-макета
	ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, ДеревоВыгрузки);
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьУсловныеЭлементы(Знач ПараметрыВыгрузки, Узел)
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
			Возврат;
		КонецЕсли;
		РегламентированнаяОтчетность.ДополнитьСтруктуруПараметров(мДанныеОтчета, ПараметрыВыгрузки, Узел);
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ РегламентированнаяОтчетность.УсловиеВыполнено(ЭтаФорма, ПараметрыВыгрузки, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Узел, НомерСтроки = Неопределено)
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учет обоих вариантов: кириллицы и латиницы
			Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, "ИмяКлючевогоУзлаСодержательнойЧасти") И Стр.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				ЗаполнитьСодержательнуюЧасть(Стр, ПараметрыВыгрузки);
			ИначеЕсли Стр.Многострочность Тогда
				НомСтр = 1;
				ПодчиненныйЭлемент = ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Стр);
				КлючПодчиненногоЭлемента = ПодчиненныйЭлемент.Ключ;
				Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, КлючПодчиненногоЭлемента + "_" + Формат(НомСтр, "ЧГ=")) Цикл
					УзелСоответствующийСтроке = СкопироватьУзел(Узел, Стр);
					ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, УзелСоответствующийСтроке, НомСтр);
					НомСтр = НомСтр + 1;
				КонецЦикла;
			Иначе
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр, НомерСтроки);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
					ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
					ЗначениеПоказателя = Неопределено;
					Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
						ВывестиПоказательВXML(Стр, ЗначениеПоказателя);
					Иначе
						ВывестиПоказательВXML(Стр, "");
					КонецЕсли;
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				ВывестиПоказательВXML(Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Родитель)
	
	Для Каждого Стр из Родитель.Строки Цикл
		Если ЗначениеЗаполнено(Стр.Ключ) Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция СкопироватьУзел(Родитель, Узел, Знач МаксИндекс = Неопределено)
	
	// Нахождение узла с максимальным индексом и с тем же кодом,
	// что и у копируемого, и добавление нового сразу после найденного.
	Если МаксИндекс = Неопределено Тогда
		СтрокиСТемЖеКодом = Родитель.Строки.НайтиСтроки(Новый Структура("Код", Узел.Код), Ложь);
		МаксИндекс = - 1;
		КолСтрокСТемЖеКодом = СтрокиСТемЖеКодом.Количество();
		Если КолСтрокСТемЖеКодом > 0 Тогда
			МаксИндекс = Родитель.Строки.Индекс(СтрокиСТемЖеКодом[КолСтрокСТемЖеКодом - 1]);
		КонецЕсли;
	КонецЕсли;
	
	ИндексСоздаваемогоУзла = МаксИндекс + 1;
	Если ИндексСоздаваемогоУзла >= Родитель.Строки.Количество() ИЛИ ИндексСоздаваемогоУзла = 0 Тогда
		// Аналогичный узел не найден или найденный узел - последний.
		НовыйУзел = Родитель.Строки.Добавить();
	Иначе
		НовыйУзел = Родитель.Строки.Вставить(ИндексСоздаваемогоУзла);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр, Узел.Строки.Количество() - 1);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

&НаСервере
Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	ЧислоВместоКодаЭлемента = (ТипЗнч(КодЭлемента) = Тип("Число"));
	Если ТипЗнч(КодЭлемента) = Тип("Число") Тогда
		Если Узел.Строки.Количество() > КодЭлемента Тогда
			Возврат Узел.Строки.Получить(КодЭлемента);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Для Каждого Стр Из Узел.Строки Цикл
			Если Стр.Код = КодЭлемента Тогда
				Возврат Стр;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСодержательнуюЧасть(УзелРодитель, ПараметрыВыгрузки)
	
	ДанныеМногоуровневыхРазделов = Новый Структура;
	СохрСтруктура = СохраненныеДанныеОтчета();
	Для каждого ЭлементСтруктуры Из СохрСтруктура.ДанныеМногоуровневыхРазделов Цикл
		ДанныеМногоуровневыхРазделов.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;
	
	ДеревоДанныхРаздел1  = ДанныеМногоуровневыхРазделов["Раздел1"];
	ДеревоДанныхРаздел2  = ДанныеМногоуровневыхРазделов["Раздел2"];
	
	// Раздел 1.
	//
	НаимСРП = ПолучитьПодчиненныйЭлемент(УзелРодитель, "НаимСРП");
	ВывестиПоказательВXML(НаимСРП, ДеревоДанныхРаздел1.Строки[0].Данные["П000010000100"]);
	
	СумПУ = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СумПУ");
	
	ДанныеМногострочнойЧастиРаздела1
	= ДеревоДанныхРаздел1.Строки[0].ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппы1];
	
	Для каждого СтрокаМнЧРаздела1 Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
		
		КБК_Р1   = СтрокаМнЧРаздела1.Данные["П000010001003"];
		ОКТМО_Р1 = СтрокаМнЧРаздела1.Данные["П000010002003"];
		
		Если НЕ (ЗначениеЗаполнено(КБК_Р1) И ЗначениеЗаполнено(ОКТМО_Р1)) Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавление виртуального показателя.
		// Стр. 030 или 040.
		СтрокаМнЧРаздела1.Данные.Вставить("П100010003003",
		СтрокаМнЧРаздела1.Данные["П000010003003"] - СтрокаМнЧРаздела1.Данные["П000010004003"]);
		
		ЭкзСумПУ = СкопироватьУзел(УзелРодитель, СумПУ);
		Для каждого Эл Из ЭкзСумПУ.Строки Цикл
			Если Эл.Строки.Количество() = 0 Тогда
				ВывестиПоказательВXML(Эл, СтрокаМнЧРаздела1.Данные[Эл.Ключ]);
			КонецЕсли;
		КонецЦикла;
		
		// Раздел 2.
		//
		РасчПлатЗН = ПолучитьПодчиненныйЭлемент(ЭкзСумПУ, "РасчПлатЗН");
		
		Для каждого СтраницаРаздела Из ДеревоДанныхРаздел2.Строки Цикл
			
			КБК_Р2   = СтраницаРаздела.Данные["П000020001003"];
			ОКТМО_Р2 = СтраницаРаздела.Данные["П000020002003"];
			
			Если НЕ (КБК_Р1 = КБК_Р2 И ОКТМО_Р1 = ОКТМО_Р2) Тогда
				Продолжить;
			КонецЕсли;
			
			// Добавление виртуальных показателей.
			// Стр. 060.
			Числитель   = СтраницаРаздела.Данные["П000020006003_Числ"];
			Знаменатель = СтраницаРаздела.Данные["П000020006003_Знам"];
			Если ЗначениеЗаполнено(Числитель) И ЗначениеЗаполнено(Знаменатель) Тогда
				СтраницаРаздела.Данные.Вставить("П100020006003", Формат(Числитель, "ЧГ=") + "/" + Формат(Знаменатель, "ЧГ="));
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020006003", "");
			КонецЕсли;
			// Стр. 070.
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные["П000020007003_КодЛьготы"]);
			Основание = СокрЛП(СтраницаРаздела.Данные["П000020007003_Основание"]);
			Если КодЛьготы = "3022100" Тогда
				СтраницаРаздела.Данные.Вставить("П100020007003", КодЛьготы + "/" + Основание);
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020007003", КодЛьготы);
			КонецЕсли;
			// Стр. 090.
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные["П000020009003_КодЛьготы"]);
			Основание = СокрЛП(СтраницаРаздела.Данные["П000020009003_Основание"]);
			Если КодЛьготы = "3022300" Тогда
				СтраницаРаздела.Данные.Вставить("П100020009003", КодЛьготы + "/" + Основание);
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020009003", КодЛьготы);
			КонецЕсли;
			// Стр. 100.
			Числитель   = СтраницаРаздела.Данные["П000020010003_Числ"];
			Знаменатель = СтраницаРаздела.Данные["П000020010003_Знам"];
			Если ЗначениеЗаполнено(Числитель) И ЗначениеЗаполнено(Знаменатель) Тогда
				СтраницаРаздела.Данные.Вставить("П100020010003", Формат(Числитель, "ЧГ=") + "/" + Формат(Знаменатель, "ЧГ="));
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020010003", "");
			КонецЕсли;
			// Стр. 180.
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные["П000020018003_КодЛьготы"]);
			Основание = СокрЛП(СтраницаРаздела.Данные["П000020018003_Основание"]);
			Если КодЛьготы = "3022400" Тогда
				СтраницаРаздела.Данные.Вставить("П100020018003", КодЛьготы + "/" + Основание);
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020018003", КодЛьготы);
			КонецЕсли;
			// Стр. 220.
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные["П000020022003_КодЛьготы"]);
			Основание = СокрЛП(СтраницаРаздела.Данные["П000020022003_Основание"]);
			Если КодЛьготы = "3022200" Тогда
				СтраницаРаздела.Данные.Вставить("П100020022003", КодЛьготы + "/" + Основание);
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020022003", КодЛьготы);
			КонецЕсли;
			// Стр. 240.
			КодЛьготы = СокрЛП(СтраницаРаздела.Данные["П000020024003_КодЛьготы"]);
			Основание = СокрЛП(СтраницаРаздела.Данные["П000020024003_Основание"]);
			Если КодЛьготы = "3022500" Тогда
				СтраницаРаздела.Данные.Вставить("П100020024003", КодЛьготы + "/" + Основание);
			Иначе
				СтраницаРаздела.Данные.Вставить("П100020024003", КодЛьготы);
			КонецЕсли;
			
			ЗаполнитьДаннымиУзел(СтраницаРаздела.Данные, СкопироватьУзел(ЭкзСумПУ, РасчПлатЗН));
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			ЗначениеПоказателяСтр = Формат(ЗначениеПоказателя, "ДФ=dd.MM.yyyy");
		Иначе
			ЗначениеПоказателяСтр = СокрЛП(ЗначениеПоказателя);
		КонецЕсли;
		Узел.Значение = ?(МаксШирина < СтрДлина(ЗначениеПоказателяСтр), СокрЛП(Лев(ЗначениеПоказателяСтр, МаксШирина)), ЗначениеПоказателяСтр);
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (СтрНайти(Узел.Обязательность, "Н") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		ИначеЕсли (СтрНайти(Узел.Обязательность, "М") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
		И УзелПуст(Узел)
		И ?(СтрНайти(Узел.Обязательность, "О") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "O") <> 0, РегламентированнаяОтчетность.ИмеютсяАналогичныеСоседниеУзлыКлюч(Узел), Истина) Тогда
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УзелПуст(Узел)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если Узел.Формат = "S" Тогда
		Возврат Истина;
		
	ИначеЕсли Узел.Формат = "N" Тогда
		Возврат Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение));
	Иначе
		Возврат НЕ ЗначениеЗаполнено(Узел.Значение);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ИдентификаторФайлаВыгрузки(ПараметрыВыгрузки, ПрефиксИмени)
	
	ИдентификаторФайла = ПрефиксИмени
	+ "_" + ?(ЗначениеЗаполнено(ПараметрыВыгрузки.КодИФНСПосредника), ПараметрыВыгрузки.КодИФНСПосредника, ПараметрыВыгрузки.КодНО)
	+ "_" + ПараметрыВыгрузки.КодНО
	+ "_" + СокрЛП(ПараметрыВыгрузки.ИННОрг) + СокрЛП(ПараметрыВыгрузки.КПП)
	+ "_" + Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд")
	+ "_" + Строка(Новый УникальныйИдентификатор);
	
	Возврат ИдентификаторФайла;
	
КонецФункции

&НаСервере
Функция ВыгрузкаОтчета(ТекстВыгрузки, ПараметрыВыгрузки)
	
	КодировкаФайлаВыгрузки = "windows-1251";
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаФайлаВыгрузки);
	ЗаписьТекста.Записать(ТекстВыгрузки);
	ЗаписьТекста.Закрыть();
	ЗаписьТекста = Неопределено;
	
	ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	АдресВоВремХранилище = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор);
	
	УдалитьФайлы(ИмяВременногоФайла);
	
	РезультатВыгрузки = Новый Структура;
	РезультатВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
	РезультатВыгрузки.Вставить("АдресФайлаВыгрузки", АдресВоВремХранилище);
	РезультатВыгрузки.Вставить("ТипФайлаВыгрузки", "ДекларацияПоЗемельномуНалогу");
	РезультатВыгрузки.Вставить("КодировкаФайлаВыгрузки", КодировкаФайлаВыгрузки);
	
	Возврат РезультатВыгрузки;
	
КонецФункции

&НаСервере
Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки)
	
	ПотокXML = РегламентированнаяОтчетность.СоздатьНовыйПотокXML("windows-1251"); // создаем новый поток для записи
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, ПараметрыВыгрузки); // пишем дерево в поток
	ТекстДляЗаписи = ПотокXML.Закрыть(); // получаем текст XML
	Возврат ТекстДляЗаписи;
	
КонецФункции

&НаСервере
Функция ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, ПараметрыВыгрузки) Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, ПараметрыВыгрузки);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, ПараметрыВыгрузки);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьВыгрузкуОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчета()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуОтчетаЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму(
		"Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтаФорма);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.Проверить(СпДокОсн);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПоказатели() Экспорт
	
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML("СхемаВыгрузки505");
	
	Если НЕ ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗаполнитьДанными(ПараметрыВыгрузки, ДеревоВыгрузки);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область УправлениеНастройкамиОтчета

&НаКлиенте
Процедура ОткрытьФормуНастроек(Команда)
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)
	Перем ТекущийРазделОтчетаСокрНаим;
	Перем НомерСтраницыРазделаОтчета;
	
	// Запоминаем текущий раздел, установленный в дереве разделов отчета.
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРазделОтчетаСокрНаим = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		НомерСтраницыРазделаОтчета  = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	КонецЕсли;
	
	мПараметры = Новый Структура;
	мПараметры.Вставить("ПроверкаСоотношений", Истина);
	мПараметры.Вставить("Автосохранение", Ложь);
	мПараметры.Вставить("ИмеетсяРеквизитДеревоВыбранныхСтраниц", Истина);
	мПараметры.Вставить("ИмяОтчета", ИДОтчета(ЭтаФорма.ИмяФормы));
	
	Если СтруктураРеквизитовФормы.ОтображатьКнопкуРасшифровать Тогда
		мПараметры.Вставить("СохранятьРасшифровкуАвтозаполнения", Истина);
	КонецЕсли;
	Если СтруктураРеквизитовФормы.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417 Тогда
		мПараметры.Вставить("РазрешитьВстроеннуюПечать", Истина);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	ПараметрыФормы.Вставить("мПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена);
	ПараметрыФормы.Вставить("мАвтоВыборКодов", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	ПараметрыФормы.Вставить("мСчетчикСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	ПараметрыФормы.Вставить("ЕдиницаИзмерения", СтруктураРеквизитовФормы.ЕдиницаИзмерения);
	ПараметрыФормы.Вставить("ТочностьЕдиницыИзмерения", СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения);
	ПараметрыФормы.Вставить("СохранятьРасшифровкуАвтозаполнения", СтруктураРеквизитовФормы.мСохранятьРасшифровку);
	ПараметрыФормы.Вставить("мПараметры", мПараметры);
	ПараметрыФормы.Вставить("мПроверятьСоотношенияПриПечатиИВыгрузки", 
		СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки);
	
	ФормаНастройкиОтчета = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("НастройкиОтчета", ПараметрыФормы, ЭтаФорма);

	мДеревоВыбранныхСтраниц.ПолучитьЭлементы().Очистить();

	КопироватьДанныеФормы(мДеревоСтраницОтчета, ФормаНастройкиОтчета.Дерево);
	
	ЗначениеАвторасчета = СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет;
	ЗначениеЕдИзм       = СтруктураРеквизитовФормы.ЕдиницаИзмерения;
	ЗначениеЗпт         = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	
	ДополнительныеПараметры = Новый Структура(
	"ЗначениеАвторасчета, ЗначениеЕдИзм, ЗначениеЗпт, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим",
	ЗначениеАвторасчета, ЗначениеЕдИзм, ЗначениеЗпт, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраницЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ФормаНастройкиОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаНастройкиОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаНастройкиОтчета.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраницЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ЗначениеАвторасчета = ДополнительныеПараметры.ЗначениеАвторасчета;
	ЗначениеЕдИзм = ДополнительныеПараметры.ЗначениеЕдИзм;
	ЗначениеЗпт = ДополнительныеПараметры.ЗначениеЗпт;
	НомерСтраницыРазделаОтчета = ДополнительныеПараметры.НомерСтраницыРазделаОтчета;
	ТекущийРазделОтчетаСокрНаим = ДополнительныеПараметры.ТекущийРазделОтчетаСокрНаим;
	
	РегламентированнаяОтчетностьКлиентСервер.ПоказатьКнопкиПечати(ЭтаФорма);
	
	Модифицированность = Истина;
	
	флЗначенияИдентичны = Истина;
	
	Для Индекс = 0 По мДеревоСтраницОтчета.ПолучитьЭлементы().Количество() - 1 Цикл
		
		Если НЕ мДеревоСтраницОтчета.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу = мДеревоВыбранныхСтраниц.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу Тогда
			флЗначенияИдентичны = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеАвторасчета <> СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
		
		Если СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Ложь Тогда
			
			ДополнительныеПараметры = Новый Структура(
			"НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт",
			НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт);
			ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВопросПерерасчетВсехРассчитываемыхПоказателейЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ТекстВопроса = НСтр("ru='Будет произведен перерасчет всех рассчитываемых показателей. Продолжить?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			
		Иначе
			
			УстановитьДоступностьОбластей(ЭтаФорма, ТекущийРазделОтчетаСокрНаим);
			
			ДопПараметры = Новый Структура(
			"НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт",
			НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт);
			ПослеОткрытияФормыВыбораСтраниц(ДопПараметры);
			
		КонецЕсли;
		
	Иначе
		
		ДопПараметры = Новый Структура(
		"НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт",
		НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт);
		ПослеОткрытияФормыВыбораСтраниц(ДопПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПерерасчетВсехРассчитываемыхПоказателейЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ЗначениеЕдИзм = ДополнительныеПараметры.ЗначениеЕдИзм;
	ЗначениеЗпт = ДополнительныеПараметры.ЗначениеЗпт;
	НомерСтраницыРазделаОтчета = ДополнительныеПараметры.НомерСтраницыРазделаОтчета;
	ТекущийРазделОтчетаСокрНаим = ДополнительныеПараметры.ТекущийРазделОтчетаСокрНаим;
	флЗначенияИдентичны = ДополнительныеПараметры.флЗначенияИдентичны;
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Истина;
		Возврат;
	КонецЕсли;
	
	РасчетНаСервере();
	
	УстановитьДоступностьОбластей(ЭтаФорма, ТекущийРазделОтчетаСокрНаим);
	
	ДопПараметры = Новый Структура(
	"НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт",
	НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим, флЗначенияИдентичны, ЗначениеЕдИзм, ЗначениеЗпт);
	ПослеОткрытияФормыВыбораСтраниц(ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОткрытияФормыВыбораСтраниц(ДополнительныеПараметры)
	
	НомерСтраницыРазделаОтчета = ДополнительныеПараметры.НомерСтраницыРазделаОтчета;
	ТекущийРазделОтчетаСокрНаим = ДополнительныеПараметры.ТекущийРазделОтчетаСокрНаим;
	флЗначенияИдентичны = ДополнительныеПараметры.флЗначенияИдентичны;
	ЗначениеЕдИзм = ДополнительныеПараметры.ЗначениеЕдИзм;
	ЗначениеЗпт = ДополнительныеПараметры.ЗначениеЗпт;
	
	Если ЗначениеЕдИзм <> СтруктураРеквизитовФормы.ЕдиницаИзмерения
		ИЛИ ЗначениеЗпт <> СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения Тогда
		ПослеСменыЕдИзмеренияИТочности();
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПоказатьСтраницыОтчетаНаКлиенте(ЭтаФорма);
	
	Если НЕ флЗначенияИдентичны Тогда
		
		СформироватьДеревоРазделовОтчетаНаСервере();
		
		Если НЕ ТекущийРазделОтчетаСокрНаим = Неопределено Тогда
			
			НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(
				РазделыОтчета.ПолучитьЭлементы(), "КолонкаРазделыОтчетаСокрНаим", ТекущийРазделОтчетаСокрНаим);
			
			Если НЕ НайденнаяСтрока = Неопределено И НЕ НомерСтраницыРазделаОтчета = Неопределено
				И НайденнаяСтрока.ПолучитьЭлементы().Количество() > 0 Тогда
				
				НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(
				НайденнаяСтрока.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", НомерСтраницыРазделаОтчета);
				
			КонецЕсли;
			
			Если НЕ НайденнаяСтрока = Неопределено Тогда
				
				Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСменыЕдИзмеренияИТочности()
	
	ПроверитьТочность();
	
	УстановитьФорматВыводаИРассчитатьНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТочность()
	
	Если СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1 Тогда
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения > 2 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: при выводе сумм в рублях точность не может превышать 2 знака'"), ЭтаФорма.Заголовок);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 2;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000 Тогда
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения > 3 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: при выводе сумм в тысячах рублей точность не может превышать 3 знака'"), ЭтаФорма.Заголовок);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 3;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000000 Тогда
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения > 6 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: при выводе сумм в миллионах рублей точность не может превышать 6 знаков'"), ЭтаФорма.Заголовок);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 6;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматВыводаИРассчитатьНаСервере(ИмяРаздела)
	
	УстановитьФорматВыводаНаСервере(ИмяРаздела);
	
	РасчетНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматВыводаНаСервере(ИмяРаздела)
	
	Если СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1 Тогда
		СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = " + СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0";
	ИначеЕсли СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000 Тогда
		СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = " + СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3";
	ИначеЕсли СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000000 Тогда
		СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = " + СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6";
	КонецЕсли;
	
	ИменаМногострочныхПоказателей   = Новый Массив;
	ИменаНемногострочныхПоказателей = Новый Массив;
	
	Если ИмяРаздела = "Раздел1" Тогда
		ИменаМногострочныхПоказателей.Добавить("П000010002103");
		ИменаМногострочныхПоказателей.Добавить("П000010002303");
		ИменаМногострочныхПоказателей.Добавить("П000010002503");
		ИменаМногострочныхПоказателей.Добавить("П000010002703");
		ИменаМногострочныхПоказателей.Добавить("П000010003003");
		ИменаМногострочныхПоказателей.Добавить("П000010004003");
	ИначеЕсли ИмяРаздела = "Раздел2" Тогда
		ИменаНемногострочныхПоказателей.Добавить("П000020005003");
		ИменаНемногострочныхПоказателей.Добавить("П000020008003");
		ИменаНемногострочныхПоказателей.Добавить("П000020011003");
		ИменаНемногострочныхПоказателей.Добавить("П000020015003");
		ИменаНемногострочныхПоказателей.Добавить("П000020019003");
		ИменаНемногострочныхПоказателей.Добавить("П000020021003");
		ИменаНемногострочныхПоказателей.Добавить("П000020023003");
		ИменаНемногострочныхПоказателей.Добавить("П000020025003");
	КонецЕсли;
	
	Для каждого ИмяПоказателя Из ИменаНемногострочныхПоказателей Цикл
		ЧислоЕЗнак = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(17, 2));
		ФорматируемаяОбласть = ТабличныйДокумент.Области[ИмяПоказателя];
		ДопустимыйЗнакПоказателя = ФорматируемаяОбласть.ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак;
		Если ДопустимыйЗнакПоказателя = ДопустимыйЗнак.Неотрицательный Тогда
			ЧислоЕЗнак = Новый ОписаниеТипов("Число",
			Новый КвалификаторыЧисла(ЧислоЕЗнак.КвалификаторыЧисла.Разрядность,
			ЧислоЕЗнак.КвалификаторыЧисла.РазрядностьДробнойЧасти, ДопустимыйЗнакПоказателя));
		КонецЕсли;
		ФорматируемаяОбласть.ТипЗначения = ЧислоЕЗнак;
		ФорматируемаяОбласть.Формат = "ЧЦ = " + ЧислоЕЗнак.КвалификаторыЧисла.Разрядность + СтруктураРеквизитовФормы.мСтрокаФормата;
	КонецЦикла;
	
	Для каждого ИмяПоказателя Из ИменаМногострочныхПоказателей Цикл
		НСтр = 1;
		Пока ТабличныйДокумент.Области.Найти(ИмяПоказателя + "_" + Формат(НСтр, "ЧГ=")) <> Неопределено Цикл
			ЧислоЕЗнак = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(17, 2));
			ФорматируемаяОбласть = ТабличныйДокумент.Области[ИмяПоказателя + "_" + Формат(НСтр, "ЧГ=")];
			ДопустимыйЗнакПоказателя = ФорматируемаяОбласть.ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак;
			Если ДопустимыйЗнакПоказателя = ДопустимыйЗнак.Неотрицательный Тогда
				ЧислоЕЗнак = Новый ОписаниеТипов("Число",
				Новый КвалификаторыЧисла(ЧислоЕЗнак.КвалификаторыЧисла.Разрядность,
				ЧислоЕЗнак.КвалификаторыЧисла.РазрядностьДробнойЧасти, ДопустимыйЗнакПоказателя));
			КонецЕсли;
			ФорматируемаяОбласть.ТипЗначения = ЧислоЕЗнак;
			ФорматируемаяОбласть.Формат = "ЧЦ = " + ЧислоЕЗнак.КвалификаторыЧисла.Разрядность + СтруктураРеквизитовФормы.мСтрокаФормата;
			НСтр = НСтр + 1;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Очистка

#Область ОчисткаОтчета

&НаКлиенте
Процедура ОчиститьОтчет(Команда)
	
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(
		ЭтаФорма, "ОчиститьВесьОтчет");
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(ВыполняемоеОповещение = Неопределено) Экспорт
	
	РегламентированнаяОтчетностьКлиент.Очистить(ЭтаФорма, ВыполняемоеОповещение);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере() Экспорт
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела <> "Титульный" Тогда
		ТабличныйДокумент.Очистить();
	КонецЕсли;
	
	ИнициализироватьМногоуровневыеРазделы();
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	ОбновитьДеревоРазделовВФормеОтчета(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела.Очистить();
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела.Добавить(1);
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела <> "Титульный" Тогда
		ВывестиДанныеВТабличныйДокумент(СтруктураРеквизитовФормы.НаимТекущегоРаздела,
		СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоРазделовВФормеОтчета(ИмяРаздела)
	
	Если НЕ ИмяРаздела = Неопределено Тогда
		
		// Пытаемся найти ранее запомненный раздел.
		НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(
			РазделыОтчета.ПолучитьЭлементы(), "КолонкаРазделыОтчетаСокрНаим", ИмяРаздела);
		
		// В случае успешного поиска, устанавливаем курсор на найденный раздел.
		Если НЕ НайденнаяСтрока = Неопределено Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОчисткаТекущейСтраницы

&НаКлиенте
Процедура ОчиститьТекущуюСтраницу(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРаздела", СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", Новый ОписаниеОповещения("ОчиститьТекущуюСтраницуЗавершение", ЭтотОбъект));
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='Внимание! Будет очищена текущая страница.%1Продолжить?'"), Символы.ПС);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОчиститьТекущуюСтраницуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОчиститьТекущуюСтраницуЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	ИмяРаздела = ДополнительныеПараметры.ИмяРаздела;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ОчисткаРегламентированногоОтчетаНаСервере(ИмяРаздела);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчисткаРегламентированногоОтчетаНаСервере(ИмяСтраницы) Экспорт
	
	Если ИмяСтраницы = "Титульный" Тогда
		
		Для каждого Обл Из ТабличныйДокумент.Области Цикл
			Если СтрЧислоВхождений(Тип(Обл), "Рисунок") = 0
				И Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
				И Обл.СодержитЗначение = Истина
				И НЕ Обл.Защита Тогда
				Обл.Очистить();
			КонецЕсли;
		КонецЦикла;
		
		Обл = ТабличныйДокумент.Области.Найти("ДатаПодписи");
		Если ЗначениеЗаполнено(Обл.Значение) Тогда
			ДатаПодписи = Лев(СтрЗаменить(Формат(Обл.Значение, "ДФ = ""дд.ММ.гггг 'г.'"""), ".", ""), 8);
		Иначе
			ДатаПодписи = "        ";
		КонецЕсли;
		
	Иначе
		
		ТабличныйДокумент.Очистить();
		ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
		ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяСтраницы));
		
		ВывестиДанныеВТабличныйДокументНаСервере(ИмяСтраницы,
		СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела);
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекущуюСтраницуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		НомерКорректировки = 0;
		ТабличныйДокумент.Области.НомерКорректировки.Значение = "0";
		СтруктураРеквизитовФормы.ВидДокумента = ?(НомерКорректировки = 0, 0, 1);
		УстановитьДоступностьОбластей(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	Иначе
		Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел2" Тогда
			СтруктПарам = Новый Структура;
			СтруктПарам.Вставить("КадастрНомер", ТабличныйДокумент.Область("П000020000100").Значение);
			СтруктПарам.Вставить("НомерСтраницы",
			Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
			Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчета = ПредставлениеСтраницыРаздела2(СтруктПарам);
		КонецЕсли;
		СпрятатьСсылкуУдалитьСтраницу();
		Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Область(1, 1, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Печать

&НаКлиенте
Процедура ПоказатьСДвухмернымШтрихкодомPDF417(Команда)
	
	РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФорму(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Отказ = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьБланкЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланкЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Команда = ДополнительныеПараметры.Команда;
	
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
		"ru='%1. Формируется печатная форма...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Печать);
	
	Если СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки = Истина Тогда // возможны 3 состояния флага
		РегламентированнаяОтчетностьКлиент.ПроверитьКонтрольныеСоотношения(ЭтаФорма, ОбработкаКС, Истина);
	КонецЕсли;
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(
		ЭтаФорма, ВидПечати, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
	
	Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");
		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.ЕдиницаИзмерения <> ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1
		ИЛИ СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения <> 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Печать возможна только при установленной единице измерения ""в рублях"" и точности ""0"".'");
		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ТипЗнч(мДеревоВыбранныхСтраниц) <> Тип("ДанныеФормыДерево") Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
	СтруктураРеквизитовФормы.мРежимПечати = Истина;
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	сохрТаблДок = ТабличныйДокумент.ПолучитьОбласть();
	
	сохрНомераСтрокМногоуровнегоРаздела = СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела;
	
	СтрПоля = "ПолеСверху, ПолеСправа, ПолеСнизу, ПолеСлева";
	ВремПоля = Новый Структура(СтрПоля);
	
	ЗаполнитьЗначенияСвойств(ВремПоля, ТабличныйДокумент, СтрПоля);
	ВывестиДанныеВТабличныйДокумент("Титульный");
	
	ОбщиеРеквизитыРазделов = ПолучитьОбщиеРеквизитыСТитульногоЛиста();
	
	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.ПолучитьЭлементы() Цикл
		
		Если СтрокаУровня1.ВыводНаПечать = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("НаименованиеЛиста",  СтрокаУровня1.Представление);
		СтруктураПараметров.Вставить("ОриентацияЛиста",    СтрокаУровня1.ОриентацияСтраницы);
		СтруктураПараметров.Вставить("ИмяСтраницы",        СтрокаУровня1.ИмяСтраницы);
		Структурапараметров.Вставить("ИмяОбластиТаблПоля", СтрокаУровня1.ИмяОбластиТабличногоПоля);
		СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
		
		Если мСтруктураМногоуровневыхРазделов.Свойство(СтрокаУровня1.ИмяСтраницы) Тогда
			
			ВывестиНаПечатьДанныеМногоуровневогоРаздела(СтруктураПараметров, ОбщиеРеквизитыРазделов, СтрПоля);
			
		Иначе
			
			ВывестиДанныеВТабличныйДокумент(СтрокаУровня1.ИмяСтраницы);
			ВывестиНаПечатьЛист(СтруктураПараметров, ОбщиеРеквизитыРазделов, СтрПоля);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела = сохрНомераСтрокМногоуровнегоРаздела;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(сохрТаблДок);
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ВремПоля, СтрПоля);
	
	РегламентированнаяОтчетностьКлиентСервер.ПроставлениеНомеровЛистов(ЭтаФорма, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	КопироватьДанныеФормы(мДеревоВыбранныхСтраниц, мДеревоСтраницОтчета);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ВывестиНаПечатьДанныеМногоуровневогоРаздела(СтруктураПараметров, ОбщиеРеквизитыРазделов, СтрПоля)
	
	ИмяТекРаздела = СтруктураПараметров.ИмяСтраницы;
	
	ДеревоДанныхРаздела = ПолучитьИзВременногоХранилища(
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяТекРаздела]);
	
	Если ИмяТекРаздела = "Раздел1" Тогда
		
		МакетРаздела = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
		ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяТекРаздела);
		
		НемногострочныеДанные = ДеревоДанныхРаздела.Строки[0].Данные;
		
		ДанныеМнЧ1 = ДеревоДанныхРаздела.Строки[0].ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппы1];
		
		СтрокМнЧ1НаЛисте = 2;
		
		// Определение количества листов.
		КолСтрокМнЧ1 = ДанныеМнЧ1.Строки.Количество();
		ДробКол1 = КолСтрокМнЧ1 / СтрокМнЧ1НаЛисте;
		ЦелКол1  = Цел(КолСтрокМнЧ1 / СтрокМнЧ1НаЛисте);
		КолЛистовРаздела1 = ?(ДробКол1 = ЦелКол1, ЦелКол1, ЦелКол1 + 1);
		
		// Подготовка многострочных данных для заполнения листов.
		МассивДанныхМнЧ1ДляПечати = Новый Массив;
		ИмяП000010001003 = "П000010001003";
		ИмяП000010002003 = "П000010002003";
		ИмяП000010002103 = "П000010002103";
		ИмяП000010002303 = "П000010002303";
		ИмяП000010002503 = "П000010002503";
		ИмяП000010002703 = "П000010002703";
		ИмяП000010003003 = "П000010003003";
		ИмяП000010004003 = "П000010004003";
		
		Для НомЛиста = 1 По КолЛистовРаздела1 Цикл
			
			Приращение1КНомеру = (НомЛиста - 1) * СтрокМнЧ1НаЛисте;
			ДанныеМнЧ1ДляПечати = Новый Структура;
			Для ИндСтрокиМнЧ = 1 По СтрокМнЧ1НаЛисте Цикл
				_ИндСтрокиМнЧ = СтрокаСИндексомПоказателя(ИндСтрокиМнЧ);
				ИндСПриращением = ИндСтрокиМнЧ + Приращение1КНомеру;
				
				НовИмяП000010001003 = ИмяП000010001003 + _ИндСтрокиМнЧ;
				НовИмяП000010002003 = ИмяП000010002003 + _ИндСтрокиМнЧ;
				НовИмяП000010002103 = ИмяП000010002103 + _ИндСтрокиМнЧ;
				НовИмяП000010002303 = ИмяП000010002303 + _ИндСтрокиМнЧ;
				НовИмяП000010002503 = ИмяП000010002503 + _ИндСтрокиМнЧ;
				НовИмяП000010002703 = ИмяП000010002703 + _ИндСтрокиМнЧ;
				НовИмяП000010003003 = ИмяП000010003003 + _ИндСтрокиМнЧ;
				НовИмяП000010004003 = ИмяП000010004003 + _ИндСтрокиМнЧ;
				
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010001003, "");
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002003, "");
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002103, 0);
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002303, 0);
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002503, 0);
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002703, 0);
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010003003, 0);
				ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010004003, 0);
				
				Если КолСтрокМнЧ1 >= ИндСПриращением Тогда
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010001003,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010001003]);
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002003,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010002003]);
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002103,
					РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010002103]));
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002303,
					РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010002303]));
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002503,
					РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010002503]));
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010002703,
					РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010002703]));
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010003003,
					РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010003003]));
					ДанныеМнЧ1ДляПечати.Вставить(НовИмяП000010004003,
					РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма,
					ДанныеМнЧ1.Строки[ИндСПриращением - 1].Данные[ИмяП000010004003]));
				КонецЕсли;
			КонецЦикла;
			МассивДанныхМнЧ1ДляПечати.Добавить(ДанныеМнЧ1ДляПечати);
			
		КонецЦикла;
		
		// Вывод листов.
		Для НомЛиста = 1 По КолЛистовРаздела1 Цикл
			
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(МакетРаздела);
			
			Для каждого Пок Из НемногострочныеДанные Цикл
				Обл = ТабличныйДокумент.Области.Найти(Пок.Ключ);
				Если Обл <> Неопределено Тогда
					Обл.Значение = Пок.Значение;
				КонецЕсли;
			КонецЦикла;
			
			Для Ном = 1 По СтрокМнЧ1НаЛисте - 1 Цикл
				ИмяОбластиПреемника = "ДобавлениеСтроки"
				+ СтруктураРеквизитовФормы.мИдГруппы1 + СтрокаСИндексомПоказателя(Ном);
				ВставитьДопСтроку(СтруктураРеквизитовФормы.мИдГруппы1, ИмяОбластиПреемника);
			КонецЦикла;
			
			ДанныеМнЧ1ДляПечати = МассивДанныхМнЧ1ДляПечати[НомЛиста - 1];
			Для каждого Пок Из ДанныеМнЧ1ДляПечати Цикл
				Обл = ТабличныйДокумент.Области.Найти(Пок.Ключ);
				Если Обл <> Неопределено Тогда
					Обл.Значение = Пок.Значение;
				КонецЕсли;
			КонецЦикла;
			
			СтруктураПараметровРаздела = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПараметров);
			СтруктураПараметровРаздела.Вставить("ИмяСтраницы", ИмяТекРаздела);
			ВывестиНаПечатьЛист(СтруктураПараметровРаздела, ОбщиеРеквизитыРазделов, СтрПоля);
			
		КонецЦикла;
		
	ИначеЕсли ИмяТекРаздела = "Раздел2" Тогда
		
		МакетРаздела = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
		ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяТекРаздела);
		
		Для каждого Ветка Из ДеревоДанныхРаздела.Строки Цикл
			
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(МакетРаздела);
			
			НемногострочныеДанные = Ветка.Данные;
			Для каждого Пок Из НемногострочныеДанные Цикл
				Обл = ТабличныйДокумент.Области.Найти(Пок.Ключ);
				Если Обл <> Неопределено Тогда
					Обл.Значение = Пок.Значение;
				КонецЕсли;
			КонецЦикла;
			
			УстановитьФорматВыводаНаСервере(ИмяТекРаздела);
			
			СтруктураПараметровРаздела = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПараметров);
			СтруктураПараметровРаздела.Вставить("ИмяСтраницы", ИмяТекРаздела);
			ВывестиНаПечатьЛист(СтруктураПараметровРаздела, ОбщиеРеквизитыРазделов, СтрПоля);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДеревоДанныхРаздела = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиНаПечатьЛист(Парам1, ОбщиеРеквизитыРазделов, СтрПоля = Неопределено)
	
	ПрефиксИменМакетов = "ПечатныйБланк2018Кв1_";
	
	ИмяМЧБ = ПрефиксИменМакетов + Парам1.ИмяСтраницы;
	
	МакетМЧБ = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(ИмяМЧБ);
	МакетМЧБ.ПолеСверху = 5;
	МакетМЧБ.ПолеСправа = 5;
	МакетМЧБ.ПолеСнизу  = 5;
	МакетМЧБ.ПолеСлева  = 5;
	
	ВремПоля = Новый Структура(СтрПоля);
	
	ЗаполнитьМакетМЧБ(Парам1, МакетМЧБ, ОбщиеРеквизитыРазделов);
	
	ЗаполнитьЗначенияСвойств(ВремПоля, ТабличныйДокумент, СтрПоля);
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(МакетМЧБ);
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, МакетМЧБ, СтрПоля, "Макет");
	
	МакетМЧБ = Неопределено;
	
	РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая(ЭтаФорма.Заголовок, Парам1, ТабличныйДокумент, СтруктураРеквизитовФормы.мПечатныеФормы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМакетМЧБ(ПараметрыПечати, МакетМЧБ, ОбщиеРеквизитыРазделов)
	
	Показатели = Новый ТаблицаЗначений;
	Показатели.Колонки.Добавить("Имя");
	Показатели.Колонки.Добавить("Значение");
	Показатели.Колонки.Добавить("ЗначениеМЧБ");
	
	Показатели.Индексы.Добавить("Имя");
	
	ИсходныйМакет = ТабличныйДокумент;
	Для Каждого Область Из ИсходныйМакет.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Область.СодержитЗначение = Истина Тогда
			НовСтр = Показатели.Добавить();
			НовСтр.Имя = Область.Имя;
			НовСтр.Значение = Область.Значение;
			НовСтр.ЗначениеМЧБ = ПолучитьЗначениеМЧБ(Область);
		КонецЕсли;
	КонецЦикла;
	
	НайденноеЗначение = Показатели.Найти("ИНН", "Имя");
	Если НЕ НайденноеЗначение = Неопределено Тогда
		Показатели.Удалить(НайденноеЗначение);
	КонецЕсли;
	ИНН = "";
	Для Ном = 0 По 9 Цикл
		ИНН = ИНН + ОбщиеРеквизитыРазделов.ИНН[Ном];
	КонецЦикла;
	НовСтр = Показатели.Добавить();
	НовСтр.Имя = "ИНН";
	НовСтр.Значение = ИНН;
	НовСтр.ЗначениеМЧБ = СокрЛП(ИНН);
	НовСтр = Показатели.Добавить();
	НовСтр.Имя = "ИНН1";
	НовСтр.Значение = ИНН;
	НовСтр.ЗначениеМЧБ = СокрЛП(ИНН);
	
	НайденноеЗначение = Показатели.Найти("КПП", "Имя");
	Если НЕ НайденноеЗначение = Неопределено Тогда
		Показатели.Удалить(НайденноеЗначение);
	КонецЕсли;
	КПП = "";
	Для Ном = 0 По 8 Цикл
		КПП = КПП + ОбщиеРеквизитыРазделов.КПП[Ном];
	КонецЦикла;
	НовСтр = Показатели.Добавить();
	НовСтр.Имя = "КПП";
	НовСтр.Значение = КПП;
	НовСтр.ЗначениеМЧБ = СокрЛП(КПП);
	НовСтр = Показатели.Добавить();
	НовСтр.Имя = "КПП1";
	НовСтр.Значение = КПП;
	НовСтр.ЗначениеМЧБ = СокрЛП(КПП);
	
	НайденноеЗначение = Показатели.Найти("ДатаПодписи", "Имя");
	Если НЕ НайденноеЗначение = Неопределено Тогда
		Показатели.Удалить(НайденноеЗначение);
	КонецЕсли;
	ДатаПодписиСтр = "";
	Для Ном = 0 По 7 Цикл
		ДатаПодписиСтр = ДатаПодписиСтр + ОбщиеРеквизитыРазделов.ДатаПодписи[Ном];
	КонецЦикла;
	НовСтр = Показатели.Добавить();
	НовСтр.Имя = "ДатаПодписи";
	НовСтр.Значение = ДатаПодписиСтр;
	НовСтр.ЗначениеМЧБ = СокрЛП(ДатаПодписиСтр);
	
	НовСтр = Показатели.Добавить();
	НовСтр.Имя = "ДатаПодписиПолная";
	Если СокрЛП(ДатаПодписиСтр) <> "" Тогда
		НовСтр.Значение = Формат(Дата(Прав(ДатаПодписиСтр, 4) + Сред(ДатаПодписиСтр, 3, 2) + Лев(ДатаПодписиСтр, 2) + "000000"), "ДФ = ""дд.ММ.гггг""");
		НовСтр.ЗначениеМЧБ = СокрЛП(Формат(Дата(Прав(ДатаПодписиСтр, 4) + Сред(ДатаПодписиСтр, 3, 2) + Лев(ДатаПодписиСтр, 2) + "000000"), "ДФ = ""дд.ММ.гггг"""));
	КонецЕсли;
	
	// Особая обработка ФИО на титульном листе.
	Если ПараметрыПечати.ИмяСтраницы = "Титульный" Тогда
		ПокПодписант = Показатели.Найти("ОргПодписант", "Имя");
		Если ПокПодписант <> Неопределено Тогда
			ФИО = РегламентированнаяОтчетность.РазложитьФИО(ПокПодписант.Значение);
			НовСтр = Показатели.Добавить();
			НовСтр.Имя = "ОргПодписантФамилия";
			НовСтр.Значение = ФИО.Фамилия;
			НовСтр.ЗначениеМЧБ = ФИО.Фамилия;
			НовСтр = Показатели.Добавить();
			НовСтр.Имя = "ОргПодписантИмя";
			НовСтр.Значение = ФИО.Имя;
			НовСтр.ЗначениеМЧБ = ФИО.Имя;
			НовСтр = Показатели.Добавить();
			НовСтр.Имя = "ОргПодписантОтчество";
			НовСтр.Значение = ФИО.Отчество;
			НовСтр.ЗначениеМЧБ = ФИО.Отчество;
		КонецЕсли;
	КонецЕсли;
	
	// Особая обработка ИНН.
	ИНН = Новый Массив(12);
	Для Инд = 1 По 9 Цикл
		Для Знакоместо = 0 По 11 Цикл
			ИНН[Знакоместо] = Показатели.Найти("ИНН" + Инд + "_" + (Знакоместо + 1), "Имя");
		КонецЦикла;
		Если ИНН[0] = Неопределено ИЛИ ИНН[1] = Неопределено ИЛИ ИНН[10] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Показатель Из Показатели Цикл
		
		Если СтрНачинаетсяС(Показатель.Имя,"П000010002003") ИЛИ Показатель.Имя = "П000020002003" Тогда
			Показатель.ЗначениеМЧБ = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(
			СокрЛП(Показатель.Значение), 11, "-", 1);
		КонецЕсли;
		
	КонецЦикла;
	
	ИндексТочкиПоКорню = Новый Структура;
	Для каждого Область Из МакетМЧБ.Области Цикл
		Если Область.Текст = "." Тогда
			ТекАдресЗначенияОбласти = АдресЗначенияОбласти(Область);
			Корень = ТекАдресЗначенияОбласти.Корень;
			ИндексТочкиЧислаНаМЧБ = ТекАдресЗначенияОбласти.ИндексЗнакоместа;
			ИндексТочкиПоКорню.Вставить(Корень,ИндексТочкиЧислаНаМЧБ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Область Из МакетМЧБ.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			ИмяОбласти = Область.Имя;
			
			Если СтрЧислоВхождений(ИмяОбласти, "НомСтр") > 0
			 ИЛИ СтрЧислоВхождений(ИмяОбласти, "СоставленаНа") > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			РезультатПоиска = Показатели.Найти(ИмяОбласти, "Имя");
			Если РезультатПоиска = Неопределено Тогда
				ТекАдресЗначенияОбласти = АдресЗначенияОбласти(Область);
				ЗначениеВыведено = Ложь;
				Если ТекАдресЗначенияОбласти.ИндексЗнакоместа <> 0 Тогда
					РезультатПоиска = Показатели.Найти(ТекАдресЗначенияОбласти.Корень, "Имя");
					Если РезультатПоиска <> Неопределено Тогда
						Если ТипЗнч(РезультатПоиска.Значение)=Тип("Число") Тогда
							//разделение числа на 2 части
							ЦелаяЧасть = Цел(РезультатПоиска.Значение);
							СтрЦелаяЧасть = СтрЗаменить(Строка(Формат(Цел(РезультатПоиска.Значение), "ЧРГ=';'")), ";", "");
							Если РезультатПоиска.Значение < 0 И СтрНайти(РезультатПоиска.ЗначениеМЧБ, ".") > 0 Тогда
								СтрДробнаяЧасть = Сред(Строка(РезультатПоиска.Значение - ЦелаяЧасть),4);
							Иначе
								СтрДробнаяЧасть = Сред(Строка(РезультатПоиска.Значение - ЦелаяЧасть),3);
							КонецЕсли;
							Если СтрЦелаяЧасть <> "" ИЛИ СтрДробнаяЧасть <> "" Тогда
								СтрЦелаяЧасть = ?(СтрЦелаяЧасть = "", "0", СтрЦелаяЧасть);
								Если РезультатПоиска.Значение < 0 Тогда
									СтрЦелаяЧасть = ?(СтрЦелаяЧасть = "0", "-0", СтрЦелаяЧасть);
								КонецЕсли;
								СтрДробнаяЧасть = ?(СтрДробнаяЧасть = "", "0", СтрДробнаяЧасть);
							КонецЕсли;
							//разделение областей показателя на 2 части
							ИндексТочкиЧислаНаМЧБ = Неопределено;
							Если ИндексТочкиПоКорню.Свойство(ТекАдресЗначенияОбласти.Корень,ИндексТочкиЧислаНаМЧБ) = Ложь Тогда
								//На МЧБ нет разделения на целую и дробную части
								ПрисвоитьЗначениеОбласти(Область, РезультатПоиска.ЗначениеМЧБ, ТекАдресЗначенияОбласти.ИндексЗнакоместа);
								ЗначениеВыведено = Истина;
							Иначе
								//определяем какая часть выводится (Целая, дробная или точка)
								Если ТекАдресЗначенияОбласти.ИндексЗнакоместа<ИндексТочкиЧислаНаМЧБ Тогда
									//выводится целая часть
									ПрисвоитьЗначениеОбласти(Область, ?(РезультатПоиска.ЗначениеМЧБ = "-","-",СтрЦелаяЧасть), ТекАдресЗначенияОбласти.ИндексЗнакоместа);
									ЗначениеВыведено = Истина;
								ИначеЕсли ТекАдресЗначенияОбласти.ИндексЗнакоместа > ИндексТочкиЧислаНаМЧБ Тогда
									//выводится дробная часть
									ПрисвоитьЗначениеОбласти(Область, СтрДробнаяЧасть, ТекАдресЗначенияОбласти.ИндексЗнакоместа - ИндексТочкиЧислаНаМЧБ);
									ЗначениеВыведено = Истина;
								Иначе
									//выводится точка
									//присваивать ничего не будем, но считаем, что область заполненяется корректно
									ЗначениеВыведено = Истина;
								КонецЕсли;
							КонецЕсли;
						Иначе
							ПрисвоитьЗначениеОбласти(Область, РезультатПоиска.ЗначениеМЧБ, ТекАдресЗначенияОбласти.ИндексЗнакоместа, ТипЗнч(РезультатПоиска.Значение) <> Тип("Строка") ИЛИ НЕ ЗначениеЗаполнено(РезультатПоиска.Значение));
							ЗначениеВыведено = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ ЗначениеВыведено
					  И ИмяОбласти <> "ОпределениеВысотыСтрокиМногострочнойЧасти" Тогда //служебное поле
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось заполнить область ""%1""!'"), ИмяОбласти);
					Сообщение.Сообщить();
					
				КонецЕсли;
			Иначе
				ПрисвоитьЗначениеОбласти(Область, РезультатПоиска.ЗначениеМЧБ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеМЧБ(Область)
	
	ТипЗначения = ТипЗнч(Область.Значение);
	Если ТипЗначения = Тип("Число") Тогда
		ФорматОбласти = Область.Формат;
		СоотвСтрокаФормата = РазобратьСтрокуФормата(ФорматОбласти);
		СоотвСтрокаФормата.Вставить("ЧРД", ".");
		СоотвСтрокаФормата.Вставить("ЧГ", "");
		Результат = Формат(Область.Значение, СобратьСтрокуФормата(СоотвСтрокаФормата));
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		Результат = СокрЛП(Область.Значение);
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Результат = Формат(Область.Значение, ?(ПустаяСтрока(Область.Формат), "ДФ=ddMMyyyy", Область.Формат));
	ИначеЕсли ТипЗначения = Тип("Булево") Тогда
		Результат = Формат(Область.Значение, Область.Формат);
	КонецЕсли;
	Возврат СокрЛП(Результат);
	
КонецФункции

&НаСервере
Процедура ПрисвоитьЗначениеОбласти(Область, Знач Значение, ИндексЗнакоместа = 0, ДополнятьСправа_ = Истина)
	
	Если ИндексЗнакоместа <> 0 Тогда
		Если СтрДлина(Значение) >= ИндексЗнакоместа Тогда
			Значение = Сред(Значение, ИндексЗнакоместа, 1);
		Иначе
			Значение = ?(ДополнятьСправа_, "-", "");
		КонецЕсли;
	ИначеЕсли НЕ ЗначениеЗаполнено(Значение) Тогда
		Значение = "-";
	КонецЕсли;
	
	Если Область.СодержитЗначение = Истина Тогда
		Область.Значение = ВРЕГ(Значение);
	Иначе
		Область.Текст = ВРЕГ(Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресЗначенияОбласти(Область)
	
	Имя = Область.Имя;
	КореньИмениОбласти = Неопределено;
	СтрИндексЗнакоместа = "";
	ИндексЗнакоместа = 0;
	ДлинаИмени = СтрДлина(Имя);
	Для ИндексВхождения = 1 По ДлинаИмени Цикл
		СтрИндексЗнакоместа = Сред(Имя, ДлинаИмени - ИндексВхождения + 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрИндексЗнакоместа) Тогда
			КореньИмениОбласти = Лев(Имя, ДлинаИмени - ИндексВхождения);
			ИндексЗнакоместа = Число(СтрИндексЗнакоместа);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Прав(КореньИмениОбласти, 1) = "_" Тогда
		КореньИмениОбласти = Лев(КореньИмениОбласти, СтрДлина(КореньИмениОбласти) - 1);
	КонецЕсли;
		
	Возврат Новый Структура("Корень, ИндексЗнакоместа", КореньИмениОбласти, ИндексЗнакоместа);
	
КонецФункции

&НаСервере
Функция РазобратьСтрокуФормата(Знач СтрокаФормата)
	
	ЗначенияПараметров = Новый Соответствие;
	Пока СтрНайти(СтрокаФормата, ";") <> 0 Цикл
		ВхождениеРазделителя = СтрНайти(СтрокаФормата, ";");
		ДоРазделителя = Лев(СтрокаФормата, ВхождениеРазделителя - 1);
		ВхождениеРавно = СтрНайти(ДоРазделителя, "=");
		Если ВхождениеРавно <> 0 И ВхождениеРавно <> 1 Тогда
			ЗначенияПараметров.Вставить(Лев(ДоРазделителя, ВхождениеРавно - 1), Сред(ДоРазделителя, ВхождениеРавно + 1));
		КонецЕсли;
		СтрокаФормата = Сред(СтрокаФормата, ВхождениеРазделителя + 1);
	КонецЦикла;
	ВхождениеРавно = СтрНайти(СтрокаФормата, "=");
	Если ВхождениеРавно <> 0 И ВхождениеРавно <> 1 Тогда
		ЗначенияПараметров.Вставить(Лев(СтрокаФормата, ВхождениеРавно - 1), Сред(СтрокаФормата, ВхождениеРавно + 1));
	КонецЕсли;
	Возврат ЗначенияПараметров;
	
КонецФункции

&НаСервере
Функция СобратьСтрокуФормата(ПараметрыФормата)
	
	СтрокаФормата = "";
	Для Каждого Параметр Из ПараметрыФормата Цикл
		СтрокаФормата = СтрокаФормата + "; " + Параметр.Ключ + "=" + Параметр.Значение;
	КонецЦикла;
	Если НЕ ПустаяСтрока(СтрокаФормата) Тогда
		СтрокаФормата = Сред(СтрокаФормата, 3);
	КонецЕсли;
	Возврат СтрокаФормата;
	
КонецФункции

&НаСервере
Функция ПолучитьОбщиеРеквизитыСТитульногоЛиста()
	
	МассивИНН         = Новый Массив(10);
	МассивКПП         = Новый Массив(9);
	МассивДатаПодписи = Новый Массив(8);
	
	Обл = ТабличныйДокумент.Области.Найти("ИНН");
	Если НЕ Обл = Неопределено Тогда
		СтрИНН = Обл.Значение;
		Для Ном = 1 По СтрДлина(СтрИНН) Цикл
			МассивИНН[Ном - 1] = Сред(СтрИНН, Ном, 1);
		КонецЦикла;
	КонецЕсли;
	
	Обл = ТабличныйДокумент.Области.Найти("КПП");
	Если НЕ Обл = Неопределено Тогда
		Для Ном = 1 По СтрДлина(Обл.Значение) Цикл
			МассивКПП[Ном - 1] = Сред(Обл.Значение, Ном, 1);
		КонецЦикла;
	КонецЕсли;
	
	Обл = ТабличныйДокумент.Области.Найти("ДатаПодписи");
	Если НЕ Обл = Неопределено Тогда
		Если ТипЗнч(Обл.Значение) = Тип("Строка") Тогда
			СтрДатаПодписи = Лев(СтрЗаменить(Обл.Значение, ".", ""), 8);
		ИначеЕсли ТипЗнч(Обл.Значение) = Тип("Дата") Тогда
			Если ЗначениеЗаполнено(Обл.Значение) Тогда
				СтрДатаПодписи = Лев(СтрЗаменить(Формат(Обл.Значение, "ДФ = ""дд.ММ.гггг 'г.'"""), ".", ""), 8);
			Иначе
				СтрДатаПодписи = "        ";
			КонецЕсли;
		КонецЕсли;
		Для Ном = 1 По СтрДлина(СтрДатаПодписи) Цикл
			МассивДатаПодписи[Ном - 1] = Сред(СтрДатаПодписи, Ном, 1);
		КонецЦикла;
	КонецЕсли;
	
	ОбщиеРеквизитыРазделов = Новый Структура;
	ОбщиеРеквизитыРазделов.Вставить("ИНН", МассивИНН);
	ОбщиеРеквизитыРазделов.Вставить("КПП", МассивКПП);
	ОбщиеРеквизитыРазделов.Вставить("ДатаПодписи", МассивДатаПодписи);
	
	Возврат ОбщиеРеквизитыРазделов;
	
КонецФункции

#КонецОбласти

#Область Автозаполнение

&НаКлиенте
Процедура ЗаполнитьАвто(Команда)
	
	Отказ = Ложь;
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ПроверитьВозможностьАвтоЗаполненияРеглОтчета(ИмяФормы, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗаполнениемРегламентированногоОтчета(
	ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Команда = ДополнительныеПараметры.Команда;
	
	ЗаданиеВыполнено = ЗаполнитьАвтоНаСервере();
	
	Если ЗаданиеВыполнено Тогда
		
		РегламентированнаяОтчетностьКлиент.ОбновитьДеревоРазделовВФормеОтчета(ЭтаФорма);
		
		Если НЕ Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока
			= Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы()[0].ПолучитьИдентификатор();
		КонецЕсли;
		
		Если Элементы.Расшифровать.Видимость Тогда
			Элементы.Расшифровать.Доступность = Истина;
			Элементы.ТабличныйДокументКонтекстноеМенюРасшифровать.Доступность = Истина;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	// Операция еще не завершена, выполняется с помощью фонового задания (асинхронно).
	ПараметрыОбработчикаОжидания = Новый Структура;
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	
	ФормаДлительнойОперации = ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", Новый Структура("ИдентификаторЗадания", СтруктураРеквизитовФормы.ИдентификаторЗадания), ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьАвтоНаСервере()
	
	Модифицированность = Истина;
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	СтруктураРеквизитовФормы.АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация",                  СтруктураРеквизитовФормы.Организация);
	ПараметрыОтчета.Вставить("мДатаНачалаПериодаОтчета",     СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыОтчета.Вставить("мДатаКонцаПериодаОтчета",      СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыОтчета.Вставить("КодНалоговогоОргана",          СтруктураДанныхТитульный.НалоговыйОрган);
	ПараметрыОтчета.Вставить("КПП",                          СтруктураДанныхТитульный.КПП);
	ПараметрыОтчета.Вставить("ДатаПодписи",                  ДатаПодписи);
	ПараметрыОтчета.Вставить("УникальныйИдентификаторФормы", ЭтаФорма.УникальныйИдентификатор);
	ПараметрыОтчета.Вставить("АдресВоВременномХранилище",    СтруктураРеквизитовФормы.АдресВоВременномХранилище);
	ПараметрыОтчета.Вставить("АдресВременногоХранилищаРасшифровки", СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки);
	
	// Помещение данных в контейнер.
	Контейнер = Новый Структура();
	Контейнер.Вставить("Титульный", СтруктураДанныхТитульный);
	Для каждого Раздел Из мСтруктураМногоуровневыхРазделов Цикл
		Если НЕ Раздел.Значение.Свойство("Подчиненность") Тогда
			Контейнер.Вставить(Раздел.Ключ, ПолучитьИзВременногоХранилища(
			СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + Раздел.Ключ]));
		КонецЕсли;
	КонецЦикла;
	
	ЗаданиеВыполнено = Ложь;
	
	НаименованиеЗадания = НСтр("ru = 'Автоматическое заполнение по данным информационной базы'");
	
	ПараметрыЗаполнения = Новый Массив;
	ПараметрыЗаполнения.Добавить(ИДОтчета(ЭтаФорма.ИмяФормы));
	ПараметрыЗаполнения.Добавить(ИДРедакцииОтчета(ЭтаФорма.ИмяФормы));
	ПараметрыЗаполнения.Добавить(ПараметрыОтчета);
	ПараметрыЗаполнения.Добавить(Контейнер);
	
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ВремяОжидания = 4;
	Иначе
		ВремяОжидания = 2;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.Выполнить("РегламентированнаяОтчетностьПереопределяемый.ЗаполнитьОтчет",
	ПараметрыЗаполнения, , НаименованиеЗадания);
	
	Попытка
		Задание.ОжидатьЗавершения(ВремяОжидания);
	Исключение
		// Специальная обработка не требуется. Предположительно, исключение вызвано истечением времени ожидания.
	КонецПопытки;
	
	СтруктураРеквизитовФормы.ИдентификаторЗадания = Задание.УникальныйИдентификатор;
	
	// Если операция уже завершилась, то сразу обрабатываем результат.
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда
		
		ЗаданиеВыполнено = Истина;
		
		ЗагрузитьПодготовленныеДанные();
		
	КонецЕсли;
	
	Возврат ЗаданиеВыполнено;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ПроверитьВыполнениеЗадания() Тогда
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Если Элементы.Расшифровать.Видимость Тогда
				Элементы.Расшифровать.Доступность = Истина;
				Элементы.ТабличныйДокументКонтекстноеМенюРасшифровать.Доступность = Истина;
			КонецЕсли;
			Возврат;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал * ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВыполнениеЗадания()
	
	Если ДлительныеОперации.ЗаданиеВыполнено(СтруктураРеквизитовФормы.ИдентификаторЗадания) Тогда 
		ЗагрузитьПодготовленныеДанные();
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные() Экспорт
	
	Контейнер = ПолучитьИзВременногоХранилища(СтруктураРеквизитовФормы.АдресВоВременномХранилище);
	
	Если ТипЗнч(Контейнер) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Получение данных из контейнера.
	СтруктураДанныхТитульный = Контейнер["Титульный"];
	Для каждого Раздел Из мСтруктураМногоуровневыхРазделов Цикл
		Если НЕ Раздел.Значение.Свойство("Подчиненность") Тогда
			
			Если Раздел.Ключ = "Раздел2" Тогда
				ДеревоРаздела2 = Контейнер[Раздел.Ключ];
				Для Каждого СтраницаРаздела2 Из ДеревоРаздела2.Строки Цикл
					ДанныеСтраницыРаздела2 = СтраницаРаздела2.Данные;
					ДанныеСтраницыРаздела2.П000020002003 = СокрЛП(ДанныеСтраницыРаздела2.П000020002003);
					Если СокрЛП(ДанныеСтраницыРаздела2["П000020007003_КодЛьготы"]) = "3022100" Тогда
						Если НЕ ЗначениеЗаполнено(ДанныеСтраницыРаздела2["П000020007003_Основание"]) Тогда
							ДанныеСтраницыРаздела2.П000020007003_Основание = "000000000000";
						КонецЕсли;
					КонецЕсли;
					Если СокрЛП(ДанныеСтраницыРаздела2["П000020009003_КодЛьготы"]) = "3022300" Тогда
						Если НЕ ЗначениеЗаполнено(ДанныеСтраницыРаздела2["П000020009003_Основание"]) Тогда
							ДанныеСтраницыРаздела2.П000020009003_Основание = "000000000000";
						КонецЕсли;
					КонецЕсли;
					Если СокрЛП(ДанныеСтраницыРаздела2["П000020018003_КодЛьготы"]) = "3022400" Тогда
						Если НЕ ЗначениеЗаполнено(ДанныеСтраницыРаздела2["П000020018003_Основание"]) Тогда
							ДанныеСтраницыРаздела2.П000020018003_Основание = "000000000000";
						КонецЕсли;
					КонецЕсли;
					Если СокрЛП(ДанныеСтраницыРаздела2["П000020022003_КодЛьготы"]) = "3022200" Тогда
						Если НЕ ЗначениеЗаполнено(ДанныеСтраницыРаздела2["П000020022003_Основание"]) Тогда
							ДанныеСтраницыРаздела2.П000020022003_Основание = "000000000000";
						КонецЕсли;
					КонецЕсли;
					Если СокрЛП(ДанныеСтраницыРаздела2["П000020024003_КодЛьготы"]) = "3022500" Тогда
						Если НЕ ЗначениеЗаполнено(ДанныеСтраницыРаздела2["П000020024003_Основание"]) Тогда
							ДанныеСтраницыРаздела2.П000020024003_Основание = "000000000000";
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			СтруктураРеквизитовФормы.Вставить("АдресВоВрХранилищеДеревоДанных" + Раздел.Ключ,
			ПоместитьВоВременноеХранилище(Контейнер[Раздел.Ключ], УникальныйИдентификатор));
			
		КонецЕсли;
	КонецЦикла;
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела.Очистить();
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела.Добавить(1);
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	Для каждого НайденныйРаздел Из РазделыОтчета.ПолучитьЭлементы() Цикл
		Если НайденныйРаздел.КолонкаРазделыОтчетаСокрНаим = СтруктураРеквизитовФормы.НаимТекущегоРаздела Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = НайденныйРаздел.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ВывестиДанныеВТабличныйДокумент(СтруктураРеквизитовФормы.НаимТекущегоРаздела,
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела);
	
	РасчетНаСервере();
	
	СформироватьРаздел1();
	
КонецПроцедуры

&НаКлиенте
Процедура Расшифровать(Команда)
	
	НомерТекущейСтраницы = 0;
	Если мСтруктураМногоуровневыхРазделов.Свойство(СтруктураРеквизитовФормы.НаимТекущегоРаздела) Тогда
		СтруктураМногоуровневогоРаздела
		= мСтруктураМногоуровневыхРазделов[СтруктураРеквизитовФормы.НаимТекущегоРаздела];
		Если СтруктураМногоуровневогоРаздела.Свойство("Многостраничность") Тогда
			НомерТекущейСтраницы = СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела[0];
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация",                         СтруктураРеквизитовФормы.Организация);
	ПараметрыОтчета.Вставить("ДатаНачалаПериодаОтчета",             СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыОтчета.Вставить("ДатаКонцаПериодаОтчета",              СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыОтчета.Вставить("НомерТекущейСтраницы",                НомерТекущейСтраницы);
	ПараметрыОтчета.Вставить("АдресВременногоХранилищаРасшифровки", СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки);
	
	ИДИменПоказателей = Новый Массив();
	ИДИменПоказателей.Добавить(Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя);
	
	РегламентированнаяОтчетностьКлиент.ОткрытьРасшифровкуОтчета(ИДОтчета(ЭтаФорма.ИмяФормы),
	ИДРедакцииОтчета(ЭтаФорма.ИмяФормы), ИДИменПоказателей, ПараметрыОтчета)
	
КонецПроцедуры

#КонецОбласти

#Область МеханизмПроверкиКонтрольныхСоотношений

#Область КС_Управление

&НаКлиенте
Процедура	Проверить(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьКонтрольныеСоотношения(ЭтаФорма, ОбработкаКС);
	
КонецПроцедуры

&НаСервере
Функция		ПроверитьКонтрольныеСоотношенияВОтчете() Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("КоличествоНайденныхОшибок"		, 0);
	СтруктураРезультата.Вставить("КоличествоНайденныхБезОшибок"		, 0);
	СтруктураРезультата.Вставить("СтруктураОшибок"					, Новый Структура);
	СтруктураРезультата.Вставить("ДопСведенияВОписаниеКС"			, Истина);
	СтруктураРезультата.Вставить("ДопСведенияВЗаголовокРасшифровки"	, Ложь);
	СтруктураРезультата.Вставить("ДопСведенияВСтрокуРасшифровки"	, Истина);
	
	Если СписокРасшифровкиПоказателей = Неопределено Тогда
		СписокРасшифровкиПоказателей = РегламентированнаяОтчетность.СоздатьСписокРасшифровкиПоказателей();
	Иначе
		СписокРасшифровкиПоказателей.Очистить();
	КонецЕсли;
	
	Если СписокРасшифровкиформулы = Неопределено Тогда
		СписокРасшифровкиформулы = РегламентированнаяОтчетность.СоздатьСписокРасшифровкиформулы();
	Иначе
		СписокРасшифровкиформулы.Очистить();
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ТекстДляЗаголовочнойЧасти = 
		РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	СчетчикФормулДляРасшифровок = 0;
	
	мДанныеОтчета = СтруктураДанныхДокументаОтчета(СтруктураРеквизитовФормы.мСохраненныйДок);
	мДанныеОтчета.Вставить("ДанныеКС", Новый Структура);
	СоздатьТаблицуДанныхКС();
	
	Если РазделЗаполнен(мДанныеОтчета.Раздел1, "Раздел1") Тогда
	
		ПроверитьКС_1_01(СтруктураРезультата);
		ПроверитьКС_1_02(СтруктураРезультата);
		ПроверитьКС_1_03(СтруктураРезультата);
	
	КонецЕсли;
	
	Если РазделЗаполнен(мДанныеОтчета.Раздел2, "Раздел2") Тогда
	
		ПроверитьКС_1_04(СтруктураРезультата);
		ПроверитьКС_1_05(СтруктураРезультата);
		ПроверитьКС_1_06(СтруктураРезультата);
		ПроверитьКС_1_07(СтруктураРезультата);
		ПроверитьКС_1_08(СтруктураРезультата);
		ПроверитьКС_1_09(СтруктураРезультата);
		ПроверитьКС_1_10(СтруктураРезультата);
		ПроверитьКС_1_11(СтруктураРезультата);
		ПроверитьКС_1_12(СтруктураРезультата);
		ПроверитьКС_1_13(СтруктураРезультата);
		//КС 1_14 приостановлено
		ПроверитьКС_1_15(СтруктураРезультата);
		ПроверитьКС_1_16(СтруктураРезультата);
		ПроверитьКС_1_17(СтруктураРезультата);
		ПроверитьКС_1_18а(СтруктураРезультата);
		ПроверитьКС_1_18б(СтруктураРезультата);
		ПроверитьКС_1_19(СтруктураРезультата);
		ПроверитьКС_1_20(СтруктураРезультата);
		ПроверитьКС_1_21(СтруктураРезультата);
		ПроверитьКС_1_22(СтруктураРезультата);
		ПроверитьКС_1_23а(СтруктураРезультата);
		ПроверитьКС_1_23б(СтруктураРезультата);
		ПроверитьКС_1_24(СтруктураРезультата);
		ПроверитьКС_1_25(СтруктураРезультата);
		ПроверитьКС_1_26(СтруктураРезультата);
		ПроверитьКС_1_27(СтруктураРезультата);
		ПроверитьКС_1_28(СтруктураРезультата);
		ПроверитьКС_1_29(СтруктураРезультата);
		ПроверитьКС_1_30(СтруктураРезультата);
		ПроверитьКС_1_31(СтруктураРезультата);
		ПроверитьКС_1_32(СтруктураРезультата);
		ПроверитьКС_1_33(СтруктураРезультата);
		ПроверитьКС_1_34(СтруктураРезультата);
		ПроверитьКС_1_35(СтруктураРезультата);
		ПроверитьКС_1_36(СтруктураРезультата);
		ПроверитьКС_1_37(СтруктураРезультата);
		
	КонецЕсли;
	
	мДанныеОтчета = Неопределено;
	
	Если СписокРасшифровкиПоказателей.Количество() > 0 Тогда
		СтруктураРезультата.Вставить("СписокРасшифровкиПоказателей",
			ПоместитьВоВременноеХранилище(СписокРасшифровкиПоказателей, Новый УникальныйИдентификатор));
	КонецЕсли;
	
	Если СписокРасшифровкиформулы.Количество() > 0 Тогда
		СтруктураРезультата.Вставить("СписокРасшифровкиформулы",
			ПоместитьВоВременноеХранилище(СписокРасшифровкиформулы, Новый УникальныйИдентификатор));
	КонецЕсли;
	
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#Область КС_Проверки

&НаСервере
Процедура	ПроверитьКС_1_01(СтруктураРезультата)
	
	// Формула КС: Р1С021 = сумме строк Р2С250 по сочетанию КБК - ОКТМО
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.1'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (строка 021 Раздела 1) '") +
		НСтр("ru='должна быть равна сумме всех значений строк 250 Раздела 2 по соответствующим кодам ОКТМО и КБК'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='КБК %1, ОКТМО %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.ОписаниеОшибок.Добавить(
		НСтр("ru='Завышена исчисленная сумма земельного налога, подлежащая уплате в бюджет за налоговый период'"));
	Настройки.ОписаниеОшибок.Добавить(
		НСтр("ru='Занижена исчисленная сумма земельного налога, подлежащая уплате в бюджет за налоговый период'"));
	ТаблицаСоответствий = ТаблицаСоответствий(мДанныеОтчета.Раздел1);
	
	Для каждого СтрокаСоответствия Из ТаблицаСоответствий Цикл
		
		ПострочнаяСумма = 0;
		ПоказателиР1 = ЗаполнитьПоказатели("Р1С010, Р1С020, Р1С021", 
			мДанныеОтчета.Раздел1[СтрокаСоответствия.НомерСтраницы - 1].Данные, СтрокаСоответствия.НомерСтроки);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("КБК, ОКТМО", СокрЛП(ПоказателиР1.Р1С010.Значение), 
			СокрЛП(ПоказателиР1.Р1С020.Значение)));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(ПоказателиР1, Настройки, "Раздел1", СтрокаСоответствия.НомерСтраницы);
		КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
		
		Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
			
			Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
			ПоказателиР2 = ЗаполнитьПоказатели("Р2С010, Р2С020, Р2С250", Раздел2);
			Если Не (СокрЛП(ПоказателиР1.Р1С010.Значение) = СокрЛП(ПоказателиР2.Р2С010.Значение) 
				И СокрЛП(ПоказателиР1.Р1С020.Значение) = СокрЛП(ПоказателиР2.Р2С020.Значение)) Тогда
				Продолжить;
			КонецЕсли;
			ПострочнаяСумма = ПострочнаяСумма + ПоказателиР2.Р2С250.Значение;
			РасшифроватьПоказатели(ПоказателиР2, Настройки, "Раздел2", НомерСтраницы, "Р2С250");
		
		КонецЦикла;
		
		ЭтоОшибка = Не ПоказателиР1.Р1С021.Значение = ПострочнаяСумма;
		Разница = ПоказателиР1.Р1С021.Значение - ПострочнаяСумма;
		Настройки.ОписаниеОшибки = ?(Разница > 0, Настройки.ОписаниеОшибок[0], Настройки.ОписаниеОшибок[1]);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			ПоказателиР1.Р1С021.Значение, ПострочнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_02(СтруктураРезультата)
	
	// Формула КС: Р1С030 = Р1С021 - (Р1С023 + Р1С025 + Р1С027) по сочетанию КБК - ОКТМО 
	//		если Р1С021 - (Р1С023 + Р1С025 + Р1С027) > 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.2'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма налога, подлежащая уплате в бюджет (строка 030 Раздела 1) должна быть равна разнице значения '") +
		НСтр("ru='строки 021 Раздела 1 и суммы строк 023, 025, 027 Раздела 1 по соответствующим кодам ОКТМО и '") +
		НСтр("ru='КБК, если такая разница больше или равна нулю'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='КБК %1, ОКТМО %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 - (%3 + %4 + %5) Итог [%6]'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.ОписаниеОшибок.Добавить(НСтр("ru='Завышена сумма земельного налога, подлежащая уплате в бюджет'"));
	Настройки.ОписаниеОшибок.Добавить(НСтр("ru='Занижена сумма земельного налога, подлежащая уплате в бюджет'"));
	ТаблицаСоответствий = ТаблицаСоответствий(мДанныеОтчета.Раздел1);
	
	Для каждого СтрокаСоответствия Из ТаблицаСоответствий Цикл
		
		Показатели = ЗаполнитьПоказатели("Р1С010, Р1С020, Р1С021, Р1С023, Р1С025, Р1С027, Р1С030", 
			мДанныеОтчета.Раздел1[СтрокаСоответствия.НомерСтраницы - 1].Данные, СтрокаСоответствия.НомерСтроки);
		РасчетнаяСумма = Показатели.Р1С021.Значение - Показатели.Р1С023.Значение - Показатели.Р1С025.Значение -
			Показатели.Р1С027.Значение;
		Если РасчетнаяСумма < 0 Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("КБК, ОКТМО", СокрЛП(Показатели.Р1С010.Значение), 
			СокрЛП(Показатели.Р1С020.Значение)));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел1", СтрокаСоответствия.НомерСтраницы);
		ЭтоОшибка = Не Показатели.Р1С030.Значение = РасчетнаяСумма;
		Разница = Показатели.Р1С030.Значение - РасчетнаяСумма;
		Настройки.ОписаниеОшибки = ?(Разница > 0, Настройки.ОписаниеОшибок[0], Настройки.ОписаниеОшибок[1]);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р1С030.Значение, Показатели.Р1С021.Значение, Показатели.Р1С023.Значение, 
			Показатели.Р1С025.Значение, Показатели.Р1С027.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_03(СтруктураРезультата)
	
	// Формула КС: Р1С040 = (Р1С023 + Р1С025 + Р1С027) - Р1С021 по сочетанию КБК - ОКТМО 
	//		если (Р1С023 + Р1С025 + Р1С027) - Р1С021 > 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.3'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма налога, исчисленная к уменьшению (строка 040 Раздела 1) должна быть равна разнице суммы строк '") +
		НСтр("ru='023, 025, 027 Раздела 1 и значения строки 021 Раздела 1 по соответствующим кодам ОКТМО и КБК, '") +
		НСтр("ru='если такая разница больше нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='КБК %1, ОКТМО %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%3 + %4 + %5) - %2 Итог [%6]'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.ОписаниеОшибок.Добавить(НСтр("ru='Завышена сумма земельного налога, подлежащая уплате в бюджет'"));
	Настройки.ОписаниеОшибок.Добавить(НСтр("ru='Занижена сумма земельного налога, подлежащая уплате в бюджет'"));
	ТаблицаСоответствий = ТаблицаСоответствий(мДанныеОтчета.Раздел1);
	
	Для каждого СтрокаСоответствия Из ТаблицаСоответствий Цикл
	
		Показатели = ЗаполнитьПоказатели("Р1С010, Р1С020, Р1С021, Р1С023, Р1С025, Р1С027, Р1С040", 
			мДанныеОтчета.Раздел1[СтрокаСоответствия.НомерСтраницы - 1].Данные, СтрокаСоответствия.НомерСтроки);
		РасчетнаяСумма = Показатели.Р1С023.Значение + Показатели.Р1С025.Значение +
			Показатели.Р1С027.Значение - Показатели.Р1С021.Значение;
		Если РасчетнаяСумма <= 0 Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("КБК, ОКТМО", СокрЛП(Показатели.Р1С010.Значение), 
			СокрЛП(Показатели.Р1С020.Значение)));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел1", СтрокаСоответствия.НомерСтраницы);
		ЭтоОшибка = Не Показатели.Р1С040.Значение = РасчетнаяСумма;
		Разница = Показатели.Р1С040.Значение - РасчетнаяСумма;
		Настройки.ОписаниеОшибки = ?(Разница > 0, Настройки.ОписаниеОшибок[1], Настройки.ОписаниеОшибок[0]);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р1С040.Значение, Показатели.Р1С021.Значение, Показатели.Р1С023.Значение, 
			Показатели.Р1С025.Значение, Показатели.Р1С027.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
	
	КонецЦикла; 
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_04(СтруктураРезультата)
	
	// Формула КС: Р2С070 заполнена если Р2С080 > 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.4'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Код налоговой льготы (строка 070 Раздела 2) должен быть заполнен, если значение строки 080 больше нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 070 заполнено (""%1"") при условии что значение строки 080 (""%2"") > 0'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не указан код налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С070_Код, Р2С080", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = Показатели.Р2С080.Значение > 0 И Не ЗначениеЗаполнено(Показатели.Р2С070_Код.Значение);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С070_Код.Значение, Показатели.Р2С080.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_05(СтруктураРезультата)
	
	// Формула КС: Р2С080 > 0 если Р2С070 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.5'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Значение строки 080 Раздела 2 должно быть заполнено, если заполнена строка 070 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 080 (""%1"") > 0 при условии что значение строки 070 (""%2"") заполнено'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не исчислена сумма налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С070_Код, Р2С080", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = ЗначениеЗаполнено(Показатели.Р2С070_Код.Значение) И Не Показатели.Р2С080.Значение > 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С080.Значение, Показатели.Р2С070_Код.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_06(СтруктураРезультата)
	
	// Формула КС: Р2С090 заполнена если Р2С100 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.6'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Код налоговой льготы (строка 090 Раздела 2) должен быть заполнен, если значение строки 100 больше нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 090 заполнено (""%1"") при условии что значение строки 100 (""%2/%3"") > 0'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не указан код налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С090_Код, Р2С100_Числитель, Р2С100_Знаменатель", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = (ЗначениеЗаполнено(Показатели.Р2С100_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С100_Знаменатель.Значение)) И Не ЗначениеЗаполнено(Показатели.Р2С090_Код.Значение);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С090_Код.Значение, Показатели.Р2С100_Числитель.Значение, Показатели.Р2С100_Знаменатель.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_07(СтруктураРезультата)
	
	// Формула КС: Р2С100 заполнена если Р2С090 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.7'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Значение строки 100 Раздела 2 (Доля необлагаемой площади земельного участка) должно быть заполнено, '") +
		НСтр("ru='если заполнена строка 090 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 100 (""%1/%2"") > 0 при условии, что значение строки 090 (""%3"") заполнено'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не указана доля необлагаемой площади земельного участка'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С090_Код, Р2С100_Числитель, Р2С100_Знаменатель", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = ЗначениеЗаполнено(Показатели.Р2С090_Код.Значение) 
			И Не (ЗначениеЗаполнено(Показатели.Р2С100_Числитель.Значение) 
			И ЗначениеЗаполнено(Показатели.Р2С100_Знаменатель.Значение));
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С100_Числитель.Значение, Показатели.Р2С100_Знаменатель.Значение, Показатели.Р2С090_Код.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_08(СтруктураРезультата)
	
	// Формула КС: Р2С180 заполнена если Р2С190 > 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.8'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Код налоговой льготы (строка 180 Раздела 2) должен быть заполнен, если значение строки 190 больше нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 180 заполнено (""%1"") при условии что значение строки 190 (""%2"") > 0'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не указан код налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С180_Код, Р2С190", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = Показатели.Р2С190.Значение > 0 И Не ЗначениеЗаполнено(Показатели.Р2С180_Код.Значение);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С180_Код.Значение, Показатели.Р2С190.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_09(СтруктураРезультата)
	
	// Формула КС: Р2С190 > 0 если Р2С180 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.9'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Значение строки 190 Раздела 2 (Сумма налоговой льготы) должно быть заполнено, если заполнена '") +
		НСтр("ru='строка 180 Раздела 2строка 180 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 190 (""%1"") > 0 при условии что значение строки 180 (""%2"") заполнено'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не исчислена сумма налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С180_Код, Р2С190", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = ЗначениеЗаполнено(Показатели.Р2С180_Код.Значение) И Не Показатели.Р2С190.Значение > 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С190.Значение, Показатели.Р2С180_Код.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_10(СтруктураРезультата)
	
	// Формула КС: Р2С200 заполнена если Р2С210 > 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.10'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.395, ст. 7'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Код налоговой льготы (строка 200 Раздела 2) должен быть заполнен, если значение строки 210 больше нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 200 заполнено (""%1"") при условии что значение строки 210 (""%2"") > 0'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не указан код налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С200, Р2С210", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = Показатели.Р2С210.Значение > 0 И Не ЗначениеЗаполнено(Показатели.Р2С200.Значение);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С200.Значение, Показатели.Р2С210.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_11(СтруктураРезультата)
	
	// Формула КС: Р2С210 > 0 если Р2С200 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.11'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.395, ст. 7'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Значение строки 210 Раздела 2 (Сумма налоговой льготы) должно быть заполнено, если заполнена '") +
		НСтр("ru='строка 200 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 210 (""%1"") > 0 при условии что значение строки 200 (""%2"") заполнено'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не исчислена сумма налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С200, Р2С210", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = ЗначениеЗаполнено(Показатели.Р2С200.Значение) И Не Показатели.Р2С210.Значение > 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С210.Значение, Показатели.Р2С200.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_12(СтруктураРезультата)
	
	// Формула КС: Р2С220 заполнена если Р2С230 > 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.12'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Код налоговой льготы (строка 220 Раздела 2) должен быть заполнен, если значение строки 230 больше нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 220 заполнено (""%1"") при условии что значение строки 230 (""%2"") > 0'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не указан код налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С220_Код, Р2С230", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = Показатели.Р2С230.Значение > 0 И Не ЗначениеЗаполнено(Показатели.Р2С220_Код.Значение);
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С220_Код.Значение, Показатели.Р2С230.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_13(СтруктураРезультата)
	
	// Формула КС: Р2С230 > 0 если Р2С220 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.13'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Значение строки 230 Раздела 2 (Сумма налоговой льготы) должно быть заполнено, если заполнена '") +
		НСтр("ru='строка 220 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='Значение строки 230 (""%1"") > 0 при условии что значение строки 220 (""%2"") заполнено'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Не исчислена сумма налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С220_Код, Р2С230", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = ЗначениеЗаполнено(Показатели.Р2С220_Код.Значение) И Не Показатели.Р2С230.Значение > 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С230.Значение, Показатели.Р2С220_Код.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_15(СтруктураРезультата)
	
	// Формула КС: Р2С160 > 0 если Р2С180 и Р2С190 заполнены или Р2С200 и Р2С210 заполнены
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.15'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387, ст.395, ст.7'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Количество полных месяцев использования льготы (строка 160 Раздела 2) должно '") +
		НСтр("ru='быть больше нуля, если заполнены строки 180 и 190 Раздела 2 или строки 200 и 210 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='Значение строки 160: (""%1"")'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Возможно неправомерное применение льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С160, Р2С180_Код, Р2С190, Р2С200, Р2С210", Раздел2);
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = (ЗначениеЗаполнено(Показатели.Р2С180_Код.Значение) И ЗначениеЗаполнено(Показатели.Р2С190.Значение) Или
			ЗначениеЗаполнено(Показатели.Р2С200.Значение) И ЗначениеЗаполнено(Показатели.Р2С210.Значение)) 
			И Не Показатели.Р2С160.Значение > 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С160.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_16(СтруктураРезультата)
	
	// Формула КС: Р2С190 = Р2С150 * (1 - Р2С170) если Р2С190 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.16'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма налоговой льготы (строка 190 Раздела 2) должна быть равна значению строки 150 '") +
		НСтр("ru='Раздела 2 х (1 - строка 170 Раздела 2)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 х (1 - %3) Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определен коэффициент Кл'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С150, Р2С170, Р2С190", Раздел2);
		Если Не ЗначениеЗаполнено(Показатели.Р2С190.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить(Показатели.Р2С150.Значение * (1 - Показатели.Р2С170.Значение));
		ЭтоОшибка = Не Показатели.Р2С190.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С190.Значение, Показатели.Р2С150.Значение, Показатели.Р2С170.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_17(СтруктураРезультата)
	
	// Формула КС: Р2С210 = Р2С150 * (1 - Р2С170) если Р2С210 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.17'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.395, ст. 7'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма налоговой льготы (строка 210 Раздела 2) должна быть равна значению строки 150 '") +
		НСтр("ru='Раздела 2 х (1 - строка 170 Раздела 2)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 х (1 - %3) Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определен коэффициент Кл'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С150, Р2С170, Р2С210", Раздел2);
		Если Не ЗначениеЗаполнено(Показатели.Р2С210.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить(Показатели.Р2С150.Значение * (1 - Показатели.Р2С170.Значение));
		ЭтоОшибка = Не Показатели.Р2С210.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С210.Значение, Показатели.Р2С150.Значение, Показатели.Р2С170.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_18а(СтруктураРезультата)
	
	// Формула КС: Р2С110 = Р2С050 - Р2С080 если Р2С060 не заполнена и Р2С080 заполнена
	// и (Р2С050 - Р2С080) >= 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.18а'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна разнице значений строк '") +
		НСтр("ru='050 и 080, если такая разница больше нуля и строка 060 не заполнена'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 - %3 Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно определена не облагаемая налогом сумма, уменьшающая величину налоговой базы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С080, Р2С110", Раздел2);
		Если Не (Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение)) И ЗначениеЗаполнено(Показатели.Р2С080.Значение) И
			Показатели.Р2С050.Значение - Показатели.Р2С080.Значение >= 0) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С060_Числитель, Р2С060_Знаменатель");
		РасчетнаяСумма = Показатели.Р2С050.Значение - Показатели.Р2С080.Значение;
		ЭтоОшибка = Не Показатели.Р2С110.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, Показатели.Р2С080.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_18б(СтруктураРезультата)
	
	// Формула КС: Р2С110 = 0 если Р2С060 не заполнена и Р2С080 заполнена
	// и (Р2С050 - Р2С080) < 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.18б'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна нулю, если разница '") +
		НСтр("ru='значений строк 050 и 080 меньше нуля и строка 060 Раздела 2 не заполнена'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = 0'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно определена не облагаемая налогом сумма, уменьшающая величину налоговой базы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С080, Р2С110", Раздел2);
		Если Не (Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение)) И ЗначениеЗаполнено(Показатели.Р2С080.Значение) И
			Показатели.Р2С050.Значение - Показатели.Р2С080.Значение <= 0) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С060_Числитель, Р2С060_Знаменатель");
		ЭтоОшибка = Не Показатели.Р2С110.Значение = 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С110.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_19(СтруктураРезультата)
	
	// Формула КС: Р2С110 = Р2С050 - (Р2С050 * Р2С100 * (1 - Р2С170)) если Р2С060 не заполнена и 
	// Р2С100 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.19'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна строке 050 - (строка 050 х строка 100 '") +
		НСтр("ru='Раздела 2 х (1 - строка 170 Раздела 2)), если строка 100 Раздела 2 отлична от нуля'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 - (%2 х %3/%4 х (1 - %5)) Итог [%6]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определена налоговая база'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели(
			"Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С100_Числитель, Р2С100_Знаменатель, Р2С110, Р2С170", Раздел2);
		Если Не (Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) 
			И ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение)) 
			И ЗначениеЗаполнено(Показатели.Р2С100_Числитель.Значение)
			И ЗначениеЗаполнено(Показатели.Р2С100_Знаменатель.Значение)) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С060_Числитель, Р2С060_Знаменатель");
		РасчетнаяСумма = Округлить(Показатели.Р2С050.Значение - (Показатели.Р2С050.Значение * (
			Показатели.Р2С100_Числитель.Значение / Показатели.Р2С100_Знаменатель.Значение) * (1 - Показатели.Р2С170.Значение)));
		ЭтоОшибка = Не Показатели.Р2С110.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, Показатели.Р2С100_Числитель.Значение, 
			Показатели.Р2С100_Знаменатель.Значение, Показатели.Р2С170.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_20(СтруктураРезультата)
	
	// Формула КС: Р2С110 = (Р2С050 * Р2С060) - Р2С080 если Р2С060, Р2С080 заполнены
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.20'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна (строка 050 '") +
		НСтр("ru='Раздела 2 х строка 060 Раздела 2) - строка 080 Раздела 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%2 х %3/%4) - %5 Итог [%6]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определена налоговая база'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С080, Р2С110", Раздел2);
		Если Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение) И ЗначениеЗаполнено(Показатели.Р2С080.Значение)) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить((Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение) - Показатели.Р2С080.Значение);
		РасчетнаяСумма = Макс(РасчетнаяСумма, 0);
		ЭтоОшибка = Не Показатели.Р2С110.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, Показатели.Р2С060_Числитель.Значение, 
			Показатели.Р2С060_Знаменатель.Значение, Показатели.Р2С080.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_21(СтруктураРезультата)
	
	// Формула КС: Р2С110 = (Р2С050 * Р2С060) если Р2С060 заполнена, Р2С080, Р2С100 не заполнены
	// и (Р2С050 * Р2С060) > 0 иначе Р2С110 = 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.21'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.392'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна (строка 050 Раздела 2 х строка 060 '") +
		НСтр("ru='Раздела 2), если такое произведение больше нуля и строки 080, 100 Раздела 2 не заполнены. '") +
		НСтр("ru='Иначе, строка 110 раздела 2 = 0'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 х %3/%4 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определена налоговая база'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели(
			"Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С080, Р2С100_Числитель, Р2С100_Знаменатель, Р2С110", Раздел2);
		Если Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение) И Не ЗначениеЗаполнено(Показатели.Р2С080.Значение)
			И Не (ЗначениеЗаполнено(Показатели.Р2С100_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С100_Знаменатель.Значение))) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить(Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение);
		ЭтоОшибка = ?(РасчетнаяСумма > 0, Не Показатели.Р2С110.Значение = РасчетнаяСумма, Не Показатели.Р2С110.Значение = 0);
		Настройки.Расшифровка = ?(РасчетнаяСумма > 0, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Настройки.ШаблонРасшифровки, Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, 
			Показатели.Р2С060_Числитель.Значение, Показатели.Р2С060_Знаменатель.Значение, РасчетнаяСумма), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 = 0", Показатели.Р2С110.Значение));
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_22(СтруктураРезультата)
	
	// Формула КС: Р2С110 = (Р2С050 * Р2С060) - Р2С080 если Р2С060, Р2С080 заполнены
	// и (Р2С050 * Р2С060) - Р2С080 > 0 иначе Р2С110 = 0
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.22'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387, п.5 ст.391'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна (строка 050 Раздела 2 х строка 060 Раздела '") +
		НСтр("ru='2) - строка 080 Раздела 2, если такая разница больше нуля. Иначе, строка 110 раздела 2 = 0'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%2 х %3/%4) - %5 Итог [%6]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определена налоговая база'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С080, Р2С110", Раздел2);
		Если Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) И 
			ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение) И ЗначениеЗаполнено(Показатели.Р2С080.Значение)) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить((Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение) - Показатели.Р2С080.Значение);
		ЭтоОшибка = ?(РасчетнаяСумма > 0, Не Показатели.Р2С110.Значение = РасчетнаяСумма, Не Показатели.Р2С110.Значение = 0);
		Настройки.Расшифровка = ?(РасчетнаяСумма > 0, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Настройки.ШаблонРасшифровки, Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, 
			Показатели.Р2С060_Числитель.Значение, Показатели.Р2С060_Знаменатель.Значение, Показатели.Р2С080.Значение, 
			РасчетнаяСумма), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 = 0", Показатели.Р2С110.Значение));
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_23а(СтруктураРезультата)
	
	// Формула КС: Р2С110 = (Р2С050* Р2С060) - (Р2С050* Р2С060) * (Р2С100 * (1 - Р2С170))
	// если (Р2С050* Р2С060) - (Р2С050* Р2С060) * (Р2С100 * (1 - Р2С170)) > 0 и Р2С060, Р2С100 заполнены
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.23а'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна (строка 050 Раздела 2 х строка 060 '") +
		НСтр("ru='Раздела 2) - (строка 050 раздела 2 х строка 060 Раздела 2) х (строка 100 раздела 2 х (1 - '") +
		НСтр("ru='строка 170 Раздела 2), если такая разница больше нуля и строки 060, 100 Раздела 2 заполнены'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, 
		НСтр("ru='%1 = (%2 х %3/%4) - (%2 х %3/%4) х (%5/%6 х (1 - %7)) Итог [%8]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определена налоговая база'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели(
			"Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С100_Числитель, Р2С100_Знаменатель, Р2С110, Р2С170", Раздел2);
		Если Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) 
			И ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение) 
			И ЗначениеЗаполнено(Показатели.Р2С100_Числитель.Значение)
			И ЗначениеЗаполнено(Показатели.Р2С100_Знаменатель.Значение) 
			И (Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / Показатели.Р2С060_Знаменатель.Значение) -
			(Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / Показатели.Р2С060_Знаменатель.Значение) *
			((Показатели.Р2С100_Числитель.Значение / Показатели.Р2С100_Знаменатель.Значение) * 
			(1 - Показатели.Р2С170.Значение)) > 0)
			Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить((Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение) - (Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение) * ((Показатели.Р2С100_Числитель.Значение / 
			Показатели.Р2С100_Знаменатель.Значение) * (1 - Показатели.Р2С170.Значение)));
		ЭтоОшибка = Не Показатели.Р2С110.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, Показатели.Р2С060_Числитель.Значение, 
			Показатели.Р2С060_Знаменатель.Значение, Показатели.Р2С100_Числитель.Значение, 
			Показатели.Р2С100_Знаменатель.Значение, Показатели.Р2С170.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_23б(СтруктураРезультата)
	
	// Формула КС: Р2С110 = 0
	// если (Р2С050* Р2С060) - (Р2С050* Р2С060) * (Р2С100 * (1 - Р2С170)) <= 0 и Р2С060, Р2С100 заполнены
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.23б'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст. 387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Налоговая база (строка 110 Раздела 2) должна быть равна нулю, если (строка 050 Раздела 2 х строка '") +
		НСтр("ru='060 Раздела 2) - (строка 050 раздела 2 х строка 060 Раздела 2) х (строка 100 раздела 2 х (1 - '") +
		НСтр("ru='строка 170 Раздела 2) меньше или равна нулю и строки 060, 100 Раздела 2 заполнены'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = 0;
		|(%2 х %3/%4) - (%2 х %3/%4) х (%5/%6 х (1 - %7)) Итог [%8]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно определена налоговая база'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели(
			"Р2С050, Р2С060_Числитель, Р2С060_Знаменатель, Р2С100_Числитель, Р2С100_Знаменатель, Р2С110, Р2С170", Раздел2);
		Если Не (ЗначениеЗаполнено(Показатели.Р2С060_Числитель.Значение) 
			И ЗначениеЗаполнено(Показатели.Р2С060_Знаменатель.Значение) 
			И ЗначениеЗаполнено(Показатели.Р2С100_Числитель.Значение)
			И ЗначениеЗаполнено(Показатели.Р2С100_Знаменатель.Значение) 
			И (Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / Показатели.Р2С060_Знаменатель.Значение) -
			(Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / Показатели.Р2С060_Знаменатель.Значение) *
			((Показатели.Р2С100_Числитель.Значение / Показатели.Р2С100_Знаменатель.Значение) * 
			(1 - Показатели.Р2С170.Значение)) <= 0)
			Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить((Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение) - (Показатели.Р2С050.Значение * Показатели.Р2С060_Числитель.Значение / 
			Показатели.Р2С060_Знаменатель.Значение) * ((Показатели.Р2С100_Числитель.Значение / 
			Показатели.Р2С100_Знаменатель.Значение) * (1 - Показатели.Р2С170.Значение)));
		ЭтоОшибка = Не Показатели.Р2С110.Значение = 0;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С110.Значение, Показатели.Р2С050.Значение, Показатели.Р2С060_Числитель.Значение, 
			Показатели.Р2С060_Знаменатель.Значение, Показатели.Р2С100_Числитель.Значение, 
			Показатели.Р2С100_Знаменатель.Значение, Показатели.Р2С170.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_24(СтруктураРезультата)
	
	// Формула КС: Р2С140 = 1 если Р2С130 = 12
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.24'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.7 ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Коэффициент Кв (строка 140 раздела 2) должен быть равен ""1"", если значение строки 130 = ""12"" '") +
		НСтр("ru='(земельный участок использовался в течение всего налогового периода)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='Значение строки 140: (""%1"")'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно указан коэффициент Кв'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		Показатели = ЗаполнитьПоказатели("Р2С130, Р2С140", Раздел2);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		ЭтоОшибка = Показатели.Р2С130.Значение = 12 И Не Показатели.Р2С140.Значение = 1;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С140.Значение);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_25(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С140 / 100 если Р2С040, Р2С145 не заполнены
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.25'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма исчисленного налога (строка 150 Раздела 2) должна быть равна (строка 110 Раздела 2 '") +
		НСтр("ru='х строка 120 Раздела 2 х строка 140 Раздела 2) / 100 (при отсутствии коэффициента Ки)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%2 х %3 х %4) / 100 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно указана сумма исчисленного налога'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2КНЗУ, Р2С040, Р2С110, Р2С120, Р2С140, Р2С145, Р2С150", Раздел2);
		Если ЗначениеЗаполнено(Показатели.Р2С040.Значение) ИЛИ ЗначениеЗаполнено(Показатели.Р2С145.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", 
			Новый Структура("НомерС, НомерК", НомерСтраницы, Показатели.Р2КНЗУ.Значение));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С040");
		РасчетнаяСумма = Округлить(Показатели.Р2С110.Значение * Показатели.Р2С120.Значение * 
			Показатели.Р2С140.Значение / 100);
		ЭтоОшибка = Не Показатели.Р2С150.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С150.Значение, Показатели.Р2С110.Значение, Показатели.Р2С120.Значение, Показатели.Р2С140.Значение, 
			РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_26(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С140 * Р2С145 / 100 если Р2С040 не заполнена и Р2С145 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.26'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма исчисленного налога (строка 150 Раздела 2) должна быть равна (строка 110 Раздела 2 х строка 120'") +
		НСтр("ru=' Раздела 2 х строка 140 Раздела 2 х строка 145 Раздела 2) / 100, (при наличии коэффициента Ки)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%2 х %3 х %4 х %5) / 100 Итог [%6]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно указана сумма исчисленного налога'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2КНЗУ, Р2С040, Р2С110, Р2С120, Р2С140, Р2С145, Р2С150", Раздел2);
		Если ЗначениеЗаполнено(Показатели.Р2С040.Значение) ИЛИ НЕ ЗначениеЗаполнено(Показатели.Р2С145.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", 
			Новый Структура("НомерС, НомерК", НомерСтраницы, Показатели.Р2КНЗУ.Значение));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С040");
		РасчетнаяСумма = Округлить(Показатели.Р2С110.Значение * Показатели.Р2С120.Значение * 
			Показатели.Р2С140.Значение * Показатели.Р2С145.Значение / 100);
		ЭтоОшибка = Не Показатели.Р2С150.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С150.Значение, Показатели.Р2С110.Значение, Показатели.Р2С120.Значение, Показатели.Р2С140.Значение, 
			Показатели.Р2С145.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_27(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С140 * 2 / 100 если Р2С040 = 1, Р2С145 не заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.27'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Для налогоплательщиков в отношении земельных участков, приобретенных (предоставленных) в '") +
		НСтр("ru='собственность на условиях осуществления на них жилищного строительства, в том случае, если '") +
		НСтр("ru='в течение налогового периода изменение размера коэффициента 2 на коэффициент 4 не происходит '") +
		НСтр("ru='и отсутствует коэффициент Ки и если по строке 040 Раздела 2 указано значение ""1"", то строка '") +
		НСтр("ru='150 Раздела 2 = ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2) / 100) х 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = ((%2 х %3 х %4) / 100) х 2 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно указана сумма исчисленного налога с учетом коэффициента 2'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.Вставить("Коэффициент", 1);
	Настройки.Вставить("КиРавноНулю", Истина);
	
	ПроверитьКС_1_30_код(Настройки);
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_28(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С1400 * Р2С145 * 2 / 100 если Р2С040 = 1, Р2С145 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.28'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Для налогоплательщиков в отношении земельных участков, приобретенных (предоставленных) в '") +
		НСтр("ru='собственность на условиях осуществления на них жилищного строительства, в том случае, если '") +
		НСтр("ru='в течение налогового периода изменение размера коэффициента 2 на коэффициент 4 не происходит '") +
		НСтр("ru='и при наличии коэффициента Ки и если по строке 040 Раздела 2 указано значение ""1"", то строка '") +
		НСтр("ru='150 Раздела 2 = ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2 х строка '") +
		НСтр("ru='145 Раздела 2) / 100) х 2'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = ((%2 х %3 х %4 х %6) / 100) х 2 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно указана сумма исчисленного налога с учетом коэффициента 2'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.Вставить("Коэффициент", 1);
	Настройки.Вставить("КиРавноНулю", Ложь);
	
	ПроверитьКС_1_30_код(Настройки);
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_29(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С140 * 4 / 100 если Р2С040 = 2, Р2С145 не заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.29'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Для налогоплательщиков в отношении земельных участков, приобретенных (предоставленных) в '") +
		НСтр("ru='собственность на условиях осуществления на них жилищного строительства, в том случае, если '") +
		НСтр("ru='в течение налогового периода изменение размера коэффициента 2 на коэффициент 4 не происходит '") +
		НСтр("ru='и отсутствует коэффициент Ки и если по строке 040 Раздела 2 указано значение ""2"", то строка '") +
		НСтр("ru='150 Раздела 2 = ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2) / 100) х 4'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = ((%2 х %3 х %4) / 100) х 4 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно указана сумма исчисленного налога с учетом коэффициента 4'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.Вставить("Коэффициент", 2);
	Настройки.Вставить("КиРавноНулю", Истина);
	
	ПроверитьКС_1_30_код(Настройки);
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_30(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С1400 * Р2С145 * 2 / 100 если Р2С040 = 2, Р2С145 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.30'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Для налогоплательщиков в отношении земельных участков, приобретенных (предоставленных) в '") +
		НСтр("ru='собственность на условиях осуществления на них жилищного строительства, в том случае, если '") +
		НСтр("ru='в течение налогового периода изменение размера коэффициента 2 на коэффициент 4 не происходит '") +
		НСтр("ru='и при наличии коэффициента Ки и если по строке 040 Раздела 2 указано значение ""2"", то строка '") +
		НСтр("ru='150 Раздела 2 = ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2 х строка '") +
		НСтр("ru='145 Раздела 2) / 100) х 4'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = ((%2 х %3 х %4 х %6) / 100) х 4 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно указана сумма исчисленного налога с учетом коэффициента 4'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.Вставить("Коэффициент", 2);
	Настройки.Вставить("КиРавноНулю", Ложь);
	
	ПроверитьКС_1_30_код(Настройки);
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_30_код(Настройки)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = мДанныеОтчета.ДанныеКС.ТаблицаДанныхКС;
	Запрос.УстановитьПараметр("Коэффициент", Настройки.Коэффициент);
	Запрос.УстановитьПараметр("КиРавноНулю", Настройки.КиРавноНулю);
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	ВТ_ТаблицаР2.НомерКадастра КАК НомерКадастра
	|ПОМЕСТИТЬ ВТ_ТаблицаР2_Отбор
	|ИЗ
	|	ВТ_ТаблицаР2 КАК ВТ_ТаблицаР2
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаР2.НомерКадастра
	|
	|ИМЕЮЩИЕ
	|	НЕ(МИНИМУМ(ВТ_ТаблицаР2.С040) = 1
	|			И МАКСИМУМ(ВТ_ТаблицаР2.С040) = 2) И
	|	ВЫБОР
	|		КОГДА &КиРавноНулю
	|			ТОГДА МИНИМУМ(ВТ_ТаблицаР2.С145) = 0
	|		ИНАЧЕ НЕ МАКСИМУМ(ВТ_ТаблицаР2.С145) = 0
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаР2.НомерКадастра КАК Р2КНЗУ,
	|	ВТ_ТаблицаР2.НомерСтраницы КАК НомерСтраницы,
	|	ВТ_ТаблицаР2.С040 КАК Р2С040,
	|	ВТ_ТаблицаР2.С110 КАК Р2С110,
	|	ВТ_ТаблицаР2.С120 КАК Р2С120,
	|	ВТ_ТаблицаР2.С140 КАК Р2С140,
	|	ВТ_ТаблицаР2.С145 КАК Р2С145,
	|	ВТ_ТаблицаР2.С150 КАК Р2С150
	|ИЗ
	|	ВТ_ТаблицаР2 КАК ВТ_ТаблицаР2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаР2_Отбор КАК ВТ_ТаблицаР2_Отбор
	|		ПО ВТ_ТаблицаР2.НомерКадастра = ВТ_ТаблицаР2_Отбор.НомерКадастра
	|ГДЕ
	|	ВТ_ТаблицаР2.С040 = &Коэффициент
	|	И ВЫБОР
	|			КОГДА &КиРавноНулю
	|				ТОГДА ВТ_ТаблицаР2.С145 = 0
	|			ИНАЧЕ НЕ ВТ_ТаблицаР2.С145 = 0
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтраницы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаР2_Отбор";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Настройки.Вставить("ДопСведенияПараметры", 
			Новый Структура("НомерС, НомерК", Выборка.НомерСтраницы, Выборка.Р2КНЗУ));
		ЗарегистрироватьФормулу(Настройки);
		
		Показатели = ЗаполнитьПоказатели("Р2КНЗУ, Р2С040, Р2С110, Р2С120, Р2С140, Р2С145, Р2С150", Выборка,, Истина);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", Выборка.НомерСтраницы);
		
		Коэффициент = ?(Выборка.Р2С040 = 1, 2, 4);
		РасчетнаяСумма1	= Округлить(Выборка.Р2С110 * Выборка.Р2С120 * Выборка.Р2С140 * Коэффициент / 100);
		РасчетнаяСумма2	= Округлить(Выборка.Р2С110 * Выборка.Р2С120 * Выборка.Р2С140 * Выборка.Р2С145 * Коэффициент / 100);
		РасчетнаяСумма	= ?(Настройки.КиРавноНулю, РасчетнаяСумма1, РасчетнаяСумма2);
		Настройки.ЭтоОшибка		= НЕ Выборка.Р2С150 = РасчетнаяСумма;
		Настройки.Расшифровка	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Выборка.Р2С150, Выборка.Р2С110, Выборка.Р2С120, Выборка.Р2С140, РасчетнаяСумма, Выборка.Р2С145);
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_31(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С140 * (2)(4) / 100 если Р2С040 = (1)(2), Р2С145 не заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.31'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Для налогоплательщиков в отношении земельных участков, приобретенных (предоставленных) в '") +
		НСтр("ru='собственность на условиях осуществления на них жилищного строительства, в случае изменения в '") +
		НСтр("ru='течение налогового периода размера коэффициента 2 на коэффициент 4, если по одному листу в строке '") +
		НСтр("ru='040 Раздела 2 указано значение ""1"", а по второму листу значение ""2"", то по одному листу строка '") +
		НСтр("ru='150 Раздела 2 = ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2) / 100) х 2 '") +
		НСтр("ru='(при отсутствии коэффициента Ки), а по второму листу строка 150 Раздела 2 = ((строка 110 Раздела '") +
		НСтр("ru='2 х строка 120 Раздела 2 х строка 140 Раздела 2) / 100) х 4 (при отсутствии коэффициента Ки)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = ((%2 х %3 х %4) / 100) х %5 Итог [%6]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно указана сумма исчисленного налога с учетом коэффициентов 2 и 4'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.Вставить("КиРавноНулю", Истина);
	
	ПроверитьКС_1_32_код(Настройки);
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_32(СтруктураРезультата)
	
	// Формула КС: Р2С150 = Р2С110 * Р2С120 * Р2С1400 * Р2С145 * (2)(4) / 100 если Р2С040 = (1)(2), Р2С145 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.32'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Для налогоплательщиков в отношении земельных участков, приобретенных (предоставленных) в '") +
		НСтр("ru='собственность на условиях осуществления на них жилищного строительства, в случае изменения в '") +
		НСтр("ru='течение налогового периода размера коэффициента 2 на коэффициент 4, если по одному листу в строке '") +
		НСтр("ru='040 Раздела 2 указано значение ""1"", а по второму листу значение ""2"", то по одному листу строка '") +
		НСтр("ru='150 Раздела 2 = ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2 х строка 145 '") +
		НСтр("ru='Раздела 2) / 100) х 2 (при наличии коэффициента Ки), а по второму листу строка 150 Раздела 2 '") +
		НСтр("ru='= ((строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2 х строка 145 Раздела 2) / '") +
		НСтр("ru='100) х 4 (при наличии коэффициента Ки)'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1, Кадастровый номер %2'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = ((%2 х %3 х %4 х %7) / 100) х %5 Итог [%6]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно указана сумма исчисленного налога с учетом коэффициента 2 и 4'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	Настройки.Вставить("КиРавноНулю", Ложь);
	
	ПроверитьКС_1_32_код(Настройки);
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_32_код(Настройки)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = мДанныеОтчета.ДанныеКС.ТаблицаДанныхКС;
	Запрос.УстановитьПараметр("КиРавноНулю", Настройки.КиРавноНулю);
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	ВТ_ТаблицаР2.НомерКадастра КАК НомерКадастра
	|ПОМЕСТИТЬ ВТ_ТаблицаР2_Отбор
	|ИЗ
	|	ВТ_ТаблицаР2 КАК ВТ_ТаблицаР2
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаР2.НомерКадастра
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ВТ_ТаблицаР2.С040) = 1 И
	|	МАКСИМУМ(ВТ_ТаблицаР2.С040) = 2 И
	|	ВЫБОР
	|		КОГДА &КиРавноНулю
	|			ТОГДА МИНИМУМ(ВТ_ТаблицаР2.С145) = 0
	|		ИНАЧЕ НЕ МАКСИМУМ(ВТ_ТаблицаР2.С145) = 0
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаР2.НомерКадастра КАК Р2КНЗУ,
	|	ВТ_ТаблицаР2.НомерСтраницы КАК НомерСтраницы,
	|	ВТ_ТаблицаР2.С040 КАК Р2С040,
	|	ВТ_ТаблицаР2.С110 КАК Р2С110,
	|	ВТ_ТаблицаР2.С120 КАК Р2С120,
	|	ВТ_ТаблицаР2.С140 КАК Р2С140,
	|	ВТ_ТаблицаР2.С145 КАК Р2С145,
	|	ВТ_ТаблицаР2.С150 КАК Р2С150
	|ИЗ
	|	ВТ_ТаблицаР2 КАК ВТ_ТаблицаР2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаР2_Отбор КАК ВТ_ТаблицаР2_Отбор
	|		ПО ВТ_ТаблицаР2.НомерКадастра = ВТ_ТаблицаР2_Отбор.НомерКадастра
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтраницы
	|ИТОГИ
	|	МИНИМУМ(НомерСтраницы)
	|ПО
	|	Р2КНЗУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаР2_Отбор";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаК = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаК.Следующий() Цикл
		Выборка = ВыборкаК.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Настройки.Вставить("ДопСведенияПараметры", 
				Новый Структура("НомерС, НомерК", Выборка.НомерСтраницы, Выборка.Р2КНЗУ));
			ЗарегистрироватьФормулу(Настройки);
		
			Показатели = ЗаполнитьПоказатели("Р2КНЗУ, Р2С040, Р2С110, Р2С120, Р2С140, Р2С145, Р2С150", Выборка,, Истина);
			РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", Выборка.НомерСтраницы);
			
			Коэффициент = ?(Выборка.Р2С040 = 1, 2, 4);
			РасчетнаяСумма1	= Округлить(Выборка.Р2С110 * Выборка.Р2С120 * Выборка.Р2С140 * Коэффициент / 100);
			РасчетнаяСумма2	= Округлить(Выборка.Р2С110 * Выборка.Р2С120 * Выборка.Р2С140 * Выборка.Р2С145 * Коэффициент / 100);
			РасчетнаяСумма	= ?(Настройки.КиРавноНулю, РасчетнаяСумма1, РасчетнаяСумма2);
			Настройки.ЭтоОшибка		= НЕ Выборка.Р2С150 = РасчетнаяСумма;
			Настройки.Расшифровка	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
				Выборка.Р2С150, Выборка.Р2С110, Выборка.Р2С120, Выборка.Р2С140, Коэффициент, РасчетнаяСумма, Выборка.Р2С145);
			ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_33(СтруктураРезультата)
	
	// Формула КС: Р2С230 = Р2С150 * Р2С220а / 100 если Р2С230 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.33'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ п.2 ст.387'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Сумма налоговой льготы (строка 230 Раздела 2) должна быть равна произведению значения строки 150 '") +
		НСтр("ru='Раздела 2 х процент, на который уменьшается исчисленная сумма налога (строка 220а) / 100'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%2 х %3) / 100 Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, НСтр("ru='Неправильно указана сумма налоговой льготы'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С150, Р2С220, Р2С230", Раздел2);
		Если Не (ЗначениеЗаполнено(Показатели.Р2С230.Значение) И ЗначениеЗаполнено(Показатели.Р2С220.Значение)) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить(Показатели.Р2С150.Значение * Показатели.Р2С220.Значение / 100);
		ЭтоОшибка = Не Показатели.Р2С230.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С230.Значение, Показатели.Р2С150.Значение, Показатели.Р2С220.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_34(СтруктураРезультата)
	
	// Формула КС: Р2С250 = Р2С150 - Р2С230 если Р2С230 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.34'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (строка 250 Раздела 2) '") +
		НСтр("ru='должна быть равна разнице значение строк 150 и 230 Раздела 2, если строка 230 Раздела 2 заполнена'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 - %3 Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно определена исчисленная сумма земельного налога, подлежащая уплате в бюджет '") +
		НСтр("ru='за налоговый период'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С150, Р2С190, Р2С210, Р2С230, Р2С250", Раздел2);
		Если Не ЗначениеЗаполнено(Показатели.Р2С230.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С190, Р2С210");
		РасчетнаяСумма = Показатели.Р2С150.Значение - Показатели.Р2С230.Значение;
		ЭтоОшибка = Не Показатели.Р2С250.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С250.Значение, Показатели.Р2С150.Значение, Показатели.Р2С230.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_35(СтруктураРезультата)
	
	// Формула КС: Р2С250 = Р2С150 - Р2С190 если Р2С190 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.35'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (строка 250 Раздела 2) '") +
		НСтр("ru='должна быть равна разнице значение строк 150 и 190 Раздела 2, если строка 190 Раздела 2 заполнена'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 - %3 Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно определена исчисленная сумма земельного налога, подлежащая уплате в '") +
		НСтр("ru='бюджет за налоговый период'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С150, Р2С190, Р2С210, Р2С230, Р2С250", Раздел2);
		Если Не ЗначениеЗаполнено(Показатели.Р2С190.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С210, Р2С230");
		РасчетнаяСумма = Округлить(Показатели.Р2С150.Значение - Показатели.Р2С190.Значение);
		ЭтоОшибка = Не Показатели.Р2С250.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С250.Значение, Показатели.Р2С150.Значение, Показатели.Р2С190.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_36(СтруктураРезультата)
	
	// Формула КС: Р2С250 = Р2С150 - Р2С210 если Р2С210 заполнена	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.36'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (строка 250 Раздела 2) '") +
		НСтр("ru='должна быть равна разнице значений строк 150 и 210 Раздела 2, если строка 210 Раздела 2 заполнена'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = %2 - %3 Итог [%4]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно определена исчисленная сумма земельного налога, подлежащая уплате в '") +
		НСтр("ru='бюджет за налоговый период'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С150, Р2С190, Р2С210, Р2С230, Р2С250", Раздел2);
		Если Не ЗначениеЗаполнено(Показатели.Р2С210.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы,, "Р2С190, Р2С230");
		РасчетнаяСумма = Округлить(Показатели.Р2С150.Значение - Показатели.Р2С210.Значение);
		ЭтоОшибка = Не Показатели.Р2С250.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С250.Значение, Показатели.Р2С150.Значение, Показатели.Р2С210.Значение, РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Процедура	ПроверитьКС_1_37(СтруктураРезультата)
	
	// Формула КС: Р2С250 = Р2С110 * Р2С120 * Р2С140 если Р2С240 заполнена
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ТекстФормулы"		, НСтр("ru='Проверка КС 1.37'"));
	Заголовки.Вставить("Нарушение"			, НСтр("ru='НК РФ ст.396'"));
	Заголовки.Вставить("ОписаниеКС"			, 
		НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (строка 250 Раздела 2) '") +
		НСтр("ru='должна быть равна (строка 110 Раздела 2 х строка 120 Раздела 2 х строка 140 Раздела 2) / 100, '") +
		НСтр("ru='если строка 240 Раздела 2 заполнена'"));
	Заголовки.Вставить("ШаблонДопСведений"	, НСтр("ru='Раздел 2, Страница %1'"));
	Заголовки.Вставить("ШаблонРасшифровки"	, НСтр("ru='%1 = (%2 х %3 х %4) / 100 Итог [%5]'"));
	Заголовки.Вставить("ОписаниеОшибки"		, 
		НСтр("ru='Неправильно определена исчисленная сумма земельного налога, подлежащая уплате в '") +
		НСтр("ru='бюджет за налоговый период'"));
	Настройки = ОбработатьНастройки(		, Заголовки, СтруктураРезультата);
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2С110, Р2С120, Р2С140, Р2С240_Код, Р2С250", Раздел2);
		Если Не ЗначениеЗаполнено(Показатели.Р2С240_Код.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Настройки.Вставить("ДопСведенияПараметры", Новый Структура("Номер", НомерСтраницы));
		ЗарегистрироватьФормулу(Настройки);
		РасшифроватьПоказатели(Показатели, Настройки, "Раздел2", НомерСтраницы);
		РасчетнаяСумма = Округлить(Показатели.Р2С110.Значение * Показатели.Р2С120.Значение * 
			Показатели.Р2С140.Значение / 100);
		ЭтоОшибка = Не Показатели.Р2С250.Значение = РасчетнаяСумма;
		Настройки.Расшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Настройки.ШаблонРасшифровки, 
			Показатели.Р2С250.Значение, Показатели.Р2С110.Значение, Показатели.Р2С120.Значение, Показатели.Р2С140.Значение, 
			РасчетнаяСумма);
		Настройки.ЭтоОшибка = ЭтоОшибка;
		ДобавитьЭлементВСтруктуруОшибок(Настройки);
		
	КонецЦикла;
	
	СтруктураРезультата = ОбработатьРезультат(Настройки, СтруктураРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область КС_ФормированиеКонтекста

&НаСервере
Функция		ПоказательПоСтроке(УсловныйКодПоказателя)
	
	Результат = Новый Структура;
	Результат.Вставить("Имя");
	Результат.Вставить("Представление");
	Результат.Вставить("ИзменяемаяРазмерность");
		
	Если УсловныйКодПоказателя = "Р1С010" Тогда
		Результат.Вставить("Имя", "П000010001003_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Код бюджетной классификации'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С020" Тогда
		Результат.Вставить("Имя", "П000010002003_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Код по ОКТМО'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С021" Тогда
		Результат.Вставить("Имя", "П000010002103_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С023" Тогда
		Результат.Вставить("Имя", "П000010002303_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма авансовых платежей, подлежащая уплате в бюджет за первый квартал (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С025" Тогда
		Результат.Вставить("Имя", "П000010002503_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма авансовых платежей, подлежащая уплате в бюджет за второй квартал (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С027" Тогда
		Результат.Вставить("Имя", "П000010002703_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма авансовых платежей, подлежащая уплате в бюджет за третий квартал (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С030" Тогда
		Результат.Вставить("Имя", "П000010003003_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма налога, подлежащая уплате в бюджет (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р1С040" Тогда
		Результат.Вставить("Имя", "П000010004003_%1");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма налога, исчисленная к уменьшению (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2КНЗУ" Тогда
		Результат.Вставить("Имя", "П000020000100");
		Результат.Вставить("Представление", 
			НСтр("ru='Кадастровый номер земельного участка'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С010" Тогда
		Результат.Вставить("Имя", "П000020001003");
		Результат.Вставить("Представление", 
			НСтр("ru='Код бюджетной классификации'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С020" Тогда
		Результат.Вставить("Имя", "П000020002003");
		Результат.Вставить("Представление", 
			НСтр("ru='Код по ОКТМО'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С040" Тогда
		Результат.Вставить("Имя", "П000020004003");
		Результат.Вставить("Представление", 
			НСтр("ru='Период строительства'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С050" Тогда
		Результат.Вставить("Имя", "П000020005003");
		Результат.Вставить("Представление", 
			НСтр("ru='Кадастровая стоимость (доля кадастровой стоимости) земельного участка / '") +
			НСтр("ru='Нормативная цена земли (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С060_Числитель" Тогда
		Результат.Вставить("Имя", "П000020006003_Числ");
		Результат.Вставить("Представление", 
			НСтр("ru='Доля налогоплательщика в праве на земельный участок (числитель)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С060_Знаменатель" Тогда
		Результат.Вставить("Имя", "П000020006003_Знам");
		Результат.Вставить("Представление", 
			НСтр("ru='Доля налогоплательщика в праве на земельный участок (знаменатель)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С070_Код" Тогда
		Результат.Вставить("Имя", "П000020007003_КодЛьготы");
		Результат.Вставить("Представление", 
			НСтр("ru='Код налоговой льготы в виде не облагаемой налогом суммы'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С070_Основание" Тогда
		Результат.Вставить("Имя", "П000020007003_Основание");
		Результат.Вставить("Представление", 
			НСтр("ru='Основание налоговой льготы в виде не облагаемой налогом суммы'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С080" Тогда
		Результат.Вставить("Имя", "П000020008003");
		Результат.Вставить("Представление", 
			НСтр("ru='Не облагаемая налогом сумма (в рублях) (п. 2 ст. 387 НК РФ)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С090_Код" Тогда
		Результат.Вставить("Имя", "П000020009003_КодЛьготы");
		Результат.Вставить("Представление", 
			НСтр("ru='Код налоговой льготы в виде доли необлагаемой площади земельного участка'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С090_Основание" Тогда
		Результат.Вставить("Имя", "П000020009003_Основание");
		Результат.Вставить("Представление", 
			НСтр("ru='Основание налоговой льготы в виде доли необлагаемой площади земельного участка'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С100_Числитель" Тогда
		Результат.Вставить("Имя", "П000020010003_Числ");
		Результат.Вставить("Представление", 
			НСтр("ru='Доля необлагаемой площади земельного участка (п. 2 ст. 387 НК РФ) (числитель)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С100_Знаменатель" Тогда
		Результат.Вставить("Имя", "П000020010003_Знам");
		Результат.Вставить("Представление", 
			НСтр("ru='Доля необлагаемой площади земельного участка (п. 2 ст. 387 НК РФ) (знаменатель)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С110" Тогда
		Результат.Вставить("Имя", "П000020011003");
		Результат.Вставить("Представление", 
			НСтр("ru='Налоговая база (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С120" Тогда
		Результат.Вставить("Имя", "П000020012003");
		Результат.Вставить("Представление", 
			НСтр("ru='Налоговая ставка (%)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С130" Тогда
		Результат.Вставить("Имя", "П000020013003");
		Результат.Вставить("Представление", 
			НСтр("ru='Количество полных месяцев владения земельным участком в течение налогового периода'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С140" Тогда
		Результат.Вставить("Имя", "П000020014003");
		Результат.Вставить("Представление", 
			НСтр("ru='Коэффициент Кв'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С145" Тогда
		Результат.Вставить("Имя", "П000020014503");
		Результат.Вставить("Представление", 
			НСтр("ru='Коэффициент Ки'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С150" Тогда
		Результат.Вставить("Имя", "П000020015003");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма исчисленного налога (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С160" Тогда
		Результат.Вставить("Имя", "П000020016003");
		Результат.Вставить("Представление", 
			НСтр("ru='Количество полных месяцев использования льготы'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С170" Тогда
		Результат.Вставить("Имя", "П000020017003");
		Результат.Вставить("Представление", 
			НСтр("ru='Коэффициент Кл'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С180_Код" Тогда
		Результат.Вставить("Имя", "П000020018003_КодЛьготы");
		Результат.Вставить("Представление", 
			НСтр("ru='Код налоговой льготы в виде освобождения от налогообложения'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С180_Основание" Тогда
		Результат.Вставить("Имя", "П000020018003_Основание");
		Результат.Вставить("Представление", 
			НСтр("ru='Основание налоговой льготы в виде освобождения от налогообложения'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С190" Тогда
		Результат.Вставить("Имя", "П000020019003");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма налоговой льготы (в рублях) (п. 2 ст. 387 НК РФ)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С200" Тогда
		Результат.Вставить("Имя", "П000020020003");
		Результат.Вставить("Представление", 
			НСтр("ru='Код налоговой льготы в виде освобождения от налогообложения'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С210" Тогда
		Результат.Вставить("Имя", "П000020021003");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма налоговой льготы (в рублях) (ст. 395, ст. 7 НК РФ)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С220_Код" Тогда
		Результат.Вставить("Имя", "П000020022003_КодЛьготы");
		Результат.Вставить("Представление", 
			НСтр("ru='Код налоговой льготы в виде уменьшения суммы налога'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С220_Основание" Тогда
		Результат.Вставить("Имя", "П000020022003_Основание");
		Результат.Вставить("Представление", 
			НСтр("ru='Основание налоговой льготы в виде уменьшения суммы налога'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С220" Тогда
		Результат.Вставить("Имя", "П000020022003_Процент");
		Результат.Вставить("Представление", 
			НСтр("ru='Процент, уменьшающий исчисленную сумму налога'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С230" Тогда
		Результат.Вставить("Имя", "П000020023003");
		Результат.Вставить("Представление", 
			НСтр("ru='Сумма налоговой льготы (в рублях) (п. 2 ст. 387 НК РФ)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С240_Код" Тогда
		Результат.Вставить("Имя", "П000020024003_КодЛьготы");
		Результат.Вставить("Представление", 
			НСтр("ru='Код налоговой льготы в виде снижения налоговой ставки'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С240_Основание" Тогда
		Результат.Вставить("Имя", "П000020024003_Основание");
		Результат.Вставить("Представление", 
			НСтр("ru='Основание налоговой льготы в виде снижения налоговой ставки'"));
		Результат.Вставить("ИзменяемаяРазмерность", Ложь);
		
	ИначеЕсли УсловныйКодПоказателя = "Р2С250" Тогда
		Результат.Вставить("Имя", "П000020025003");
		Результат.Вставить("Представление", 
			НСтр("ru='Исчисленная сумма налога, подлежащая уплате в бюджет за налоговый период (в рублях)'"));
		Результат.Вставить("ИзменяемаяРазмерность", Истина);
		
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр(
		"ru='Не найден показатель соответствующий условному коду: """ + УсловныйКодПоказателя + """'");
		Сообщение.Сообщить();
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

&НаСервере
Функция		СтруктураДанныхДокументаОтчета(ДокументОтчета)
	
	СтруктураДанныхОтчета = Новый Структура;
	
	СохраненнаяСтруктураДанныхОтчета = ДокументОтчета.ДанныеОтчета.Получить();
	
	// Данные обычных разделов.
	Для Каждого Раздел Из СохраненнаяСтруктураДанныхОтчета.ПоказателиОтчета Цикл
		СтруктураДанныхОтчета.Вставить(СтрЗаменить(Раздел.Ключ, "ПолеТабличногоДокумента", ""),
			РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
	КонецЦикла;
	
	// Данные многоуровневых разделов.
	Для Каждого Раздел Из СохраненнаяСтруктураДанныхОтчета.ДанныеМногоуровневыхРазделов Цикл
		СтруктураДанныхОтчета.Вставить(Раздел.Ключ, ТаблицаДанныхДерева(Раздел.Значение));
	КонецЦикла;
	
	Возврат СтруктураДанныхОтчета;
	
КонецФункции

&НаСервере
Процедура	ДеревоВТаблицу(Дерево, Таблица, МнЧ = Ложь, НомСтраницы = Неопределено)
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Индекс = СтрокаЧГ0(Дерево.Строки.Индекс(СтрокаДерева) + 1);
		
		Если (НомСтраницы = Неопределено)
		 ИЛИ (СтрокаДерева.Уровень() = 0 И НЕ МнЧ) Тогда
			НомСтраницы = Индекс;
		КонецЕсли;
		
		Если ТипЗнч(СтрокаДерева.Данные) = Тип("Структура") Тогда
			Для Каждого Элемент Из СтрокаДерева.Данные Цикл
				НоваяСтрока = Таблица.Добавить();
				НоваяСтрока.Страница = НомСтраницы;
				НоваяСтрока.Имя = Элемент.Ключ;
				НоваяСтрока.Значение = Элемент.Значение;
				НоваяСтрока.Строка = ?(МнЧ, Индекс, "");
			КонецЦикла;
			
			Для Каждого Элемент Из СтрокаДерева.ДанныеМногострочныхЧастей Цикл
				Если ТипЗнч(Элемент.Значение) = Тип("ДеревоЗначений") Тогда
					ДеревоВТаблицу(Элемент.Значение, Таблица, Истина, НомСтраницы);
				Иначе
					НоваяСтрока = Таблица.Добавить();
					НоваяСтрока.Страница = НомСтраницы;
					НоваяСтрока.Имя = Элемент.Ключ;
					НоваяСтрока.Значение = Элемент.Значение;
					НоваяСтрока.Строка = ?(МнЧ, Индекс, "");
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			ДеревоВТаблицу(СтрокаДерева, Таблица, , НомСтраницы);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция		ТаблицаДанныхДерева(ДанныеРаздела)
	
	ТаблицаЗаполнения = ТаблицаДляЗаполнения();
	ДеревоВТаблицу(ДанныеРаздела, ТаблицаЗаполнения);
	
	ТаблицаДанных = ТаблицаДляДанных();
	
	Страница = Неопределено;
	ДанныеСтраницы = Новый Структура;
	
	Для Каждого Элемент Из ТаблицаЗаполнения Цикл
		
		Если Страница <> Элемент.Страница Тогда
			
			Страница = Элемент.Страница;
			ДанныеСтраницы = Новый Структура;
			
			НоваяСтрока = ТаблицаДанных.Добавить();
			НоваяСтрока.НомерСтраницы = Страница;
			НоваяСтрока.Данные = ДанныеСтраницы;
			
		КонецЕсли;
		
		ДанныеСтраницы.Вставить(Элемент.Имя + ?(Элемент.Строка = "", "", "_" + Элемент.Строка), Элемент.Значение);
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

&НаСервере
Функция		ТаблицаДляДанных()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("НомерСтраницы");
	Таблица.Колонки.Добавить("Данные");
	
	Возврат Таблица;
	
КонецФункции

&НаСервере
Функция		ТаблицаДляЗаполнения()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Страница");
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Значение");
	Таблица.Колонки.Добавить("Строка");
	
	Возврат Таблица;
	
КонецФункции

&НаСервере
Процедура	СоздатьТаблицуДанныхКС()
	
	ТаблицаР2	= Новый ТаблицаЗначений;
	
	К4			= Новый КвалификаторыСтроки(4);
	К100		= Новый КвалификаторыСтроки(100);
	
	ТаблицаР2.Колонки.Добавить("НомерКадастра"	, Новый ОписаниеТипов("Строка",,К100));
	ТаблицаР2.Колонки.Добавить("НомерСтраницы"	, Новый ОписаниеТипов("Строка",,К4));
	ТаблицаР2.Колонки.Добавить("С040"			, Новый ОписаниеТипов("Число"));
	ТаблицаР2.Колонки.Добавить("С110"			, Новый ОписаниеТипов("Число"));
	ТаблицаР2.Колонки.Добавить("С120"			, Новый ОписаниеТипов("Число"));
	ТаблицаР2.Колонки.Добавить("С140"			, Новый ОписаниеТипов("Число"));
	ТаблицаР2.Колонки.Добавить("С145"			, Новый ОписаниеТипов("Число"));
	ТаблицаР2.Колонки.Добавить("С150"			, Новый ОписаниеТипов("Число"));
	
	КоличествоСтраниц = мДанныеОтчета.Раздел2.Количество();
	
	Для НомерСтраницы = 1 По КоличествоСтраниц Цикл
		
		Раздел2 = мДанныеОтчета.Раздел2[НомерСтраницы - 1].Данные;
		Показатели = ЗаполнитьПоказатели("Р2КНЗУ, Р2С040, Р2С110, Р2С120, Р2С140, Р2С145, Р2С150", Раздел2);
		НоваяСтрока = ТаблицаР2.Добавить();
		НоваяСтрока.НомерКадастра = Показатели.Р2КНЗУ.Значение;
		НоваяСтрока.НомерСтраницы = "" + НомерСтраницы;
		НоваяСтрока.С040 = Показатели.Р2С040.Значение;
		НоваяСтрока.С110 = Показатели.Р2С110.Значение;
		НоваяСтрока.С120 = Показатели.Р2С120.Значение;
		НоваяСтрока.С140 = Показатели.Р2С140.Значение;
		НоваяСтрока.С145 = Показатели.Р2С145.Значение;
		НоваяСтрока.С150 = Показатели.Р2С150.Значение;
		
	КонецЦикла;
	
	ТаблицаДанныхКС = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ТаблицаДанныхКС;
	Запрос.УстановитьПараметр("ТаблицаР2", ТаблицаР2);
	
	Запрос.Текст =
	
	"ВЫБРАТЬ
	|	ТаблицаР2.НомерКадастра КАК НомерКадастра,
	|	ТаблицаР2.НомерСтраницы КАК НомерСтраницы,
	|	ТаблицаР2.С040 КАК С040,
	|	ТаблицаР2.С110 КАК С110,
	|	ТаблицаР2.С120 КАК С120,
	|	ТаблицаР2.С140 КАК С140,
	|	ТаблицаР2.С145 КАК С145,
	|	ТаблицаР2.С150 КАК С150
	|ПОМЕСТИТЬ ВТ_ТаблицаР2
	|ИЗ
	|	&ТаблицаР2 КАК ТаблицаР2";

	Запрос.Выполнить();
	мДанныеОтчета.ДанныеКС.Вставить("ТаблицаДанныхКС", ТаблицаДанныхКС);
	
КонецПроцедуры

#КонецОбласти

#Область КС_Сервис

&НаСервере
Функция		ОбработатьНастройки(Знач Настройки = Неопределено, 
	Знач ИзменяемыеСвойства 	= Неопределено, 
	Знач СтруктураРезультата 	= Неопределено)
	
	Если Настройки = Неопределено Тогда
		
		Настройки = Новый Структура;
		Настройки.Вставить("ТекстФормулы"			, (НСтр("ru=''")));
		Настройки.Вставить("Нарушение"				, (НСтр("ru=''")));
		Настройки.Вставить("ОписаниеКС"				, (НСтр("ru=''")));
		Настройки.Вставить("ОписаниеОшибки"			, (НСтр("ru=''")));
		Настройки.Вставить("ОписаниеОшибок"			, Новый Массив);
		Настройки.Вставить("Расшифровка"			, (НСтр("ru=''")));
		Настройки.Вставить("ШаблонРасшифровки"		, (НСтр("ru=''")));
		Настройки.Вставить("ДопСведения"			, (НСтр("ru=''")));
		Настройки.Вставить("ШаблонДопСведений"		, (НСтр("ru=''")));
		Настройки.Вставить("ДопСведенияПараметры"	, Новый Структура);
		Настройки.Вставить("ЭтоОшибка"				, Ложь);
		Настройки.Вставить("СтруктураОшибок");
		Настройки.Вставить("КоличествоНайденныхОшибок");
		Настройки.Вставить("КоличествоНайденныхБезОшибок");
		Настройки.Вставить("ДопСведенияВОписаниеКС");
		Настройки.Вставить("ДопСведенияВЗаголовокРасшифровки");
		Настройки.Вставить("ДопСведенияВСтрокуРасшифровки");
	
	КонецЕсли;
	
	Если Не ИзменяемыеСвойства = Неопределено Тогда
		Для каждого ЭлементКоллекции Из ИзменяемыеСвойства Цикл
			Если Настройки.Свойство(ЭлементКоллекции.Ключ) Тогда
				Настройки.Вставить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Если Не СтруктураРезультата = Неопределено Тогда
		Для каждого ЭлементКоллекции Из СтруктураРезультата Цикл
			Если Настройки.Свойство(ЭлементКоллекции.Ключ) Тогда
				Настройки.Вставить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

&НаСервере
Функция		ЗаполнитьПоказатели(Знач СтрокаПоказателей, Источник, НомерСтроки = 0, ПоКолонке = Ложь)

	МассивНазваний			= СтрРазделить(СтрокаПоказателей, ", ", Ложь);
	СтруктураПоказателей	= Новый Структура;
	
	Для каждого ЭлементМассива Из МассивНазваний Цикл
		Показатель = ПоказательПоСтроке(ЭлементМассива);
		СтруктураПоказателей.Вставить(ЭлементМассива, Показатель);
	КонецЦикла;
	
	Для каждого ЭлементКоллекции Из СтруктураПоказателей Цикл
		Если ЗначениеЗаполнено(НомерСтроки) Тогда
			ВставитьЗначениеМногострочногоПоказателя(Источник, ЭлементКоллекции.Значение, НомерСтроки);
		Иначе
			ВставитьЗначениеПоказателя(Источник, ЭлементКоллекции.Значение, ?(ПоКолонке, ЭлементКоллекции.Ключ, ""));
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураПоказателей;
	
КонецФункции

&НаСервере
Процедура	РасшифроватьПоказатели(Знач СтруктураПоказателей, 
	Знач Настройки,
	Знач НазваниеСтраницы,
	Знач НомерСтраницы,
	Знач ВключаяПоказатели	= Неопределено,
	Знач ИсключаяПоказатели	= Неопределено)
	
	Для каждого ЭлементКоллекции Из СтруктураПоказателей Цикл
		
		Если ПропуститьЭлемент(ЭлементКоллекции.Ключ, ВключаяПоказатели, ИсключаяПоказатели) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементКоллекции.Значение.Представление = ПредставлениеПоказателя(ЭлементКоллекции.Значение, 
			Настройки.ДопСведенияВСтрокуРасшифровки, НомерСтраницы);
		ВнестиПоказательВРасшифровку(ЭлементКоллекции.Значение, НазваниеСтраницы, СтрокаЧГ0(НомерСтраницы));
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция		ОбработатьРезультат(Знач Настройки, Знач СтруктураРезультата);
	
	ЗаполнитьЗначенияСвойств(СтруктураРезультата, Настройки,
		"КоличествоНайденныхОшибок, КоличествоНайденныхБезОшибок");
	
	Возврат СтруктураРезультата;
	
КонецФункции

&НаСервере
Функция		ПропуститьЭлемент(НазваниеПоказателя, СтрокаВключая, СтрокаИсключая)
	
	Если СтрокаВключая = Неопределено И СтрокаИсключая = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Обработать	= Не СтрокаВключая = Неопределено И Не СтрНайти(СтрокаВключая, НазваниеПоказателя) = 0
		Или Не СтрокаИсключая = Неопределено И СтрНайти(СтрокаИсключая, НазваниеПоказателя) = 0;
	Исключить	= Не СтрокаИсключая = Неопределено И Не СтрНайти(СтрокаИсключая, НазваниеПоказателя) = 0
		Или Не СтрокаВключая = Неопределено И СтрНайти(СтрокаВключая, НазваниеПоказателя) = 0;
	
	Если Исключить Тогда
		Возврат Истина;
	КонецЕсли; 
	
	Если Обработать Тогда
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура	ЗарегистрироватьФормулу(Знач Настройки);

	СчетчикФормулДляРасшифровок = СчетчикФормулДляРасшифровок + 1;
	ОписаниеКС = СформироватьОписаниеКС(Настройки, "ДопСведенияВЗаголовокРасшифровки");
	РегламентированнаяОтчетность.ЗарегистрироватьФормулуДляРасшифровки(СписокРасшифровкиФормулы, 
		ОписаниеКС, , СчетчикФормулДляРасшифровок);
	
КонецПроцедуры

&НаСервере
Функция		СформироватьОписаниеКС(Знач Настройки, ЗначениеСвойства)
	
	Если Настройки[ЗначениеСвойства] Тогда
		
		Если Не Настройки.ДопСведенияПараметры = Неопределено И
			Не Настройки.ДопСведенияПараметры.Количество() = 0 Тогда
			
			МассивПараметров = Новый Массив;
			Для каждого ЭлементКоллекции Из Настройки.ДопСведенияПараметры Цикл
				МассивПараметров.Добавить(ЭлементКоллекции.Значение);
			КонецЦикла;
			
			КоличествоПараметров = МассивПараметров.Количество();
			
			Если КоличествоПараметров = 1 Тогда
				Настройки.ДопСведения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Настройки.ШаблонДопСведений, МассивПараметров[0]);
			ИначеЕсли КоличествоПараметров = 2 Тогда
				Настройки.ДопСведения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Настройки.ШаблонДопСведений, МассивПараметров[0], МассивПараметров[1]);
			ИначеЕсли КоличествоПараметров = 3 Тогда
				Настройки.ДопСведения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Настройки.ШаблонДопСведений, МассивПараметров[0], МассивПараметров[1], МассивПараметров[2]);
			ИначеЕсли КоличествоПараметров = 4 Тогда
				Настройки.ДопСведения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Настройки.ШаблонДопСведений, МассивПараметров[0], МассивПараметров[1], МассивПараметров[2], 
					МассивПараметров[3]);
			ИначеЕсли КоличествоПараметров = 5 Тогда
				Настройки.ДопСведения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Настройки.ШаблонДопСведений, МассивПараметров[0], МассивПараметров[1], МассивПараметров[2], 
					МассивПараметров[3], МассивПараметров[4]);
			Иначе
				Настройки.ДопСведения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Настройки.ШаблонДопСведений, МассивПараметров[0], МассивПараметров[1], МассивПараметров[2], 
					МассивПараметров[3], МассивПараметров[4], МассивПараметров[5]);
			КонецЕсли;
				
			Возврат Настройки.ОписаниеКС + " (" + Настройки.ДопСведения + ")"; 
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Настройки.ОписаниеКС;
	
КонецФункции

&НаСервере
Процедура	ДобавитьЭлементВСтруктуруОшибок(Настройки)
	
	Если Настройки.ЭтоОшибка Тогда
		Настройки.КоличествоНайденныхОшибок = Настройки.КоличествоНайденныхОшибок + 1;
	Иначе
		Настройки.КоличествоНайденныхБезОшибок = Настройки.КоличествоНайденныхБезОшибок + 1;
	КонецЕсли;
	
	Счетчик		= Формат(Настройки.КоличествоНайденныхОшибок + Настройки.КоличествоНайденныхБезОшибок, "ЧГ=0");
	ОписаниеКС	= СформироватьОписаниеКС(Настройки, "ДопСведенияВОписаниеКС");
	ОписаниеКС	= ?(ПустаяСтрока(Настройки.ТекстФормулы), "", Настройки.ТекстФормулы + Символы.ПС) + ОписаниеКС;
	
	Настройки.СтруктураОшибок.Вставить("ТекстовоеПредставлениеФормулы" + Счетчик, ОписаниеКС);
	Настройки.СтруктураОшибок.Вставить("Расшифровка"	+ Счетчик, Настройки.Расшифровка);
	Настройки.СтруктураОшибок.Вставить("ЭтоОшибка"		+ Счетчик, Настройки.ЭтоОшибка);
	Настройки.СтруктураОшибок.Вставить("Нарушение"		+ Счетчик, Настройки.Нарушение);
	Настройки.СтруктураОшибок.Вставить("Описание"		+ Счетчик, Настройки.ОписаниеОшибки);
	Настройки.СтруктураОшибок.Вставить("Комментарий"	+ Счетчик, НСтр("ru=''"));
	
КонецПроцедуры

&НаСервере
Функция		ТаблицаСоответствий(Данные)
	
	Перем ЗначениеКБК, ЗначениеОКТ;
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("НомерСтраницы");
	Результат.Колонки.Добавить("НомерСтроки");
	
	ПоказательКБК = СтрЗаменить(ПоказательПоСтроке("Р1С010").Имя, "%1", "");
	ПоказательОКТ = СтрЗаменить(ПоказательПоСтроке("Р1С020").Имя, "%1", "");
	
	НомерСтраницы = 0;
	
	Для каждого Страница Из Данные Цикл
		НомерСтраницы = НомерСтраницы + 1;
		НомерСтроки = 1;
		Пока Страница.Данные.Свойство(ПоказательКБК + СтрокаЧГ0(НомерСтроки), ЗначениеКБК) И
			Страница.Данные.Свойство(ПоказательОКТ + СтрокаЧГ0(НомерСтроки), ЗначениеОКТ) Цикл
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.НомерСтраницы = НомерСтраницы;
			НоваяСтрока.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла; 
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция		ПредставлениеПоказателя(Показатель, ПолноеПредставление, НомерСтраницы = Неопределено)
	
	Раздел = Новый Структура();
	Раздел.Вставить("П00001", "Раздел 1");
	Раздел.Вставить("П00002", "Раздел 2");
	
	ПодстрокаРаздела = Раздел[Лев(Показатель.Имя, 6)];
	ПодстрокаСтроки = Сред(Показатель.Имя, 9, 3);
	ПодстрокаСтраницы = ?(ПодстрокаРаздела = "Раздел 1", "", Нстр("ru=' Страница '") + НомерСтраницы);
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru='Строка %1 %2%3'"), 
		ПодстрокаСтроки, ПодстрокаРаздела, ПодстрокаСтраницы);
	Представление = ?(ПолноеПредставление, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru='%1. %2'"), 
		Представление, Показатель.Представление), Представление);
	
	Возврат Представление;
	
КонецФункции

&НаСервере
Процедура	ВставитьЗначениеПоказателя(ИсточникДанных, Показатель, ИмяКолонки = "")
	
	Если ЗначениеЗаполнено(ИмяКолонки) Тогда
		ЗначениеПоказателя = ИсточникДанных[ИмяКолонки];
	ИначеЕсли ИсточникДанных.Свойство(Показатель.Имя) Тогда
		ЗначениеПоказателя = ИсточникДанных[Показатель.Имя];
	Иначе
		ЗначениеПоказателя = 0;
	КонецЕсли;
	
	Если Показатель.Свойство("ИзменяемаяРазмерность") И Показатель.ИзменяемаяРазмерность = Истина Тогда
		ЗначениеПоказателя = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма, ЗначениеПоказателя);
	КонецЕсли;
	
	Если Показатель.Свойство("Формат") И ЗначениеЗаполнено(Показатель.Формат) Тогда
		ПредставлениеЗначения = Формат(ЗначениеПоказателя, Показатель.Формат);
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Строка") Тогда
		ПредставлениеЗначения = СокрЛП(ЗначениеПоказателя);
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Число") Тогда
		ПредставлениеЗначения = Формат(ЗначениеПоказателя, "ЧРД=,; ЧН=");
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
		ПредставлениеЗначения = Формат(ЗначениеПоказателя, "ДЛФ=D");
	Иначе
		ПредставлениеЗначения = Строка(ЗначениеПоказателя);
	КонецЕсли;
	
	Показатель.Вставить("Значение", ЗначениеПоказателя);
	Показатель.Вставить("ЗначениеСтр", ПредставлениеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура	ВставитьЗначениеМногострочногоПоказателя(ИсточникДанных, Показатель, НомерСтроки)
	
	НомерСтрокиСтр = СтрокаЧГ0(НомерСтроки);
	ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Показатель.Имя, НомерСтрокиСтр);
	
	Если ИсточникДанных.Свойство(ИмяПоказателя) Тогда
		ЗначениеПоказателя = ИсточникДанных[ИмяПоказателя];
	Иначе
		ЗначениеПоказателя = 0;
	КонецЕсли;
	
	Если Показатель.Свойство("ИзменяемаяРазмерность") И Показатель.ИзменяемаяРазмерность = Истина Тогда
		ЗначениеПоказателя = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма, ЗначениеПоказателя);
	КонецЕсли;
	
	Если Показатель.Свойство("Формат") И ЗначениеЗаполнено(Показатель.Формат) Тогда
		ПредставлениеЗначения = Формат(ЗначениеПоказателя, Показатель.Формат);
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Строка") Тогда
		ПредставлениеЗначения = ЗначениеПоказателя;
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
		ПредставлениеЗначения = Формат(ЗначениеПоказателя, "ДЛФ=D");
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Число") Тогда
		ПредставлениеЗначения = Формат(ЗначениеПоказателя, "ЧРД=,; ЧН=");
	Иначе
		ПредставлениеЗначения = Строка(ЗначениеПоказателя);
	КонецЕсли;
	
	Показатель.Вставить("Значение", ЗначениеПоказателя);
	Показатель.Вставить("ЗначениеСтр", ПредставлениеЗначения);
	Показатель.Вставить("НомерСтроки", НомерСтрокиСтр);
	
КонецПроцедуры

&НаСервере
Процедура	ВнестиПоказательВРасшифровку(Показатель, 
	ИмяСтраницы, НомерСтраницы = Неопределено, ДокументРО = Неопределено)
	
	ПредставлениеПоказателя = Показатель.Представление;
	
	ЗначениеПоказателя = ?(ТипЗнч(Показатель.Значение) = Тип ("Дата"), Показатель.ЗначениеСтр, Показатель.Значение);
	
	Если Показатель.Свойство("НомерСтроки") Тогда
		ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Показатель.Имя, Показатель.НомерСтроки);
	Иначе
		ИмяПоказателя = Показатель.Имя;
	КонецЕсли;
	
	НомСтраницы = НомерСтраницы;
	
	Если ИмяСтраницы = "Раздел1Прил1Подр1_1и1_2" Тогда
		НомСтраницы = НомерСтраницы + ",1";
	ИначеЕсли ИмяСтраницы = "Раздел1Прил1Подр1_3_1" Тогда
		НомСтраницы = НомерСтраницы + ",2";
	ИначеЕсли ИмяСтраницы = "Раздел1Прил1Подр1_3_2" Тогда
		НомСтраницы = НомерСтраницы + ",3";
	ИначеЕсли ИмяСтраницы = "Раздел1Прил1Подр1_4" Тогда
		НомСтраницы = НомерСтраницы + ",4";
	КонецЕсли;
	
	РегламентированнаяОтчетность.ЗарегистрироватьПоказательДляРасшифровки(
		СписокРасшифровкиПоказателей,
		ИмяПоказателя,
		ПредставлениеПоказателя,
		ЗначениеПоказателя,
		"",
		?(ДокументРО = Неопределено, СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка, ДокументРО.Ссылка),
		ИмяСтраницы,
		Неопределено,
		НомСтраницы,
		СчетчикФормулДляРасшифровок);
	
КонецПроцедуры

&НаСервере
Функция		СтрокаЧГ0(ИсходноеЧисло)
	
	Возврат Формат(ИсходноеЧисло, "ЧН=; ЧГ=0");
	
КонецФункции

&НаСервере
Функция		Округлить(Показатель)
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(ЭтаФорма, Показатель);
	
КонецФункции

&НаСервере
Функция		РазделЗаполнен(ДанныеРаздела, ИмяПодраздела = Неопределено)
	
	Результат = Ложь;
	
	ПрефиксПоказателя = ИмяПодраздела;
	
	Если ИмяПодраздела = "Раздел1" Тогда
		ПрефиксПоказателя = "П00001";
	ИначеЕсли ИмяПодраздела = "Раздел2" Тогда
		ПрефиксПоказателя = "П00002";
	КонецЕсли;
	
	Для Каждого СтраницаРаздела Из ДанныеРаздела Цикл
		
		Для Каждого Показатель Из СтраницаРаздела.Данные Цикл
			Результат = ЗначениеЗаполнено(Показатель.Значение);
			
			Если ПрефиксПоказателя = Неопределено Тогда
				Если Результат Тогда
					Возврат Результат;
				КонецЕсли;
			ИначеЕсли СтрНайти(Показатель.Ключ, ПрефиксПоказателя) > 0 Тогда
				Если Результат Тогда
					Возврат Результат;
				КонецЕсли;
			КонецЕсли;
			
			Результат = Ложь;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДатуВЯчейках(Форма)
	
	Форма.ТабличныйДокумент.Области.ДатаПодписи.Значение = Форма.ДатаПодписи;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизацииНаСервере(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда
	
		СтрокаСведений = "ИННЮЛ, КППЮЛ, НаимЮЛПол, КодНО, НаимНО, ТипНП, ТелОрганизации";
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
			СтруктураРеквизитовФормы.Организация, ДатаПодписи, СтрокаСведений);
		
		ТабличныйДокумент.Области.ИНН.Значение = СокрЛП(СведенияОбОрганизации.ИННЮЛ);
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.РегистрацияВНалОргане) Тогда
			
			ТабличныйДокумент.Области["КПП"].Значение
			= СокрЛП(СтруктураРеквизитовФормы.РегистрацияВНалОргане.КПП);
			
			ТабличныйДокумент.Области["НалоговыйОрган"].Значение
			= СокрЛП(СтруктураРеквизитовФормы.РегистрацияВНалОргане.Код);
			
		Иначе
			
			ТабличныйДокумент.Области.КПП.Значение = СокрЛП(СведенияОбОрганизации.КППЮЛ);
			
			КодНО = СведенияОбОрганизации.КодНО;
			Для Инд = СтрДлина(СведенияОбОрганизации.КодНО) + 1 По 4 Цикл
				КодНО = " " + КодНО;
			КонецЦикла;
			ТабличныйДокумент.Области.НалоговыйОрган.Значение = КодНО;
			
		КонецЕсли;
		
		ТабличныйДокумент.Области.НаимОрг.Значение = СокрЛП(СведенияОбОрганизации.НаимЮлПол);
		
		СтруктураРеквизитовФормы.НачальноеЗначениеНаимОрганизации = ТабличныйДокумент.Области.НаимОрг.Значение;
		
		ТабличныйДокумент.Области.ТелОрганизации.Значение = СокрЛП(СведенияОбОрганизации.ТелОрганизации);
		
		ЗаполнитьДатуВЯчейках(ЭтаФорма);
		
	КонецЕсли;
	
	ЗаполнитьСведенияОПодписантах();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОПодписантах() Экспорт
	
	ТипПодписанта = "1";
	Подписант = "";
	НаименованиеОрганизацииПредставителя = "";
	ДокументПредставителя = "";
	
	//выясним, есть ли представитель
	КодНО = СокрЛП(ТабличныйДокумент.Области.НалоговыйОрган.Значение);
	КПП   = СокрЛП(ТабличныйДокумент.Области.КПП.Значение);

	СведенияОПредставителе = РегламентированнаяОтчетностьВызовСервера.ПолучитьПоКодамСведенияОПредставителе(СтруктураРеквизитовФормы.Организация, КодНО, КПП);
	
	ТипПодписанта = СведенияОПредставителе.ТипПодписанта;
	
	Если ТипПодписанта = "1" Тогда
		//представителя нет
		Если РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация) Тогда
			//подписант - руководитель
			СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СтруктураРеквизитовФормы.Организация, ДатаПодписи, "ФИОРук, ФИО");
			Подписант = СокрЛП(?(ЗначениеЗаполнено(СведенияОбОрганизации.ФИОРук), СведенияОбОрганизации.ФИОРук, СведенияОбОрганизации.ФИО));	
		КонецЕсли;
	Иначе
		//есть представитель, подписант - он
	    Подписант 								= СведенияОПредставителе.ФИОПредставителя;
 		НаименованиеОрганизацииПредставителя 	= СведенияОПредставителе.НаименованиеОрганизацииПредставителя;
		ДокументПредставителя 					= СведенияОПредставителе.ДокументПредставителя;
	КонецЕсли;
	
	ТабличныйДокумент.Области.ПрПодп.Значение = ТипПодписанта;
	ТабличныйДокумент.Области.ОргПодписант.Значение = Подписант;
	ТабличныйДокумент.Области.ОргУп.Значение = НаименованиеОрганизацииПредставителя;
	ТабличныйДокумент.Области.ДокУпПред.Значение = ДокументПредставителя;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказатель(Элемент, Область, КодПоказателя, Знач ЭтоСумма, Знач ТекстВыбора, ТаблицаВыбора, СтандартнаяОбработка, КолонкаПоиска = "Код") Экспорт
	
	Если ТаблицаВыбора = Неопределено ИЛИ СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Область.Имя = КодПоказателя ИЛИ МногострочнаяЧастьТекущейОбласти(ЭтаФорма, Область.Имя) <> Неопределено Тогда
		ИсходноеЗначение = ?(ТипЗнч(Область.Значение) = Тип("Строка"), СокрЛП(Область.Значение), Область.Значение);
	Иначе
		Разделитель = ?(Лев(Область.Имя, СтрДлина(КодПоказателя) + 1) = КодПоказателя + "_", "_", "");
		ИндексОбласти = 1;
		ИсходноеЗначение = "";
		Пока ЭтаФорма[ТекущийЭлемент.Имя].Области.Найти(КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")) <> Неопределено Цикл
			ИсходноеЗначение = ИсходноеЗначение + Строка(ЭтаФорма[ТекущийЭлемент.Имя].Области[КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")].Значение);
			ИндексОбласти = ИндексОбласти + 1;
		КонецЦикла;
		ИсходноеЗначение = СокрЛП(ИсходноеЗначение);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",          ТекстВыбора);
	ПараметрыФормы.Вставить("ТаблицаЗначений",    ТаблицаВыбора);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура(КолонкаПоиска, ИсходноеЗначение));
	
	ДополнительныеПараметры = Новый Структура("ИндексОбласти, КодПоказателя, КолонкаПоиска, Область, Разделитель, Элемент", ИндексОбласти, КодПоказателя, КолонкаПоиска, Область, Разделитель, Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиПоказательЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказательЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ИндексОбласти = ДополнительныеПараметры.ИндексОбласти;
	КодПоказателя = ДополнительныеПараметры.КодПоказателя;
	КолонкаПоиска = ДополнительныеПараметры.КолонкаПоиска;
	Область = ДополнительныеПараметры.Область;
	Разделитель = ДополнительныеПараметры.Разделитель;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Область.Имя = КодПоказателя ИЛИ МногострочнаяЧастьТекущейОбласти(ЭтаФорма, Область.Имя) <> Неопределено Тогда
		Область.Значение = РезультатВыбора[КолонкаПоиска];
	Иначе
		СтрокаРезультатВыбора = Строка(РезультатВыбора[КолонкаПоиска]);
		Для Инд = СтрДлина(СтрокаРезультатВыбора) + 1 По ИндексОбласти - 1 Цикл
			СтрокаРезультатВыбора = " " + РезультатВыбора[КолонкаПоиска];
		КонецЦикла;
		Для Инд = 1 По ИндексОбласти - 1 Цикл
			ЭтаФорма[ТекущийЭлемент.Имя].Области[КодПоказателя + Разделитель + Формат(Инд, "ЧГ=")].Значение = СокрЛП(Сред(РезультатВыбора[КолонкаПоиска], Инд, 1));
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ТабличныйДокументПриИзмененииСодержимогоОбласти(Элемент, Область);
	
	Если КодПоказателя = "ПоМесту" ИЛИ КодПоказателя = "КодФормыРеорганизации" Тогда
		УстановитьДоступностьОбластей(ЭтаФорма, "Титульный");
		ЗаполнитьПериодОтчета(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьМногоуровневыйРаздел(ИмяРаздела)
	
	УдалитьИзВременногоХранилища(
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	
	ДеревоДанныхРаздела = Новый ДеревоЗначений;
	ДеревоДанныхРаздела.Колонки.Добавить("Данные");
	ДеревоДанныхРаздела.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	СохрТаблДокумент = Новый ТабличныйДокумент;
	СохрТаблДокумент.Вывести(ТабличныйДокумент);
	
	ТекВетка = ДеревоДанныхРаздела.Строки.Добавить();
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(
	ИДРедакцииОтчета(ЭтаФорма.ИмяФормы) + "_" + ИмяРаздела));
	
	ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяРаздела);
	
	ТекВетка.Данные = ДанныеТаблДокумента.Данные;
	ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
	
	ИнициализироватьПодчиненныеРазделы(ИмяРаздела, ТекВетка);
	
	СтруктураРеквизитовФормы.Вставить("АдресВоВрХранилищеДеревоДанных" + ИмяРаздела,
	ПоместитьВоВременноеХранилище(ДеревоДанныхРаздела, УникальныйИдентификатор));
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(СохрТаблДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	Если Ячейка.Раздел = "Раздел1" Тогда
		
		Для каждого НайденныйРаздел Из РазделыОтчета.ПолучитьЭлементы() Цикл
			Если НайденныйРаздел.КолонкаРазделыОтчетаСокрНаим = Ячейка.Раздел Тогда
				СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
				Элементы.РазделыОтчета.ТекущаяСтрока = НайденныйРаздел.ПолучитьИдентификатор();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Ячейка.Раздел = "Раздел2" Тогда
		
		Для каждого НайденныйРаздел Из РазделыОтчета.ПолучитьЭлементы() Цикл
			Если НайденныйРаздел.КолонкаРазделыОтчетаСокрНаим = Ячейка.Раздел Тогда
				Для каждого НайденнаяСтраница Из НайденныйРаздел.ПолучитьЭлементы() Цикл
					Если НайденнаяСтраница.КолонкаНомерСтраницыРазделаОтчета = Число(Ячейка.Страница) Тогда
						СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
						Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтраница.ПолучитьИдентификатор();
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Ячейка.Раздел = "Титульный" Тогда
		РегламентированнаяОтчетностьКлиент.АктивизироватьЯчейку(ЭтаФорма, Ячейка);
	Иначе
		ТекущийЭлемент = Элементы.ТабличныйДокумент;
		НайденнаяОбласть = ТабличныйДокумент.Области.Найти(Ячейка.ИмяЯчейки);
		Если НЕ НайденнаяОбласть = Неопределено Тогда
			Элементы.ТабличныйДокумент.ТекущаяОбласть = НайденнаяОбласть;
		КонецЕсли;
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
		РазделыОтчетаПриАктивизацииСтроки(Элементы.РазделыОтчета);
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДеревоРазделовОтчетаНаКлиенте() Экспорт
	
	// Необходимо для работы универсального механизма.
	Возврат;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоРазделовОтчетаНаСервере()
	
	ЭтаФорма["РазделыОтчета"].ПолучитьЭлементы().Очистить();
	
	Для Каждого ЭлементДереваСтраницОтчета Из ЭтаФорма["мДеревоСтраницОтчета"].ПолучитьЭлементы() Цикл
		
		Если ЭлементДереваСтраницОтчета.ПоказатьСтраницу = 1 Тогда
			
			ЭлементРазделовОтчета = ЭтаФорма["РазделыОтчета"].ПолучитьЭлементы().Добавить();
			ЭлементРазделовОтчета.КолонкаРазделыОтчета         = ЭлементДереваСтраницОтчета.Представление;
			ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			
			ИмяРаздела = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			
			Если мСтруктураМногоуровневыхРазделов.Свойство(ИмяРаздела) Тогда
				
				СтруктураРаздела = мСтруктураМногоуровневыхРазделов[ИмяРаздела];
				
				ДеревоДанныхРаздела = ПолучитьИзВременногоХранилища(
					СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
				
				Если СтруктураРаздела.Свойство("Многостраничность") Тогда
					
					КолСтрокВДеревеДанныхРаздела = Макс(ДеревоДанныхРаздела.Строки.Количество(), 1);
					
					Для НомерСтраницыРаздела = 1 По КолСтрокВДеревеДанныхРаздела Цикл
						
						ЭлементРаздел = ЭлементРазделовОтчета.ПолучитьЭлементы().Добавить();
						
						Если ИмяРаздела = "Раздел2" Тогда
							
							ДанныеСтраницы = ДеревоДанныхРаздела.Строки[НомерСтраницыРаздела - 1].Данные;
							
							СтруктПарам = Новый Структура;
							СтруктПарам.Вставить("КадастрНомер", ДанныеСтраницы.П000020000100);
							СтруктПарам.Вставить("НомерСтраницы", НомерСтраницыРаздела);
							
							ЭлементРаздел.КолонкаРазделыОтчета = ПредставлениеСтраницыРаздела2(СтруктПарам);
							
						Иначе
							
							НаимСтраницы = "Стр. ";
							Если СтруктураРаздела.Свойство("НаимСтраницы") Тогда
								НаимСтраницы = СтруктураРаздела["НаимСтраницы"];
							КонецЕсли;
							ЭлементРаздел.КолонкаРазделыОтчета = НаимСтраницы
							+ ?(Прав(НаимСтраницы, 1) = " ", "", ". Стр. ") + Формат(НомерСтраницыРаздела, "ЧГ=");
							
						КонецЕсли;
						
						ЭлементРаздел.КолонкаРазделыОтчетаСокрНаим      = ИмяРаздела;
						ЭлементРаздел.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницыРаздела;
						
						Если СтруктураРаздела.Свойство("ИменаПодчиненныхРазделов") Тогда
							
							ИменаПодчиненныхРазделов = СтруктураРаздела["ИменаПодчиненныхРазделов"];
							КолПодчРазделов = ИменаПодчиненныхРазделов.Количество();
							
							Для ИндПодчРаздела = 0 По КолПодчРазделов - 1 Цикл
								
								ИмяПодчРаздела = ИменаПодчиненныхРазделов[ИндПодчРаздела];
								СтруктураПодчРаздела = мСтруктураМногоуровневыхРазделов[ИмяПодчРаздела];
								
								ЭлементПодчРаздел = ЭлементРаздел.ПолучитьЭлементы().Добавить();
								НаимСтраницы = "Стр. ";
								Если СтруктураПодчРаздела.Свойство("НаимСтраницы") Тогда
									НаимСтраницы = СтруктураПодчРаздела["НаимСтраницы"];
								КонецЕсли;
								ЭлементПодчРаздел.КолонкаРазделыОтчета              = НаимСтраницы;
								ЭлементПодчРаздел.КолонкаРазделыОтчетаСокрНаим      = ИмяПодчРаздела;
								ЭлементПодчРаздел.КолонкаНомерСтраницыРазделаОтчета = ИндПодчРаздела + 1;
								
							КонецЦикла;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				ДеревоДанныхРаздела = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтраницыРаздела2(СтруктураПараметров)
	
	КадастрНомер = СокрЛП(СтруктураПараметров.КадастрНомер);
	
	Возврат "Стр. " + Формат(СтруктураПараметров.НомерСтраницы, "ЧГ=")
	+ ?(ЗначениеЗаполнено(КадастрНомер), " (" + КадастрНомер + ")", "");
	
КонецФункции

&НаКлиенте
Процедура РазделыОтчетаПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено
	 ИЛИ Элемент.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	Элементы.ОчиститьТекущуюСтраницу.Доступность = Истина;
	Если СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере Тогда
		
		Если мСтруктураМногоуровневыхРазделов.Свойство(Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим) Тогда
			Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета
				= ?(Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = Неопределено
				ИЛИ Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = 0,
				1, Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
		КонецЕсли;
		
		Если мСтруктураМногоуровневыхРазделов.Свойство(Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим) Тогда
			
			НовыеНомераСтрокМногоуровнегоРаздела = Новый Массив;
			
			ТекРодитель = Элемент.ТекущиеДанные.ПолучитьРодителя();
			Если ТекРодитель <> Неопределено Тогда
				ТекЭлемент = Элемент.ТекущиеДанные;
				Пока ТекРодитель.ПолучитьРодителя() <> Неопределено Цикл
					СтруктураЭлемента = мСтруктураМногоуровневыхРазделов[ТекЭлемент.КолонкаРазделыОтчетаСокрНаим];
					Если СтруктураЭлемента.Свойство("Подчиненность") Тогда
						ИмяГлРаздела = СтруктураЭлемента["ИмяГлавногоРаздела"];
						СтруктураРодителя = мСтруктураМногоуровневыхРазделов[ИмяГлРаздела];
						ИндексПодчРаздела = СтруктураРодителя["ИменаПодчиненныхРазделов"].Найти(ТекЭлемент.КолонкаРазделыОтчетаСокрНаим);
						НовыеНомераСтрокМногоуровнегоРаздела.Вставить(0, ИндексПодчРаздела + 1);
					Иначе
						НовыеНомераСтрокМногоуровнегоРаздела.Вставить(0, 1);
					КонецЕсли;
					НовыеНомераСтрокМногоуровнегоРаздела.Вставить(0, ТекРодитель.КолонкаНомерСтраницыРазделаОтчета);
					ТекЭлемент = ТекРодитель;
					ТекРодитель = ТекЭлемент.ПолучитьРодителя();
				КонецЦикла;
			КонецЕсли;
			
			СтруктураЭлемента = мСтруктураМногоуровневыхРазделов[Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим];
			Если СтруктураЭлемента.Свойство("Многостраничность") Тогда
				НовыеНомераСтрокМногоуровнегоРаздела.Добавить(Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
			Иначе
				НовыеНомераСтрокМногоуровнегоРаздела.Добавить(1);
			КонецЕсли;
			
			ВывестиДанныеВТабличныйДокументНаСервере(
				Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим,
				НовыеНомераСтрокМногоуровнегоРаздела);
			
		Иначе
			ВывестиДанныеВТабличныйДокументНаСервере(
				Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		КонецЕсли;
		
	Иначе
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
	КонецЕсли;
	
	КМенюРО = Элементы.РазделыОтчета.КонтекстноеМеню;
	КМенюРО.Видимость = Ложь;
	КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Ложь;
	КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Ложь;
	Если мСтруктураМногоуровневыхРазделов.Свойство(
		Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим) Тогда
		Если мСтруктураМногоуровневыхРазделов[Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим].Свойство(
			"Многостраничность") Тогда
			Элемент.Развернуть(Элемент.ТекущаяСтрока);
			КМенюРО.Видимость = Истина;
			Если Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
				Элементы.ОчиститьТекущуюСтраницу.Доступность = Ложь;
			КонецЕсли;
			
			Если НЕ Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено
				И Элемент.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы().Количество() > 1 Тогда
				КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Истина;
				КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Истина;
				
			Иначе
				КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Истина;
				КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Ложь;
				
			КонецЕсли;
			
			#Если ВебКлиент Тогда
				ВидимостьМеню = КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость;
				КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Ложь;
			#КонецЕсли
			
			КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Заголовок = "Добавить страницу";
			
			#Если ВебКлиент Тогда
				КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = ВидимостьМеню;
			#КонецЕсли
		КонецЕсли;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Титульный" Тогда
		Элементы.Обновить.Доступность = Истина;
		Элементы.ТабличныйДокументКонтекстноеМенюГруппаСтроки.Видимость = Ложь;
	Иначе
		Элементы.Обновить.Доступность = Ложь;
		Элементы.ТабличныйДокументКонтекстноеМенюГруппаСтроки.Видимость = Истина;
	КонецЕсли;
	
	СпрятатьСсылкуУдалитьСтраницу();
	
	Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Область(1, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСведенияОбОрганизацииНаСервере();
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		УстановитьДоступностьОбластей(ЭтаФорма, "Титульный");
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
		Заголовок = Заголовок + " за " + СтруктураРеквизитовФормы.СтрПериодОтчета;
	КонецЕсли;
	
	ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
	Если СтрНайти(Заголовок, ОргСтр) = 0 Тогда
		Заголовок = Заголовок + ОргСтр;
	КонецЕсли;
	
	Заголовок = Заголовок + ?(СтрНайти(ЭтаФорма.ИмяФормы, "Внешний") > 0, " - внешний отчет", "");
	
	ПодключитьОбработчикОжидания("ВыполнитьПроверкуПередОткрытием", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуПередОткрытием()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОткрытиемФормыРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически Тогда
		
		ПараметрыОбработчикаОжидания = Новый Структура;
		ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
		ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
		ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
		ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
		
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		
		ФормаДлительнойОперации = ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", Новый Структура(
		"ИдентификаторЗадания", СтруктураРеквизитовФормы.ИдентификаторЗадания), ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьНомерКорректировки(Форма)
	
	НомерКорр = Форма.СтруктураРеквизитовФормы.ВидДокумента * Форма.НомерКорректировки;
	НомерКорректировкиСтрока = Формат(НомерКорр, "ЧН=0; ЧГ=");
	СтрДлинаНомерКорректировкиСтрока = СтрДлина(НомерКорректировкиСтрока);
	
	Форма.ТабличныйДокумент.Области["НомерКорректировки"].Значение = НомерКорректировкиСтрока;
	
Конецпроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СТЫКОВКИ С СЕРВИСОМ СДАЧИ ОТЧЕТНОСТИ ЧЕРЕЗ ПРЕДСТАВИТЕЛЯ

&НаКлиенте
Процедура ОтправитьЧерезПредставителя(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезПредставителяЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителяЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиЧерезПредставителяВРегламентированномОтчете(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВИнтернетеЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма);
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтаФорма);
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	СохранитьНаКлиенте(, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ДополнительныеПараметры.Форма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьОтчетИОткрытьФормуПрисоединенныеФайлы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ОбъектОтчета(ЭтаФормаИмя) Экспорт
	
	Если мОбъектОтчета = Неопределено Тогда
		мОбъектОтчета = РегламентированнаяОтчетностьВызовСервера.ОбъектОтчета(ЭтаФормаИмя);
	КонецЕсли;
	
	Возврат мОбъектОтчета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИДОтчета(ЭтаФормаИмя)
	
	Возврат ЧастьУникальногоИмениФормы(ЭтаФормаИмя, 1);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИДРедакцииОтчета(ЭтаФормаИмя)
	
	Возврат ЧастьУникальногоИмениФормы(ЭтаФормаИмя, 3);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЧастьУникальногоИмениФормы(ЭтаФормаИмя, Индекс)
	
	Возврат СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЭтаФормаИмя, ".")[Индекс];
	
КонецФункции