#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ДанноеУведомлениеДоступноДляОрганизации() Экспорт 
	Возврат Истина;
КонецФункции

Функция ДанноеУведомлениеДоступноДляИП() Экспорт 
	Возврат Истина;
КонецФункции

Функция ПолучитьОсновнуюФорму() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьФормуПоУмолчанию() Экспорт 
	Возврат "Отчет.РегламентированноеУведомлениеПолучениеПатента.Форма.ФормаОтчета";
КонецФункции

Функция ПолучитьТаблицуФорм() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуФормУведомления();
	
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2015_1";
	Стр.ОписаниеФормы = "Приказ ФНС России от 25.01.2017 № ММВ-7-2/34@";
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2019_1";
	Стр.ОписаниеФормы = "Приказ ФНС России от 24.04.2019 № ММВ-7-2/204@";
	
	Возврат Результат;
КонецФункции

Функция ПечатьСразу(Объект, ИмяФормы) Экспорт
	Если ИмяФормы = "Форма2015_1" Или ИмяФормы = "Форма2019_1" Тогда
		Возврат ПечатьСразу_Форма2015_1(Объект);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция СформироватьМакет(Объект, ИмяФормы) Экспорт
	Если ИмяФормы = "Форма2015_1" Или ИмяФормы = "Форма2019_1" Тогда
		Возврат СформироватьМакет_Форма2015_1(Объект);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ЭлектронноеПредставление(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2015_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2015_1(Объект, УникальныйИдентификатор);
	ИначеЕсли ИмяФормы = "Форма2019_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2019_1(Объект, УникальныйИдентификатор);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ПроверитьДокумент(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
КонецФункции

Функция СформироватьСписокЛистов(Объект) Экспорт
	Если Объект.ИмяФормы = "Форма2015_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2015_1(Объект);
	ИначеЕсли Объект.ИмяФормы = "Форма2019_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2019_1(Объект);
	КонецЕсли;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт 
	Если ИмяФормы = "Форма2015_1" Тогда 
		Данные = Объект.ДанныеУведомления.Получить();
		Данные.Вставить("Организация", Объект.Организация);
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2015_1(Данные, УникальныйИдентификатор);
	ИначеЕсли ИмяФормы = "Форма2019_1" Тогда 
		Данные = Объект.ДанныеУведомления.Получить();
		Данные.Вставить("Организация", Объект.Организация);
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2019_1(Данные, УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция СформироватьСписокЛистовФорма2015_1(Объект)
	Листы = Новый СписокЗначений;
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = Объект.Ссылка.ДанныеУведомления.Получить().СтруктураРеквизитов;
	
	ПараметрыПечати = Новый Структура;
	РеквизитыИФНС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.РегистрацияВИФНС, "Наименование, Код"); 
	ПараметрыПечати.Вставить("НаимНО", РеквизитыИФНС.Наименование);
	ПараметрыПечати.Вставить("КодНО", РеквизитыИФНС.Код);
	
	ПараметрыПечати.Вставить("УведомлениеНомер", СтруктураПараметров._Номер);
	ПараметрыПечати.Вставить("Дата", Формат(Объект.ДатаПодписи, "ДЛФ=DD"));
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		НаимОргПолн = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, "НаимЮЛПол").НаимЮЛПол;
	Иначе
		НаимОргПолн = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, "ФИО").ФИО;
	КонецЕсли;
	ПараметрыПечати.Вставить("НаимОргПолн", НаимОргПолн);
	ПараметрыПечати.Вставить("ДатаЗапроса", Формат(СтруктураПараметров._ДатаТреб, "ДЛФ=DD"));
	ПараметрыПечати.Вставить("НомерЗапроса", СтруктураПараметров._НомТреб);
	
	ДокументыСтр = "";
	Информация = "";
	Приложения = "";
	
	Для Каждого Стр Из СтруктураПараметров._ПорНомДок Цикл 
		Если Стр.Информация Тогда 
			Информация = Информация + Стр.НомДок+ "; ";
		Иначе
			ДокументыСтр = ДокументыСтр + Стр.НомДок+ "; ";
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из СтруктураПараметров._ПриложенныеФайлы.Строки Цикл 
		Приложения = Приложения + Стр.Документ+ "; ";
	КонецЦикла;
	
	ПараметрыПечати.Вставить("Документы", ДокументыСтр);
	ПараметрыПечати.Вставить("Информация", Информация);
	ПараметрыПечати.Вставить("Приложения", Приложения);
	ПараметрыПечати.Вставить("Причина", СтруктураПараметров._ОписПричНепред);
	ПараметрыПечати.Вставить("ДатаПредст", Формат(СтруктураПараметров._СрокПредст, "ДЛФ=DD"));
	ПараметрыПечати.Вставить("КП1", Строка(СтруктураПараметров._КодПрич / 10));
	ПараметрыПечати.Вставить("КП2", "0");
	ПараметрыПечати.Вставить("ПрПодп", Строка(СтруктураПараметров._ПрПодп));
	
	ПараметрыПечати.Вставить("Должность", СтруктураПараметров._ДолжнПодп);
	ПараметрыПечати.Вставить("ФИО", СтруктураПараметров._ФамилияПодп + " " + СтруктураПараметров._ИмяПодп + " " + СтруктураПараметров._ОтчествоПодп);
	ПараметрыПечати.Вставить("Телефон", СтруктураПараметров._Тлф);
	ПараметрыПечати.Вставить("ЭлектроннаяПочта", СтруктураПараметров._Email);
	ПараметрыПечати.Вставить("ДокументПредставителя", СтруктураПараметров._НаимДокПодп);
	
	НомСтр = 0;
	МакетПФ = Отчеты.РегламентированноеУведомлениеНевозможностьПредоставления.ПолучитьМакет("Печать_MXL_Форма2015_1");
	Для Инд = 1 По 4 Цикл 
		Стр = МакетПФ.ПолучитьОбласть("Стр" + Инд);
		ЗаполнитьЗначенияСвойств(Стр.Параметры, ПараметрыПечати);
		Если Не ПечатнаяФорма.ПроверитьВывод(Стр) Тогда 
			ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
		КонецЕсли;
		ПечатнаяФорма.Вывести(Стр);
	КонецЦикла;
	ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	
	Возврат Листы;
КонецФункции

Функция СформироватьСписокЛистовФорма2019_1(Объект)
	ОТД = Новый ОписаниеТипов("Дата");
	Листы = Новый СписокЗначений;
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = Объект.Ссылка.ДанныеУведомления.Получить().СтруктураРеквизитов;
	
	ПараметрыПечати = Новый Структура;
	РеквизитыИФНС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.РегистрацияВИФНС, "Наименование, Код"); 
	ПараметрыПечати.Вставить("НаимНО", РеквизитыИФНС.Наименование + ", " + РеквизитыИФНС.Код);
	
	ПараметрыПечати.Вставить("УведомлениеНомер", СтруктураПараметров._Номер);
	ПараметрыПечати.Вставить("Дата", Формат(Объект.ДатаПодписи, "ДЛФ=DD"));
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		СведОрг = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, "НаимЮЛПол, ИННЮЛ, КППЮЛ");
		НаимОргПолн = СведОрг.НаимЮЛПол + " ИНН:" + СведОрг.ИННЮЛ + " КПП:" + СведОрг.КППЮЛ;
	Иначе
		СведОрг = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, "ФИО, ИННФЛ");
		НаимОргПолн = СведОрг.ФИО + " ИНН:" + СведОрг.ИННФЛ;
	КонецЕсли;
	ПараметрыПечати.Вставить("НаимОргПолн", НаимОргПолн);
	ПараметрыПечати.Вставить("ДатаЗапроса", Формат(СтруктураПараметров._ДатаТреб, "ДЛФ=DD"));
	ПараметрыПечати.Вставить("НомерЗапроса", СтруктураПараметров._НомТреб);
	ПараметрыПечати.Вставить("ПрПодп", Строка(СтруктураПараметров._ПрПодп));
	ПараметрыПечати.Вставить("Должность", СтруктураПараметров._ДолжнПодп);
	ПараметрыПечати.Вставить("ФИО", СтруктураПараметров._ФамилияПодп + " " + СтруктураПараметров._ИмяПодп + " " + СтруктураПараметров._ОтчествоПодп);
	ПараметрыПечати.Вставить("Телефон", СтруктураПараметров._Тлф);
	ПараметрыПечати.Вставить("ЭлектроннаяПочта", СтруктураПараметров._Email);
	ПараметрыПечати.Вставить("ДокументПредставителя", СтруктураПараметров._НаимДокПодп);
	
	Приложения = Новый Соответствие;
	НомСтр = 0;
	МакетПФ = Отчеты.РегламентированноеУведомлениеНевозможностьПредоставления.ПолучитьМакет("Печать_MXL_Форма2019_1");
	Стр = МакетПФ.ПолучитьОбласть("СтрНачальная");
	ЗаполнитьЗначенияСвойств(Стр.Параметры, ПараметрыПечати);
	ПечатнаяФорма.Вывести(Стр);
	МП = Новый Массив;
	Для Каждого СтрТек Из СтруктураПараметров._СвНевозмПред Цикл 
		КодПричины = СтрТек.КодПричины;
		Если КодПричины = "10" Тогда
			Стр2 = МакетПФ.ПолучитьОбласть("СтрПродление");
			Стр2.Параметры.Причина = ПолучитьЗначениеИзСписка(СтрТек, "_ОписПрич");
			Стр2.Параметры.ДатаПродления = Формат(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(СтрТек, "_СрокПредст")), "ДЛФ=DD; ДП=-");
		ИначеЕсли КодПричины = "21" Тогда
			Стр2 = МакетПФ.ПолучитьОбласть("СтрОтсутствие");
			Стр2.Параметры.ПричинаУтраты = ПолучитьЗначениеИзСписка(СтрТек, "_ОбстоятУтрат");
			Стр2.Параметры.Причина = "документы утрачены";
		ИначеЕсли КодПричины = "22" Тогда
			Стр2 = МакетПФ.ПолучитьОбласть("СтрОтсутствие");
			Стр2.Параметры.Причина = СтрТек.Описание;
		ИначеЕсли КодПричины = "23" Тогда
			Стр2 = МакетПФ.ПолучитьОбласть("СтрОтсутствие");
			ОписаниеАудит = "ОРНЗ: " + ПолучитьЗначениеИзСписка(СтрТек, "_ОРНЗ") + ", ";
			Если ПолучитьЗначениеИзСписка(СтрТек, "ЮрФиз") = "0" Тогда 
				ОписаниеАудит = ОписаниеАудит + ПолучитьЗначениеИзСписка(СтрТек, "_НаимОрг") + ", ИНН: "
				+ ПолучитьЗначениеИзСписка(СтрТек, "_ИННЮЛ") + ", КПП: " + ПолучитьЗначениеИзСписка(СтрТек, "_КПП");
			 Иначе
				ОписаниеАудит = ОписаниеАудит + ПолучитьЗначениеИзСписка(СтрТек, "_Фамилия") + " "
				+ ПолучитьЗначениеИзСписка(СтрТек, "_Имя") + " "
				+ ПолучитьЗначениеИзСписка(СтрТек, "_Отчество") + ", ИНН: "
				+ ПолучитьЗначениеИзСписка(СтрТек, "_ИННФЛ");
			КонецЕсли;
			Стр2.Параметры.ПереданоАудит = ОписаниеАудит;
			Стр2.Параметры.Причина = "документы переданы аудитору";
		ИначеЕсли КодПричины = "24" Тогда
			Стр2 = МакетПФ.ПолучитьОбласть("СтрОтсутствие");
			Стр2.Параметры.ПереданоГосОрг = ПолучитьЗначениеИзСписка(СтрТек, "_НаимПОДокПрд") + ", " + ПолучитьЗначениеИзСписка(СтрТек, "_АдрПОДокПрд");
			Стр2.Параметры.Причина = "документы переданы в правоохранительный орган";
		ИначеЕсли КодПричины = "30" Тогда
			Стр2 = МакетПФ.ПолучитьОбласть("СтрПредстРанее");
			Стр2.Параметры.Причина = "ИФНС " + ПолучитьЗначениеИзСписка(СтрТек, "_КодНОДокПред");
			Если ПолучитьЗначениеИзСписка(СтрТек, "ЭБФорма") = "0" Тогда 
				Стр2.Параметры.ДокНО = ПолучитьЗначениеИзСписка(СтрТек, "_ИмяФайлОтв") + " / " + ПолучитьЗначениеИзСписка(СтрТек, "_ИмяОпис");
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрПД Из СтрТек.ПриложенныеФайлы.Строки Цикл 
			Приложения.Вставить(СтрПД.Документ);
		КонецЦикла;
		
		Стр0 = МакетПФ.ПолучитьОбласть("СтрДок");
		Стр1 = МакетПФ.ПолучитьОбласть("СтрИнфо");
		
		ДокСтр = "";
		Для Каждого Док1 Из СтрТек.ДокИнф.Строки Цикл 
			Если Не ЗначениеЗаполнено(Док1.СведДокИнф) Тогда 
				Продолжить;
			КонецЕсли;
			ДокСтр = ДокСтр + Док1.СведДокИнф + ";";
			Если ЗначениеЗаполнено(Док1.ПорНомДок) Тогда 
				ДокСтр = ДокСтр + Док1.ПорНомДок + ";";
			КонецЕсли;
			Для Каждого Док2 Из Док1.Строки Цикл 
				Если ЗначениеЗаполнено(Док2.ПорНомДок) Тогда 
					ДокСтр = ДокСтр + Док2.ПорНомДок + ";";
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Стр0.Параметры.Документы = ДокСтр;
		
		МП.Очистить();
		МП.Добавить(Стр0);
		МП.Добавить(Стр1);
		МП.Добавить(Стр2);
		
		Если Не ПечатнаяФорма.ПроверитьВывод(МП) Тогда 
			ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
		КонецЕсли;
		ПечатнаяФорма.Вывести(Стр0);
		ПечатнаяФорма.Вывести(Стр1);
		ПечатнаяФорма.Вывести(Стр2);
	КонецЦикла;
	
	ПриложенияСтр = "";
	Для Каждого КЗ Из Приложения Цикл 
		ПриложенияСтр = ПриложенияСтр + КЗ.Ключ + "; ";
	КонецЦикла;
	
	Стр = МакетПФ.ПолучитьОбласть("СтрПосл");
	Стр.Параметры.Приложения = ПриложенияСтр;
	ЗаполнитьЗначенияСвойств(Стр.Параметры, ПараметрыПечати);
	Если Не ПечатнаяФорма.ПроверитьВывод(Стр) Тогда 
		ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	КонецЕсли;
	ПечатнаяФорма.Вывести(Стр);
	ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	
	Возврат Листы;
КонецФункции

Процедура ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр) Экспорт 
	НомСтр = НомСтр + 1;
	Лист = Новый Массив;
	Лист.Добавить(ПоместитьВоВременноеХранилище(ПечатнаяФорма));
	Лист.Добавить(Новый УникальныйИдентификатор);
	Лист.Добавить(Метаданные.Отчеты[Объект.ИмяОтчета].Синоним + " Лист." + НомСтр);
	Листы.Добавить(Лист, Метаданные.Отчеты[Объект.ИмяОтчета].Синоним + " Лист." + НомСтр);
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
КонецПроцедуры

Функция СформироватьМакет_Форма2015_1(Объект)
	ПечатнаяФорма = Новый ТабличныйДокумент;
	ПечатнаяФорма.Вывести(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьМакет("МакетПредупреждениеОНевозможностиПечати"));
	Возврат ПечатнаяФорма;
КонецФункции

Функция ПечатьСразу_Форма2015_1(Объект)
	
	ПечатнаяФорма = СформироватьМакет_Форма2015_1(Объект);
	
	ПечатнаяФорма.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ПечатнаяФорма.АвтоМасштаб = Истина;
	ПечатнаяФорма.ПолеСверху = 0;
	ПечатнаяФорма.ПолеСнизу = 0;
	ПечатнаяФорма.ПолеСлева = 0;
	ПечатнаяФорма.ПолеСправа = 0;
	ПечатнаяФорма.ОбластьПечати = ПечатнаяФорма.Область();
	
	Возврат ПечатнаяФорма;
	
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления_Форма2015_1(СведенияОтправки)
	Префикс = "ON_UVNPDUS";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2015_1(Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	
	Попытка
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._Номер) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен номер уведомления", "ОсновныеСведения", "_Номер"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИмяФайлТреб) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено имя файла входящего требования", "ОсновныеСведения", "_ИмяФайлТреб"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._НомТреб) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен номер входящего требования", "ОсновныеСведения", "_НомТреб"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ДатаТреб) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнена дата входящего требования", "ОсновныеСведения", "_ДатаТреб"));
		КонецЕсли;
		
		Если Данные.СтруктураРеквизитов._ОтпрЮЛ = 0 Тогда 
			Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._НаимОргОтпр) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен отправитель", "СведенияОбОтправителе", "_НаимОргОтпр"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИННЮЛОтпр) Или СтрДлина(Данные.СтруктураРеквизитов._ИННЮЛОтпр) <> 10 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Данные.СтруктураРеквизитов._ИННЮЛОтпр) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен/неправильно заполнен ИНН отправителя", "СведенияОбОтправителе", "_ИННЮЛОтпр"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._КППОтпр) Или СтрДлина(Данные.СтруктураРеквизитов._КППОтпр) <> 9 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Данные.СтруктураРеквизитов._КППОтпр) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен/неправильно заполнен КПП отправителя", "СведенияОбОтправителе", "_КППОтпр"));
			КонецЕсли;
		ИначеЕсли Данные.СтруктураРеквизитов._ОтпрЮЛ = 1 Тогда 
			Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИННФЛОтпр) Или СтрДлина(Данные.СтруктураРеквизитов._ИННФЛОтпр) <> 12 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Данные.СтруктураРеквизитов._ИННФЛОтпр) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен/неправильно заполнен ИНН отправителя", "СведенияОбОтправителе", "_ИННФЛОтпр"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ФамилияОтпр) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнена фамилия", "СведенияОбОтправителе", "_ФамилияОтпр"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИмяОтпр) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено имя отправителя", "СведенияОбОтправителе", "_ИмяОтпр"));
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ФамилияПодп) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнена фамилия подписанта", "Подписант", "_ФамилияПодп"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИмяПодп) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено имя подписанта", "Подписант", "_ИмяПодп"));
		КонецЕсли;
		Если (Данные.СтруктураРеквизитов._ПрПодп = 2 Или Данные.СтруктураРеквизитов._ПрПодп = 4) И Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._НаимДокПодп) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен документ представителя", "Подписант", "_НаимДокПодп"));
		КонецЕсли;
		
	Исключение
	КонецПопытки;
	
	Возврат ТаблицаОшибок;
КонецФункции

Функция СтрокаСоответствуетФорматуНомераДокумента(Стр)
	Если Не ЗначениеЗаполнено(Стр) Тогда 
		Возврат Истина;
	КонецЕсли;
	Если СтрДлина(Стр) <> 4 
		Или Сред(Стр, 2, 1) <> "."
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(Стр, 1))
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Прав(Стр, 2)) Тогда 
		
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2019_1(Данные, УникальныйИдентификатор)
	ОТД = Новый ОписаниеТипов("Дата");
	ТаблицаОшибок = Новый СписокЗначений;
	
	Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._Номер) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен номер уведомления", "ОсновныеСведения", "_Номер"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИмяФайлТреб) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено имя файла входящего требования", "ОсновныеСведения", "_ИмяФайлТреб"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._НомТреб) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен номер входящего требования", "ОсновныеСведения", "_НомТреб"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ДатаТреб) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнена дата входящего требования", "ОсновныеСведения", "_ДатаТреб"));
	КонецЕсли;
	
	ЕстьПричины = Ложь;
	Для Каждого Стр0 Из Данные.СтруктураРеквизитов._СвНевозмПред Цикл
		Если ЗначениеЗаполнено(Стр0.КодПричины) Тогда 
			ЕстьПричины = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если Не ЕстьПричины Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен список причин невозможности передать документы", "СведенияОДокументах", "_СвНевозмПред"));
	КонецЕсли;
	
	Для Каждого Стр0 Из Данные.СтруктураРеквизитов._СвНевозмПред Цикл
		КодПричины = Стр0.КодПричины;
		Если Не ЗначениеЗаполнено(КодПричины) Тогда 
			Продолжить;
		КонецЕсли;
		НомерСтроки = Данные.СтруктураРеквизитов._СвНевозмПред.Индекс(Стр0);
		
		Если Стр0.ДокИнф.Строки.Количество() = 0 Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнены номера документов требования", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
		КонецЕсли;
		Для Каждого Стр1 Из Стр0.ДокИнф.Строки Цикл 
			Если Не СтрокаСоответствуетФорматуНомераДокумента(Стр1.ПорНомДок) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Номер не соответствует формату", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
			Для Каждого Стр2 Из Стр1.Строки Цикл 
				Если Не СтрокаСоответствуетФорматуНомераДокумента(Стр2.ПорНомДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Номер не соответствует формату", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Если КодПричины = "10" Тогда
			Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ОписПрич")) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено описание причины", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_СрокПредст"))) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен срок представления", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
		ИначеЕсли КодПричины = "21" Тогда
			Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ОбстоятУтрат")) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнены обстоятельства утраты", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
		ИначеЕсли КодПричины = "23" Тогда
			Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ОРНЗ")) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Основной регистрационный номер записи из Реестра аудиторов и аудиторских организаций", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
			Если ПолучитьЗначениеИзСписка(Стр0, "ЮрФиз") = "0" Тогда 
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_НаимОрг")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Полное наименование организации", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ИННЮЛ")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: ИНН", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_КПП")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: КПП", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
			Иначе
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ИННФЛ")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: ИНН", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_Фамилия")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: фамилия", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_Имя")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: имя", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли КодПричины = "24" Тогда 
			Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_НаимПОДокПрд")) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено наименование правоохранительного органа", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_АдрПОДокПрд")) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен адрес правоохранительного органа", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
			КонецЕсли;
		ИначеЕсли КодПричины = "30" Тогда 
			Если ПолучитьЗначениеИзСписка(Стр0, "ЭБФорма") = "0" Тогда
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ИмяФайлОтв")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Имя файла документа, в ответ на который (приложением к которому; одновременно с которым) ранее были представлены указанные документы (информация)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ИмяОпис")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Имя файла документа, необходимого для обеспечения электронного документооборота в отношениях, регулируемых законодательством о налогах и сборах (КНД 1184002)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_ДатаПрмЭ"))) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Дата приема налоговым органом  представленного ранее документа (информации)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
			Иначе
				Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_СведДокО")) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Сведения о документе, в ответ на который (приложением к которому; одновременно с которым) ранее были представлены указанные документы (информация)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_ДатаПрмБ"))) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Дата приема налоговым органом  представленного ранее документа (информации)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
				КонецЕсли;
				
				_СведДокО = ПолучитьЗначениеИзСписка(Стр0, "_СведДокО");
				Если (_СведДокО = "01" Или _СведДокО = "02" Или _СведДокО = "99") Тогда 
					Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_НомДокОтв")) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Номер документа, в ответ на который (приложением к которому; одновременно с которым) ранее были представлены указанные документы (информация)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_ДатаДокОтв"))) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Дата документа, в ответ на который (приложением к которому; одновременно с которым) ранее были представлены указанные документы (информация)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
					КонецЕсли;
				ИначеЕсли _СведДокО = "03" Тогда
					Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_КНДПредДок")) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: КНД налоговой декларации (расчета), к которой ранее были представлены указанные документы (информация)", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_ОтчетГод")) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Отчетный год", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ПолучитьЗначениеИзСписка(Стр0, "_Период")) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен: Отчетный период", "СведенияОДокументах", "_СвНевозмПред", Стр0.УИД));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ФамилияПодп) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнена фамилия подписанта", "Подписант", "_ФамилияПодп"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИмяПодп) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено имя подписанта", "Подписант", "_ИмяПодп"));
	КонецЕсли;
	Если (Данные.СтруктураРеквизитов._ПрПодп = 2 Или Данные.СтруктураРеквизитов._ПрПодп = 4) И Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._НаимДокПодп) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен документ представителя", "Подписант", "_НаимДокПодп"));
	КонецЕсли;
	
	Если Данные.СтруктураРеквизитов._ОтпрЮЛ = 0 Тогда 
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._НаимОргОтпр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен отправитель", "СведенияОбОтправителе", "_НаимОргОтпр"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИННЮЛОтпр) Или СтрДлина(Данные.СтруктураРеквизитов._ИННЮЛОтпр) <> 10 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Данные.СтруктураРеквизитов._ИННЮЛОтпр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен/неправильно заполнен ИНН отправителя", "СведенияОбОтправителе", "_ИННЮЛОтпр"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._КППОтпр) Или СтрДлина(Данные.СтруктураРеквизитов._КППОтпр) <> 9 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Данные.СтруктураРеквизитов._КППОтпр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен/неправильно заполнен КПП отправителя", "СведенияОбОтправителе", "_КППОтпр"));
		КонецЕсли;
	ИначеЕсли Данные.СтруктураРеквизитов._ОтпрЮЛ = 1 Тогда 
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИННФЛОтпр) Или СтрДлина(Данные.СтруктураРеквизитов._ИННФЛОтпр) <> 12 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Данные.СтруктураРеквизитов._ИННФЛОтпр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнен/неправильно заполнен ИНН отправителя", "СведенияОбОтправителе", "_ИННФЛОтпр"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ФамилияОтпр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнена фамилия", "СведенияОбОтправителе", "_ФамилияОтпр"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Данные.СтруктураРеквизитов._ИмяОтпр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам("Не заполнено имя отправителя", "СведенияОбОтправителе", "_ИмяОтпр"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаОшибок;
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2015_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Новый Структура;
	
	ДанныеОрг = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьСведенияОбОрганизации(Объект);
	ОсновныеСведения.Вставить("ЭтоПБОЮЛ", Не ДанныеОрг.ЭтоЮрЛицо);
	Если Не ДанныеОрг.ЭтоЮрЛицо Тогда
		ОсновныеСведения.Вставить("ИННФЛ", ДанныеОрг.ИНН);
		ОсновныеСведения.Вставить("Фамилия", ДанныеОрг.Фамилия);
		ОсновныеСведения.Вставить("Имя", ДанныеОрг.Имя);
		ОсновныеСведения.Вставить("Отчество", ДанныеОрг.Отчество);
	Иначе
		ОсновныеСведения.Вставить("НаимОрг", ДанныеОрг.НаименованиеПолное);
		ОсновныеСведения.Вставить("ИННЮЛ", ДанныеОрг.ИНН);
		ОсновныеСведения.Вставить("КПП", ДанныеОрг.КПП);
	КонецЕсли;
	
	Документы.УведомлениеОСпецрежимахНалогообложения.ЗаполнитьОбщиеДанные(Объект, ОсновныеСведения);
	Данные = Объект.ДанныеУведомления.Получить();
	СтруктураРеквизитов = Данные.СтруктураРеквизитов;
	Для Каждого КЗ Из СтруктураРеквизитов Цикл 
		Если ТипЗнч(КЗ.Значение) = Тип("Строка")
			Или ТипЗнч(КЗ.Значение) = Тип("Число") Тогда 
			ОсновныеСведения.Вставить(КЗ.Ключ, КЗ.Значение);
		ИначеЕсли ТипЗнч(КЗ.Значение) = Тип("Дата") Тогда
			ОсновныеСведения.Вставить(КЗ.Ключ, Формат(КЗ.Значение, "ДФ=dd.MM.yyyy"));
		КонецЕсли;
	КонецЦикла;
	
	ОсновныеСведения.Вставить("КодНО", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РегистрацияВИФНС, "Код"));
	ОсновныеСведения.Вставить("_Дата", Формат(Объект.ДатаПодписи, "ДФ=dd.MM.yyyy"));
	
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления_Форма2015_1(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	
	Возврат ОсновныеСведения;
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2019_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Новый Структура;
	
	ДанныеОрг = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьСведенияОбОрганизации(Объект);
	ОсновныеСведения.Вставить("ЭтоПБОЮЛ", Не ДанныеОрг.ЭтоЮрЛицо);
	Если Не ДанныеОрг.ЭтоЮрЛицо Тогда
		ОсновныеСведения.Вставить("ИННФЛ", ДанныеОрг.ИНН);
		ОсновныеСведения.Вставить("Фамилия", ДанныеОрг.Фамилия);
		ОсновныеСведения.Вставить("Имя", ДанныеОрг.Имя);
		ОсновныеСведения.Вставить("Отчество", ДанныеОрг.Отчество);
	Иначе
		ОсновныеСведения.Вставить("НаимОрг", ДанныеОрг.НаименованиеПолное);
		ОсновныеСведения.Вставить("ИННЮЛ", ДанныеОрг.ИНН);
		ОсновныеСведения.Вставить("КПП", ДанныеОрг.КПП);
		ОсновныеСведения.Вставить("ИННОрг", ДанныеОрг.ИНН);
		ОсновныеСведения.Вставить("КППОрг", ДанныеОрг.КПП);
	КонецЕсли;
	
	Документы.УведомлениеОСпецрежимахНалогообложения.ЗаполнитьОбщиеДанные(Объект, ОсновныеСведения);
	Данные = Объект.ДанныеУведомления.Получить();
	СтруктураРеквизитов = Данные.СтруктураРеквизитов;
	Для Каждого КЗ Из СтруктураРеквизитов Цикл 
		Если ТипЗнч(КЗ.Значение) = Тип("Строка")
			Или ТипЗнч(КЗ.Значение) = Тип("Число") Тогда 
			ОсновныеСведения.Вставить(КЗ.Ключ, КЗ.Значение);
		ИначеЕсли ТипЗнч(КЗ.Значение) = Тип("Дата") Тогда
			ОсновныеСведения.Вставить(КЗ.Ключ, Формат(КЗ.Значение, "ДФ=dd.MM.yyyy"));
		КонецЕсли;
	КонецЦикла;
	
	ОсновныеСведения.Вставить("КодНО", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РегистрацияВИФНС, "Код"));
	ОсновныеСведения.Вставить("_Дата", Формат(Объект.ДатаПодписи, "ДФ=dd.MM.yyyy"));
	
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления_Форма2015_1(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	
	Возврат ОсновныеСведения;
КонецФункции

Функция ЗаполнитьДанными_Форма2015_1(Объект, СтруктураВыгрузки, СписокФайлов, ИменаФайлов, ОсновныеСведения)
	Данные = Объект.ДанныеУведомления.Получить();
	КолФайл = 0;
	
	Для Каждого Стр1 Из Данные.СтруктураРеквизитов._ПриложенныеФайлы.Строки Цикл 
		УИД1 = Строка(Новый УникальныйИдентификатор);
		Для Каждого Стр2 Из Стр1.Строки Цикл
			ПрисоединенныйФайл = Стр2.ПрисоединенныйФайл;
			Если ЗначениеЗаполнено(ПрисоединенныйФайл) И ПрисоединенныйФайл.ПолучитьОбъект() <> Неопределено Тогда
				КолФайл = КолФайл + 1;
				УИД2 = Строка(Новый УникальныйИдентификатор);
				Расширение = Сред(Стр2.Документ, СтрНайти(Стр2.Документ, ".", НаправлениеПоиска.СКонца));
				ИмяФайлаПрод = "PR_" + ОсновныеСведения.ИдентификаторОтправителя + "_"  + ОсновныеСведения.КодНО 
								+ "_" + УИД1 + ОсновныеСведения.ДатаИмФайла + УИД2 + Расширение;
				ИменаФайлов.Добавить(ИмяФайлаПрод);
				СписокФайлов.Добавить(ПрисоединенныйФайл);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Узел_Документ = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(СтруктураВыгрузки, "Документ");
	Узел_СвНевозмПред = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Документ, "СвНевозмПред");
	Узел_ПорНомДок = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПред, "ПорНомДок");
	Для Каждого Стр Из Данные.СтруктураРеквизитов._ПорНомДок Цикл 
		Узел_Нов = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_ПорНомДок);
		Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_Нов, Стр.НомДок);
	КонецЦикла;
	РегламентированнаяОтчетность.УдалитьУзел(Узел_ПорНомДок);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		Узел_ИмяФайл = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПред, "ИмяФайл");
		Для Каждого Стр Из ИменаФайлов Цикл 
			Узел_Нов = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_ИмяФайл);
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_Нов, Стр);
		КонецЦикла;
		РегламентированнаяОтчетность.УдалитьУзел(Узел_ИмяФайл);
		
		Если КолФайл > 0 Тогда
			Узел_КолФайл = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Документ, "КолФайл");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_КолФайл, КолФайл);
		КонецЕсли;
	КонецЕсли;
КонецФункции

Функция ПолучитьЗначениеИзСписка(Строка, Имя)
	Элт = Строка.Данные.НайтиПоЗначению(Имя);
	Возврат ?(Элт = Неопределено, "", Элт.Представление);
КонецФункции

Функция ЗаполнитьДанными_Форма2019_1(Объект, СтруктураВыгрузки, СписокФайлов, ИменаФайлов, ОсновныеСведения)
	ОТД = Новый ОписаниеТипов("Дата");
	Данные = Объект.ДанныеУведомления.Получить();
	КолФайл = 0;
	
	ФайлыПоСтраницам = Новый Массив;
	Для Каждого Стр0 Из Данные.СтруктураРеквизитов._СвНевозмПред Цикл 
		МассивИмен = Новый Массив;
		ФайлыПоСтраницам.Вставить(Данные.СтруктураРеквизитов._СвНевозмПред.Индекс(Стр0), МассивИмен);
		Для Каждого Стр1 Из Стр0.ПриложенныеФайлы.Строки Цикл 
			УИД1 = Строка(Новый УникальныйИдентификатор);
			Для Каждого Стр2 Из Стр1.Строки Цикл
				ПрисоединенныйФайл = Стр2.ПрисоединенныйФайл;
				Если ЗначениеЗаполнено(ПрисоединенныйФайл) И ПрисоединенныйФайл.ПолучитьОбъект() <> Неопределено Тогда
					КолФайл = КолФайл + 1;
					УИД2 = Строка(Новый УникальныйИдентификатор);
					Расширение = Сред(Стр2.Документ, СтрНайти(Стр2.Документ, ".", НаправлениеПоиска.СКонца));
					ИмяФайлаПрод = "PR_" + ОсновныеСведения.ИдентификаторОтправителя + "_"  + ОсновныеСведения.КодНО 
									+ "_" + УИД1 + ОсновныеСведения.ДатаИмФайла + УИД2 + Расширение;
					ИменаФайлов.Добавить(ИмяФайлаПрод);
					СписокФайлов.Добавить(ПрисоединенныйФайл);
					МассивИмен.Добавить(ИмяФайлаПрод);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Узел_Документ = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(СтруктураВыгрузки, "Документ");
	Узел_СвНевозмПред = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Документ, "СвНевозмПред");
	Для Каждого Стр0 Из Данные.СтруктураРеквизитов._СвНевозмПред Цикл
		КодПричины = Стр0.КодПричины;
		Если Не ЗначениеЗаполнено(КодПричины) Тогда 
			Продолжить;
		КонецЕсли;
		
		Узел_СвНевозмПредТек = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_СвНевозмПред);
		Узел_КодПрич = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "КодПрич");
		Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_КодПрич, КодПричины);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
			Узел_ИмяФайл = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "ИмяФайл");
			Для Каждого ИФ Из ФайлыПоСтраницам[Данные.СтруктураРеквизитов._СвНевозмПред.Индекс(Стр0)] Цикл
				Узел_ИмяФайлТек = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_ИмяФайл);
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_ИмяФайлТек, ИФ);
			КонецЦикла;
			РегламентированнаяОтчетность.УдалитьУзел(Узел_ИмяФайл);
		КонецЕсли;
		
		Узел_ДокИнф = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "ДокИнф");
		Для Каждого Стр1 Из Стр0.ДокИнф.Строки Цикл 
			Если Не ЗначениеЗаполнено(Стр1.СведДокИнф) Тогда 
				Продолжить;
			КонецЕсли;
			Узел_ДокИнфТек = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_ДокИнф);
			Узел_СведДокИнф = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ДокИнфТек, "СведДокИнф");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_СведДокИнф, Стр1.СведДокИнф);
			
			Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ДокИнфТек, "ПорНомДок");
			Узел_2 = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_1);
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_2, Стр1.ПорНомДок);
			Для Каждого Стр2 Из Стр1.Строки Цикл
				Узел_2 = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_1);
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_2, Стр2.ПорНомДок);
			КонецЦикла;
		КонецЦикла;
		
		Если КодПричины = "10" Тогда
			Узел_ОписПрич = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "ОписПрич");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_ОписПрич, ПолучитьЗначениеИзСписка(Стр0, "_ОписПрич"));
			Узел_СрокПредст = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "СрокПредст");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_СрокПредст, Формат(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_СрокПредст")), "ДФ=dd.MM.yyyy; ДП=01.01.0001"));
		ИначеЕсли КодПричины = "21" Тогда
			Узел_ОбстоятУтрат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "ОбстоятУтрат");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_ОбстоятУтрат, ПолучитьЗначениеИзСписка(Стр0, "_ОбстоятУтрат"));
		ИначеЕсли КодПричины = "23" Тогда
			Узел_СведАудит = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "СведАудит");
			Узел_ОРНЗ = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СведАудит, "ОРНЗ");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_ОРНЗ, ПолучитьЗначениеИзСписка(Стр0, "_ОРНЗ"));
			Если ПолучитьЗначениеИзСписка(Стр0, "ЮрФиз") = "0" Тогда 
				Узел_Св = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СведАудит, "АудитОрг");
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "НаимОрг");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_НаимОрг"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ИННЮЛ");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_ИННЮЛ"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "КПП");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_КПП"));
			Иначе
				Узел_Св = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СведАудит, "АудитИнд");
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ИННФЛ");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_ИННФЛ"));
				Узел_ФИО = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ФИО");
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ФИО, "Фамилия");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_Фамилия"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ФИО, "Имя");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_Имя"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ФИО, "Отчество");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_Отчество"));
			КонецЕсли;
		ИначеЕсли КодПричины = "24" Тогда
			Узел_СведПравОрг = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "СведПравОрг");
			Узел_НаимПОДокПрд = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СведПравОрг, "НаимПОДокПрд");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_НаимПОДокПрд, ПолучитьЗначениеИзСписка(Стр0, "_НаимПОДокПрд"));
			Узел_АдрПОДокПрд = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СведПравОрг, "АдрПОДокПрд");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_АдрПОДокПрд, ПолучитьЗначениеИзСписка(Стр0, "_АдрПОДокПрд"));
		ИначеЕсли КодПричины = "30" Тогда 
			Узел_ДокПредстНО = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_СвНевозмПредТек, "ДокПредстНО");
			Узел_КодНОДокПред = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ДокПредстНО, "КодНОДокПред");
			Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_КодНОДокПред, ПолучитьЗначениеИзСписка(Стр0, "_КодНОДокПред"));
			Если ПолучитьЗначениеИзСписка(Стр0, "ЭБФорма") = "0" Тогда 
				Узел_Св = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ДокПредстНО, "ДокПредЭл");
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ИмяФайлОтв");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_ИмяФайлОтв"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ИмяОпис");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_ИмяОпис"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ДатаПрмЭ");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, Формат(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_ДатаПрмЭ")), "ДФ=dd.MM.yyyy; ДП=01.01.0001"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ИмяДокР");
				Для Каждого Стр Из Стр0._ИмяДокР Цикл 
					Если ЗначениеЗаполнено(Стр.ИмяДокР) Тогда
						Узел_2 = Документы.УведомлениеОСпецрежимахНалогообложения.НовыйУзелИзПрототипа(Узел_1);
						Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_2, Стр.ИмяДокР);
					КонецЕсли;
				КонецЦикла;
			Иначе
				Узел_Св = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_ДокПредстНО, "ДокПредБум");
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "СведДокО");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_СведДокО"));
				Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ДатаПрмБ");
				Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, Формат(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_ДатаПрмБ")), "ДФ=dd.MM.yyyy; ДП=01.01.0001"));
				_СведДокО = ПолучитьЗначениеИзСписка(Стр0, "_СведДокО");
				Если (_СведДокО = "01" Или _СведДокО = "02" Или _СведДокО = "99") Тогда 
					Узел_Св = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "РеквДокОтв");
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "НомДокОтв");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_НомДокОтв"));
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "НаимДокОтв");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_НаимДокОтв"));
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ТипДокОтв");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_ТипДокОтв"));
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ДатаДокОтв");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, Формат(ОТД.ПривестиЗначение(ПолучитьЗначениеИзСписка(Стр0, "_ДатаДокОтв")), "ДФ=dd.MM.yyyy; ДП=01.01.0001"));
				ИначеЕсли _СведДокО = "03" Тогда
					Узел_Св = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "НДДокПредР");
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "КНДПредДок");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_КНДПредДок"));
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "ОтчетГод");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_ОтчетГод"));
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "Период");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_Период"));
					Узел_1 = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПодчиненныйЭлемент(Узел_Св, "НомКорр");
					Документы.УведомлениеОСпецрежимахНалогообложения.ВывестиПоказательВXML(Узел_1, ПолучитьЗначениеИзСписка(Стр0, "_НомКорр"));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	РегламентированнаяОтчетность.УдалитьУзел(Узел_СвНевозмПред);
КонецФункции

Функция ЭлектронноеПредставление_Форма2015_1(Объект, УникальныйИдентификатор)
	СписокФайлов = Новый Массив;
	ИменаФайлов = Новый Массив;
	ПроизвольнаяСтрока = Новый ОписаниеТипов("Строка");
	ДвоичныеДанные = Новый ОписаниеТипов("ДвоичныеДанные");
	
	ДанныеУведомления = Объект.ДанныеУведомления.Получить();
	ДанныеУведомления.Вставить("Организация", Объект.Организация);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2015_1(ДанныеУведомления, УникальныйИдентификатор);
	Если Ошибки.Количество() > 0 Тогда 
		Если ДанныеУведомления.Свойство("РазрешитьВыгружатьСОшибками") И ДанныеУведомления.РазрешитьВыгружатьСОшибками = Ложь Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При проверке выгрузки обнаружены ошибки. Для просмотра списка ошибок перейдите в форму уведомления, меню ""Проверка"", пункт ""Проверить выгрузку""");
			ВызватьИсключение "";
		Иначе 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При проверке выгрузки обнаружены ошибки. Для просмотра списка ошибок перейдите в форму уведомления, меню ""Проверка"", пункт ""Проверить выгрузку""");
		КонецЕсли;
	КонецЕсли;
	
	СведенияЭлектронногоПредставления = Новый ТаблицаЗначений;
	СведенияЭлектронногоПредставления.Колонки.Добавить("ИмяФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("ТекстФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("КодировкаТекста", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("ДвоичныеДанныеФайла", ДвоичныеДанные);
	
	ДатаИмФайла = Формат(ТекущаяДатаСеанса(), "ДФ=_yyyyMMdd_");
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2015_1(Объект, УникальныйИдентификатор);
	Если ОсновныеСведения.ЭтоПБОЮЛ Тогда
		ИННФЛ = ?(ЗначениеЗаполнено(ОсновныеСведения.ИННФЛ), ОсновныеСведения.ИННФЛ, "000000000000");
		ИдентификаторОтправителя = СокрЛП(ИННФЛ);
	Иначе
		ИННЮЛ = ?(ЗначениеЗаполнено(ОсновныеСведения.ИННЮЛ), ОсновныеСведения.ИННЮЛ, "0000000000");
		КПП = ?(ЗначениеЗаполнено(ОсновныеСведения.КПП), ОсновныеСведения.КПП, "000000000");
		ИдентификаторОтправителя = ИННЮЛ + КПП;
	КонецЕсли;
	ОсновныеСведения.Вставить("ДатаИмФайла", ДатаИмФайла);
	ОсновныеСведения.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2015_1");
	Документы.УведомлениеОСпецрежимахНалогообложения.ОбработатьУсловныеЭлементы(ОсновныеСведения, СтруктураВыгрузки);
	Документы.УведомлениеОСпецрежимахНалогообложения.ЗаполнитьПараметры(ОсновныеСведения, СтруктураВыгрузки);
	ЗаполнитьДанными_Форма2015_1(Объект, СтруктураВыгрузки, СписокФайлов, ИменаФайлов, ОсновныеСведения);
	Документы.УведомлениеОСпецрежимахНалогообложения.ОтсечьНезаполненныеНеобязательныеУзлы(СтруктураВыгрузки);
	
	Текст = Документы.УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВXML(СтруктураВыгрузки, ОсновныеСведения);
	
	СтрокаСведенийЭлектронногоПредставления = СведенияЭлектронногоПредставления.Добавить();
	СтрокаСведенийЭлектронногоПредставления.ИмяФайла = ОсновныеСведения.ИдФайл + ".xml";
	СтрокаСведенийЭлектронногоПредставления.ТекстФайла = Текст;
	СтрокаСведенийЭлектронногоПредставления.КодировкаТекста = "windows-1251";
	
	Если СведенияЭлектронногоПредставления.Количество() = 0 Тогда
		СведенияЭлектронногоПредставления = Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
		Для Инд = 0 По СписокФайлов.ВГраница() Цикл 
			Файл = СписокФайлов[Инд];
			ИмяФайлаПрод = ИменаФайлов[Инд];
			СтрокаСведенийЭлектронногоПредставления = СведенияЭлектронногоПредставления.Добавить();
			СтрокаСведенийЭлектронногоПредставления.ИмяФайла = ИмяФайлаПрод;
			СтрокаСведенийЭлектронногоПредставления.ДвоичныеДанныеФайла = МодульРаботаСФайлами.ДвоичныеДанныеФайла(Файл);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Функция ЭлектронноеПредставление_Форма2019_1(Объект, УникальныйИдентификатор)
	СписокФайлов = Новый Массив;
	ИменаФайлов = Новый Массив;
	ПроизвольнаяСтрока = Новый ОписаниеТипов("Строка");
	ДвоичныеДанные = Новый ОписаниеТипов("ДвоичныеДанные");
	
	ДанныеУведомления = Объект.ДанныеУведомления.Получить();
	ДанныеУведомления.Вставить("Организация", Объект.Организация);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2019_1(ДанныеУведомления, УникальныйИдентификатор);
	Если Ошибки.Количество() > 0 Тогда 
		Если ДанныеУведомления.Свойство("РазрешитьВыгружатьСОшибками") И ДанныеУведомления.РазрешитьВыгружатьСОшибками = Ложь Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При проверке выгрузки обнаружены ошибки. Для просмотра списка ошибок перейдите в форму уведомления, меню ""Проверка"", пункт ""Проверить выгрузку""");
			ВызватьИсключение "";
		Иначе 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При проверке выгрузки обнаружены ошибки. Для просмотра списка ошибок перейдите в форму уведомления, меню ""Проверка"", пункт ""Проверить выгрузку""");
		КонецЕсли;
	КонецЕсли;
	
	СведенияЭлектронногоПредставления = Новый ТаблицаЗначений;
	СведенияЭлектронногоПредставления.Колонки.Добавить("ИмяФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("ТекстФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("КодировкаТекста", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("ДвоичныеДанныеФайла", ДвоичныеДанные);
	
	ДатаИмФайла = Формат(ТекущаяДатаСеанса(), "ДФ=_yyyyMMdd_");
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2019_1(Объект, УникальныйИдентификатор);
	Если ОсновныеСведения.ЭтоПБОЮЛ Тогда
		ИННФЛ = ?(ЗначениеЗаполнено(ОсновныеСведения.ИННФЛ), ОсновныеСведения.ИННФЛ, "000000000000");
		ИдентификаторОтправителя = СокрЛП(ИННФЛ);
	Иначе
		ИННЮЛ = ?(ЗначениеЗаполнено(ОсновныеСведения.ИННЮЛ), ОсновныеСведения.ИННЮЛ, "0000000000");
		КПП = ?(ЗначениеЗаполнено(ОсновныеСведения.КПП), ОсновныеСведения.КПП, "000000000");
		ИдентификаторОтправителя = ИННЮЛ + КПП;
	КонецЕсли;
	ОсновныеСведения.Вставить("ДатаИмФайла", ДатаИмФайла);
	ОсновныеСведения.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2019_1");
	Документы.УведомлениеОСпецрежимахНалогообложения.ОбработатьУсловныеЭлементы(ОсновныеСведения, СтруктураВыгрузки);
	Документы.УведомлениеОСпецрежимахНалогообложения.ЗаполнитьПараметры(ОсновныеСведения, СтруктураВыгрузки);
	ЗаполнитьДанными_Форма2019_1(Объект, СтруктураВыгрузки, СписокФайлов, ИменаФайлов, ОсновныеСведения);
	Документы.УведомлениеОСпецрежимахНалогообложения.ОтсечьНезаполненныеНеобязательныеУзлы(СтруктураВыгрузки);
	
	Текст = Документы.УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВXML(СтруктураВыгрузки, ОсновныеСведения);
	
	СтрокаСведенийЭлектронногоПредставления = СведенияЭлектронногоПредставления.Добавить();
	СтрокаСведенийЭлектронногоПредставления.ИмяФайла = ОсновныеСведения.ИдФайл + ".xml";
	СтрокаСведенийЭлектронногоПредставления.ТекстФайла = Текст;
	СтрокаСведенийЭлектронногоПредставления.КодировкаТекста = "windows-1251";
	
	Если СведенияЭлектронногоПредставления.Количество() = 0 Тогда
		СведенияЭлектронногоПредставления = Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
		Для Инд = 0 По СписокФайлов.ВГраница() Цикл 
			Файл = СписокФайлов[Инд];
			ИмяФайлаПрод = ИменаФайлов[Инд];
			СтрокаСведенийЭлектронногоПредставления = СведенияЭлектронногоПредставления.Добавить();
			СтрокаСведенийЭлектронногоПредставления.ИмяФайла = ИмяФайлаПрод;
			СтрокаСведенийЭлектронногоПредставления.ДвоичныеДанныеФайла = МодульРаботаСФайлами.ДвоичныеДанныеФайла(Файл);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Процедура ПеренестиФайлыИзРегистраВПрисоединенныеФайлы(Ссылка) Экспорт
	Если Метаданные.РегистрыСведений.Найти("УдалитьДополнительныеФайлыУведомлений") = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ДанныеУведомления").Получить();
	Если ДанныеОтчета.СтруктураРеквизитов._ПриложенныеФайлы.Колонки.Найти("ПрисоединенныйФайл") <> Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПрисоединенныеФайлыПодсистемаСуществует = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
		ПрисоединенныеФайлыПодсистемаСуществует = Истина;
	КонецЕсли;
	
	Попытка
		НачатьТранзакцию();
		ДокОбъект = Ссылка.ПолучитьОбъект();
		ПриложенныеФайлы = ДанныеОтчета.СтруктураРеквизитов._ПриложенныеФайлы;
		ПриложенныеФайлы.Колонки.Добавить("ПрисоединенныйФайл");
		
		Для Каждого Стр1 Из ПриложенныеФайлы.Строки Цикл 
			Для Каждого Стр2 Из Стр1.Строки Цикл 
				Если ПрисоединенныеФайлыПодсистемаСуществует Тогда 
					ЗаписьРегистра = РегистрыСведений["УдалитьДополнительныеФайлыУведомлений"].СоздатьМенеджерЗаписи();
					ЗаписьРегистра.Уведомление = Ссылка;
					ЗаписьРегистра.ВидДополнительногоФайла = Строка(Стр2.УИДДокумент) + "@" + Строка(Стр2.УИДФайл);
					ЗаписьРегистра.Прочитать();
					
					Если ЗаписьРегистра.Выбран() Тогда 
						ПараметрыФайла = Новый Структура;
						ПараметрыФайла.Вставить("ВладелецФайлов", Ссылка);
						ПараметрыФайла.Вставить("Автор", Неопределено);
						ПараметрыФайла.Вставить("ИмяБезРасширения", СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", ""));
						ПараметрыФайла.Вставить("РасширениеБезТочки", Неопределено);
						ПараметрыФайла.Вставить("ВремяИзменения", Неопределено);
						ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", Неопределено);
						НоваяСсылкаНаФайл = МодульРаботаСФайлами.ДобавитьФайл(ПараметрыФайла, 
																ПоместитьВоВременноеХранилище(ЗаписьРегистра.СодержимоеФайла.Получить()), ,
																"Файл создан автоматически из формы уведомления, редактирование запрещено.");
						Стр2.ПрисоединенныйФайл = НоваяСсылкаНаФайл;
						ЗаписьРегистра.Удалить();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		ДокОбъект.ДанныеУведомления = Новый ХранилищеЗначения(ДанныеОтчета);
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокОбъект);
		ЗафиксироватьТранзакцию();
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Регламентированная отчетность. Обработка обновления. Преобразование присоедниненных файлов уведомлений'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОтменитьТранзакцию();
	КонецПопытки;
КонецПроцедуры

Процедура СоздатьУведомленияОНевозможностиПредоставления(ВыборкаСтрока) Экспорт
	Попытка
		Если Не ОбщегоНазначения.ПодсистемаСуществует("РегламентированнаяОтчетность.СообщенияВКонтролирующиеОрганы.КонвертацияОтчетовПриПереходеС82") Тогда
			Возврат;
		КонецЕсли;
		
		НачатьТранзакцию();
		РегОтчет = ВыборкаСтрока.Ссылка.ПолучитьОбъект();
		Уведомление = Документы.УведомлениеОСпецрежимахНалогообложения.СоздатьДокумент();
		Уведомление.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.НевозможностьПредоставленияДокументов;
		Уведомление.Организация = ВыборкаСтрока.Организация;
		
		ДанныеОтчета = ВыборкаСтрока.ДанныеОтчета.Получить();
		Уведомление.ИмяОтчета = "РегламентированноеУведомлениеНевозможностьПредоставления";
		Уведомление.ИмяФормы = "Форма2015_1";
		Уведомление.ПодписантФамилия = ДанныеОтчета.СобранныеДанныеРазделов._ФамилияПодп;
		Уведомление.ПодписантИмя = ДанныеОтчета.СобранныеДанныеРазделов._ИмяПодп;
		Уведомление.ПодписантОтчество = ДанныеОтчета.СобранныеДанныеРазделов._ОтчествоПодп;
		Уведомление.ДатаПодписи = РегОтчет.Дата;
		Уведомление.Дата = ТекущаяДатаСеанса();
		Уведомление.РегистрацияВИФНС = ДанныеОтчета.Регистрация;
		Уведомление.Записать();
		
		СтруктураУведомления = Новый Структура;
		Для Каждого КЗ Из ДанныеОтчета.ПоказателиОтчета.ПолеТабличногоДокументаТитульный Цикл 
			Если ТипЗнч(КЗ.Значение) = Тип("Строка")
				Или ТипЗнч(КЗ.Значение) = Тип("Число")
				Или ТипЗнч(КЗ.Значение) = Тип("Дата") Тогда 
				СтруктураУведомления.Вставить(КЗ.Ключ, КЗ.Значение);
			КонецЕсли;
		КонецЦикла;
		Для Каждого КЗ Из ДанныеОтчета.СобранныеДанныеРазделов Цикл 
			Если ТипЗнч(КЗ.Значение) = Тип("Строка")
				Или ТипЗнч(КЗ.Значение) = Тип("Число")
				Или ТипЗнч(КЗ.Значение) = Тип("Дата") Тогда 
				СтруктураУведомления.Вставить(КЗ.Ключ, КЗ.Значение);
			КонецЕсли;
		КонецЦикла;
		СтруктураУведомления.Вставить("_ПорНомДок", ДанныеОтчета.СобранныеДанныеРазделов._ПорНомДок);
		НовоеДерево = Новый ДеревоЗначений;
		НовоеДерево.Колонки.Добавить("Документ");
		НовоеДерево.Колонки.Добавить("ИндексКартинки");
		НовоеДерево.Колонки.Добавить("ПрисоединенныйФайл");
		НовоеДерево.Колонки.Добавить("УИДДокумент");
		НовоеДерево.Колонки.Добавить("УИДФайл");
		
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
			МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
		КонецЕсли;
		
		Для Каждого Стр1 Из ДанныеОтчета.СобранныеДанныеРазделов._ПриложенныеФайлы.Строки Цикл 
			НовСтр = НовоеДерево.Строки.Добавить();
			НовСтр.ИндексКартинки = 0;
			НовСтр.Документ = Стр1.Документ;
			НовСтр.УИДДокумент = Новый УникальныйИдентификатор(Стр1.УИДДокумент);
			Для Каждого Стр2 Из Стр1.Строки Цикл
				НовСтрПодч = НовСтр.Строки.Добавить();
				НовСтрПодч.ИндексКартинки = 2;
				НовСтрПодч.Документ = Стр2.Документ;
				НовСтрПодч.УИДДокумент = Новый УникальныйИдентификатор(Стр2.УИДДокумент);
				НовСтрПодч.УИДФайл = Новый УникальныйИдентификатор(Стр2.УИДФайл);
				Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
					ЗаписьРегистраСведений = РегистрыСведений.ДополнительныеФайлыРегламентированныхОтчетов.СоздатьМенеджерЗаписи();
					ЗаписьРегистраСведений.РегламентированныйОтчет = ВыборкаСтрока.Ссылка;
					ЗаписьРегистраСведений.ВидДополнительногоФайла = Стр2.УИДДокумент + "@" + Стр2.УИДФайл;
					ЗаписьРегистраСведений.Прочитать();
					Если ЗаписьРегистраСведений.Выбран() Тогда
						ПараметрыФайла = Новый Структура;
						ПараметрыФайла.Вставить("ВладелецФайлов", Уведомление.Ссылка);
						ПараметрыФайла.Вставить("Автор", Неопределено);
						ПараметрыФайла.Вставить("ИмяБезРасширения", СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", ""));
						ПараметрыФайла.Вставить("РасширениеБезТочки", Неопределено);
						ПараметрыФайла.Вставить("ВремяИзменения", Неопределено);
						ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", Неопределено);
						НоваяСсылкаНаФайл = МодульРаботаСФайлами.ДобавитьФайл(ПараметрыФайла, 
																ПоместитьВоВременноеХранилище(ЗаписьРегистраСведений.СодержимоеФайла.Получить()), ,
																"Файл создан автоматически из формы уведомления, редактирование запрещено.");
						НовСтрПодч.ПрисоединенныйФайл = НоваяСсылкаНаФайл;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		СтруктураУведомления.Вставить("_ПриложенныеФайлы", НовоеДерево);
		СтруктураПараметров = Новый Структура("СтруктураРеквизитов", СтруктураУведомления);
		Уведомление.ДанныеУведомления = Новый ХранилищеЗначения(СтруктураПараметров);
		
		РегОтчет.Комментарий = "##УведомлениеОСпецрежимахНалогообложения##" + ВыборкаСтрока.Комментарий;
		РегОтчет.ПометкаУдаления = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(РегОтчет);
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Уведомление);
		
		ЗаписьСоответствия = РегистрыСведений["СоответствиеРегОтчетовУведомлениям"].СоздатьМенеджерЗаписи();
		ЗаписьСоответствия.РегОтчет = РегОтчет;
		ЗаписьСоответствия.Прочитать();
		ЗаписьСоответствия.РегОтчет = РегОтчет;
		ЗаписьСоответствия.Уведомление = Уведомление;
		ЗаписьСоответствия.Записать(Истина);

		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда 
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Регламентированная отчетность. Не удалось преобразовать отчет'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,ВыборкаСтрока.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
КонецПроцедуры
#КонецОбласти
#КонецЕсли