// Область "Настройки" описывает параметры инициализации механизма загрузки
// регламентированных отчетов из файлов выгрузки. Формат области настроек - JSON.
//
// Описание параметров:
//  * Установкой параметра "РазрешитьЗагрузку" в "true" или "false" регулируем видимость
//    кнопки "Загрузить" и возможность загрузки из файла в форме отчета.
//  * Параметр "ФорматыСхемВыгрузки" описывает соответствие имен макетов схем выгрузки
//    поддерживаемым форматам загрузки. Если у формы единственная схема выгрузки или
//    нужно всегда использовать схему по умолчанию - указываем значение "null".
//  * Параметр "ОбластиОбработчиков" содержит список имен используемых обработчиков
//    загружаемых данных. Имена соответствуют областям макета, в которых можно размещать
//    произвольные алгоритмы на языке "1С:Предприятие" для обработки доступных объектов.
//
// Примечание:
//  Сопоставление имен узлов из файла XML и имен показателей отчета выполняется по данным схемы выгрузки,
//  поэтому обработчики используются для получения значений показателей не описанных в схеме выгрузки.
//
#Область Настройки
{
	"РазрешитьЗагрузку"   : true,
	
	"ФорматыСхемВыгрузки" : null,
	"ОбластиОбработчиков" : {
		"ПередЗагрузкой"  : "ПредОбработкаСхемыЗагрузки",
		"ПослеЗагрузки"   : "ПостОбработкаДокументаОтчета",
	}
}
#КонецОбласти

// Доступны параметры:
//
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПредОбработкаСхемыЗагрузки
	
#КонецОбласти

// Доступны параметры:
//
//  П.ДанныеОтчета      - заполненные данные документа регламентированного отчета;
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПостОбработкаДокументаОтчета
	
	СоответствиеЗначенияВыгрузкиНомеруКвартала = Новый Соответствие;
	СоответствиеЗначенияВыгрузкиНомеруКвартала.Вставить("21", "01");
	СоответствиеЗначенияВыгрузкиНомеруКвартала.Вставить("22", "02");
	СоответствиеЗначенияВыгрузкиНомеруКвартала.Вставить("23", "03");
	СоответствиеЗначенияВыгрузкиНомеруКвартала.Вставить("24", "04");
	
	СоответствиеЗначенияВыгрузкиНалоговомуПериоду = Новый Соответствие;
	СоответствиеЗначенияВыгрузкиНалоговомуПериоду.Вставить("20", "3");
	СоответствиеЗначенияВыгрузкиНалоговомуПериоду.Вставить("31", "6");
	СоответствиеЗначенияВыгрузкиНалоговомуПериоду.Вставить("33", "9");
	СоответствиеЗначенияВыгрузкиНалоговомуПериоду.Вставить("34", "0");
	
	УзлыНалог = УзлыПоXPath(П.ДеревоДляЗагрузки, "/Документ/СодПерСвед/Налог"); // Титульный
	
	Если ЗначениеЗаполнено(УзлыНалог) Тогда
		Для НомерУзла = 1 По УзлыНалог.Количество() Цикл
			УзелНалог = УзлыНалог[НомерУзла - 1];
			УзелНалог.Обязательность = "О";
			
			Если НомерУзла > 4 Тогда
				Прервать;
			КонецЕсли;
			
			УзелНалПредДекНП = УзлыПоXPath(УзелНалог, "@НалПредДекНП")[0];
			УзелНомерГлНК    = УзлыПоXPath(УзелНалог, "@НомерГлНК")[0];
			УзелПериодОтч    = УзлыПоXPath(УзелНалог, "@ПериодОтч")[0];
			
			ЗначениеНомераКвартала = СокрЛП(СоответствиеЗначенияВыгрузкиНомеруКвартала[УзелПериодОтч.Значение]);
			Если УзелНалПредДекНП.Значение = "Налог на добавленную стоимость" Тогда
				ЗначениеНалоговогоПериода = "3";
			Иначе
				ЗначениеНалоговогоПериода = СокрЛП(СоответствиеЗначенияВыгрузкиНалоговомуПериоду[УзелПериодОтч.Значение]);
			КонецЕсли;
			
			УзелНалПредДекНП.Раздел = "Титульный";
			УзелНалПредДекНП.Ключ   = "НаимНалог"     + НомерУзла;
			УзелНомерГлНК.Раздел    = "Титульный";
			УзелНомерГлНК.Ключ      = "НомерГлавыНК"  + НомерУзла;
			УзелПериодОтч.Раздел    = "Титульный";
			УзелПериодОтч.Ключ      = "НомерКвартала" + НомерУзла;
			
			УзелПериодОтч.Значение = ЗначениеНомераКвартала;
			
			УзелПериодОтчДоп = УзелНалог.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(УзелПериодОтчДоп, УзелПериодОтч, , "Родитель, Строки, Значение");
			УзелПериодОтчДоп.МинРазмерность = 1;
			УзелПериодОтчДоп.Ключ = "НалоговыйПериод" + НомерУзла;
			УзелПериодОтчДоп.Значение = ЗначениеНалоговогоПериода;
			
			ЗаполнитьДаннымиСохраненныйДокументИзДереваДляЗагрузки(УзелНалог, П.ДанныеОтчета, П.ПараметрыОтчета);
		КонецЦикла;
	КонецЕсли;
	
#КонецОбласти