&НаКлиенте
Перем ПараметрыОбработчикаОжидания; // Хранит параметры обработчика ожидания.

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
// Процедура подготовки параметров учета.
Функция ПодготовитьПараметрыОтчета()
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ДатаВыдачи",                       Отчет.ДатаВыдачи);
	ПараметрыОтчета.Вставить("НачалоПериода",                    НачалоМесяца(Отчет.НачалоПериода));
	ПараметрыОтчета.Вставить("КонецПериода",                     КонецМесяца(Отчет.КонецПериода));
	ПараметрыОтчета.Вставить("Организация",                      Отчет.Организация);
	ПараметрыОтчета.Вставить("ЛицевойСчет",                      Отчет.ЛицевойСчет);
	ПараметрыОтчета.Вставить("ПараметрВыводаЗадолженности",      Отчет.ПараметрВыводаЗадолженности);
	ПараметрыОтчета.Вставить("ВыводитьСведенияПоМесяцам",        Отчет.ВыводитьСведенияПоМесяцам);
	ПараметрыОтчета.Вставить("ТочностьОбъемаНачислений",         Константы.УПЖКХ_ТочностьОбъемаНачислений.Получить());
	ПараметрыОтчета.Вставить("ДетализироватьСведенияПоПеням",    Отчет.ДетализироватьСведенияПоПеням);	
	ПараметрыОтчета.Вставить("ТаблицаПериодов",                  ПолучитьТаблицуПериодов(Отчет.НачалоПериода, Отчет.КонецПериода));
	ПараметрыОтчета.Вставить("ВариантОтбораУслуг",               Отчет.ВариантОтбораУслуг);
	ПараметрыОтчета.Вставить("СписокУслуг",                      СформироватьСписокУслугДляЗапроса());
	ПараметрыОтчета.Вставить("НастройкиУчетнойПолитикиТСЖОтказ", Ложь);
	ПараметрыОтчета.Вставить("НастройкиУчетнойПолитикиТСЖ",      УПЖКХ_ОбщегоНазначенияСервер.ПолучитьПараметрыУчетнойПолитикиЖКХ(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.Организация, ПараметрыОтчета.НастройкиУчетнойПолитикиТСЖОтказ));
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Функция - Получить таблицу периодов
//
// Параметры:
//  НачалоПериода	 - Дата	 - начальная дата отчета.
//  КонецПериода	 - Дата	 - конечная дата отчета.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица месяцев.
//
Функция ПолучитьТаблицуПериодов(НачалоПериода, КонецПериода)
	
	ТаблицаПериодов = Новый ТаблицаЗначений;
	ТаблицаПериодов.Колонки.Добавить("НачалоПериода", Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ТаблицаПериодов.Колонки.Добавить("КонецПериода",  Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	
	НоваяСтрока = ТаблицаПериодов.Добавить();
	НоваяСтрока.НачалоПериода = НачалоМесяца(НачалоПериода);
	НоваяСтрока.КонецПериода  = КонецМесяца(НачалоПериода);
	
	Если НЕ (Месяц(НачалоПериода) = Месяц(КонецПериода) И Год(НачалоПериода) = Год(КонецПериода)) Тогда
		
		ТекущийМесяц = НачалоПериода;
		
		Пока НачалоМесяца(ТекущийМесяц) < НачалоМесяца(КонецПериода) Цикл
			ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
			
			НоваяСтрока = ТаблицаПериодов.Добавить();
			НоваяСтрока.НачалоПериода = НачалоМесяца(ТекущийМесяц);
			НоваяСтрока.КонецПериода  = КонецМесяца(ТекущийМесяц);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаПериодов;
	
КонецФункции

&НаСервере
// Процедура формирует отчет на сервере.
Функция СформироватьОтчетНаСервере() Экспорт
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено", Истина);
	КонецЕсли;
	
	ИБФайловая = УПЖКХ_ТиповыеМетодыСервер.ИнформационнаяБазаФайловая();
	
	УПЖКХ_ТиповыеМетодыСервер.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ИдентификаторЗадания = Неопределено;
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета();
	
	Если ИБФайловая Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Отчеты.КВП_ВыпискаПоЛицевомуСчету.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
		РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		РезультатВыполнения = УПЖКХ_ТиповыеМетодыСервер.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор, 
			"Отчеты.КВП_ВыпискаПоЛицевомуСчету.СформироватьОтчет", 
			ПараметрыОтчета, 
			УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтаФорма));
		
		ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
		АдресХранилища       = РезультатВыполнения.АдресХранилища;
	КонецЕсли;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанные();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
// Процедура загрузки подготовленных данных.
Процедура ЗагрузитьПодготовленныеДанные()
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Процедуры проверяет выполнение задания.
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
			ЗагрузитьПодготовленныеДанные();
			УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		Иначе
			УПЖКХ_ТиповыеМетодыКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
// Процедура проверяет, выполнено ли задание.
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат УПЖКХ_ТиповыеМетодыСервер.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Управляет видимостью элементов формы.
Процедура УправлениеФормой(Форма)
	
	Отчет    = Форма.Отчет;
	Элементы = Форма.Элементы;
		
	Форма.Период = УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Форма.ВидПериода, Отчет.НачалоПериода, Отчет.КонецПериода);
		
	УПЖКХ_ТиповыеМетодыКлиентСервер.ПереключитьТекущуюСтраницуВыбораПериода(Форма.ВидПериода,
																			Элементы.ГруппаПоляВводаПериода);
	
	Элементы.СравнитьМакетСТиповым.Видимость = УПЖКХ_ОбщегоНазначенияКлиентСервер.ОпределитьИспользованиеПользовательскогоМакетаПечати("Отчет.КВП_ВыпискаПоЛицевомуСчету", "ПФ_MXL_Макет");
	
	// Если вариант отбора услуг не заполнен, то по умолчанию устанавливаем "формировать отчет по всем услугам".
	Если ПустаяСтрока(Отчет.ВариантОтбораУслуг) Тогда
		Отчет.ВариантОтбораУслуг = "формировать отчет по всем услугам";
	КонецЕсли;
	
	Элементы.ГруппаПанельНастроек.Видимость = Отчет.ВариантОтбораУслуг = "формировать отчет по указанным услугам" ИЛИ Отчет.ВариантОтбораУслуг = "формировать отчет, кроме указанных услуг";
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
// Процедура обновляет текст заголовка.
Процедура ОбновитьТекстЗаголовка(Форма)
	
	Отчет = Форма.Отчет;
	ЗаголовокОтчета = "Выписка по лицевому счету за "
					+ УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.НачалоПериода, Отчет.КонецПериода, Истина);
	
	Форма.Заголовок = ЗаголовокОтчета;
	
КонецПроцедуры

&НаСервере
// Устанавливает начальные настройки.
Процедура УстановитьНачальныеНастройки()
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
КонецПроцедуры

// Процедура получает сведения о настройках кап. ремонта по текущей организации
// и управляет текстом гиперссылки открытия настроек кап. ремонта.
&НаСервере
Функция ОбновитьРеквизитыКапРемонта()
	
	ЗаполнитьРеквизитыОтчетаПоКапремонту();
	
	мМассивВариантовОтбораУслуг = Новый Массив;
	мМассивВариантовОтбораУслуг.Добавить("формировать отчет по всем услугам");
	мМассивВариантовОтбораУслуг.Добавить("формировать отчет по указанным услугам");
	мМассивВариантовОтбораУслуг.Добавить("формировать отчет, кроме указанных услуг");
	
	Если ТипЗнч(мСтруктураНастроекКапРемонта) = Тип("Структура")
	   И мСтруктураНастроекКапРемонта.НастройкиОпределены
	   И мСтруктураНастроекКапРемонта.ВедетсяРаздельныйУчет Тогда
		
		мМассивВариантовОтбораУслуг.Добавить("формировать отчет по услугам, отличным от кап. ремонта");
		мМассивВариантовОтбораУслуг.Добавить("формировать отчет по услугам кап. ремонта");
		
	Иначе
		
		Если Отчет.ВариантОтбораУслуг = "формировать отчет по услугам, отличным от кап. ремонта"
			ИЛИ Отчет.ВариантОтбораУслуг = "формировать отчет по услугам кап. ремонта" Тогда
			Отчет.ВариантОтбораУслуг = "формировать отчет по всем услугам";
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ВариантОтбораУслуг.СписокВыбора.ЗагрузитьЗначения(мМассивВариантовОтбораУслуг);
	
КонецФункции // ОбновитьРеквизитыКапРемонта()

// Процедура предназначена для заполнения сведения о ведение раздельного учета по капремонту в организации.
//
&НаСервере
Процедура ЗаполнитьРеквизитыОтчетаПоКапремонту()
	
	Попытка
		мСтруктураНастроекКапРемонта = УПЖКХ_ПараметрыУчетаСервер.ПолучитьНастройкиУчетаУслугКапремонта(Отчет.Организация, Истина);
	Исключение
		ВызватьИсключение УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьОписаниеОшибки(УПЖКХ_ОбщегоНазначенияКлиентСервер.ПодготовитьТекстОбОшибкеДляПользователя("<error>Не удалось получить настройки учета капитального ремонта. Пожалуйста, проверьте, корректно ли заданы настройки учета капитального ремонта.", ОписаниеОшибки()));
		Возврат;
	КонецПопытки;

КонецПроцедуры // ЗаполнитьРеквизитыОтчетаПоКапремонту()

// Предназначена для формирования списка услуг по которым будут получены данные о начислениях.
&НаСервере
Функция СформироватьСписокУслугДляЗапроса()
	
	СписокУслуг = Новый СписокЗначений;
	
	Если Отчет.ВариантОтбораУслуг = "формировать отчет по всем услугам" Тогда
		// Оставляем пустой список.
	ИначеЕсли Отчет.ВариантОтбораУслуг = "формировать отчет по указанным услугам"
		ИЛИ Отчет.ВариантОтбораУслуг = "формировать отчет, кроме указанных услуг" Тогда
		
		Для каждого ТекСтрока Из Отчет.ВыбранныеУслуги Цикл
			Если НЕ ТекСтрока.Услуга = ПредопределенноеЗначение("Справочник.КВП_Услуги.ПустаяСсылка") Тогда
				СписокУслуг.Добавить(ТекСтрока.Услуга);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Отчет.ВариантОтбораУслуг = "формировать отчет по услугам, отличным от кап. ремонта"
		ИЛИ Отчет.ВариантОтбораУслуг = "формировать отчет по услугам кап. ремонта" Тогда
		
		СписокУслуг = мСтруктураНастроекКапРемонта.СписокУслуг;
		
	КонецЕсли;
	
	Возврат СписокУслуг;
	
КонецФункции // СформироватьСписокУслугДляЗапроса()

#КонецОбласти 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Прочитаем переданные параметры.
	Если Параметры.Свойство("ЛицевойСчет") Тогда
		Параметры.Свойство("ЛицевойСчет", Отчет.ЛицевойСчет);
		ИспользоватьРучныеНастройки = Истина;
	КонецЕсли;
	
	Если Отчет.Организация.Пустая() Тогда
		Отчет.Организация = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.ЗаполнитьСписокВыбораВидПериода(
			Перечисления.ДоступныеПериодыОтчета.День,
			Элементы.ВидПериода.СписокВыбора, ВидПериода);
	
	УстановитьНачальныеНастройки();
	
	ОбновитьРеквизитыКапРемонта();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриОткрытии" формы.
Процедура ПриОткрытии(Отказ)
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтаФорма, Отказ);
	
	Если Не ЗначениеЗаполнено(Отчет.НачалоПериода) Тогда
		Отчет.НачалоПериода = НачалоДня(УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьРабочуюДату());
		Отчет.КонецПериода = КонецДня(Отчет.НачалоПериода);
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриЗакрытии" формы.
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	УПЖКХ_ТиповыеМетодыКлиент.ПриЗакрытии(ЭтаФорма, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
// Процедура возникает при сохранении пользовательских настроек на сервере.
//
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	УПЖКХ_ТиповыеМетодыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтаФорма, Настройки);
	
КонецПроцедуры // ПриСохраненииПользовательскихНастроекНаСервере()

&НаСервере
// Процедура возникает при загрузке пользовательских настроек на сервере.
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	Если Не ИспользоватьРучныеНастройки Тогда
		
		УПЖКХ_ТиповыеМетодыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтаФорма, Настройки);
		
		ОбновитьТекстЗаголовка(ЭтаФорма);
		
		УправлениеФормой(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Процедура возникает при загрузке вариант отчета на сервере.
//
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	УПЖКХ_ТиповыеМетодыВызовСервера.УстановитьНастройкиПоУмолчанию(ЭтаФорма);
	
КонецПроцедуры

///////////////////////
// Поле периода

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "ВидПериода".
Процедура ВидПериодаПриИзменении(Элемент)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВидПериодаПриИзменении(Элемент, ВидПериода, Отчет.НачалоПериода, Отчет.КонецПериода, Период);
	
	ПараметрыСтраниц = Новый Структура;
	ПараметрыСтраниц.Вставить("ПроизвольныйПериод", "ГруппаПроизвольныйПериод");
	ПараметрыСтраниц.Вставить("ПериодПоВидам"     , "ГруппаПериодПоВидам");
	ПараметрыСтраниц.Вставить("День"              , "ГруппаДень");
	УПЖКХ_ТиповыеМетодыКлиентСервер.ПереключитьТекущуюСтраницуВыбораПериода(ВидПериода, Элементы.ГруппаПоляВводаПериода,
																			ПараметрыСтраниц);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "Период".
Процедура ПериодПриИзменении(Элемент)
	
	УПЖКХ_ТиповыеМетодыКлиент.ПериодПриИзменении(Элемент, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля "Период".
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодНачалоВыбораЗавершение", ЭтотОбъект);
	
	УПЖКХ_ТиповыеМетодыКлиент.ПериодНачалоВыбора(ЭтаФорма, Элемент, СтандартнаяОбработка, 
		ВидПериода, Отчет.НачалоПериода, ОписаниеОповещения);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура окончания асинхроного выбора периода.
Процедура ПериодНачалоВыбораЗавершение(СтруктураПериода, ДополнительныеПараметры) Экспорт
	
	// Установим полученный период
	Если СтруктураПериода <> Неопределено Тогда
		ВидПериода = СтруктураПериода.ВидПериода;
		Период = СтруктураПериода.Период;
		Отчет.НачалоПериода = СтруктураПериода.НачалоПериода;
		Отчет.КонецПериода = СтруктураПериода.КонецПериода;
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ОбработкаВыбора" поля "Период".
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ПериодОбработкаВыбора(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка,
		ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "АвтоПодбор" поля "Период".
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ПериодАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка,
		ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ОкончаниеВводаТекста" поля "Период".
Процедура ПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ПериодОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка,
		ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "НачалоПериодаДень".
Процедура НачалоПериодаДеньПриИзменении(Элемент)
	
	Отчет.КонецПериода = КонецДня(Отчет.НачалоПериода);
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "НачалоПериода".
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "КонецПериода".
Процедура КонецПериодаПриИзменении(Элемент)
	
	ОбновитьТекстЗаголовка(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтбораУслугПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры // ВариантОтбораУслугПриИзменении()

///////////////////////
// Прочие элементы

&НаКлиенте
// Обработчик события "ПриИзменении" поля "Организация".
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	ОбновитьРеквизитыКапРемонта();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" поля "ЛицевойСчет".
Процедура ЛицевойСчетПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" поля "ПараметрВыводаЗадолженности".
Процедура ПараметрВыводаЗадолженностиПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" поля "ДатаВыдачи".
Процедура ДатаВыдачиПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

#Область ПроцедурыДействияКомандныхПанелейФормы

&НаКлиенте
// Обработчик команды "СформироватьОтчет".
Процедура СформироватьОтчет(Команда)
	
	ОчиститьСообщения();
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	Если Не РезультатВыполнения.ЗаданиеВыполнено Тогда
		УПЖКХ_ТиповыеМетодыКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Обработчик команды "ОткрытьНастройки".
Процедура ОткрытьНастройки(Команда)
	
	ФормаНастроек = ПолучитьФорму("Отчет.КВП_ВыпискаПоЛицевомуСчету.ФормаНастроек",,,Истина);
	ФормаНастроек.Открыть();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик команды "СравнитьМакетСТиповым".
Процедура СравнитьМакетСТиповым(Команда)
	
	УПЖКХ_РаботаСДиалогамиКлиент.СравнитьТиповойМакетСПользовательским("Отчет.КВП_ВыпискаПоЛицевомуСчету", "ПФ_MXL_Макет");
	
КонецПроцедуры

#КонецОбласти 
