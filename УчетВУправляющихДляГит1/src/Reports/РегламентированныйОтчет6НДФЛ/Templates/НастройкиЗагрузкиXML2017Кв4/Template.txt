// Область "Настройки" описывает параметры инициализации механизма загрузки
// регламентированных отчетов из файлов выгрузки. Формат области настроек - JSON.
//
// Описание параметров:
//  * Установкой параметра "РазрешитьЗагрузку" в "true" или "false" регулируем видимость
//    кнопки "Загрузить" и возможность загрузки из файла в форме отчета.
//  * Параметр "ФорматыСхемВыгрузки" описывает соответствие имен макетов схем выгрузки
//    поддерживаемым форматам загрузки. Если у формы единственная схема выгрузки или
//    нужно всегда использовать схему по умолчанию - указываем значение "null".
//  * Параметр "ОбластиОбработчиков" содержит список имен используемых обработчиков
//    загружаемых данных. Имена соответствуют областям макета, в которых можно размещать
//    произвольные алгоритмы на языке "1С:Предприятие" для обработки доступных объектов.
//
// Примечание:
//  Сопоставление имен узлов из файла XML и имен показателей отчета выполняется по данным схемы выгрузки,
//  поэтому обработчики используются для получения значений показателей не описанных в схеме выгрузки.
//
#Область Настройки
{
	"РазрешитьЗагрузку"   : true,
	
	"ФорматыСхемВыгрузки" : null,
	"ОбластиОбработчиков" : {
		"ПередЗагрузкой"  : "ПредОбработкаСхемыЗагрузки",
		"ПослеЗагрузки"   : "ПостОбработкаДокументаОтчета",
	}
}
#КонецОбласти

// Доступны параметры:
//
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПредОбработкаСхемыЗагрузки
	
#КонецОбласти

// Доступны параметры:
//
//  П.ДанныеОтчета      - заполненные данные документа регламентированного отчета;
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПостОбработкаДокументаОтчета
	
	Титульный = Неопределено;
	
	П.ДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаТитульный", Титульный);
	Если Титульный <> Неопределено Тогда
		
		Титульный.Период         = П.ПараметрыОтчета.ПериодОтч;
		Титульный.ОтчетГод       = П.ПараметрыОтчета.ГодПериодОтч;
		Титульный.НалоговыйОрган = П.ПараметрыОтчета.КодИФНС;
		Титульный.ПоМесту        = П.ПараметрыОтчета.ПоМесту;
		
		Если ЗначениеЗаполнено(П.ПараметрыОтчета.ФормРеорг) Тогда
			Титульный.КодФормыРеорганизации          = П.ПараметрыОтчета.ФормРеорг;
			Титульный.ИННРеорганизованнойОрганизации = П.ПараметрыОтчета.ИННРеорг;
			Титульный.КППРеорганизованнойОрганизации = П.ПараметрыОтчета.КППРеорг;
		КонецЕсли;
		
	КонецЕсли;
	
	Раздел2 = Неопределено;
	
	П.ДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел2", Раздел2);
	Если Раздел2 <> Неопределено Тогда
		
		ИтогоСтр130 = 0;
		ИтогоСтр140 = 0;
		
		Ном = 1;
		Постфикс = "_1";
		
		ЗначСтр130 = Раздел2["П000020013001" + Постфикс];
		ЗначСтр140 = Раздел2["П000020014001" + Постфикс];
		
		ЕстьПокСтр130 = Истина;
		
		Пока ЕстьПокСтр130 Цикл
			
			ИтогоСтр130 = ИтогоСтр130 + ЧислоИзСтроки(ЗначСтр130);
			ИтогоСтр140 = ИтогоСтр140 + ЧислоИзСтроки(ЗначСтр140);
			
			Ном = Ном + 1;
			Постфикс = "_" + Формат(Ном, "ЧГ=");
			
			ЕстьПокСтр130 = Раздел2.Свойство("П000020013001" + Постфикс);
			
			Если ЕстьПокСтр130 Тогда
				
				Раздел2.Свойство("П000020013001" + Постфикс, ЗначСтр130);
				Раздел2.Свойство("П000020014001" + Постфикс, ЗначСтр140);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Раздел2.Вставить("П000020013002", ИтогоСтр130);
		Раздел2.Вставить("П000020014002", ИтогоСтр140);
		
	КонецЕсли;
	
#КонецОбласти