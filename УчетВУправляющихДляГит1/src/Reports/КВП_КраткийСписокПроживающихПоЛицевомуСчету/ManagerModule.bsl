
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура формирует отчет.
Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	Если ПараметрыОтчета.ЛицевойСчет.Пустая() Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Укажите лицевой счет.");
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = Отчеты.КВП_КраткийСписокПроживающихПоЛицевомуСчету.ПолучитьМакет("Макет");
	
	ТабличныйДокумент.Очистить();
	
	// 1. Сбор данных.
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ЛицевойСчет", ПараметрыОтчета.ЛицевойСчет);
	Запрос.УстановитьПараметр("Помещение",   ПараметрыОтчета.ЛицевойСчет.Адрес);
	Запрос.УстановитьПараметр("Дата",        КонецДня(ПараметрыОтчета.ДатаПросмотра));
	
	Запрос.Текст = ПолучитьТекстЗапроса(ПараметрыОтчета);
	Результат = Запрос.Выполнить();
	
	// 2. Вывод отчета.
	
	Макет = ПолучитьМакет("Макет");
	
	// Вывод заголовка.
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ЛицевойСчет   = СокрЛП(ПараметрыОтчета.ЛицевойСчет);
	ОбластьМакета.Параметры.ДатаПросмотра = Формат(ПараметрыОтчета.ДатаПросмотра, "ДФ=dd.MM.yyyy");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	// Вывод таблицы.
	ВыборкаИтогов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтогов.Следующий();
	Итоги = ВыборкаИтогов;
	ВыборкаДеталей = ВыборкаИтогов.Выбрать();
	Пока ВыборкаДеталей.Следующий() Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьМакета.Параметры.Заполнить(ВыборкаДеталей);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Итоги);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	// Первую колонку не печатаем
	ТабличныйДокумент.ОбластьПечати = ТабличныйДокумент.Область(1, 2,
	                                  ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);
	
	// Присвоим имя для сохранения параметров печати табличного документа
	ТабличныйДокумент.ИмяПараметровПечати = "КраткийСписокПроживающихПоЛицевомуСчетуКВП";
	
	ПоместитьВоВременноеХранилище(ТабличныйДокумент, АдресХранилища);
	
КонецПроцедуры

// Возвращает текст запроса в зависимости от выбранного типа объектов.
Функция ПолучитьТекстЗапроса(ПараметрыОтчета)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(УПЖКХ_СведенияОЗарегистрированныхСрезПоследних.Жилец, УПЖКХ_СведенияОПроживающихСрезПоследних.Жилец) КАК ПроживающийСсылка,
	|	УПЖКХ_СведенияОЗарегистрированныхСрезПоследних.Зарегистрирован,
	|	УПЖКХ_СведенияОПроживающихСрезПоследних.Проживает
	|ПОМЕСТИТЬ врТаблицаЖильцов
	|ИЗ
	|	РегистрСведений.УПЖКХ_СведенияОЗарегистрированных.СрезПоследних(
	|			,
	|			ЛицевойСчет = &ЛицевойСчет
	|				И ДатаИзменения <= &Дата) КАК УПЖКХ_СведенияОЗарегистрированныхСрезПоследних
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_СведенияОПроживающих.СрезПоследних(
	|				,
	|				ЛицевойСчет = &ЛицевойСчет
	|					И ДатаИзменения <= &Дата) КАК УПЖКХ_СведенияОПроживающихСрезПоследних
	|		ПО УПЖКХ_СведенияОЗарегистрированныхСрезПоследних.Жилец = УПЖКХ_СведенияОПроживающихСрезПоследних.Жилец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	врТаблицаЖильцов.ПроживающийСсылка КАК ПроживающийСсылка,
	|	врТаблицаЖильцов.ПроживающийСсылка.Наименование КАК Проживающий,
	|	врТаблицаЖильцов.Зарегистрирован КАК Зарегистрирован,
	|	врТаблицаЖильцов.Проживает КАК Проживает,
	|	УПЖКХ_СобственникиПомещенийСрезПоследних.Действует КАК Собственник,
	|	врТаблицаЖильцов.ПроживающийСсылка.Владелец КАК ЛицевойСчет,
	|	ВЫБОР
	|		КОГДА УПЖКХ_СобственникиПомещенийСрезПоследних.Действует
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СобственникКоличество,
	|	ВЫБОР
	|		КОГДА врТаблицаЖильцов.Зарегистрирован
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗарегистрированКоличество,
	|	ВЫБОР
	|		КОГДА врТаблицаЖильцов.Проживает
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроживаетКоличество
	|ИЗ
	|	врТаблицаЖильцов КАК врТаблицаЖильцов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_СобственникиПомещений.СрезПоследних(&Дата, Помещение = &Помещение) КАК УПЖКХ_СобственникиПомещенийСрезПоследних
	|		ПО врТаблицаЖильцов.ПроживающийСсылка.ФизЛицо = УПЖКХ_СобственникиПомещенийСрезПоследних.Собственник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Проживающий
	|ИТОГИ
	|	СУММА(СобственникКоличество),
	|	СУММА(ЗарегистрированКоличество),
	|	СУММА(ПроживаетКоличество)
	|ПО
	|	ЛицевойСчет";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапроса()

#КонецОбласти

#КонецЕсли