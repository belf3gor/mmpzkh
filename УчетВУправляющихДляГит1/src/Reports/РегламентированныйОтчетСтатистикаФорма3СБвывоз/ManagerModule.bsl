#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
		
	Если НаДату > '20110101' Тогда
		Возврат Перечисления.ВерсииФорматовВыгрузки.ВерсияФСГС;
	КонецЕсли;
		
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2013Кв1";
	НоваяФорма.ОписаниеОтчета     = "Форма утверждена приказом Росстата от 27.07.2012 № 421.";
	НоваяФорма.РедакцияФормы	  = "от 27.07.2012 № 421.";
	НоваяФорма.ДатаНачалоДействия = '20130101';
	НоваяФорма.ДатаКонецДействия  = '20141231';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2015Кв1";
	НоваяФорма.ОписаниеОтчета     = "Форма утверждена приказом Росстата от 27.08.2014 № 536.";
	НоваяФорма.РедакцияФормы	  = "от 27.08.2014 № 536.";
	НоваяФорма.ДатаНачалоДействия = '20150101';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));

	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма20130101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "607013", '20120727', "421", "ФормаОтчета2013Кв1");
	Форма20150101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "607013", '20140827', "536", "ФормаОтчета2015Кв1");
	
	ВерсияВыгрузки = РегламентированнаяОтчетность.ПолучитьВерсиюВыгрузкиСтатОтчета("РегламентированныйОтчетСтатистикаФорма3СБвывоз", "ФормаОтчета2015Кв1");
	РегламентированнаяОтчетность.ОпределитьФорматВДеревеФормИФорматов(Форма20150101, ВерсияВыгрузки);
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "", ИмяОбъекта = "",
			ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

#КонецОбласти

#Если Не ВнешнееСоединение Тогда
#Область ФормированиеТекстаВыгрузки

Функция СформироватьСтруктуруПараметров(КонтекстФормы, мСохраненныйДок) 
	
	Перем ПолученноеЗначение, ТекЗначение;
	
	мДанныеОтчета = мСохраненныйДок.ДанныеОтчета.Получить();
	КонтекстФормы.мДанныеОтчета.Вставить("ПолеТабличногоДокументаРазделВывозимыеТовары", КонтекстФормы.ДанныеМногостраничныхРазделов.РазделВывозимыеТовары);
	
	ТаблицаЛистовРаздела = КонтекстФормы.ДанныеМногостраничныхРазделов.РазделВывозимыеТовары;
	Для Каждого Лист Из ТаблицаЛистовРаздела Цикл
		// Имитируем многострочность значений КодОКПД и КодИсточника.
		НомСуффикса = 1;
		Пока Лист.Данные.Свойство("П000001000102_" + Формат(НомСуффикса, "ЧГ="), ТекЗначение) Цикл
			Лист.Данные.Вставить("ПД00001000101_" + Формат(НомСуффикса, "ЧГ="), СокрЛП(Лист.Данные["КодОКПД"]));
			Если ЗначениеЗаполнено(Лист.Данные["КодИсточника1"]) Тогда
				Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 1);
			ИначеЕсли ЗначениеЗаполнено(Лист.Данные["КодИсточника2"]) Тогда
				Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 2);
			ИначеЕсли ЗначениеЗаполнено(Лист.Данные["КодИсточника3"]) Тогда
				Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 3);
			КонецЕсли;
			НомСуффикса = НомСуффикса + 1;
		КонецЦикла;
		// Дополнительно, вставляем данные по строке "Всего" (значение "НомСуффикса" уже инкрементировано).
		Лист.Данные.Вставить("П000001000102_" + Формат(НомСуффикса, "ЧГ="), "1000");
		Лист.Данные.Вставить("П000001000103_" + Формат(НомСуффикса, "ЧГ="), Лист.Данные["ПроданоВсего"]);
		Лист.Данные.Вставить("ПД00001000101_" + Формат(НомСуффикса, "ЧГ="), СокрЛП(Лист.Данные["КодОКПД"]));
		Если ЗначениеЗаполнено(Лист.Данные["КодИсточника1"]) Тогда
			Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 1);
		ИначеЕсли ЗначениеЗаполнено(Лист.Данные["КодИсточника2"]) Тогда
			Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 2);
		ИначеЕсли ЗначениеЗаполнено(Лист.Данные["КодИсточника3"]) Тогда
			Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 3);
		КонецЕсли;
		// Инкрементируем значение "НомСуффикса".
		НомСуффикса = НомСуффикса + 1;
		// Дополнительно, вставляем данные по строке "на экспорт".
		Лист.Данные.Вставить("П000001000102_" + Формат(НомСуффикса, "ЧГ="), "5000");
		Лист.Данные.Вставить("П000001000103_" + Формат(НомСуффикса, "ЧГ="), Лист.Данные["ПроданоНаЭкспорт"]);
		Лист.Данные.Вставить("ПД00001000101_" + Формат(НомСуффикса, "ЧГ="), СокрЛП(Лист.Данные["КодОКПД"]));
		Если ЗначениеЗаполнено(Лист.Данные["КодИсточника1"]) Тогда
			Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 1);
		ИначеЕсли ЗначениеЗаполнено(Лист.Данные["КодИсточника2"]) Тогда
			Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 2);
		ИначеЕсли ЗначениеЗаполнено(Лист.Данные["КодИсточника3"]) Тогда
			Лист.Данные.Вставить("ПД00001000102_" + Формат(НомСуффикса, "ЧГ="), 3);
		КонецЕсли;
		Лист.Данные = РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Лист.Данные);
	КонецЦикла;
	
	ТабДокументТитульный             = КонтекстФормы.мДанныеОтчета.ПолеТабличногоДокументаТитульный;
	ТабДокументРазделВывозимыеТовары = КонтекстФормы.мДанныеОтчета.ПолеТабличногоДокументаРазделВывозимыеТовары;
	ТабДокументПодписи               = КонтекстФормы.мДанныеОтчета.ПолеТабличногоДокументаПодписи;
	
	ДопАтрибуты = РегламентированнаяОтчетность.СформироватьСтруктуруДопАтрибутов(КонтекстФормы.ИмяФормы, "АтрибВыгрузкиXML2015Кв1");
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(мСохраненныйДок, "Организация, ДатаПодписи, ДатаОкончания");
	СтрокаСведений = "ФИОРук";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(РеквизитыДокумента.Организация, РеквизитыДокумента.ДатаПодписи, СтрокаСведений);
	
	ПараметрыВыгрузки = Новый Структура;
	
	ДопАтрибуты.Свойство("code", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("КодШаблона", ПолученноеЗначение);
	ДопАтрибуты.Свойство("idf", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("КодФормы", ПолученноеЗначение);
	ДопАтрибуты.Свойство("shifr", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("ШифрФормы", ПолученноеЗначение);
	ДопАтрибуты.Свойство("version", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("ВерсияШаблона", ПолученноеЗначение);
	ДопАтрибуты.Свойство("format_version", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("ВерсияФормата", ПолученноеЗначение);
	
	ДопАтрибуты.Свойство("idp", ПолученноеЗначение); // периодичность (количество отчетов в год)
	МесПериод = 12 / Число(ПолученноеЗначение);      // количество месяцев в периоде
	ОтчПериод = Окр(Месяц(РеквизитыДокумента.ДатаОкончания) / МесПериод);
	РасчПериод = Формат(Год(РеквизитыДокумента.ДатаОкончания),"ЧГ=0");
	
	ПараметрыВыгрузки.Вставить("ОКПО",  СокрЛП(ТабДокументТитульный.ОргКодОКПО));
	ПараметрыВыгрузки.Вставить("ОтчПериод", СокрЛП(ОтчПериод));
	ПараметрыВыгрузки.Вставить("РасчПериод", СокрЛП(РасчПериод));
	ПараметрыВыгрузки.Вставить("ОргНазвание", СокрЛП(ТабДокументТитульный.ОргНазв));
	ПараметрыВыгрузки.Вставить("ОргДиректор", СокрЛП(СведенияОбОрганизации.ФИОРук));
	ПараметрыВыгрузки.Вставить("ОргДолжностьИсп", СокрЛП(ТабДокументПодписи.ОргДолжностьИсп));
	ПараметрыВыгрузки.Вставить("ОргИсполнитель", СокрЛП(ТабДокументПодписи.ОргИсполнитель));
	ПараметрыВыгрузки.Вставить("ОргТелефонИсп", СокрЛП(ТабДокументПодписи.ОргТелефонИсп));
	
	// Преобразование выгружаемых атрибутов, в соответствии
	// с форматом выгрузки статотчетности.
	ДопАтрибуты.Свойство("idp", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("КодПериодичности", Число(СокрЛП(ПолученноеЗначение)));
	ПараметрыВыгрузки.Вставить("Документ", мСохраненныйДок);
	
	РегламентированнаяОтчетность.АтрибутыВФорматеВыгрузки(ПараметрыВыгрузки);
	
	ПараметрыВыгрузки.Вставить("ИмяФайла", Строка(Новый УникальныйИдентификатор) + ".xml");
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "row");
	
	Для НомСтрТабл = 0 По ТабДокументРазделВывозимыеТовары.Количество() - 1 Цикл
		
		ПараметрыСтраницыРаздела = ТабДокументРазделВывозимыеТовары[НомСтрТабл];
		СтруктураТекСтраницы = Новый Структура;
		
		НомСтр = 1;
		Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыСтраницыРаздела.Данные, "П000001000103_" + Формат(НомСтр, "ЧГ=")) Цикл
			СтруктураТекСтраницы.Вставить("ПР00001000103_" + Формат(НомСтр, "ЧГ="), ПараметрыСтраницыРаздела.Данные["П000001000103_" + Формат(НомСтр, "ЧГ=")]);
			НомСтр = НомСтр + 1;
		КонецЦикла;
		СтруктураТекСтраницы.Вставить("ПРПроданоВсего",     ПараметрыСтраницыРаздела.Данные["ПроданоВсего"]);
		СтруктураТекСтраницы.Вставить("ПРПроданоНаЭкспорт", ПараметрыСтраницыРаздела.Данные["ПроданоНаЭкспорт"]);
		
		ПараметрыВыгрузки.Вставить("ВывозимыеТоварыСтраница" + Формат((НомСтрТабл + 1), "ЧГ="), СтруктураТекСтраницы);
		
	КонецЦикла;
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

Процедура ПостОбработкаДереваВыгрузки(ДеревоВыгрузки)
	
	НайденныеУзлы = Новый Массив;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Код", "code");
	ПараметрыОтбора.Вставить("Многостраничность", Истина);
	ПараметрыОтбора.Вставить("Многострочность", Ложь);
	ПараметрыОтбора.Вставить("ЗначениеПоУмолчанию", "202");
	ПараметрыОтбора.Вставить("Значение", "202");
	
	НайденныеАтрибутыУзлов = ДеревоВыгрузки.Строки.НайтиСтроки(ПараметрыОтбора, Истина);
	
	// Формируем массив узлов, которые нужно объединить.
	Для каждого СтрокаАтрибутаУзла Из НайденныеАтрибутыУзлов Цикл
		РодительскийУзел = ?(СтрокаАтрибутаУзла.Родитель = Неопределено, СтрокаАтрибутаУзла.Владелец(), СтрокаАтрибутаУзла.Родитель);
		Если РодительскийУзел.Строки.НайтиСтроки(Новый Структура("Код, ЗначениеПоУмолчанию", "s1", "00")).Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если РодительскийУзел.Строки.НайтиСтроки(Новый Структура("Код, ЗначениеПоУмолчанию", "s2", "00")).Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		НайденныеУзлы.Добавить(РодительскийУзел);
	КонецЦикла;
	
	Если НайденныеУзлы.Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗначенийЭлементовУзлов = Новый ТаблицаЗначений;
	ТаблицаЗначенийЭлементовУзлов.Колонки.Добавить("КодКолонки", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийЭлементовУзлов.Колонки.Добавить("Значение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(17, 1)));
	
	ПервыйУзел = НайденныеУзлы[0];
	Для каждого НайденныйУзел Из НайденныеУзлы Цикл
		НайденныеСтроки = НайденныйУзел.Строки.НайтиСтроки(Новый Структура("Код", "col"));
		Для каждого Стр Из НайденныеСтроки Цикл
			СтрКод = Стр.Строки.Найти("code", "Код");
			Если СтрКод <> Неопределено Тогда
				СтрокаТаблицы = ТаблицаЗначенийЭлементовУзлов.Добавить();
				СтрокаТаблицы.КодКолонки = СтрКод.ЗначениеПоУмолчанию;
				Попытка
					СтрокаТаблицы.Значение = ?(ПустаяСтрока(Стр.Значение), 0, Число(Стр.Значение));
				Исключение
					СтрокаТаблицы.Значение = 0;
				КонецПопытки;
			КонецЕсли; 
		КонецЦикла;
		// Удаляем все найденные узлы, кроме первого.
		Если НайденныйУзел <> ПервыйУзел Тогда
			РегламентированнаяОтчетность.УдалитьУзел(НайденныйУзел);
		КонецЕсли; 
	КонецЦикла;
	
	ТаблицаЗначенийЭлементовУзлов.Свернуть("КодКолонки", "Значение");
	
	НайденныеСтроки = ПервыйУзел.Строки.НайтиСтроки(Новый Структура("Код", "col"));
	Для каждого Стр Из НайденныеСтроки Цикл
		СтрКод = Стр.Строки.Найти("code", "Код");
		Если СтрКод <> Неопределено Тогда
			СтрокаТаблицы = ТаблицаЗначенийЭлементовУзлов.Найти(СтрКод.ЗначениеПоУмолчанию, "КодКолонки");
			Если СтрокаТаблицы <> Неопределено Тогда
				РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, СтрокаТаблицы.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДаннымиУзел(КонтекстФормы, ПараметрыВыгрузки, Узел, НомерСтроки = Неопределено)
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		
		Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
			Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
				ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
				ЗначениеПоказателя = Неопределено;
				Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
					ФорматПредставленияНуля = Неопределено;
					Если ПараметрыВыгрузки.Свойство("ЧН" + ПолныйКодПоказателя, ФорматПредставленияНуля) Тогда
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя, ФорматПредставленияНуля);
					Иначе
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя);
					КонецЕсли;
				Иначе
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, "");
				КонецЕсли;
			Иначе
				Стр.Значение = "";
			КонецЕсли;
		ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
			РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
		Иначе
			Стр.Значение = Стр.ЗначениеПоУмолчанию;
			Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
				ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
				ЗначениеПоказателя = Неопределено;
				Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
					ФорматПредставленияНуля = Неопределено;
					Если ПараметрыВыгрузки.Свойство("ЧН" + ПолныйКодПоказателя, ФорматПредставленияНуля) Тогда
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя, ФорматПредставленияНуля);
					Иначе
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя);
					КонецЕсли;
				Иначе
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, "");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			Если Стр.Многостраничность И Стр.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				ЛистыМногостраничногоРаздела = Неопределено;
				Если Не ПустаяСтрока(Стр.Раздел) И КонтекстФормы.мДанныеОтчета.Свойство(Стр.Раздел, ЛистыМногостраничногоРаздела) Тогда
					Для Каждого Лист Из ЛистыМногостраничногоРаздела Цикл
						ДанныеЛиста = Лист.Данные;
						ДанныеЛиста.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти","");
						Если Стр.Многострочность Тогда
							НомСтр = 1;
							ПодчиненныйЭлемент = РегламентированнаяОтчетность.ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Стр);
							КлючПодчиненногоЭлемента = ПодчиненныйЭлемент.Ключ;
							Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ДанныеЛиста, КлючПодчиненногоЭлемента + "_" + Формат(НомСтр, "ЧГ=")) Цикл
								УзелСоответствующийСтроке = РегламентированнаяОтчетность.СкопироватьУзел(Узел, Стр);
								ЗаполнитьДаннымиУзел(КонтекстФормы, ДанныеЛиста, УзелСоответствующийСтроке, НомСтр);
								НомСтр = НомСтр + 1;
							КонецЦикла;
						Иначе
							УзелМногостраничногоРаздела = РегламентированнаяОтчетность.СкопироватьУзел(Узел, Стр);
							ЗаполнитьДаннымиУзел(КонтекстФормы, ДанныеЛиста, УзелМногостраничногоРаздела);
						КонецЕсли;
					КонецЦикла;
				Иначе
					ЗаполнитьДаннымиУзел(КонтекстФормы, ПараметрыВыгрузки, Стр);
				КонецЕсли;
			ИначеЕсли Стр.Многострочность И Стр.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				НомСтр = 1;
				ПодчиненныйЭлемент = РегламентированнаяОтчетность.ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Стр);
				КлючПодчиненногоЭлемента = ПодчиненныйЭлемент.Ключ;
				Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, КлючПодчиненногоЭлемента + "_" + Формат(НомСтр, "ЧГ=")) Цикл
					УзелСоответствующийСтроке = РегламентированнаяОтчетность.СкопироватьУзел(Узел, Стр);
					ЗаполнитьДаннымиУзел(КонтекстФормы, ПараметрыВыгрузки, УзелСоответствующийСтроке, НомСтр);
					НомСтр = НомСтр + 1;
				КонецЦикла;
			Иначе
				ЗаполнитьДаннымиУзел(КонтекстФормы, ПараметрыВыгрузки, Стр, НомерСтроки);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанными(КонтекстФормы, ДеревоВыгрузки, ПараметрыВыгрузки) Экспорт 
	
	РегламентированнаяОтчетность.ОбработатьУсловныеЭлементы(КонтекстФормы, ПараметрыВыгрузки, ДеревоВыгрузки);
	ЗаполнитьДаннымиУзел(КонтекстФормы, ПараметрыВыгрузки, ДеревоВыгрузки);
	ПостОбработкаДереваВыгрузки(ДеревоВыгрузки);
	РегламентированнаяОтчетность.ОтсечьНезаполненныеНеобязательныеУзлыСтатистики(ДеревоВыгрузки);
	
КонецПроцедуры

Функция ТекстВыгрузкиОтчетаСтатистики(мСохраненныйДок, ВыбраннаяФорма) Экспорт 
	ТекстВыгрузки = "";
	Если ВыбраннаяФорма = "ФормаОтчета2015Кв1" Тогда 
		КонтекстФормы = УниверсальныйОтчетСтатистики.СформироватьКонтекстФормыДляПоказателей(мСохраненныйДок);
		ПараметрыВыгрузки = СформироватьСтруктуруПараметров(КонтекстФормы, мСохраненныйДок);
		ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(КонтекстФормы, "СхемаВыгрузкиXML2015Кв1");
		ЗаполнитьДанными(КонтекстФормы, ДеревоВыгрузки, ПараметрыВыгрузки);
		ТекстВыгрузки = РегламентированнаяОтчетность.ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки);
	КонецЕсли;
	
	Возврат ТекстВыгрузки;
КонецФункции

#КонецОбласти
#КонецЕсли

#КонецЕсли