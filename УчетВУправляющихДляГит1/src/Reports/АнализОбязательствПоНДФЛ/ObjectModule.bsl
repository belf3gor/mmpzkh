#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции


// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		СхемаКомпоновкиДанных.Параметры.ВыборкаКонтрольСроков.ВключатьВДоступныеПоля 	= Ложь;
		СхемаКомпоновкиДанных.Параметры.ВыборкаКонтрольСроков.ОграничениеИспользования 	= Истина;
		
		Если КлючВарианта = "КонтрольСроковУплатыПоСтатьям" Или КлючВарианта = "КонтрольСроковУплатыПоИсточникам" Тогда
			СхемаКомпоновкиДанных.Параметры.ПериодОбязательств.ВключатьВДоступныеПоля 	= Ложь;
			СхемаКомпоновкиДанных.Параметры.ПериодОбязательств.ОграничениеИспользования 	= Истина;
		ИначеЕсли КлючВарианта = "УдержанныйНДФЛ" Или КлючВарианта = "УдержанныйНДФЛПоСтатьям"
			Или КлючВарианта = "УдержанныйНДФЛПоИсточникам" Тогда
			СхемаКомпоновкиДанных.Параметры.ПериодКонтрольСроков.ВключатьВДоступныеПоля   = Ложь;
			СхемаКомпоновкиДанных.Параметры.ПериодКонтрольСроков.ОграничениеИспользования = Истина;
		КонецЕсли;
		
		ИмяОпции = "РаботаВХозрасчетнойОрганизации";
		ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
		
		Если Не ФункциональнаяОпцияИспользуется Или ПолучитьФункциональнуюОпцию(ИмяОпции) Тогда 
			ПолеСтатьяРасходов = СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхНДФЛ.Поля.Найти("СтатьяРасходов");
			Если ПолеСтатьяРасходов <> Неопределено Тогда
				ПолеСтатьяРасходов.ОграничениеИспользования.Поле 		= Истина;
				ПолеСтатьяРасходов.ОграничениеИспользования.Группировка = Истина;
				ПолеСтатьяРасходов.ОграничениеИспользования.Порядок 	= Истина;
				ПолеСтатьяРасходов.ОграничениеИспользования.Условие 	= Истина;
				ПолеСтатьяРасходов.ОграничениеИспользованияРеквизитов.Поле 			= Истина;
				ПолеСтатьяРасходов.ОграничениеИспользованияРеквизитов.Группировка 	= Истина;
				ПолеСтатьяРасходов.ОграничениеИспользованияРеквизитов.Порядок 		= Истина;
				ПолеСтатьяРасходов.ОграничениеИспользованияРеквизитов.Условие 		= Истина;
			КонецЕсли;
		КонецЕсли;
		
		ЗарплатаКадрыОтчеты.ПодключитьСхему(Контекст, ЭтотОбъект, КлючСхемы, КлючВарианта, НовыеПользовательскиеНастройкиКД);
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#КонецЕсли