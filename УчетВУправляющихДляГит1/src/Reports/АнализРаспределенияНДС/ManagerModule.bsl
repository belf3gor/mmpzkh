#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//  ДокументОбъект - если указан, то печать отчета по документу
//
Процедура СформироватьОтчет(Знач ПараметрыОтчета, АдресХранилища) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ДокументРезультат.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб			= Истина;
	
	Макет = Отчеты.АнализРаспределенияНДС.ПолучитьМакет("Таблица");
	
	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");
	
	Заголовок = "Анализ распределения сумм НДС";
	
	РеквизитыДокументаРаспределения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыОтчета.ДокументРаспределения, "Организация, НачалоПериода");
	ДокументРаспределенияЗаполнен = ЗначениеЗаполнено(ПараметрыОтчета.ДокументРаспределения);
	
	НачалоПериода = ?(НЕ ДокументРаспределенияЗаполнен, НачалоПериода, РеквизитыДокументаРаспределения.НачалоПериода);
	КонецПериода  = ?(НЕ ДокументРаспределенияЗаполнен, КонецПериода, КонецКвартала(РеквизитыДокументаРаспределения.НачалоПериода));
	
	ОбластьЗаголовок.Параметры.Период = "Период: " + ПредставлениеПериода(НачалоДня(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода)) + ?(НЕ ДокументРаспределенияЗаполнен, "", " по документу """ + ПараметрыОтчета.ДокументРаспределения + """");
	
	СведенияОбОрганизации	= БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
		?(ДокументРаспределенияЗаполнен, РеквизитыДокументаРаспределения.Организация, ПараметрыОтчета.Организация),
		ПараметрыОтчета.КонецПериода);
		
	ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = ПредставлениеОрганизации;
	
	ОбластьЗаголовок.Параметры.Заголовок	= Заголовок;
	
	ОбластьЗаголовок.Параметры.ИННОрганизации	= СокрЛП(СведенияОбОрганизации.ИНН)
												+ ?(ЗначениеЗаполнено(СведенияОбОрганизации.КПП)," / ", "")
												+ СокрЛП(СведенияОбОрганизации.КПП);
	
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	
	ВывестиВОтчетДанныеПоБазеРаспределения(ПараметрыОтчета, ДокументРезультат, Макет);
	
	ВывестиВОтчетДанныеПоРаспределениюКосвенныхРасходов(ПараметрыОтчета, ДокументРезультат, Макет);
	
	ПоместитьВоВременноеХранилище(ДокументРезультат, АдресХранилища);
	
КонецПроцедуры

// Выполняет запрос и формирует таблицу соответствующую базе распределения
// косвенных расходов
//
Процедура ВывестиВОтчетДанныеПоБазеРаспределения(ПараметрыОтчета, ДокументРезультат, Макет)
	
	Запрос = Новый Запрос;
	
	Если НЕ ПараметрыОтчета.ВыручкаПоДокументам Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		|	РаспределениеНДС.Ссылка КАК ДокументРаспределения,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) КАК ВыручкаЕНВД,
		|	СУММА(РаспределениеНДС.ВыручкаБезНДС) КАК ВыручкаБезНДС,
		|	СУММА(РаспределениеНДС.ВыручкаНДС0) КАК ВыручкаНДС0,
		|	СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВыручкаНДС,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) + СУММА(РаспределениеНДС.ВыручкаБезНДС) + СУММА(РаспределениеНДС.ВыручкаНДС0) + СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВсегоВыручка
		|ИЗ
		|	Документ.РаспределениеНДС КАК РаспределениеНДС
		|ГДЕ
		|	РаспределениеНДС.Организация = &Организация
		|	И РаспределениеНДС.НачалоПериода МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаспределениеНДС.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаспределениеНДС.Проведен = &Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределениеНДС.Ссылка";
		
	Иначе
		
		Запрос.Текст = "ВЫБРАТЬ
		|	РаспределениеНДС.Ссылка КАК ДокументРаспределения,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) КАК ВыручкаЕНВД,
		|	СУММА(РаспределениеНДС.ВыручкаБезНДС) КАК ВыручкаБезНДС,
		|	СУММА(РаспределениеНДС.ВыручкаНДС0) КАК ВыручкаНДС0,
		|	СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВыручкаНДС,
		|	СУММА(РаспределениеНДС.ВыручкаЕНВД) + СУММА(РаспределениеНДС.ВыручкаБезНДС) + СУММА(РаспределениеНДС.ВыручкаНДС0) + СУММА(РаспределениеНДС.ВыручкаНДС) КАК ВсегоВыручка
		|ИЗ
		|	Документ.РаспределениеНДС КАК РаспределениеНДС
		|ГДЕ
		|	РаспределениеНДС.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределениеНДС.Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("Проведен", 		Истина);
	Запрос.УстановитьПараметр("Ссылка", 		ПараметрыОтчета.ДокументРаспределения);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ПараметрыБазыРаспределения");
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
		
	Результат.Сбросить();
	Пока Результат.Следующий() Цикл
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ДокументБазыРаспределения");
		ОбластьШапкаТаблицы.Параметры.Заполнить(Результат);
		ОбластьШапкаТаблицы.Параметры.Расшифровка = Результат.ДокументРаспределения;
		ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
		
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("СтрокаБазыРаспределения");
		ОбластьШапкаТаблицы.Параметры.Заполнить(Результат);
		ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуТабличнойЧасти(ДокументРезультат, ОбластьСтрока, Выборка)
	
	Если ТипЗнч(Выборка.СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ОбластьСтрока.Параметры.ДатаОперации = "" + Формат(Выборка.СчетФактура.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy") 
			+ ", №" + Выборка.СчетФактура.НомерВходящегоДокумента;
	Иначе
		Документ = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруПолученный(Выборка.СчетФактура);
		Если Документ = Неопределено Тогда
			Попытка
				ОбластьСтрока.Параметры.ДатаОперации = "" + Формат(Выборка.СчетФактура.Дата, "ДФ=dd.MM.yyyy") 
					+ ", №" + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.СчетФактура.Номер, Истина, Ложь);
			Исключение
				ОбластьСтрока.Параметры.ДатаОперации = "";
			КонецПопытки; 
		Иначе
			ОбластьСтрока.Параметры.ДатаОперации = "" + Формат(Документ.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy") 
					+ ", №" + Документ.НомерВходящегоДокумента;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьСтрока.Параметры.Заполнить(Выборка);
	ДокументРезультат.Вывести(ОбластьСтрока);
	
КонецПроцедуры

// Выполняет запрос к регистру НДСПоКосвеннымРасходам
// и выбирает информацию по косвенным расходам текущего периода, которые были
// распределены на соответствующие ставки реализации
//
Процедура ВывестиВОтчетДанныеПоРаспределениюКосвенныхРасходов(ПараметрыОтчета, ДокументРезультат, Макет)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода",              ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",               КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("Ссылка",                     ПараметрыОтчета.ДокументРаспределения);
	Запрос.УстановитьПараметр("ВыручкаПоДокументам",        ПараметрыОтчета.ВыручкаПоДокументам);
	Запрос.УстановитьПараметр("РаздельныйУчетНДСНаСчете19", ПараметрыОтчета.РаздельныйУчетНДСНаСчете19);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспределениеНДС.Ссылка КАК ДокументРаспределения
	|ПОМЕСТИТЬ ДокументыРаспределения
	|ИЗ
	|	Документ.РаспределениеНДС КАК РаспределениеНДС
	|ГДЕ
	|	РаспределениеНДС.Ссылка = &Ссылка
	|	И &ВыручкаПоДокументам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РаспределениеНДС.Ссылка
	|ИЗ
	|	Документ.РаспределениеНДС КАК РаспределениеНДС
	|ГДЕ
	|	РаспределениеНДС.Проведен
	|	И РаспределениеНДС.Организация = &Организация
	|	И РаспределениеНДС.НачалоПериода МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РаспределениеНДС.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ &ВыручкаПоДокументам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспределениеНДСРаспределяемыйНДС.Ссылка КАК ДокументРаспределения,
	|	РаспределениеНДСРаспределяемыйНДС.ВидЦенности КАК ВидЦенности,
	|	РаспределениеНДСРаспределяемыйНДС.Поставщик КАК Поставщик,
	|	РаспределениеНДСРаспределяемыйНДС.СчетФактура КАК СчетФактура,
	|	РаспределениеНДСРаспределяемыйНДС.СтавкаНДС КАК СтавкаНДС,
	|	РаспределениеНДСРаспределяемыйНДС.СчетУчетаНДС КАК СчетУчетаНДС,
	|	РаспределениеНДСРаспределяемыйНДС.СуммаБезНДС КАК ВсегоСумма,
	|	РаспределениеНДСРаспределяемыйНДС.НДС КАК ВсегоНДС,
	|	РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимости КАК БезНДССумма,
	|	РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимости КАК БезНДС,
	|	РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСДляОперацийПо0 КАК НДС0Сумма,
	|	РаспределениеНДСРаспределяемыйНДС.НДСДляОперацийПо0 КАК НДС0,
	|	РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСУчитываетсяВCтоимостиЕНВД КАК ЕНВДСумма,
	|	РаспределениеНДСРаспределяемыйНДС.НДСУчитываетсяВCтоимостиЕНВД КАК ЕНВДНДС,
	|	РаспределениеНДСРаспределяемыйНДС.СуммаБезНДСПринимаетсяКВычету КАК Сумма,
	|	РаспределениеНДСРаспределяемыйНДС.НДСПринимаетсяКВычету КАК НДС
	|ПОМЕСТИТЬ РаспределяемыйНДС
	|ИЗ
	|	ДокументыРаспределения КАК РаспределениеНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеНДС.РаспределяемыйНДС КАК РаспределениеНДСРаспределяемыйНДС
	|		ПО РаспределениеНДС.ДокументРаспределения = РаспределениеНДСРаспределяемыйНДС.Ссылка
	|ГДЕ
	|	&РаздельныйУчетНДСНаСчете19
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РаспределениеНДССоставКосвенныхРасходов.Ссылка,
	|	РаспределениеНДССоставКосвенныхРасходов.ВидЦенности,
	|	РаспределениеНДССоставКосвенныхРасходов.Поставщик,
	|	РаспределениеНДССоставКосвенныхРасходов.СчетФактура,
	|	РаспределениеНДССоставКосвенныхРасходов.СтавкаНДС,
	|	РаспределениеНДССоставКосвенныхРасходов.СчетУчетаНДС,
	|	РаспределениеНДССоставКосвенныхРасходов.СуммаВсего,
	|	РаспределениеНДССоставКосвенныхРасходов.НДСВсего,
	|	РаспределениеНДССоставКосвенныхРасходов.БезНДССумма,
	|	РаспределениеНДССоставКосвенныхРасходов.БезНДС,
	|	РаспределениеНДССоставКосвенныхРасходов.НДС0Сумма,
	|	РаспределениеНДССоставКосвенныхРасходов.НДС0,
	|	РаспределениеНДССоставКосвенныхРасходов.ЕНВДСумма,
	|	РаспределениеНДССоставКосвенныхРасходов.ЕНВДНДС,
	|	РаспределениеНДССоставКосвенныхРасходов.НДССумма,
	|	РаспределениеНДССоставКосвенныхРасходов.НДС
	|ИЗ
	|	ДокументыРаспределения КАК РаспределениеНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеНДС.СоставКосвенныхРасходов КАК РаспределениеНДССоставКосвенныхРасходов
	|		ПО РаспределениеНДС.ДокументРаспределения = РаспределениеНДССоставКосвенныхРасходов.Ссылка
	|ГДЕ
	|	НЕ &РаздельныйУчетНДСНаСчете19
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРаспределения,
	|	СчетФактура,
	|	Поставщик,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаспределяемыйНДС.СчетФактура КАК СчетФактура,
	|	РаспределяемыйНДС.ДокументРаспределения КАК ДокументРаспределения
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	РаспределяемыйНДС КАК РаспределяемыйНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДокументРаспределения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленный.Регистратор КАК ДокументРаспределения,
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.Поставщик КАК Поставщик,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС КАК СчетУчетаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА НДСПредъявленный.НДС > 0
	|				ТОГДА НДСПредъявленный.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДСВключен,
	|	СУММА(ВЫБОР
	|			КОГДА НДСПредъявленный.НДС < 0
	|				ТОГДА -НДСПредъявленный.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДСИсключен
	|ПОМЕСТИТЬ НДСВключенныйВСтоимость
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО НДСПредъявленный.СчетФактура = СписокСчетовФактур.СчетФактура
	|			И НДСПредъявленный.Регистратор = СписокСчетовФактур.ДокументРаспределения
	|ГДЕ
	|	НДСПредъявленный.Период <= &КонецПериода
	|	И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НДСПредъявленный.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НДСВключенВСтоимость)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленный.Регистратор,
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.Поставщик,
	|	НДСПредъявленный.ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРаспределения,
	|	СчетФактура,
	|	Поставщик,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределяемыйНДС.ДокументРаспределения КАК ДокументРаспределения,
	|	РаспределяемыйНДС.ВидЦенности,
	|	РаспределяемыйНДС.Поставщик,
	|	РаспределяемыйНДС.СчетФактура,
	|	РаспределяемыйНДС.СтавкаНДС,
	|	СУММА(РаспределяемыйНДС.ВсегоСумма) КАК ВсегоСумма,
	|	СУММА(РаспределяемыйНДС.ВсегоНДС) КАК ВсегоНДС,
	|	СУММА(РаспределяемыйНДС.БезНДССумма) КАК БезНДССумма,
	|	СУММА(РаспределяемыйНДС.БезНДС) КАК БезНДС,
	|	СУММА(РаспределяемыйНДС.НДС0Сумма) КАК НДС0Сумма,
	|	СУММА(РаспределяемыйНДС.НДС0) КАК НДС0,
	|	СУММА(РаспределяемыйНДС.ЕНВДСумма) КАК ЕНВДСумма,
	|	СУММА(РаспределяемыйНДС.ЕНВДНДС) КАК ЕНВДНДС,
	|	СУММА(РаспределяемыйНДС.Сумма) КАК Сумма,
	|	СУММА(РаспределяемыйНДС.НДС) КАК НДС,
	|	СУММА(НДСВключенныйВСтоимость.НДСВключен) КАК НДСВключен,
	|	СУММА(НДСВключенныйВСтоимость.НДСИсключен) КАК НДСИсключен
	|ИЗ
	|	РаспределяемыйНДС КАК РаспределяемыйНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСВключенныйВСтоимость КАК НДСВключенныйВСтоимость
	|		ПО РаспределяемыйНДС.ДокументРаспределения = НДСВключенныйВСтоимость.ДокументРаспределения
	|			И РаспределяемыйНДС.СчетФактура = НДСВключенныйВСтоимость.СчетФактура
	|			И РаспределяемыйНДС.Поставщик = НДСВключенныйВСтоимость.Поставщик
	|			И РаспределяемыйНДС.ВидЦенности = НДСВключенныйВСтоимость.ВидЦенности
	|			И РаспределяемыйНДС.СтавкаНДС = НДСВключенныйВСтоимость.СтавкаНДС
	|			И РаспределяемыйНДС.СчетУчетаНДС = НДСВключенныйВСтоимость.СчетУчетаНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределяемыйНДС.ДокументРаспределения,
	|	РаспределяемыйНДС.СчетФактура,
	|	РаспределяемыйНДС.Поставщик,
	|	РаспределяемыйНДС.ВидЦенности,
	|	РаспределяемыйНДС.СтавкаНДС
	|ИТОГИ
	|	СУММА(ВсегоСумма),
	|	СУММА(ВсегоНДС),
	|	СУММА(БезНДССумма),
	|	СУММА(БезНДС),
	|	СУММА(НДС0Сумма),
	|	СУММА(НДС0),
	|	СУММА(ЕНВДСумма),
	|	СУММА(ЕНВДНДС),
	|	СУММА(Сумма),
	|	СУММА(НДС),
	|	СУММА(НДСВключен),
	|	СУММА(НДСИсключен)
	|	
	|	&ТекстДляЗамены";
	
	Если ПараметрыОтчета.ВыручкаПоДокументам Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстДляЗамены", "ПО ОБЩИЕ, ДокументРаспределения");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстДляЗамены", "ПО ОБЩИЕ");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	НаименованиеВертикальнойОбластиМакета = "";
	Если ПараметрыОтчета.РаздельныйУчетНДСНаСчете19 Тогда
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицыРаздельныйУчетНДСНаСчете19");
		НаименованиеВертикальнойОбластиМакета = "|РаздельныйУчетНДСНаСчете19" 
	Иначе
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицыРаспределения");
	КонецЕсли;
	
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьСтрокаДокумента = Макет.ПолучитьОбласть("СтрокаДокументРаспределения"+НаименованиеВертикальнойОбластиМакета);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка"+НаименованиеВертикальнойОбластиМакета);
	ОбластьИтоги  = Макет.ПолучитьОбласть("Итоги"+НаименованиеВертикальнойОбластиМакета);
	
	Пока ВыборкаИтоги.Следующий() Цикл
		
		Если ПараметрыОтчета.ВыручкаПоДокументам Тогда
			
			ВыборкаДокументы = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			Пока ВыборкаДокументы.Следующий() Цикл
				
				ОбластьСтрокаДокумента.Параметры.Заполнить(ВыборкаДокументы);
				ОбластьСтрокаДокумента.Параметры.Расшифровка = ВыборкаДокументы.ДокументРаспределения;
				ДокументРезультат.Вывести(ОбластьСтрокаДокумента);
				
				ВыборкаСтроки = ВыборкаДокументы.Выбрать();
				Пока ВыборкаСтроки.Следующий() Цикл
					ВывестиСтрокуТабличнойЧасти(ДокументРезультат, ОбластьСтрока, ВыборкаСтроки);
				КонецЦикла;
				
			КонецЦикла;
			
		Иначе
			
			ВыборкаСтроки = ВыборкаИтоги.Выбрать();
			Пока ВыборкаСтроки.Следующий() Цикл
				ВывестиСтрокуТабличнойЧасти(ДокументРезультат, ОбластьСтрока, ВыборкаСтроки);
			КонецЦикла;
			
		КонецЕсли;
		
		ОбластьИтоги.Параметры.Заполнить(ВыборкаИтоги);
		ДокументРезультат.Вывести(ОбластьИтоги);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли