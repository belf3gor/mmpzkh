// Область "Настройки" описывает параметры инициализации механизма загрузки
// регламентированных отчетов из файлов выгрузки. Формат области настроек - JSON.
//
// Описание параметров:
//  * Установкой параметра "РазрешитьЗагрузку" в "true" или "false" регулируем видимость
//    кнопки "Загрузить" и возможность загрузки из файла в форме отчета.
//  * Параметр "ФорматыСхемВыгрузки" описывает соответствие имен макетов схем выгрузки
//    поддерживаемым форматам загрузки. Если у формы единственная схема выгрузки или
//    нужно всегда использовать схему по умолчанию - указываем значение "null".
//  * Параметр "ОбластиОбработчиков" содержит список имен используемых обработчиков
//    загружаемых данных. Имена соответствуют областям макета, в которых можно размещать
//    произвольные алгоритмы на языке "1С:Предприятие" для обработки доступных объектов.
//  * Параметр "ИмяФайлаНастроек" содержит путь к файлу настроек для быстрого описания
//    правил загрузки без сохранения конфигурации. После отладки правил загрузки нужно
//    обязательно перенести содержимое файла в макет и удалить параметр или присвоить ему
//    значение "null". В целях безопасности нельзя выпускать конфигурации с заполненным
//    параметром "ИмяФайлаНастроек".
//
// Примечание:
//  Сопоставление имен узлов из файла XML и имен показателей отчета выполняется по данным схемы выгрузки,
//  поэтому обработчики используются для получения значений показателей не описанных в схеме выгрузки.
//
#Область Настройки
{
	"РазрешитьЗагрузку"   : true,
	
	"ФорматыСхемВыгрузки" : null,
	"ОбластиОбработчиков" : {
		"ПередЗагрузкой"  : "ПредОбработкаСхемыЗагрузки",
		"ПослеЗагрузки"   : "ПостОбработкаДокументаОтчета",
	}
}
#КонецОбласти

// Доступны параметры:
//
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПредОбработкаСхемыЗагрузки
	
	НайденныеУзлы = УзлыПоXPath(П.ДеревоДляЗагрузки, "//РасчФинРез/ОперЦБ");
	Для Каждого НайденныйУзел Из НайденныеУзлы Цикл
		НайденныйУзел.Многостраничность = Истина;
	КонецЦикла;
	
	НайденныеСтроки = П.ДеревоДляЗагрузки.Строки.НайтиСтроки(Новый Структура("Тип", "П"), Истина);
	Для Каждого СтрокаДерева Из НайденныеСтроки Цикл
		СтрокаДерева.Тип = "Э";
	КонецЦикла;
	
#КонецОбласти

// Доступны параметры:
//
//  П.ДанныеОтчета      - заполненные данные документа регламентированного отчета;
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПостОбработкаДокументаОтчета
	
	Титульный = Неопределено;
	
	П.ДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаТитульный", Титульный);
	Если Титульный <> Неопределено Тогда
		
		Титульный.ДатаДоговора = ДатаИзСтрокиЛюбогоФормата(П.ПараметрыОтчета.ДатаДоговораИТ);
		
		Титульный.НомерДоговора = П.ПараметрыОтчета.НомерДоговораИТ;
		
		Титульный.НаименованиеДоговора = П.ПараметрыОтчета.НаименованиеДоговораИТ;
		
		Титульный.НомерКонтактногоТелефона = П.ПараметрыОтчета.НомерТелефона;
		
	КонецЕсли;
	
	Раздел1 = Неопределено;
	
	П.ДанныеОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел1", Раздел1);
	Если Раздел1 <> Неопределено Тогда
		Для Каждого СтраницаРаздела Из Раздел1 Цикл
			
			СтраницаРаздела.Данные.ЧислоУчастников = П.ПараметрыОтчета.ЧислоУчастниковДоговораИТ;
			
		КонецЦикла;
	КонецЕсли;
	
	Раздел3МнЧасть = УзлыПоXPath(П.ДеревоДляЗагрузки, "//РасчФинРез/РаспРасхУпТов/РасхУчДогИТ"); // Раздел 3 (многострочная часть)
	
	Для НомСтроки = 1 По Раздел3МнЧасть.Количество() Цикл
		ТекущаяСтрока = Раздел3МнЧасть[НомСтроки - 1];
		
		ТекущаяСтрока.Раздел = "Раздел3";
		
		УзелДолУчПриб = УзлыПоXPath(ТекущаяСтрока, "@ДолУчПриб")[0];
		УзелДолУчПриб.Ключ = "П000030002003";
		
		УзелСумРасх = УзлыПоXPath(ТекущаяСтрока, "@СумРасх")[0];
		УзелСумРасх.Ключ = "П000030002004";
		
		УзелИННФЛ_Уч = УзлыПоXPath(ТекущаяСтрока, "ИННФЛ_Уч")[0];
		УзелИННФЛ_Уч.МинРазмерность = 10;
		УзелИННФЛ_Уч.Ключ = "П000030002001";
		
		УзелИННЮЛ_Уч = УзлыПоXPath(ТекущаяСтрока, "УчЮЛ/@ИННЮЛ_Уч")[0];
		УзелКПП_Уч = УзлыПоXPath(ТекущаяСтрока, "УчЮЛ/@КПП_Уч")[0];
		УзелКПП_Уч.Ключ = "П000030002005";
		
		Если ЗначениеЗаполнено(УзелИННЮЛ_Уч.Значение) Тогда
			УзелИННФЛ_Уч.Значение = УзелИННЮЛ_Уч.Значение;
		КонецЕсли;
		
		ЗаполнитьДаннымиСохраненныйДокументИзДереваДляЗагрузки(ТекущаяСтрока, П.ДанныеОтчета, П.ПараметрыОтчета, , , НомСтроки);
	КонецЦикла;
	
#КонецОбласти