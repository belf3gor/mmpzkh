&НаСервере
Перем мОбъектОтчета;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обход ошибки платформы Параметр сеанса отсутствует или удален.
	ТабличныйДокумент.Очистить();
	
	СформироватьСтруктуруРеквизитовФормы();
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;
	
	СтруктураРеквизитовФормы.мСоставПоказателей = "СоставПоказателей2007Кв1";
	
	СтруктураДанныхРаздел1 = Новый Структура;
	СтруктураДанныхРаздел2 = Новый Структура;
	СтруктураДанныхРаздел3 = Новый Структура;
	
	СтруктураРеквизитовФормы.мОписаниеТиповЧислоЕ = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
	
	СтруктураРеквизитовФормы.мИдГруппы1 = "П0000100001";
	
	// количество строк многострочных разделов по "бумажной" форме отчета
	// (минимальное количество строк, которое должно присутствовать всегда)
	КолИсхСтрокИдГруппы1 = 1;
	
	КолКолонокИдГруппы1 = 25;
	
	ИнициализацияМногостраничныхРазделов();
	
	// структура многострочных разделов формы
	мСтруктураМногострочныхРазделов = Новый Структура;
	мСтруктураМногострочныхРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, СтруктураРеквизитовФормы.мИдГруппы1);
	
	// Структура содержит данные дополнительных страниц
	мСтруктураМногостраничныхРазделов = Новый Структура;
	мСтруктураМногостраничныхРазделов.Вставить("Приложение", "ТаблицаСтраницыПриложение");
	
	// структура исходных строк (минимального кол-ва) многострочных разделов формы
	мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, КолИсхСтрокИдГруппы1);
	
	мСтруктураКолвоКолонокРазделов = Новый Структура;
	мСтруктураКолвоКолонокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, КолКолонокИдГруппы1);
	
	ЦветЗаполняетсяТолькоВручную     = Новый Цвет(255, 255, 225);
	ЦветЗаполняетсяАвтоКорректир     = Новый Цвет(255, 240, 200);
	ЦветЗаполняетсяАвтоБезКорректир  = Новый Цвет(230, 240, 220);
	ЦветЗаполняетсяАвтоАрифметика    = Новый Цвет(192, 220, 192); // ЗЕЛЕНЫЙ
	ЦветЗаполняетсяОднозначноВручную = Новый Цвет(255, 255, 192); // ЖЕЛТЫЙ
	ЦветЗаполняетсяАвтоматически     = Новый Цвет(255, 255, 255); // Белый, ввод запрещен
	
	// структура цветов раскраски автозаполняемых ячеек
	мСтруктураЦветовРаскраски = Новый Структура;
	мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
	мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
	мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);
	мСтруктураЦветовРаскраски.Вставить("_4", ЦветЗаполняетсяАвтоАрифметика);
	мСтруктураЦветовРаскраски.Вставить("_0", ЦветЗаполняетсяОднозначноВручную);
	мСтруктураЦветовРаскраски.Вставить("_5", ЦветЗаполняетсяАвтоматически);
	
	мСтруктураВариантыЗаполнения       = Новый Структура;
	мСтруктураВариантыЗаполненияЭталон = Новый Структура;
	
	// Версия формы
	СтруктураРеквизитовФормы.мВерсияФормы = "01/12/2014";
	
	// Код отчета, равен КНД
	СтруктураРеквизитовФормы.мКодОтчета = "1151099";

	СтруктураРеквизитовФормы.мПечатныеформы = Новый СписокЗначений;
	
	мСтрукТЗДокументы = Новый Структура;
	
	ИмяРаздела = "Раздел1";
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела;
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, "ФормаОтчета")) + "_" + ИмяРаздела));
	
	СтруктураРеквизитовФормы.мВыбраннаяФорма   = Параметры.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мПериодичность    = Параметры.мПериодичность;
	СтруктураРеквизитовФормы.мСкопированаФорма = Параметры.мСкопированаФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок   = Параметры.мСохраненныйДок;
	СтруктураРеквизитовФормы.Организация       = Параметры.Организация;
	
	ДатаПодписи	= Параметры.ДатаПодписи; 
	
	ПорядкиОкругленияОтчетности = Новый Структура;
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1",       Перечисления.ПорядкиОкругленияОтчетности.Окр1);
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1000",    Перечисления.ПорядкиОкругленияОтчетности.Окр1000);
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1000000", Перечисления.ПорядкиОкругленияОтчетности.Окр1000000);
	
	СтруктураРеквизитовФормы.СправочникиВидыКонтактнойИнформацииАдресОрганизации = РегламентированнаяОтчетность.ВидКонтактнойИнформации("ЮрАдресКонтрагента");
	
	Если ДополнительныеДанныеРазделов = Неопределено Тогда
		ДополнительныеДанныеРазделов = Новый Структура;
	КонецЕсли;
	
	Инициализация(Параметры.БезОткрытияФормы);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = Новый Структура;
	
	СтруктураРеквизитовФормы.Вставить("ВидДокумента");
	СтруктураРеквизитовФормы.Вставить("НаимТекущегоРаздела");
	СтруктураРеквизитовФормы.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитовФормы.Вставить("ТочностьЕдиницыИзмерения");
	СтруктураРеквизитовФормы.Вставить("мЗаписьЗапрещена");
	СтруктураРеквизитовФормы.Вставить("Организация");
	СтруктураРеквизитовФормы.Вставить("ИП");
	СтруктураРеквизитовФормы.Вставить("КодНО");
	СтруктураРеквизитовФормы.Вставить("мАвтоВыборКодов");
	СтруктураРеквизитовФормы.Вставить("мБезОткрытияФормы");
	СтруктураРеквизитовФормы.Вставить("мВариант");
	СтруктураРеквизитовФормы.Вставить("мВерсияФормы");
	СтруктураРеквизитовФормы.Вставить("мВПрограммеИзмененаОрганизация");
	СтруктураРеквизитовФормы.Вставить("мВыбраннаяФорма");
	СтруктураРеквизитовФормы.Вставить("мДелитель");
	СтруктураРеквизитовФормы.Вставить("мДокументВосстановлен");
	СтруктураРеквизитовФормы.Вставить("мЗаписываетсяНовыйДокумент");
	СтруктураРеквизитовФормы.Вставить("мИдГруппы1");
	СтруктураРеквизитовФормы.Вставить("мИННЮЛ");
	СтруктураРеквизитовФормы.Вставить("мКодОтчета");
	СтруктураРеквизитовФормы.Вставить("мКППЮЛ");
	СтруктураРеквизитовФормы.Вставить("мОписаниеТиповЧислоЕ");
	СтруктураРеквизитовФормы.Вставить("мПечатныеФормы");
	СтруктураРеквизитовФормы.Вставить("мПечатьБезШтрихкодаРазрешена");
	СтруктураРеквизитовФормы.Вставить("мПериодичность");
	СтруктураРеквизитовФормы.Вставить("мРежимПечати");
	СтруктураРеквизитовФормы.Вставить("мСкопированаФорма");
	СтруктураРеквизитовФормы.Вставить("мСоставПоказателей");
	СтруктураРеквизитовФормы.Вставить("мСохраненныйДок");
	СтруктураРеквизитовФормы.Вставить("мСтрокаФормата");
	СтруктураРеквизитовФормы.Вставить("мСчетчикСтраниц");
	СтруктураРеквизитовФормы.Вставить("ФлажокОтклАвтоРасчет", Ложь);
	СтруктураРеквизитовФормы.Вставить("СтрПериодОтчета");
	СтруктураРеквизитовФормы.Вставить("НаимОрганизации");
	СтруктураРеквизитовФормы.Вставить("ВыводитьСтраницуВТаблДокНаСервере");
	СтруктураРеквизитовФормы.Вставить("СписокПечатаемыхЛистов");
	СтруктураРеквизитовФормы.Вставить("ТекущаяСтрокаРазделовОтчета");
	СтруктураРеквизитовФормы.Вставить("СправочникиВидыКонтактнойИнформацииАдресОрганизации");
	СтруктураРеквизитовФормы.Вставить("ЭтоЮрЛицо");
	СтруктураРеквизитовФормы.Вставить("мДатаНачалаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мДатаКонцаПериодаОтчета");
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтруктуруЗависимыхЯчеекДляАвторасчета()
	
	СтруктураЗависимыхЯчеекДляАвторасчета = Новый Структура;
	СтруктураЗависимыхЯчеекДляАвторасчета.Вставить("П000010000114", "П000010000114");
	СтруктураЗависимыхЯчеекДляАвторасчета.Вставить("П000010000115", "П000010000115");
	СтруктураЗависимыхЯчеекДляАвторасчета.Вставить("П000010000119", "П000010000119");
	СтруктураЗависимыхЯчеекДляАвторасчета.Вставить("П000010000120", "П000010000120");
			
КонецПроцедуры // ЗаполнитьСтруктуруЗависимыхЯчеекДляАвторасчета()

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	Перем ИсходноеКоличествоСтрокГруппы;
		
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	
	ЗаполнитьСтруктуруЗависимыхЯчеекДляАвторасчета();
	
	ФормироватьСтруктуруСтраницОтчета();

	ФормироватьСтруктуруСвойствСтраниц();
	
	СформироватьДеревоРазделовОтчетаНаСервере();
    		
	СтруктураРеквизитовФормы.ВидДокумента = 0;
	НомерКорректировки = 1;
	
	СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	СтруктураРеквизитовФормы.мДокументВосстановлен = Ложь;
    
	// для обновления листа о ФЛ, если Истина, то обновлять перезаписывать данные на листе
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
	НачальныеОперацииСМногострочнымиБлоками();

	Если НЕ БезОткрытияФормы Тогда
		
		СформироватьСпискиВыбора();
						
	КонецЕсли;

	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда

		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			// документ скопирован
			ВосстановитьСохраненныеДанные();
		Иначе
			
			// Это новый документ.
			// Установим реквизиты формы по умолчанию
			СтруктураРеквизитовФормы.ЕдиницаИзмерения         = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 2;

			КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);

			ОбновитьТЗСпецификаций();
	
			мОбновитьКомментарий = Истина;

		КонецЕсли;
		
		Модифицированность = Истина;

	Иначе
		
		// При открытии или при копировании сохраненного
		// отчета восстанавливаем сохраненные данные.
		ВосстановитьСохраненныеДанные();
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
			
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
			
			Модифицированность = Истина;
			
			ДатаПодписи	= Параметры.ДатаПодписи; 
			
			ТабличныйДокумент.Области.Число.Значение = ДатаПодписи;
			ТабличныйДокумент.Области.Месяц.Значение = ДатаПодписи;
			ТабличныйДокумент.Области.Год.Значение   = ДатаПодписи;
			
		КонецЕсли;
		
		Если НЕ БезОткрытияФормы
           И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
				   
        	ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);

        КонецЕсли;
		
	КонецЕсли;
	ПоказатьПериод();
	
	СтруктураРеквизитовФормы.ИП = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено
	 ИЛИ ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
		ИзмененаОрганизация = СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация;
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
		ЗаполнитьСведенияОбОрганизацииНаСервере();
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = ИзмененаОрганизация;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.НаимОрганизации = СтруктураРеквизитовФормы.Организация.Наименование;
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	ТабличныйДокумент.Области.Число.Значение = ДатаПодписи;
	ТабличныйДокумент.Области.Месяц.Значение = ДатаПодписи;
	ТабличныйДокумент.Области.Год.Значение = ДатаПодписи;
	Если СтруктураРеквизитовФормы.ВидДокумента = 0 Тогда 
		НомерКорректировки = 0;
	КонецЕсли;
	ТабличныйДокумент.Области.НомерКорректировки.Значение = Строка(НомерКорректировки);
	
	Если НЕ БезОткрытияФормы Тогда
		УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
	КонецЕсли;
	
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтаФорма);
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма);
	ИнициализацияОбласти();
	Если ТабличныйДокумент.Области.Найти("УдалитьСтрокуП000010000101_2") = Неопределено Тогда
		Обл = ТабличныйДокумент.Области.Найти("УдалитьСтрокуП000010000101_1");
		Если Обл <> Неопределено Тогда 
			Обл.Гиперссылка = Ложь;
			Обл.Текст = "";
		КонецЕсли;
	КонецЕсли;
	
	УстановитьФорматВывода(ЭтаФорма, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета  = ДатаПодписи;
	СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = ДатаПодписи;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьОбластейРаздела1(Форма)
	
	Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
		
		КодПричины = КодПричиныВозникновенияЗаявления(Форма.ТабличныйДокумент.Области.ПричинаЗаявления.Значение);
		
		ОбластьНомерОтметки     = Форма.ТабличныйДокумент.Области.НомерОтметки;
		ОбластьДатаОтметки      = Форма.ТабличныйДокумент.Области.ДатаОтметки;
		ОбластьИзмениеЦеныМесяц = Форма.ТабличныйДокумент.Области.ИзмениеЦеныМесяц;
		ОбластьИзмениеЦеныГод   = Форма.ТабличныйДокумент.Области.ИзмениеЦеныГод;
		
		Если КодПричины = "5" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОбластьНомерОтметки);
			РазрешитьВводДанныхВОбласть(Форма, ОбластьДатаОтметки);
			РазрешитьВводДанныхВОбласть(Форма, ОбластьИзмениеЦеныМесяц);
			РазрешитьВводДанныхВОбласть(Форма, ОбластьИзмениеЦеныГод);
		ИначеЕсли КодПричины = "4" ИЛИ КодПричины = "6" Тогда
			РазрешитьВводДанныхВОбласть(Форма, ОбластьНомерОтметки);
			РазрешитьВводДанныхВОбласть(Форма, ОбластьДатаОтметки);
			ЗапретитьВводДанныхВОбласть(Форма, ОбластьИзмениеЦеныМесяц);
			ЗапретитьВводДанныхВОбласть(Форма, ОбластьИзмениеЦеныГод);
		Иначе
			ЗапретитьВводДанныхВОбласть(Форма, ОбластьНомерОтметки);
			ЗапретитьВводДанныхВОбласть(Форма, ОбластьДатаОтметки);
			ЗапретитьВводДанныхВОбласть(Форма, ОбластьИзмениеЦеныМесяц);
			ЗапретитьВводДанныхВОбласть(Форма, ОбластьИзмениеЦеныГод);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КтоОтправляетПриИзменении(Ложь);
	
	Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
		Заголовок = Заголовок + " от " + СтруктураРеквизитовФормы.СтрПериодОтчета;
	КонецЕсли;
	
	ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
	Если СтрНайти(ЭтаФорма.Заголовок, ОргСтр) = 0 Тогда
		ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + ОргСтр;
	КонецЕсли;
	
	Заголовок = Заголовок + ?(СтрНайти(ЭтаФорма.ИмяФормы, "Внешний") > 0, " - внешний отчет", "");
	
	ПодключитьОбработчикОжидания("ВыполнитьПроверкуПередОткрытием", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуПередОткрытием()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОткрытиемФормыРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
				
		Возврат;
		
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
			
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДеревоНастройкиСтраниц(Знач мСохраненныйДок)
	
	Если мСохраненныйДок <> Неопределено Тогда
				
		Если ТипЗнч(мСохраненныйДок) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			мСохраненныйДок = мСохраненныйДок.ПолучитьОбъект();
		КонецЕсли;
		
		Если Не мСохраненныйДок.ЭтоНовый() Тогда
			// Сохраняем выбранные листы для печати в хранилище
			ХранилищеДанных = Новый ХранилищеЗначения(РеквизитФормыВЗначение("мДеревоСтраницОтчета"));
			мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
			Попытка
				мСохраненныйДок.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
Конецпроцедуры

&НаСервере
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	МакетСоставаПоказателей = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(СтруктураРеквизитовФормы.мСоставПоказателей);
	
	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл
				
		ТекОбласть    = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы   = ТекОбласть.Имя;
								
		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета
            					
			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// признак многострочности определяется по третьей колонке макета
			ПризнМногострочность = СокрЛП(МакетСоставаПоказателей.Область(Ном, 3).Текст);
			// по четвертой колонке определяется тип данных реквизита
			ТипДанныхРеквизита   = СокрЛП(МакетСоставаПоказателей.Область(Ном, 4).Текст);
			// по колонке 5 определяется исходное количество строк многострочного раздела
			стрКолСтрокИсходный  = СокрЛП(МакетСоставаПоказателей.Область(Ном, 5).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = РегламентированнаяОтчетность.ПолучитьВариантЗаполненияПоказателяРегОтчета(МакетСоставаПоказателей, Ном);
			// код показателя из структуры, берется из колонки 9
			КодПоказателяПоСтруктуре = СокрЛП(МакетСоставаПоказателей.Область(Ном, 9).Текст);

			чКолСтрокИсходный    = ? (ПустаяСтрока(стрКолСтрокИсходный),  0, Число(стрКолСтрокИсходный));
			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));

			// формируем таблицу значений, содержащей состав показателей отчета
			НоваяСтрока = мТаблицаСоставПоказателей.Добавить();
			НоваяСтрока.ИмяПоляТаблДокумента   = ИмяСтраницы;
			НоваяСтрока.КодПоказателяПоСоставу = КодПоказателя;
			НоваяСтрока.КодПоказателяПоФорме   = КодПоказателяПоФорме;
			НоваяСтрока.ПризнМногострочности   = ПризнМногострочность;
			НоваяСтрока.ТипДанныхПоказателя    = ТипДанныхРеквизита;
			НоваяСтрока.КодПоказателяПоСтруктуре = КодПоказателяпоСтруктуре;
			           
		КонецЦикла;
        
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСтраницОтчета() 
	
	мДеревоСтраницОтчета.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел1";
	СтрокаУровня1.Представление            = "Раздел 1";
	СтрокаУровня1.ОриентацияСтраницы       = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел2";
	СтрокаУровня1.Представление            = "Раздел 2";
	СтрокаУровня1.ОриентацияСтраницы       = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел3";
	СтрокаУровня1.Представление            = "Раздел 3";
	СтрокаУровня1.ОриентацияСтраницы       = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Приложение";
	СтрокаУровня1.Представление            = "Приложение";
	СтрокаУровня1.ОриентацияСтраницы       = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Отправители";
	СтрокаУровня1.Представление            = "Сведения" + Символы.ПС + "об отправителе" + Символы.ПС + "и подписантах";
	СтрокаУровня1.ОриентацияСтраницы       = "Портрет";
	СтрокаУровня1.ВыводНаПечать            = 0;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСвойствСтраниц() Экспорт
	
	мСвойстваРазделовДекларации.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                   = "Раздел1";
	СтрокаУровня1.СохранятьМногострКакТЗ        = Истина;
	СтрокаУровня1.МногостраничностьВРазделе     = Ложь;
	СтрокаУровня1.ОчищатьРаздел                 = Истина;
	СтрокаУровня1.ИмяПредставления              = "ПродавецНаимОрг";
	СтрокаУровня1.ПредставлениеДанных           = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки   = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым = Ложь;
	
	НовСтр = СтрокаУровня1.МногострочностьВРазделе.Добавить();
	НовСтр.ИдГруппы = СтруктураРеквизитовФормы.мИдГруппы1;
	НовСтр.Область = "МногострочнаяЧасть";
	НовСтр.ВысотаСтроки = 1;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                   = "Раздел2";
	СтрокаУровня1.СохранятьМногострКакТЗ        = Истина;
	СтрокаУровня1.МногостраничностьВРазделе     = Ложь;
	СтрокаУровня1.ОчищатьРаздел                 = Истина;
	СтрокаУровня1.ИмяПредставления              = "";
	СтрокаУровня1.ПредставлениеДанных           = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки   = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым = Ложь;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                   = "Раздел3";
	СтрокаУровня1.СохранятьМногострКакТЗ        = Истина;
	СтрокаУровня1.МногостраничностьВРазделе     = Ложь;
	СтрокаУровня1.ОчищатьРаздел                 = Истина;
	СтрокаУровня1.ИмяПредставления              = "";
	СтрокаУровня1.ПредставлениеДанных           = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки   = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым = Ложь;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                   = "Приложение";
	СтрокаУровня1.СохранятьМногострКакТЗ        = Истина;
	СтрокаУровня1.МногостраничностьВРазделе     = Истина;
	СтрокаУровня1.ОчищатьРаздел                 = Истина;
	СтрокаУровня1.ИмяПредставления              = "";
	СтрокаУровня1.ПредставлениеДанных           = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки   = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоРазделовОтчетаНаСервере() Экспорт
	
	СформироватьДеревоРазделовОтчета(ЭтаФорма);
			
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДеревоРазделовОтчетаНаКлиенте() Экспорт
	
	СформироватьДеревоРазделовОтчета(ЭтаФорма);
			
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьДеревоРазделовОтчета(Форма)
	
	Форма["РазделыОтчета"].ПолучитьЭлементы().Очистить();
	
	Для Каждого ЭлементДереваСтраницОтчета Из Форма["мДеревоСтраницОтчета"].ПолучитьЭлементы() Цикл
		
		Если ЭлементДереваСтраницОтчета.ПоказатьСтраницу = 1 Тогда
			
			ЭлементРазделовОтчета = Форма["РазделыОтчета"].ПолучитьЭлементы().Добавить();
			ЭлементРазделовОтчета.КолонкаРазделыОтчета         = ЭлементДереваСтраницОтчета.Представление;
			ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			ЭлементРазделовОтчета.ИндексКартинки               = 1;
			ЭлементРазделовОтчета.РазделМногостраничный        = Ложь;
			ЭлементРазделовОтчета.РазделМногострочный          = РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма, "мТаблицаМногострочныхРазделов");
			
			НайденноеЗначение = Неопределено;
			
			Если Форма["мСтруктураМногостраничныхРазделов"].Свойство(ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим, НайденноеЗначение) Тогда
				
				Если НЕ НайденноеЗначение = Неопределено Тогда
					
					НайденноеЗначение = Форма[НайденноеЗначение];
					
					ЭлементРазделовОтчета.ИндексКартинки        = 0;
					ЭлементРазделовОтчета.РазделМногостраничный = Истина;
					
					Для НомерСтраницы = 1 По НайденноеЗначение.Количество() Цикл
						
						СтраницаРазделаОтчета = ЭлементРазделовОтчета.ПолучитьЭлементы().Добавить();
						
						СтраницаРазделаОтчета.КолонкаРазделыОтчета              = "Стр. " + НомерСтраницы;
						СтраницаРазделаОтчета.КолонкаРазделыОтчетаСокрНаим      = ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим;
						СтраницаРазделаОтчета.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницы;
						СтраницаРазделаОтчета.ИндексКартинки                    = 1;
						СтраницаРазделаОтчета.РазделМногостраничный             = ЭлементРазделовОтчета.РазделМногостраничный;
						СтраницаРазделаОтчета.РазделМногострочный               = ЭлементРазделовОтчета.РазделМногострочный;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
				
		КонецЕсли;
				
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Процедура СформироватьСпискиВыбора()
	
	МакетСоставаПоказателей = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет("СпискиВыбора2015Кв1");
	
	КоллекцияСписковВыбора = Новый Соответствие;
	Для Каждого Область Из МакетСоставаПоказателей.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			ВерхОбласти = Область.Верх;
			НизОбласти = Область.Низ;
			ТаблСписка = Новый ТаблицаЗначений;
			ТаблСписка.Колонки.Добавить("Код", , , МакетСоставаПоказателей.Область(ВерхОбласти, 1, ВерхОбласти, 1).ШиринаКолонки);
			Если Область.Имя = "ЕдИзмПодАкцТоваров"
			 ИЛИ Область.Имя = "ЕдИзмНБ" Тогда
				ТаблСписка.Колонки.Добавить("Название", , "Название", МакетСоставаПоказателей.Область(ВерхОбласти, 2, ВерхОбласти, 2).ШиринаКолонки);
			Иначе
				ТаблСписка.Колонки.Добавить("Название", , "Название", МакетСоставаПоказателей.Область(ВерхОбласти, 3, ВерхОбласти, 3).ШиринаКолонки);
			КонецЕсли;
			Для НомСтр = ВерхОбласти По НизОбласти Цикл
				КодПоказателя = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
				Если КодПоказателя <> "###" Тогда
					НовСтрока = ТаблСписка.Добавить();
					НовСтрока.Код = КодПоказателя;
					Если Область.Имя = "ЕдИзмПодАкцТоваров"
					 ИЛИ Область.Имя = "ЕдИзмНБ" Тогда	
						НовСтрока.Название = СокрП(МакетСоставаПоказателей.Область(НомСтр, 2).Текст);
					Иначе
						НовСтрока.Название = СокрП(МакетСоставаПоказателей.Область(НомСтр, 3).Текст);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			КоллекцияСписковВыбора.Вставить(Область.Имя, ТаблСписка);
		КонецЕсли;
	КонецЦикла;
		
	СвойстваПоказателей.Очистить();
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000104", 1, , "Выбор единицы измерения подакцизного товара", КоллекцияСписковВыбора["ЕдИзмПодАкцТоваров"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000123", 1, , "Выбор единицы измерения дополнительной величины для исчисления налоговой базы", КоллекцияСписковВыбора["ЕдИзмНБ"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПрод_3", 1, , "Выбор типа продавца", КоллекцияСписковВыбора["ТипПродавцаРаздел3"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПок_3", 1, , "Выбор типа покупателя", КоллекцияСписковВыбора["ТипПокупателяРаздел3"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПрод_Пр", 1, , "Выбор типа продавца", КоллекцияСписковВыбора["ТипПродавцаПрил"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипПок_Пр", 1, , "Выбор типа покупателя", КоллекцияСписковВыбора["ТипПокупателяПрил"]);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеМногостраничныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;
	Перем Автозаполнение;
	Перем ТаблицаМногостраничногоДокумента;
	Перем СтрукТЗДокументы;

	// В случае, если формы была скопирована по F9, то проверим, не изменилась ли организация.
	Если СтруктураРеквизитовФормы.Организация <> СтруктураРеквизитовФормы.мСохраненныйДок.Организация
	   И СтруктураРеквизитовФормы.Организация <> РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации")
	   И СтруктураРеквизитовФормы.Организация <> Неопределено Тогда
		// Меняем переменную, для того, чтобы запустилась процедура ЗаполнитьСведенияОбОрганизации
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	Иначе
		// восстанавливаем реквизиты отчета
		СтруктураРеквизитовФормы.Организация = СтруктураРеквизитовФормы.мСохраненныйДок.Организация;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения;
	ДатаПодписи                                       = СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи;
	Комментарий                                       = СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий;
	
	// восстанавливаем сохраненные данные отчета
	СписокСохранения = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	
	// список листов, выводимых на печать
	ДеревоНастройкиСтраниц = СтруктураРеквизитовФормы.мСохраненныйДок.ДеревоНастройкиСтраниц.Получить();
	ЗначениеВРеквизитФормы(ДеревоНастройкиСтраниц.Скопировать(), "мДеревоСтраницОтчета");
	
	// восстанавливаем сохраненные данные многострочных разделов
	Если СписокСохранения.Свойство( "ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов) Тогда
		
		Для Каждого ЭлементСтруктуры Из ДанныеМногострочныхРазделов Цикл
			НайденноеЗначение = Неопределено;
			мСтруктураМногострочныхРазделов.Свойство(ЭлементСтруктуры.Ключ, НайденноеЗначение);
			Если НЕ НайденноеЗначение = Неопределено Тогда
				ЗначениеВДанныеФормы(ЭлементСтруктуры.Значение, ЭтаФорма[НайденноеЗначение]);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы       = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭтаФорма[ЭлементСтруктуры.Значение];
			// Если лист многостраничный, то всегда выведется текущий на момент сохранения блок многострочности
			// а в процедуре вывести активную страницу, выведутся оставшиеся данные.
			Если  СтруктураРеквизитовФормы.мИдГруппы1 = ИмяГруппы  Тогда
				ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппы, СтруктураГруппы);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
		
	// восстанавливаем сохраненные данные вариантов автозаполнения ячеек
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		
		Если НЕ ДанныеВариановАвтоЗаполнения = Неопределено Тогда
			
			мСтруктураВариантыЗаполнения.Очистить();
			
			Для Каждого ЭлементСтруктуры Из ДанныеВариановАвтоЗаполнения Цикл
				
				мСтруктураВариантыЗаполнения.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Ключ);
				
				ЭтаФорма["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Очистить();
				
				Для Каждого ЭлементМассива Из ЭлементСтруктуры.Значение Цикл
										
					ТаблВарЗаполнения = ЭтаФорма["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Добавить().ТаблицаВариантовЗаполнения;
										
					ЗначениеВДанныеФормы(ЭлементМассива, ТаблВарЗаполнения);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

	// восстановим сохраненные данные редактируемых ячеек
	СписокСохранения.Свойство("ПоказателиОтчета", ПоказателиОтчета);
	
	// восстанавливаем версию формы
	СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы);
	
	// восстанавливаем сохраненные данные многостраничных разделов
	Если СписокСохранения.Свойство("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов) Тогда
		
		Для Каждого ЭлементСтруктуры Из ДанныеМногостраничныхРазделов Цикл
					
			НайденноеЗначение = Неопределено;
			
			мСтруктураМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, НайденноеЗначение);
			
			Если НЕ НайденноеЗначение = Неопределено Тогда
				
				НайденноеЗначение = ЭтаФорма[НайденноеЗначение];
				
				ЗначениеВДанныеФормы(ЭлементСтруктуры.Значение, НайденноеЗначение);
				                     				
				Если НайденноеЗначение[0].Свойство("Данные") Тогда
					
					Для Каждого СтрокаТаблЗнач Из НайденноеЗначение Цикл
						
						Если ТипЗнч(СтрокаТаблЗнач.Данные) = Тип("СписокЗначений") Тогда
							
							СтрокаТаблЗнач.Данные.Очистить();
							СтрокаТаблЗнач.Данные.Добавить(ЭлементСтруктуры.Значение[НайденноеЗначение.Индекс(СтрокаТаблЗнач)].Данные);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				Если НайденноеЗначение[0].Свойство("ДополнительныеДанные") Тогда
					
					Для Каждого СтрокаТаблЗнач Из НайденноеЗначение Цикл
						
						Если ТипЗнч(СтрокаТаблЗнач.ДополнительныеДанные) = Тип("СписокЗначений") Тогда
							
							СтрокаТаблЗнач.ДополнительныеДанные.Очистить();
							СтрокаТаблЗнач.ДополнительныеДанные.Добавить(ЭлементСтруктуры.Значение[НайденноеЗначение.Индекс(СтрокаТаблЗнач)].ДополнительныеДанные);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
								
				Если НайденноеЗначение[0].Свойство("ДанныеДопСтрок") Тогда
					
					Ном = 0;
					
					Для Каждого СтрокаТаблЗнач Из ЭлементСтруктуры.Значение Цикл
						
						Если ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("Структура") Тогда
							
							Для Каждого ЭлСтруктуры Из СтрокаТаблЗнач.ДанныеДопСтрок Цикл
								
								ТаблЗнач = ЭлСтруктуры.Значение;
								
								ЭлСтруктуры.Значение = ЭтаФорма["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
								
								ЗначениеВДанныеФормы(ТаблЗнач, ЭлСтруктуры.Значение);
								
								НайденноеЗначение[Ном].ДанныеДопСтрок.Добавить(ЭлСтруктуры.Значение);
								
							КонецЦикла;
							
						ИначеЕсли ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("ТаблицаЗначений") Тогда
							
							СтрокаТаблЗначДанныеДопСтрок = ЭтаФорма["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
														
							ЗначениеВДанныеФормы(СтрокаТаблЗнач.ДанныеДопСтрок, СтрокаТаблЗначДанныеДопСтрок);
							
							НайденноеЗначение[Ном].ДанныеДопСтрок.Добавить(СтрокаТаблЗначДанныеДопСтрок, "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела");
							
						КонецЕсли;
						
						Ном = Ном + 1;
						
					КонецЦикла;
					
				КонецЕсли;
								   				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		Для Каждого Страница Из ЭтаФорма[Раздел.Значение] Цикл
			Страница.АктивнаяСтраница = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	// Восстанавливаем структуру, хранящую таблицы значений с данными по товаро-транспортным документам.
	СписокСохранения.Свойство("мСтрукТЗДокументы", СтрукТЗДокументы);
	Если СтрукТЗДокументы = Неопределено Тогда
		мСтрукТЗДокументы = Новый Структура;
	Иначе
		Для Каждого ЭлементСтруктуры Из СтрукТЗДокументы Цикл
			ТранспДокументы = Новый Массив;
			Для Каждого Док Из ЭлементСтруктуры.Значение Цикл
				ТранспДокументы.Добавить(Новый Структура("Серия, Номер, Дата", Док.Серия, Док.Номер, Док.Дата));
			КонецЦикла;
			мСтрукТЗДокументы.Вставить(ЭлементСтруктуры.Ключ, ТранспДокументы);
		КонецЦикла;
	КонецЕсли;
	
	СписокСохранения.Свойство("ФлПодсчетСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	СписокСохранения.Свойство("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	Если СтруктураРеквизитовФормы.мАвтоВыборКодов = Неопределено Тогда
		СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	КонецЕсли;

	// восстанавливаем  ТЗ спецификаций Раздел1;
	ТЗСпецификацийРаздел1 = Неопределено;
	СписокСохранения.Свойство("мТЗСпецификацийРаздел1", ТЗСпецификацийРаздел1);
	ЗначениеВДанныеФормы(ТЗСпецификацийРаздел1, мТЗСпецификацийРаздел1);
																				
	// восстанавливаем ТЗ спецификаций Раздел1 стр 07;
	ТЗСпецификацийРаздел1_07 = Неопределено;
	СписокСохранения.Свойство("мТЗСпецификацийРаздел1_07", ТЗСпецификацийРаздел1_07);
	ЗначениеВДанныеФормы(ТЗСпецификацийРаздел1_07, мТЗСпецификацийРаздел1_07);
		
	// восстанавливаем ТЗ спецификаций Раздел3;
	ТЗСпецификацийРаздел3 = Неопределено;
	СписокСохранения.Свойство("мТЗСпецификацийРаздел3", ТЗСпецификацийРаздел3);
	ЗначениеВДанныеФормы(ТЗСпецификацийРаздел3, мТЗСпецификацийРаздел3);
	
	// восстанавливаем  массив ТЗ спецификаций Приложения;
	МассивТЗСпецификацийПрил = Неопределено;
	СписокСохранения.Свойство("мМассивТЗСпецификацийПрил", МассивТЗСпецификацийПрил);
		
	Для Каждого ЭлементМассива Из МассивТЗСпецификацийПрил Цикл
		
		ЗначениеВДанныеФормы(ЭлементМассива, мМассивТЗСпецификацийПрил.Добавить().Таблица);
		
	КонецЦикла;
		
	СписокСохранения.Свойство("Автозаполнение", Автозаполнение);
	СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Автозаполнение;
	СнятьЗащитуСЯчеек(ЭтаФорма, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	
	// Восстанавливаем структуру, хранящую данные реквизитов выгрузки.
	СписокСохранения.Свойство("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки);
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
	Иначе
		// В предыдущих редакция формы эти реквизиты могут отсутствовать.
		Если НЕ мСтруктураРеквизитовВыгрузки.Свойство("НаимДовПодп2") Тогда
			мСтруктураРеквизитовВыгрузки.Вставить("НаимДовПодп2", "");
		КонецЕсли;
		Если НЕ мСтруктураРеквизитовВыгрузки.Свойство("НомерДовПодп2") Тогда
			мСтруктураРеквизитовВыгрузки.Вставить("НомерДовПодп2", "");
		КонецЕсли;
		Если НЕ мСтруктураРеквизитовВыгрузки.Свойство("ДатаДовПодп2") Тогда
			мСтруктураРеквизитовВыгрузки.Вставить("ДатаДовПодп2", "");
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ СписокСохранения.Свойство("ПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена) Тогда
		СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена = Ложь;
	КонецЕсли;
	
	СписокСохранения.Свойство("ДополнительныеДанныеРазделов", ДополнительныеДанныеРазделов);

	Префикс = "ПолеТабличногоДокумента";
	
	Для Каждого Раздел Из мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл	
		
		Если НЕ Раздел.МногостраничностьВРазделе Тогда
			ЭтаФорма["СтруктураДанных" + Раздел.ИмяСтраницы] = ПоказателиОтчета[Префикс + Раздел.ИмяСтраницы];
		КонецЕсли;
	
	КонецЦикла;

	ИмяТекРаздела = "Раздел1";
	
	Для Каждого ЭлСтруктуры Из ЭтаФорма["СтруктураДанных" + ИмяТекРаздела] Цикл
		Попытка
			ТабличныйДокумент.Области[ЭлСтруктуры.Ключ].Значение = ЭлСтруктуры.Значение;
		Исключение
		КонецПопытки;
	КонецЦикла;

	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтаФорма);

	КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
	
	Модифицированность   = Ложь;
	СтруктураРеквизитовФормы.мДокументВосстановлен = Истина;
		
	СформироватьДеревоРазделовОтчетаНаСервере();

КонецПроцедуры

&НаСервере
Функция ЗначениеСодержитсяВСписке(ПроверяемоеЗначение, КонтрольныйСписок)
	
	Результат = Ложь;
	
	Значения = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КонтрольныйСписок, ",");
	Для Инд = 0 По Значения.ВГраница() Цикл
		Значения[Инд] = СокрЛП(Значения[Инд]);
	КонецЦикла;
	
	Результат = (Значения.Найти(ПроверяемоеЗначение) <> Неопределено);
	
	Возврат Результат
	
КонецФункции

&НаСервере
Процедура ПоказатьПериод()
	
	СтруктураРеквизитовФормы.СтрПериодОтчета = ПредставлениеПериода(НачалоДня(ДатаПодписи), КонецДня(ДатаПодписи), "ФП = Истина" );
    	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизацииНаСервере(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	ЗаполнитьСведенияОбОрганизации(ЭтаФорма, ВПрограммеИзмененаДатаПодписи);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСведенияОбОрганизации(Форма, ВПрограммеИзмененаДатаПодписи = Ложь)

	Если (Форма.СтруктураРеквизитовФормы.Организация = Неопределено) Или (Форма.СтруктураРеквизитовФормы.Организация = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат
	КонецЕсли;
	
	// Составляем список данных, необходимых для вывода в декларацию
	Сведения = "";//Новый СписокЗначений;
	
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда
		Сведения = "ИННЮЛ, КППЮЛ, НаимЮЛПол, ";
		Сведения = Сведения + ?(Форма.СтруктураРеквизитовФормы.ИП, "АдрМЖ", "АдрЮР");
		Сведения = Сведения +", ";
	КонецЕсли;
	Сведения = Сведения + "КодНО, ФИОРук, ФИОБух, ФИО, Гражд, ГраждСтрана";

	// Теперь получаем данные из глобальной общей функции
	ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Форма.СтруктураРеквизитовФормы.Организация, Форма.ДатаПодписи, Сведения);

	// Инициализируем переменные
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда
		ИННЮЛ = "";
		КППЮЛ = "";
		НаимЮЛПол = "";
		Адр       = "";
	КонецЕсли;

	ФИОРук = "";
	ФИОБух = "";
	ФИО = "";
	//сКодНО = "":

	// Получаем данные
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда

		//ОргСведения.Свойство("ИННЮЛ",      ИННЮЛ    );
		ИННЮЛ = ОргСведения.ИННЮЛ;
		//ОргСведения.Свойство("КППЮЛ",      КППЮЛ    );
		КППЮЛ = ОргСведения.КППЮЛ;
		ОргСведения.Свойство("НаимЮЛПол",  НаимЮЛПол);
		НаимЮЛПол = ОргСведения.НаимЮЛПол;
		Если Форма.СтруктураРеквизитовФормы.ИП Тогда
			 Адр = ОргСведения.АдрМЖ;
		Иначе
			 Адр = ОргСведения.АдрЮР;
		КонецЕсли;

	КонецЕсли;

	ФИОРук = ОргСведения.ФИОРук;
	ОргСведения.Свойство("ФИОБух",     ФИОБух   );
	ФИОБух = ОргСведения.ФИОБух;
	ФИО = ОргСведения.ФИО;
	Если Форма.СтруктураРеквизитовФормы.ИП Тогда
		НаимЮЛПол = ФИО;
		ФИОРук = ФИО;
		ФИОБух = "";
	КонецЕсли;

	Если ВПрограммеИзмененаДатаПодписи Тогда

		// Обновим только то, что зависит от даты подписи и выйдем
		Форма.ТабличныйДокумент.Области.ФИОРук.Значение  = ФИОРук;
		Если Форма.СтруктураРеквизитовФормы.ИП Тогда
			Форма.ТабличныйДокумент.Области.ПокупательНаимОрг.Значение = ФИО;
		КонецЕсли;

		Возврат;
	КонецЕсли;

	Форма.СтруктураРеквизитовФормы.мИННЮЛ = ИННЮЛ;
	Форма.СтруктураРеквизитовФормы.КодНО = ОргСведения.КодНО;
	КППЮЛ = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(КППЮЛ, 9, " ");

	Форма.СтруктураРеквизитовФормы.мКППЮЛ = КППЮЛ;

	Если (Форма.СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(Форма.ТабличныйДокумент.Области.ПокупательМестоНахожд.Значение)) Тогда
		
		// Адрес организации в представлении КЛАДР.
		СтруктураАдреса = Неопределено;
		Если НЕ ПустаяСтрока(Адр) Тогда
			СтруктураАдреса = РегламентированнаяОтчетностьКлиентСервер.РазложитьАдрес(Адр);
		КонецЕсли;
		
		АдресОрганизации = Новый Структура;
		АдресОрганизации.Вставить("Страна",          "");
		АдресОрганизации.Вставить("Индекс",          "");
		АдресОрганизации.Вставить("Регион",          "");
		АдресОрганизации.Вставить("Район",           "");
		АдресОрганизации.Вставить("Город",           "");
		АдресОрганизации.Вставить("НаселенныйПункт", "");
		АдресОрганизации.Вставить("Улица",           "");
		АдресОрганизации.Вставить("Дом",             "");
		АдресОрганизации.Вставить("Корпус",          "");
		АдресОрганизации.Вставить("Квартира",        "");
		
		Если Не СтруктураАдреса = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(АдресОрганизации, СтруктураАдреса);
		КонецЕсли;
		
		Если ТипЗнч(Форма.ДополнительныеДанныеРазделов) <> Тип("Структура") Тогда
			Форма.ДополнительныеДанныеРазделов = Новый Структура;
		КонецЕсли;
		Форма.ДополнительныеДанныеРазделов.Вставить("АдресПокупателяРаздел1", АдресОрганизации);

		Адр = СтрЗаменить(Адр, ",,,,,,,,,", "");
		Форма.ТабличныйДокумент.Области.ПокупательМестоНахожд.Значение = Адр;

		Если НЕ ЗначениеЗаполнено(СтрЗаменить(Адр, ",","")) Тогда
			ОбработанныйАдр = "";
			Форма.ТабличныйДокумент.Области.ПокупательМестоНахожд.Значение = ОбработанныйАдр;
		Иначе
			ОбработанныйАдр = РегламентированнаяОтчетностьКлиентСервер.ПредставлениеАдресаВФормате9Запятых(Адр);
			Форма.ТабличныйДокумент.Области.ПокупательМестоНахожд.Значение = ОбработанныйАдр;
		КонецЕсли;
		
		КодСтраны = ?(ПустаяСтрока(Форма.ТабличныйДокумент.Области.ПокупательБайконур.Значение), "643", "398");
		Форма.ТабличныйДокумент.Области.КодСтраныПокупатель.Значение = КодСтраны;

	КонецЕсли;

	Если (Форма.СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(Форма.ТабличныйДокумент.Области.ПокупательИНН_КПП.Значение)) Тогда
		Форма.ТабличныйДокумент.Области.ПокупательИНН_КПП.Значение = Форма.СтруктураРеквизитовФормы.мИННЮЛ;
	КонецЕсли;

	// Работаем с названием ЮЛ
	Если (Форма.СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(Форма.ТабличныйДокумент.Области.ПокупательНаимОрг.Значение)) Тогда
		// Если пустая строка на Титульном листе, то запишем имеющееся у нас значение
		Форма.ТабличныйДокумент.Области.ПокупательНаимОрг.Значение = НаимЮЛПол;
	КонецЕсли;

	// Работаем с ФИО руководителя	
	Если (Форма.СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация) Или (ПустаяСтрока(Форма.ТабличныйДокумент.Области.ФИОРук.Значение)) Тогда
		
		СведенияОПредставителе = Форма.ПолучитьСведенияОПредставителе(ОргСведения.КодНО);
		Если СведенияОПредставителе.ТипПредставителя = "1" тогда
			СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Форма.СтруктураРеквизитовФормы.Организация, Форма.ДатаПодписи, "ФИОРук, ФИО");
			ФИОПодписанта = СокрЛП(?(ЗначениеЗаполнено(СведенияОбОрганизации.ФИОРук), СведенияОбОрганизации.ФИОРук, СведенияОбОрганизации.ФИО));
		Иначе
			ФИОПодписанта = СведенияОПредставителе.ФИОПредставителя;
		КонецЕсли;
		
		Форма.ТабличныйДокумент.Области.ФИОРук.Значение = ФИОПодписанта;
		
	КонецЕсли;

	// Даты проставятся всегда при открытии документа или при изменении организации
	Если (НЕ Форма.СтруктураРеквизитовФормы.мДокументВосстановлен) ИЛИ (Форма.СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация) Тогда
	// Заполним даты формирования декларации (подписи)
		Модифицированность = Истина;
		ЗаполнитьДатуВЯчейках(Форма);
	КонецЕсли;
	
	Если Форма.ТабличныйДокумент.Области.КодСтраныПокупатель.Значение="" Тогда
		 Форма.ТабличныйДокумент.Области.КодСтраныПокупатель.Значение = "643";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветФонаЯчейкиНаКлиенте(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	
	РегламентированнаяОтчетностьКлиентСервер.УстановитьЦветФонаЯчейки(ЭтаФорма, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЦветФонаЯчейкиНаСервере(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	
	РегламентированнаяОтчетностьКлиентСервер.УстановитьЦветФонаЯчейки(ЭтаФорма, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьПеременныеМодуляНаСервере(ИмяПоказателя, ЗначениеПоказателя) Экспорт
    	
	ПеременныеМодуля = Новый Структура;
	
	флОбновитьПеременныеМодуля = ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля);
	
	Возврат флОбновитьПеременныеМодуля;
	
КонецФункции

&НаКлиенте
Функция ОбновитьПеременныеМодуляНаКлиенте(ИмяПоказателя, ЗначениеПоказателя) Экспорт

	ПеременныеМодуля = Новый Структура;
	
	флОбновитьПеременныеМодуля = ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля);
	
	Возврат флОбновитьПеременныеМодуля;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля)

	// Процедура "заглушка", для других механизмов.
	ИзмененияПроизошли = Ложь;

	Возврат ИзмененияПроизошли;

КонецФункции

&НаКлиенте
Процедура СохранитьОтчет(Команда)
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение=Неопределено) Экспорт
	
	Вариант = СтруктураРеквизитовФормы.ВидДокумента * НомерКорректировки;
	
	Если ЗначениеЗаполнено(мСтруктураРеквизитовВыгрузки.КодИФНС) Тогда
		КодИФНС = мСтруктураРеквизитовВыгрузки.КодИФНС;
	Иначе
		КодИФНС = "не применимо";
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПриЗаписиРегламентированногоОтчетаНаКлиенте(ЭтаФорма, КодИФНС, Автосохранение, Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		НаименованиеТекущегоРаздела = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	Иначе
		НаименованиеТекущегоРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	КонецЕсли;
	
	РезультатСохранения = ПередСохранением(КодИФНС, ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаименованиеТекущегоРаздела);
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением = (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	Если НуженВопросПередСохранением Тогда
		
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС);
		
	Иначе
		
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС)
	
	Если ПодобныйОтчетСуществует Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчет с видом %1 уже существует.
			|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС", Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, КодИФНС", Автосохранение, Вариант, ВыполняемоеОповещение, КодИФНС);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.Открыть();
	Иначе
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
		РезультатСохранения = Сохранить(Автосохранение, КодИФНС, Вариант, Ложь, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
	Иначе
		Возврат;
	КонецЕсли;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
	РезультатСохранения = Сохранить(Автосохранение, , Вариант, ВидДокументаНомерКорректировкиИзменен, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения)
	
	КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если РезультатСохранения Тогда
		
		РегламентированнаяОтчетностьКлиент.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПередСохранением(КодИФНС = "не применимо", ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаимТекРаздела)
	
	Если РегламентированнаяОтчетность.БылиИзмененыКлючевыеРеквизитыОтчета(ЭтаФорма, КодИФНС)
	   И РегламентированнаяОтчетность.СуществуетДокументСАналогичнымиРеквизитами(ЭтаФорма, КодИФНС) Тогда
		
		ПодобныйОтчетСуществует = Истина;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мЗаписываетсяНовыйДокумент = (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	Если Вариант <> Неопределено И ((СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) ИЛИ (СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено) ИЛИ (Вариант <> СтруктураРеквизитовФормы.мВариант)) Тогда
		
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
						
		ИначеЕсли Вариант <> СтруктураРеквизитовФормы.мВариант Тогда
			
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Истина;
								
		КонецЕсли;

	КонецЕсли;
	
	Если ПодобныйОтчетСуществует
	 ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
	 
	 	Возврат Ложь;
		
 	КонецЕсли;
		
	Возврат Сохранить(Автосохранение, КодИФНС, Вариант, , НаимТекРаздела);
		
КонецФункции

&НаСервере
Функция Сохранить(Автосохранение = Ложь, КодИФНС = "не применимо", Вариант, ВидДокументаНомерКорректировкиИзменен = Неопределено, НаимТекРаздела) Экспорт
	
	ДанныеДляРазблокирования = Неопределено;
	
	Если НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
			ДанныеДляРазблокирования = Новый Структура("Ключ,ИдФормы",
			СтруктураРеквизитовФормы.мСохраненныйДок, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчетаНаСервере(ЭтаФорма, КодИФНС, Автосохранение, Вариант, ВидДокументаНомерКорректировкиИзменен, СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецЕсли;
	
	// установим текущие значения реквизитов документа
	
	Если СтруктураРеквизитовФормы.КодНО = "" ИЛИ СтруктураРеквизитовФормы.КодНО = Неопределено Тогда
		Если ЗначениеЗаполнено(мСтруктураРеквизитовВыгрузки.КодИФНС) Тогда
			СтруктураРеквизитовФормы.КодНО = мСтруктураРеквизитовВыгрузки.КодИФНС;
		Иначе
			СтруктураРеквизитовФормы.КодНО = ПолучитьКодИМНС();
		КонецЕсли;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мСохраненныйДок.ИсточникОтчета = Метаданные.Отчеты[ИмяОтчета(ЭтаФорма.ИмяФормы)].Имя;
	СтруктураРеквизитовФормы.мСохраненныйДок.НаименованиеОтчета = Метаданные.Отчеты[ИмяОтчета(ЭтаФорма.ИмяФормы)].ОсновнаяФорма.Синоним;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаНачала               = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания            = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий              = Комментарий;
	СтруктураРеквизитовФормы.мСохраненныйДок.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВыбраннаяФорма           = СтруктураРеквизитовФормы.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок.Организация              = СтруктураРеквизитовФормы.Организация;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи              = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	СтруктураРеквизитовФормы.мСохраненныйДок.КодНалоговогоОргана      = СтруктураРеквизитовФормы.КодНО;
	СтруктураРеквизитовФормы.мСохраненныйДок.Вид                      = Вариант;
	СтруктураРеквизитовФормы.мСохраненныйДок.ПредставлениеВида        = РегламентированнаяОтчетность.ПредставлениеВидаДокумента(СтруктураРеквизитовФормы.мСохраненныйДок.Вид);
	СтруктураРеквизитовФормы.мВариант                                 = Вариант;
    
	СтруктураРеквизитовФормы.мСохраненныйДок.ФорматВыгрузки = "Автоматически";
		
	СохранитьДанныеРаздела(НаимТекРаздела, Ложь, Ложь);
	
	ЗаполнитьПустыеСтруктурыДанных();
		
	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();
	
	ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	
	// формируем список сохранения
	СписокСохранения = Новый Структура();
    	
	// вставляем данные редактируемых ячеек таблицы	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	ДанныеМногострочныхРазделов = Новый Структура;
	
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		
		ДанныеМногострочныхРазделов.Вставить(ЭлементСтруктуры.Ключ, ДанныеФормыВЗначение(ЭтаФорма[ЭлементСтруктуры.Значение], Тип("ТаблицаЗначений")));
		
	КонецЦикла;
	
	// вставляем данные многострочных разделов
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов);
	
	ДанныеМногостраничныхРазделов = Новый Структура;
	
	МассивТиповНеопределено = Новый Массив; 
	МассивТиповНеопределено.Добавить(Тип("Неопределено"));
	ОписаниеТиповНеопределено = Новый ОписаниеТипов(МассивТиповНеопределено);
	
	Для Каждого ЭлементСтруктуры Из мСтруктураМногостраничныхРазделов Цикл
		
		ДанныеМногостраничныхРазделов.Вставить(ЭлементСтруктуры.Ключ, ДанныеФормыВЗначение(ЭтаФорма[ЭлементСтруктуры.Значение], Тип("ТаблицаЗначений")));
		
		ТаблЗнач = Неопределено;
		
		Если ДанныеМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, ТаблЗнач) Тогда
						
			Если НЕ ТаблЗнач.Колонки.Найти("Данные") = Неопределено Тогда
												
				ТаблЗнач.Колонки.Удалить("Данные");
				ТаблЗнач.Колонки.Добавить("Данные", ОписаниеТиповНеопределено);
				
				Ном = 0;
				
				Для Каждого СтрокаТаблЗнач Из ТаблЗнач Цикл
					
					СтрокаТаблЗнач.Данные = ЭтаФорма[ЭлементСтруктуры.Значение][Ном].Данные[0].Значение;
					
					Ном = Ном + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если НЕ ТаблЗнач.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
												
				ТаблЗнач.Колонки.Удалить("ДополнительныеДанные");
				ТаблЗнач.Колонки.Добавить("ДополнительныеДанные", ОписаниеТиповНеопределено);
				
				Ном = 0;
				
				Для Каждого СтрокаТаблЗнач Из ТаблЗнач Цикл
					
					СтрокаТаблЗнач.ДополнительныеДанные = ЭтаФорма[ЭлементСтруктуры.Значение][Ном].ДополнительныеДанные[0].Значение;
					
					Ном = Ном + 1;
					
				КонецЦикла;
				
			КонецЕсли;
						
			Если НЕ ТаблЗнач.Колонки.Найти("ДанныеДопСтрок") = Неопределено Тогда
															
				ТаблЗнач.Колонки.Удалить("ДанныеДопСтрок");
				ТаблЗнач.Колонки.Добавить("ДанныеДопСтрок", ОписаниеТиповНеопределено);
				
				Ном = 0;
				
				Для Каждого СтрокаТаблЗнач Из ТаблЗнач Цикл
						
					Если (ЭтаФорма[ЭлементСтруктуры.Значение][Ном].ДанныеДопСтрок.Количество() > 0)
					   И (ЭтаФорма[ЭлементСтруктуры.Значение][Ном].ДанныеДопСтрок[0].Представление = "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела") Тогда
																			
						СтрокаТаблЗнач.ДанныеДопСтрок = ДанныеФормыВЗначение(ЭтаФорма[ЭлементСтруктуры.Значение][Ном].ДанныеДопСтрок[0].Значение, Тип("ТаблицаЗначений"));
						
						Ном = Ном + 1;
						
						Продолжить;
							
					КонецЕсли;
					
					СтруктураДанныхДопСтрок = Новый Структура;
					
					Для Каждого ЭлСтруктуры Из ЭтаФорма[ЭлементСтруктуры.Значение][Ном].ДанныеДопСтрок Цикл
														
						СтруктураДанныхДопСтрок.Вставить(ДанныеФормыВЗначение(ЭлСтруктуры.Значение, Тип("ТаблицаЗначений")));	
							
					КонецЦикла;
					
					СтрокаТаблЗнач.ДанныеДопСтрок = СтруктураДанныхДопСтрок;
			
					Ном = Ном + 1;
								
				КонецЦикла;
				
			КонецЕсли;
						
		КонецЕсли;
				
	КонецЦикла;
		
	// вставляем данные многостраничных разделов
	СписокСохранения.Вставить("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов);
	
	ДанныеВариантовАвтоЗаполнения = Новый Структура;
	
	Для Каждого ЭлементСтруктуры Из мСтруктураВариантыЗаполнения Цикл
		
		МассивТаблЗнач = Новый Массив;
		
		ДанныеВариантовАвтоЗаполнения.Вставить(ЭлементСтруктуры.Ключ, МассивТаблЗнач);
			
		Для Каждого ЭлементМассива Из ЭтаФорма["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ] Цикл
			
			МассивТаблЗнач.Добавить(ДанныеФормыВЗначение(ЭлементМассива.ТаблицаВариантовЗаполнения, Тип("ТаблицаЗначений")));
			
		КонецЦикла;
			
	КонецЦикла;
	
	// вставляем данные вариантов автозаполнения ячеек
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", ДанныеВариантовАвтоЗаполнения);
	
	// вствляем версию формы
	СписокСохранения.Вставить("ВерсияФормы", СтруктураРеквизитовФормы.мВерсияФормы);
	
	// вствляем флажок пересчета страниц
	СписокСохранения.Вставить("ФлПодсчетСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	
	// вствляем флажок отключения
	СписокСохранения.Вставить("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов);

	// вствляем флажок ручной ввод ВРЕМЕННАЯ ОТМЕНА
	СписокСохранения.Вставить("Автозаполнение", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);

	СтрукТЗДокументы = Новый Структура;
		
	Для Каждого ЭлементСтруктуры Из мСтрукТЗДокументы Цикл
		
		ТЗДокументы = Новый ТаблицаЗначений;
		
		ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
		ОписаниеТиповДата   = Новый ОписаниеТипов("Дата", , Новый КвалификаторыДаты(ЧастиДаты.Дата));
		
		ТЗДокументы.Колонки.Добавить("Серия", ОписаниеТиповСтрока, "Серия");
		ТЗДокументы.Колонки.Добавить("Номер", ОписаниеТиповСтрока, "Номер");
		ТЗДокументы.Колонки.Добавить("Дата",  ОписаниеТиповДата,   "Дата");
		
		Для Каждого Док Из ЭлементСтруктуры.Значение Цикл
						
			НовДок = ТЗДокументы.Добавить();
			НовДок.Дата  = Док.Дата;
			НовДок.Номер = Док.Номер;
			НовДок.Серия = Док.Серия;
			
		КонецЦикла;
		
		СтрукТЗДокументы.Вставить(ЭлементСтруктуры.Ключ, ТЗДокументы);
				
	КонецЦикла;
		
	// вставляем структуру, хранящую таблицы значений с данными по товаро-транспортным документам
	СписокСохранения.Вставить("мСтрукТЗДокументы", СтрукТЗДокументы);
	
	// вставляем структуру, хранящую данные об отправителе, подписантах
	СохранитьДанныеОбласти();
	СписокСохранения.Вставить("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки);
	
	// вставляем спецификации раздел1
	СписокСохранения.Вставить("мТЗСпецификацийРаздел1", ДанныеФормыВЗначение(мТЗСпецификацийРаздел1, Тип("ТаблицаЗначений"))); 
	
	// вставляем спецификации раздел1
	СписокСохранения.Вставить("мТЗСпецификацийРаздел1_07", ДанныеФормыВЗначение(мТЗСпецификацийРаздел1_07, Тип("ТаблицаЗначений"))); 

	// вставляем спецификации раздел3
	СписокСохранения.Вставить("мТЗСпецификацийРаздел3", ДанныеФормыВЗначение(мТЗСпецификацийРаздел3, Тип("ТаблицаЗначений"))); 
	
	МассивТЗСпецификацийПрил = Новый Массив;
	
	Для Каждого ЭлементТаблицы Из мМассивТЗСпецификацийПрил Цикл
		
		МассивТЗСпецификацийПрил.Добавить(ДанныеФормыВЗначение(ЭлементТаблицы.Таблица, Тип("ТаблицаЗначений")));
		
	КонецЦикла;
		
	// вставляем массив спецификации Приложения
	СписокСохранения.Вставить("мМассивТЗСпецификацийПрил", МассивТЗСпецификацийПрил);
	
	// вствляем флажок разрешения печати без штрихкода
	СписокСохранения.Вставить("ПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена);
	
	СписокСохранения.Вставить("ДополнительныеДанныеРазделов", ДополнительныеДанныеРазделов);

	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;

	// записываем документ, хранящий данные отчета	
	Попытка
		СтруктураРеквизитовФормы.мСохраненныйДок.Записать();
		РегламентированнаяОтчетность.СохранитьСтатусОтправкиРеглОтчета(ЭтаФорма);
	Исключение
		Если НЕ Автосохранение Тогда
			
			Сообщение = Новый СообщениеПользователю;

			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), РегламентированнаяОтчетностьКлиентСервер.СформироватьТекстСообщения(ОписаниеОшибки()));

			Сообщение.Сообщить();
			
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;

		Возврат Ложь;
	КонецПопытки;
			
	Модифицированность   = Ложь;
	
	СохранитьДеревоНастройкиСтраниц(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
	
	Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы
       И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
				   
    	ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);

    КонецЕсли;
	
	Если ДанныеДляРазблокирования <> Неопределено Тогда
		РазблокироватьДанныеДляРедактирования(ДанныеДляРазблокирования.Ключ, ДанныеДляРазблокирования.ИдФормы);
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

&НаСервере
Процедура ЗаполнитьПустыеСтруктурыДанных()
	
	СохрТабличныйДокумент = Новый ТабличныйДокумент;
	
	СохрТабличныйДокумент.Вывести(ТабличныйДокумент);
	
	Для Каждого Раздел Из мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл
		
		Если НЕ Раздел.МногостраничностьВРазделе
		  И (ЭтаФорма["СтруктураДанных" + Раздел.ИмяСтраницы].Количество() = 0 ИЛИ ЭтаФорма["СтруктураДанных" + Раздел.ИмяСтраницы].Количество() = 2) Тогда
		  
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, "ФормаОтчета")) + "_" + Раздел.ИмяСтраницы));
			
			СохранитьДанныеРаздела(Раздел.ИмяСтраницы, Ложь);
			
		ИначеЕсли Раздел.МногостраничностьВРазделе
			    И (ЭтаФорма[мСтруктураМногостраничныхРазделов[Раздел.ИмяСтраницы]][0].Данные.Количество() = 0) Тогда
				
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, "ФормаОтчета")) + "_" + Раздел.ИмяСтраницы));
			СохранитьДанныеРаздела(Раздел.ИмяСтраницы, Ложь);	
			
		КонецЕсли;
		
		Если Раздел.ИмяСтраницы = "Приложение" Тогда
			Для Каждого Страница Из ЭтаФорма[мСтруктураМногостраничныхРазделов[Раздел.ИмяСтраницы]] Цикл
		   		Если Страница.ДополнительныеДанные.Количество() = 0 Тогда
					СтруктураДополнительныхДанных = Новый Структура;
					Страница.ДополнительныеДанные.Очистить();
					Страница.ДополнительныеДанные.Добавить(СтруктураДополнительныхДанных);
					Страница.ДополнительныеДанные[0].Пометка = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(СохрТабличныйДокумент);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета)
	
	Префикс = "ПолеТабличногоДокумента";
	
	Для Каждого Раздел Из мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл
			
		Если НЕ Раздел.МногостраничностьВРазделе Тогда
			ПоказателиОтчета.Вставить(Префикс + Раздел.ИмяСтраницы, ЭтаФорма["СтруктураДанных" + Раздел.ИмяСтраницы]);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеРаздела(НаимРаздела, ОчищатьРазделЕслиЕстьМногострочность, ИзменятьАктивнуюСтраницу = Истина)
	
	// Многостраничные разделы.
	//
	Если НаимРаздела = "Приложение" Тогда
		
		СтруктураДанных = Новый Структура;
		
		Для Каждого Область Из ТабличныйДокумент.Области Цикл
			
			Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И Область.СодержитЗначение Тогда
				СтруктураДанных.Вставить(Область.Имя, Область.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		Если НаимРаздела = "Приложение" Тогда
			
			НаимТаблицыСтраницы = "ТаблицаСтраницыПриложение";
			
		КонецЕсли;
		
		АктивнаяСтраница = ЭтаФорма[НаимТаблицыСтраницы].НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
		
		Если АктивнаяСтраница.Количество() > 0 Тогда
			
			АктивнаяСтраница = АктивнаяСтраница[0];
			
			АктивнаяСтраница.Данные.Очистить();
			АктивнаяСтраница.Данные.Добавить(СтруктураДанных);
			
			АктивнаяСтраница.АктивнаяСтраница = НЕ ИзменятьАктивнуюСтраницу;
			
		КонецЕсли;
		
	// Многострочные разделы.
	//
	ИначеЕсли НаимРаздела = "Раздел1" Тогда
		
		ЭтаФорма["СтруктураДанных" + НаимРаздела] = СобратьДанныеТекущегоТаблПоля(ЭтаФорма, "ТабличныйДокумент");
		
		ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(НаимРаздела);
		
		ОбновитьСтруктуруМногострочногоРаздела(ЭтаФорма, ИмяГруппы);
		
	// Обычные разделы.
	//
	ИначеЕсли НаимРаздела <> "Отправители" Тогда
		
		ЭтаФорма["СтруктураДанных" + НаимРаздела] = СобратьДанныеТекущегоТаблПоля(ЭтаФорма, "ТабличныйДокумент");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СобратьДанныеТекущегоТаблПоляНаСервере(ИмяТабличногоПоля) Экспорт
	
	Возврат СобратьДанныеТекущегоТаблПоля(ЭтаФорма, ИмяТабличногоПоля);

КонецФункции

&НаКлиенте
Функция СобратьДанныеТекущегоТаблПоляНаКлиенте(ИмяТабличногоПоля) Экспорт
	
	Возврат СобратьДанныеТекущегоТаблПоля(ЭтаФорма, ИмяТабличногоПоля);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СобратьДанныеТекущегоТаблПоля(Форма, ТабличноеПоле) Экспорт

	СтруктураДанныхПоля = Новый Структура;
	Для Каждого ТекущаяОбласть Из Форма[ТабличноеПоле].Области Цикл
        Если ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И ТекущаяОбласть.СодержитЗначение = Истина Тогда
			СтруктураДанныхПоля.Вставить(ТекущаяОбласть.Имя, ТекущаяОбласть.Значение);
		КонецЕсли;
		
	КонецЦикла;

 	Возврат СтруктураДанныхПоля;

КонецФункции

&НаСервере
Процедура ДатаПодписиПриИзмененииНаСервере()
	
	ЗаполнитьДатуВЯчейкахНаСервере();

	// Заполнить ТОЛЬКО параметры по текущей организации зависящие от даты подписания документа
	ЗаполнитьСведенияОбОрганизацииНаСервере(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказатель(Элемент, Область, КодПоказателя, Знач ЭтоСумма, Знач ТекстВыбора, ТаблицаВыбора, СтандартнаяОбработка, КолонкаПоиска = "Код") Экспорт
	
	Если ТаблицаВыбора = Неопределено ИЛИ СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
		
	//если показатель входит в состав многострочной части, тогда имя области не будет совпадать с переданным кодом показателя
	//необходимо это учесть
	//считаем, что в многострочной части не может быть показателя, состоящего из нескольких ячеек
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетностьКлиентСервер.ПоказательВходитВМногострочнуюЧасть(ЭтаФорма, КодПоказателя) > 0 Тогда	
		ИсходноеЗначение = ?(ТипЗнч(Область.Значение) = Тип("Строка"), СокрЛП(Область.Значение), Область.Значение);
	Иначе
		Разделитель = ?(Лев(Область.Имя, СтрДлина(КодПоказателя) + 1) = КодПоказателя + "_", "_", "");
		ИндексОбласти = 1;
		ИсходноеЗначение = "";
		Пока ЭтаФорма[ТекущийЭлемент.Имя].Области.Найти(КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")) <> Неопределено Цикл
			ИсходноеЗначение = ИсходноеЗначение + Строка(ЭтаФорма[ТекущийЭлемент.Имя].Области[КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")].Значение);
			ИндексОбласти = ИндексОбласти + 1;
		КонецЦикла;
		ИсходноеЗначение = СокрЛП(ИсходноеЗначение);
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",          ТекстВыбора);
	ПараметрыФормы.Вставить("ТаблицаЗначений",    ТаблицаВыбора);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура(КолонкаПоиска, ИсходноеЗначение));
	
	ДополнительныеПараметры = Новый Структура("ИндексОбласти, КодПоказателя, КолонкаПоиска, Область, Разделитель, Элемент", ИндексОбласти, КодПоказателя, КолонкаПоиска, Область, Разделитель, Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиПоказательЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказательЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ИндексОбласти = ДополнительныеПараметры.ИндексОбласти;
	КодПоказателя = ДополнительныеПараметры.КодПоказателя;
	КолонкаПоиска = ДополнительныеПараметры.КолонкаПоиска;
	Область = ДополнительныеПараметры.Область;
	Разделитель = ДополнительныеПараметры.Разделитель;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетностьКлиентСервер.ПоказательВходитВМногострочнуюЧасть(ЭтаФорма, КодПоказателя) > 0 Тогда	
		Область.Значение = РезультатВыбора[КолонкаПоиска];
	Иначе
		СтрокаРезультатВыбора = Строка(РезультатВыбора[КолонкаПоиска]);
		Для Инд = СтрДлина(СтрокаРезультатВыбора) + 1 По ИндексОбласти - 1 Цикл
			СтрокаРезультатВыбора = " " + РезультатВыбора[КолонкаПоиска];
		КонецЦикла;
		Для Инд = 1 По ИндексОбласти - 1 Цикл
			ЭтаФорма[ТекущийЭлемент.Имя].Области[КодПоказателя + Разделитель + Формат(Инд, "ЧГ=")].Значение = СокрЛП(Сред(РезультатВыбора[КолонкаПоиска], Инд, 1));
		КонецЦикла;
	КонецЕсли;
	
	// Изменение связанных данных.
	Если КодПоказателя = "П000010000104" Тогда
		
		НомерСтроки = Сред(Область.Имя, 15);
		
		ВыбНазвание = ?(ВРег(СокрЛП(РезультатВыбора.Название)) = "ПУСТОЕ ЗНАЧЕНИЕ", "", РезультатВыбора.Название);
		
		Область.Значение = ВыбНазвание;
		ТабличныйДокумент.Области["П000010000122_" + НомерСтроки].Значение = РезультатВыбора.Код;
		
		СтруктураГруппы1 = Неопределено;
		мСтруктураМногострочныхРазделов.Свойство(СтруктураРеквизитовФормы.мИдГруппы1, СтруктураГруппы1);
		
		СтруктураГруппы1 = ЭтаФорма[СтруктураГруппы1];
		
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000104 = ВыбНазвание;
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000122 = РезультатВыбора.Код;
		
	ИначеЕсли КодПоказателя = "П000010000123" Тогда
		
		НомерСтроки = Сред(Область.Имя, 15);
		
		ВыбНазвание = ?(ВРег(СокрЛП(РезультатВыбора.Название)) = "ПУСТОЕ ЗНАЧЕНИЕ", "", РезультатВыбора.Название);
		
		Область.Значение = ВыбНазвание;
		ТабличныйДокумент.Области["П000010000124_" + НомерСтроки].Значение = РезультатВыбора.Код;
		
		СтруктураГруппы1 = Неопределено;
		мСтруктураМногострочныхРазделов.Свойство(СтруктураРеквизитовФормы.мИдГруппы1, СтруктураГруппы1);
		
		СтруктураГруппы1 = ЭтаФорма[СтруктураГруппы1];
		
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000123 = ВыбНазвание;
		СтруктураГруппы1[Число(НомерСтроки) - 1].П000010000124 = РезультатВыбора.Код;
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ТабличныйДокументПриИзмененииСодержимогоОбласти(Элемент, Область);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетНаКлиенте(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт

	Расчет(ЭтаФорма, ИмяСтраницыПанели, ОбластьИмя);

КонецПроцедуры

&НаСервере
Процедура РасчетНаСервере(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт

	Расчет(ЭтаФорма, ИмяСтраницыПанели, ОбластьИмя);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура Расчет(Форма, ИмяСтраницыПанели = "", Знач ОбластьИмя = "")
	
	Если Форма.СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Или Форма.СтруктураРеквизитовФормы.мРежимПечати Тогда
		Возврат; // отключен режим АвтоЗаполнения
	КонецЕсли;
	
	НомПоз = СтрНайти(ОбластьИмя, "_");
	
	Если НомПоз > 0 Тогда
		ОбластьИмя = Лев(ОбластьИмя, НомПоз - 1);
	КонецЕсли;
	
	Если ИмяСтраницыПанели = "Раздел1" Тогда
		
		Группа1 = Неопределено;
		Форма.мСтруктураМногострочныхРазделов.Свойство(Форма.СтруктураРеквизитовФормы.мИдГруппы1, Группа1);
		
		Группа1 = Форма[Группа1];
				
		Если НЕ ЗначениеЗаполнено(ОбластьИмя) ИЛИ СтрЧислоВхождений(Форма.СтруктураЗависимыхЯчеекДляАвторасчета.П000010000114, ОбластьИмя) > 0 Тогда
			
			РассчитыватьИтог14 = Истина;
			ПроверитьАдвалорные = (НЕ Группа1.Количество() = 1);
			
			Если ПроверитьАдвалорные Тогда

				Для Инд = 0 По Группа1.Количество() - 1 Цикл
					СуффиксНомера = "_" + Формат(Инд + 1, "ЧГ=0");
					
					НБАкцизов = Форма.ТабличныйДокумент.Области["П000010000114" + СуффиксНомера].Значение;
					Если НБАкцизов = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					СтАкцизТверд = СтрЗаменить(Форма.ТабличныйДокумент.Области["П000010000116" + СуффиксНомера].Значение, ",", ".");
					ЗаполненаСтАкцизТверд = (СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ (ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)) И Число(СтАкцизТверд) <> 0));
					
					Если ЗаполненаСтАкцизТверд Тогда
						РассчитыватьИтог14 = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			СуммаПоКолонке14 = 0;
			
			Для Инд = 0 По Группа1.Количество() - 1 Цикл
				
				Строка = "_" + Формат(Инд + 1, "ЧГ=0");
				
				П000010000114 = Форма.ТабличныйДокумент.Области["П000010000114" + Строка].Значение; 
				СуммаПоКолонке14 = СуммаПоКолонке14 + П000010000114;
				
			КонецЦикла;
			
			Форма.ТабличныйДокумент.Области.П000010000114.Значение = ?(РассчитыватьИтог14, СуммаПоКолонке14, 0);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьИмя) ИЛИ СтрЧислоВхождений(Форма.СтруктураЗависимыхЯчеекДляАвторасчета.П000010000115, ОбластьИмя) > 0 Тогда
			
			СуммаПоКолонке15 = 0;
			
			Для Инд = 0 По Группа1.Количество() - 1 Цикл
				
				Строка = "_" + Формат(Инд + 1, "ЧГ=0");
				
				П000010000115 = Форма.ТабличныйДокумент.Области["П000010000115" + Строка].Значение;
				СуммаПоКолонке15 = СуммаПоКолонке15 + П000010000115;
				
			КонецЦикла;
			
			Форма.ТабличныйДокумент.Области.П000010000115.Значение = СуммаПоКолонке15;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьИмя) ИЛИ СтрЧислоВхождений(Форма.СтруктураЗависимыхЯчеекДляАвторасчета.П000010000119, ОбластьИмя) > 0 Тогда
			
			СуммаПоКолонке19 = 0;
			
			Для Инд = 0 По Группа1.Количество() - 1 Цикл
				
				Строка = "_" + Формат(Инд + 1, "ЧГ=0");
				
				П000010000119 = Форма.ТабличныйДокумент.Области["П000010000119" + Строка].Значение; 
				СуммаПоКолонке19 = СуммаПоКолонке19 + П000010000119;
				
			КонецЦикла;
			
			Форма.ТабличныйДокумент.Области.П000010000119.Значение = Окр(СуммаПоКолонке19);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьИмя) ИЛИ СтрЧислоВхождений(Форма.СтруктураЗависимыхЯчеекДляАвторасчета.П000010000120, ОбластьИмя) > 0 Тогда
			
			СуммаПоКолонке20 = 0;
			
			Для Инд = 0 По Группа1.Количество() - 1 Цикл
				
				Строка = "_" + Формат(Инд + 1, "ЧГ=0");
				
				П000010000120 = Форма.ТабличныйДокумент.Области["П000010000120" + Строка].Значение;
				СуммаПоКолонке20 = СуммаПоКолонке20 + П000010000120;
				
			КонецЦикла;
			
			Форма.ТабличныйДокумент.Области.П000010000120.Значение = Окр(СуммаПоКолонке20);
			
		КонецЕсли;
		
	ИначеЕсли ИмяСтраницыПанели = "" Тогда	
		
		Расчет(Форма, "Раздел1", ОбластьИмя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыОтчетаПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено
	 ИЛИ Элемент.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Отправители" Тогда 
		Элементы.СтраницаОтчета.Видимость = Ложь;
		Элементы.ГруппаСведенияОПодписантах.Видимость = Истина;
		СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
		Элементы.ОчиститьТекущуюСтраницу.Доступность = Ложь;
		Элементы.Обновить.Доступность = Ложь;
		КМенюРО = Элементы.РазделыОтчета.КонтекстноеМеню;
		КМенюРО.Видимость = Ложь;
		КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Ложь;
		КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Ложь;
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
		Возврат;
	Иначе
		Элементы.СтраницаОтчета.Видимость = Истина;
		Элементы.ГруппаСведенияОПодписантах.Видимость = Ложь;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	Элементы.ОчиститьТекущуюСтраницу.Доступность = Истина;
	
	Если СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере Тогда
		
		Если Элемент.ТекущиеДанные.РазделМногостраничный Тогда
			
			Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = ?(Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = Неопределено
															        ИЛИ Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = 0, 1, Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
																
		КонецЕсли;														
				
		ВывестиМакетСтраницыВТабличныйДокументНаСервере(Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим, Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
		
	Иначе
		
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
		
	КонецЕсли;
		
	КМенюРО = Элементы.РазделыОтчета.КонтекстноеМеню;
	Если Элемент.ТекущиеДанные.РазделМногостраничный Тогда
		Элемент.Развернуть(Элемент.ТекущаяСтрока);
		КМенюРО.Видимость = Истина;
		Если Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
			Элементы.ОчиститьТекущуюСтраницу.Доступность = Ложь;
		КонецЕсли;
		
		Если НЕ Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено
			И Элемент.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы().Количество() > 1 Тогда
			
			КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Истина;
			КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Истина;
		Иначе
			КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Истина;
			КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Ложь;
		КонецЕсли;
	Иначе
		КМенюРО.Видимость = Ложь;
		КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Ложь;
		КМенюРО.ПодчиненныеЭлементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость = Ложь;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Раздел1" Тогда
		Элементы.Обновить.Доступность = Истина;
	Иначе
		Элементы.Обновить.Доступность = Ложь;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Раздел1" Тогда
		Элементы.ТабличныйДокументКонтекстноеМенюГруппаСтроки.Видимость = Истина;
	Иначе
		Элементы.ТабличныйДокументКонтекстноеМенюГруппаСтроки.Видимость = Ложь;
	КонецЕсли;

	РегламентированнаяОтчетностьКлиент.СпрятатьСсылкуУдалитьСтраницу(ЭтаФорма);
	
	Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Область(1, 1);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМакетСтраницыВТабличныйДокументНаСервере(ИмяТекРаздела, НомерСтраницыРазделаОтчета)
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела, Истина);
	
	ВывестиМакетВТаблДокНаСервере(ИмяТекРаздела, НомерСтраницыРазделаОтчета);
	
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяТекРаздела;
	
	УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМакетВТаблДокНаСервере(ИмяТекРаздела, НомерСтраницыРазделаОтчета)
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, "ФормаОтчета")) + "_" + ИмяТекРаздела));
		
	// Многостраничные разделы.
	//
	Если ИмяТекРаздела = "Приложение" Тогда
	 
	  	Если ИмяТекРаздела = "Приложение" Тогда
	    	НаимТаблицыСтраницы = "ТаблицаСтраницыПриложение";
	    КонецЕсли;
	    
	    СтруктураДанных = Новый Структура;
	    
	    СписокДанных = ЭтаФорма[НаимТаблицыСтраницы][НомерСтраницыРазделаОтчета - 1].Данные;
		
		Для каждого ТаблицаСтраницы Из ЭтаФорма[НаимТаблицыСтраницы] Цикл
			ТаблицаСтраницы.АктивнаяСтраница = Ложь;
		КонецЦикла;
		
		ЭтаФорма[НаимТаблицыСтраницы][НомерСтраницыРазделаОтчета - 1].АктивнаяСтраница = Истина;
		
	    Если СписокДанных.Количество() > 0 Тогда
	    	СтруктураДанных = СписокДанных[0].Значение;
	    КонецЕсли;
	     	 
		Для Каждого ЭлСтруктуры Из СтруктураДанных Цикл
			Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
			Если Обл <> Неопределено Тогда
				Обл.Значение = ЭлСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтаФорма[НаимТаблицыСтраницы].Количество() = 1 Тогда 
			Обл = ТабличныйДокумент.Области["УдалитьСтраницуЗначок"];
			Обл.Гиперссылка = Ложь;
			Обл.Текст = "";
			Обл = ТабличныйДокумент.Области["УдалитьСтраницу"];
			Обл.Гиперссылка = Ложь;
			Обл.Текст = "";
		Иначе
			Обл = ТабличныйДокумент.Области["УдалитьСтраницуЗначок"];
			Обл.Гиперссылка = Истина;
			Обл.Текст = "х";
			Обл = ТабличныйДокумент.Области["УдалитьСтраницу"];
			Обл.Гиперссылка = Истина;
			Обл.Текст = "Удалить страницу";
		КонецЕсли;
		
	// Многострочные разделы.
	//
	ИначеЕсли ИмяТекРаздела = "Раздел1" Тогда
		
		Для Каждого ЭлСтруктуры Из ЭтаФорма["СтруктураДанных" + ИмяТекРаздела] Цикл
			Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
			Если Обл <> Неопределено Тогда
				Обл.Значение = ЭлСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
		ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ИмяТекРаздела);
		
		СтруктураГруппы = Неопределено;
		
		мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
		
		СтруктураГруппы = ЭтаФорма[СтруктураГруппы];
		
		ВывестиРазделВТабличныйДокумент(ЭтаФорма, ИмяГруппы, СтруктураГруппы);
		
		Если ТабличныйДокумент.Области.Найти("УдалитьСтрокуП000010000101_2") = Неопределено Тогда
			Обл = ТабличныйДокумент.Области.Найти("УдалитьСтрокуП000010000101_1");
			Если Обл <> Неопределено Тогда 
				Обл.Гиперссылка = Ложь;
				Обл.Текст = "";
			КонецЕсли;
		КонецЕсли;
		
		ТабличныйДокумент.Области.НомерКорректировки.Значение = Строка(НомерКорректировки);
		
	// Обычные разделы.
	Иначе
		
		Для Каждого ЭлСтруктуры Из ЭтаФорма["СтруктураДанных" + ИмяТекРаздела] Цикл
			Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
			Если Обл <> Неопределено Тогда
				Обл.Значение = ЭлСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;	
	
	ВывестиДату(ЭтаФорма);
	НомерЗаяв = Неопределено;
	СтруктураДанныхРаздел1.Свойство("НомерЗаяв", НомерЗаяв);
	Обл = ТабличныйДокумент.Области.Найти("НомерЗаяв");
	Если Обл <> Неопределено Тогда 
		Обл.Значение = НомерЗаяв;
	КонецЕсли;
	
	Если ИмяТекРаздела <> "Раздел2" Тогда
		
		УстановитьФорматВыводаНаСервере(ИмяТекРаздела);
		
		УстановитьЦветФонаЯчейкиНаСервере(,, ИмяТекРаздела);
		
		Если ИмяТекРаздела = "Раздел1" Тогда
			СнятьЗащитуСЯчеек(ЭтаФорма, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницу(Команда)
	
	ВставитьДополнительнуюСтраницуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	Если Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Приложение" Тогда
		мМассивТЗСпецификацийПрил.Добавить().Таблица.Очистить();
	КонецЕсли;
	
	Обл = ТабличныйДокумент.Области["УдалитьСтраницуЗначок"];
	Обл.Гиперссылка = Истина;
	Обл.Текст = "х";
	Обл = ТабличныйДокумент.Области["УдалитьСтраницу"];
	Обл.Гиперссылка = Истина;
	Обл.Текст = "Удалить страницу";
	
	Для Каждого КЗ Из мСтруктураМногострочныхРазделов Цикл 
		ИмяОбласти = КЗ.Ключ;
		Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + ИмяОбласти + "01_1");
		Если Обл <> Неопределено Тогда 
			Обл.Гиперссылка = Ложь;
			Обл.Текст = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВставитьДополнительнуюСтраницуНаСервере(ИмяТекущейСтраницыПанели = Неопределено)
	
	РегламентированнаяОтчетность.ВставитьДополнительнуюСтраницуНаСервере(ЭтаФорма, ИмяТекущейСтраницыПанели);
	РегламентированнаяОтчетность.ПослеВставкиДополнительнойСтраницыНаСервере(ЭтаФорма, ИмяТекущейСтраницыПанели);
	
	ВывестиДату(ЭтаФорма);
	
	НомерЗаяв = Неопределено;
	СтруктураДанныхРаздел1.Свойство("НомерЗаяв", НомерЗаяв);
	ТабличныйДокумент.Области.НомерЗаяв.Значение = СтруктураДанныхРаздел1.НомерЗаяв;
	
	УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиДату(Форма)
	
	ОблЧисло = Форма.ТабличныйДокумент.Области.Найти("Число");
	Если ОблЧисло = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ОблЧисло.Значение = Форма.ДатаПодписи;
	Форма.ТабличныйДокумент.Области.Месяц.Значение = Форма.ДатаПодписи;
	Форма.ТабличныйДокумент.Области.Год.Значение   = Форма.ДатаПодписи;
	
КонецПроцедуры

&НаСервере
Функция ЗаполненЛистРазделаПриложение(ЛистРаздела)
	
	ДанныеЛиста = ЛистРаздела.Данные[0].Значение;
	
	Результат = ЗначениеЗаполнено(ДанныеЛиста.ТипПрод_Пр)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецИНН_КПП)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецНаимОрг)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.КодСтраныПродавец)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецМестоНахожд)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПродавецБайконур)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ТипПок_Пр)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательИНН_КПП)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательНаимОрг)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.КодСтраныПокупатель)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательМестоНахожд)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ПокупательБайконур)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.НомерДог)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ДатаДог)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.НомерСпецификации)
	        ИЛИ ЗначениеЗаполнено(ДанныеЛиста.ДатаСпецификации);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УдалитьСтраницу(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтраницуЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Удалить текущую страницу?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.РазделыОтчета.ТекущиеДанные;
	
	НомерСтрРазделаОтчета = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	
	КопияТекущиеДанные = ТекущиеДанные;
	
	ТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ПолучитьЭлементы().Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтраницаРазделаПослеУдаления = Неопределено;
	Если КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = ТекущиеДанные.ПолучитьЭлементы().Количество() Тогда
		ТекущаяСтраницаРазделаПослеУдаления = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(ТекущиеДанные.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета - 1);
	КонецЕсли;
	
	УдалитьДополнительнуюСтраницуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	
	ТекущиеДанные.ПолучитьЭлементы().Удалить(ТекущиеДанные.ПолучитьЭлементы().Индекс(КопияТекущиеДанные));
	
	НомерСтраницы = 1;
	
	Для Каждого СтраницаРаздела Из ТекущиеДанные.ПолучитьЭлементы() Цикл
		
		СтраницаРаздела.КолонкаРазделыОтчета              = "Стр. " + НомерСтраницы;
		СтраницаРаздела.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницы;
		
		НомерСтраницы = НомерСтраницы + 1;
		
	КонецЦикла;
	
	Если НЕ ТекущаяСтраницаРазделаПослеУдаления = Неопределено Тогда
		Элементы.РазделыОтчета.ТекущаяСтрока = ТекущаяСтраницаРазделаПослеУдаления.ПолучитьИдентификатор();
	КонецЕсли;
	
	Если ТекущиеДанные.ПолучитьЭлементы().Количество() = 1 Тогда 
		Обл = ТабличныйДокумент.Области["УдалитьСтраницуЗначок"];
		Обл.Гиперссылка = Ложь;
		Обл.Текст = "";
		Обл = ТабличныйДокумент.Области["УдалитьСтраницу"];
		Обл.Гиперссылка = Ложь;
		Обл.Текст = "";
	Иначе
		Обл = ТабличныйДокумент.Области["УдалитьСтраницуЗначок"];
		Обл.Гиперссылка = Истина;
		Обл.Текст = "х";
		Обл = ТабличныйДокумент.Области["УдалитьСтраницу"];
		Обл.Гиперссылка = Истина;
		Обл.Текст = "Удалить страницу";
	КонецЕсли;
	
	Для Каждого КЗ Из мСтруктураМногострочныхРазделов Цикл 
		Обл = ТабличныйДокумент.Области.Найти(КЗ.Ключ + "01_2");
		Если Обл <> Неопределено Тогда 
			Инд = 0;
			Пока Истина Цикл 
				Инд = Инд + 1;
				Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + КЗ.Ключ + "01_" + Формат(Инд, "ЧГ="));
				Если Обл = Неопределено Тогда 
					Прервать;
				КонецЕсли;
				Обл.Гиперссылка = Истина;
				Обл.Текст = "х";
			КонецЦикла;
		Иначе
			Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + КЗ.Ключ + "01_1");
			Если Обл <> Неопределено Тогда 
				Обл.Гиперссылка = Ложь;
				Обл.Текст = "";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Приложение" Тогда
		
		мМассивТЗСпецификацийПрил.Удалить(НомерСтрРазделаОтчета - 1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТабличноеПолеНаКлиенте(ВыбТабличноеПолеИмя) Экспорт

	ОчиститьТабличноеПоле(ЭтаФорма, ВыбТабличноеПолеИмя);

КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличноеПолеНаСервере(ВыбТабличноеПолеИмя) Экспорт

	ОчиститьТабличноеПоле(ЭтаФорма, ВыбТабличноеПолеИмя);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьТабличноеПоле(Форма, ВыбТабличноеПолеИмя)

	Перем СписокПоказателейНеПодлежащихОчистке;
	
	СписокПоказателейНеПодлежащихОчистке = Новый СписокЗначений;
	СписокПоказателейНеПодлежащихОчистке.Добавить("ПокупательИНН_КПП");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ПокупательНаимОрг");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ПокупательМестоНахожд");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОРук");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Число");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Месяц");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Год");
	СписокПоказателейНеПодлежащихОчистке.Добавить("НомерКорректировки");
	
	// Предварительно проверим наличие дополнительных строк,
	// в заданном поле табличного документа.
	// При наличии таких строк удаляем их.
	ВыбТабличноеПоле = Форма["ТабличныйДокумент"];
	ИмяГруппыДопСтрок = Форма.ОпределитьИдМногострочногоРазделаПоТабличномуПолю(ВыбТабличноеПолеИмя);
	
	Если ИмяГруппыДопСтрок <> Неопределено Тогда
		// Поле табличного документа содержит многострочный раздел.
		
		Форма.УдалитьНеобходимоеКоличествоДопСтрокНаСервере(ВыбТабличноеПоле, ВыбТабличноеПолеИмя, ИмяГруппыДопСтрок);

		Форма["ТабличныйДокумент"] = ВыбТабличноеПоле;

	КонецЕсли;
	
	// Непосредственно очищаем ячейки табличного документа.
	Для Инд = 0 По ВыбТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ВыбТабличноеПоле.Области[Инд];

		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;

		Если ТекущаяОбласть.СодержитЗначение <> Истина Тогда
			Продолжить;
		КонецЕсли;

		// Ячейки не подлежащие очистки
		Если СписокПоказателейНеПодлежащихОчистке.НайтиПоЗначению(ТекущаяОбласть.Имя) <> Неопределено И ВыбТабличноеПолеИмя = "Раздел1" Тогда
			Продолжить;
		КонецЕсли;

		ТекущаяОбласть.Очистить();

	КонецЦикла;
		
	ИмяСтраницыПанели = ВыбТабличноеПолеИмя;
	
	ОчиститьСлужебныеДанныеРаздела(Форма, ИмяСтраницыПанели);

	#Если НаКлиенте Тогда
		Форма.ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделуНаКлиенте(ИмяСтраницыПанели);
	#Иначе
		Форма.ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделуНаСервере(ИмяСтраницыПанели);
	#КонецЕсли
          		
	Если ИмяГруппыДопСтрок <> Неопределено Тогда
		ОбновитьСтруктуруМногострочногоРаздела(Форма, ИмяГруппыДопСтрок);
	КонецЕсли;

	Если ВыбТабличноеПолеИмя="Раздел1" Тогда
		
		ВыбТабличноеПоле.Области.КодСтраныПокупатель.Значение
		= ?(ПустаяСтрока(ВыбТабличноеПоле.Области.ПокупательБайконур.Значение), "643", "398");
		
		Расчет(Форма, ВыбТабличноеПолеИмя, "");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОпределитьИдМногострочногоРазделаПоТабличномуПолю(ТекТабличноеПоле) Экспорт
	Перем ИдГруппы;

	Если ТекТабличноеПоле = "Раздел1" Тогда
		ИдГруппы = СтруктураРеквизитовФормы.мИдГруппы1;
	КонецЕсли;

	Возврат ИдГруппы;

КонецФункции

&НаСервере
Процедура УдалитьНеобходимоеКоличествоДопСтрокНаСервере(ВыбТабличноеПоле, ВыбТабличноеПолеИмя, ИмяГруппыДопСтрок) Экспорт
	
	// Получим таблицу значений, содержащую данные дополнительных строк.
	ПромТаблицаДопСтрокРаздела = Неопределено;
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппыДопСтрок, ПромТаблицаДопСтрокРаздела);
	
	ПромТаблицаДопСтрокРаздела = ЭтаФорма[ПромТаблицаДопСтрокРаздела];
	
	ИсходноеКоличествоСтрокГруппы = 0;
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппыДопСтрок, ИсходноеКоличествоСтрокГруппы);
	
	ТекущееКоличествоСтрок = ПромТаблицаДопСтрокРаздела.Количество();
	
	Если ТекущееКоличествоСтрок > ИсходноеКоличествоСтрокГруппы Тогда
		// Имеются дополнительные строки.
		// Удаляем их.
		
		НСтр = ПромТаблицаДопСтрокРаздела.Количество();
		
		Пока НСтр > ИсходноеКоличествоСтрокГруппы Цикл
			
			ТекСтрокаТаблицы = ПромТаблицаДопСтрокРаздела[НСтр - 1];
			
			ИмяПервойКолонки = ИмяГруппыДопСтрок + "01";
			ИмяПервойЯчейкиУдаляемойСтроки  = ИмяПервойКолонки + "_" + Формат(НСтр, "ЧГ=0");
			
			// область первой ячейки удаляемой строки многострочного раздела
			ОбластьЯчейки = ВыбТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
			
			НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
			НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
			
			// Определим область удаляемой строки
			УдаляемаяОбласть = ВыбТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
				
			// непосредственно удаляем область строки табличного документа
			ВыбТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
				
			// удаляем из структуры многострочного раздела соответствующую строку
			ПромТаблицаДопСтрокРаздела.Удалить(ТекСтрокаТаблицы);
			
			НСтр = НСтр - 1;
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделуНаКлиенте(Знач ИмяРаздела) Экспорт

	ПеременныеМодуля = Новый Структура;
	
	ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(ИмяРаздела, ПеременныеМодуля);

КонецПроцедуры

&НаСервере
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделуНаСервере(Знач ИмяРаздела) Экспорт

	ПеременныеМодуля = Новый Структура;
	
	ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(ИмяРаздела, ПеременныеМодуля);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(Знач ИмяРаздела, ПеременныеМодуля)
    	
	Возврат;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьСлужебныеДанныеРаздела(Форма, ИмяСтраницыПанели)
	
	Если ИмяСтраницыПанели = "Раздел1" Тогда
		Если ТипЗнч(Форма.ДополнительныеДанныеРазделов) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
			
		Если Форма.ДополнительныеДанныеРазделов.Свойство("АдресПродавцаРаздел1") Тогда
			ОчиститьСтруктуру(Форма.ДополнительныеДанныеРазделов.АдресПродавцаРаздел1);
		КонецЕсли;
		
		Если Форма.ДополнительныеДанныеРазделов.Свойство("АдресКомиссионераРаздел1") Тогда
			ОчиститьСтруктуру(Форма.ДополнительныеДанныеРазделов.АдресКомиссионераРаздел1);
		КонецЕсли;
		
	ИначеЕсли ИмяСтраницыПанели = "Раздел3" Тогда
		Если ТипЗнч(Форма.ДополнительныеДанныеРазделов) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если Форма.ДополнительныеДанныеРазделов.Свойство("АдресПродавцаРаздел3") Тогда
			ОчиститьСтруктуру(Форма.ДополнительныеДанныеРазделов.АдресПродавцаРаздел3);
		КонецЕсли;
		
		Если Форма.ДополнительныеДанныеРазделов.Свойство("АдресПокупателяРаздел3") Тогда
			ОчиститьСтруктуру(Форма.ДополнительныеДанныеРазделов.АдресПокупателяРаздел3);
		КонецЕсли;
		
	ИначеЕсли ИмяСтраницыПанели = "Приложение" Тогда
		
		СтраницыРазделаПриложение = Неопределено;
		
		Форма.мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели, СтраницыРазделаПриложение);
		СтраницыРазделаПриложение = Форма[СтраницыРазделаПриложение];
		
		АктивнаяСтраница = СтраницыРазделаПриложение.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
				
		Если АктивнаяСтраница.Количество() = 0 Тогда
			Возврат;
		Иначе
			АктивнаяСтраница = АктивнаяСтраница[0];
		КонецЕсли;
		
		Если АктивнаяСтраница.ДополнительныеДанные.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные[0].Значение) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если АктивнаяСтраница.ДополнительныеДанные[0].Значение.Свойство("АдресПродавца") Тогда
			ОчиститьСтруктуру(АктивнаяСтраница.ДополнительныеДанные[0].Значение.АдресПродавца);
			АктивнаяСтраница.ДополнительныеДанные[0].Пометка = Ложь;
		КонецЕсли;
		
		Если АктивнаяСтраница.ДополнительныеДанные[0].Значение.Свойство("АдресПокупателя") Тогда
			ОчиститьСтруктуру(АктивнаяСтраница.ДополнительныеДанные[0].Значение.АдресПокупателя);
			АктивнаяСтраница.ДополнительныеДанные[0].Пометка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьСтруктуру(ОчищаемаяСтруктура)
	
	Если ТипЗнч(ОчищаемаяСтруктура) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементСтруктуры Из ОчищаемаяСтруктура Цикл
		Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Структура") Тогда
			ОчиститьСтруктуру(ОчищаемаяСтруктура[ЭлементСтруктуры.Ключ]);
		Иначе
			ОчищаемаяСтруктура[ЭлементСтруктуры.Ключ] = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	КтоОтправляетПриИзменении("");
	ПодписантПриИзменении("");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	ЗаполнитьСведенияОбОрганизацииНаСервере();	
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	
	мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекущуюСтраницу(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьТекущуюСтраницуЗавершение", ЭтотОбъект);
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(
		ЭтаФорма, "ОчиститьТекущийЭкземплярМногостраничногоРаздела"
		+ Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекущуюСтраницуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВывестиДату(ЭтаФорма);
	
	Если НЕ СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
		ТабличныйДокумент.Области.НомерЗаяв.Значение = СтруктураДанныхРаздел1.НомерЗаяв;
	Иначе
		НомерКорректировки = 0;
		ТабличныйДокумент.Области.НомерКорректировки.Значение = Строка(НомерКорректировки);
		СтруктураРеквизитовФормы.ВидДокумента = 0;
		ТабличныйДокумент.Области.Число.Значение = ДатаПодписи;
		ТабличныйДокумент.Области.Месяц.Значение = ДатаПодписи;
		ТабличныйДокумент.Области.Год.Значение   = ДатаПодписи;
	КонецЕсли;
	
	УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчет(Команда)
	
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(ЭтаФорма, "ОчиститьВесьОтчет");
	УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(ВыполняемоеОповещение = Неопределено) Экспорт
	
	РегламентированнаяОтчетностьКлиент.Очистить(ЭтаФорма, ВыполняемоеОповещение);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере() Экспорт
	
	СтруктураДанныхРаздел1.Очистить();
	СтруктураДанныхРаздел2.Очистить();
	СтруктураДанныхРаздел3.Очистить();
	
	ТаблицаСтраницыПриложение.Очистить();
	
	ИнициализацияМногостраничныхРазделов();
	
	Для Каждого мнгРаздел Из мСтруктураМногострочныхРазделов Цикл
		ЭтаФорма[мнгРаздел.Ключ].Очистить();
	КонецЦикла;
	
	НачальныеОперацииСМногострочнымиБлоками();
	
	ЗаполнитьПустыеСтруктурыДанных();
	
	АдресПокупателяРаздел1 = ДополнительныеДанныеПоАдресу(ДополнительныеДанныеРазделов, "АдресПокупателяРаздел1");
	ДополнительныеДанныеРазделов.Очистить();
	ДополнительныеДанныеРазделов.Вставить("АдресПокупателяРаздел1", АдресПокупателяРаздел1);

	Если НЕ СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный"
	   И НЕ СтруктураРеквизитовФормы.НаимТекущегоРаздела = "ФизЛица" Тогда
		ОчиститьТабличноеПолеНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	КонецЕсли;
	
	СтруктураДанныхРаздел1.Вставить("НомерКорректировки", НомерКорректировки);
	
	СтруктураДанныхРаздел1.Вставить("Число", Формат(ДатаПодписи, "ДФ='d'"));
	СтруктураДанныхРаздел1.Вставить("Месяц", Формат(ДатаПодписи, "ДФ='M'"));
	СтруктураДанныхРаздел1.Вставить("Год",   Формат(ДатаПодписи, "ДФ='yyyy'"));
			
	ВывестиМакетСтраницыВТабличныйДокументНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела, 1);
		
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
	РегламентированнаяОтчетностьКлиентСервер.ОчиститьВариантыЗаполнения(ЭтаФорма);
	
	ОбновитьТЗСпецификаций();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияМногостраничныхРазделов()
	
	СтрПерваяСтраница = ТаблицаСтраницыПриложение.Добавить();
	СтрПерваяСтраница.Представление               = "Новая страница";
	СтрПерваяСтраница.АктивнаяСтраница            = Истина;
	СтрПерваяСтраница.АвтоматическоеПредставление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПоказателиОтчетаЗаполняемыеИзИБНаСервере(ИмяСтраницы)
	
	РегламентированнаяОтчетностьКлиентСервер.ОчиститьПоказателиОтчетаЗаполняемыеИзИБ(ЭтаФорма, ИмяСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроек(Команда)
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
	    
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)
    Перем ТекущийРазделОтчетаСокрНаим;
	Перем НомерСтраницыРазделаОтчета;
	
	// Запоминаем текущий раздел, установленный в дереве разделов отчета.
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРазделОтчетаСокрНаим = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		НомерСтраницыРазделаОтчета  = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	КонецЕсли;
	
	мПараметры = Новый Структура;
	мПараметры.Вставить("ПроверкаСоотношений", Ложь);
	мПараметры.Вставить("Автосохранение", Ложь);
	мПараметры.Вставить("ЗначенияИзСписка", Истина);
	мПараметры.Вставить("СчетчикСтраниц", Ложь);
	мПараметры.Вставить("ИмяОтчета", ИмяОтчета(ЭтаФорма.ИмяФормы));
	мПараметры.Вставить("ИмеетсяРеквизитДеревоВыбранныхСтраниц", Истина);
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	ПараметрыФормы.Вставить("мПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена);
	ПараметрыФормы.Вставить("мСчетчикСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	ПараметрыФормы.Вставить("мАвтоВыборКодов", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	ПараметрыФормы.Вставить("мПараметры", мПараметры);
					
	ФормаНастройкиОтчета = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("НастройкиОтчета", ПараметрыФормы, ЭтаФорма);
	
	мДеревоВыбранныхСтраниц.ПолучитьЭлементы().Очистить();
    
	КопироватьДанныеФормы(мДеревоСтраницОтчета, ФормаНастройкиОтчета.Дерево);
	
	ЗначениеАвторасчета = СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет;

	ДополнительныеПараметры = Новый Структура("ЗначениеАвторасчета, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим", ЗначениеАвторасчета, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраницЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ФормаНастройкиОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаНастройкиОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаНастройкиОтчета.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраницЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ЗначениеАвторасчета = ДополнительныеПараметры.ЗначениеАвторасчета;
	НомерСтраницыРазделаОтчета = ДополнительныеПараметры.НомерСтраницыРазделаОтчета;
	ТекущийРазделОтчетаСокрНаим = ДополнительныеПараметры.ТекущийРазделОтчетаСокрНаим;
	
	Модифицированность = Истина;
	
	флЗначенияИдентичны = Истина;
	
	Для Индекс = 0 По мДеревоСтраницОтчета.ПолучитьЭлементы().Количество() - 1 Цикл
		
		Если НЕ мДеревоСтраницОтчета.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу = мДеревоВыбранныхСтраниц.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу Тогда	
			флЗначенияИдентичны = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеАвторасчета <> СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
		
		Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
			СнятьЗащитуСЯчеек(ЭтаФорма, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
		КонецЕсли;
		
	КонецЕсли;
	
	// Управляем видимостью страниц основной панели формы
	РегламентированнаяОтчетностьКлиент.ПоказатьСтраницыОтчетаНаКлиенте(ЭтаФорма);
	
	Если НЕ флЗначенияИдентичны Тогда
		
		СформироватьДеревоРазделовОтчетаНаКлиенте();
		
		Если НЕ ТекущийРазделОтчетаСокрНаим = Неопределено Тогда
			
			// Пытаемся найти ранее запомненный раздел.
			НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(РазделыОтчета.ПолучитьЭлементы(), "КолонкаРазделыОтчетаСокрНаим", ТекущийРазделОтчетаСокрНаим);
			
			Если НЕ НайденнаяСтрока = Неопределено 
				И НЕ НомерСтраницыРазделаОтчета = Неопределено 
				И НайденнаяСтрока.ПолучитьЭлементы().Количество() > 0 Тогда
				
				НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(НайденнаяСтрока.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", НомерСтраницыРазделаОтчета);
				
			КонецЕсли;
			
			// В случае успешного поиска, устанавливаем курсор на найденный раздел.
			Если НЕ НайденнаяСтрока = Неопределено Тогда
				Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Отказ = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьБланкЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланкЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Команда = ДополнительныеПараметры.Команда;
		
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
    	        	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1. Формируется печатная форма...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Печать);
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтаФорма, ВидПечати, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;	
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
	
	Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		
		Сообщение = Новый СообщениеПользователю;

		Сообщение.Текст = НСтр("ru='Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");

		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ТипЗнч(мДеревоВыбранныхСтраниц) <> Тип("ДанныеФормыДерево") Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
	СтруктураРеквизитовФормы.мРежимПечати = Истина;
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела, Ложь, Ложь);
	
	сохрТаблДок = ТабличныйДокумент.ПолучитьОбласть();
	
	СтрПоля = "ПолеСверху, ПолеСправа, ПолеСнизу, ПолеСлева";
	ВремПоля = Новый Структура(СтрПоля);
	
	ЗаполнитьЗначенияСвойств(ВремПоля, ТабличныйДокумент, СтрПоля);
	
	ВывестиМакетВТаблДокНаСервере("Раздел1", 0);
	
	ОбщиеРеквизитыРазделов = ПолучитьОбщиеРеквизитыСТитульногоЛиста();
	
	НомераСтраниц = Новый Структура;
	СтраницВсего = 0;
	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.ПолучитьЭлементы() Цикл
		Если СтрокаУровня1.ВыводНаПечать = 0
			ИЛИ СтрокаУровня1.ИмяСтраницы = "Отправители" Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаУровня1.ИмяСтраницы = "Приложение" Тогда
			Для Каждого Страница Из ТаблицаСтраницыПриложение Цикл
				СтраницВсего = СтраницВсего + 1;
			КонецЦикла;
		Иначе
			СтраницВсего = СтраницВсего + 1;
			НомераСтраниц.Вставить(СтрокаУровня1.ИмяСтраницы, СтраницВсего);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.ПолучитьЭлементы() Цикл
		
		Если СтрокаУровня1.ВыводНаПечать = 0
			ИЛИ СтрокаУровня1.ИмяСтраницы = "Отправители" Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("НаименованиеЛиста",  СтрокаУровня1.Представление);
		СтруктураПараметров.Вставить("ОриентацияЛиста",    СтрокаУровня1.ОриентацияСтраницы);
		СтруктураПараметров.Вставить("ИмяСтраницы",        СтрокаУровня1.ИмяСтраницы);
		Структурапараметров.Вставить("ИмяОбластиТаблПоля", СтрокаУровня1.ИмяОбластиТабличногоПоля);
		СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
		
		Если НЕ СтрокаУровня1.ИмяСтраницы = "Приложение" Тогда
			СтруктураПараметров.Вставить("НомерСтраницы",      НомераСтраниц[СтрокаУровня1.ИмяСтраницы]);
			СтруктураПараметров.Вставить("ВсегоСтраниц",       СтраницВсего);
		КонецЕсли;
		
		Если СтрокаУровня1.ИмяСтраницы = "Раздел1" Тогда
			
			ВывестиМакетВТаблДокНаСервере(СтрокаУровня1.ИмяСтраницы, 0);
			ВывестиНаПечатьЛистРаздел1(СтруктураПараметров);
		
		ИначеЕсли НЕ мСтруктураМногостраничныхРазделов.Свойство(СтрокаУровня1.ИмяСтраницы) Тогда
			
			ВывестиМакетВТаблДокНаСервере(СтрокаУровня1.ИмяСтраницы, 0);
			ВывестиНаПечатьЛист(СтруктураПараметров, ОбщиеРеквизитыРазделов, СтрПоля);
			
		Иначе
			
			СтруктураПараметров.Вставить("ИмяСтраницыПанели", СтрокаУровня1.ИмяСтраницы);
			ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметров, ОбщиеРеквизитыРазделов);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(сохрТаблДок);
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ВремПоля, СтрПоля);
	
	РегламентированнаяОтчетностьКлиентСервер.ПроставлениеНомеровЛистов(ЭтаФорма, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);

	КопироватьДанныеФормы(мДеревоВыбранныхСтраниц, мДеревоСтраницОтчета);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПолучитьОбщиеРеквизитыСТитульногоЛиста()
	
	МассивНомерЗаяв   = "";
	МассивЧисло       = "";
	МассивМесяц       = "";
	МассивГод       = "";
	МассивДатаПодписи = Новый Массив(8);
					
	ИсходныйМакет = ТабличныйДокумент;
		
	Область = ИсходныйМакет.Области.Найти("НомерЗаяв");
	
	Если НЕ Область = Неопределено Тогда
		
		МассивНомерЗаяв = Область.Значение;
				
	КонецЕсли;
		
	Область = ИсходныйМакет.Области.Найти("Число");
	
	Если НЕ Область = Неопределено Тогда
		
		МассивЧисло = Область.Значение;
		
	КонецЕсли;
	
	Область = ИсходныйМакет.Области.Найти("Месяц");
	
	Если НЕ Область = Неопределено Тогда
		
		МассивМесяц = Область.Значение;
		
	КонецЕсли;

	Область = ИсходныйМакет.Области.Найти("Год");
	
	Если НЕ Область = Неопределено Тогда
		
		МассивГод = Область.Значение;
		
	КонецЕсли;
			
	ОбщиеРеквизитыРазделов = Новый Структура;
		
	ОбщиеРеквизитыРазделов.Вставить("НомерЗаяв", МассивНомерЗаяв);
	ОбщиеРеквизитыРазделов.Вставить("Число", МассивЧисло);
	ОбщиеРеквизитыРазделов.Вставить("Месяц", МассивМесяц);
	ОбщиеРеквизитыРазделов.Вставить("Год", МассивГод);
		
	Возврат ОбщиеРеквизитыРазделов;
	
КонецФункции

&НаСервере
Процедура ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметровСтраницы, ОбщиеРеквизитыРазделов)
	
	Перем ИмяСтраницы;
	Перем ТаблицаСтраницРаздела;
	
	СтруктураПараметровСтраницы.Свойство("ИмяСтраницыПанели", ИмяСтраницы);
	
	Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницы, ТаблицаСтраницРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСтраницРаздела = ЭтаФорма[ТаблицаСтраницРаздела];
	
	МассивПризнаковАктивности = Новый Массив;
	Для Каждого Страница Из ТаблицаСтраницРаздела Цикл
		ТекущийИндекс = ТаблицаСтраницРаздела.Индекс(Страница);
		МассивПризнаковАктивности.Добавить(ТаблицаСтраницРаздела[ТекущийИндекс].АктивнаяСтраница);
	КонецЦикла;
	
	Для Каждого Страница Из ТаблицаСтраницРаздела Цикл
		ТекущийИндекс = ТаблицаСтраницРаздела.Индекс(Страница);
		ВывестиМакетВТаблДокНаСервере(ИмяСтраницы, ТекущийИндекс + 1);
		ВывестиНаПечатьЛист(СтруктураПараметровСтраницы, ОбщиеРеквизитыРазделов);
	КонецЦикла;
	
	Для Каждого Страница Из ТаблицаСтраницРаздела Цикл
		ТекущийИндекс = ТаблицаСтраницРаздела.Индекс(Страница);
		ТаблицаСтраницРаздела[ТекущийИндекс].АктивнаяСтраница = МассивПризнаковАктивности[ТекущийИндекс];
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиНаПечатьЛист(Парам1, ОбщиеРеквизитыРазделов, СтрПоля = Неопределено)
	
	СоответствиеРазделовМакетам = Новый Соответствие;
	
	СоответствиеРазделовМакетам.Вставить("Раздел1",    "ФормаОтчета2015кв1_Раздел1");
	СоответствиеРазделовМакетам.Вставить("Раздел2",    "ФормаОтчета2015кв1_Раздел2");
	СоответствиеРазделовМакетам.Вставить("Раздел3",    "ПечатнаяФормаОтчета2015Кв1_Раздел3");
	СоответствиеРазделовМакетам.Вставить("Приложение", "ФормаОтчета2015кв1_Приложение");
	
	ПечатнаяФормаРаздела = СоответствиеРазделовМакетам[Парам1.ИмяСтраницы];
	
	МакетПечатнойФормы = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(ПечатнаяФормаРаздела);
	МакетПечатнойФормы.ПолеСверху = 5;
	МакетПечатнойФормы.ПолеСправа = 5;
	МакетПечатнойФормы.ПолеСнизу  = 5;
	МакетПечатнойФормы.ПолеСлева  = 5;
	
	ИдГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(Парам1.ИмяСтраницы);
	
	Если ИдГруппы <> Неопределено Тогда
		
		КолвоСтрокВТаблДок = ЭтаФорма[ИдГруппы].Количество();
		
		ОбластьМнгЧасти              = МакетПечатнойФормы.ПолучитьОбласть("МногострочнаяЧасть");
		ВысотаОбластиМнгЧасти        = МакетПечатнойФормы.ПолучитьОбласть("МногострочнаяЧасть").ВысотаТаблицы;
		ИмяОбластиПоследнейДопСтроки = ОбластьМнгЧасти.ПолучитьОбласть(ВысотаОбластиМнгЧасти, , ВысотаОбластиМнгЧасти).Области[1].Имя;
		
		КолвоСтрокВПечатнойФорме = Число(Сред(ИмяОбластиПоследнейДопСтроки, СтрНайти(ИмяОбластиПоследнейДопСтроки, "_") + 1));
		КолвоСтрокВПечатнойФормеСтр = Формат(КолвоСтрокВПечатнойФорме, "ЧГ=");
				
		Если КолвоСтрокВТаблДок > КолвоСтрокВПечатнойФорме Тогда
						
			НомерВерхЯчейкиОбласти = МакетПечатнойФормы.Область(ИдГруппы + "01_" + КолвоСтрокВПечатнойФормеСтр).Верх;
			НомерНижнЯчейкиОбласти = МакетПечатнойФормы.Область(ИдГруппы + Формат(мСтруктураКолвоКолонокРазделов[ИдГруппы], "ЧЦ=2; ЧВН=") + "_" + КолвоСтрокВПечатнойФормеСтр).Низ;
				
			ВставляемаяОбласть = МакетПечатнойФормы.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти);
			
			КолвоДопСтрок = КолвоСтрокВТаблДок - КолвоСтрокВПечатнойФорме;
									
			Для Ном = 1 По КолвоДопСтрок Цикл
							
				МакетПечатнойФормы.ВставитьОбласть(ВставляемаяОбласть, , ТипСмещенияТабличногоДокумента.ПоВертикали);
													
			КонецЦикла;
			
			Для Каждого Область Из МакетПечатнойФормы.ПолучитьОбласть(НомерВерхЯчейкиОбласти + КолвоДопСтрок, , НомерНижнЯчейкиОбласти + КолвоДопСтрок).Области Цикл
				НайденнаяОбласть = МакетПечатнойФормы.Области.Найти(Область.Имя);
				Если НЕ НайденнаяОбласть = Неопределено Тогда 
					НайденнаяОбласть.Имя = Лев(НайденнаяОбласть.Имя, СтрНайти(НайденнаяОбласть.Имя, "_") - 1) + "_" + Формат(КолвоСтрокВТаблДок, "ЧЦ=3");
				КонецЕсли;
			КонецЦикла;
			
			Для Ном = 0 По КолвоДопСтрок - 1 Цикл
				
				Для Каждого Область Из МакетПечатнойФормы.ПолучитьОбласть(НомерВерхЯчейкиОбласти + КолвоДопСтрок, , НомерНижнЯчейкиОбласти + КолвоДопСтрок).Области Цикл
					НайденнаяОбласть = МакетПечатнойФормы.Область(НомерВерхЯчейкиОбласти + Ном, Область.Лево, НомерВерхЯчейкиОбласти + Ном, Область.Право);
					Если НЕ НайденнаяОбласть = Неопределено Тогда 
						НайденнаяОбласть.Имя = Лев(Область.Имя, СтрНайти(Область.Имя, "_") - 1) + "_" + Формат(КолвоСтрокВПечатнойФорме + Ном, "ЧЦ=3");
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			
			УдаляемыеОбласти = Новый Массив;
			Для Каждого Обл Из МакетПечатнойФормы.Области Цикл
				Если Обл.Лево = 0 И Обл.Право = 0 И СтрНайти(Обл.Имя, "ОсновнаяЧасть") = 0 И СтрНайти(Обл.Имя, "Ограничители") = 0 И СтрНайти(Обл.Имя, "ПустаяСтрока") = 0 Тогда
					УдаляемыеОбласти.Добавить(Обл);
				КонецЕсли;
			КонецЦикла;
			Для Каждого Обл Из УдаляемыеОбласти Цикл
				Обл.Имя = Неопределено;
			КонецЦикла;
			
			МакетПечатнойФормы.Область(НомерВерхЯчейкиОбласти, 0, НомерНижнЯчейкиОбласти, 0).Имя = "МногострочнаяЧасть";
						
		КонецЕсли;
						
	КонецЕсли;
	
	ЗаполнитьПечатнуюФорму(Парам1, МакетПечатнойФормы, ОбщиеРеквизитыРазделов);
	
	УдалитьОбластиБайконура(МакетПечатнойФормы);
	
	УдалитьПримечания(МакетПечатнойФормы);
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(МакетПечатнойФормы);
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, МакетПечатнойФормы, СтрПоля, "Макет");
	
	РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая(ЭтаФорма.Заголовок, Парам1, ТабличныйДокумент, СтруктураРеквизитовФормы.мПечатныеФормы);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиНаПечатьЛистРаздел1(СтруктураПараметров)
			
	МакетПечатиРаздела = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет("ПечатнаяФормаОтчета2015Кв1_Раздел1");
	
	МакетОбразцаПечатиРаздела = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет("ПечатнаяФормаОтчета2015Кв1_Раздел1");
	
	МакетПечатиРаздела = Новый ТабличныйДокумент;
	
	ОбластьОбразца = МакетОбразцаПечатиРаздела.ПолучитьОбласть("ОсновнаяЧасть_1");
	МакетПечатиРаздела.Вывести(ОбластьОбразца);
	
	ИмяОпорнойОбласти = "П000010000101";
	НомерСтроки = 1;
	Пока Истина Цикл
		Постфикс = "_" + Формат(НомерСтроки, "ЧГ=0");
		ИмяОбласти = ИмяОпорнойОбласти + Постфикс;
		Если ТабличныйДокумент.Области.Найти(ИмяОбласти) = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ОбластьОбразца = МакетОбразцаПечатиРаздела.ПолучитьОбласть("МногострочнаяЧасть");
		
		СтруктураГруппы = "";
		
		мСтруктураМногострочныхРазделов.Свойство(СтруктураРеквизитовФормы.мИдГруппы1, СтруктураГруппы);
		
		СтруктураГруппы = ЭтаФорма[СтруктураГруппы];
			
		Для НСтр = 1 По СтруктураГруппы.Количество() Цикл
			
			Для НомерГрафы = 1 По мСтруктураКолвоКолонокРазделов[СтруктураРеквизитовФормы.мИдГруппы1] Цикл
				
				ИмяКолонки = СтруктураРеквизитовФормы.мИдГруппы1 + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
				
				Если СтруктураГруппы[0].Свойство(ИмяКолонки) Тогда	
					
					ОбластьПоказателя = ОбластьОбразца.Области.Найти(ИмяКолонки + "_1");
					
					Если ОбластьПоказателя <> Неопределено Тогда
						ОбластьПоказателя.Имя = ИмяКолонки + Постфикс;
					КонецЕсли;
					
				КонецЕсли;	
				
			КонецЦикла;
		КонецЦикла;
		
		МакетПечатиРаздела.Вывести(ОбластьОбразца);
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	ОбластьОбразца = МакетОбразцаПечатиРаздела.ПолучитьОбласть("ОсновнаяЧасть_2");
	МакетПечатиРаздела.Вывести(ОбластьОбразца);
	ОбластьОбразца = МакетОбразцаПечатиРаздела.ПолучитьОбласть("Ограничители");
	МакетПечатиРаздела.Вывести(ОбластьОбразца);
	
	ЗаполнитьМакетПечатиРаздела(СтруктураПараметров, МакетПечатиРаздела);
	
	УстановитьФорматВывода(ЭтаФорма, "Раздел1", МакетПечатиРаздела);
	
	МакетПечатиРаздела.ОтображатьЗаголовки = Ложь;
	МакетПечатиРаздела.ОтображатьСетку     = Ложь;
	МакетПечатиРаздела.ЧерноБелыйПросмотр  = Истина;
	МакетПечатиРаздела.ЧерноБелаяПечать    = Истина;
	МакетПечатиРаздела.Автомасштаб         = Истина;
	МакетПечатиРаздела.ОриентацияСтраницы  = ?(СтруктураПараметров.ОриентацияЛиста = "Портрет", ОриентацияСтраницы.Портрет, ОриентацияСтраницы.Ландшафт);
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(МакетПечатиРаздела, СтруктураПараметров.НаименованиеЛиста);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМакетПечатиРаздела(СтруктураПараметров, МакетПечатиРаздела)
	
	Показатели = Новый ТаблицаЗначений;
	Показатели.Колонки.Добавить("Имя");
	Показатели.Колонки.Добавить("Значение");
	Показатели.Колонки.Добавить("ЗначениеПечати");
	
	Показатели.Индексы.Добавить("Имя");
	
	ИсходныйМакет = ТабличныйДокумент;
	Для Каждого Область Из ИсходныйМакет.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И Область.СодержитЗначение = Истина Тогда
			НовСтр = Показатели.Добавить();
			НовСтр.Имя = Область.Имя;
			НовСтр.Значение = Область.Значение;
			НовСтр.ЗначениеПечати = ПолучитьЗначениеПечати(Область);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Область Из МакетПечатиРаздела.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			ИмяОбласти = Область.Имя;
			РезультатПоиска = Показатели.Найти(ИмяОбласти, "Имя");
			Если РезультатПоиска = Неопределено Тогда
				ТекАдресЗначенияОбласти = АдресЗначенияОбласти(Область);
				ЗначениеВыведено = Ложь;
				Если ТекАдресЗначенияОбласти.ИндексЗнакоместа <> 0 Тогда
					РезультатПоиска = Показатели.Найти(ТекАдресЗначенияОбласти.Корень, "Имя");
					Если РезультатПоиска <> Неопределено Тогда
						ПрисвоитьЗначениеОбласти(Область, РезультатПоиска.ЗначениеПечати, ТекАдресЗначенияОбласти.ИндексЗнакоместа, ТипЗнч(РезультатПоиска.Значение) <> Тип("Строка") ИЛИ НЕ ЗначениеЗаполнено(РезультатПоиска.Значение));
						ЗначениеВыведено = Истина;
					КонецЕсли;
				КонецЕсли;
				Если НЕ ЗначениеВыведено И НЕ ИмяОбласти = "Стр" Тогда
					
					Сообщение = Новый СообщениеПользователю;

					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось заполнить область ""%1""!'"), ИмяОбласти);

					Сообщение.Сообщить();
					
				КонецЕсли;
			Иначе
				Если СтруктураПараметров.ИмяСтраницы = "Титульный" Тогда
					Дополнять_ = НЕ (Лев(РезультатПоиска.Имя, СтрДлина("НомСтр")) = "НомСтр"
								ИЛИ Лев(РезультатПоиска.Имя, СтрДлина("СоставленаНа")) = "СоставленаНа");
				Иначе
					Дополнять_ = Лев(РезультатПоиска.Имя, СтрДлина("НомСтр")) <> "НомСтр";
				КонецЕсли;
				ПрисвоитьЗначениеОбласти(Область, РезультатПоиска.ЗначениеПечати, , Дополнять_);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если МакетПечатиРаздела.Области.Найти("Стр") <> Неопределено Тогда
		МакетПечатиРаздела.Области["Стр"].Значение = СтруктураПараметров.НомерСтраницы;
	КонецЕсли;
	Если МакетПечатиРаздела.Области.Найти("СтрВсего") <> Неопределено Тогда
		МакетПечатиРаздела.Области["СтрВсего"].Значение = СтруктураПараметров.ВсегоСтраниц;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресЗначенияОбласти(Область)
	
	Имя = Область.Имя;
	КореньИмениОбласти = Неопределено;
	СтрИндексЗнакоместа = "";
	ИндексЗнакоместа = 0;
	ДлинаИмени = СтрДлина(Имя);
	Для ИндексВхождения = 1 По ДлинаИмени Цикл
		СтрИндексЗнакоместа = Сред(Имя, ДлинаИмени - ИндексВхождения + 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрИндексЗнакоместа) Тогда
			КореньИмениОбласти = Лев(Имя, ДлинаИмени - ИндексВхождения);
			ИндексЗнакоместа = Число(СтрИндексЗнакоместа);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Прав(КореньИмениОбласти, 1) = "_" Тогда
		КореньИмениОбласти = Лев(КореньИмениОбласти, СтрДлина(КореньИмениОбласти) - 1);
	КонецЕсли;
		
	Возврат Новый Структура("Корень, ИндексЗнакоместа", КореньИмениОбласти, ИндексЗнакоместа);
	
КонецФункции

&НаСервере
Функция ПолучитьЗначениеПечати(Область)
	
	ТипЗначения = ТипЗнч(Область.Значение);
	Если ТипЗначения = Тип("Число") Тогда
		ФорматОбласти = Область.Формат;
		СоотвСтрокаФормата = РазобратьСтрокуФормата(ФорматОбласти);
		СоотвСтрокаФормата.Вставить("ЧРД", ".");
		СоотвСтрокаФормата.Вставить("ЧГ", "");
		Результат = Формат(Область.Значение, СобратьСтрокуФормата(СоотвСтрокаФормата));
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		Результат = СокрЛП(Область.Значение);
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Результат = Формат(Область.Значение, ?(ПустаяСтрока(Область.Формат), "ДФ=dd.MM.yyyy", Область.Формат));
	ИначеЕсли ТипЗначения = Тип("Булево") Тогда
		Результат = Формат(Область.Значение, Область.Формат);
	КонецЕсли;
	Возврат СокрЛП(Результат);
	
КонецФункции

&НаСервере
Процедура ПрисвоитьЗначениеОбласти(Область, Знач Значение, ИндексЗнакоместа = 0, Дополнять_ = Истина)
	
	Дополнять_ = Ложь;
	
	Если ИндексЗнакоместа <> 0 Тогда
		Если СтрДлина(Значение) >= ИндексЗнакоместа Тогда
			Значение = Сред(Значение, ИндексЗнакоместа, 1);
		Иначе
			Значение = ?(Дополнять_, "-", "");
		КонецЕсли;
	ИначеЕсли НЕ ЗначениеЗаполнено(Значение) И Дополнять_ Тогда
		Значение = "-";
	КонецЕсли;
	
	Если Область.СодержитЗначение = Истина Тогда
		Область.Значение = Значение;
	Иначе
		Область.Текст = Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РазобратьСтрокуФормата(Знач СтрокаФормата)
	
	ЗначенияПараметров = Новый Соответствие;
	Пока СтрНайти(СтрокаФормата, ";") <> 0 Цикл
		ВхождениеРазделителя = СтрНайти(СтрокаФормата, ";");
		ДоРазделителя = Лев(СтрокаФормата, ВхождениеРазделителя - 1);
		ВхождениеРавно = СтрНайти(ДоРазделителя, "=");
		Если ВхождениеРавно <> 0 И ВхождениеРавно <> 1 Тогда
			ЗначенияПараметров.Вставить(Лев(ДоРазделителя, ВхождениеРавно - 1), Сред(ДоРазделителя, ВхождениеРавно + 1));
		КонецЕсли;
		СтрокаФормата = Сред(СтрокаФормата, ВхождениеРазделителя + 1);
	КонецЦикла;
	ВхождениеРавно = СтрНайти(СтрокаФормата, "=");
	Если ВхождениеРавно <> 0 И ВхождениеРавно <> 1 Тогда
		ЗначенияПараметров.Вставить(Лев(СтрокаФормата, ВхождениеРавно - 1), Сред(СтрокаФормата, ВхождениеРавно + 1));
	КонецЕсли;
	Возврат ЗначенияПараметров;
	
КонецФункции

&НаСервере
Функция СобратьСтрокуФормата(ПараметрыФормата)
	
	СтрокаФормата = "";
	Для Каждого Параметр Из ПараметрыФормата Цикл
		СтрокаФормата = СтрокаФормата + "; " + Параметр.Ключ + "=" + Параметр.Значение;
	КонецЦикла;
	Если НЕ ПустаяСтрока(СтрокаФормата) Тогда
		СтрокаФормата = Сред(СтрокаФормата, 3);
	КонецЕсли;
	Возврат СтрокаФормата;
	
КонецФункции

&НаСервере
Процедура УдалитьОбластиБайконура(ПечатнаяФорма)
	
	МассивИменУдаляемыхОбластей = Новый Массив;
	МассивИменУдаляемыхОбластей.Добавить("ПродавецБайконур");
	МассивИменУдаляемыхОбластей.Добавить("ПродавецБайконурНадпись");
	МассивИменУдаляемыхОбластей.Добавить("ПокупательБайконур");
	МассивИменУдаляемыхОбластей.Добавить("ПокупательБайконурНадпись");
	МассивИменУдаляемыхОбластей.Добавить("КомиссионерБайконур");
	МассивИменУдаляемыхОбластей.Добавить("КомиссионерБайконурНадпись");
	
	Для Каждого ИмяОбласти Из МассивИменУдаляемыхОбластей Цикл
		Если ПечатнаяФорма.Области.Найти(ИмяОбласти) <> Неопределено Тогда
			ПечатнаяФорма.Области[ИмяОбласти].Очистить(Истина, Истина, Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПримечания(ПечатнаяФорма)
	
	ИмяОбласти = "ИННОрг_1";
	Если ПечатнаяФорма.Области.Найти(ИмяОбласти) <> Неопределено Тогда
		ПечатнаяФорма.Области[ИмяОбласти].Примечание.Текст = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПечатнуюФорму(ПараметрыПечати, МакетПечатнойФормы, ОбщиеРеквизитыРазделов)
	
	ИсходныйМакет = ТабличныйДокумент;
	
	Для Каждого Область Из ИсходныйМакет.Области Цикл
		
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И Область.СодержитЗначение Тогда
			
			Если ПараметрыПечати.ИмяСтраницы = "Раздел1" Тогда
				
				Если СтрЧислоВхождений(Область.Имя, "НомерЗаяв") > 0
				 ИЛИ СтрЧислоВхождений(Область.Имя, "Число")> 0
				 ИЛИ СтрЧислоВхождений(Область.Имя, "Месяц") > 0
				 ИЛИ СтрЧислоВхождений(Область.Имя, "Год") > 0  Тогда
					
					Продолжить;
				КонецЕсли;
				
				Если Область.Имя = "ДатаПодписи" Тогда
					МакетПечатнойФормы.Области["ДатаПодписиРук"].Значение = Область.Значение;
					МакетПечатнойФормы.Области["ДатаПодписиБух"].Значение = Область.Значение;
					Продолжить;
				КонецЕсли;
				
			ИначеЕсли ПараметрыПечати.ИмяСтраницы = "Раздел3" Тогда
				
				Если СтрЧислоВхождений(НРег(Область.Имя), "байконур") > 0 Тогда
					Продолжить;
				КонецЕсли;
				
				СплошнаяЛиния = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
				НетЛинии = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
				
				Если Область.Имя = "ТипПрод_3" Тогда
					
					МакетПечатнойФормы.Области["Продавец"].Значение = "Продавец,";
					Если СтрЧислоВхождений(НРег(Область.Значение), "продавец") > 0 Тогда
						МакетПечатнойФормы.Области["Продавец"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Продавец"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					МакетПечатнойФормы.Области["Комитент"].Значение = "комитент,";
					Если СтрЧислоВхождений(НРег(Область.Значение), "комитент") > 0 Тогда
						МакетПечатнойФормы.Области["Комитент"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Комитент"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					МакетПечатнойФормы.Области["Доверитель"].Значение = "доверитель,";
					Если СтрЧислоВхождений(НРег(Область.Значение), "доверитель") > 0 Тогда
						МакетПечатнойФормы.Области["Доверитель"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Доверитель"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					МакетПечатнойФормы.Области["Принципал"].Значение = "принципал";
					Если СтрЧислоВхождений(НРег(Область.Значение), "принципал") > 0 Тогда
						МакетПечатнойФормы.Области["Принципал"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Принципал"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					Продолжить;
					
				ИначеЕсли Область.Имя = "ТипПок_3" Тогда
					
					МакетПечатнойФормы.Области["Покупатель"].Значение = "Покупатель,";
					Если СтрЧислоВхождений(НРег(Область.Значение), "покупатель") > 0 Тогда
						МакетПечатнойФормы.Области["Покупатель"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Покупатель"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					МакетПечатнойФормы.Области["Комиссионер"].Значение = "комиссионер,";
					Если СтрЧислоВхождений(НРег(Область.Значение), "комиссионер") > 0 Тогда
						МакетПечатнойФормы.Области["Комиссионер"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Комиссионер"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					МакетПечатнойФормы.Области["Поверенный"].Значение = "поверенный,";
					Если СтрЧислоВхождений(НРег(Область.Значение), "поверенный") > 0 Тогда
						МакетПечатнойФормы.Области["Поверенный"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Поверенный"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					МакетПечатнойФормы.Области["Агент"].Значение = "агент";
					Если СтрЧислоВхождений(НРег(Область.Значение), "агент") > 0 Тогда
						МакетПечатнойФормы.Области["Агент"].ГраницаСнизу = СплошнаяЛиния;
					Иначе
						МакетПечатнойФормы.Области["Агент"].ГраницаСнизу = НетЛинии;
					КонецЕсли;
					
					Продолжить;
					
				КонецЕсли;
				
			КонецЕсли;
			
			МакетПечатнойФормы.Области[Область.Имя].Значение = Область.Значение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Реквизит Из ОбщиеРеквизитыРазделов Цикл
		
		Индекс = 1;
		Постфикс = "_1";
		Разделитель = Истина;
		
		МакетПечатнойФормы.Области[Реквизит.Ключ].Значение = Реквизит.Значение;
		
	КонецЦикла;
	
	Если НЕ ПараметрыПечати.ИмяСтраницы = "Приложение" Тогда
		Если МакетПечатнойФормы.Области.Найти("Стр") <> Неопределено Тогда
			МакетПечатнойФормы.Области["Стр"].Значение = ПараметрыПечати.НомерСтраницы;
		КонецЕсли;
		Если МакетПечатнойФормы.Области.Найти("СтрВсего") <> Неопределено Тогда
			МакетПечатнойФормы.Области["СтрВсего"].Значение = ПараметрыПечати.ВсегоСтраниц;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗагрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузкаОтчетаИзФайла(ДанныеДляЗагрузки) Экспорт
	
	Если Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(НСтр("ru='Отчет ""%1"" не сохранен.'"), ЭтотОбъект.Заголовок);
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки)
	
	СформироватьСтруктуруПоказателей();
	
	ПараметрыОтчета = СформироватьСтруктуруПараметров();
	
	ДеревоДляЗагрузки = ИзвлечьСтруктуруXML();
	
	Если НЕ РегламентированнаяОтчетностьЗагрузка.ЗагрузкаОтчетаИзФайла(
		ЭтотОбъект, ПараметрыОтчета, ДанныеДляЗагрузки, ДеревоДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчет() Экспорт
			
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтаФорма);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.СформироватьИЗаписать(СпДокОсн);
	
КонецПроцедуры

&НаКлиенте
Функция УниверсальнаяВыгрузка(ТекстВыгрузки = Неопределено, ПроверятьБлокировки = Истина) Экспорт
	
	Перем ПараметрыВыгрузки;
				
	Если НЕ УниверсальнаяВыгрузкаXMLНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLНаСервере(ТекстВыгрузки, ПараметрыВыгрузки)
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	
	Если НЕ ВыгрузкаВозможна(ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстВыгрузки = ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки);
	
	КаталогВремФайлов = "";
	ИмяФайла = ПараметрыВыгрузки.ИдФайл + ".xml";
	ИмяВременногоФайла = РегламентированнаяОтчетностьКлиентСервер.ПолучитьПолноеИмяВременногоФайла(
																КаталогВремФайлов, ИмяФайла, Истина);
	
	ПотокЗаписи = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ПотокЗаписи.Записать(ТекстВыгрузки);
	ПотокЗаписи.Закрыть();
		
	ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	ПараметрыВыгрузки.Вставить("АдресФайлаВыгрузкиВоВременномХранилище", ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор()));
	
	УдалитьФайлы(КаталогВремФайлов);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СформироватьСтруктуруПоказателей()
	
	мДанныеОтчета = Новый Структура;
	
	СохрСтруктура = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		мДанныеОтчета.Вставить(Раздел.Ключ, РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
	КонецЦикла;

	ТекущийРаздел = Неопределено;
	Если мСтруктураМногостраничныхРазделов.Свойство(СтруктураРеквизитовФормы.НаимТекущегоРаздела, ТекущийРаздел) Тогда
		АктивнаяСтраница = ЭтаФорма[ТекущийРаздел].НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
		Если АктивнаяСтраница.Количество() > 0 Тогда
			АктивнаяСтраница = АктивнаяСтраница[0];
			ИндексАктивнойСтраницы = ЭтаФорма[ТекущийРаздел].Индекс(АктивнаяСтраница);
		КонецЕсли;
	КонецЕсли;

	Для Каждого ЭлементСтруктуры Из СохрСтруктура.ДанныеМногостраничныхРазделов Цикл
		
		НайденноеЗначение = Неопределено;
		
		мСтруктураМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, НайденноеЗначение);
		
		Если НЕ НайденноеЗначение = Неопределено Тогда
			
			НайденноеЗначение = ЭтаФорма[НайденноеЗначение];
			
			ЗначениеВДанныеФормы(ЭлементСтруктуры.Значение, НайденноеЗначение);
			
			Если НайденноеЗначение[0].Свойство("Данные") Тогда
				
				Для Каждого СтрокаТаблЗнач Из НайденноеЗначение Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.Данные) = Тип("СписокЗначений") Тогда
						
						СтрокаТаблЗнач.Данные.Очистить();
						СтрокаТаблЗнач.Данные.Добавить(ЭлементСтруктуры.Значение[НайденноеЗначение.Индекс(СтрокаТаблЗнач)].Данные);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если НайденноеЗначение[0].Свойство("ДополнительныеДанные") Тогда
				
				Для Каждого СтрокаТаблЗнач Из НайденноеЗначение Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.ДополнительныеДанные) = Тип("СписокЗначений") Тогда
						
						СтрокаТаблЗнач.ДополнительныеДанные.Очистить();
						СтрокаТаблЗнач.ДополнительныеДанные.Добавить(ЭлементСтруктуры.Значение[НайденноеЗначение.Индекс(СтрокаТаблЗнач)].ДополнительныеДанные);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
				
			Если НайденноеЗначение[0].Свойство("ДанныеДопСтрок") Тогда
				
				Ном = 0;
				
				Для Каждого СтрокаТаблЗнач Из ЭлементСтруктуры.Значение Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("Структура") Тогда
						
						Для Каждого ЭлСтруктуры Из СтрокаТаблЗнач.ДанныеДопСтрок Цикл
							
							ТаблЗнач = ЭлСтруктуры.Значение;
							
							ЭлСтруктуры.Значение = ЭтаФорма["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
							
							ЗначениеВДанныеФормы(ТаблЗнач, ЭлСтруктуры.Значение);
							
							НайденноеЗначение[Ном].ДанныеДопСтрок.Добавить(ЭлСтруктуры.Значение);
							
						КонецЦикла;
						
					ИначеЕсли ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("ТаблицаЗначений") Тогда
						
						СтрокаТаблЗначДанныеДопСтрок = ЭтаФорма["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
						
						ЗначениеВДанныеФормы(СтрокаТаблЗнач.ДанныеДопСтрок, СтрокаТаблЗначДанныеДопСтрок);
						
						НайденноеЗначение[Ном].ДанныеДопСтрок.Добавить(СтрокаТаблЗначДанныеДопСтрок, "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела");
						
					КонецЕсли;
					
					Ном = Ном + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		Для Каждого Страница Из ЭтаФорма[Раздел.Значение] Цикл
			Страница.АктивнаяСтраница = Ложь;
		КонецЦикла;
	КонецЦикла;

	ТекущийРаздел = Неопределено;
	Если мСтруктураМногостраничныхРазделов.Свойство(СтруктураРеквизитовФормы.НаимТекущегоРаздела, ТекущийРаздел) Тогда
		Если ИндексАктивнойСтраницы <> Неопределено Тогда
			ЭтаФорма[ТекущийРаздел][ИндексАктивнойСтраницы].АктивнаяСтраница = Истина;
		КонецЕсли;
	КонецЕсли;

	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		ТаблицаЛистовРаздела = ЭтаФорма[Раздел.Значение];
		Для Каждого Лист Из ТаблицаЛистовРаздела Цикл
			Лист.Данные[0].Значение = РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Лист.Данные[0].Значение);
		КонецЦикла;
		мДанныеОтчета.Вставить(Раздел.Ключ, ЭтаФорма[Раздел.Значение]);
	КонецЦикла;

КонецФункции

&НаСервере
Функция СформироватьСтруктуруПараметров()
	
	мСтруктураРеквизитовВыгрузки = Неопределено;
	ДанныеСохраненногоОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	Если НЕ ДанныеСохраненногоОтчета.Свойство("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки) Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
	КонецЕсли;
	
	ПараметрыВыгрузки = Новый Структура;
    	
	ЭтоПБОЮЛ = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);

	ПараметрыВыгрузки.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);
	
	ПараметрыВыгрузки.Вставить("ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	ПараметрыВыгрузки.Вставить("ДатаДок", Формат(ТекущаяДатаСеанса(), "ДФ=дд.ММ.гггг"));
	ПараметрыВыгрузки.Вставить("ДатаДокДата", ТекущаяДатаСеанса()); // Используется для проверки даты выгрузки документа
	
	ПараметрыВыгрузки.Вставить("КодНО", мСтруктураРеквизитовВыгрузки.КодИФНС);
	
	Если мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ Тогда
		ПараметрыВыгрузки.Вставить("НаимОтпр", 		мСтруктураРеквизитовВыгрузки.НаимОтпр);
		ПараметрыВыгрузки.Вставить("ИННЮЛОтпр",		мСтруктураРеквизитовВыгрузки.ИННЮЛОтпр);
		ПараметрыВыгрузки.Вставить("КППОтпр",       мСтруктураРеквизитовВыгрузки.КППОтпр);
	Иначе
		ПараметрыВыгрузки.Вставить("ИННФЛОтпр",     мСтруктураРеквизитовВыгрузки.ИННФЛОтпр);	
		ПараметрыВыгрузки.Вставить("ФамилияФЛОтпр", мСтруктураРеквизитовВыгрузки.ФамилияФЛОтпр);
		ПараметрыВыгрузки.Вставить("ИмяФЛОтпр",     мСтруктураРеквизитовВыгрузки.ИмяФЛОтпр);
		ПараметрыВыгрузки.Вставить("ОтчествоФЛОтпр",мСтруктураРеквизитовВыгрузки.ОтчествоФЛОтпр);	
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ПрПодп", Формат(мСтруктураРеквизитовВыгрузки.ПрПодп, ""));
	
	Если мСтруктураРеквизитовВыгрузки.ПрПодп = 3 Тогда
		ПараметрыВыгрузки.Вставить("НаимДовПодп",  мСтруктураРеквизитовВыгрузки.НаимДовПодп);
		ПараметрыВыгрузки.Вставить("НомерДовПодп", мСтруктураРеквизитовВыгрузки.НомерДовПодп);
		ПараметрыВыгрузки.Вставить("ДатаДовПодп",  Формат(мСтруктураРеквизитовВыгрузки.ДатаДовПодп, "ДФ=дд.ММ.гггг"));
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ИННФЛПодп",     мСтруктураРеквизитовВыгрузки.ИННФЛПодп);
	ПараметрыВыгрузки.Вставить("ДолжностьПодп", мСтруктураРеквизитовВыгрузки.ДолжностьПодп);
	
	ПараметрыВыгрузки.Вставить("ФамилияПодп",  мСтруктураРеквизитовВыгрузки.ФамилияПодп);
	ПараметрыВыгрузки.Вставить("ИмяПодп",      мСтруктураРеквизитовВыгрузки.ИмяПодп);
	ПараметрыВыгрузки.Вставить("ОтчествоПодп", мСтруктураРеквизитовВыгрузки.ОтчествоПодп);
	
	ПараметрыВыгрузки.Вставить("ИмяФайлаСхемы",             "ON_ZVLRPOK_2_430_14_05_07_01.xsd");
	ПараметрыВыгрузки.Вставить("ПрефиксИмениФайлаВыгрузки", "ON_ZVLRPOK");
	
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "Документ");

	ПараметрыВыгрузки.Вставить("ИдФайл", СформироватьИдФайл(ПараметрыВыгрузки));
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

&НаСервере
Функция ИзвлечьСтруктуруXML()
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет("СхемаВыгрузки507");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 1).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки)
	
	ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ДеревоВыгрузки); // формируем точную структуру XML-макета
	ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, ДеревоВыгрузки); // заполняем дерево данными
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
		
КонецПроцедуры

&НаСервере
Функция ПроверитьДанные(Узел)
	
	СтатусПроверки = Истина;
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если НЕ ЗначениеСоответствуетФормату(Узел) Тогда
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	Для Каждого Стр из Узел.Строки Цикл
		СтатусПроверки = (ПроверитьДанные(Стр) И СтатусПроверки); // именно в указанном порядке для выдачи всех ошибок
	КонецЦикла;
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки)
	
	ПотокXML = РегламентированнаяОтчетность.СоздатьНовыйПотокXML(); // создаем новый поток для записи
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, ПараметрыВыгрузки); // пишем дерево в поток
	ТекстДляЗаписи = ПотокXML.Закрыть(); // получаем текст XML
	ТекстДляЗаписи = "<?xml version=""1.0"" encoding=""windows-1251""?>" + Сред(ТекстДляЗаписи, СтрНайти(ТекстДляЗаписи, Символы.ПС));
	Возврат ТекстДляЗаписи;
	
КонецФункции

&НаСервере
Функция СформироватьИдФайл(ПараметрыВыгрузки)
	
	ИдентОтпр = "";
	Если ПараметрыВыгрузки.Свойство("ИННФЛОтпр") И ЗначениеЗаполнено(ПараметрыВыгрузки.ИННФЛОтпр) Тогда
		ИдентОтпр = ПараметрыВыгрузки.ИННФЛОтпр;
	ИначеЕсли ПараметрыВыгрузки.Свойство("ИННЮЛОтпр") И ЗначениеЗаполнено(ПараметрыВыгрузки.ИННЮЛОтпр) Тогда
		ИдентОтпр = ПараметрыВыгрузки.ИННЮЛОтпр + ПараметрыВыгрузки.КППОтпр;
	КонецЕсли;
	
	Возврат ПараметрыВыгрузки.ПрефиксИмениФайлаВыгрузки
		+ "_" + ПараметрыВыгрузки.КодНО
		+ "_" + ПараметрыВыгрузки.КодНО
		+ "_" + ИдентОтпр
		+ "_" + Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд")
		+ "_" + Строка(Новый УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбработатьУсловныеЭлементы(Знач ПараметрыВыгрузки, Узел)
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
			Возврат;
		КонецЕсли;
		РегламентированнаяОтчетность.ДополнитьСтруктуруПараметровПрочиеОтчеты(мДанныеОтчета, ПараметрыВыгрузки, Узел);
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ РегламентированнаяОтчетность.УсловиеВыполнено(ЭтаФорма, ПараметрыВыгрузки, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Узел, НомерСтроки = Неопределено)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, "ИмяКлючевогоУзлаСодержательнойЧасти") И ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти = Стр.Код Тогда
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр);	
				ЗаполнитьСодержательнуюЧасть(Стр);
			Иначе
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, Стр.Ключ) Тогда
					ВывестиПоказательВXML(Стр, ПараметрыВыгрузки[Стр.Ключ]);
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				Ключ = Сред(Стр.ЗначениеПоУмолчанию, 2);
				Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, Ключ) Тогда
					ВывестиПоказательВXML(Стр, ПараметрыВыгрузки[Ключ]);
				КонецЕсли;
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (СтрНайти(Узел.Обязательность, "Н") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		ИначеЕсли (СтрНайти(Узел.Обязательность, "М") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
		И УзелПуст(Узел)
		И ?(СтрНайти(Узел.Обязательность, "О") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "O") <> 0, РегламентированнаяОтчетность.ИмеютсяАналогичныеСоседниеУзлыКлюч(Узел), Истина) Тогда
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗначениеСоответствуетФормату(Стр)
	
	СтатусПроверки = Истина;
	Если Стр.Формат = "Т" ИЛИ Стр.Формат = "T" Тогда // учтем оба варианта: кириллицу и латиницу
		ДлинаЗначения = СтрДлина(Стр.Значение);
		Если ДлинаЗначения < Стр.МинРазмерность Тогда
			СтатусПроверки = Ложь;
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Формирование XML: Длина значения показателя """ + РегламентированнаяОтчетность.ПредставлениеПоказателя(Стр) + """ (" + АдресПоказателя(Стр)+ ") меньше минимально возможной (" + ДлинаЗначения + " < " + Стр.МинРазмерность + ")", "Титульный", "");
		КонецЕсли;
		Если ДлинаЗначения > Стр.МаксРазмерность Тогда
			СтатусПроверки = Ложь;
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Формирование XML: Длина значения показателя """ + РегламентированнаяОтчетность.ПредставлениеПоказателя(Стр) + """  (" + АдресПоказателя(Стр)+ ") больше максимально возможной (" + ДлинаЗначения + " > " + Стр.МаксРазмерность + ")", "Титульный", "");
		КонецЕсли;
	КонецЕсли;
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Функция АдресПоказателя(СтрокаПоказателя)
	
	Результат = СтрокаПоказателя.Код;
	
	СтрокаВерхнегоУровня = СтрокаПоказателя.Родитель;
	Пока ЗначениеЗаполнено(СтрокаВерхнегоУровня) Цикл
		Результат = СтрокаВерхнегоУровня.Код + "\" + Результат;
		СтрокаВерхнегоУровня = СтрокаВерхнегоУровня.Родитель;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, ПараметрыВыгрузки) Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		ПотокXML.ЗаписатьАтрибут("xsi:noNamespaceSchemaLocation", ПараметрыВыгрузки.ИмяФайлаСхемы);
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, ПараметрыВыгрузки);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда 
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, ПараметрыВыгрузки);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КодПричиныВозникновенияЗаявления(ПредставлениеПричины)
	
	КодПричины = "0"; // Недопустимый код для обработки ошибочного ввода
	
	Если НРег(СокрЛП(ПредставлениеПричины)) = "первичное заявление" Тогда
		КодПричины = "1";
	ИначеЕсли НРег(СокрЛП(ПредставлениеПричины)) = "взамен ранее представленного" Тогда
		КодПричины = "4";
	ИначеЕсли НРег(СокрЛП(ПредставлениеПричины)) = "корректировка ранее представленного при увеличении цены" Тогда
		КодПричины = "5";
	ИначеЕсли НРег(СокрЛП(ПредставлениеПричины)) = "взамен ранее представленного при частичном возврате товара" Тогда
		КодПричины = "6";
	КонецЕсли;
	
	Возврат КодПричины;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСодержательнуюЧасть(УзелРодитель)
	
	Раздел1    = мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	Раздел3    = мДанныеОтчета.ПолеТабличногоДокументаРаздел3;
	ДополнитьСтруктуруДанныхСвойствамиСоЗначениямиПоУмолчанию_Раздел3(Раздел3);
	Приложение = мДанныеОтчета.Приложение;
	
	ДанныеСохраненногоОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	
	ДополнительныеДанныеОдностраничныхРазделов = ДанныеСохраненногоОтчета.ДополнительныеДанныеРазделов;
	
	Узел_СвЗвл = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СвЗвл");
	ЗаполнитьДаннымиУзел(Раздел1, Узел_СвЗвл);
	УстановитьЗначениеЭлемента(Узел_СвЗвл, "НомерДокНП", Формат(Раздел1.НомерЗаяв,"ЧН=; ЧГ=0"));
	УстановитьЗначениеЭлемента(Узел_СвЗвл, "ДатаДокНП",  Формат(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи, "ДФ=дд.ММ.гггг"));
	УстановитьЗначениеЭлемента(Узел_СвЗвл, "ПрЛизинг",   ?(СокрЛП(Раздел1.ПрЛизинг) = "", "0", "1"));
	УстановитьЗначениеЭлемента(Узел_СвЗвл, "ПрДавСырья", ?(СокрЛП(Раздел1.ПрДавСырья) = "", "0", "1"));
	
	КодПричины = КодПричиныВозникновенияЗаявления(СокрЛП(Раздел1.ПричинаЗаявления));
	УстановитьЗначениеЭлемента(Узел_СвЗвл, "ПВДок", КодПричины);
	
	Если КодПричины = "4" ИЛИ КодПричины = "6" Тогда
		УстановитьЗначениеЭлемента(Узел_СвЗвл, "НомОтм", СокрЛП(Раздел1.НомерОтметки));
		УстановитьЗначениеЭлемента(Узел_СвЗвл, "ДатаОтм", Раздел1.ДатаОтметки);
	КонецЕсли;
	
	Узел_СвКонтракт1 = ПолучитьПодчиненныйЭлемент(Узел_СвЗвл, "СвКонтракт1");
	
	Если НЕ ПустаяСтрока(Раздел1.ПродавецБайконур) Тогда
		УстановитьЗначениеЭлемента(Узел_СвКонтракт1, "ПрБкнрПродР1", "1");
	КонецЕсли;
	УстановитьЗначениеЭлемента(Узел_СвКонтракт1, "ИдНомПродР1", СокрЛП(Раздел1.ПродавецИНН_КПП));
	УстановитьЗначениеЭлемента(Узел_СвКонтракт1, "ПрПродФЛ", ?(СокрЛП(Раздел1.ПрПриобретенияУФЛ) = "", "0", "1"));
	
	Если НЕ ПустаяСтрока(Раздел1.ПокупательБайконур) Тогда
		УстановитьЗначениеЭлемента(Узел_СвКонтракт1, "ПрБкнрПокР1", "1");
	КонецЕсли;
	УстановитьЗначениеЭлемента(Узел_СвКонтракт1, "ИдНомПокР1", СокрЛП(Раздел1.ПокупательИНН_КПП));
	
	Узел_СвКонтр1 = ПолучитьПодчиненныйЭлемент(Узел_СвКонтракт1, "СвКонтр1");
	
	ПрототипУзла_СвСпециф = ПолучитьПодчиненныйЭлемент(Узел_СвКонтр1, "СвСпециф");
	
	Для Сч = 1 По мТЗСпецификацийРаздел1.Количество() Цикл
		Строка = мТЗСпецификацийРаздел1[Сч - 1];
		
		Узел_СвСпециф = НовыйУзелИзПрототипа(ПрототипУзла_СвСпециф);
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомПСпециф", Сч);
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомСпециф",  СокрЛП(Строка.Номер));
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	Узел_СвКонтрКомисс = ПолучитьПодчиненныйЭлемент(Узел_СвЗвл, "СвКонтрКомисс");
	
	Если НЕ ПустаяСтрока(Раздел1.КомиссионерБайконур) Тогда
		УстановитьЗначениеЭлемента(Узел_СвКонтрКомисс, "ПрБкнрОрг", "1");
	КонецЕсли;
	УстановитьЗначениеЭлемента(Узел_СвКонтрКомисс, "ИдНомОрг", СокрЛП(Раздел1.ИННОрг_1));
	
	Узел_СвКонтракт2 = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрКомисс, "СвКонтракт2");
	
	ПрототипУзла_СвСпециф = ПолучитьПодчиненныйЭлемент(Узел_СвКонтракт2, "СвСпециф");
	
	Для Сч = 1 По мТЗСпецификацийРаздел1_07.Количество() Цикл
		Строка = мТЗСпецификацийРаздел1_07[Сч - 1];
		
		Узел_СвСпециф = НовыйУзелИзПрототипа(ПрототипУзла_СвСпециф);
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомПСпециф", Сч);
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомСпециф",  СокрЛП(Строка.Номер));
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	ПрототипУзла_СвТовар = ПолучитьПодчиненныйЭлемент(Узел_СвЗвл, "СвТовар");
	ЧислоВыгруженныхСтрок = 0;
	НомерСтроки = 1;
	НомерСтрокиСтр = "1";
	Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Раздел1, "П000010000101_" + НомерСтрокиСтр) Цикл
		
		Если ЗначениеЗаполнено(Раздел1["П000010000102_" + НомерСтрокиСтр]) Тогда
			ЧислоВыгруженныхСтрок = ЧислоВыгруженныхСтрок + 1;
			Узел_СвТовар = НовыйУзелИзПрототипа(ПрототипУзла_СвТовар);
			
			ВыгрузитьСтрокуМногострочнойЧасти(Раздел1, Узел_СвТовар, НомерСтроки);
			
			УстановитьЗначениеЭлемента(Узел_СвТовар, "НомТовПП", Формат(ЧислоВыгруженныхСтрок, "ЧГ="));
			
			// Единица измерения.
			ИмяСлужебногоПоля = "П000010000122_" + НомерСтрокиСтр;
			
			Если Раздел1.Свойство(ИмяСлужебногоПоля) И СокрЛП(Раздел1[ИмяСлужебногоПоля]) <> "" Тогда
				ИмяПоляЕдИзм = ИмяСлужебногоПоля;
			Иначе
				ИмяПоляЕдИзм = "П000010000104_" + НомерСтрокиСтр;
			КонецЕсли;
			
			УстановитьЗначениеЭлемента(Узел_СвТовар, "ЕдИзмТов", СокрЛП(Раздел1[ИмяПоляЕдИзм]));
			
			КодВалюты = СокрЛП(Раздел1["П000010000107_" + НомерСтрокиСтр]);
			БазаВалюты = 0;
			Если Раздел1.Свойство("П000010000125_" + НомерСтрокиСтр) Тогда
				БазаВалюты = Раздел1["П000010000125_" + НомерСтрокиСтр];
			КонецЕсли;
			Если КодВалюты = "643" ИЛИ БазаВалюты = 0 Тогда
				БазаВалюты = 1;
			КонецЕсли;
			УстановитьЗначениеЭлемента(Узел_СвТовар, "БазаВал", БазаВалюты);
			
			СтАкцизТверд = СтрЗаменить(Раздел1["П000010000116_" + НомерСтрокиСтр], ",", ".");
			СтАкцизАдвал = СтрЗаменить(Раздел1["П000010000117_" + НомерСтрокиСтр], ",", ".");
			СтНДС        = СтрЗаменить(Раздел1["П000010000118_" + НомерСтрокиСтр], ",", ".");
			
			СтруктураСтавок = Новый Структура;
			СтруктураСтавок.Вставить("Ст16", СтАкцизТверд);
			СтруктураСтавок.Вставить("Ст17", СтАкцизАдвал);
			СтруктураСтавок.Вставить("Ст18", СтНДС);
			СтруктураСтавок.Вставить("Ст21", Раздел1["П000010000121_" + НомерСтрокиСтр]);
			
			СтруктураСтавокМодиф = АнализироватьПоляСтавки(СтруктураСтавок);
			
			ПрОсвАкциз = СтруктураСтавокМодиф.Переключатель3;
			ПрОсвНДС   = СтруктураСтавокМодиф.Переключатель1;
			УстановитьЗначениеЭлемента(Узел_СвТовар, "ПрОсвАкциз", ПрОсвАкциз);
			УстановитьЗначениеЭлемента(Узел_СвТовар, "ПрОсвНДС",   ПрОсвНДС);
			
			// Значение единицы измерения дополнительной величины налоговой базы.
			ЗначениеКоличТов = Раздел1["П000010000105_" + НомерСтрокиСтр];
			ЗначениеНБАкциз = Раздел1["П000010000114_" + НомерСтрокиСтр];
			ЗначениеПрОсвАкциз = СтруктураСтавокМодиф.Переключатель3;
			ЗаполнятьЕдИзмТовНБАкц = (ЗначениеКоличТов <> ЗначениеНБАкциз)
			                       И (ЗначениеКоличТов <>0)
			                       И (ЗначениеПрОсвАкциз <> 3 И ЗначениеПрОсвАкциз <> 4);
			Если ЗаполнятьЕдИзмТовНБАкц Тогда
				ИмяСлужебногоПоля = "П000010000124_" + НомерСтрокиСтр;
				ИмяПоляПредставления = "П000010000123_" + НомерСтрокиСтр;
				Если Раздел1.Свойство(ИмяСлужебногоПоля) И НЕ ПустаяСтрока(Раздел1[ИмяСлужебногоПоля])Тогда
					ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
				Иначе
					// При вводе значения не через подбор из списка используется значение введенное вручную.
					ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
				КонецЕсли;
				УстановитьЗначениеЭлемента(Узел_СвТовар, "ЕдИзмТовНБАкц", ЗначениеЕдИзмТовНБАкц);
			КонецЕсли;
			
			// Поскольку на форме эти показатели строковые - при выгрузке приводим к числовому виду.
			ЗаполненаТвердаяСтавкаАкцизов = (СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ (ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)) И Число(СтАкцизТверд) <> 0));
			СтАкцизТверд = ?(СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)), "0.00", СтАкцизТверд);
			СтАкцизАдвал = ?(СтАкцизАдвал = "Льгота" ИЛИ СтАкцизАдвал = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизАдвал)), "0.00", СтАкцизАдвал);
			СтНДС = ?(СтНДС = "Льгота" ИЛИ СтНДС = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтНДС)), "0.00", СтНДС);
			
			Если ЗаполненаТвердаяСтавкаАкцизов Тогда
				Если ПрОсвАкциз = 4 Тогда
					ОписаниеТипа = Новый ОписаниеТипов("Число");
					СтАкцизТверд = ОписаниеТипа.ПривестиЗначение(СтАкцизТверд);
				КонецЕсли;
				УстановитьЗначениеЭлемента(Узел_СвТовар, "СтАкцизТверд", ?(ПрОсвАкциз = 2, "-1", СтАкцизТверд));
			Иначе
				УстановитьЗначениеЭлемента(Узел_СвТовар, "СтАкцизАдвал", ?(ПрОсвАкциз = 2, "-1", СтАкцизАдвал));
			КонецЕсли;
			УстановитьЗначениеЭлемента(Узел_СвТовар, "СтНДС", ?(ПрОсвНДС = 2, "-1", СтНДС));
			
			// Сведения о товарно-транспортных документах.
			ПрототипУзла_СвТСД = ПолучитьПодчиненныйЭлемент(Узел_СвТовар, "СвТСД");
			ТТДокументы = Неопределено;
			Если НЕ мСтрукТЗДокументы.Свойство("К_" + НомерСтрокиСтр, ТТДокументы) ИЛИ НЕ ЗначениеЗаполнено(ТТДокументы) Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого СтрТТДокумент Из ТТДокументы Цикл
				ТТДСерия = ВРЕГ(СокрЛП(СтрТТДокумент.Серия));
				ТТДНомер = ВРЕГ(СокрЛП(СтрТТДокумент.Номер));
				ТТДДата = СтрТТДокумент.Дата;
				
				Если НЕ ЗначениеЗаполнено(ТТДСерия) И НЕ ЗначениеЗаполнено(ТТДНомер) И НЕ ЗначениеЗаполнено(ТТДДата) Тогда
					Продолжить;
				КонецЕсли;
				
				Узел_СвТСД = НовыйУзелИзПрототипа(ПрототипУзла_СвТСД);
				УстановитьЗначениеЭлемента(Узел_СвТСД, "СерНомТСД", ТТДСерия + ?(ПустаяСтрока(ТТДСерия) ИЛИ ПустаяСтрока(ТТДНомер), "", ", ") + ТТДНомер);
				УстановитьЗначениеЭлемента(Узел_СвТСД, "ДатаТСД", Формат(ТТДДата, "ДФ=dd.MM.yyyy"));
			КонецЦикла;
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
		НомерСтрокиСтр = Формат(НомерСтроки, "ЧГ=");
	КонецЦикла;
	
	// Выгрузка раздела 3.
	Узел_СвКонтракт3 = ПолучитьПодчиненныйЭлемент(Узел_СвЗвл, "СвКонтракт3");
	ЗаполнитьДаннымиУзел(Раздел3, Узел_СвКонтракт3);
	
	// Тип продавца.
	стрТипПрод = Раздел3.ТипПрод_3;
	Если стрТипПрод = "(комитент)" Тогда
		ТипПрод_3 = 0;
	ИначеЕсли стрТипПрод = "(доверитель)" Тогда
		ТипПрод_3 = 1;
	ИначеЕсли стрТипПрод = "(принципал)" Тогда
		ТипПрод_3 = 2;
	ИначеЕсли стрТипПрод = "(продавец)" Тогда
		ТипПрод_3 = 6;
	Иначе
		ТипПрод_3 = "";
	КонецЕсли;
	УстановитьЗначениеЭлемента(Узел_СвКонтракт3, "ТипПродР3", ТипПрод_3);
	
	// Тип покупателя.
	стрТипПок = Раздел3.ТипПок_3;
	Если стрТипПок = "(комиссионер)" Тогда
		ТипПок_3 = 3;
	ИначеЕсли стрТипПок = "(поверенный)" Тогда
		ТипПок_3 = 4;
	ИначеЕсли стрТипПок = "(агент)" Тогда
		ТипПок_3 = 5;
	ИначеЕсли стрТипПок = "(покупатель)" Тогда
		ТипПок_3 = 7;
	Иначе
		ТипПок_3 = "";
	КонецЕсли;
	УстановитьЗначениеЭлемента(Узел_СвКонтракт3, "ТипПокР3", ТипПок_3);
	
	Если НЕ ПустаяСтрока(Раздел3.ПродавецБайконур) Тогда
		УстановитьЗначениеЭлемента(Узел_СвКонтракт3, "ПрБкнрПродР3", "1");
	КонецЕсли;
	Если НЕ ПустаяСтрока(Раздел3.ПокупательБайконур) Тогда
		УстановитьЗначениеЭлемента(Узел_СвКонтракт3, "ПрБкнрПокР3", "1");
	КонецЕсли;
	
	УстановитьЗначениеЭлемента(Узел_СвКонтракт3, "ИдНомПродР3", СокрЛП(Раздел3.ПродавецИНН_КПП));
	
	УстановитьЗначениеЭлемента(Узел_СвКонтракт3, "ИдНомПокР3", СокрЛП(Раздел3.ПокупательИНН_КПП));
	
	Узел_СвКонтрР3 = ПолучитьПодчиненныйЭлемент(Узел_СвКонтракт3, "СвКонтрР3");
	ПрототипУзла_УзелСвСпециф = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрР3, "СвСпециф");
	
	Для Сч = 1 По мТЗСпецификацийРаздел3.Количество() Цикл
		Строка = мТЗСпецификацийРаздел3[Сч - 1];
		
		Узел_СвСпециф = НовыйУзелИзПрототипа(ПрототипУзла_УзелСвСпециф);
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомПСпециф", Сч);
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомСпециф", СокрЛП(Строка.Номер));
		УстановитьЗначениеЭлемента(Узел_СвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	Если КодПричины = "5" Тогда
		Узел_СвПредДок = ПолучитьПодчиненныйЭлемент(Узел_СвЗвл, "СвПредДок");
		УстановитьЗначениеЭлемента(Узел_СвПредДок, "НомОтм", СокрЛП(Раздел1.НомерОтметки));
		УстановитьЗначениеЭлемента(Узел_СвПредДок, "ДатаОтм", Раздел1.ДатаОтметки);
		УстановитьЗначениеЭлемента(Узел_СвПредДок, "МесУвелЦены", "--" + Формат(Раздел1.ИзмениеЦеныМесяц, "ЧЦ=2; ЧН=; ЧВН="));
		УстановитьЗначениеЭлемента(Узел_СвПредДок, "ГодУвелЦены", Формат(Раздел1.ИзмениеЦеныГод, "ЧЦ=4; ЧН=; ЧВН=; ЧГ=0"));
	КонецЕсли;
	
	НомерПриложения = 0;
	ПрототипУзла_СвКонтрПр = ПолучитьПодчиненныйЭлемент(УзелРодитель, "СвКонтрПр");
	Для Каждого ЛистПриложения Из Приложение Цикл
		
		Индекс = Приложение.Индекс(ЛистПриложения);
		
		ДанныеЛиста = ЛистПриложения.Данные[0].Значение;
		
		ДополнительныеДанныеЛиста = ЛистПриложения.ДополнительныеДанные[0].Значение;
		
		Узел_СвКонтрПр = НовыйУзелИзПрототипа(ПрототипУзла_СвКонтрПр);
		ЗаполнитьДаннымиУзел(ДанныеЛиста, Узел_СвКонтрПр);
		
		стрТипПрод = ДанныеЛиста.ТипПрод_Пр;
		Если стрТипПрод = "(комитент)" Тогда
			ТипПрод_Пр = 0;
		ИначеЕсли стрТипПрод = "(доверитель)" Тогда
			ТипПрод_Пр = 1;
		ИначеЕсли стрТипПрод = "(принципал)" Тогда
			ТипПрод_Пр = 2;
		ИначеЕсли стрТипПрод = "(комиссионер)" Тогда
			ТипПрод_Пр = 3;
		ИначеЕсли стрТипПрод = "(поверенный)" Тогда
			ТипПрод_Пр = 4;
		ИначеЕсли стрТипПрод = "(агент)" Тогда
			ТипПрод_Пр = 5;
		ИначеЕсли стрТипПрод = "(продавец)" Тогда
			ТипПрод_Пр = 6;
		Иначе
			ТипПрод_Пр = "";
		КонецЕсли;
		
		УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "ТипПродП", ТипПрод_Пр);
		
		стрТипПок = ДанныеЛиста.ТипПок_Пр;
		Если стрТипПок = "(комитент)" Тогда
			ТипПок_Пр = 0;
		ИначеЕсли стрТипПок = "(доверитель)" Тогда
			ТипПок_Пр = 1;
		ИначеЕсли стрТипПок = "(принципал)" Тогда
			ТипПок_Пр = 2;
		ИначеЕсли стрТипПок = "(комиссионер)" Тогда
			ТипПок_Пр = 3;
		ИначеЕсли стрТипПок = "(поверенный)" Тогда
			ТипПок_Пр = 4;
		ИначеЕсли стрТипПок = "(агент)" Тогда
			ТипПок_Пр = 5;
		ИначеЕсли стрТипПок = "(покупатель)" Тогда
			ТипПок_Пр = 7;
		Иначе
			ТипПок_Пр = "";
		КонецЕсли;
		УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "ТипПокП", ТипПок_Пр);
		
		Если НЕ ПустаяСтрока(ДанныеЛиста.ПродавецБайконур) Тогда
			УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "ПрБкнрПродП", "1");
		КонецЕсли;
		Если НЕ ПустаяСтрока(ДанныеЛиста.ПокупательБайконур) Тогда
			УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "ПрБкнрПокП", "1");
		КонецЕсли;
		
		УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "ИдНомПродП", СокрЛП(ДанныеЛиста.ПродавецИНН_КПП));
		УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "ИдНомПокП",  СокрЛП(ДанныеЛиста.ПокупательИНН_КПП));
		
		Узел_СвКонтрП = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрПр, "СвКонтрП");
		ПрототипУзла_СвСпециф = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрП, "СвСпециф");
		
		Для Сч = 1 По ДанныеСохраненногоОтчета.мМассивТЗСпецификацийПрил[Индекс].Количество() Цикл
			Строка = ДанныеСохраненногоОтчета.мМассивТЗСпецификацийПрил[Индекс][Сч - 1];
			
			Узел_СвСпециф = НовыйУзелИзПрототипа(ПрототипУзла_СвСпециф);
			УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомПСпециф", Сч);
			УстановитьЗначениеЭлемента(Узел_СвСпециф, "НомСпециф", СокрЛП(Строка.Номер));
			УстановитьЗначениеЭлемента(Узел_СвСпециф, "ДатаСпециф", Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
		КонецЦикла;
		
		Если НЕ УзелПуст(Узел_СвКонтрПр) Тогда
			НомерПриложения = НомерПриложения + 1;
			УстановитьЗначениеЭлемента(Узел_СвКонтрПр, "НомКонтрПП", Формат(НомерПриложения, "ЧГ="));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НовыйУзелИзПрототипа(ПрототипУзла);
	
	РодительУзла = ПрототипУзла.Родитель;
	
	ПозицияИсходногоУзла = РодительУзла.Строки.Индекс(ПрототипУзла);
	НовыйУзел = РодительУзла.Строки.Вставить(ПозицияИсходногоУзла);
	ЗаполнитьЗначенияСвойств(НовыйУзел, ПрототипУзла, , "Родитель, Строки");
	Для Каждого Стр из ПрототипУзла.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

&НаСервере
Функция ДополнительныеДанныеПоАдресу(Данные, Адрес)
	
	Перем ЗначениеДанных;
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		ПозицияРазделителя = СтрНайти(Адрес, ".");
		Если ПозицияРазделителя = 0 Тогда
			АдресВерхнегоУровня = СокрЛП(Адрес);
			Данные.Свойство(АдресВерхнегоУровня, ЗначениеДанных);
		Иначе
			АдресВерхнегоУровня = СокрЛП(Лев(Адрес, ПозицияРазделителя - 1));
			ДанныеВерхнегоУровня = Неопределено;
			Если Данные.Свойство(АдресВерхнегоУровня, ДанныеВерхнегоУровня) Тогда
				АдресСледующегоУровня = СокрЛП(Сред(Адрес, ПозицияРазделителя + 1));
				ЗначениеДанных = ДополнительныеДанныеПоАдресу(ДанныеВерхнегоУровня, АдресСледующегоУровня);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеДанных;
	
КонецФункции

&НаСервере
Функция ЗначениеСуммыЗаполнено(ЗначениеСуммы)
	
	Возврат (Окр(ЗначениеСуммы) <> 0)
	
КонецФункции

&НаСервере
Функция ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = Формат(ЗначениеПоказателя, "ДФ=dd.MM.yyyy");
		Иначе
			Узел.Значение = ?(МаксШирина < СтрДлина(СокрЛП(ЗначениеПоказателя)), СокрЛП(Лев(СокрЛП(ЗначениеПоказателя), МаксШирина)), СокрЛП(ЗначениеПоказателя));
		КонецЕсли;
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	ИначеЕсли Узел.Формат = "gYear" Тогда
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, "ДФ=гггг"));
		Иначе
			Узел.Значение = Прав(СокрЛП(ЗначениеПоказателя), 4);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция УзелПуст(Узел)
	
	ПустойУзел = ?(Узел.Формат = "N", Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение)), НЕ ЗначениеЗаполнено(Узел.Значение));
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат ПустойУзел;
	
КонецФункции

&НаСервере
Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	Для Каждого Стр Из Узел.Строки Цикл
		Если Стр.Код = КодЭлемента Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция СкопироватьУзел(Родитель, Узел)
	
	РодительСтрока = ?(Родитель = Неопределено, Узел.Владелец(), Родитель);
	
	// находим узел в максимальным индексом с тем же кодом, что и у копируемого и добавляем его сразу после найденного элемента
	СтрокиСТемЖеКодом = РодительСтрока.Строки.НайтиСтроки(Новый Структура("Код", Узел.Код), Ложь);
	МаксИндекс = -1;
	Для Каждого СтрокаСТемЖеКодом Из СтрокиСТемЖеКодом Цикл
		ТекИндекс = РодительСтрока.Строки.Индекс(СтрокаСТемЖеКодом);
		Если ТекИндекс > МаксИндекс Тогда
			МаксИндекс = ТекИндекс;
		КонецЕсли;
	КонецЦикла;
	
	ИндексСоздаваемогоУзла = МаксИндекс + 1;
	Если ИндексСоздаваемогоУзла >= РодительСтрока.Строки.Количество() ИЛИ ИндексСоздаваемогоУзла = 0 Тогда // если аналогичный узел не найден или найденный узел - последний
		НовыйУзел = РодительСтрока.Строки.Добавить();
	Иначе
		НовыйУзел = РодительСтрока.Строки.Вставить(ИндексСоздаваемогоУзла);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	Возврат НовыйУзел;
	
КонецФункции

&НаСервере
Процедура УстановитьЗначениеЭлемента(Узел, КодЭлемента, Значение)
	
	ВывестиПоказательВXML(ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента), Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	РегламентированнаяОтчетностьКлиент.АктивизироватьЯчейку(ЭтаФорма, Ячейка);
	
КонецПроцедуры

&НаСервере
Процедура ОчисткаРегламентированногоОтчетаНаСервере(ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста, РежимОчистки, Многостраничность, КолВоСтраницВРазделе) Экспорт
	
	УдалятьсяВсеДопЭкземпляры = Ложь;
	
	Если СтрНайти(РежимОчистки, "ОчиститьТекущийРаздел") > 0 Тогда
		
		Если (Многостраничность) И (КолВоСтраницВРазделе > 1) Тогда
			
			ОчиститьПоказателиОтчетаЗаполняемыеИзИБНаСервере(ИмяСтраницы);
			
			УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
			
			УдалятьсяВсеДопЭкземпляры = Истина;
			
		Иначе
			
			ОчиститьТабличноеПолеНаСервере(ИмяСтраницы);
			
		КонецЕсли;
		
		СформироватьДеревоРазделовОтчетаНаСервере();
		
	ИначеЕсли СтрНайти(РежимОчистки, "ОчиститьТекущийЭкземплярМногостраничногоРаздела") > 0 Тогда
		
		ОчиститьТабличноеПолеНаСервере(ИмяСтраницы);
		
	КонецЕсли;
	
	Если ИмяСтраницы = "Титульный" Тогда
		Если Элементы.Найти("ДатаПодписи") = Неопределено Тогда
			Область = ТабличныйДокумент.Области.Найти("ДатаПодписи");
			Если НЕ Область = Неопределено Тогда
				Если ТипЗнч(Область.Значение) = Тип("Строка") Тогда
					ДатаПодписи = Лев(СтрЗаменить(Область.Значение, ".", ""), 8);
				ИначеЕсли ТипЗнч(Область.Значение) = Тип("Дата") Тогда
					Если ЗначениеЗаполнено(Область.Значение) Тогда
						ДатаПодписи = Лев(СтрЗаменить(Формат(Область.Значение, "ДФ = ""дд.ММ.гггг 'г.'"""), ".", ""), 8);
					Иначе
						ДатаПодписи = "        ";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСтраницы = "Раздел1" Тогда
		
		мТЗСпецификацийРаздел1.Очистить();
		
		мТЗСпецификацийРаздел1_07.Очистить();
		
	ИначеЕсли ИмяСтраницы = "Раздел3" Тогда
		
		мТЗСпецификацийРаздел3.Очистить();
		
	ИначеЕсли ИмяСтраницы = "Приложение" Тогда
		
		мМассивТЗСпецификацийПрил[РазделыОтчета.НайтиПоИдентификатору(СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета).КолонкаНомерСтраницыРазделаОтчета - 1].Таблица.Очистить();
		
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиентСервер.ПослеОчисткиРегламентированногоОтчета(ЭтаФорма, ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста);
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаРегламентированногоОтчетаНаКлиенте(ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста, РежимОчистки, Многостраничность, КолВоСтраницВРазделе) Экспорт
	
	УдалятьсяВсеДопЭкземпляры = Ложь;
	
	Если СтрНайти(РежимОчистки, "ОчиститьТекущийРаздел") > 0 Тогда
		
		Если (Многостраничность) И (КолВоСтраницВРазделе > 1) Тогда

			РегламентированнаяОтчетностьКлиентСервер.ОчиститьПоказателиОтчетаЗаполняемыеИзИБ(ЭтаФорма, ИмяСтраницы);

			УдалятьсяВсеДопЭкземпляры = Истина;
			
		Иначе

			ОчиститьТабличноеПолеНаКлиенте(ИмяСтраницы);

		КонецЕсли;
		
		СформироватьДеревоРазделовОтчетаНаКлиенте();
		
	ИначеЕсли СтрНайти(РежимОчистки, "ОчиститьТекущийЭкземплярМногостраничногоРаздела") > 0 Тогда
		
		ОчиститьТабличноеПолеНаКлиенте(ИмяСтраницы);
		
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиентСервер.ПослеОчисткиРегламентированногоОтчета(ЭтаФорма, ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	ДобавитьСтрокуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуНаСервере(КолонкаРазделыОтчетаСокрНаим)
	
	ВставитьДополнительнуюСтроку(ЭтаФорма, КолонкаРазделыОтчетаСокрНаим);	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВставитьДополнительнуюСтроку(Форма, ТекущаяСтраница)
	Перем СтруктураГруппы;
	Перем МаксимальноеКоличествоСтрокГруппы;

	// Сначала выполним проверку на активную страницу панели формы,
	// которая содержит многострочный раздел
			
	#Если НаКлиенте Тогда
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ТекущаяСтраница);
	#Иначе
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ТекущаяСтраница);
	#КонецЕсли
		
	Если ИмяГруппы = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
			
		Сообщение.Текст = НСтр("ru='Не выбран многострочный раздел, в который необходимо добавить строку.'");
			
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;

	// Исходное количество строк группы
	Форма.мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, МаксимальноеКоличествоСтрокГруппы);
    	
	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];
		
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	СтруктураГруппы = Форма[СтруктураГруппы];

	// определяем последнюю строку многострочной группы
	КолСтрок = СтруктураГруппы.Количество();
	КолСтрокСтр = Формат(КолСтрок, "ЧГ=0");
	КолСтрокСтр1 = Формат(КолСтрок + 1, "ЧГ=0");
	ИмяПервойЯчейкиПоследнейСтроки = ИмяГруппы + "01_" + КолСтрокСтр;

	// область первой ячейки последней строки многострочного раздела
	ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];

	// номера строк исходной области
	НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
	НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
	СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;

	// номера строк вставляемой области
	НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
	НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;

	РегламентированнаяОтчетностьВызовСервера.СпециальногоДобавленияСтроки(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти);
	
	Форма[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
	
	Для НомерГрафы = 1 По Форма.мСтруктураКолвоКолонокРазделов[ИмяГруппы] Цикл
		
		ИмяКолонки = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
		
		// переопределяем имена вставленной области
		Если СтруктураГруппы[0].Свойство(ИмяКолонки) Тогда
			
			ИмяОбластиЯчейки    = ИмяКолонки + "_" + КолСтрокСтр;
			ИмяНовОбластиЯчейки = ИмяКолонки + "_" + КолСтрокСтр1;
			
			Попытка
				ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
			Исключение
				Продолжить;
			КонецПопытки;
						
			ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;
			
			// определяем границы ячейки исходной области
			НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
			НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
			
			НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
			НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
			
			// Назначаем имя ячейки исходной области,
			// (т.к. в результате вставки новой области оно было очищено)
			ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
			ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;
			
			// Назначаем имена областей для удаления строк
			Если НомерГрафы = 1 Тогда
				НовОбластьЯчейкиУдалитьСтроку = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх, 1, ОбластьЯчейки.Верх, 1);
				НовОбластьЯчейкиУдалитьСтроку.Имя = "УдалитьСтроку" + ИмяНовОбластиЯчейки;
				ИсхОбластьЯчейкиУдалитьСтроку = ТекТабличноеПоле.Область(ОбластьЯчейкиИсходнойОбласти.Верх, 1, ОбластьЯчейкиИсходнойОбласти.Верх, 1);
				ИсхОбластьЯчейкиУдалитьСтроку.Имя = "УдалитьСтроку" + ИмяОбластиЯчейки;
			КонецЕсли;
			
			// очищаем значения ячеек вставленной области
			Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
				ОбластьЯчейки.Очистить();
			Иначе
				// порядековый номер последней строки перед вставкой
				ИсходныйНомер        = Число("0" + ОбластьЯчейки.Текст);
				НовыйПорядковыйНомер = ИсходныйНомер + 1;
				
				ОбластьЯчейки.Текст  = ? (НЕ НовыйПорядковыйНомер > МаксимальноеКоличествоСтрокГруппы, Строка(НовыйПорядковыйНомер), "");
				
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;

	Если ИмяГруппы = Форма.СтруктураРеквизитовФормы.мИдГруппы1 Тогда
		Обл = ТекТабличноеПоле.Области.Найти("П000010000114_" + КолСтрокСтр1);
		Если Обл <> Неопределено Тогда
			Обл.Формат = "ЧДЦ=6;ЧН=";
		Конецесли;
		Обл = ТекТабличноеПоле.Области.Найти("П000010000116_" + КолСтрокСтр1);
		Если Обл <> Неопределено Тогда
			Обл.Значение = "0,00";
		Конецесли;
		Обл = ТекТабличноеПоле.Области.Найти("П000010000117_" + КолСтрокСтр1);
		Если Обл <> Неопределено Тогда
			Обл.Значение = "0,00";
		Конецесли;
		Обл = ТекТабличноеПоле.Области.Найти("П000010000118_" + КолСтрокСтр1);
		Если Обл <> Неопределено Тогда
			Обл.Значение = "0,00";
		Конецесли;
	КонецЕсли;
	
	// Назначим текущей областью таблицы первую редактируемую ячейку добавленной строки.
	// Определяем текущую область таблицы
	ИмяТекущейОбласти = ИмяГруппы + "01_" + КолСтрокСтр1;

	Форма.Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];

	// установим фокус на поле табличн. документа
	Форма.ТекущийЭлемент = Форма.Элементы[ТекТабличноеПолеИмя];

	// добавляем в структуру многострочной группы новую строку
	СтруктураГруппы.Добавить();
	Инд = 0;
	Пока Истина Цикл 
		Инд = Инд + 1;
		Обл = ТекТабличноеПоле.Области.Найти("УдалитьСтроку" + ИмяГруппы + "01_" + Формат(Инд, "ЧГ="));
		Если Обл = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		Обл.Гиперссылка = Истина;
		Обл.Текст = "х";
	КонецЦикла;
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
		
	ТекТабличноеПоле = ЭтаФорма["ТабличныйДокумент"];
	
	ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
	ИмяГруппы      = РазделТекущейОбласти(ЭтаФорма, ТекущаяОбласть);
	
	Если ИмяГруппы = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = НСтр("ru='Не выбрана удаляемая строка многострочного раздела.'");
		
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтрокуЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Удалить выбранную строку?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		
		УдалитьДополнительнуюСтрокуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		
	#Иначе
		
		УдалитьДополнительнуюСтрокуНаКлиенте(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция АнализироватьПоляСтавки(СтруктураСтавок)

	СтруктураСтавокМодиф = Новый Структура;
	
	Если СтруктураСтавок.Ст18 = "Льгота" Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаНДС", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель1", 2);
	Иначе 	
		СтруктураСтавокМодиф.Вставить("СтавкаНДС", ?(СтруктураСтавок.Ст18 = "", 0, Число(СтруктураСтавок.Ст18)));
		СтруктураСтавокМодиф.Вставить("Переключатель1", 1);
	КонецЕсли;
	
	Если СтруктураСтавок.Ст16 = "" И СтруктураСтавок.Ст17 = "" Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза",0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 1);
	ИначеЕсли СтруктураСтавок.Ст16 = "Льгота" И Число(СтруктураСтавок.Ст17) = 0 Тогда	
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 2);
	ИначеЕсли СтруктураСтавок.Ст16 = "-" И СтруктураСтавок.Ст17 = "-" Тогда	
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		
		Если СтруктураСтавок.Ст21 = " " Тогда
			СтруктураСтавокМодиф.Вставить("Переключатель3", 4);
		Иначе
			СтруктураСтавокМодиф.Вставить("Переключатель3", 3);
		КонецЕсли;
		
	ИначеЕсли СтруктураСтавок.Ст17 = "Льгота" И Число(СтруктураСтавок.Ст16) = 0 Тогда	
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", 0);
		СтруктураСтавокМодиф.Вставить("Переключатель2", 2);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 2);
	ИначеЕсли ?(СтруктураСтавок.Ст17 = "", 0, Число(СтруктураСтавок.Ст17)) = 0  Тогда
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", ?(СтруктураСтавок.Ст16 = "", 0, Число(СтруктураСтавок.Ст16)));
		СтруктураСтавокМодиф.Вставить("Переключатель2", 1);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 1);
	Иначе 
		СтруктураСтавокМодиф.Вставить("СтавкаАкциза", ?(СтруктураСтавок.Ст17 = "", 0, Число(СтруктураСтавок.Ст17)));
		СтруктураСтавокМодиф.Вставить("Переключатель2", 2);
		СтруктураСтавокМодиф.Вставить("Переключатель3", 1);
	КонецЕсли;
	
	Возврат СтруктураСтавокМодиф

КонецФункции

&НаСервере
Процедура ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппы, СтруктураГруппы) Экспорт
	
	ВывестиРазделВТабличныйДокумент(ЭтаФорма, ИмяГруппы, СтруктураГруппы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРазделВТабличныйДокументНаКлиенте(ИмяГруппы, СтруктураГруппы) Экспорт
	
	ВывестиРазделВТабличныйДокумент(ЭтаФорма, ИмяГруппы, СтруктураГруппы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиРазделВТабличныйДокумент(Форма, ИмяГруппы, СтруктураГруппы)
	
	Перем ИсходноеКоличествоСтрокГруппы;

	// определим исходное колво строк многострочного раздела,
	// соответствующее бумажной форме отчета
	Форма.мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

	// реальное кол-во строк (колво заполненных строк)
	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();

	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];

	// При восстановлении сохраненных данных отчета сравниваем исходное кол-во
	// отображаемых в форме строк многострочного раздела с реальным количеством
	// созданных строк в сохраненном отчете.
	Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда
		
		// При превышении реального кол-ва строк над исходным кол-вом (по шаблону),
		// автоматически добавляем необходимое колво строк.
		Форма.ДобавитьНеобходимоеКоличествоДопСтрокНаСервере(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИсходноеКоличествоСтрокГруппы, ТекущееКоличествоСтрокГруппы, ИмяГруппы, СтруктураГруппы);

		Форма[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
		
	КонецЕсли;

	// выводим значения строк
	Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
		
		НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
		НомерСтрокиВФормеТаблицы = Формат(НомерСтрокиВТаблице + 1, "ЧГ=");
		
		Для НомерГрафы = 1 По Форма.мСтруктураКолвоКолонокРазделов[ИмяГруппы] Цикл
			
			КолонкаТаблицыИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
			
			Если СтруктураГруппы[0].Свойство(КолонкаТаблицыИмя) Тогда
				
				Попытка
					
					ИмяЯчейки                = КолонкаТаблицыИмя + "_" + НомерСтрокиВФормеТаблицы;
					ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
					ЗначениеЯчейки           = СтрокаТаблицы[КолонкаТаблицыИмя];
					
				Исключение
					
					Продолжить;
					
				КонецПопытки;
				
				Если ОбластьЯчейкиТаблицы.СодержитЗначение = Истина Тогда
					ОбластьЯчейкиТаблицы.Значение = ЗначениеЯчейки;
				Иначе
					// устанавливаем порядковый номер строки
					ОбластьЯчейкиТаблицы.Текст = ЗначениеЯчейки;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНеобходимоеКоличествоДопСтрокНаСервере(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИсходноеКоличествоСтрокГруппы, ТекущееКоличествоСтрокГруппы, ИмяГруппы, СтруктураГруппы) Экспорт
		
	ОбластьДопСтроки = ТекТабличноеПоле.ПолучитьОбласть("МногострочнаяЧасть");
	
	МногострочнаяЧастьВерх = ТекТабличноеПоле.Области.МногострочнаяЧасть.Верх;
			
	ТекТабличноеПоле.ВставитьОбласть(ОбластьДопСтроки.Область(), ТекТабличноеПоле.Область(МногострочнаяЧастьВерх + 1, , МногострочнаяЧастьВерх + ТекущееКоличествоСтрокГруппы - 1), ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	Для НомНовойСтроки = ИсходноеКоличествоСтрокГруппы По ТекущееКоличествоСтрокГруппы Цикл
		
		Постфикс = Формат(ТекущееКоличествоСтрокГруппы - НомНовойСтроки + 1, "ЧГ=");
		
		Для Каждого Область Из ОбластьДопСтроки.Области Цикл
			
			ОбластьТаблДок = ТекТабличноеПоле.Область(МногострочнаяЧастьВерх + ТекущееКоличествоСтрокГруппы - НомНовойСтроки, Область.Лево, МногострочнаяЧастьВерх + ТекущееКоличествоСтрокГруппы - НомНовойСтроки, Область.Право);
			
			Если ОбластьТаблДок.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				
				Если ОбластьТаблДок.СодержитЗначение Тогда
					ОбластьТаблДок.Очистить();
				КонецЕсли;
				
				ОбластьТаблДок.Имя = Лев(Область.Имя, СтрНайти(Область.Имя, "_")) + Постфикс;
				
			КонецЕсли;
			
		КонецЦикла;
						
	КонецЦикла;	
		
	ТекТабличноеПоле.Область(ТекТабличноеПоле.Области.МногострочнаяЧасть.Верх, , ТекТабличноеПоле.Области.МногострочнаяЧасть.Верх).Имя = "";
	
	ТекТабличноеПоле.Область(МногострочнаяЧастьВерх, , МногострочнаяЧастьВерх).Имя = "МногострочнаяЧасть";
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьСтрокуМногострочнойЧасти(Данные, Узел, НомерСтроки, ПоказателиИсключения = Неопределено, ВыгружатьПустую = Ложь)
	
	Если ПоказателиИсключения = Неопределено Тогда
		СтрИсключения = Новый Структура;
	Иначе
		СтрИсключения = Новый Структура(ПоказателиИсключения);
	КонецЕсли;
	
	СуффиксТекущейСтроки = Формат(НомерСтроки, "ЧГ=");
	
	СтрокаПустая = Истина;
	Для Каждого Стр Из Узел.Строки Цикл
		Если НЕ ЗначениеЗаполнено(Стр.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ СтрИсключения.Свойство(Стр.Ключ) И ЗначениеПоказателяЗаполнено(Данные[Стр.Ключ + СуффиксТекущейСтроки], Стр) Тогда
			СтрокаПустая = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СтрокаПустая ИЛИ ВыгружатьПустую Тогда
		Для Каждого Стр Из Узел.Строки Цикл
			Если ЗначениеЗаполнено(Стр.Ключ) Тогда
				ВывестиПоказательВXML(Стр, Данные[Стр.Ключ + СуффиксТекущейСтроки]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьРеквизит(Текст, ДобСтрока, Префикс)
	
	Если ЗначениеЗаполнено(ДобСтрока) Тогда
		Текст = Текст + Префикс + ДобСтрока;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДатуВЯчейкахНаСервере() Экспорт
    	
	ЗаполнитьДатуВЯчейках(ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДатуВЯчейкахНаКлиенте() Экспорт
    	
	ЗаполнитьДатуВЯчейках(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДатуВЯчейках(Форма)

	Перем ТаблицаСтраниц;
	
	Если Форма.СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация Тогда
		Возврат;
	КонецЕсли;

	Форма.СтруктураДанныхРаздел1.Вставить("ДатаПодписиРук", Формат(Форма.ДатаПодписи, "ДФ=дд.ММ.гггг"));
	Форма.СтруктураДанныхРаздел3.Вставить("ДатаПодписиРук", Формат(Форма.ДатаПодписи, "ДФ=дд.ММ.гггг"));
	
	Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1"
	 ИЛИ Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел3" Тогда
		
		Форма.ТабличныйДокумент.Области.ДатаПодписиРук.Значение = Формат(Форма.ДатаПодписи, "ДФ=дд.ММ.гггг");
		Форма.ТабличныйДокумент.Области.ДатаПодписиРук.Защита = Истина;
		
	КонецЕсли;
	
	Если Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
		
		ВывестиДату(Форма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗначениеПоказателяЗаполнено(ЗначениеПоказателя, ЯвляетсяСуммой)
	
	Если ТипЗнч(ЯвляетсяСуммой) = Тип("Булево") Тогда
		ПоказательЯвляетсяСуммой = ЯвляетсяСуммой;
	Иначе
		ПоказательЯвляетсяСуммой = (ЯвляетсяСуммой.Формат = "N" И ЯвляетсяСуммой.МаксРазмерность >= 10 И ЯвляетсяСуммой.МинРазмерность = 99999);
	КонецЕсли;
	
	Возврат ?(ПоказательЯвляетсяСуммой, ЗначениеСуммыЗаполнено(ЗначениеПоказателя), ЗначениеЗаполнено(ЗначениеПоказателя));
	
КонецФункции

&НаСервере
Процедура УдалитьДополнительнуюСтраницуНаСервере(ИмяТекущейСтраницыПанели = Неопределено)

	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
	РегламентированнаяОтчетность.УдалитьДополнительнуюСтраницуНаСервере(ЭтаФорма, ИмяТекущейСтраницыПанели);
	
	УстановитьДоступностьОбластейРаздела1(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если СокрЛП(Текст) <> СокрЛП(Комментарий) Тогда
		мОбновитьКомментарий = Ложь;	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		мОбновитьКомментарий = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачальныеОперацииСМногострочнымиБлоками()
	Перем ИсходноеКоличествоСтрокГруппы;

	// создаем исходную структуру многострочных разделов
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = ЭтаФорма[ЭлементСтруктуры.Значение];
		
		мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

		Для НСтр = 1 По ИсходноеКоличествоСтрокГруппы Цикл

			СтруктураГруппы.Добавить();
            			
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСтруктуруМногострочногоРазделаНаСервере(ИмяГруппы) Экспорт
	
	ОбновитьСтруктуруМногострочногоРаздела(ЭтаФорма, ИмяГруппы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтруктуруМногострочногоРазделаНаКлиенте(ИмяГруппы) Экспорт
	
	ОбновитьСтруктуруМногострочногоРаздела(ЭтаФорма, ИмяГруппы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтруктуруМногострочногоРаздела(Форма, ИмяГруппы)
	
	Перем СтруктураГруппы;
	
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	СтруктураГруппы = Форма[СтруктураГруппы];
		
	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];
	
	Для НСтр = 1 По СтруктураГруппы.Количество() Цикл
		
		ТекСтрокаТаблицы = СтруктураГруппы[НСтр - 1];
		
		НСтрСтр = Формат(НСтр, "ЧГ=");
		
		Для НомерГрафы = 1 По Форма.мСтруктураКолвоКолонокРазделов[ИмяГруппы] Цикл
				
			ИмяКолонки = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
			
			Если СтруктураГруппы[0].Свойство(ИмяКолонки) Тогда	
				
				ИмяЯчейки     = ИмяКолонки + "_" + НСтрСтр;
				
				ОбластьЯчейки = ТекТабличноеПоле.Области.Найти(ИмяЯчейки);
				Если ОбластьЯчейки = Неопределено Тогда 
					Продолжить;
				КонецЕсли;
				
				Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
					ЗначениеЯчейки = ОбластьЯчейки.Значение;
				Иначе
					ЗначениеЯчейки = ОбластьЯчейки.Текст;
				КонецЕсли;
				
				ТекСтрокаТаблицы[ИмяКолонки] = ЗначениеЯчейки;
				
			КонецЕсли;	
			
		КонецЦикла;
	КонецЦикла;
              	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТЗСпецификаций()
	
	мТЗСпецификацийРаздел1.Очистить();
	
	мТЗСпецификацийРаздел1_07.Очистить();
	
	мТЗСпецификацийРаздел3.Очистить();
	
	мМассивТЗСпецификацийПрил.Очистить();
	
	мМассивТЗСпецификацийПрил.Добавить().Таблица.Очистить();
	
КонецПроцедуры

&НаСервере
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ИмяЛиста = Неопределено) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдГруппы1", СтруктураРеквизитовФормы.мИдГруппы1);
	
	Возврат ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста, , ИдГрупп);
	
КонецФункции

&НаКлиенте
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ИмяЛиста = Неопределено) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдГруппы1", СтруктураРеквизитовФормы.мИдГруппы1);
	
	Возврат ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста, , ИдГрупп);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста = Неопределено, ТекущаяСтраница, ИдГрупп)
	Перем ИмяГруппы;
	
	Если ИмяЛиста = Неопределено Тогда
		ИмяТекущейСтраницыПанели = ТекущаяСтраница.Имя;
	Иначе
		ИмяТекущейСтраницыПанели = ИмяЛиста;
	КонецЕсли;
	
	Если ИмяТекущейСтраницыПанели = "Раздел1" Тогда
		ИмяГруппы = ИдГрупп.мИдГруппы1;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ИмяГруппы;
	
КонецФункции

&НаСервере
Функция ОпределитьТабличноеПолеПоИдМногострочногоРазделаНаСервере(ИдГруппы) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдГруппы1", СтруктураРеквизитовФормы.мИдГруппы1);
	
	Возврат ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы, ИдГрупп);
	
КонецФункции

&НаКлиенте
Функция ОпределитьТабличноеПолеПоИдМногострочногоРазделаНаКлиенте(ИдГруппы) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдГруппы1", СтруктураРеквизитовФормы.мИдГруппы1);
	
	Возврат ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы, ИдГрупп);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы, ИдГрупп)
	
	Перем ТекТабличноеПоле;
	
	Если ИдГруппы = ИдГрупп.мИдГруппы1 Тогда
		ТекТабличноеПоле = "Раздел1";
	КонецЕсли;
	
	Возврат ТекТабличноеПоле;
	
КонецФункции

&НаСервере
Функция ПолучитьКодИМНС()
	
	Если (СтруктураРеквизитовФормы.Организация = Неопределено) Или (СтруктураРеквизитовФормы.Организация = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат "";
	КонецЕсли;
	
	// Составляем список данных, необходимых для вывода в декларацию
	Сведения = "КодНО";
	// Теперь получаем данные из глобальной общей функции
	ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СтруктураРеквизитовФормы.Организация, ДатаПодписи, Сведения);

	Результат = ОргСведения.КодНО;

	Возврат Результат;

КонецФункции

&НаСервере
Функция ПолучитьСведенияОПредставителе(КодИФНС) Экспорт
	
	флПредставительЮрЛицо = Истина;
	НаименованиеОрганизацииПредставителя = "";
	ФИОПредставителя = "";
	ТипПредставителя = "1";
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	РегистрацииВНалоговомОргане.Код,
	|	РегистрацииВНалоговомОргане.КПП,
	|	РегистрацииВНалоговомОргане.Представитель,
	|	РегистрацииВНалоговомОргане.УполномоченноеЛицоПредставителя
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Код = &Код
	|	И РегистрацииВНалоговомОргане.Владелец = &Владелец");
	Запрос.УстановитьПараметр("Код", КодИФНС);
	Запрос.УстановитьПараметр("Владелец", СтруктураРеквизитовФормы.Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Представитель) Тогда
		ТипПредставителя = "2";
		Если НЕ РегламентированнаяОтчетность.ПредставительЯвляетсяФизЛицом(Выборка.Представитель) Тогда
			флПредставительЮрЛицо = Истина;
			ФИОПредставителя = СокрЛП(Выборка.УполномоченноеЛицоПредставителя);
			ПредставительСсылка = Выборка.Представитель;
		Иначе
			флПредставительЮрЛицо = Ложь;
			ПредставительСсылка = Выборка.Представитель;
			ФИОПредставителя = СокрЛП(ПредставительСсылка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("ТипПредставителя, флПредставительЮрЛицо, ФИОПредставителя, ПредставительСсылка", 
							 ТипПредставителя, флПредставительЮрЛицо, ФИОПредставителя, ПредставительСсылка);
	
КонецФункции

&НаКлиенте
Функция ПолучитьТекстыИзТЗСпецификаций(ТЗСпецификаций)
	
	Тексты = Новый Структура;
	ТекстНомер 	= "";
	ТекстДата 	= "";
	
	Для Сч = 1 По ТЗСпецификаций.Количество() Цикл
		
		Строка = ТЗСпецификаций[Сч - 1];
			
		ТекстНомер = ДобавитьРеквизит(ТекстНомер, Строка.Номер, ?(ТекстНомер = "", "", ", "));
		ТекстДата  = ДобавитьРеквизит(ТекстДата,  Формат(Строка.Дата, "ДФ=dd.MM.yyyy"),  ?(ТекстДата  = "", "", ", "));
		
	КонецЦикла; 
	
	Тексты.Вставить("Номер", ТекстНомер);
	Тексты.Вставить("Дата", ТекстДата);
	
	Возврат Тексты;
	
КонецФункции

&НаСервере
Функция ВыгрузкаВозможна(Параметры)
	
	СтатусПроверки = Истина;
	
	КодыГосударствЧленовЕАЭС = Новый Массив;
	КодыГосударствЧленовЕАЭС.Добавить("051");// Армения
	КодыГосударствЧленовЕАЭС.Добавить("112");// Беларусь
	КодыГосударствЧленовЕАЭС.Добавить("398");// Казахстан
	КодыГосударствЧленовЕАЭС.Добавить("417");// Киргизия
	КодыГосударствЧленовЕАЭС.Добавить("643");// Россия
	
	ПроверитьВозможностьВыгрузки_ПараметрыВыгрузки(СтатусПроверки);
	
	ПроверитьВозможностьВыгрузки_Раздел1(Параметры, СтатусПроверки);
	
	ПроверитьВозможностьВыгрузки_Товары(СтатусПроверки);
	
	ПроверитьВозможностьВыгрузки_Раздел3(СтатусПроверки, КодыГосударствЧленовЕАЭС);
	
	ПроверитьВозможностьВыгрузки_Приложение(СтатусПроверки, КодыГосударствЧленовЕАЭС);
	
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Процедура ПроверитьВозможностьВыгрузки_ПараметрыВыгрузки(СтатусПроверки)
	
	Раздел1 = мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	КодПричины = КодПричиныВозникновенияЗаявления(СокрЛП(Раздел1.ПричинаЗаявления));
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПричинаЗаявления) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Не указана причина возникновения заявления", "Раздел1", "ПричинаЗаявления");
		СтатусПроверки = Ложь;
	ИначеЕсли НЕ ЗначениеСодержитсяВСписке(КодПричины, "1,4,5,6") Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Указана неверная причина возникновения заявления", "Раздел1", "ПричинаЗаявления");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если КодПричины = "4" ИЛИ КодПричины = "5" ИЛИ КодПричины = "6" Тогда
		Если НЕ ЗначениеЗаполнено(Раздел1.НомерОтметки) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Не указан номер отметки о регистрации в налоговом органе ранее представленного заявления", "Раздел1", "НомерОтметки");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1.ДатаОтметки) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Не указана дата отметки о регистрации в налоговом органе ранее представленного заявления", "Раздел1", "ДатаОтметки");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если КодПричины = "5" Тогда
			Если НЕ ЗначениеЗаполнено(Раздел1.ИзмениеЦеныМесяц) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Не указан месяц, в котором участниками договора (контракта) увеличена цена", "Раздел1", "ИзмениеЦеныМесяц");
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Раздел1.ИзмениеЦеныГод) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Не указан год, в котором участниками договора (контракта) увеличена цена", "Раздел1", "ИзмениеЦеныГод");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	мСтруктураРеквизитовВыгрузки = Неопределено;
	ДанныеСохраненногоОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	Если НЕ ДанныеСохраненногоОтчета.Свойство("мСтруктураРеквизитовВыгрузки", мСтруктураРеквизитовВыгрузки) Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
	КонецЕсли;
	
	КодНО                 = мСтруктураРеквизитовВыгрузки.КодИФНС;
	ОтправительЯвляетсяЮЛ = мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ;
	
	Если ЭтаФорма.КтоОтправляет = 1 Тогда
		Если СтруктураРеквизитовФормы.ИП Тогда
			ОтправительЯвляетсяЮЛ = Ложь;
		Иначе
			ОтправительЯвляетсяЮЛ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	НаимОтпр      = мСтруктураРеквизитовВыгрузки.НаимОтпр;
	ИННЮЛОтпр     = мСтруктураРеквизитовВыгрузки.ИННЮЛОтпр;
	КППОтпр       = мСтруктураРеквизитовВыгрузки.КППОтпр;
	ФамилияФЛОтпр = мСтруктураРеквизитовВыгрузки.ФамилияФЛОтпр;
	ИмяФЛОтпр     = мСтруктураРеквизитовВыгрузки.ИмяФЛОтпр;
	ИННФЛОтпр     = мСтруктураРеквизитовВыгрузки.ИННФЛОтпр;
	
	ПрПодп       = мСтруктураРеквизитовВыгрузки.ПрПодп;
	ФамилияПодп  = мСтруктураРеквизитовВыгрузки.ФамилияПодп;
	ИмяПодп      = мСтруктураРеквизитовВыгрузки.ИмяПодп;
	НаимДовПодп  = мСтруктураРеквизитовВыгрузки.НаимДовПодп;
	НомерДовПодп = мСтруктураРеквизитовВыгрузки.НомерДовПодп;
	ДатаДовПодп  = мСтруктураРеквизитовВыгрузки.ДатаДовПодп;
	
	Если НЕ ЗначениеЗаполнено(КодНО) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнен код налогового органа.'"), "Раздел1");
		СтатусПроверки = Ложь;
	ИначеЕсли СтрДлина(СокрЛП(КодНО))<> 4 Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах неверно заполнен код налогового органа.'"), "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ОтправительЯвляетсяЮЛ Тогда
		Если НЕ ЗначениеЗаполнено(НаимОтпр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнено наименование организации отправителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ИННЮЛОтпр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнен ИНН организации отправителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(КППОтпр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнен КПП организации отправителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(ФамилияФЛОтпр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнена фамилия отправителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ИмяФЛОтпр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнено имя отправителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ИННФЛОтпр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнен ИНН отправителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФамилияПодп) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнена фамилия подписанта.'"), "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИмяПодп) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнено имя подписанта.'"), "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ПрПодп = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовПодп) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнено наименование документа, подтверждающего полномочия представителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НомерДовПодп) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнен номер документа, подтверждающего полномочия представителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДатаДовПодп) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, НСтр("ru='В сведениях об отправителе и подписантах не заполнена дата документа, подтверждающего полномочия представителя.'"), "Раздел1");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьВыгрузки_Раздел1(Параметры, СтатусПроверки)
	
	Раздел1 = мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	Раздел3 = мДанныеОтчета.ПолеТабличногоДокументаРаздел3;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.НомерЗаяв) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан номер заявления!", "Раздел1", "НомерЗаяв");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	ДатаДок = Параметры.ДатаДокДата;
	Если ДатаДок < '20100701' Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Дата формирования файла выгрузки не может быть раньше 01.07.2010", "Раздел1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Продавец.
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПродавецНаимОрг) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано полное наименование (ФИО) продавца", "Раздел1", "ПродавецНаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.КодСтраныПродавец) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан код страны продавца", "Раздел1", "КодСтраныПродавец");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПродавецМестоНахожд) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан адрес местонахождения (жительства) продавца", "Раздел1", "ПродавецМестоНахожд");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Покупатель.
	
	ДлинаИНН = СтрДлина(Раздел1.ПокупательИНН_КПП);
	Если ДлинаИНН = 0 Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан ИНН покупателя!", "Раздел1", "ПокупательИНН_КПП");
		СтатусПроверки = Ложь;
	ИначеЕсли ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 неверно задан ИНН покупателя!", "Раздел1", "ПокупательИНН_КПП");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПокупательНаимОрг) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано полное наименование (ФИО) покупателя", "Раздел1", "ПокупательНаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.КодСтраныПокупатель) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан код страны покупателя", "Раздел1", "КодСтраныПокупатель");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ПокупательМестоНахожд) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан адрес местонахождения (жительства) покупателя", "Раздел1", "ПокупательМестоНахожд");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Сведения о контракте.
	
	Если НЕ ЗначениеЗаполнено(Раздел1.НомерДог) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан номер договора(контракта) строки 05!", "Раздел1", "НомерДог");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.ДатаДог) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задана дата договора(контракта) строки 05!", "Раздел1", "ДатаДог");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	ЕстьДоговорЛизинга = НЕ ПустаяСтрока(Раздел1.ПрЛизинг);
	ЕстьДоговорПереработки = НЕ ПустаяСтрока(Раздел1.ПрДавСырья);
	
	Если ЕстьДоговорЛизинга и ЕстьДоговорПереработки Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не могут быть одновременно заданы признаки заключения договора лизинга и договора переработки давальческого сырья", "Раздел1", "ПрЛизинг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Сведения о комиссионере (посреднике).
	Если НЕ ЗначениеЗаполнено(Раздел1.ПокупательНаимОрг) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано полное наименование (ФИО) покупателя", "Раздел1", "ПокупательНаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	
	ЕстьСведенияОКомиссионере = НЕ ПустаяСтрока(Раздел1.ИННОрг_1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.НаимОрг_1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.КодСтраныОрг1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.МестоНахождОрг_1)
	                        ИЛИ НЕ ПустаяСтрока(Раздел1.НомКонтр_2)
	                        ИЛИ ЗначениеЗаполнено(Раздел1.ДатаКонтр_2);
	
	Если ЕстьСведенияОКомиссионере Тогда
		
	Если НЕ ЗначениеЗаполнено(Раздел1.НаимОрг_1) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задано полное наименование (ФИО) посредника", "Раздел1", "НаимОрг_1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.КодСтраныОрг1) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан код страны посредника", "Раздел1", "КодСтраныОрг1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Раздел1.МестоНахождОрг_1) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан адрес местонахождения (жительства) посредника", "Раздел1", "МестоНахождОрг_1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
		Если НЕ ЗначениеЗаполнено(Раздел1.НомКонтр_2) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задан номер договора (контракта) посредника", "Раздел1", "НомКонтр_2");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1.ДатаКонтр_2) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 1 не задана дата договора (контракта) посредника", "Раздел1", "ДатаКонтр_2");
			СтатусПроверки = Ложь;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьСтруктуруДанныхСвойствамиСоЗначениямиПоУмолчанию_Раздел3(Раздел3)
	
	Если НЕ Раздел3.Свойство("ПродавецИНН_КПП") Тогда
		Раздел3.Вставить("ПродавецИНН_КПП", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПокупательИНН_КПП") Тогда
		Раздел3.Вставить("ПокупательИНН_КПП", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ТипПрод_3") Тогда
		Раздел3.Вставить("ТипПрод_3", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ТипПок_3") Тогда
		Раздел3.Вставить("ТипПок_3", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПродавецБайконур") Тогда
		Раздел3.Вставить("ПродавецБайконур", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПокупательБайконур") Тогда
		Раздел3.Вставить("ПокупательБайконур", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПродавецНаимОрг") Тогда
		Раздел3.Вставить("ПродавецНаимОрг", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("КодСтраныПродавец") Тогда
		Раздел3.Вставить("КодСтраныПродавец", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПродавецМестоНахожд") Тогда
		Раздел3.Вставить("ПродавецМестоНахожд", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПокупательНаимОрг") Тогда
		Раздел3.Вставить("ПокупательНаимОрг", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("КодСтраныПокупатель") Тогда
		Раздел3.Вставить("КодСтраныПокупатель", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ПокупательМестоНахожд") Тогда
		Раздел3.Вставить("ПокупательМестоНахожд", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("НомерДог") Тогда
		Раздел3.Вставить("НомерДог", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ДатаДог") Тогда
		Раздел3.Вставить("ДатаДог", '00010101');
	КонецЕсли;
	Если НЕ Раздел3.Свойство("НомерСпецификации") Тогда
		Раздел3.Вставить("НомерСпецификации", "");
	КонецЕсли;
	Если НЕ Раздел3.Свойство("ДатаСпецификации") Тогда
		Раздел3.Вставить("ДатаСпецификации", "");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьВыгрузки_Раздел3(СтатусПроверки, КодыГосударствЧленовЕАЭС)
	
	Раздел3 = мДанныеОтчета.ПолеТабличногоДокументаРаздел3;
	
	ДополнитьСтруктуруДанныхСвойствамиСоЗначениямиПоУмолчанию_Раздел3(Раздел3);
	
	Если ЗначениеЗаполнено(Раздел3.ПродавецИНН_КПП)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательИНН_КПП)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ТипПрод_3)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ТипПок_3)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПродавецБайконур)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательБайконур)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПродавецНаимОрг)
	 ИЛИ ЗначениеЗаполнено(Раздел3.КодСтраныПродавец)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПродавецМестоНахожд)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательНаимОрг)
	 ИЛИ ЗначениеЗаполнено(Раздел3.КодСтраныПокупатель)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ПокупательМестоНахожд)
	 ИЛИ ЗначениеЗаполнено(Раздел3.НомерДог)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ДатаДог)
	 ИЛИ ЗначениеЗаполнено(Раздел3.НомерСпецификации)
	 ИЛИ ЗначениеЗаполнено(Раздел3.ДатаСпецификации) Тогда
		
		// Продавец.
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ТипПрод_3) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан тип продавца", "Раздел3", "ТипПрод_3");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		ИдентификаторПродавца = СокрЛП(Раздел3.ПродавецИНН_КПП);
		Если НЕ ЗначениеЗаполнено(ИдентификаторПродавца) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан идентификационный код (номер) продавца", "Раздел3", "ПродавецИНН_КПП");
			СтатусПроверки = Ложь;
		ИначеЕсли СтрДлина(ИдентификаторПродавца) < 8 ИЛИ СтрДлина(ИдентификаторПродавца) > 14 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 неверно задан идентификационный код (номер) продавца", "Раздел3", "ПродавецИНН_КПП");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задано полное наименование (ФИО) продавца", "Раздел3", "ПродавецНаимОрг");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(Раздел3.КодСтраныПродавец) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан код страны продавца", "Раздел3", "КодСтраныПродавец");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПродавецМестоНахожд) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан адрес местонахождения (жительства) продавца", "Раздел3", "ПродавецМестоНахожд");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Покупатель.
		
		Если ПустаяСтрока(Раздел3.ТипПок_3) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан тип покупателя", "Раздел3", "ТипПок_3");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задано полное наименование (ФИО) покупателя", "Раздел3", "ПокупательНаимОрг");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(Раздел3.КодСтраныПокупатель) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан код страны покупателя", "Раздел3", "КодСтраныПокупатель");
			СтатусПроверки = Ложь;
		Иначе
			Если НЕ ЗначениеЗаполнено(Раздел3.ПокупательИНН_КПП)
				И КодыГосударствЧленовЕАЭС.Найти(СокрЛП(Раздел3.КодСтраныПокупатель)) <> Неопределено Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан идентификационный код (номер) покупателя из государства-члена ЕАЭС", "Раздел3", "ПокупательИНН_КПП");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ПокупательМестоНахожд) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан адрес местонахождения (жительства) покупателя", "Раздел3", "ПокупательМестоНахожд");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Контракт.
		
		Если НЕ ЗначениеЗаполнено(Раздел3.НомерДог) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задан номер договора (контракта) в строке 12!", "Раздел3", "НомерДог");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел3.ДатаДог) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В разделе 3 не задана дата договора (контракта) в строке 12!", "Раздел3", "ДатаДог");
			СтатусПроверки = Ложь;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьВыгрузки_Приложение(СтатусПроверки, КодыГосударствЧленовЕАЭС)
	
	Приложение = мДанныеОтчета.Приложение;
	
	Для Каждого Лист Из Приложение Цикл
		
		Если НЕ ЗаполненЛистРазделаПриложение(Лист) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеЛиста = Лист.Данные[0].Значение;
		НомерЛиста = Приложение.Индекс(Лист) + 1;
		
		// Продавец.
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ТипПрод_Пр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан тип продавца", "Приложение", "ТипПрод_Пр", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задано полное наименование (ФИО) продавца", "Приложение", "ПродавецНаимОрг", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(ДанныеЛиста.КодСтраныПродавец) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан код страны продавца", "Приложение", "КодСтраныПродавец", НомерЛиста);
			СтатусПроверки = Ложь;
		Иначе
			Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецИНН_КПП)
				И КодыГосударствЧленовЕАЭС.Найти(СокрЛП(ДанныеЛиста.КодСтраныПродавец)) <> Неопределено Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан идентификационный код (номер) продавца из государства-члена ЕАЭС", "Приложение", "ПродавецИНН_КПП", НомерЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецМестоНахожд) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан адрес местонахождения (жительства) продавца", "Приложение", "ПродавецМестоНахожд", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Покупатель.
		Если ПустаяСтрока(ДанныеЛиста.ТипПок_Пр) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан тип покупателя", "Приложение", "ТипПок_Пр", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПродавецНаимОрг) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задано полное наименование (ФИО) покупателя ", "Приложение", "ПокупательНаимОрг", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПустаяСтрока(ДанныеЛиста.КодСтраныПокупатель) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан код страны покупателя", "Приложение", "КодСтраныПокупатель", НомерЛиста);
			СтатусПроверки = Ложь;
		Иначе
			Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПокупательИНН_КПП)
				И КодыГосударствЧленовЕАЭС.Найти(СокрЛП(ДанныеЛиста.КодСтраныПокупатель)) <> Неопределено Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан идентификационный код (номер) покупателя из государства-члена ЕАЭС", "Приложение", "ПокупательИНН_КПП", НомерЛиста);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ПокупательМестоНахожд) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан адрес местонахождения (жительства) покупателя", "Приложение", "ПокупательМестоНахожд", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Контракт.
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.НомерДог) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задан номер договора(контракта)!", "Приложение", "НомерДог", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеЛиста.ДатаДог) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "На листе " + НомерЛиста + " раздела Приложение не задана дата договора(контракта)!", "Приложение", "ДатаДог", НомерЛиста);
			СтатусПроверки = Ложь;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьВыгрузки_Товары(СтатусПроверки)
	
	Раздел1 = мДанныеОтчета.ПолеТабличногоДокументаРаздел1;
	
	ЧислоВыгружаемыхТоваров = 0;
	НомСтроки = 1;
	
	ЕстьДоговорЛизинга = НЕ ПустаяСтрока(Раздел1.ПрЛизинг);
	ЕстьДоговорПереработки = НЕ ПустаяСтрока(Раздел1.ПрДавСырья);
	
	Пока Раздел1.Свойство("П000010000101_" + Формат(НомСтроки, "ЧГ=")) Цикл
		СтрНомСтроки = Формат(НомСтроки, "ЧГ=");
		НомСтроки = НомСтроки + 1; // для следующего прохождения цикла, в этом используется лишь СтрНомСтроки
		СтрокаЗаполнена = Ложь;
		Для Инд = 2 По 13 Цикл
			Если ЗначениеЗаполнено(Раздел1["П0000100001" + Формат(Инд, "ЧЦ=2; ЧВН=; ЧГ=") + "_" + СтрНомСтроки]) Тогда
				СтрокаЗаполнена = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СтрокаЗаполнена Тогда
			Продолжить;
		КонецЕсли;
		
		ЧислоВыгружаемыхТоваров = ЧислоВыгружаемыхТоваров + 1;
		
		СтруктураСтавок = Новый Структура;
		СтруктураСтавок.Вставить("Ст16", СтрЗаменить(Раздел1["П000010000116_" + СтрНомСтроки], ",", "."));
		СтруктураСтавок.Вставить("Ст17", СтрЗаменить(Раздел1["П000010000117_" + СтрНомСтроки], ",", "."));
		СтруктураСтавок.Вставить("Ст18", СтрЗаменить(Раздел1["П000010000118_" + СтрНомСтроки], ",", "."));
		СтруктураСтавок.Вставить("Ст21", Раздел1["П000010000121_" + СтрНомСтроки]);
		
		СтруктураСтавокМодиф = АнализироватьПоляСтавки(СтруктураСтавок);
		ПрОсвАкциз = СтруктураСтавокМодиф.Переключатель3;
		ПрОсвНДС   = СтруктураСтавокМодиф.Переключатель1;
		
		СтАкцизТверд = СтрЗаменить(Раздел1["П000010000116_" + СтрНомСтроки], ",", ".");
		СтАкцизАдвал = СтрЗаменить(Раздел1["П000010000117_" + СтрНомСтроки], ",", ".");
		СтНДС        = СтрЗаменить(Раздел1["П000010000118_" + СтрНомСтроки], ",", ".");
		
		СтАкцизТверд = ?(СтАкцизТверд = "Льгота" ИЛИ СтАкцизТверд = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизТверд)), 0, Число(СтАкцизТверд));
		СтАкцизАдвал = ?(СтАкцизАдвал = "Льгота" ИЛИ СтАкцизАдвал = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтАкцизАдвал)), 0, Число(СтАкцизАдвал));
		СтНДС = ?(СтНДС = "Льгота" ИЛИ СтНДС = "-" ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтНДС)), 0, Число(СтНДС));
		
		// Значение единицы измерения дополнительной величины налоговой базы.
		ЗначениеКоличТов = Раздел1["П000010000105_" + СтрНомСтроки];
		ЗначениеНБАкциз = Раздел1["П000010000114_" + СтрНомСтроки];
		ЗаполнятьЕдИзмТовНБАкц = (ЗначениеКоличТов <> ЗначениеНБАкциз)
		                       И (ЗначениеКоличТов <>0)
		                       И НЕ (ПрОсвАкциз = 3 ИЛИ ПрОсвАкциз = 4);
		ИмяСлужебногоПоля = "П000010000124_" + СтрНомСтроки;
		ИмяПоляПредставления = "П000010000123_" + СтрНомСтроки;
		Если Раздел1.Свойство(ИмяСлужебногоПоля) И НЕ ПустаяСтрока(Раздел1[ИмяСлужебногоПоля])Тогда
			ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
		Иначе
			ЗначениеЕдИзмТовНБАкц = Раздел1[ИмяСлужебногоПоля];
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000102_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнено наименование товара!", "Раздел1", "П000010000102_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		ТНВЭД = Раздел1["П000010000103_" + СтрНомСтроки];
		Если (ПрОсвАкциз <> 4)	
		 ИЛИ (ПрОсвНДС = 2)
		 ИЛИ (ПрОсвНДС = 1 И СтНДС = 10) Тогда
			Если ПустаяСтрока(ТНВЭД) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнен код ТН ВЭД!", "Раздел1", "П000010000103_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТНВЭД) И СтрДлина(СокрЛП(ТНВЭД)) <> 10 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " указан код ТН ВЭД, состоящий менее чем из 10 символов!", "Раздел1", "П000010000103_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Единица измерения.
		Если (НЕ ЕстьДоговорЛизинга И НЕ ЕстьДоговорПереработки)
		 ИЛИ (НЕ ПустаяСтрока(Раздел1["П000010000105_" + СтрНомСтроки])) Тогда
			Если ПустаяСтрока(Раздел1["П000010000104_" + СтрНомСтроки]) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена единица измерения товара!", "Раздел1", "П000010000104_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Количество товара.
		Если (НЕ ЕстьДоговорЛизинга И НЕ ЕстьДоговорПереработки)
		 ИЛИ (НЕ ПустаяСтрока(Раздел1["П000010000104_" + СтрНомСтроки])) Тогда
			Если НЕ ЗначениеЗаполнено(Раздел1["П000010000105_" + СтрНомСтроки]) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указано количество товара!", "Раздел1", "П000010000105_" + СтрНомСтроки);	
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		СтоимостьТовара = Раздел1["П000010000106_" + СтрНомСтроки];
		Если НЕ ЗначениеЗаполнено(СтоимостьТовара) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена стоимость товара!", "Раздел1", "П000010000106_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		КодВалюты = Раздел1["П000010000107_" + СтрНомСтроки];
		Если ПустаяСтрока(КодВалюты) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана валюта товара!", "Раздел1", "П000010000107_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		КурсВалюты = Раздел1["П000010000108_" + СтрНомСтроки];
		
		БазаВалюты = 0;
		Если Раздел1.Свойство("П000010000125_" + СтрНомСтроки) Тогда
			БазаВалюты = Раздел1["П000010000125_" + СтрНомСтроки];
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(КурсВалюты) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указан курс валюты товара!", "Раздел1", "П000010000108_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		ИначеЕсли КодВалюты = "643" Тогда 
			Если КурсВалюты <> 1 Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " для валюты с кодом 643 (Российский рубль) курс валюты должен равняться 1!", "Раздел1", "П000010000108_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Транспортный (товаросопроводительный) документ.
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000109_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указаны серия, номер транспортного документа!", "Раздел1", "П000010000109_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000110_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана дата транспортного документа!", "Раздел1", "П000010000110_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Счет-фактура.
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000111_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указан номер счета-фактуры!", "Раздел1", "П000010000111_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000112_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана дата счета-фактуры!", "Раздел1", "П000010000112_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Дата принятия на учет товара.
		Если НЕ ЗначениеЗаполнено(Раздел1["П000010000113_" + СтрНомСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена дата принятия товара на учет!", "Раздел1", "П000010000113_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Налоговая база.
		Если ЗаполнятьЕдИзмТовНБАкц Тогда
			Если НЕ ЗначениеЗаполнено(ЗначениеЕдИзмТовНБАкц) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена единица измерения дополнительной величины, используемой для исчисления налоговой базы (акциз)!", "Раздел1", "П000010000123_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ЗначениеЕдИзмТовНБАкц) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " ошибочно заполнена единица измерения дополнительной величины, используемой для исчисления налоговой базы (акциз)!", "Раздел1", "П000010000123_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		НалоговаяБазаНДС = Раздел1["П000010000115_" + СтрНомСтроки];
		Если НалоговаяБазаНДС <= 0 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не заполнена налоговая база (НДС) !", "Раздел1", "П000010000115_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		
		Если (ПрОсвАкциз = 1)
		   И НЕ ЗначениеЗаполнено(СтруктураСтавокМодиф.СтавкаАкциза) Тогда
			Если НЕ ЗначениеЗаполнено(СтАкцизТверд) И НЕ ЗначениеЗаполнено(СтАкцизАдвал) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не указана ставка акцизов!", "Раздел1", "П000010000116_" + СтрНомСтроки);
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если ПрОсвНДС = 1 И СтНДС <> 10 И СтНДС <> 18 И СтНДС <> 20 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " неверно задана ставка НДС!", "Раздел1", "П000010000118_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		// Суммы налогов.
		Если (ПрОсвАкциз = 1 ИЛИ ПрОсвАкциз = 3) И Раздел1["П000010000119_" + СтрНомСтроки] <= 0 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не задана сумма Акциза!", "Раздел1", "П000010000119_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ПрОсвНДС = 1 И Раздел1["П000010000120_" + СтрНомСтроки] <= 0 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 в строке " + СтрНомСтроки + " не задана сумма НДС!", "Раздел1", "П000010000120_" + СтрНомСтроки);
			СтатусПроверки = Ложь;
		КонецЕсли;
		
	КонецЦикла;
		
	Если ЧислоВыгружаемыхТоваров = 0 Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "В таблице товаров раздела 1 не заполнена ни одна строка!", "Раздел1", "П000010000102_1");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗапретитьВводДанныхВОбласть(Форма, ОбрабатываемаяОбласть)
	
	ЦветНедоступнойОбласти = Форма.мСтруктураЦветовРаскраски["_5"];
	
	ОбрабатываемаяОбласть.Защита   = Истина;
	ОбрабатываемаяОбласть.ЦветФона = ЦветНедоступнойОбласти;
	ОбрабатываемаяОбласть.Очистить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РазрешитьВводДанныхВОбласть(Форма, ОбрабатываемаяОбласть)
	
	ЦветОбластиРучногоВвода = Форма.мСтруктураЦветовРаскраски["_0"];
	
	ОбрабатываемаяОбласть.Защита   = Ложь;
	ОбрабатываемаяОбласть.ЦветФона = ЦветОбластиРучногоВвода;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СнятьЗащитуСЯчеек(Форма, ПоставитьЗащиту)

	Группа1 = Неопределено;
	Форма.мСтруктураМногострочныхРазделов.Свойство(Форма.СтруктураРеквизитовФормы.мИдГруппы1, Группа1);

	ЦветФонаРазБлокировки = Неопределено;
	Форма.мСтруктураЦветовРаскраски.Свойство("_0", ЦветФонаРазБлокировки);

	ЦветФонаБлокировки = Неопределено;
	Форма.мСтруктураЦветовРаскраски.Свойство("_4", ЦветФонаБлокировки);

	Цвет          = ?(ПоставитьЗащиту, ЦветФонаБлокировки, ЦветФонаРазБлокировки);

	Форма.ТабличныйДокумент.Области.П000010000114.Защита = ПоставитьЗащиту;
	Форма.ТабличныйДокумент.Области.П000010000114.ЦветФона = Цвет;

	Форма.ТабличныйДокумент.Области.П000010000115.Защита = ПоставитьЗащиту;
	Форма.ТабличныйДокумент.Области.П000010000115.ЦветФона = Цвет;

	Форма.ТабличныйДокумент.Области.П000010000119.Защита = ПоставитьЗащиту;
	Форма.ТабличныйДокумент.Области.П000010000119.ЦветФона = Цвет;

	Форма.ТабличныйДокумент.Области.П000010000120.Защита = ПоставитьЗащиту;
	Форма.ТабличныйДокумент.Области.П000010000120.ЦветФона = Цвет;

КонецПроцедуры

&НаСервере
Процедура УстановитьФорматВыводаНаСервере(НаимРаздела)
	
	УстановитьФорматВывода(ЭтаФорма, НаимРаздела);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматВывода(Форма, НаимРаздела, ТаблДок = Неопределено)
	
	Перем СтруктураГруппы;
	Перем РазмерностьПрописьюТекст;
	
	Если Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения = Форма.ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1 Тогда
		Форма.СтруктураРеквизитовФормы.мДелитель = 1;
		РазмерностьПрописьюТекст = "в рублях";
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "ЧЦ = 17; ЧДЦ = " + Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0";
		
	ИначеЕсли Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения = Форма.ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000 Тогда
		Форма.СтруктураРеквизитовФормы.мДелитель = 1000;
		РазмерностьПрописьюТекст = "в тыс. рублей";
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "ЧЦ = 17; ЧДЦ = " + Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3";
		
	ИначеЕсли Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения = Форма.ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000000 Тогда
		Форма.СтруктураРеквизитовФормы.мДелитель = 1000000;
		РазмерностьПрописьюТекст = "в млн. рублей";
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "ЧЦ = 17; ЧДЦ = " + Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6";
		
	КонецЕсли;
	
	Если ТаблДок = Неопределено Тогда
		ТаблДок = Форма.ТабличныйДокумент;
	КонецЕсли;
	
	Если НаимРаздела = "Раздел1" Тогда
		
		Инд = 1;
		_Инд = "_" + Формат(Инд, "ЧГ=");
		ОблГрафа14 = ТаблДок.Области.Найти("П000010000114" + _Инд);
		
		Пока ОблГрафа14 <> Неопределено Цикл
			
			ОблГрафа16 = ТаблДок.Области.Найти("П000010000116" + _Инд);
			ОблГрафа17 = ТаблДок.Области.Найти("П000010000117" + _Инд);
			
			Если ОблГрафа16.Значение = "-" ИЛИ ОблГрафа17.Значение = "-" Тогда
				ОблГрафа14.Формат = "ЧДЦ=6;ЧН=-";
				
				ОблГрафа16.Значение = "-";
				ОблГрафа17.Значение = "-";
				
			Иначе
				ОблГрафа14.Формат = "ЧДЦ=6;ЧН=";
				
				ОблГрафа16.Значение = ?(ЗначениеЗаполнено(ОблГрафа16.Значение), ОблГрафа16.Значение, "0,00");
				ОблГрафа17.Значение = ?(ЗначениеЗаполнено(ОблГрафа17.Значение), ОблГрафа17.Значение, "0,00");
				
				ОблГрафа16.Значение = ?(ОблГрафа17.Значение = "Льгота", "0,00", ОблГрафа16.Значение);
				ОблГрафа17.Значение = ?(ОблГрафа16.Значение = "Льгота", "0,00", ОблГрафа17.Значение);
				
			КонецЕсли;
			
			ОблГрафа18 = ТаблДок.Области.Найти("П000010000118" + _Инд);
			
			ОблГрафа18.Значение = ?(ЗначениеЗаполнено(ОблГрафа18.Значение), ОблГрафа18.Значение, "0,00");
			
			Инд = Инд + 1;
			_Инд = "_" + Формат(Инд, "ЧГ=");
			ОблГрафа14 = ТаблДок.Области.Найти("П000010000114" + _Инд);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтрокуНаСервере(ТекущаяСтраница)
	
	УдалитьДополнительнуюСтроку(ЭтаФорма, , , ТекущаяСтраница);

	Если ТекущаяСтраница = "Раздел1" Тогда
		РасчетНаСервере("Раздел1");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДополнительнуюСтрокуНаКлиенте(ТекущаяСтраница)
	
	УдалитьДополнительнуюСтроку(ЭтаФорма, , , ТекущаяСтраница);
	
	Если ТекущаяСтраница = "Раздел1" Тогда
		РасчетНаКлиенте("Раздел1");
	КонецЕсли;	
			
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УдалитьДополнительнуюСтроку(Форма, СпециальноеДобавление = Ложь, ИмяКолонкиНомера = Неопределено, ТекущаяСтраница) Экспорт
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем СтруктураГруппы;
	
	// Сначала выполним проверку на активную страницу панели формы,
	// которая содержит многострочный раздел
	#Если НаКлиенте Тогда
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ТекущаяСтраница);
	#Иначе
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ТекущаяСтраница);
	#КонецЕсли
	
	// исходное количество строк группы
	Форма.мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];
	ТекущаяОбласть = Форма.Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть;
	ИмяЯчейки      = ТекущаяОбласть.Имя;
	
	ИмяГруппы = РазделТекущейОбласти(Форма, ТекущаяОбласть);
	
	// предварительно обновим структуру многострочных разделов
	ОбновитьСтруктуруМногострочногоРаздела(Форма,ИмяГруппы);
	// номер строки многострочного раздела определим
	// по имени выделенной ячейки
	НомУдаляемойСтроки = Число( Сред( ИмяЯчейки, СтрНайти( ИмяЯчейки, "_") + 1));
	НомерСтрокиТаблицы = НомУдаляемойСтроки - 1; // нумерация строк в таблице значений начинается с нуля
	СтруктураГруппы = Форма[СтруктураГруппы];
	Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы Тогда
		// удаляется добавленная строка
		ИмяПервойЯчейкиУдаляемойСтроки = ИмяГруппы + "01_" + Формат(НомУдаляемойСтроки, "ЧГ=");
		
		// область первой ячейки удаляемой строки многострочного раздела
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
		
		// номера строк удаляемой области
		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
		НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
		
		РегламентированнаяОтчетностьВызовСервера.СпециальногоУдаленияСтроки(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти);
		
		Форма[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
				
		КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;
		
		// обновим имена ячеек сдвинутых строк
		Если КолвоСдвигаемыхСтрок > 0 Тогда
			
			Для НСтр = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
				
				ТекСтрока = СтруктураГруппы[НСтр-1];
				
				НСтрСтр = Формат(НСтр, "ЧГ=");
				НСтрСтр1 = Формат(НСтр - 1, "ЧГ=");
				
				Для НомерГрафы = 1 По Форма.мСтруктураКолвоКолонокРазделов[ИмяГруппы] Цикл
					
					КолонкаИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
					
					Если СтруктураГруппы[0].Свойство(КолонкаИмя) Тогда
						                        						
						ИмяСдвинутойЯчейки = КолонкаИмя + "_" + НСтрСтр;
						НовоеИмяСдвинутойЯчейки = КолонкаИмя + "_" + НСтрСтр1;
						
						Попытка
							ОбластьСдвинутойЯчейки     = ТекТабличноеПоле.Области[ИмяСдвинутойЯчейки];
						Исключение
							Продолжить;
						КонецПопытки;
													
						ОбластьСдвинутойЯчейки.Имя = НовоеИмяСдвинутойЯчейки;
						
						Если НомерГрафы = 1 Тогда
							ОбластьЯчейкиУдалитьСтроку = ТекТабличноеПоле.Область(ОбластьСдвинутойЯчейки.Верх, 1, ОбластьСдвинутойЯчейки.Верх, 1);
							ОбластьЯчейкиУдалитьСтроку.Имя = "УдалитьСтроку" + НовоеИмяСдвинутойЯчейки;
						КонецЕсли;
						
						Если ОбластьСдвинутойЯчейки.СодержитЗначение <> Истина Тогда
							// обновляем порядковый номер строки
							
							СтарыйНомер = Число(ТекСтрока[КолонкаИмя]);
							НовыйПорядковыйНомер = Строка(Макс(СтарыйНомер - 1, 0));
							ОбластьСдвинутойЯчейки.Текст = НовыйПорядковыйНомер + ".";
							
							// обновляем значение элемента таблицы значений
							ТекСтрока[КолонкаИмя] = НовыйПорядковыйНомер;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
		// обновляем структуру многострочного раздела
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
		
	Иначе
		// строка не удаляется в форме табличного документа,
		// а должна осуществляться сдвиг строк
		
		// обновляем структуру многострочного раздела
		// методом удаления выбранной строки и добавления пустой строки
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
		
		ДобавляемаяСтрока = СтруктураГруппы.Добавить();
		
		// обновляем порядковые номера строк, выводимых в табличный документ
		Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
			
			НомерСтрокиВТаблице = СтруктураГруппы.Индекс(СтрокаТаблицы);
			НомерСтрокиВФормеТаблицы = Формат(НомерСтрокиВТаблице + 1, "ЧГ=0");
			
			Для НомерГрафы = 1 По Форма.мСтруктураКолвоКолонокРазделов[ИмяГруппы] Цикл
				КолонкаТаблицыИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=");
				
				Если СтруктураГруппы[0].Свойство(КолонкаТаблицыИмя) Тогда
					
					ИмяЯчейки = КолонкаТаблицыИмя + "_" + НомерСтрокиВФормеТаблицы;
					
					Попытка 
						ОбластьЯчейкиТаблицы = ТекТабличноеПоле.Области[ИмяЯчейки];
					Исключение
						Продолжить;
					КонецПопытки;
										
					Если ОбластьЯчейкиТаблицы.СодержитЗначение <> Истина Тогда
						
						ПервыйНомерВГруппе = 1;
						
						// обновляем порядковый номер строки
						НовыйПорядковыйНомер = Строка(ПервыйНомерВГруппе + НомерСтрокиВТаблице);
						
						СтрокаТаблицы[КолонкаТаблицыИмя] = НовыйПорядковыйНомер;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		// Данные в многострочном блоке обновляются в декларациях!
		// Обновляем экранную форму табличного документа
		#Если НаКлиенте Тогда
			Форма.ВывестиРазделВТабличныйДокументНаКлиенте(ИмяГруппы, СтруктураГруппы);
		#Иначе
			Форма.ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппы, СтруктураГруппы);
		#КонецЕсли
		
	КонецЕсли;
	
	Если ТекущаяСтраница = "Раздел1" Тогда
	
		Расчет(Форма, ТекущаяСтраница,"");
	КонецЕсли;
		
	// назначим текущей областью таблицы первую редактируемую ячейку
	// удаленной строки, если ее порядковый номер меньше кол-ва строк
	// многострочного раздела, или последней строки раздела в противном случае
	
	// определяем текущую область таблицы
	НомТекущейСтроки = Мин(НомУдаляемойСтроки, СтруктураГруппы.Количество());
	ИмяТекущейОбласти = ИмяГруппы + "01_" + Формат(НомТекущейСтроки, "ЧГ=");
	
	Если ТекущаяСтраница = "Раздел1" Тогда
		КлючСтруктуры = "К_" + Формат(НомУдаляемойСтроки);
		Если Форма.мСтрукТЗДокументы.Свойство(КлючСтруктуры) Тогда
			Форма.мСтрукТЗДокументы.Удалить(КлючСтруктуры);
		КонецЕсли;
		Если НомУдаляемойСтроки <= СтруктураГруппы.Количество() Тогда
			Ном = НомТекущейСтроки + 1;
			КлючСтруктуры = "К_" + Формат(Ном, "ЧГ=");
			Пока Форма.мСтрукТЗДокументы.Свойство(КлючСтруктуры) Цикл
				Форма.мСтрукТЗДокументы.Вставить("К_" + Формат(Ном - 1, "ЧГ="), Форма.мСтрукТЗДокументы[КлючСтруктуры]);
				Ном = Ном + 1;
				КлючСтруктуры = "К_" + Формат(Ном, "ЧГ=");
			КонецЦикла;
			Форма.мСтрукТЗДокументы.Удалить("К_" + Формат(Ном - 1, "ЧГ="));
		КонецЕсли;
	КонецЕсли;
	
	Форма.Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	
	// установим фокус на поле табличн. документа
	ТекущийЭлемент = Форма.Элементы[ТекТабличноеПолеИмя];
	Если СтруктураГруппы.Количество() = 1 Тогда
		Обл = ТекТабличноеПоле.Области["УдалитьСтроку" + ИмяГруппы + "01_1"];
		Обл.Гиперссылка = Ложь;
		Обл.Текст = "";
	КонецЕсли;
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Перем ТаблицаСтраницРаздела;
	
	ИмяОбласти = Область.Имя;
	
	Если СтрЧислоВхождений(ИмяОбласти, "ДобавитьСтроку") = 1 И Область.Текст <> "" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДобавитьСтроку("");
		
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "УдалитьСтроку") = 1 И Область.Текст <> "" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		УдалитьСтроку("");
		
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "ДобавитьСтраницу") > 0 И Область.Текст <> "" Тогда
		
		ДобавитьСтраницу("");
		
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "УдалитьСтраницу") > 0 И Область.Текст <> "" Тогда
		
		УдалитьСтраницу("");
		
	КонецЕсли;
		
	Если Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Раздел1" Тогда
		
		ПолеТабличногоДокументаРаздел1 = ТабличныйДокумент;
		
		Если ИмяОбласти = "ПричинаЗаявления" Тогда
			
			СтандартнаяОбработка = Ложь;
			Модифицированность = Истина;
			
			СписокПричинЗаявления = Новый СписокЗначений;
			СписокПричинЗаявления.Добавить("Пустое значение");
			СписокПричинЗаявления.Добавить("первичное заявление");
			СписокПричинЗаявления.Добавить("взамен ранее представленного");
			СписокПричинЗаявления.Добавить("корректировка ранее представленного при увеличении цены");
			СписокПричинЗаявления.Добавить("взамен ранее представленного при частичном возврате товара");
			
			ТекЭлементСписка = СписокПричинЗаявления.НайтиПоЗначению(СокрЛП(Область.Значение));
			
			Оповещение = Новый ОписаниеОповещения("ПослеВыбораПричиныВозникновенияЗаявления", ЭтаФорма, Область);
			СписокПричинЗаявления.ПоказатьВыборЭлемента(Оповещение, "Выбор причины возникновения заявления", ТекЭлементСписка);
			
		ИначеЕсли ИмяОбласти = "КодСтраныПродавец"
		 ИЛИ ИмяОбласти = "ПродавецМестоНахожд"
		 ИЛИ ИмяОбласти = "КодСтраныОрг1" 
		 ИЛИ ИмяОбласти = "МестоНахождОрг_1" Тогда
			ВвестиМестонахождениеРаздел1(Элемент, Область, СтандартнаяОбработка);
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "ПродавецБайконур" Тогда
			СтандартнаяОбработка = Ложь;
			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел1");
			ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
			
			ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов);
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "ПокупательБайконур" Тогда
			СтандартнаяОбработка = Ложь;
			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			КодСтраны = ?(ПустаяСтрока(Область.Значение), "643", "398");
			ПолеТабличногоДокументаРаздел1.Области.КодСтраныПокупатель.Значение = КодСтраны;
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "КомиссионерБайконур" Тогда
			СтандартнаяОбработка = Ложь;
			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресКомиссионераРаздел1");
			ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныОрг1");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "МестоНахождОрг_1");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "КомиссионерБайконур");
			
			ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов);   
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли СтрНайти(ИмяОбласти, "П000010000109") = 1 ИЛИ СтрНайти(ИмяОбласти, "П000010000110") = 1 Тогда	
			
			СтандартнаяОбработка = Ложь;
			
			НомерСтроки = Сред(ИмяОбласти, 14);
			КлючСтруктуры = "К" + НомерСтроки;
			
			ТранспДокументы = Неопределено;
			мСтрукТЗДокументы.Свойство(КлючСтруктуры, ТранспДокументы);
			
			Если ТранспДокументы = Неопределено Тогда
				ТранспДокументы = Новый Массив;	
			КонецЕсли;
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Документы", ТранспДокументы);
			
			//открытие формы заполнения
			ФормаЗаполнения = ПолучитьФорму("Отчет.РегламентированныйОтчетЗаявлениеОВвозеТоваров.Форма.ФормаЗаполнения", ПараметрыФормы, ЭтаФорма);
			ДополнительныеПараметры = Новый Структура("КлючСтруктуры, НомерСтроки, Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, ТранспДокументы, Элемент", КлючСтруктуры, НомерСтроки, Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, ТранспДокументы, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуЗаполненияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			ФормаЗаполнения.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаЗаполнения.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаЗаполнения.Открыть();
			
		ИначеЕсли ИмяОбласти = "НомерСпецификации" ИЛИ ИмяОбласти = "ДатаСпецификации" Тогда
			
			СтандартнаяОбработка = Ложь;
			ФормаВводаСпецификаций = ПолучитьФорму("Отчет.РегламентированныйОтчетЗаявлениеОВвозеТоваров.Форма.ФормаВводаСпецификаций", , ЭтаФорма);
			КопироватьДанныеФормы(мТЗСпецификацийРаздел1, ФормаВводаСпецификаций.ТЗСпецификаций);
			ДополнительныеПараметры = Новый Структура("Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент", Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВводаСпецификацииРаздел1Завершение", ЭтотОбъект, ДополнительныеПараметры);
			ФормаВводаСпецификаций.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаВводаСпецификаций.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаВводаСпецификаций.Открыть();
			
		ИначеЕсли ИмяОбласти = "НомерСпецификации_07" ИЛИ ИмяОбласти = "ДатаСпецификации_07" Тогда
			
			СтандартнаяОбработка = Ложь;
			ФормаВводаСпецификаций = ПолучитьФорму("Отчет.РегламентированныйОтчетЗаявлениеОВвозеТоваров.Форма.ФормаВводаСпецификаций", , ЭтаФорма);
			КопироватьДанныеФормы(мТЗСпецификацийРаздел1_07, ФормаВводаСпецификаций.ТЗСпецификаций);
			ДополнительныеПараметры = Новый Структура("Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент", Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВводаСпецификации_07Раздел1Завершение", ЭтотОбъект, ДополнительныеПараметры);
			ФормаВводаСпецификаций.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаВводаСпецификаций.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаВводаСпецификаций.Открыть();
		ИначеЕсли СтрНайти(Область.Имя, "ПрЛизинг") > 0 Тогда
			
			СтандартнаяОбработка = Ложь;
			ПолеТабличногоДокументаРаздел1.Области.ПрЛизинг.Значение
				= ?(СокрЛП(ПолеТабличногоДокументаРаздел1.Области.ПрЛизинг.Значение) = "", "Х", "");
			ПолеТабличногоДокументаРаздел1.Области.ПрДавСырья.Значение = "";
			Модифицированность = Истина;
			
		ИначеЕсли СтрНайти(Область.Имя, "ПрДавСырья") > 0 Тогда
			
			СтандартнаяОбработка = Ложь;
			ПолеТабличногоДокументаРаздел1.Области.ПрДавСырья.Значение
				= ?(СокрЛП(ПолеТабличногоДокументаРаздел1.Области.ПрДавСырья.Значение) = "", "Х", "");
			ПолеТабличногоДокументаРаздел1.Области.ПрЛизинг.Значение = "";
			Модифицированность = Истина;
			
		ИначеЕсли СтрНайти(Область.Имя, "ПрПриобретенияУФЛ") > 0 Тогда
			
			СтандартнаяОбработка = Ложь;
			ПолеТабличногоДокументаРаздел1.Области.ПрПриобретенияУФЛ.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
		ИначеЕсли СтрНайти(Область.Имя, "П000010000107") > 0 Тогда
			
			СтандартнаяОбработка = Ложь;
			ФормаВыбора = ПолучитьФорму("Справочник.Валюты.ФормаВыбора", Новый Структура("РежимВыбора", Истина), ЭтаФорма);
			ФормаВыбора.Заголовок = "Выбор кода валюты";
			ДополнительныеПараметры = Новый Структура("Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент", Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораВалютыРаздел1Завершение", ЭтотОбъект, ДополнительныеПараметры);
			ФормаВыбора.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаВыбора.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаВыбора.Открыть();
			
		ИначеЕсли СтрНайти(Область.Имя, "П000010000116") > 0
			ИЛИ СтрНайти(Область.Имя, "П000010000117") > 0
			Или СтрНайти(Область.Имя, "П000010000118") > 0 Тогда
			
			СтандартнаяОбработка = Ложь;
			
			НомерДопСтр = Сред(Область.Имя,15);
			
			СтруктураСтавокТабл = Новый Структура;
			СтруктураСтавокТабл.Вставить("Ст16", ПолеТабличногоДокументаРаздел1.Области["П000010000116_" + НомерДопСтр].Значение);
			СтруктураСтавокТабл.Вставить("Ст17", ПолеТабличногоДокументаРаздел1.Области["П000010000117_" + НомерДопСтр].Значение);
			СтруктураСтавокТабл.Вставить("Ст18", ПолеТабличногоДокументаРаздел1.Области["П000010000118_" + НомерДопСтр].Значение);
			СтруктураСтавокТабл.Вставить("Ст21", ПолеТабличногоДокументаРаздел1.Области["П000010000121_" + НомерДопСтр].Значение);
			
			СтруктураСтавокФорма = АнализироватьПоляСтавки(СтруктураСтавокТабл);
			ФормаВводаСтавкиНалога = ПолучитьФорму("Отчет.РегламентированныйОтчетЗаявлениеОВвозеТоваров.Форма.ФормаВводаСтавкиНалога", СтруктураСтавокФорма, ЭтаФорма);
			ДополнительныеПараметры = Новый Структура("НомерДопСтр, Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент", НомерДопСтр, Область, ПолеТабличногоДокументаРаздел1, СтандартнаяОбработка, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВводаСтавкиНалогаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ФормаВводаСтавкиНалога.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаВводаСтавкиНалога.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаВводаСтавкиНалога.Открыть();
		ИначеЕсли СтрНачинаетсяС(Область.Имя, "П000010000104")
			ИЛИ СтрНачинаетсяС(Область.Имя, "П000010000123") Тогда
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент, "Название");
		Иначе
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		КонецЕсли;
	ИначеЕсли Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Раздел3" Тогда
		
		ПолеТабличногоДокументаРаздел3 = ТабличныйДокумент;
		
		Если ИмяОбласти = "КодСтраныПродавец"
		 ИЛИ ИмяОбласти = "ПродавецМестоНахожд"
		 ИЛИ ИмяОбласти = "КодСтраныПокупатель" 
		 ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
			ВвестиМестонахождениеРаздел3(Элемент, Область, СтандартнаяОбработка);
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "ПродавецБайконур" Тогда
			СтандартнаяОбработка = Ложь;
			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел3");
			ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
			
			ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов);
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "ПокупательБайконур" Тогда
			СтандартнаяОбработка = Ложь;
			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателяРаздел3");
			ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
			
			ОтобразитьКодСтраны(ПараметрыОбработки, ДополнительныеДанныеРазделов);
            СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "НомерСпецификации" ИЛИ ИмяОбласти = "ДатаСпецификации" Тогда
			
			СтандартнаяОбработка = Ложь;
			ФормаВводаСпецификаций = ПолучитьФорму("Отчет.РегламентированныйОтчетЗаявлениеОВвозеТоваров.Форма.ФормаВводаСпецификаций", , ЭтаФорма);
			КопироватьДанныеФормы(мТЗСпецификацийРаздел3, ФормаВводаСпецификаций.ТЗСпецификаций);
			ДополнительныеПараметры =  Новый Структура("Область, ПолеТабличногоДокументаРаздел3, СтандартнаяОбработка, Элемент", Область, ПолеТабличногоДокументаРаздел3, СтандартнаяОбработка, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВводаСпецификацийРаздел3Завершение", ЭтотОбъект,ДополнительныеПараметры);
			ФормаВводаСпецификаций.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаВводаСпецификаций.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаВводаСпецификаций.Открыть();
		Иначе
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		КонецЕсли;
	ИначеЕсли Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Приложение" Тогда
		
		ПолеТабличногоДокументаПриложение = ТабличныйДокумент;
	
		Если ИмяОбласти = "КодСтраныПродавец"
		 ИЛИ ИмяОбласти = "ПродавецМестоНахожд"
		 ИЛИ ИмяОбласти = "КодСтраныПокупатель" 
		 ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
			ВвестиМестонахождениеПриложение(Элемент, Область, СтандартнаяОбработка);
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "ПродавецБайконур" Тогда
			
			Если Не мСтруктураМногостраничныхРазделов.Свойство("Приложение", ТаблицаСтраницРаздела) Тогда
				Возврат;
			КонецЕсли;
			
			ТаблицаСтраницРаздела = ЭтаФорма[ТаблицаСтраницРаздела];
			
			СтрТаблицаСтраницы = ТаблицаСтраницРаздела.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
			
			Если СтрТаблицаСтраницы.Количество() = 0 Тогда
				Возврат;
			Иначе
				СтрТаблицаСтраницы = СтрТаблицаСтраницы[0];
			КонецЕсли;

			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПродавца");
			ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
						
			Если СтрТаблицаСтраницы.ДополнительныеДанные.Количество() = 0 Тогда
				СтрТаблицаСтраницы.ДополнительныеДанные.Добавить(Новый Структура);
			КонецЕсли;
			
			ОтобразитьКодСтраны(ПараметрыОбработки, СтрТаблицаСтраницы.ДополнительныеДанные[0].Значение);
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "ПокупательБайконур" Тогда
			
			Если Не мСтруктураМногостраничныхРазделов.Свойство("Приложение", ТаблицаСтраницРаздела) Тогда
				Возврат;
			КонецЕсли;
			
			ТаблицаСтраницРаздела = ЭтаФорма[ТаблицаСтраницРаздела];
			
			СтрТаблицаСтраницы = ТаблицаСтраницРаздела.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
			
			Если СтрТаблицаСтраницы.Количество() = 0 Тогда
				Возврат;
			Иначе
				СтрТаблицаСтраницы = СтрТаблицаСтраницы[0];
			КонецЕсли;

			Область.Значение = ?(СокрЛП(Область.Значение) = "", "Х", "");
			Модифицированность = Истина;
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателя");
			ПараметрыОбработки.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
			ПараметрыОбработки.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
			
			Если СтрТаблицаСтраницы.ДополнительныеДанные.Количество() = 0 Тогда
				СтрТаблицаСтраницы.ДополнительныеДанные.Добавить(Новый Структура);
			КонецЕсли;
			
			ОтобразитьКодСтраны(ПараметрыОбработки, СтрТаблицаСтраницы.ДополнительныеДанные[0].Значение);
		 	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		ИначеЕсли ИмяОбласти = "НомерСпецификации" ИЛИ ИмяОбласти = "ДатаСпецификации" Тогда
			
			Если Не мСтруктураМногостраничныхРазделов.Свойство("Приложение", ТаблицаСтраницРаздела) Тогда
				Возврат;
			КонецЕсли;
			
			ТаблицаСтраницРаздела = ЭтаФорма[ТаблицаСтраницРаздела];
			
			СтрТаблицаСтраницы = ТаблицаСтраницРаздела.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
			
			Если СтрТаблицаСтраницы.Количество() = 0 Тогда
				Возврат;
			Иначе
				СтрТаблицаСтраницы = СтрТаблицаСтраницы[0];
			КонецЕсли;

			// получаем номер текущей доп страницы		
			НомерТекущейСтраницы = ТаблицаСтраницРаздела.Индекс(СтрТаблицаСтраницы);
		
			ТЗСтраницы = мМассивТЗСпецификацийПрил[НомерТекущейСтраницы];
			
			СтандартнаяОбработка = Ложь;
			ФормаВводаСпецификаций = ПолучитьФорму("Отчет.РегламентированныйОтчетЗаявлениеОВвозеТоваров.Форма.ФормаВводаСпецификаций", , ЭтаФорма);
			КопироватьДанныеФормы(ТЗСтраницы.Таблица, ФормаВводаСпецификаций.ТЗСпецификаций);
			ДополнительныеПараметры = Новый Структура("Область, ПолеТабличногоДокументаПриложение, СтандартнаяОбработка, ТЗСтраницы, Элемент", Область, ПолеТабличногоДокументаПриложение, СтандартнаяОбработка, ТЗСтраницы, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВводаСпецификацийПриложениеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ФормаВводаСпецификаций.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
			ФормаВводаСпецификаций.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаВводаСпецификаций.Открыть();
		Иначе
			СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПричиныВозникновенияЗаявления(ВыбЭлемент, ТекОбласть) Экспорт
	
	Если ВыбЭлемент = Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(ТекОбласть.Значение) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не выбрана причина возникновения заявления'");
			Сообщение.Сообщить();
		КонецЕсли;
	Иначе
		ВыбЗначение = ?(ВыбЭлемент.Значение = "Пустое значение", "", СокрЛП(ВыбЭлемент.Значение));
		ТекОбласть.Значение = ВыбЗначение;
		УстановитьДоступностьОбластейРаздела1(ЭтаФорма)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаСпецификацийПриложениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаПриложение = ДополнительныеПараметры.ПолеТабличногоДокументаПриложение;
	СтандартнаяОбработка = ДополнительныеПараметры.СтандартнаяОбработка;
	ТЗСтраницы = ДополнительныеПараметры.ТЗСтраницы;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		КопироватьДанныеФормы(Результат, ТЗСтраницы.Таблица);
		Тексты = ПолучитьТекстыИзТЗСпецификаций(ТЗСтраницы.Таблица);
		ПолеТабличногоДокументаПриложение.Области.НомерСпецификации.Значение = Тексты.Номер;
		ПолеТабличногоДокументаПриложение.Области.ДатаСпецификации.Значение = Тексты.Дата;
		Модифицированность = Истина;
	КонецЕсли;
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаСпецификацийРаздел3Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаРаздел3 = ДополнительныеПараметры.ПолеТабличногоДокументаРаздел3;
	СтандартнаяОбработка = ДополнительныеПараметры.СтандартнаяОбработка;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		КопироватьДанныеФормы(Результат, мТЗСпецификацийРаздел3);
		Тексты = ПолучитьТекстыИзТЗСпецификаций(мТЗСпецификацийРаздел3);
		ПолеТабличногоДокументаРаздел3.Области.НомерСпецификации.Значение = Тексты.Номер;
		ПолеТабличногоДокументаРаздел3.Области.ДатаСпецификации.Значение = Тексты.Дата;
		Модифицированность = Истина;
	КонецЕсли;
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаСтавкиНалогаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
	НомерДопСтр = ДополнительныеПараметры.НомерДопСтр;
	Область = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаРаздел1 = ДополнительныеПараметры.ПолеТабличногоДокументаРаздел1;
	СтандартнаяОбработка = ДополнительныеПараметры.СтандартнаяОбработка;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		
		НовСтруктура = Результат;
		
		Если НовСтруктура.ПредставлениеПрочерк Тогда
			ПолеТабличногоДокументаРаздел1.Области["П000010000114_" + НомерДопСтр].Значение = 0; 
			ПолеТабличногоДокументаРаздел1.Области["П000010000114_" + НомерДопСтр].Формат = "ЧН=-";
		Иначе
			ПолеТабличногоДокументаРаздел1.Области["П000010000114_" + НомерДопСтр].Формат = "ЧДЦ=6; ЧН=";
		КонецЕсли;
		
		ПолеТабличногоДокументаРаздел1.Области["П000010000116_" + НомерДопСтр].Значение = НовСтруктура.Ст16;
		ПолеТабличногоДокументаРаздел1.Области["П000010000117_" + НомерДопСтр].Значение = НовСтруктура.Ст17;
		ПолеТабличногоДокументаРаздел1.Области["П000010000118_" + НомерДопСтр].Значение = НовСтруктура.Ст18;
		ПолеТабличногоДокументаРаздел1.Области["П000010000121_" + НомерДопСтр].Значение = НовСтруктура.Ст21;
		
		РасчетНаКлиенте();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораВалютыРаздел1Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область						   = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаРаздел1 = ДополнительныеПараметры.ПолеТабличногоДокументаРаздел1;
	СтандартнаяОбработка 		   = ДополнительныеПараметры.СтандартнаяОбработка;
	Элемент						   = ДополнительныеПараметры.Элемент;
	                              	
	Если Результат <> Неопределено Тогда
		
		КодЭлементаСправочника = КодЭлементаСправочника(Результат);
		
		СуффиксНомераСтроки = Сред(Область.Имя, 14);
		
		Если Область.Значение = КодЭлементаСправочника Тогда	
			Область.Значение = "";
		Иначе
			Область.Значение = КодЭлементаСправочника;
			Если Область.Значение = "643" Тогда
				ПолеТабличногоДокументаРаздел1.Области["П000010000108" + СуффиксНомераСтроки].Значение = 1;
			КонецЕсли;
		КонецЕсли;
		
		ПолеТабличногоДокументаРаздел1.Области["П000010000125" + СуффиксНомераСтроки].Значение = 0;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаСервере
Функция КодЭлементаСправочника(Результат)
	
	Возврат Результат.Код;
	
КонецФункции	

&НаКлиенте
Процедура ОткрытьФормуВводаСпецификации_07Раздел1Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаРаздел1 = ДополнительныеПараметры.ПолеТабличногоДокументаРаздел1;
	СтандартнаяОбработка = ДополнительныеПараметры.СтандартнаяОбработка;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		КопироватьДанныеФормы(Результат, мТЗСпецификацийРаздел1_07);
		Тексты = ПолучитьТекстыИзТЗСпецификаций(мТЗСпецификацийРаздел1_07);
		ПолеТабличногоДокументаРаздел1.Области.НомерСпецификации_07.Значение = Тексты.Номер;
		ПолеТабличногоДокументаРаздел1.Области.ДатаСпецификации_07.Значение = Тексты.Дата;
		Модифицированность = Истина;
	КонецЕсли;
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаСпецификацииРаздел1Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаРаздел1 = ДополнительныеПараметры.ПолеТабличногоДокументаРаздел1;
	СтандартнаяОбработка = ДополнительныеПараметры.СтандартнаяОбработка;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		КопироватьДанныеФормы(Результат, мТЗСпецификацийРаздел1);
		Тексты = ПолучитьТекстыИзТЗСпецификаций(мТЗСпецификацийРаздел1);
		ПолеТабличногоДокументаРаздел1.Области.НомерСпецификации.Значение = Тексты.Номер;
		ПолеТабличногоДокументаРаздел1.Области.ДатаСпецификации.Значение = Тексты.Дата;
		Модифицированность = Истина;
	КонецЕсли;
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаполненияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КлючСтруктуры = ДополнительныеПараметры.КлючСтруктуры;
	НомерСтроки = ДополнительныеПараметры.НомерСтроки;
	Область = ДополнительныеПараметры.Область;
	ПолеТабличногоДокументаРаздел1 = ДополнительныеПараметры.ПолеТабличногоДокументаРаздел1;
	СтандартнаяОбработка = ДополнительныеПараметры.СтандартнаяОбработка;
	ТранспДокументы = ДополнительныеПараметры.ТранспДокументы;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		
		ТранспДокументы.Очистить();
		
		Для Каждого Док Из Результат Цикл
			ТранспДокументы.Добавить(Новый Структура("Серия, Номер, Дата", Док.Серия, Док.Номер, Док.Дата));
		КонецЦикла;
		
		мСтрукТЗДокументы.Вставить(КлючСтруктуры, ТранспДокументы);
		
		СерияНомерТД = "";
		ДатаТД       = "";
		
		Для каждого Док Из Результат Цикл
			
			Серия = СокрЛП(Док.Серия);                   
			Номер = СокрЛП(Док.Номер);
			Дата  = Формат(Док.Дата, "ДФ=""дд.ММ.гггг 'г.'""");
			
			Если СерияНомерТД <> "" Тогда
				СерияНомерТД = СерияНомерТД + Символы.ПС;
			КонецЕсли;
			
			Если ДатаТД <> "" Тогда
				ДатаТД = ДатаТД + Символы.ПС;
			КонецЕсли;
			
			СерияНомерТД = СерияНомерТД + Серия + ?(ПустаяСтрока(Серия) ИЛИ ПустаяСтрока(Номер), "", ", ") + Номер;
			
			ДатаТД = ДатаТД + Дата; 
			
		КонецЦикла; 
		
		ПолеТабличногоДокументаРаздел1.Области["П000010000109" + НомерСтроки].Значение = СерияНомерТД;
		ПолеТабличногоДокументаРаздел1.Области["П000010000110" + НомерСтроки].Значение = ДатаТД;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДинамическийСписокВыбора(Область, СтандартнаяОбработка, Элемент, КолонкаПоиска = "Код")
	
	ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтаФорма, Область.Имя);
	
	//создание динамического списка выбора
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		//обработка таблицы значений - удаление вариантов, не проходящих по условиям
		КодПоказателя = ПараметрВводаПоля.Показатель;
		КопироватьДанныеФормы(ПараметрВводаПоля.ТаблицаЗначений, ТЗВыбора);
		ВвестиПоказатель(Элемент, Область, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ТЗВыбора, СтандартнаяОбработка, КолонкаПоиска);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьКодСтраны(ПараметрыВвода, ХранилищеДополнительныхСведений)
	
	ИмяЭлементаСтруктуры = ПараметрыВвода.ИмяЭлементаСтруктуры;
	ИмяОбластиКодаСтраны = ПараметрыВвода.ИмяОбластиКодаСтраны;
	ИмяОбластиМестонахождения = ПараметрыВвода.ИмяОбластиМестонахождения;
	ИмяОбластиМестонахождениеБайконур = ПараметрыВвода.ИмяОбластиМестонахождениеБайконур;
	
	Если ТипЗнч(ХранилищеДополнительныхСведений) <> Тип("Структура")
	 ИЛИ НЕ ХранилищеДополнительныхСведений.Свойство(ИмяЭлементаСтруктуры) Тогда
		Возврат;
	КонецЕсли;
	           
	АдресМестонахождения = ХранилищеДополнительныхСведений[ИмяЭлементаСтруктуры];
	НаБайконуре = НЕ ПустаяСтрока(ТабличныйДокумент.Области[ИмяОбластиМестонахождениеБайконур].Значение);
	
	КодСтраны = "";
	
	Если ЗначениеЗаполнено(АдресМестонахождения.Страна) Тогда
		КодСтраны = ОпределитьКодСтраны(АдресМестонахождения.Страна);
	КонецЕсли;

	ТабличныйДокумент.Области[ИмяОбластиКодаСтраны].Значение = ?(НаБайконуре, "398", КодСтраны);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьКодСтраны(Страна)
	
	Возврат Справочники.СтраныМира.НайтиПоНаименованию(Страна).Код;
	
КонецФункции

&НаКлиенте
Процедура ВвестиМестонахождениеРаздел1(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	ПараметрыВвода = Новый Структура;
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел1");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
	ИначеЕсли ИмяОбласти = "КодСтраныОрг1" 
	      ИЛИ ИмяОбласти = "МестоНахождОрг_1" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресКомиссионераРаздел1");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныОрг1");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "МестоНахождОрг_1");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "КомиссионерБайконур");
		
	КонецЕсли;
	
	ВвестиМестонахождение(ПараметрыВвода, ДополнительныеДанныеРазделов, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиМестонахождениеРаздел3(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	ПараметрыВвода = Новый Структура;
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПродавцаРаздел3");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
	ИначеЕсли ИмяОбласти = "КодСтраныПокупатель" 
	      ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателяРаздел3");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
	КонецЕсли;
	
	ВвестиМестонахождение(ПараметрыВвода, ДополнительныеДанныеРазделов, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиМестонахождениеПриложение(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	ТаблицаСтраницРаздела = Неопределено;
	
	Если Не мСтруктураМногостраничныхРазделов.Свойство("Приложение", ТаблицаСтраницРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	СтраницыРазделаПриложение = ЭтаФорма[ТаблицаСтраницРаздела];
	
	АктивнаяСтраница = СтраницыРазделаПриложение.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
		
	Если АктивнаяСтраница.Количество() = 0 Тогда
		Возврат;
	Иначе
		АктивнаяСтраница = АктивнаяСтраница[0];
	КонецЕсли;
	
	Если АктивнаяСтраница.ДополнительныеДанные.Количество() = 0 Тогда
		АктивнаяСтраница.ДополнительныеДанные.Добавить(Новый Структура);
	КонецЕсли;
			
	ПараметрыВвода = Новый Структура;
	Если ИмяОбласти = "КодСтраныПродавец"
	 ИЛИ ИмяОбласти = "ПродавецМестоНахожд" Тогда
	
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПродавца");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПродавец");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПродавецМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПродавецБайконур");
		
	ИначеЕсли ИмяОбласти = "КодСтраныПокупатель" 
	      ИЛИ ИмяОбласти = "ПокупательМестоНахожд" Тогда
		
		ПараметрыВвода.Вставить("ИмяЭлементаСтруктуры", "АдресПокупателя");
		ПараметрыВвода.Вставить("ИмяОбластиКодаСтраны", "КодСтраныПокупатель");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождения", "ПокупательМестоНахожд");
		ПараметрыВвода.Вставить("ИмяОбластиМестонахождениеБайконур", "ПокупательБайконур");
		
	КонецЕсли;
	
	ВвестиМестонахождение(ПараметрыВвода, АктивнаяСтраница.ДополнительныеДанные[0].Значение, Элемент);
	
	АктивнаяСтраница.ДополнительныеДанные[0].Пометка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиМестонахождение(ПараметрыВвода, ХранилищеДополнительныхСведений, Элемент)
	
	ИмяЭлементаСтруктуры = ПараметрыВвода.ИмяЭлементаСтруктуры;
	ИмяОбластиКодаСтраны = ПараметрыВвода.ИмяОбластиКодаСтраны;
	ИмяОбластиМестонахождения = ПараметрыВвода.ИмяОбластиМестонахождения;
	
	Если ТипЗнч(ХранилищеДополнительныхСведений) <> Тип("Структура") Тогда
		ХранилищеДополнительныхСведений = Новый Структура;
	КонецЕсли;
	
	ВводимыйАдрес = Неопределено;
	Если НЕ ХранилищеДополнительныхСведений.Свойство(ИмяЭлементаСтруктуры, ВводимыйАдрес) Тогда
		ВводимыйАдрес = Новый Структура;
		ВводимыйАдрес.Вставить("Страна",          "");
		ВводимыйАдрес.Вставить("Индекс",          "");
		ВводимыйАдрес.Вставить("Регион",          "");
		ВводимыйАдрес.Вставить("Район",           "");
		ВводимыйАдрес.Вставить("Город",           "");
		ВводимыйАдрес.Вставить("НаселенныйПункт", "");
		ВводимыйАдрес.Вставить("Улица",           "");
		ВводимыйАдрес.Вставить("Дом",             "");
		ВводимыйАдрес.Вставить("Корпус",          "");
		ВводимыйАдрес.Вставить("Квартира",        "");
		
		ВводимыйАдрес.Вставить("ТипДома",          "");
		ВводимыйАдрес.Вставить("ТипКорпуса",       "");
		ВводимыйАдрес.Вставить("ТипКвартиры",      "");
		
		ВводимыйАдрес.Вставить("Представление", "");
		
	КонецЕсли;
	
	ЗначенияПолей = Новый СписокЗначений;
	
	Если ВводимыйАдрес["Страна"] = "" Тогда
		ВводимыйАдрес["Страна"] = "РОССИЯ"
	КонецЕсли;
	
	КодСтраны = ОпределитьКодСтраны(ВводимыйАдрес["Страна"]);
	
	ЗначенияПолей.Добавить(КодСтраны,                        "КодСтраны");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Страна"],          "Страна");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Индекс"],          "Индекс");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Регион"],          "Регион");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Район"],           "Район");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Город"],           "Город");
	ЗначенияПолей.Добавить(ВводимыйАдрес["НаселенныйПункт"], "НаселенныйПункт");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Улица"],           "Улица");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Дом"],             "Дом");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Корпус"],          "Корпус");
	ЗначенияПолей.Добавить(ВводимыйАдрес["Квартира"],        "Квартира");
	ЗначенияПолей.Добавить(ВводимыйАдрес["ТипДома"],         "ТипДома");
	ЗначенияПолей.Добавить(ВводимыйАдрес["ТипКорпуса"],      "ТипКорпуса");
	ЗначенияПолей.Добавить(ВводимыйАдрес["ТипКвартиры"],     "ТипКвартиры");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",               "Ввод адреса");
	ПараметрыФормы.Вставить("ЗначенияПолей", 		   ЗначенияПолей);
	ПараметрыФормы.Вставить("Представление", 		   ВводимыйАдрес.Представление);
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации", СтруктураРеквизитовФормы.СправочникиВидыКонтактнойИнформацииАдресОрганизации);
			
	// Если это иностранное государство - удаляем из представления адреса "страну", во избежание ее дублирования.
	Если ЗначениеЗаполнено(ВводимыйАдрес["Страна"]) И ВводимыйАдрес["Страна"] <> "РОССИЯ" Тогда
		Если Лев(ВводимыйАдрес.Представление, СтрДлина(ВводимыйАдрес["Страна"]) + 2) = ВводимыйАдрес["Страна"] + ", " Тогда
			ВводимыйАдрес.Представление = Сред(ВводимыйАдрес.Представление, СтрДлина(ВводимыйАдрес["Страна"]) + 3);
			ПараметрыФормы.Вставить("Представление", ВводимыйАдрес.Представление);
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВводимыйАдрес", ВводимыйАдрес);
	ДополнительныеПараметры.Вставить("ИмяЭлементаСтруктуры", ИмяЭлементаСтруктуры);
	ДополнительныеПараметры.Вставить("ПараметрыВвода", ПараметрыВвода);
	ДополнительныеПараметры.Вставить("ХранилищеДополнительныхСведений", ХранилищеДополнительныхСведений);
	ДополнительныеПараметры.Вставить("ИмяОбластиМестонахождения", ИмяОбластиМестонахождения);
	
	ТипЗначения = Тип("ОписаниеОповещения");
	ПараметрыКонструктора = Новый Массив(3);
	ПараметрыКонструктора[0] = "ОткрытьФормуКонтактнойИнформацииЗавершение";
	ПараметрыКонструктора[1] = ЭтаФорма;
	ПараметрыКонструктора[2] = ДополнительныеПараметры;
	
	Оповещение = Новый (ТипЗначения, ПараметрыКонструктора);
	
	ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент").ОткрытьФормуКонтактнойИнформации(ПараметрыФормы, , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, Параметры) Экспорт
	
	ОбновитьАдресВТабличномДокументе(Результат, Параметры.ВводимыйАдрес, Параметры.ИмяЭлементаСтруктуры, Параметры.ПараметрыВвода, Параметры.ХранилищеДополнительныхСведений, Параметры.ИмяОбластиМестонахождения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАдресВТабличномДокументе(Результат, ВводимыйАдрес, ИмяЭлементаСтруктуры, ПараметрыВвода, ХранилищеДополнительныхСведений, ИмяОбластиМестонахождения)
	
	Если Результат <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			
			// Обход ошибки платформы: в веб-клиенте, в результате выполнения процедуры "СформироватьАдрес"
			// общего модуля "РегламентированнаяОтчетностьВызовСервера", не изменяются значения ключей
			// соответствия "ВводимыйАдрес", передаваемого в качестве параметра.
			ВводимыйАдрес_ = ВводимыйАдрес;
			
			ВводимыйАдрес_.Вставить("Страна",          "");
			ВводимыйАдрес_.Вставить("Индекс",          "");
			ВводимыйАдрес_.Вставить("Регион",          "");
			ВводимыйАдрес_.Вставить("Район",           "");
			ВводимыйАдрес_.Вставить("Город",           "");
			ВводимыйАдрес_.Вставить("НаселенныйПункт", "");
			ВводимыйАдрес_.Вставить("Улица",           "");
			ВводимыйАдрес_.Вставить("Дом",             "");
			ВводимыйАдрес_.Вставить("Корпус",          "");
			ВводимыйАдрес_.Вставить("Квартира",        "");
			ВводимыйАдрес_.Вставить("ТипДома",         "");
			ВводимыйАдрес_.Вставить("ТипКорпуса",      "");
			ВводимыйАдрес_.Вставить("ТипКвартиры",     "");
			
			РегламентированнаяОтчетностьВызовСервера.СформироватьАдрес(Результат.КонтактнаяИнформация, ВводимыйАдрес_);
			
			ВводимыйАдрес_.Представление = Результат.Представление;
			
			ХранилищеДополнительныхСведений.Вставить(ИмяЭлементаСтруктуры, ВводимыйАдрес_);
			
			ТабличныйДокумент.Области[ИмяОбластиМестонахождения].Значение = ВводимыйАдрес_.Представление;
			
			ОтобразитьКодСтраны(ПараметрыВвода, ХранилищеДополнительныхСведений);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриИзмененииСодержимогоОбласти(Элемент, Область)
	Модифицированность = Истина;
		
	ИмяТекущейСтраницыПанели = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;

	ОбластьИмя = Область.Имя;
	Если ОбластьИмя = "Число" Или ОбластьИмя = "Месяц" Или ОбластьИмя = "Год" Тогда
		ДатаПодписи = Область.Значение;
		ТабличныйДокумент.Области.Число.Значение = ДатаПодписи;
		ТабличныйДокумент.Области.Месяц.Значение = ДатаПодписи;
		ТабличныйДокумент.Области.Год.Значение = ДатаПодписи;
		ДатаПодписиПриИзмененииНаСервере();
		Заголовок = СтрЗаменить(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета, ПредставлениеПериода(НачалоДня(ДатаПодписи), КонецДня(ДатаПодписи), "ФП = Истина" ));
		СтруктураРеквизитовФормы.СтрПериодОтчета = ПредставлениеПериода(НачалоДня(ДатаПодписи), КонецДня(ДатаПодписи), "ФП = Истина");
		Возврат;
	ИначеЕсли ОбластьИмя = "НомерКорректировки" Тогда 
		ОписаниеТипа = Новый ОписаниеТипов("Число");
		НомерКорректировки = ОписаниеТипа.ПривестиЗначение(Область.Значение);
		СтруктураРеквизитовФормы.ВидДокумента = ?(НомерКорректировки = 0, 0, 1);
		Область.Значение = Строка(НомерКорректировки);
		Возврат;
	КонецЕсли;

	Если ИмяТекущейСтраницыПанели = "Раздел1" Тогда
						
		Если СтрНайти(ОбластьИмя, "П000010000104") > 0  И СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
			НомерСтроки = Сред(ОбластьИмя, 15);
			ИмяСлужебногоПоля = "П000010000122_" + НомерСтроки;
			Если ТабличныйДокумент.Области.Найти(ИмяСлужебногоПоля) <> Неопределено Тогда
				ТабличныйДокумент.Области[ИмяСлужебногоПоля].Значение = ""; //очищаем служебное поле кода единицы измерения
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru='При вводе единицы измерения вручную в сформированный файл выгрузки попадет введенное значение!'");
				Сообщение.Сообщить();
			КонецЕсли;
				
		ИначеЕсли СтрНайти(ОбластьИмя, "П000010000123") = 1 И СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
			НомерСтроки = Сред(ОбластьИмя, 15);
			ИмяСлужебногоПоля = "П000010000124_" + НомерСтроки;
			Если ТабличныйДокумент.Области.Найти(ИмяСлужебногоПоля) <> Неопределено Тогда
				ТабличныйДокумент.Области[ИмяСлужебногоПоля].Значение = ""; //очищаем служебное поле кода единицы измерения
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru='При вводе единицы измерения дополнительной величины вручную в сформированный файл выгрузки попадет введенное значение!'");
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;	
		
		РасчетНаКлиенте(ИмяТекущейСтраницыПанели, ОбластьИмя);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
		Модифицированность = Истина;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуОтчетаЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтаФорма);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.Проверить(СпДокОсн);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьДанныеРеквизитовВыгрузки()
	
	Если СтруктурареквизитовФормы.НаимТекущегоРаздела = "Раздел1" Тогда
		ФИОПодпТитул = ТабличныйДокумент.Области.ФИОРук.Значение;
	Иначе
		ФИОПодпТитул = СтруктураДанныхРаздел1.ФИОРук;
	КонецЕсли;
	
	ЭтоПБОЮЛ = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);
	
	СтрокаСведений = "ИННЮЛ, ИННФЛ, КППЮЛ, НаимЮЛПол, ФИО, КодНО";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СтруктураРеквизитовФормы.Организация, ДатаПодписи, СтрокаСведений);
	
	ПараметрыВыгрузки = Новый Структура;
	
	ПараметрыВыгрузки.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);
	ПараметрыВыгрузки.Вставить("НалогоплательщикИННЮЛ", СведенияОбОрганизации.ИННЮЛ);
	ПараметрыВыгрузки.Вставить("НалогоплательщикКПП", 	СведенияОбОрганизации.КППЮЛ);
	ПараметрыВыгрузки.Вставить("НалогоплательщикИННФЛ", СведенияОбОрганизации.ИННФЛ);
	ПараметрыВыгрузки.Вставить("НалогоплательщикНаимЮЛ",СведенияОбОрганизации.НаимЮЛПол);
	ПараметрыВыгрузки.Вставить("НалогоплательщикФИО", 	СведенияОбОрганизации.ФИО);
	
	ПараметрыВыгрузки.Вставить("КодИФНС", СведенияОбОрганизации.КодНО);
	
	ДанныеПредставителя = ПолучитьСведенияОПредставителе(СведенияОбОрганизации.КодНО); //возвращает ТипПредставителя, флПредставительЮрЛицо, ФИОПредставителя, ПредставительСсылка
	
	ПараметрыВыгрузки.Вставить("ПрОтпр",	Число(ДанныеПредставителя.ТипПредставителя)); //1 - НП, 2 - представитель НП
	
	ОтправительЯвляетсяЮЛ = Истина;
	
	НаимОтпр  = "";
	ИННЮЛОтпр = "";
	КППОтпр   = "";
	
	ИННФЛОтпр = "";
	
	ФамилияФЛОтпр  = "";
	ИмяФЛОтпр      = "";
	ОтчествоФЛОтпр = "";
	
	ПрПодп        = 0;
	ИННФЛПодп     = "";
	ДолжностьПодп = "";
	
	ФамилияПодп  = "";
	ИмяПодп      = "";
	ОтчествоПодп = "";
	
	НаимДовПодп  = "";
	НомерДовПодп = "";
	ДатаДовПодп  = "";
	
	Если ДанныеПредставителя.ТипПредставителя = "1" Тогда
		Если НЕ ЭтоПБОЮЛ Тогда
			ОтправительЯвляетсяЮЛ = Истина;
			НаимОтпр        = СведенияОбОрганизации.НаимЮЛПол;
			ИННЮЛОтпр       = СведенияОбОрганизации.ИННЮЛ;
			КППОтпр         = СведенияОбОрганизации.КППЮЛ;
			
			ПрПодп = 2;
			ФИОПодп = РегламентированнаяОтчетность.РазложитьФИО(ФИОПодпТитул);
			ФамилияПодп  = ФИОПодп.Фамилия;
			ИмяПодп      = ФИОПодп.Имя;
			ОтчествоПодп = ФИОПодп.Отчество;
			
		Иначе
			ОтправительЯвляетсяЮЛ = Ложь;
			ИННФЛОтпр      = СведенияОбОрганизации.ИННФЛ;
			ФИОотправителя = РегламентированнаяОтчетность.РазложитьФИО(ФИОПодпТитул);
			ФамилияФЛОтпр  = ФИОотправителя.Фамилия;
			ИмяФЛОтпр      = ФИОотправителя.Имя;
			ОтчествоФЛОтпр = ФИОотправителя.Отчество;
			
			ПрПодп = 1;
			ФИОПодп = РегламентированнаяОтчетность.РазложитьФИО(ФИОПодпТитул);
			ФамилияПодп  = ФИОПодп.Фамилия;
			ИмяПодп      = ФИОПодп.Имя;
			ОтчествоПодп = ФИОПодп.Отчество;
			
		КонецЕсли;
		
	Иначе
		Если ДанныеПредставителя.флПредставительЮрЛицо Тогда
			ОтправительЯвляетсяЮЛ = Истина;
			ПредставительСсылка = ДанныеПредставителя.ПредставительСсылка;
			НаимОтпр  = СокрЛП(ПредставительСсылка);
			ИННЮЛОтпр = ПредставительСсылка.ИНН;
			КППОтпр   = ПредставительСсылка.КПП;
			
			ПрПодп = 3;
			ФИОПодп = РегламентированнаяОтчетность.РазложитьФИО(ФИОПодпТитул);
			ФамилияПодп  = ФИОПодп.Фамилия;
			ИмяПодп      = ФИОПодп.Имя;
			ОтчествоПодп = ФИОПодп.Отчество;
			
		Иначе
			ОтправительЯвляетсяЮЛ = Ложь;
			ФИОотправителя = РегламентированнаяОтчетность.РазложитьФИО(ФИОПодпТитул);
			ФамилияФЛОтпр  = ФИОотправителя.Фамилия;
			ИмяФЛОтпр      = ФИОотправителя.Имя;
			ОтчествоФЛОтпр = ФИОотправителя.Отчество;
			
			ПрПодп = 3;
			ФамилияПодп  = ФИОотправителя.Фамилия;
			ИмяПодп      = ФИОотправителя.Имя;
			ОтчествоПодп = ФИОотправителя.Отчество;
			
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ОтправительЯвляетсяЮЛ", ОтправительЯвляетсяЮЛ);
	
	ПараметрыВыгрузки.Вставить("НаимОтпр",  НаимОтпр);
	ПараметрыВыгрузки.Вставить("ИННЮЛОтпр", ИННЮЛОтпр);
	ПараметрыВыгрузки.Вставить("КППОтпр",   КППОтпр);
	
	ПараметрыВыгрузки.Вставить("ИННФЛОтпр", ИННФЛОтпр);
	
	ПараметрыВыгрузки.Вставить("ФамилияФЛОтпр",  ФамилияФЛОтпр);
	ПараметрыВыгрузки.Вставить("ИмяФЛОтпр",      ИмяФЛОтпр);
	ПараметрыВыгрузки.Вставить("ОтчествоФЛОтпр", ОтчествоФЛОтпр);
	
	ПараметрыВыгрузки.Вставить("ПрПодп",        ПрПодп);
	ПараметрыВыгрузки.Вставить("ИННФЛПодп",     ИННФЛПодп);
	ПараметрыВыгрузки.Вставить("ДолжностьПодп", ДолжностьПодп);
	
	ПараметрыВыгрузки.Вставить("ФамилияПодп",  ФамилияПодп);
	ПараметрыВыгрузки.Вставить("ИмяПодп",      ИмяПодп);
	ПараметрыВыгрузки.Вставить("ОтчествоПодп", ОтчествоПодп);
	
	ПараметрыВыгрузки.Вставить("НаимДовПодп",  НаимДовПодп);
	ПараметрыВыгрузки.Вставить("НомерДовПодп", НомерДовПодп);
	ПараметрыВыгрузки.Вставить("ДатаДовПодп",  ДатаДовПодп);
	
	ВосстановитьСохраненныеДанныеОбласти();
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

&НаСервере
Функция ПроверитьПоказатели() Экспорт
	
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	Если НЕ ВыгрузкаВозможна(ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СТЫКОВКИ С СЕРВИСОМ СДАЧИ ОТЧЕТНОСТИ ЧЕРЕЗ ПРЕДСТАВИТЕЛЯ

&НаКлиенте
Процедура ОтправитьЧерезПредставителя(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезПредставителяЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителяЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиЧерезПредставителяВРегламентированномОтчете(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВИнтернетеЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма);
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтаФорма);
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	СохранитьНаКлиенте(, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ДополнительныеПараметры.Форма.Закрыть();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазделТекущейОбласти(Форма, ТекущаяОбласть, ДлИмениОбласти = 13) Экспорт
	
	Если ТекущаяОбласть = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяТекущейОбласти = СтрЗаменить(ТекущаяОбласть.Имя, "ДобавитьСтрокуЗначок", "ДобавитьСтроку");
	Если ИмяТекущейОбласти = "ДобавитьСтроку1" Тогда
		Возврат Форма.СтруктураРеквизитовФормы.мИдГруппы1;
	ИначеЕсли СтрНайти(ИмяТекущейОбласти, "УдалитьСтроку") > 0 Тогда
		ИмяТекущейОбласти = СтрЗаменить(ИмяТекущейОбласти, "УдалитьСтроку", "");
	КонецЕсли;
	
	ИмяАктивногоРаздела = Лев(ИмяТекущейОбласти, ДлИмениОбласти);
	
	Для Каждого ЭлементСтруктуры Из Форма.мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = Форма[ЭлементСтруктуры.Значение];
		Если СтруктураГруппы[0].Свойство(ИмяАктивногоРаздела) Тогда
			Возврат ИмяГруппы;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ТабличныйДокументПриАктивизацииОбласти(Элемент)
	Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Доступность = Ложь;
	Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Доступность = Ложь;
	
	ИмяГруппы = РазделТекущейОбласти(ЭтаФорма, Элемент.ТекущаяОбласть);
	
	Если ЗначениеЗаполнено(ИмяГруппы) Тогда 
		ТестовоеИмя = ИмяГруппы + "01_" + Формат(мСтруктураИсхКолвоСтрокРазделов[ИмяГруппы] + 1, "ЧГ=");
		Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Доступность = Истина;
		Если СтрЧислоВхождений(Элемент.ТекущаяОбласть.Имя, "ДобавитьСтроку") = 0 И
			ТабличныйДокумент.Области.Найти(ТестовоеИмя) <> Неопределено Тогда
			Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Доступность = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#Область ГруппаСведенияОПодписантах
&НаСервере
Процедура СохранитьДанныеОбласти()
	
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда 
		мСтруктураРеквизитовВыгрузки = ОбновитьДанныеРеквизитовВыгрузки();
	КонецЕсли;
	
	мСтруктураРеквизитовВыгрузки.КодИФНС = КодИФНС;
	
	Если КтоОтправляет = 1 Тогда
		мСтруктураРеквизитовВыгрузки.ПрОтпр = 1;
		Если СтруктураРеквизитовФормы.ИП Тогда
			мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ = Ложь;
		Иначе
			мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ = Истина;
		КонецЕсли;
	ИначеЕсли КтоОтправляет = 2 Тогда
		мСтруктураРеквизитовВыгрузки.ПрОтпр = 2;
		мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ = Истина;
	ИначеЕсли КтоОтправляет = 3 Тогда
		мСтруктураРеквизитовВыгрузки.ПрОтпр = 2;
		мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ = Ложь;
	КонецЕсли;
	мСтруктураРеквизитовВыгрузки.ФамилияФЛОтпр = ФамилияОтправляет;
	мСтруктураРеквизитовВыгрузки.ИмяФЛОтпр = ИмяОтправляет;
	мСтруктураРеквизитовВыгрузки.ОтчествоФЛОтпр = ОтчествоОтправляет;
	мСтруктураРеквизитовВыгрузки.ИННФЛОтпр = ИННОтправляет;
	мСтруктураРеквизитовВыгрузки.КППОтпр = КППОтправляет;
	мСтруктураРеквизитовВыгрузки.ИННЮЛОтпр = ИННОтправляетЮЛ;
	мСтруктураРеквизитовВыгрузки.НаимОтпр = НаименованиеОтправляет;
	
	мСтруктураРеквизитовВыгрузки.ПрПодп = Подписант;
	мСтруктураРеквизитовВыгрузки.ФамилияПодп = ФамилияПодписант;
	мСтруктураРеквизитовВыгрузки.ИмяПодп = ИмяПодписант;
	мСтруктураРеквизитовВыгрузки.ОтчествоПодп = ОтчествоПодписант;
	мСтруктураРеквизитовВыгрузки.ИННФЛПодп = ИННПодписант;
	мСтруктураРеквизитовВыгрузки.ДолжностьПодп = ДолжностьПодписант;
	мСтруктураРеквизитовВыгрузки.НаимДовПодп = ДоверенностьПодписант;
	мСтруктураРеквизитовВыгрузки.НомерДовПодп = НомерДоверенностиПодписант;
	мСтруктураРеквизитовВыгрузки.ДатаДовПодп = ДатаДоверенностиПодписант;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеДанныеОбласти()
	
	Если мСтруктураРеквизитовВыгрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодИФНС = мСтруктураРеквизитовВыгрузки.КодИФНС;
	
	Если мСтруктураРеквизитовВыгрузки.ПрОтпр = 1 Тогда 
		КтоОтправляет = 1;
	ИначеЕсли мСтруктураРеквизитовВыгрузки.ОтправительЯвляетсяЮЛ Тогда 
		КтоОтправляет = 2;
	Иначе
		КтоОтправляет = 3;
	КонецЕсли;
	ФамилияОтправляет = мСтруктураРеквизитовВыгрузки.ФамилияФЛОтпр;
	ИмяОтправляет = мСтруктураРеквизитовВыгрузки.ИмяФЛОтпр;
	ОтчествоОтправляет = мСтруктураРеквизитовВыгрузки.ОтчествоФЛОтпр;
	ИННОтправляет = мСтруктураРеквизитовВыгрузки.ИННФЛОтпр;
	КППОтправляет = мСтруктураРеквизитовВыгрузки.КППОтпр;
	ИННОтправляетЮЛ = мСтруктураРеквизитовВыгрузки.ИННЮЛОтпр;
	НаименованиеОтправляет = мСтруктураРеквизитовВыгрузки.НаимОтпр;
	
	Подписант = мСтруктураРеквизитовВыгрузки.ПрПодп;
	ФамилияПодписант = мСтруктураРеквизитовВыгрузки.ФамилияПодп;
	ИмяПодписант = мСтруктураРеквизитовВыгрузки.ИмяПодп;
	ОтчествоПодписант = мСтруктураРеквизитовВыгрузки.ОтчествоПодп;
	ИННПодписант = мСтруктураРеквизитовВыгрузки.ИННФЛПодп;
	ДолжностьПодписант = мСтруктураРеквизитовВыгрузки.ДолжностьПодп;
	ДоверенностьПодписант = мСтруктураРеквизитовВыгрузки.НаимДовПодп;
	НомерДоверенностиПодписант = мСтруктураРеквизитовВыгрузки.НомерДовПодп;
	ДатаДоверенностиПодписант = мСтруктураРеквизитовВыгрузки.ДатаДовПодп;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияОбласти()
	ВосстановитьСохраненныеДанныеОбласти();
	СтруктураРеквизитовФормы.ЭтоЮрЛицо = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);
	Если СтруктураРеквизитовФормы.ЭтоЮрЛицо = Истина Тогда
		СВ = Элементы.Подписант.СписокВыбора;
		СВ.Удалить(0);
		Если (Подписант = 0) Тогда 
			Подписант = 2;
		КонецЕсли;
	Иначе
		СВ = Элементы.Подписант.СписокВыбора;
		СВ.Удалить(1);
		Если (Подписант = 0) Тогда
			Подписант = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если (КтоОтправляет = 0) Тогда
		КтоОтправляет = 1;
	КонецЕсли;
	
	Элементы.ГруппаДоверенностьПодписант.Доступность = (Подписант = 3);
	
	Если СтруктураРеквизитовФормы.ЭтоЮрЛицо = Истина Тогда
		Элементы.ГруппаОтправляетФЛ.Видимость = (КтоОтправляет = 3);
		Элементы.ГруппаИННКППОтправляетФЛ.Видимость = (КтоОтправляет = 3);
		Элементы.ГруппаОтправляетЮЛ.Видимость = (КтоОтправляет <> 3);
		Элементы.ГруппаИННКППОтправляетЮЛ.Видимость = (КтоОтправляет <> 3);
	Иначе
		Элементы.ГруппаОтправляетФЛ.Видимость = (КтоОтправляет = 1 Или КтоОтправляет = 3);
		Элементы.ГруппаИННКППОтправляетФЛ.Видимость = (КтоОтправляет = 1 Или КтоОтправляет = 3);
		Элементы.ГруппаОтправляетЮЛ.Видимость = (КтоОтправляет = 2);
		Элементы.ГруппаИННКППОтправляетЮЛ.Видимость = (КтоОтправляет = 2);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
	Элементы.ГруппаДоверенностьПодписант.Доступность = (Подписант = 3);
КонецПроцедуры

&НаКлиенте
Процедура КтоОтправляетПриИзменении(Элемент)
	Если Элемент <> Ложь Тогда 
		Модифицированность = Истина;
	КонецЕсли;
	Если СтруктураРеквизитовФормы.ЭтоЮрЛицо = Истина Тогда 
		Элементы.ГруппаОтправляетФЛ.Видимость = (КтоОтправляет = 3);
		Элементы.ГруппаИННКППОтправляетФЛ.Видимость = (КтоОтправляет = 3);
		Элементы.ГруппаОтправляетЮЛ.Видимость = (КтоОтправляет <> 3);
		Элементы.ГруппаИННКППОтправляетЮЛ.Видимость = (КтоОтправляет <> 3);
	Иначе
		Элементы.ГруппаОтправляетФЛ.Видимость = (КтоОтправляет = 1 Или КтоОтправляет = 3);
		Элементы.ГруппаИННКППОтправляетФЛ.Видимость = (КтоОтправляет = 1 Или КтоОтправляет = 3);
		Элементы.ГруппаОтправляетЮЛ.Видимость = (КтоОтправляет = 2);
		Элементы.ГруппаИННКППОтправляетЮЛ.Видимость = (КтоОтправляет = 2);
	КонецЕсли;
	Если КтоОтправляет = 1 Тогда
		Элементы.ГруппаОтправляет.Доступность = Ложь;
		Элементы.ГруппаИННКППОтправляет.Доступность = Ложь;
	Иначе
		Элементы.ГруппаОтправляет.Доступность = Истина;
		Элементы.ГруппаИННКППОтправляет.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КодИФНСПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ФамилияОтправляетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИмяОтправляетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоОтправляетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОтправляетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИННОтправляетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИННОтправляетЮЛПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КППОтправляетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ФамилияПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИмяПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИННПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НомерДоверенностиПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДатаДоверенностиПодписантПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьОтчетИОткрытьФормуПрисоединенныеФайлы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Функция ОбъектОтчета(ЭтаФормаИмя) Экспорт
	
	Если мОбъектОтчета = Неопределено Тогда 
		мОбъектОтчета = РегламентированнаяОтчетностьВызовСервера.ОбъектОтчета(ЭтаФормаИмя);
	КонецЕсли;
	
	Возврат мОбъектОтчета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяОтчета(ЭтаФормаИмя)
	Форма_Имя = СтрЗаменить(ЭтаФормаИмя, "Внешний", "");
	Возврат Сред(Лев(Форма_Имя, СтрНайти(Форма_Имя, ".Форма.") - 1), 7);
КонецФункции