
#Область ОбработчикиСобытийФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭУ(Форма)
		
	Если Форма.ОтправительЯвляетсяЮЛ Тогда
		Форма.Элементы.ГруппаОтпрЮФ.ТекущаяСтраница = Форма.Элементы.ГруппаОтпрЮФ.ПодчиненныеЭлементы.ГруппаЮЛ;
	Иначе
		Форма.Элементы.ГруппаОтпрЮФ.ТекущаяСтраница = Форма.Элементы.ГруппаОтпрЮФ.ПодчиненныеЭлементы.ГруппаФЛ;
	КонецЕсли;
	
	Если Форма.ПрПодп1 = 3 Тогда
		Форма.Элементы.НаимДовПодп1.Доступность  = Истина;
		Форма.Элементы.НомерДовПодп1.Доступность = Истина;
		Форма.Элементы.ДатаДовПодп1.Доступность  = Истина;
	Иначе
		Форма.Элементы.НаимДовПодп1.Доступность  = Ложь;
		Форма.Элементы.НомерДовПодп1.Доступность = Ложь;
		Форма.Элементы.ДатаДовПодп1.Доступность  = Ложь;
	КонецЕсли;
	
	Если Форма.ПрКурьер = 3 Тогда
		Если НЕ Форма.ОтправленПочтой Тогда
			Форма.Элементы.НаимДовКурьер.Доступность  = Истина;
			Форма.Элементы.НомерДовКурьер.Доступность = Истина;
			Форма.Элементы.ДатаДовКурьер.Доступность  = Истина;
		КонецЕсли;
	Иначе
		Форма.Элементы.НаимДовКурьер.Доступность  = Ложь;
		Форма.Элементы.НомерДовКурьер.Доступность = Ложь;
		Форма.Элементы.ДатаДовКурьер.Доступность  = Ложь;
	КонецЕсли;
	
	Если Форма.ОтправленПочтой Тогда
		Форма.Элементы.ПрКурьер.Доступность 	  = Ложь;
		Форма.Элементы.ФамилияКурьер.Доступность  = Ложь;
		Форма.Элементы.ИмяКурьер.Доступность 	  = Ложь;
		Форма.Элементы.ОтчествоКурьер.Доступность = Ложь;
		Форма.Элементы.НаимДовКурьер.Доступность  = Ложь;
		Форма.Элементы.НомерДовКурьер.Доступность = Ложь;
		Форма.Элементы.ДатаДовКурьер.Доступность  = Ложь;
		Форма.Элементы.ИННФЛКурьер.Доступность 	  = Ложь;
	Иначе
		Если Форма.ПрКурьер = 3 Тогда
			Форма.Элементы.НаимДовКурьер.Доступность  = Истина;
			Форма.Элементы.НомерДовКурьер.Доступность = Истина;
			Форма.Элементы.ДатаДовКурьер.Доступность  = Истина;
		КонецЕсли;
		Форма.Элементы.ПрКурьер.Доступность 	  = Истина;
		Форма.Элементы.ФамилияКурьер.Доступность  = Истина;
		Форма.Элементы.ИмяКурьер.Доступность 	  = Истина;
		Форма.Элементы.ОтчествоКурьер.Доступность = Истина;
		Форма.Элементы.ИННФЛКурьер.Доступность 	  = Истина;
	КонецЕсли;
	
	флОтпрПредст = (Форма.ПрОтпр = 2);
	Форма.Элементы.ОтправительЯвляетсяЮЛ.Доступность = флОтпрПредст;	
	Форма.Элементы.ГруппаОтпрЮФ.Доступность = флОтпрПредст;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПрОтпрПриИзменении(Элемент)
	
	Если ПрОтпр = 1 Тогда
		
		//заполним данные отправителя данными налогоплательщика
		ОтправительЯвляетсяЮЛ = НЕ СтруктураРеквизитов.ЭтоПБОЮЛ;
		НаимОтпр              = СтруктураРеквизитов.НалогоплательщикНаимЮЛ;
		ИННЮЛОтпр 			  = СтруктураРеквизитов.НалогоплательщикИННЮЛ;	
		КППОтпр 			  = СтруктураРеквизитов.НалогоплательщикКПП;	
		
		ИННФЛОтпр 			  = СтруктураРеквизитов.НалогоплательщикИННФЛ;	
		
		ФИОФЛОтпр			  = РазложитьФИО();
		
		ФамилияФЛОтпр  = ФИОФЛОтпр.Фамилия;	
		ИмяФЛОтпр      = ФИОФЛОтпр.Имя;	
		ОтчествоФЛОтпр = ФИОФЛОтпр.Отчество;	
		
	КонецЕсли;
	
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция РазложитьФИО()
	
	Возврат РегламентированнаяОтчетность.РазложитьФИО(СтруктураРеквизитов.НалогоплательщикФИО);
	
КонецФункции

&НаКлиенте
Процедура ОтправительЯвляетсяЮЛПриИзменении(Элемент)
	
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрПодп1ПриИзменении(Элемент)
	
	Если ПрПодп1 <> 3 Тогда
		НаимДовПодп1  = "";
		НомерДовПодп1 = "";
		ДатаДовПодп1  = '00010101'
	КонецЕсли;
	
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправленПочтойПриИзменении(Элемент)
	
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрКурьерПриИзменении(Элемент)
	
	Если ПрКурьер <> 3 Тогда
		НаимДовКурьер  = "";
		НомерДовКурьер = "";
		ДатаДовКурьер  = '00010101'
	КонецЕсли;
	
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ ФормаЗаполнена() Тогда
		Возврат;
	КонецЕсли; 
	
	Модифицированность = Ложь;
	
	ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, ЭтаФорма);
	
	Закрыть(СтруктураРеквизитов); 	
	
КонецПроцедуры

&НаКлиенте
Функция ФормаЗаполнена()
	
	Статус = Истина;
	
	Если НЕ ЗначениеЗаполнено(КодИФНС) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнен код налогового органа!'");
		Сообщение.Сообщить();
		
		Статус = Ложь;
		
	КонецЕсли;
	
	Если (ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННЮЛОтпр)) ИЛИ (НЕ ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННФЛОтпр)) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнен ИНН отправителя!'");
		Сообщение.Сообщить();
		
		Статус = Ложь;
		
	КонецЕсли; 
	
	Если ПрПодп1 = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(НомерДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовПодп1) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнены сведения о доверенности подписанта 1!'");
			Сообщение.Сообщить();
			
			Статус = Ложь;
			
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОтправленПочтой И ПрКурьер = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовКурьер) ИЛИ НЕ ЗначениеЗаполнено(НомерДовКурьер) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовКурьер) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнены сведения о доверенности курьера!'");
			Сообщение.Сообщить();
			
			Статус = Ложь;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции 
   
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтруктураРеквизитов <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураРеквизитов);
		
	КонецЕсли; 
	
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
