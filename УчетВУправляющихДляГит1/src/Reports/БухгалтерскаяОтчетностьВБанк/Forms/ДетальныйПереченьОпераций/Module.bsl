#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированныйОтчет = Параметры.РегламентированныйОтчет;
	ИмяМакетаОтчета = Параметры.ИмяМакетаОтчета;
	ПрефиксИдентификатораДанных = Параметры.ПрефиксИдентификатораДанных;
	
	Заголовок = Параметры.ЗаголовокОтчета;
	
	НомерСекции = 1;
	СформироватьСтраницуПеречня();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеДокументаОтчетаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если СтрНайти(Область.Имя, "Навигация") = 1 Тогда
		ПерейтиНаСегментОтчета(Область.Имя);
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПерейтиНаСегментОтчета(ИмяКомандыПерехода)
	
	ВыборкаСегментов = ВыборкаСегментовОтчета();
	ВсегоСегментов = ВыборкаСегментов.Количество();
	
	АктивныйСегмент = НомерСекции;
	
	Если ИмяКомандыПерехода = "НавигацияПервый"
	   И АктивныйСегмент <> 1 Тогда
		НомерСекции = 1;
		СформироватьСтраницуПеречня();
	КонецЕсли;
	
	Если ИмяКомандыПерехода = "НавигацияПредыдущий"
	   И АктивныйСегмент > 1 Тогда
		НомерСекции = НомерСекции - 1;
		СформироватьСтраницуПеречня();
	КонецЕсли;
	
	Если ИмяКомандыПерехода = "НавигацияСледующий"
	   И АктивныйСегмент < ВсегоСегментов Тогда
		НомерСекции = НомерСекции + 1;
		СформироватьСтраницуПеречня();
	КонецЕсли;
	
	Если ИмяКомандыПерехода = "НавигацияПоследний"
	   И АктивныйСегмент <> ВсегоСегментов Тогда
		НомерСекции = ВсегоСегментов;
		СформироватьСтраницуПеречня();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтраницуПеречня()
	
	МакетОтчета = Отчеты.БухгалтерскаяОтчетностьВБанк.ПолучитьМакет(ИмяМакетаОтчета);
	
	ДокументОтчета = Новый ТабличныйДокумент;
	
	Секция_Шапка = МакетОтчета.ПолучитьОбласть("Шапка");
	ДокументОтчета.Вывести(Секция_Шапка);
	
	ВыборкаСегментов = ВыборкаСегментовОтчета();
	
	Если ВыборкаСегментов.Количество() > 1 Тогда
		Область_Навигация = МакетОтчета.ПолучитьОбласть("Навигация");
		НомерСтраницы = "Стр. " + СтрокаЧГ0(НомерСекции) + " из " + СтрокаЧГ0(ВыборкаСегментов.Количество());
		Область_Навигация.Параметры.НомерСтраницы = НомерСтраницы;
		ДокументОтчета.Вывести(Область_Навигация);
	КонецЕсли;
	
	Секция_ШапкаТаблицы = МакетОтчета.ПолучитьОбласть("ШапкаТаблицы");
	ДокументОтчета.Вывести(Секция_ШапкаТаблицы);
	
	ВысотаШапки = ДокументОтчета.ВысотаТаблицы;
	
	ВидДополнительногоФайла = ПрефиксИдентификатораДанных + "." + СтрокаЧГ0(НомерСекции);
	СтрокиСекции = ДанныеРегистраДополнительныхФайлов(ВидДополнительногоФайла);
	
	Если СтрокиСекции <> Неопределено Тогда
		Для Каждого СтрокаСекции Из СтрокиСекции Цикл
			Область_СтрокаТаблицы = МакетОтчета.ПолучитьОбласть("СтрокаТаблицы");
			Область_СтрокаТаблицы.Параметры.Заполнить(СтрокаСекции);
			ДокументОтчета.Вывести(Область_СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	ПолеДокументаОтчета.Очистить();
	ПолеДокументаОтчета.Вывести(ДокументОтчета);
	ПолеДокументаОтчета.ФиксацияСверху = ВысотаШапки;
	
КонецПроцедуры

&НаСервере
Функция ВыборкаСегментовОтчета()
	
	ШаблонОтбора = ПрефиксИдентификатораДанных + ".%";
	
	Возврат ВыборкаСегментовОтчетаПоШаблону(ШаблонОтбора);
	
КонецФункции

&НаСервере
Функция ВыборкаСегментовОтчетаПоШаблону(ШаблонОтбора)
	
	ЗапросПоДанным = Новый Запрос;
	ЗапросПоДанным.Текст = "ВЫБРАТЬ
	                       |	ДополнительныеФайлыРегламентированныхОтчетов.ВидДополнительногоФайла КАК ВидДополнительногоФайла
	                       |ИЗ
	                       |	РегистрСведений.ДополнительныеФайлыРегламентированныхОтчетов КАК ДополнительныеФайлыРегламентированныхОтчетов
	                       |ГДЕ
	                       |	ДополнительныеФайлыРегламентированныхОтчетов.РегламентированныйОтчет = &РегламентированныйОтчет
	                       |	И ДополнительныеФайлыРегламентированныхОтчетов.ВидДополнительногоФайла ПОДОБНО &ШаблонОтбора
	                       |
	                       |УПОРЯДОЧИТЬ ПО
	                       |	ВидДополнительногоФайла";
	
	ЗапросПоДанным.УстановитьПараметр("ШаблонОтбора", ШаблонОтбора);
	ЗапросПоДанным.УстановитьПараметр("РегламентированныйОтчет", РегламентированныйОтчет);
	
	ВыборкаСегментов = ЗапросПоДанным.Выполнить().Выбрать();
	
	Возврат ВыборкаСегментов;
	
КонецФункции

&НаСервере
Функция ДанныеРегистраДополнительныхФайлов(ВидДополнительногоФайла)
	
	ЗаписьРегистраСведений = РегистрыСведений.ДополнительныеФайлыРегламентированныхОтчетов.СоздатьМенеджерЗаписи();
	
	ЗаписьРегистраСведений.РегламентированныйОтчет = РегламентированныйОтчет;
	ЗаписьРегистраСведений.ВидДополнительногоФайла = ВидДополнительногоФайла;
	
	ЗаписьРегистраСведений.Прочитать();
	
	Если ЗаписьРегистраСведений.Выбран() Тогда
		Данные = ЗаписьРегистраСведений.СодержимоеФайла.Получить();
	Иначе
		Данные = Неопределено;
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаЧГ0(ФорматируемоеЧисло)
	
	Возврат Формат(ФорматируемоеЧисло, "ЧН=; ЧГ=0");
	
КонецФункции

#КонецОбласти
