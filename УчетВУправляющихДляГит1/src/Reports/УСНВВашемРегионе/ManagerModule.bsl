#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Формирует отчет "УСН в вашем регионе", помещает его в составе структуры во временное хранилище.
//
// Параметры:
//  ПараметрыОтчета - Структура - параметры для запроса информации в сервисе и формирования отчета.
//  	* КодРегиона - Строка - код субъекта РФ
//		* РегионПредставление - Строка - наименование субъекта РФ
//  	* КодОКВЭД - Строка - код вида деятельности по классификатору ОКВЭД (ред.2)
//		* ВидДеятельностиПредставление - Строка - наименование вида деятельности
//  	* ЭтоЮрЛицо - Булево - получать информацию для организации-юр.лица или для предпринимателя
//  	* ПрименяетсяУСНДоходы - Булево - получать информацию для системы налогообложения УСН (доходы)
//  	* ПрименяетсяУСНДоходыРасходы - Булево - получать информацию для системы налогообложения УСН (доходы - расходы)
//		* ПоказыватьПрименить - Булево - показывать ли в сформированном ответе гиперссылки "Применить".
//										Например, не нужно показывать гиперссылки, если нет прав на изменение ставок.
//		* Организация - СправочникСсылка.Организации, Неопределено - организация, которая будет передана в расшифровку отчета
//  АдресХранилища - Строка	- адрес временного хранилища
//
Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	Результат = ДанныеОтчета(ПараметрыОтчета);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеОтчета(Параметры)

	Результат = Новый Структура;
	Результат.Вставить("ДокументРезультат");
	Результат.Вставить("ОписаниеОшибки", "");
	
	ОтветСервиса = РегиональныеСтавкиНалогов.ИнформацияУСНВВашемРегионе(Параметры);
	
	Если ЗначениеЗаполнено(ОтветСервиса.ОписаниеОшибки) Тогда
		Результат.ОписаниеОшибки = ОтветСервиса.ОписаниеОшибки;
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(ОтветСервиса.Ответ) <> Тип("Структура")
		ИЛИ НЕ ОтветСервиса.Ответ.Свойство("ОписаниеОшибки")
		ИЛИ НЕ ОтветСервиса.Ответ.Свойство("Информация") Тогда
		Результат.ОписаниеОшибки = НСтр("ru='Ошибка при получении данных сервиса.'");
		Возврат Результат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтветСервиса.Ответ.ОписаниеОшибки) Тогда
		Результат.ОписаниеОшибки = ОтветСервиса.Ответ.ОписаниеОшибки;
		Возврат Результат;
	КонецЕсли;
	
	Информация = ОтветСервиса.Ответ.Информация;
	
	ДокументРезультат = Новый ТабличныйДокумент;
	Области = ВсеОбластиМакета();
	
	ЗаполнитьЗначенияСвойств(Области.ЗаголовокОбщий.Параметры, Параметры);
	Области.ЗаголовокОбщий.Параметры.НаименованиеНалога = Информация.НаименованиеНалога;
	ДокументРезультат.Вывести(Области.ЗаголовокОбщий);
	ДокументРезультат.Вывести(Области.ОкончаниеРаздела);
	
	ПоказыватьВсеОбъектыНалогообложения = Информация.ОбъектыНалогообложения.Количество() > 1;
	
	Для каждого ОбъектНалогообложения Из Информация.ОбъектыНалогообложения Цикл
		
		ВывестиИнформациюОсновнаяСтавка(
			ОбъектНалогообложения, ДокументРезультат, Области, Параметры, ПоказыватьВсеОбъектыНалогообложения);
		
		ВывестиИнформациюЛьготныеСтавки(
			ОбъектНалогообложения, ДокументРезультат, Области, Параметры);
		
	КонецЦикла;
	
	ВывестиИнформациюНалоговыеКаникулы(
		Информация.НалоговыеКаникулы, ДокументРезультат, Области, Параметры, ПоказыватьВсеОбъектыНалогообложения);
	
	Результат.ДокументРезультат = ДокументРезультат;
	Возврат Результат;
	
КонецФункции

Процедура ВывестиИнформациюОсновнаяСтавка(ОбъектНалогообложения, ДокументРезультат, Области, ПараметрыОтчета, ПоказыватьВсеОбъектыНалогообложения)
	
	Если ПоказыватьВсеОбъектыНалогообложения Тогда
		Области.ЗаголовокОбъекта.Параметры.НаименованиеНалога = ОбъектНалогообложения.НаименованиеНалога;
		ДокументРезультат.Вывести(Области.ЗаголовокОбъекта);
		ДокументРезультат.Вывести(Области.Разделитель);
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru='Основная ставка'");
	ОсновнаяСтавка = ОбъектНалогообложения.ОсновнаяСтавка;
	Области.ЗаголовокРаздела.Параметры.ЗаголовокРаздела = ВРег(ТекстЗаголовка);
	ДокументРезультат.Вывести(Области.ЗаголовокРаздела);
	ДокументРезультат.Вывести(Области.Разделитель);
	
	Области.ОсновнаяСтавка.Параметры.СтавкаПредставление = СтрШаблон("%1%%", ОсновнаяСтавка.СтавкаНалога);
	Области.ОсновнаяСтавка.Параметры.Описание = ОсновнаяСтавка.Описание;
	ДокументРезультат.Вывести(Области.ОсновнаяСтавка);
	
	Если ПараметрыОтчета.ПоказыватьПрименить 
		И ОсновнаяСтавка.Применяется Тогда
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "Организация");
		Области.ПрименитьБезУсловий.Параметры.РасшифровкаПрименить = Новый Структура(
			"Действие,Организация,УСНДоходы,СтавкаНалога", 
			"ПрименитьСтавку",
			Организация, 
			ОбъектНалогообложения.УСНДоходы, 
			ОсновнаяСтавка.СтавкаНалога);
		ДокументРезультат.Вывести(Области.ПрименитьБезУсловий);
	КонецЕсли;
	ДокументРезультат.Вывести(Области.ОкончаниеРаздела);
	
КонецПроцедуры

Процедура ВывестиИнформациюЛьготныеСтавки(ОбъектНалогообложения, ДокументРезультат, Области, ПараметрыОтчета)

	ТекстЗаголовка = НСтр("ru='Льготные ставки'");
	ЛьготныеСтавки = ОбъектНалогообложения.ЛьготныеСтавки;
	Области.ЗаголовокРаздела.Параметры.ЗаголовокРаздела = ВРег(ТекстЗаголовка);
	ДокументРезультат.Вывести(Области.ЗаголовокРаздела);
	ДокументРезультат.Вывести(Области.Разделитель);
	
	Если ЛьготныеСтавки.Применяются Тогда
		
		Для каждого ИнформацияПоСтавке Из ЛьготныеСтавки.СтавкиНалога Цикл
			ВывестиИнформациюЛьготнаяСтавка(
				ИнформацияПоСтавке, ОбъектНалогообложения, ДокументРезультат, Области, ПараметрыОтчета);
		КонецЦикла;
		
	Иначе
		
		Области.НетЛьготнойСтавки.Параметры.Описание = ЛьготныеСтавки.Описание;
		ДокументРезультат.Вывести(Области.НетЛьготнойСтавки);
		ДокументРезультат.Вывести(Области.ОкончаниеРаздела);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиИнформациюЛьготнаяСтавка(ИнформацияПоСтавке, ОбъектНалогообложения, ДокументРезультат, Области, ПараметрыОтчета)
	
	ЕстьУсловия = ИнформацияПоСтавке.Условия.Количество() > 0;
	ПоказанНормативныйАкт = Ложь;
	ЕстьСсылкаНаНормативныйАкт = ЗначениеЗаполнено(ИнформацияПоСтавке.СсылкаНаНормативныйАкт);
	
	ТекущаяОбласть = ?(ЕстьУсловия, Области.ЛьготнаяСтавка, Области.ЛьготнаяСтавкаБезУсловий);
	ТекущаяОбласть.Параметры.СтавкаПредставление = СтрШаблон("%1%%", ИнформацияПоСтавке.СтавкаНалога);
	ТекущаяОбласть.Параметры.Описание = ИнформацияПоСтавке.Описание;
	ДокументРезультат.Вывести(ТекущаяОбласть);
	
	Если ПараметрыОтчета.ПоказыватьПрименить Тогда
		Если ЕстьУсловия Тогда
			ТекущаяОбласть = Области.ПрименитьСУсловием;
		ИначеЕсли ЕстьСсылкаНаНормативныйАкт Тогда
			ТекущаяОбласть = Области.ПрименитьСоСсылкойНаНормативныйАкт;
		Иначе
			ТекущаяОбласть = Области.ПрименитьСНормативнымАктом;
		КонецЕсли;
		ТекущаяОбласть.Параметры.РасшифровкаПрименить = Новый Структура(
			"Действие,Организация,УСНДоходы,СтавкаНалога", 
			"ПрименитьСтавку",
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "Организация"), 
			ОбъектНалогообложения.УСНДоходы, 
			ИнформацияПоСтавке.СтавкаНалога);
	Иначе
		Если ЕстьУсловия Тогда
			ТекущаяОбласть = Области.Условие;
		ИначеЕсли ЕстьСсылкаНаНормативныйАкт Тогда
			ТекущаяОбласть = Области.СсылкаНаНормативныйАкт;
		Иначе
			ТекущаяОбласть = Области.НормативныйАкт;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьУсловия Тогда
		ТекущаяОбласть.Параметры.Условие = ИнформацияПоСтавке.Условия[0];
	Иначе
		Если ЕстьСсылкаНаНормативныйАкт Тогда
			ТекущаяОбласть.Параметры.РасшифровкаНормативныйАкт = Новый Структура(
				"Действие,Ссылка",
				"ПерейтиПоСсылке",
				ИнформацияПоСтавке.СсылкаНаНормативныйАкт);
		КонецЕсли;
		ТекущаяОбласть.Параметры.НормативныйАкт = ИнформацияПоСтавке.НормативныйАкт;
		ПоказанНормативныйАкт = Истина;
	КонецЕсли;
	ДокументРезультат.Вывести(ТекущаяОбласть);
	
	Для Номер = 1 По ИнформацияПоСтавке.Условия.ВГраница() Цикл
		Области.Условие.Параметры.Условие = ИнформацияПоСтавке.Условия[Номер];
		ДокументРезультат.Вывести(Области.Условие);
	КонецЦикла;
	
	Если НЕ ПоказанНормативныйАкт Тогда
		Если ЕстьСсылкаНаНормативныйАкт Тогда
			ТекущаяОбласть = Области.СсылкаНаНормативныйАкт;
			ТекущаяОбласть.Параметры.РасшифровкаНормативныйАкт = Новый Структура(
				"Действие,Ссылка",
				"ПерейтиПоСсылке",
				ИнформацияПоСтавке.СсылкаНаНормативныйАкт);
		Иначе
			ТекущаяОбласть = Области.НормативныйАкт;
		КонецЕсли;
		ТекущаяОбласть.Параметры.НормативныйАкт = ИнформацияПоСтавке.НормативныйАкт;
		ДокументРезультат.Вывести(ТекущаяОбласть);
	КонецЕсли;
	
	ДокументРезультат.Вывести(Области.ОкончаниеРаздела);
	
КонецПроцедуры

Процедура ВывестиИнформациюНалоговыеКаникулы(ИнформацияКаникулы, ДокументРезультат, Области, ПараметрыОтчета, ПоказыватьВсеОбъектыНалогообложения)
	
	ЭтоЮрЛицо = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ЭтоЮрЛицо", Ложь);
	Если ЭтоЮрЛицо Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru='Налоговые каникулы'");
	Если ПоказыватьВсеОбъектыНалогообложения Тогда
		Области.ЗаголовокОбъекта.Параметры.НаименованиеНалога = ТекстЗаголовка;
		ДокументРезультат.Вывести(Области.ЗаголовокОбъекта);
	Иначе
		Области.ЗаголовокРаздела.Параметры.ЗаголовокРаздела = ВРег(ТекстЗаголовка);
		ДокументРезультат.Вывести(Области.ЗаголовокРаздела);
	КонецЕсли; 
	ДокументРезультат.Вывести(Области.Разделитель);
	
	Если ИнформацияКаникулы.Применяются Тогда
		
		Области.ЛьготнаяСтавка.Параметры.СтавкаПредставление = "0%";
		Области.ЛьготнаяСтавка.Параметры.Описание = ИнформацияКаникулы.Описание;
		ДокументРезультат.Вывести(Области.ЛьготнаяСтавка);
		
		ЕстьУсловия = ИнформацияКаникулы.Условия.Количество() > 0;
		ПоказанНормативныйАкт = Ложь;
		ЕстьСсылкаНаНормативныйАкт = ЗначениеЗаполнено(ИнформацияКаникулы.СсылкаНаНормативныйАкт);
		
		Если ПараметрыОтчета.ПоказыватьПрименить Тогда
			Если ЕстьУсловия Тогда
				ТекущаяОбласть = Области.ПрименитьСУсловием;
			ИначеЕсли ЕстьСсылкаНаНормативныйАкт Тогда
				ТекущаяОбласть = Области.ПрименитьСоСсылкойНаНормативныйАкт;
			Иначе
				ТекущаяОбласть = Области.ПрименитьСНормативнымАктом;
			КонецЕсли;
			ТекущаяОбласть.Параметры.РасшифровкаПрименить = Новый Структура(
				"Действие,Организация,НалоговыеКаникулы",
				"ПрименитьСтавку",
				ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "Организация"), 
				Истина);
		Иначе
			Если ЕстьУсловия Тогда
				ТекущаяОбласть = Области.Условие;
			ИначеЕсли ЕстьСсылкаНаНормативныйАкт Тогда
				ТекущаяОбласть = Области.СсылкаНаНормативныйАкт;
			Иначе
				ТекущаяОбласть = Области.НормативныйАкт;
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьУсловия Тогда
			ТекущаяОбласть.Параметры.Условие = ИнформацияКаникулы.Условия[0];
		Иначе
			Если ЕстьСсылкаНаНормативныйАкт Тогда
				ТекущаяОбласть.Параметры.РасшифровкаНормативныйАкт = Новый Структура(
					"Действие,Ссылка",
					"ПерейтиПоСсылке",
					ИнформацияКаникулы.СсылкаНаНормативныйАкт);
			КонецЕсли;
			ТекущаяОбласть.Параметры.НормативныйАкт = ИнформацияКаникулы.НормативныйАкт;
			ПоказанНормативныйАкт = Истина;
		КонецЕсли;
		ДокументРезультат.Вывести(ТекущаяОбласть);
		
		Для Номер = 1 По ИнформацияКаникулы.Условия.ВГраница() Цикл
			Области.Условие.Параметры.Условие = ИнформацияКаникулы.Условия[Номер];
			ДокументРезультат.Вывести(Области.Условие);
		КонецЦикла;
		
		Если НЕ ПоказанНормативныйАкт Тогда
			Если ЕстьСсылкаНаНормативныйАкт Тогда
				ТекущаяОбласть = Области.СсылкаНаНормативныйАкт;
				ТекущаяОбласть.Параметры.РасшифровкаНормативныйАкт = Новый Структура(
					"Действие,Ссылка",
					"ПерейтиПоСсылке",
					ИнформацияКаникулы.СсылкаНаНормативныйАкт);
			Иначе
				ТекущаяОбласть = Области.НормативныйАкт;
			КонецЕсли;
			ТекущаяОбласть.Параметры.НормативныйАкт = ИнформацияКаникулы.НормативныйАкт;
			ДокументРезультат.Вывести(ТекущаяОбласть);
		КонецЕсли;
		
	Иначе
		
		Области.НетЛьготнойСтавки.Параметры.Описание = ИнформацияКаникулы.Описание;
		ДокументРезультат.Вывести(Области.НетЛьготнойСтавки);
		
	КонецЕсли;
	
КонецПроцедуры
	
Функция ВсеОбластиМакета()
	
	Результат = Новый Структура;
	
	Макет = ПолучитьМакет("УСНВВашемРегионе");
	Для каждого ОбластьМакета Из Макет.Области Цикл
		Результат.Вставить(ОбластьМакета.Имя, Макет.ПолучитьОбласть(ОбластьМакета.Имя));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли