// Область "Настройки" описывает параметры инициализации механизма загрузки
// регламентированных отчетов из файлов выгрузки. Формат области настроек - JSON.
//
// Описание параметров:
//  * Установкой параметра "РазрешитьЗагрузку" в "true" или "false" регулируем видимость
//    кнопки "Загрузить" и возможность загрузки из файла в форме отчета.
//  * Параметр "ФорматыСхемВыгрузки" описывает соответствие имен макетов схем выгрузки
//    поддерживаемым форматам загрузки. Если у формы единственная схема выгрузки или
//    нужно всегда использовать схему по умолчанию - указываем значение "null".
//  * Параметр "ОбластиОбработчиков" содержит список имен используемых обработчиков
//    загружаемых данных. Имена соответствуют областям макета, в которых можно размещать
//    произвольные алгоритмы на языке "1С:Предприятие" для обработки доступных объектов.
//  * Параметр "ИмяФайлаНастроек" содержит путь к файлу настроек для быстрого описания
//    правил загрузки без сохранения конфигурации. После отладки правил загрузки нужно
//    обязательно перенести содержимое файла в макет и удалить параметр или присвоить ему
//    значение "null". В целях безопасности нельзя выпускать конфигурации с заполненным
//    параметром "ИмяФайлаНастроек".
//
// Примечание:
//  Сопоставление имен узлов из файла XML и имен показателей отчета выполняется по данным схемы выгрузки,
//  поэтому обработчики используются для получения значений показателей не описанных в схеме выгрузки.
//
#Область Настройки
{
	"РазрешитьЗагрузку"   : true,
	
	"ФорматыСхемВыгрузки" : null,
	"ОбластиОбработчиков" : {
		"ПередЗагрузкой"  : "ПредОбработкаСхемыЗагрузки",
		"ПослеЗагрузки"   : "ПостОбработкаДокументаОтчета",
	}
}
#КонецОбласти

// Доступны параметры:
//
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПредОбработкаСхемыЗагрузки
	
	НайденныеУзлы = УзлыПоXPath(П.ДеревоДляЗагрузки, "//СумАкцПУ/НалБазаВид/РасчНалБазаВид");
	Для Каждого НайденныйУзел Из НайденныеУзлы Цикл
		НайденныйУзел.Обязательность = НайденныйУзел.Обязательность + "П"; // пропустим узел при автоматической загрузке
		НайденныйУзел.Многостраничность = Ложь;
	КонецЦикла;
	
#КонецОбласти

// Доступны параметры:
//
//  П.ДанныеОтчета      - заполненные данные документа регламентированного отчета;
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПостОбработкаДокументаОтчета
	
	Раздел1 = Неопределено;
	
	П.ДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел1", Раздел1);
	
	УзелРаздел1 = УзлыПоXPath(П.ДеревоДляЗагрузки, "//СумНалПУ")[0];
	
	Если Раздел1 <> Неопределено И УзелРаздел1 <> Неопределено Тогда
		
		УзлыРаздел1Атрибуты = УзлыПоXPath(УзелРаздел1, "@*");
		Для Каждого УзелАтрибут Из УзлыРаздел1Атрибуты Цикл
			Если ЗначениеЗаполнено(УзелАтрибут.Ключ) Тогда
				Раздел1.Вставить(УзелАтрибут.Ключ, ЗначениеПоказателяИзXML(УзелАтрибут));
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	УзлыНалБазаВид = УзлыПоXPath(П.ДеревоДляЗагрузки, "//СумАкцПУ/НалБазаВид"); // приложение 1 к разделу 2
	
	Если ЗначениеЗаполнено(УзлыНалБазаВид) Тогда
		НомСтраницы = 1;
		Для НомерСтраницы = 1 По УзлыНалБазаВид.Количество() Цикл
			УзелНалБазаВид = УзлыНалБазаВид[НомерСтраницы - 1];
			
			УзелВидПАТов = УзлыПоXPath(УзелНалБазаВид, "@ВидПАТов")[0];
			УзелОКЕИ     = УзлыПоXPath(УзелНалБазаВид, "@ОКЕИ")[0];
			УзелНалБаза  = УзлыПоXPath(УзелНалБазаВид, "@НалБаза")[0];
			
			УзелВидПАТов.Раздел = "Раздел2_Прил1";
			УзелВидПАТов.Ключ = "П000210001001";
			
			УзелОКЕИ.Раздел = "Раздел2_Прил1";
			УзелОКЕИ.Ключ = "П000210002001";
			
			УзелНалБаза.Раздел = "Раздел2_Прил1";
			УзелНалБаза.Ключ = "П000210005003";
			
			УзелКБК = УзлыПоXPath(УзелНалБазаВид, "../@КБК")[0];
			НовУзелКБК = УзелНалБазаВид.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НовУзелКБК, УзелКБК, , "Родитель,Строки");
			НовУзелКБК.Раздел = "Раздел2_Прил1";
			НовУзелКБК.Ключ = "П000210003001";
			
			СуммаИтого = 0;
			УзлыРасчНалБазаВид = УзлыПоXPath(УзелНалБазаВид, "РасчНалБазаВид");
			Для Каждого УзелРасчНалБазаВид Из УзлыРасчНалБазаВид Цикл
				УзелРасчНалБазаВид.Обязательность = "НМ";
				СуммаИтого = СуммаИтого + ЧислоИзСтроки(УзлыПоXPath(УзелРасчНалБазаВид, "@НалБазаПрсч")[0].Значение);
			КонецЦикла;
			УзелНалБаза.Значение = Формат(СуммаИтого,"ЧДЦ=3; ЧРД=.; ЧН=; ЧГ=");
			
			Если СуммаИтого = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьДаннымиСохраненныйДокументИзДереваДляЗагрузки(УзелНалБазаВид, П.ДанныеОтчета, П.ПараметрыОтчета, , НомСтраницы);
			НомСтраницы = НомСтраницы + 1;
		КонецЦикла;
	КонецЕсли;
	
#КонецОбласти