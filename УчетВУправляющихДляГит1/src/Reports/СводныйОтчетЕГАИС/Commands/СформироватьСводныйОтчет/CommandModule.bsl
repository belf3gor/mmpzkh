
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СписокОтчетов = УбратьИзМассиваНеполученныеОтчеты(ПараметрКоманды);
	Если СписокОтчетов.Количество() = 0 Тогда
		Если ПараметрКоманды.Количество() = 1 Тогда
			ВызватьИсключение НСтр("ru='Команда не может быть выполнена для указанного объекта!'");
		Иначе
			ВызватьИсключение НСтр("ru='Команда не может быть выполнена для указанных объектов!'");
		КонецЕсли;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ОтчетЕГАИС", СписокОтчетов);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.СводныйОтчетЕГАИС.Форма",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция УбратьИзМассиваНеполученныеОтчеты(СписокОтчетов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОтчетов", СписокОтчетов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО ОтчетЕГАИС.Ссылка = СтатусыДокументовЕГАИС.Документ
	|ГДЕ
	|	ОтчетЕГАИС.Ссылка В(&СписокОтчетов)
	|	И СтатусыДокументовЕГАИС.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиОтчетаЕГАИС.ПолученОтчет)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти