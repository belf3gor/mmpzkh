#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ДанныеОФизЛицеОрганизации, ВыборкаНалогиНачисленныеИтоги, ВыборкаАвансыЗачтенныеИтоги, ВыборкаДоходовПоМесяцамПоРазделам, ВыборкаНалогиУдержанныеИтоги, ВыборкаНалогиВзысканиеИтоги;
Перем ВыборкаНалогиВозвращенныеИтоги, ВыборкаВычетыИтоги;
Перем ВыборкаВычетыПоМесяцамПоВидам, ВыборкаНалогиПеречисленныеИтоги, Ответственный, ДатаПодписи;
Перем ВыборкаСтандартныеВычеты, ВыборкаСоциальныеВычеты, ВыборкаСправкиНДФЛ, ВыборкаУведомления, ВыборкаСтатусов, ВыборкаНалогНачисленныйПоСтавке13, ВыборкаВычетовПоСтавке13;
Перем ТаблицаРегистрацийВНО;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	
	ДокументРезультат.НачатьАвтогруппировкуСтрок();
	
	ДокументРезультат.Очистить();
	
	НастройкиОтчета = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();
	
	НастройкиОтчета.Выбор.Элементы.Очистить();
	
	ПоследняяГруппировка = ЗарплатаКадрыОтчеты.ПолучитьПоследнийЭлементСтруктуры(НастройкиОтчета);

	Группировка = ПоследняяГруппировка.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	
	ОтчетыСервер.ДобавитьВыбранноеПоле(Группировка.Выбор, "ГоловнаяОрганизация");
	ОтчетыСервер.ДобавитьВыбранноеПоле(Группировка.Выбор, "Организация");
	ОтчетыСервер.ДобавитьВыбранноеПоле(Группировка.Выбор, "ГОД");
	ОтчетыСервер.ДобавитьВыбранноеПоле(Группировка.Выбор, "ФизическоеЛицо");
	
	ЭлементСортировки = Группировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	ЭлементСортировки.Использование = Истина;
	
	ЭлементСортировки = НастройкиОтчета.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементСортировки.Использование = Истина;
	ЭлементСортировки.Поле = Новый ПолеКомпоновкиДанных("ФизическоеЛицо");
	
	ОтборПоОрганизации = Ложь;
	Для Каждого ЭлементОтбора Из НастройкиОтчета.Отбор.Элементы Цикл
		ЭлементОрганизация = Новый ПолеКомпоновкиДанных("Организация");
		Если ЭлементОтбора.Использование = Истина И ЭлементОтбора.ЛевоеЗначение = ЭлементОрганизация Тогда
			ОтборПоОрганизации = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	ПараметрКомпоновки = Новый ПараметрКомпоновкиДанных("ОтборПоОрганизации");
	ПараметрОтборПоОрганизации = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКомпоновки); 
	ПараметрОтборПоОрганизации.Значение = ОтборПоОрганизации;
	ПараметрОтборПоОрганизации.Использование = Истина;
			
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	Ответственный = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Ответственный"));
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	Данные = Новый ТаблицаЗначений;
	
	ПроцессорВывода.УстановитьОбъект(Данные);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	МассивТипов = Новый Массив(2);
	МассивТипов[0]= Тип("Null");
	СписокФизЛицОрганизация = Новый ТаблицаЗначений;
	МассивТипов[1]= Тип("СправочникСсылка.ФизическиеЛица");
	СписокФизЛицОрганизация.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов(МассивТипов));
	МассивТипов[1]= Тип("СправочникСсылка.Организации");
	СписокФизЛицОрганизация.Колонки.Добавить("ГоловнаяОрганизация", Новый ОписаниеТипов(МассивТипов));
	СписокФизЛицОрганизация.Колонки.Добавить("Организация", Новый ОписаниеТипов(МассивТипов));
	МассивТипов[1]= Тип("Число");
	СписокФизЛицОрганизация.Колонки.Добавить("Год", Новый ОписаниеТипов(МассивТипов));
	МассивТипов[1]= Тип("Булево");
	СписокФизЛицОрганизация.Колонки.Добавить("НеОтбиратьПоОП", Новый ОписаниеТипов(МассивТипов));

	ПолучитьСпискиФизЛицИСотрудников(Данные, СписокФизЛицОрганизация);	
	
	ПолучитьДанныеДляПостроенияОтчета(СписокФизЛицОрганизация);
	
	// Параметры документа
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;
	
	ВывестиКарточки(ДокументРезультат, Данные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьДанныеДляПостроенияОтчета(СписокФизЛицОрганизация)
	
	Период = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "Период").Значение; 
	
	ДатаНачалаНП = Период.ДатаНачала;
	ДатаКонцаНП  = Период.ДатаОкончания;
	ДатаПодписи  = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "ДатаПодписи").Значение;
	Если ТипЗнч(ДатаПодписи) = Тип("СтандартнаяДатаНачала") Тогда 
		ДатаПодписи = ДатаПодписи.Дата;
	КонецЕсли;	
	
	Если ДатаПодписи = Неопределено Или ДатаПодписи = '00010101' Тогда
		ДатаПодписи = ТекущаяДатаСеанса();
	КонецЕсли;

	ФизическоеЛицоОтветственного = ?(Ответственный = Неопределено, Справочники.ФизическиеЛица.ПустаяСсылка(), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ответственный.Значение, "ФизическоеЛицо"));
	
	// Создание запроса и установка всех необходимых параметров.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	Запрос.УстановитьПараметр("ФизлицоОтветственного", ФизическоеЛицоОтветственного);
	Запрос.УстановитьПараметр("НачалоНП", ДатаНачалаНП);
	Запрос.УстановитьПараметр("КонецНП", ДатаКонцаНП);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаПодписи);
	Запрос.УстановитьПараметр("СписокФизЛицОрганизация", СписокФизЛицОрганизация);
	Запрос.УстановитьПараметр("Резидент", Справочники.СтатусыНалогоплательщиковПоНДФЛ.Резидент);
	Запрос.УстановитьПараметр("Нерезидент", Справочники.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент);
	Запрос.УстановитьПараметр("ДатаИзмененияНалогообложенияДивидендов", УчетНДФЛ.ДатаИзмененияНалогообложенияДивидендов());
	Запрос.УстановитьПараметр("ДатаИзмененияСроковУплатыНалога", УчетНДФЛ.ДатаИзмененияСроковУплатыНалога());
	Запрос.УстановитьПараметр("ДатаЗакона285ФЗ", УчетНДФЛ.ДатаЗакона285ФЗ());
	Запрос.УстановитьПараметр("ГодЗакона285ФЗ", Год(УчетНДФЛ.ДатаЗакона285ФЗ()));
		
	// Сформируем текст запроса с датами месяцев налогового периода.
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(Запрос.МенеджерВременныхТаблиц, ДатаНачалаНП, ДатаКонцаНП,,,, Истина);
	
	// -----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ ОБ ОРГАНИЗАЦИИ И ФИЗЛИЦЕ (Данные разделов 1 и 2).
	// 	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизЛицаОрганизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ФизЛицаОрганизация.Организация,
	|	ФизЛицаОрганизация.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ФизЛицаОрганизация.Год КАК Год,
	|	ФизЛицаОрганизация.НеОтбиратьПоОП
	|ПОМЕСТИТЬ ВТСписокФизЛицОрганизация
	|ИЗ
	|	&СписокФизЛицОрганизация КАК ФизЛицаОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	ГоловнаяОрганизация,
	|	Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТСписокФизЛицОрганизация.ФизическоеЛицо,
	|	&ДатаАктуальности КАК Период
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК ВТСписокФизЛицОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТСписокФизЛицОрганизация.ГоловнаяОрганизация КАК СтруктурнаяЕдиница,
	|	&ДатаАктуальности КАК Период
	|ПОМЕСТИТЬ ВТГоловныеОрганизации
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК ВТСписокФизЛицОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТСписокФизЛицОрганизация.ГоловнаяОрганизация,
	|	ВТСписокФизЛицОрганизация.ФизическоеЛицо,
	|	ВТСписокФизЛицОрганизация.Год,
	|	ВЫБОР
	|		КОГДА Организации.Ссылка ЕСТЬ NULL 
	|			ТОГДА ВТСписокФизЛицОрганизация.Организация
	|		ИНАЧЕ Организации.Ссылка
	|	КОНЕЦ КАК Организация
	|ПОМЕСТИТЬ ВТОбособленныеПодразделения
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК ВТСписокФизЛицОрганизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ВТСписокФизЛицОрганизация.ГоловнаяОрганизация = Организации.ГоловнаяОрганизация
	|			И (ВТСписокФизЛицОрганизация.НеОтбиратьПоОП)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТСписокФизЛицОрганизация.ГоловнаяОрганизация,
	|	ВТСписокФизЛицОрганизация.ФизическоеЛицо,
	|	ВТСписокФизЛицОрганизация.Год,
	|	ВТСписокФизЛицОрганизация.Организация
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК ВТСписокФизЛицОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(Периоды.Период) КАК Период,
	|	СписокФизЛицОрганизация.Год КАК Год,
	|	СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТТаблицаДляСрезаНаКонецГода
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО (ГОД(Периоды.Период) = СписокФизЛицОрганизация.Год)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокФизЛицОрганизация.Год,
	|	СписокФизЛицОрганизация.ФизическоеЛицо";
	
	Запрос.Выполнить();
	
	МассивНеобходимыхДанных = Новый Массив;
	МассивНеобходимыхДанных.Добавить("ФИОПолные");
	МассивНеобходимыхДанных.Добавить("ДокументВид");
	МассивНеобходимыхДанных.Добавить("ДокументНомер");
	МассивНеобходимыхДанных.Добавить("ДокументСерия");
	МассивНеобходимыхДанных.Добавить("АдресПоПрописке");
	МассивНеобходимыхДанных.Добавить("АдресЗаПределамиРФ");
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(
		Запрос.МенеджерВременныхТаблиц,
		"ВТФизическиеЛица");
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(ОписательВременныхТаблиц, Ложь, МассивНеобходимыхДанных);
	
	// Получим кадровые данные ответственного лица.
	ФизическоеЛицоОтветственногоВМассиве = Новый Массив;
	ФизическоеЛицоОтветственногоВМассиве.Добавить(ФизическоеЛицоОтветственного);
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц 	= ФизическоеЛицоОтветственногоВМассиве;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода 			= ДатаПодписи;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода 		= ДатаПодписи;
	ПараметрыПолученияСотрудниковОрганизаций.КадровыеДанные			= "ВидЗанятости,Организация,Должность";
	
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций);
	КадровыйУчет.СоздатьВТСтраныМираДляГражданства(Запрос.МенеджерВременныхТаблиц);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.Вставить("ВсеЗаписи", Истина);
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ГражданствоФизическихЛиц",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТТаблицаДляСрезаНаКонецГода",
			"Период,ФизическоеЛицо"),
		ПараметрыПостроения);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ИсторияРегистрацийВНалоговомОргане",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТГоловныеОрганизации",
			"Период,СтруктурнаяЕдиница"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГОД(ГражданствоФизЛицаСрезПоследних.Период) КАК Год,
	|	ГражданствоФизЛицаСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтраныМира.Код КАК КодСтраны,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ГражданствоФизЛицаСрезПоследних.Страна, ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)) = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА """"
	|		ИНАЧЕ ГражданствоФизЛицаСрезПоследних.ИНН
	|	КОНЕЦ КАК ИННВСтранеГражданства
	|ПОМЕСТИТЬ ВТГражданствоФизЛиц
	|ИЗ
	|	ВТГражданствоФизическихЛицСрезПоследних КАК ГражданствоФизЛицаСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтраныМираДляГражданства КАК СтраныМира
	|		ПО ГражданствоФизЛицаСрезПоследних.Страна = СтраныМира.Страна
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Периоды.Период КАК Период,
	|	СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО (ГОД(Периоды.Период) = СписокФизЛицОрганизация.Год)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(Периоды.Период) КАК Период,
	|	СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТИзмеренияДатыДляВТСтатусыФизЛицНаКонецНалоговогоПериода
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО (ГОД(Периоды.Период) = СписокФизЛицОрганизация.Год)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокФизЛицОрганизация.Год,
	|	СписокФизЛицОрганизация.ФизическоеЛицо";
	
	Запрос.Выполнить();
	
	// Статусы застрахованных на конец месяца.
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"СтатусФизическихЛицКакНалогоплательщиковНДФЛ",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца",
			"ФизическоеЛицо"),
		,
		"ВТСтатусФизЛицНаКонецМесяцаПромежуточная");
		
	// Статусы застрахованных на конец налогового периода.
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"СтатусФизическихЛицКакНалогоплательщиковНДФЛ",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТИзмеренияДатыДляВТСтатусыФизЛицНаКонецНалоговогоПериода",
			"ФизическоеЛицо"),
		,
		"ВТСтатусыФизЛицНаКонецНалоговогоПериодаПромежуточная");
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.Период, МЕСЯЦ) КАК Месяц,
	|	ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.Период < &ДатаИзмененияНалогообложенияДивидендов
	|				ТОГДА СтатусФизЛицНаКонецМесяца.Статус.КодФНС
	|			ИНАЧЕ СтатусФизЛицНаКонецМесяца.Статус.КодФНС_2015
	|		КОНЕЦ, 1) КАК Статус,
	|	ВЫБОР
	|		КОГДА ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.Период < &ДатаЗакона285ФЗ
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(СтатусФизЛицНаКонецМесяца.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) В (ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент), ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Беженцы))
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		КОГДА ЕСТЬNULL(СтатусФизЛицНаКонецМесяца.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЯвляетсяРезидентомРФ,
	|	ВЫБОР
	|		КОГДА ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.Период < &ДатаЗакона285ФЗ
	|				И СтатусФизЛицНаКонецМесяца.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Беженцы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|		ИНАЧЕ ЕСТЬNULL(СтатусФизЛицНаКонецМесяца.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент))
	|	КОНЕЦ КАК ТекущийСтатус
	|ПОМЕСТИТЬ ВТСтатусФизЛицНаКонецМесяца
	|ИЗ
	|	ВТИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца КАК ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛицНаКонецМесяцаПромежуточная КАК СтатусФизЛицНаКонецМесяца
	|		ПО ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.Период = СтатусФизЛицНаКонецМесяца.Период
	|			И ИзмеренияДатыДляВТСтатусФизЛицНаКонецМесяца.ФизическоеЛицо = СтатусФизЛицНаКонецМесяца.ФизическоеЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокФизЛицОрганизация.ГОД КАК Год,
	|	СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА СписокФизЛицОрганизация.ГОД < &ГодЗакона285ФЗ
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(СтатусыФизЛицНаКонецНалоговогоПериода.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) В (ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент), ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Беженцы))
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		КОГДА ЕСТЬNULL(СтатусыФизЛицНаКонецНалоговогоПериода.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЯвляетсяРезидентомРФ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусыФизЛицНаКонецНалоговогоПериода.Статус, &Резидент) <> &Резидент
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьИностранныйАдрес,
	|	ВЫБОР
	|		КОГДА СписокФизЛицОрганизация.ГОД < &ГодЗакона285ФЗ
	|				И СтатусыФизЛицНаКонецНалоговогоПериода.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Беженцы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|		ИНАЧЕ ЕСТЬNULL(СтатусыФизЛицНаКонецНалоговогоПериода.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент))
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ ВТСтатусыФизЛицНаКонецНалоговогоПериода
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыФизЛицНаКонецНалоговогоПериодаПромежуточная КАК СтатусыФизЛицНаКонецНалоговогоПериода
	|		ПО (СписокФизЛицОрганизация.ГОД = ГОД(СтатусыФизЛицНаКонецНалоговогоПериода.Период))
	|			И СписокФизЛицОрганизация.ФизическоеЛицо = СтатусыФизЛицНаКонецНалоговогоПериода.ФизическоеЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусФизЛицНаКонецМесяца.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(СтатусФизЛицНаКонецМесяца.Месяц, МЕСЯЦ) КАК Период
	|ПОМЕСТИТЬ ВТИзмеренияДатыДляВТПрименениеСтандартныхВычетовПоНДФЛСрезПоследних
	|ИЗ
	|	ВТСтатусФизЛицНаКонецМесяца КАК СтатусФизЛицНаКонецМесяца
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусФизЛицНаКонецМесяца.ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(СтатусФизЛицНаКонецМесяца.Месяц, МЕСЯЦ)";
	
	Запрос.Выполнить();

	// Статусы застрахованных на конец налогового периода.
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ПрименениеСтандартныхВычетовПоНДФЛ",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТИзмеренияДатыДляВТПрименениеСтандартныхВычетовПоНДФЛСрезПоследних",
			"ФизическоеЛицо"));
			
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусФизЛицНаКонецМесяца.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(СтатусФизЛицНаКонецМесяца.Месяц, МЕСЯЦ) КАК Период,
	|	НДФЛПрименениеВычетов.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СтатусыФизЛицНаКонецНалоговогоПериода.ЯвляетсяРезидентомРФ
	|ПОМЕСТИТЬ ВТПрименениеВычетовВМесяцаРезидентства
	|ИЗ
	|	ВТСтатусФизЛицНаКонецМесяца КАК СтатусФизЛицНаКонецМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыФизЛицНаКонецНалоговогоПериода КАК СтатусыФизЛицНаКонецНалоговогоПериода
	|		ПО (ГОД(СтатусФизЛицНаКонецМесяца.Месяц) = СтатусыФизЛицНаКонецНалоговогоПериода.Год)
	|			И СтатусФизЛицНаКонецМесяца.ФизическоеЛицо = СтатусыФизЛицНаКонецНалоговогоПериода.ФизическоеЛицо
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПрименениеСтандартныхВычетовПоНДФЛСрезПоследних КАК НДФЛПрименениеВычетов
	|		ПО (НДФЛПрименениеВычетов.ФизическоеЛицо = СтатусФизЛицНаКонецМесяца.ФизическоеЛицо)
	|			И (НДФЛПрименениеВычетов.Период = СтатусФизЛицНаКонецМесяца.Месяц)
	|ГДЕ
	|	СтатусыФизЛицНаКонецНалоговогоПериода.ЯвляетсяРезидентомРФ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Период,
	|	ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ДатаАктуальности КАК Период,
	|	ФизЛица.ГоловнаяОрганизация.ИндивидуальныйПредприниматель КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛицаДляПолученияФИО
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК ФизЛица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаАктуальности,
	|	&ФизлицоОтветственного";
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(
		Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛицаДляПолученияФИО");
		
	ОписательВременныхТаблиц.ИмяВТКадровыеДанныеФизическихЛиц = "ВТФИООтветственных";
	
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(ОписательВременныхТаблиц, Истина, "ФИОПолные,ИОФамилия");

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СписокФизЛицОрганизация.Год КАК Год,
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация.ИНН КАК Организация_ИНН,
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.КПП КАК Организация_КПП,
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.Код КАК Организация_КодНалоговогоОргана,
	|	ВЫБОР
	|		КОГДА СписокФизЛицОрганизация.ГоловнаяОрганизация.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА СписокФизЛицОрганизация.ГоловнаяОрганизация.НаименованиеПолное
	|		ИНАЧЕ ФИОПредпринимателя.ФИОПолные
	|	КОНЕЦ КАК Организация_Наименование,
	|	ДанныеОФизЛице.ИНН КАК ФизическоеЛицо_ИНН,
	|	КадровыеДанныеФизЛиц.ФИОПолные КАК ФизическоеЛицо_ФИО,
	|	ФИОФизлицаОтветственного.ИОФамилия КАК Ответственный_ФИО,
	|	ДолжностьОтветственного.Должность.Наименование КАК Ответственный_Должность,
	|	КадровыеДанныеФизЛиц.ДокументВид.Наименование КАК ВидДокумента,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеФизЛиц.ДокументВид ЕСТЬ NULL 
	|			ТОГДА """"
	|		ИНАЧЕ КадровыеДанныеФизЛиц.ДокументВид.КодМВД
	|	КОНЕЦ КАК КодДокументаУдЛичн,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеФизЛиц.ДокументВид ЕСТЬ NULL 
	|			ТОГДА """"
	|		ИНАЧЕ КадровыеДанныеФизЛиц.ДокументСерия + "" "" + КадровыеДанныеФизЛиц.ДокументНомер
	|	КОНЕЦ КАК ФизическоеЛицо_СерияНомерДокументаУдЛичн,
	|	ДанныеОФизЛице.ДатаРождения КАК ФизическоеЛицо_ДатаРождения,
	|	ЕСТЬNULL(ГражданствоФизЛица.КодСтраны, ""643"") КАК ФизическоеЛицо_КодСтраны,
	|	ДоходыПредыдущегоМестаРаботыНДФЛ.Размер КАК ФизическоеЛицо_ДоходыПредыдущегоМестаРаботы,
	|	ЕСТЬNULL(КадровыеДанныеФизЛиц.АдресПоПрописке, """") КАК АдресРегистрации,
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицНаКонецНалоговогоПериода.ИспользоватьИностранныйАдрес
	|			ТОГДА ЕСТЬNULL(ГражданствоФизЛица.КодСтраны, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ФизическоеЛицо_АдресКодСтраны,
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицНаКонецНалоговогоПериода.ИспользоватьИностранныйАдрес
	|			ТОГДА ЕСТЬNULL(КадровыеДанныеФизЛиц.АдресЗаПределамиРФ, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ФизическоеЛицо_ИностранныйАдрес
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственных КАК ФИОПредпринимателя
	|		ПО (ФИОПредпринимателя.ФизическоеЛицо = СписокФизЛицОрганизация.ГоловнаяОрганизация.ИндивидуальныйПредприниматель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ДанныеОФизЛице
	|		ПО СписокФизЛицОрганизация.ФизическоеЛицо = ДанныеОФизЛице.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ДанныеОбОтветственном
	|		ПО (ДанныеОбОтветственном.Ссылка = &ФизлицоОтветственного)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизЛиц
	|		ПО СписокФизЛицОрганизация.ФизическоеЛицо = КадровыеДанныеФизЛиц.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсторияРегистрацийВНалоговомОрганеСрезПоследних КАК ИсторияРегистрацийВНалоговомОргане
	|		ПО СписокФизЛицОрганизация.ГоловнаяОрганизация = ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственных КАК ФИОФизлицаОтветственного
	|		ПО (ФИОФизлицаОтветственного.ФизическоеЛицо = &ФизлицоОтветственного)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГражданствоФизЛиц КАК ГражданствоФизЛица
	|		ПО СписокФизЛицОрганизация.ФизическоеЛицо = ГражданствоФизЛица.ФизическоеЛицо
	|			И СписокФизЛицОрганизация.Год = ГражданствоФизЛица.Год
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоходыПредыдущегоМестаРаботыНДФЛ КАК ДоходыПредыдущегоМестаРаботыНДФЛ
	|		ПО СписокФизЛицОрганизация.ФизическоеЛицо = ДоходыПредыдущегоМестаРаботыНДФЛ.ФизическоеЛицо
	|			И СписокФизЛицОрганизация.ГоловнаяОрганизация = ДоходыПредыдущегоМестаРаботыНДФЛ.ГоловнаяОрганизация
	|			И (СписокФизЛицОрганизация.Год = ГОД(ДоходыПредыдущегоМестаРаботыНДФЛ.МесяцНалоговогоПериода))
	|			И (МЕСЯЦ(ДоходыПредыдущегоМестаРаботыНДФЛ.МесяцНалоговогоПериода) = 12)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК ДолжностьОтветственного
	|		ПО (ДолжностьОтветственного.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ВнутреннееСовместительство))
	|			И (ДолжностьОтветственного.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыФизЛицНаКонецНалоговогоПериода КАК СтатусыФизЛицНаКонецНалоговогоПериода
	|		ПО СписокФизЛицОрганизация.ФизическоеЛицо = СтатусыФизЛицНаКонецНалоговогоПериода.ФизическоеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	Год";
	
	ДанныеОФизЛицеОрганизации = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	УчетНДФЛ.СоздатьВТСтандартныеВычетыПоНДФЛЗаПериод(Запрос.МенеджерВременныхТаблиц, Ложь, ДатаПодписи, "ВТПрименениеВычетовВМесяцаРезидентства");
	УчетНДФЛ.СоздатьВТДополнительныеСвойстваКодаДохода(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДФЛСведенияОДоходах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НДФЛСведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НДФЛСведенияОДоходах.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	НДФЛСведенияОДоходах.СуммаДохода КАК СуммаДохода,
	|	НДФЛСведенияОДоходах.КодДохода КАК КодДохода,
	|	НДФЛСведенияОДоходах.КатегорияДохода КАК КатегорияДохода,
	|	НДФЛСведенияОДоходах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	НДФЛСведенияОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	НДФЛСведенияОДоходах.Период КАК Период,
	|	НДФЛСведенияОДоходах.СуммаВычета КАК СуммаВычета,
	|	НДФЛСведенияОДоходах.КодВычета КАК КодВычета,
	|	НДФЛСведенияОДоходах.ДатаПолученияДохода КАК ПериодДляОпределенияСтатуса,
	|	НДФЛСведенияОДоходах.КодДохода.Код КАК КодДоходаКод,
	|	СвойстваКодаДохода.СоответствуетОплатеТруда
	|		И (НДФЛСведенияОДоходах.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ОплатаТруда)
	|			ИЛИ НДФЛСведенияОДоходах.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ДоходыПредыдущихРедакций)) КАК СоответствуетОплатеТруда,
	|	НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	НДФЛСведенияОДоходах.КодДохода.КодПрименяемыйВНалоговойОтчетностиС2010Года КАК КодДоходаКодДляОтчетности,
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛСведенияОДоходах.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА НДФЛСведенияОДоходах.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛСведенияОДоходах.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА НДФЛСведенияОДоходах.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛСведенияОДоходах.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ НДФЛСведенияОДоходах.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ КАК КодВычетаКодДляОтчетности,
	|	НДФЛСведенияОДоходах.КодВычета.НеПредоставляетсяНерезидентам КАК ВычетНеПредоставляетсяНерезидентам,
	|	НДФЛСведенияОДоходах.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	НДФЛСведенияОДоходах.ДатаПолученияДоходаФиксирована КАК ДатаПолученияДоходаФиксирована
	|ПОМЕСТИТЬ ВТСтрокиДоходов
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК НДФЛСведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДополнительныеСвойстваКодаДохода КАК СвойстваКодаДохода
	|		ПО НДФЛСведенияОДоходах.КодДохода = СвойстваКодаДохода.КодДохода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО НДФЛСведенияОДоходах.ФизическоеЛицо = СписокФизЛицОрганизация.ФизическоеЛицо
	|			И НДФЛСведенияОДоходах.ГоловнаяОрганизация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|			И (СписокФизЛицОрганизация.НеОтбиратьПоОП
	|				ИЛИ НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.Организация)
	|ГДЕ
	|	НДФЛСведенияОДоходах.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НДФЛРасчетыСБюджетом.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ВидДвижения КАК ВидДвижения,
	|	НДФЛРасчетыСБюджетом.Сумма КАК Сумма,
	|	НДФЛРасчетыСБюджетом.ВариантУдержания КАК ВариантУдержания,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	НДФЛРасчетыСБюджетом.Период КАК Период,
	|	НДФЛРасчетыСБюджетом.КатегорияДохода КАК КатегорияДохода,
	|	НДФЛРасчетыСБюджетом.КодДохода КАК КодДохода,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК ПериодДляОпределенияСтатуса,
	|	НДФЛРасчетыСБюджетом.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	НДФЛРасчетыСБюджетом.КрайнийСрокУплаты КАК СрокПеречисления,
	|	НДФЛРасчетыСБюджетом.СуммаВыплаченногоДохода КАК СуммаВыплаченногоДохода
	|ПОМЕСТИТЬ ВТСтрокиНалогов
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК НДФЛРасчетыСБюджетом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО НДФЛРасчетыСБюджетом.ФизическоеЛицо = СписокФизЛицОрганизация.ФизическоеЛицо
	|			И (ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = СписокФизЛицОрганизация.Год)
	|			И НДФЛРасчетыСБюджетом.ГоловнаяОрганизация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|			И (СписокФизЛицОрганизация.НеОтбиратьПоОП
	|				ИЛИ НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.Организация)
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыеПлатежиИностранцевПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.ВидДвижения КАК ВидДвижения,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.Сумма КАК Сумма,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.Период КАК Период,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.КодДохода КАК КодДохода,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.МесяцНалоговогоПериода КАК ПериодДляОпределенияСтатуса,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТСтрокиАвансов
	|ИЗ
	|	ВТСтрокиНалогов КАК АвансовыеПлатежиИностранцевПоНДФЛ
	|ГДЕ
	|	АвансовыеПлатежиИностранцевПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.ЗачетАвансовыхПлатежей)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДоходов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДоходов.ПериодДляОпределенияСтатуса КАК Период,
	|	СтрокиДоходов.КодДохода КАК КодДохода,
	|	СтрокиДоходов.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	СтрокиДоходов.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТСтрокиУчета
	|ИЗ
	|	ВТСтрокиДоходов КАК СтрокиДоходов
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиНалогов.ФизическоеЛицо,
	|	СтрокиНалогов.ПериодДляОпределенияСтатуса,
	|	СтрокиНалогов.КодДохода,
	|	СтрокиНалогов.СтавкаНалогообложенияРезидента,
	|	СтрокиНалогов.КатегорияДохода
	|ИЗ
	|	ВТСтрокиНалогов КАК СтрокиНалогов
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиНалогов.ФизическоеЛицо,
	|	КОНЕЦПЕРИОДА(СтрокиНалогов.ПериодДляОпределенияСтатуса, ГОД),
	|	СтрокиНалогов.КодДохода,
	|	СтрокиНалогов.СтавкаНалогообложенияРезидента,
	|	СтрокиНалогов.КатегорияДохода
	|ИЗ
	|	ВТСтрокиНалогов КАК СтрокиНалогов";
	Запрос.Выполнить();
	
	УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента(Запрос.МенеджерВременныхТаблиц, "ВТСтрокиУчета",,,"ВТСтавкаНДФЛ");
	УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента2016(Запрос.МенеджерВременныхТаблиц, "ВТСтрокиУчета");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтандартныеВычетыПоНДФЛЗаПериод.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыПоНДФЛЗаПериод.Месяц КАК Месяц,
	|	СтандартныеВычетыПоНДФЛЗаПериод.КодВычета КАК КодВычетаЛичный,
	|	СтандартныеВычетыПоНДФЛЗаПериод.Количество КАК КоличествоДетей,
	|	СтандартныеВычетыПоНДФЛЗаПериод.Основание КАК Основание
	|ПОМЕСТИТЬ ВТСтандартныеВычетыПоМесяцамРезидентства
	|ИЗ
	|	ВТСтандартныеВычетыПоНДФЛЗаПериод КАК СтандартныеВычетыПоНДФЛЗаПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрименениеВычетовВМесяцаРезидентства.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПрименениеВычетовВМесяцаРезидентства.Период КАК Месяц,
	|	ПрименениеВычетовВМесяцаРезидентства.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СтандартныеВычетыПоМесяцамРезидентства.КодВычетаЛичный КАК КодВычета,
	|	СтандартныеВычетыПоМесяцамРезидентства.КоличествоДетей КАК КоличествоДетей,
	|	СтандартныеВычетыПоМесяцамРезидентства.Основание КАК Основание
	|ПОМЕСТИТЬ ВТСтандартныеВычетыФизЛицОрганизации
	|ИЗ
	|	ВТПрименениеВычетовВМесяцаРезидентства КАК ПрименениеВычетовВМесяцаРезидентства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтандартныеВычетыПоМесяцамРезидентства КАК СтандартныеВычетыПоМесяцамРезидентства
	|		ПО ПрименениеВычетовВМесяцаРезидентства.ФизическоеЛицо = СтандартныеВычетыПоМесяцамРезидентства.ФизическоеЛицо
	|			И ПрименениеВычетовВМесяцаРезидентства.Период = СтандартныеВычетыПоМесяцамРезидентства.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычета КАК КодВычета,
	|	СУММА(НДФЛИмущественныеВычетыФизлиц.Сумма) КАК Размер,
	|	НДФЛИмущественныеВычетыФизлиц.Период КАК Дата
	|ПОМЕСТИТЬ ВТСоциальныеИИмущественныеВычеты
	|ИЗ
	|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК НДФЛИмущественныеВычетыФизлиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО НДФЛИмущественныеВычетыФизлиц.ФизическоеЛицо = СписокФизЛицОрганизация.ФизическоеЛицо
	|			И НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|			И (СписокФизЛицОрганизация.НеОтбиратьПоОП
	|				ИЛИ НДФЛИмущественныеВычетыФизлиц.Организация = СписокФизЛицОрганизация.Организация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = ГОД(&НачалоНП))
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокФизЛицОрганизация.ФизическоеЛицо,
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычета,
	|	НДФЛИмущественныеВычетыФизлиц.Период
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокФизЛицОрганизация.ФизическоеЛицо,
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета,
	|	СУММА(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Сумма),
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|		ПО СписокФизЛицОрганизация.ГоловнаяОрганизация = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ГоловнаяОрганизация
	|			И СписокФизЛицОрганизация.ФизическоеЛицо = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизическоеЛицо
	|			И (СписокФизЛицОрганизация.НеОтбиратьПоОП
	|				ИЛИ СписокФизЛицОрганизация.Организация = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные))
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация,
	|	СписокФизЛицОрганизация.ФизическоеЛицо,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛСведенияОДоходах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НДФЛСведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ГОД(НДФЛСведенияОДоходах.МесяцНалоговогоПериода) КАК Год,
	|	НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.МесяцНалоговогоПериода, МЕСЯЦ) КАК Месяц,
	|	НДФЛСведенияОДоходах.СуммаДохода КАК СуммаДохода,
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицПоГодам.Статус = &Резидент
	|			ТОГДА НДФЛСведенияОДоходах.СуммаВычета
	|		КОГДА НДФЛСведенияОДоходах.ВычетНеПредоставляетсяНерезидентам
	|			ТОГДА 0
	|		ИНАЧЕ НДФЛСведенияОДоходах.СуммаВычета
	|	КОНЕЦ КАК СуммаВычета,
	|	НДФЛСведенияОДоходах.КодДохода КАК КодДохода,
	|	НДФЛСведенияОДоходах.КодДоходаКод КАК КодДоходаКод,
	|	НДФЛСведенияОДоходах.КодДоходаКодДляОтчетности КАК КодДоходаКодДляОтчетности,
	|	ВЫБОР
	|		КОГДА ГОД(НДФЛСведенияОДоходах.МесяцНалоговогоПериода) < 2017
	|			ТОГДА СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ
	|		ИНАЧЕ СтавкиНДФЛ.СтавкаНДФЛ
	|	КОНЕЦ КАК Ставка,
	|	ВЫБОР
	|		КОГДА ГОД(НДФЛСведенияОДоходах.МесяцНалоговогоПериода) < 2017
	|			ТОГДА СтавкаНДФЛПоСтавкеРезидента.СтавкаЧислом
	|		ИНАЧЕ СтавкиНДФЛ.СтавкаЧислом
	|	КОНЕЦ КАК СтавкаЧислом,
	|	НДФЛСведенияОДоходах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.СоответствуетОплатеТруда
	|			ТОГДА НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.МесяцНалоговогоПериода, МЕСЯЦ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК МесяцНачисления,
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.СоответствуетОплатеТруда
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ НДФЛСведенияОДоходах.ДатаПолученияДохода
	|	КОНЕЦ КАК ДатаВыплаты,
	|	НДФЛСведенияОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	НДФЛСведенияОДоходах.ДатаПолученияДоходаФиксирована КАК ДатаПолученияДоходаФиксирована,
	|	НДФЛСведенияОДоходах.Период КАК ДатаНачисления,
	|	НДФЛСведенияОДоходах.КодВычета КАК КодВычета,
	|	НДФЛСведенияОДоходах.КодВычетаКодДляОтчетности КАК КодВычетаКодДляОтчетности,
	|	ВЫБОР
	|		КОГДА ГОД(НДФЛСведенияОДоходах.МесяцНалоговогоПериода) < 2017
	|			ТОГДА ВЫБОР
	|					КОГДА СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							И НДФЛСведенияОДоходах.КодДохода <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		КОГДА СтавкиНДФЛ.СтавкаНДФЛ = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				И НДФЛСведенияОДоходах.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоходРазделаПо13
	|ПОМЕСТИТЬ ВТДоходыПоСтавкам
	|ИЗ
	|	ВТСтрокиДоходов КАК НДФЛСведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО НДФЛСведенияОДоходах.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И НДФЛСведенияОДоходах.КодДохода = СтавкаНДФЛПоСтавкеРезидента.КодДохода
	|			И НДФЛСведенияОДоходах.СтавкаНалогообложенияРезидента = СтавкаНДФЛПоСтавкеРезидента.СтавкаНалогообложенияРезидента
	|			И НДФЛСведенияОДоходах.ПериодДляОпределенияСтатуса = СтавкаНДФЛПоСтавкеРезидента.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛ КАК СтавкиНДФЛ
	|		ПО НДФЛСведенияОДоходах.ФизическоеЛицо = СтавкиНДФЛ.ФизическоеЛицо
	|			И НДФЛСведенияОДоходах.КатегорияДохода = СтавкиНДФЛ.КатегорияДохода
	|			И НДФЛСведенияОДоходах.СтавкаНалогообложенияРезидента = СтавкиНДФЛ.СтавкаНалогообложенияРезидента
	|			И НДФЛСведенияОДоходах.ПериодДляОпределенияСтатуса = СтавкиНДФЛ.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыФизЛицНаКонецНалоговогоПериода КАК СтатусыФизЛицПоГодам
	|		ПО НДФЛСведенияОДоходах.ФизическоеЛицо = СтатусыФизЛицПоГодам.ФизическоеЛицо
	|			И (ГОД(НДФЛСведенияОДоходах.ПериодДляОпределенияСтатуса) = СтатусыФизЛицПоГодам.Год)
	|ГДЕ
	|	(НЕ НДФЛСведенияОДоходах.ИсточникДоходаЗаПределамиРФ
	|			ИЛИ СтатусыФизЛицПоГодам.Статус = &Резидент)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НДФЛРасчетыСБюджетом.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК ГОД,
	|	ВЫБОР
	|		КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НДФЛРасчетыСБюджетом.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогНачислено,
	|	ВЫБОР
	|		КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НДФЛРасчетыСБюджетом.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)
	|			ТОГДА НДФЛРасчетыСБюджетом.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогУдержано,
	|	ВЫБОР
	|		КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НДФЛРасчетыСБюджетом.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)
	|				И НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				И ВЫБОР
	|					КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) < 2017
	|						ТОГДА СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ
	|					ИНАЧЕ СтавкиНДФЛ.СтавкаНДФЛ
	|				КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				И ВЫБОР
	|					КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) < 2017
	|						ТОГДА СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.СтавкаНДФЛ
	|					ИНАЧЕ СтавкаНДФЛНаКонецНалоговогоПериода.СтавкаНДФЛ
	|				КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|			ТОГДА НДФЛРасчетыСБюджетом.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК УдержаноКЗачетуПоСтавке13,
	|	ВЫБОР
	|		КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НДФЛРасчетыСБюджетом.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.ПереданоНаВзысканиеВНалоговыйОрган)
	|			ТОГДА НДФЛРасчетыСБюджетом.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогПереданоНаВзыскание,
	|	ВЫБОР
	|		КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НДФЛРасчетыСБюджетом.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.ВозвращеноНалоговымАгентом)
	|			ТОГДА -НДФЛРасчетыСБюджетом.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогВозвращено,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК Месяц,
	|	НДФЛРасчетыСБюджетом.Период КАК Период,
	|	ВЫБОР
	|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) < 2017
	|			ТОГДА СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ
	|		ИНАЧЕ СтавкиНДФЛ.СтавкаНДФЛ
	|	КОНЕЦ КАК Ставка,
	|	ВЫБОР
	|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) < 2017
	|			ТОГДА СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.СтавкаНДФЛ
	|		ИНАЧЕ СтавкаНДФЛНаКонецНалоговогоПериода.СтавкаНДФЛ
	|	КОНЕЦ КАК СтавкаПоИтогамГода,
	|	НДФЛРасчетыСБюджетом.КодДохода КАК КодДохода,
	|	НДФЛРасчетыСБюджетом.СрокПеречисления КАК СрокПеречисления,
	|	НДФЛРасчетыСБюджетом.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТДанныеОНалогах
	|ИЗ
	|	ВТСтрокиНалогов КАК НДФЛРасчетыСБюджетом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО НДФЛРасчетыСБюджетом.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И НДФЛРасчетыСБюджетом.КодДохода = СтавкаНДФЛПоСтавкеРезидента.КодДохода
	|			И НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = СтавкаНДФЛПоСтавкеРезидента.СтавкаНалогообложенияРезидента
	|			И НДФЛРасчетыСБюджетом.ПериодДляОпределенияСтатуса = СтавкаНДФЛПоСтавкеРезидента.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода
	|		ПО НДФЛРасчетыСБюджетом.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.ФизическоеЛицо
	|			И НДФЛРасчетыСБюджетом.КодДохода = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.КодДохода
	|			И НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.СтавкаНалогообложенияРезидента
	|			И (КОНЕЦПЕРИОДА(НДФЛРасчетыСБюджетом.ПериодДляОпределенияСтатуса, ГОД) = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛ КАК СтавкиНДФЛ
	|		ПО НДФЛРасчетыСБюджетом.ФизическоеЛицо = СтавкиНДФЛ.ФизическоеЛицо
	|			И НДФЛРасчетыСБюджетом.КатегорияДохода = СтавкиНДФЛ.КатегорияДохода
	|			И НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = СтавкиНДФЛ.СтавкаНалогообложенияРезидента
	|			И НДФЛРасчетыСБюджетом.ПериодДляОпределенияСтатуса = СтавкиНДФЛ.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛ КАК СтавкаНДФЛНаКонецНалоговогоПериода
	|		ПО НДФЛРасчетыСБюджетом.ФизическоеЛицо = СтавкаНДФЛНаКонецНалоговогоПериода.ФизическоеЛицо
	|			И НДФЛРасчетыСБюджетом.КатегорияДохода = СтавкаНДФЛНаКонецНалоговогоПериода.КатегорияДохода
	|			И НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = СтавкаНДФЛНаКонецНалоговогоПериода.СтавкаНалогообложенияРезидента
	|			И (КОНЕЦПЕРИОДА(НДФЛРасчетыСБюджетом.ПериодДляОпределенияСтатуса, ГОД) = СтавкаНДФЛНаКонецНалоговогоПериода.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Авансы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Авансы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ГОД(Авансы.МесяцНалоговогоПериода) КАК ГОД,
	|	Авансы.Сумма КАК Зачтено,
	|	Авансы.МесяцНалоговогоПериода КАК Месяц,
	|	Авансы.Период КАК Период,
	|	ВЫБОР
	|		КОГДА ГОД(Авансы.МесяцНалоговогоПериода) < 2017
	|			ТОГДА СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ
	|		ИНАЧЕ СтавкиНДФЛ.СтавкаНДФЛ
	|	КОНЕЦ КАК Ставка,
	|	ВЫБОР
	|		КОГДА ГОД(Авансы.МесяцНалоговогоПериода) < 2017
	|			ТОГДА СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.СтавкаНДФЛ
	|		ИНАЧЕ СтавкаНДФЛНаКонецНалоговогоПериода.СтавкаНДФЛ
	|	КОНЕЦ КАК СтавкаПоИтогамГода,
	|	Авансы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТДанныеОЗачтенныхАвансах
	|ИЗ
	|	ВТСтрокиАвансов КАК Авансы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО Авансы.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И Авансы.КодДохода = СтавкаНДФЛПоСтавкеРезидента.КодДохода
	|			И Авансы.СтавкаНалогообложенияРезидента = СтавкаНДФЛПоСтавкеРезидента.СтавкаНалогообложенияРезидента
	|			И Авансы.ПериодДляОпределенияСтатуса = СтавкаНДФЛПоСтавкеРезидента.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода
	|		ПО Авансы.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.ФизическоеЛицо
	|			И Авансы.КодДохода = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.КодДохода
	|			И Авансы.СтавкаНалогообложенияРезидента = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.СтавкаНалогообложенияРезидента
	|			И (КОНЕЦПЕРИОДА(Авансы.ПериодДляОпределенияСтатуса, ГОД) = СтавкаНДФЛПоСтавкеРезидентаНаКонецНалоговогоПериода.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛ КАК СтавкиНДФЛ
	|		ПО Авансы.ФизическоеЛицо = СтавкиНДФЛ.ФизическоеЛицо
	|			И Авансы.КатегорияДохода = СтавкиНДФЛ.КатегорияДохода
	|			И Авансы.СтавкаНалогообложенияРезидента = СтавкиНДФЛ.СтавкаНалогообложенияРезидента
	|			И Авансы.ПериодДляОпределенияСтатуса = СтавкиНДФЛ.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛ КАК СтавкаНДФЛНаКонецНалоговогоПериода
	|		ПО Авансы.ФизическоеЛицо = СтавкаНДФЛНаКонецНалоговогоПериода.ФизическоеЛицо
	|			И Авансы.КатегорияДохода = СтавкаНДФЛНаКонецНалоговогоПериода.КатегорияДохода
	|			И Авансы.СтавкаНалогообложенияРезидента = СтавкаНДФЛНаКонецНалоговогоПериода.СтавкаНалогообложенияРезидента
	|			И (КОНЕЦПЕРИОДА(Авансы.ПериодДляОпределенияСтатуса, ГОД) = СтавкаНДФЛНаКонецНалоговогоПериода.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НДФЛИмущественныеВычетыФизлиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ) КАК Месяц,
	|	СУММА(НДФЛИмущественныеВычетыФизлиц.Сумма) КАК СуммаВычета,
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицНаКонецНалоговогоПериода.ЯвляетсяРезидентомРФ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка,
	|	НДФЛИмущественныеВычетыФизлиц.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычета КАК КодВычета,
	|	ВЫБОР
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.Период < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.Период < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.Период < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ КАК КодВычетаКодДляОтчетности,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК МесяцПериодаПредоставленияВычета
	|ПОМЕСТИТЬ ВТВычетыПоСтавкам
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИмущественныеВычетыНДФЛ КАК НДФЛИмущественныеВычетыФизлиц
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыФизЛицНаКонецНалоговогоПериода КАК СтатусыФизЛицНаКонецНалоговогоПериода
	|			ПО НДФЛИмущественныеВычетыФизлиц.ФизическоеЛицо = СтатусыФизЛицНаКонецНалоговогоПериода.ФизическоеЛицо
	|				И (ГОД(НДФЛИмущественныеВычетыФизлиц.Период) = СтатусыФизЛицНаКонецНалоговогоПериода.Год)
	|		ПО СписокФизЛицОрганизация.ГоловнаяОрганизация = НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация
	|			И СписокФизЛицОрганизация.ФизическоеЛицо = НДФЛИмущественныеВычетыФизлиц.ФизическоеЛицо
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП)
	|			И (СписокФизЛицОрганизация.НеОтбиратьПоОП
	|				ИЛИ СписокФизЛицОрганизация.Организация = НДФЛИмущественныеВычетыФизлиц.Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация,
	|	НДФЛИмущественныеВычетыФизлиц.ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ),
	|	НДФЛИмущественныеВычетыФизлиц.РегистрацияВНалоговомОргане,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычета,
	|	ВЫБОР
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.Период < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.Период < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.Период < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ НДФЛИмущественныеВычетыФизлиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицНаКонецНалоговогоПериода.ЯвляетсяРезидентомРФ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ГоловнаяОрганизация,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода, МЕСЯЦ),
	|	СУММА(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Сумма),
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицНаКонецНалоговогоПериода.ЯвляетсяРезидентомРФ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.РегистрацияВНалоговомОргане,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета,
	|	ВЫБОР
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцПериодаПредоставленияВычета
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК ВТСписокФизЛицОрганизация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыФизЛицНаКонецНалоговогоПериода КАК СтатусыФизЛицНаКонецНалоговогоПериода
	|			ПО НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизическоеЛицо = СтатусыФизЛицНаКонецНалоговогоПериода.ФизическоеЛицо
	|				И (ГОД(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода) = СтатусыФизЛицНаКонецНалоговогоПериода.Год)
	|		ПО ВТСписокФизЛицОрганизация.ГоловнаяОрганизация = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ГоловнаяОрганизация
	|			И ВТСписокФизЛицОрганизация.ФизическоеЛицо = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизическоеЛицо
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|			И (ВТСписокФизЛицОрганизация.НеОтбиратьПоОП
	|				ИЛИ ВТСписокФизЛицОрганизация.Организация = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ГоловнаяОрганизация,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода, МЕСЯЦ),
	|	ВЫБОР
	|		КОГДА СтатусыФизЛицНаКонецНалоговогоПериода.ЯвляетсяРезидентомРФ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.РегистрацияВНалоговомОргане,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета,
	|	ВЫБОР
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцПериодаПредоставленияВычета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВычетыПоСтавкам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВычетыПоСтавкам.Месяц КАК Месяц,
	|	СУММА(ВычетыПоСтавкам.СуммаВычета) КАК СуммаВычета,
	|	ВычетыПоСтавкам.Ставка КАК Ставка,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТВычетыСгруппированныеДанные
	|ИЗ
	|	ВТВычетыПоСтавкам КАК ВычетыПоСтавкам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВычетыПоСтавкам.ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо,
	|	ВычетыПоСтавкам.Месяц,
	|	ВычетыПоСтавкам.Ставка,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахПоРазделам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОДоходахПоРазделам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахПоРазделам.Месяц КАК Месяц,
	|	СУММА(СведенияОДоходахПоРазделам.СуммаДохода) КАК СуммаДохода,
	|	СведенияОДоходахПоРазделам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТДоходыПоМесяцамПоСтавке13Итог
	|ИЗ
	|	ВТДоходыПоСтавкам КАК СведенияОДоходахПоРазделам
	|ГДЕ
	|	СведенияОДоходахПоРазделам.ДоходРазделаПо13
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахПоРазделам.ГоловнаяОрганизация,
	|	СведенияОДоходахПоРазделам.ФизическоеЛицо,
	|	СведенияОДоходахПоРазделам.Месяц,
	|	СведенияОДоходахПоРазделам.РегистрацияВНалоговомОргане
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыПоСтавкам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДоходыПоСтавкам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДоходыПоСтавкам.Год КАК Год,
	|	ДоходыПоСтавкам.Месяц КАК Месяц,
	|	ДоходыПоСтавкам.Ставка КАК Ставка,
	|	ЕСТЬNULL(МАКСИМУМ(ПредыдущийМесяц.Месяц), ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДоходыПоСтавкам.Месяц, ГОД), СЕКУНДА, -1)) КАК ПредыдущийМесяц,
	|	ДоходыПоСтавкам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТДоходыПо13
	|ИЗ
	|	ВТДоходыПоСтавкам КАК ДоходыПоСтавкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоСтавкам КАК ПредыдущийМесяц
	|		ПО ДоходыПоСтавкам.ГоловнаяОрганизация = ПредыдущийМесяц.ГоловнаяОрганизация
	|			И ДоходыПоСтавкам.ФизическоеЛицо = ПредыдущийМесяц.ФизическоеЛицо
	|			И ДоходыПоСтавкам.Год = ПредыдущийМесяц.Год
	|			И ДоходыПоСтавкам.Месяц > ПредыдущийМесяц.Месяц
	|			И ДоходыПоСтавкам.РегистрацияВНалоговомОргане = ПредыдущийМесяц.РегистрацияВНалоговомОргане
	|			И ДоходыПоСтавкам.ДоходРазделаПо13 = ПредыдущийМесяц.ДоходРазделаПо13
	|ГДЕ
	|	ДоходыПоСтавкам.ДоходРазделаПо13
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыПоСтавкам.ГоловнаяОрганизация,
	|	ДоходыПоСтавкам.ФизическоеЛицо,
	|	ДоходыПоСтавкам.Год,
	|	ДоходыПоСтавкам.Месяц,
	|	ДоходыПоСтавкам.Ставка,
	|	ДоходыПоСтавкам.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыПо13.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДоходыПо13.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДоходыПо13.Год КАК Год,
	|	ДоходыПо13.Месяц КАК Месяц,
	|	ДоходыПо13.Ставка КАК Ставка,
	|	ДоходыПо13.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СУММА(ДоходыПоСтавкам.СуммаДохода) КАК СуммаДохода,
	|	СУММА(ДоходыПоСтавкам.СуммаВычета) КАК СуммаВычета
	|ПОМЕСТИТЬ ВТДоходыДоПолученияСтатусаРезидента
	|ИЗ
	|	ВТДоходыПо13 КАК ДоходыПо13
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоСтавкам КАК ДоходыПоСтавкам
	|		ПО ДоходыПо13.ГоловнаяОрганизация = ДоходыПоСтавкам.ГоловнаяОрганизация
	|			И ДоходыПо13.ФизическоеЛицо = ДоходыПоСтавкам.ФизическоеЛицо
	|			И (ДоходыПоСтавкам.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|			И ДоходыПо13.Месяц > ДоходыПоСтавкам.Месяц
	|			И ДоходыПо13.ПредыдущийМесяц < ДоходыПоСтавкам.Месяц
	|			И ДоходыПо13.Год = ДоходыПоСтавкам.Год
	|			И ДоходыПо13.РегистрацияВНалоговомОргане = ДоходыПоСтавкам.РегистрацияВНалоговомОргане
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыПо13.Год,
	|	ДоходыПо13.ГоловнаяОрганизация,
	|	ДоходыПо13.ФизическоеЛицо,
	|	ДоходыПо13.Месяц,
	|	ДоходыПо13.Ставка,
	|	ДоходыПо13.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Период КАК Период,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Ставка КАК Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Сумма КАК Сумма,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.РеквизитыПлатежногоПоручения КАК РеквизитыПП,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ВТРасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбособленныеПодразделения КАК СписокФизЛицОрганизация
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ФизическоеЛицо = СписокФизЛицОрганизация.ФизическоеЛицо
	|			И (РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|			И (РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Организация = СписокФизЛицОрганизация.Организация
	|ГДЕ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.МесяцНалоговогоПериода КАК Месяц,
	|	МИНИМУМ(ДанныеОНалогах.Период) КАК Дата,
	|	ДанныеОНалогах.Ставка КАК Ставка,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТПерваяСтрокаПеречисленногоНалогаЗаМесяц
	|ИЗ
	|	ВТРасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК ДанныеОНалогах
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	ДанныеОНалогах.МесяцНалоговогоПериода,
	|	ДанныеОНалогах.Ставка,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане,
	|	ДанныеОНалогах.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.ГОД КАК ГОД,
	|	ДанныеОНалогах.Месяц КАК МесяцНалоговогоПериода,
	|	ДанныеОНалогах.СтавкаПоИтогамГода КАК Ставка,
	|	СУММА(ДанныеОНалогах.НалогУдержано) КАК Налог,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТУдержаноНДФЛПоМесяцам
	|ИЗ
	|	ВТДанныеОНалогах КАК ДанныеОНалогах
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОНалогах.ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	ДанныеОНалогах.ГОД,
	|	ДанныеОНалогах.Месяц,
	|	ДанныеОНалогах.СтавкаПоИтогамГода,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ДанныеОНалогах.Ставка КАК Ставка,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СУММА(ДанныеОНалогах.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛПоМесяцам
	|ИЗ
	|	ВТРасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК ДанныеОНалогах
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	ДанныеОНалогах.МесяцНалоговогоПериода,
	|	ДанныеОНалогах.Ставка,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане,
	|	ДанныеОНалогах.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Перечислено.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Перечислено.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Перечислено.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Перечислено.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УдержаноНДФЛПоСтавке13.Налог, 0) = 0
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(УдержаноНДФЛПоСтавке30.Налог, 0) + ЕСТЬNULL(УдержаноНДФЛПоСтавке13.Налог, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ Перечислено.Сумма - (ВЫРАЗИТЬ(Перечислено.Сумма * ЕСТЬNULL(УдержаноНДФЛПоСтавке30.Налог, 0) / (ЕСТЬNULL(УдержаноНДФЛПоСтавке30.Налог, 0) + ЕСТЬNULL(УдержаноНДФЛПоСтавке13.Налог, 0)) КАК ЧИСЛО(22, 0)))
	|	КОНЕЦ КАК КЗачетуПоСтавке13
	|ПОМЕСТИТЬ ВТКЗачетуПоПеречислениям
	|ИЗ
	|	ВТПеречисленоНДФЛПоМесяцам КАК Перечислено
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке13
	|		ПО Перечислено.ФизическоеЛицо = УдержаноНДФЛПоСтавке13.ФизическоеЛицо
	|			И Перечислено.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке13.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке13.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И Перечислено.ГоловнаяОрганизация = УдержаноНДФЛПоСтавке13.ГоловнаяОрганизация
	|			И Перечислено.РегистрацияВНалоговомОргане = УдержаноНДФЛПоСтавке13.РегистрацияВНалоговомОргане
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке30
	|		ПО Перечислено.ФизическоеЛицо = УдержаноНДФЛПоСтавке30.ФизическоеЛицо
	|			И Перечислено.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке30.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке30.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|			И Перечислено.ГоловнаяОрганизация = УдержаноНДФЛПоСтавке30.ГоловнаяОрганизация
	|			И Перечислено.РегистрацияВНалоговомОргане = УдержаноНДФЛПоСтавке30.РегистрацияВНалоговомОргане
	|ГДЕ
	|	Перечислено.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&КонецНП КАК Период,
	|	СтандартныеВычетыПоМесяцамРезидентства.КодВычетаЛичный КАК КодВычета
	|ПОМЕСТИТЬ ВТСтандартныеВычетыВсе
	|ИЗ
	|	ВТСтандартныеВычетыПоМесяцамРезидентства КАК СтандартныеВычетыПоМесяцамРезидентства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыПоСтавкам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТВсеРегистрации
	|ИЗ
	|	ВТДоходыПоСтавкам КАК ДоходыПоСтавкам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТВычетыПоСтавкам КАК ВычетыПоСтавкам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТДанныеОНалогах КАК ДанныеОНалогах
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТРасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОЗачтенныхАвансах.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТДанныеОЗачтенныхАвансах КАК ДанныеОЗачтенныхАвансах";
	Запрос.Выполнить();
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"РазмерВычетовНДФЛ",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТСтандартныеВычетыВсе",
			"Период,КодВычета"));
			
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗачтенныеАвансы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ЗачтенныеАвансы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗачтенныеАвансы.Месяц КАК Месяц,
	|	ЗачтенныеАвансы.Период КАК Дата,
	|	ЗачтенныеАвансы.Ставка КАК Ставка,
	|	ЗачтенныеАвансы.Зачтено КАК ЗачтеноАвансов,
	|	ЗачтенныеАвансы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗачтенныеАвансы.СтавкаПоИтогамГода КАК СтавкаПоИтогамГода
	|ИЗ
	|	ВТДанныеОЗачтенныхАвансах КАК ЗачтенныеАвансы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	Дата,
	|	Ставка
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.КодПоОКАТО КАК КодПоОКАТО,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КПП КАК КПП,
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Ссылка В
	|			(ВЫБРАТЬ
	|				ВсеРегистрации.РегистрацияВНалоговомОргане
	|			ИЗ
	|				ВТВсеРегистрации КАК ВсеРегистрации)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВычетыПоСтавкам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВычетыПоСтавкам.Месяц КАК Месяц,
	|	СУММА(ВычетыПоСтавкам.СуммаВычета) КАК СуммаВычета
	|ИЗ
	|	ВТВычетыПоСтавкам КАК ВычетыПоСтавкам
	|ГДЕ
	|	ВычетыПоСтавкам.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВычетыПоСтавкам.ФизическоеЛицо,
	|	ВычетыПоСтавкам.ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.Месяц,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВычетыПоСтавкам.СуммаВычета) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц
	|ИТОГИ
	|	СУММА(СуммаВычета)
	|ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусФизЛицНаКонецМесяца.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КОНЕЦПЕРИОДА(СтатусФизЛицНаКонецМесяца.Месяц, МЕСЯЦ) КАК Месяц,
	|	СтатусФизЛицНаКонецМесяца.Статус КАК Статус
	|ИЗ
	|	ВТСтатусФизЛицНаКонецМесяца КАК СтатусФизЛицНаКонецМесяца
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо,
	|	Месяц
	|ИТОГИ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПодтверждения.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДокументыПодтверждения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	МАКСИМУМ(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ДатаУведомления) КАК ДатаУведомления,
	|	МАКСИМУМ(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерУведомления) КАК Номер,
	|	МАКСИМУМ(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.КодНалоговогоОргана) КАК КодОрганаУведомление
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТСписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ВТСписокФизЛицОрганизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		МАКСИМУМ(НДФЛИмущественныеВычетыФизлиц.Регистратор) КАК Регистратор
	|	ИЗ
	|		РегистрНакопления.ИмущественныеВычетыНДФЛ КАК НДФЛИмущественныеВычетыФизлиц
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК ВТСписокФизЛицОрганизация
	|			ПО НДФЛИмущественныеВычетыФизлиц.ФизическоеЛицо = ВТСписокФизЛицОрганизация.ФизическоеЛицо
	|				И НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация = ВТСписокФизЛицОрганизация.ГоловнаяОрганизация
	|				И (ВТСписокФизЛицОрганизация.НеОтбиратьПоОП
	|					ИЛИ НДФЛИмущественныеВычетыФизлиц.Организация = ВТСписокФизЛицОрганизация.Организация)
	|				И (НДФЛИмущественныеВычетыФизлиц.Год = ГОД(&НачалоНП))
	|				И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТСписокФизЛицОрганизация.ФизическоеЛицо,
	|		ВТСписокФизЛицОрганизация.ГоловнаяОрганизация) КАК ДокументыПодтверждения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ КАК НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации
	|		ПО ДокументыПодтверждения.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Ссылка
	|			И ДокументыПодтверждения.ФизическоеЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПодтверждения.ГоловнаяОрганизация,
	|	ДокументыПодтверждения.ФизическоеЛицо
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеДатыСправок.Дата КАК ДатаСправки,
	|	МАКСИМУМ(СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.НомерСправки) КАК НомерСправки,
	|	МаксимальныеДатыСправок.Сотрудник КАК ФизическоеЛицо,
	|	1 КАК ТипСправки
	|ИЗ
	|	(ВЫБРАТЬ
	|		СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник КАК Сотрудник,
	|		МАКСИМУМ(СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Дата) КАК Дата
	|	ИЗ
	|		ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкиНДФЛДляПередачиВНалоговыйОрган.Сотрудники КАК СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации
	|			ПО СписокФизЛицОрганизация.ФизическоеЛицо = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник
	|				И СписокФизЛицОрганизация.ГоловнаяОрганизация = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Организация.ГоловнаяОрганизация
	|				И (СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.НалоговыйПериод = ГОД(&НачалоНП))
	|				И (СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.ОснованиеДляПредставленияСправок в (ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ЕжегоднаяОтчетностьЗаРеорганизованнуюОрганизацию), ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ЕжегоднаяОтчетность)))
	|				И (СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Проведен)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник) КАК МаксимальныеДатыСправок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкиНДФЛДляПередачиВНалоговыйОрган.Сотрудники КАК СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации
	|		ПО МаксимальныеДатыСправок.Сотрудник = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник
	|			И МаксимальныеДатыСправок.Дата = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	МаксимальныеДатыСправок.Сотрудник,
	|	МаксимальныеДатыСправок.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МаксимальныеДатыСправок.Дата,
	|	МАКСИМУМ(СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.НомерСправки),
	|	МаксимальныеДатыСправок.Сотрудник,
	|	3
	|ИЗ
	|	(ВЫБРАТЬ
	|		СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник КАК Сотрудник,
	|		МАКСИМУМ(СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Дата) КАК Дата
	|	ИЗ
	|		ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкиНДФЛДляПередачиВНалоговыйОрган.Сотрудники КАК СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации
	|			ПО СписокФизЛицОрганизация.ФизическоеЛицо = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник
	|				И СписокФизЛицОрганизация.ГоловнаяОрганизация = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Организация.ГоловнаяОрганизация
	|				И (СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.НалоговыйПериод = ГОД(&НачалоНП))
	|				И (СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.ОснованиеДляПредставленияСправок В (ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ОНевозможностиУдержанияНДФЛЗаРеорганизованнуюОрганизацию), ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ОНевозможностиУдержанияНДФЛ)))
	|				И (СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Проведен)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник) КАК МаксимальныеДатыСправок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкиНДФЛДляПередачиВНалоговыйОрган.Сотрудники КАК СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации
	|		ПО МаксимальныеДатыСправок.Сотрудник = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Сотрудник
	|			И МаксимальныеДатыСправок.Дата = СправкаПоНДФЛВНалоговыйОрганСотрудникиОрганизации.Ссылка.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	МаксимальныеДатыСправок.Сотрудник,
	|	МаксимальныеДатыСправок.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоследниеДатыСправок.Дата,
	|	МАКСИМУМ(СправкаПоНДФЛСотруднику.Номер),
	|	ПоследниеДатыСправок.ФизическоеЛицо,
	|	2
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокФизЛицОрганизация.ФизическоеЛицо КАК ФизическоеЛицо,
	|		МАКСИМУМ(СправкаПоНДФЛСотруднику.Дата) КАК Дата
	|	ИЗ
	|		ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкаНДФЛ КАК СправкаПоНДФЛСотруднику
	|			ПО СписокФизЛицОрганизация.ФизическоеЛицо = СправкаПоНДФЛСотруднику.Сотрудник
	|				И СписокФизЛицОрганизация.ГоловнаяОрганизация = СправкаПоНДФЛСотруднику.Организация.ГоловнаяОрганизация
	|				И (СправкаПоНДФЛСотруднику.НалоговыйПериод = ГОД(&НачалоНП))
	|				И (НЕ СправкаПоНДФЛСотруднику.ПометкаУдаления)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписокФизЛицОрганизация.ФизическоеЛицо) КАК ПоследниеДатыСправок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СправкаНДФЛ КАК СправкаПоНДФЛСотруднику
	|		ПО ПоследниеДатыСправок.Дата = СправкаПоНДФЛСотруднику.Дата
	|			И ПоследниеДатыСправок.ФизическоеЛицо = СправкаПоНДФЛСотруднику.Сотрудник
	|			И (НЕ СправкаПоНДФЛСотруднику.ПометкаУдаления)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоследниеДатыСправок.ФизическоеЛицо,
	|	ПоследниеДатыСправок.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо,
	|	ТипСправки
	|ИТОГИ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтандартныеВычетыФизЛицОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыФизЛицОрганизации.Месяц КАК Месяц,
	|	ВЫБОР
	|		КОГДА СтандартныеВычетыФизЛицОрганизации.Месяц < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА ВидыВычетовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА СтандартныеВычетыФизЛицОрганизации.Месяц < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА ВидыВычетовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА СтандартныеВычетыФизЛицОрганизации.Месяц < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА ВидыВычетовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ ВидыВычетовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ КАК КодВычета,
	|	СтандартныеВычетыФизЛицОрганизации.КоличествоДетей КАК КоличествоДетей,
	|	СтандартныеВычетыФизЛицОрганизации.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ЕСТЬNULL(СтандартныеВычетыФизЛицОрганизации.КоличествоДетей, 1) * ЕСТЬNULL(НДФЛРазмерВычетовСрезПоследних.Размер, 0) КАК Размер,
	|	ВЫРАЗИТЬ(СтандартныеВычетыФизЛицОрганизации.Основание КАК СТРОКА(1000)) КАК Основание
	|ИЗ
	|	ВТСтандартныеВычетыФизЛицОрганизации КАК СтандартныеВычетыФизЛицОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРазмерВычетовНДФЛСрезПоследних КАК НДФЛРазмерВычетовСрезПоследних
	|		ПО СтандартныеВычетыФизЛицОрганизации.КодВычета = НДФЛРазмерВычетовСрезПоследних.КодВычета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО СтандартныеВычетыФизЛицОрганизации.ФизическоеЛицо = СписокФизЛицОрганизация.ФизическоеЛицо
	|			И СтандартныеВычетыФизЛицОрганизации.ГоловнаяОрганизация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыВычетовНДФЛ КАК ВидыВычетовНДФЛ
	|		ПО СтандартныеВычетыФизЛицОрганизации.КодВычета = ВидыВычетовНДФЛ.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	КодВычета,
	|	Месяц
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоциальныеИИмущественныеВычеты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СоциальныеИИмущественныеВычеты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА СоциальныеИИмущественныеВычеты.Дата < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА СоциальныеИИмущественныеВычеты.Дата < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА СоциальныеИИмущественныеВычеты.Дата < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ КАК КодВычетаКод,
	|	СоциальныеИИмущественныеВычеты.Размер КАК Размер,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА НАЧАЛОПЕРИОДА(СоциальныеИИмущественныеВычеты.Дата, МЕСЯЦ) = СтатусФизЛицНаКонецМесяца.Месяц
	|				ТОГДА СоциальныеИИмущественныеВычеты.Дата
	|			ИНАЧЕ СтатусФизЛицНаКонецМесяца.Месяц
	|		КОНЕЦ) КАК Дата
	|ИЗ
	|	ВТСтатусФизЛицНаКонецМесяца КАК СтатусФизЛицНаКонецМесяца
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСоциальныеИИмущественныеВычеты КАК СоциальныеИИмущественныеВычеты
	|		ПО СтатусФизЛицНаКонецМесяца.ФизическоеЛицо = СоциальныеИИмущественныеВычеты.ФизическоеЛицо
	|			И (КОНЕЦПЕРИОДА(СтатусФизЛицНаКонецМесяца.Месяц, МЕСЯЦ) >= СоциальныеИИмущественныеВычеты.Дата)
	|ГДЕ
	|	СтатусФизЛицНаКонецМесяца.ЯвляетсяРезидентомРФ
	|
	|СГРУППИРОВАТЬ ПО
	|	СоциальныеИИмущественныеВычеты.ФизическоеЛицо,
	|	СоциальныеИИмущественныеВычеты.Размер,
	|	СоциальныеИИмущественныеВычеты.Дата,
	|	СоциальныеИИмущественныеВычеты.ГоловнаяОрганизация,
	|	ВЫБОР
	|		КОГДА СоциальныеИИмущественныеВычеты.Дата < ДАТАВРЕМЯ(2011, 1, 1, 0, 0, 0)
	|			ТОГДА СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2010Года
	|		КОГДА СоциальныеИИмущественныеВычеты.Дата < ДАТАВРЕМЯ(2015, 1, 1, 0, 0, 0)
	|			ТОГДА СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2011Года
	|		КОГДА СоциальныеИИмущественныеВычеты.Дата < ДАТАВРЕМЯ(2016, 1, 1, 0, 0, 0)
	|			ТОГДА СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2015Года
	|		ИНАЧЕ СоциальныеИИмущественныеВычеты.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	КодВычетаКод,
	|	Дата
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Доходы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Доходы.Раздел КАК Раздел,
	|	Доходы.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	МАКСИМУМ(Доходы.СуммаДоходаДоРезидентства) КАК СуммаДоходаДоРезидентства,
	|	МАКСИМУМ(Доходы.СуммаВычетаДоРезидентства) КАК СуммаВычетаДоРезидентства,
	|	МАКСИМУМ(Доходы.ОблагаемаяСуммаДоходаДоРезидентства) КАК ОблагаемаяСуммаДоходаДоРезидентства,
	|	Доходы.ДатаНачисления КАК ДатаНачисления,
	|	Доходы.МесяцНачисления КАК МесяцНачисления,
	|	Доходы.ДатаВыплаты КАК ДатаВыплаты,
	|	Доходы.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	Доходы.КодДоходаСсылка КАК КодДоходаСсылка,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода,
	|	Доходы.КодВычетаСсылка КАК КодВычетаСсылка,
	|	СУММА(Доходы.СуммаВычета) КАК СуммаВычета,
	|	СУММА(Доходы.ОблагаемаяСуммаДохода) КАК ОблагаемаяСуммаДохода,
	|	СУММА(Доходы.НалогНачислено) КАК НалогНачислено,
	|	Доходы.КодДохода КАК КодДохода,
	|	Доходы.КодВычета КАК КодВычета,
	|	Доходы.Ставка КАК Ставка,
	|	Доходы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДоходыПоМесяцам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		ДоходыПоМесяцам.ФизическоеЛицо КАК ФизическоеЛицо,
	|		""Раздел4_Ставка13"" КАК Раздел,
	|		ДоходыПоМесяцам.Месяц КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(ЕСТЬNULL(ДоходыДоПолученияСтатусаРезидента.СуммаДохода, 0)) КАК СуммаДоходаДоРезидентства,
	|		МАКСИМУМ(ЕСТЬNULL(ДоходыДоПолученияСтатусаРезидента.СуммаВычета, 0)) КАК СуммаВычетаДоРезидентства,
	|		МАКСИМУМ(ЕСТЬNULL(ДоходыДоПолученияСтатусаРезидента.СуммаДохода, 0) - ЕСТЬNULL(ДоходыДоПолученияСтатусаРезидента.СуммаВычета, 0)) КАК ОблагаемаяСуммаДоходаДоРезидентства,
	|		ДоходыПоМесяцам.ДатаНачисления КАК ДатаНачисления,
	|		ДоходыПоМесяцам.МесяцНачисления КАК МесяцНачисления,
	|		ДоходыПоМесяцам.ДатаВыплаты КАК ДатаВыплаты,
	|		ДоходыПоМесяцам.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|		ДоходыПоМесяцам.КодДохода КАК КодДоходаСсылка,
	|		СУММА(ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0)) КАК СуммаДохода,
	|		ДоходыПоМесяцам.КодВычета КАК КодВычетаСсылка,
	|		СУММА(ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0)) КАК СуммаВычета,
	|		СУММА(ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0) - ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0)) КАК ОблагаемаяСуммаДохода,
	|		0 КАК НалогНачислено,
	|		ДоходыПоМесяцам.КодДоходаКодДляОтчетности КАК КодДохода,
	|		ДоходыПоМесяцам.КодВычетаКодДляОтчетности КАК КодВычета,
	|		ДоходыПоМесяцам.Ставка КАК Ставка,
	|		ДоходыПоМесяцам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|	ИЗ
	|		ВТДоходыПоСтавкам КАК ДоходыПоМесяцам
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыДоПолученияСтатусаРезидента КАК ДоходыДоПолученияСтатусаРезидента
	|			ПО ДоходыПоМесяцам.ГоловнаяОрганизация = ДоходыДоПолученияСтатусаРезидента.ГоловнаяОрганизация
	|				И ДоходыПоМесяцам.ФизическоеЛицо = ДоходыДоПолученияСтатусаРезидента.ФизическоеЛицо
	|				И ДоходыПоМесяцам.Год = ДоходыДоПолученияСтатусаРезидента.Год
	|				И ДоходыПоМесяцам.Месяц = ДоходыДоПолученияСтатусаРезидента.Месяц
	|				И ДоходыПоМесяцам.РегистрацияВНалоговомОргане = ДоходыДоПолученияСтатусаРезидента.РегистрацияВНалоговомОргане
	|				И ДоходыПоМесяцам.Ставка = ДоходыДоПолученияСтатусаРезидента.Ставка
	|	ГДЕ
	|		ДоходыПоМесяцам.ДоходРазделаПо13
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДоходыПоМесяцам.ФизическоеЛицо,
	|		ДоходыПоМесяцам.Месяц,
	|		ДоходыПоМесяцам.МесяцНачисления,
	|		ДоходыПоМесяцам.ДатаВыплаты,
	|		ДоходыПоМесяцам.КодДохода,
	|		ДоходыПоМесяцам.КодВычета,
	|		ДоходыПоМесяцам.ГоловнаяОрганизация,
	|		ДоходыПоМесяцам.ДатаНачисления,
	|		ДоходыПоМесяцам.КодДоходаКодДляОтчетности,
	|		ДоходыПоМесяцам.КодВычетаКодДляОтчетности,
	|		ДоходыПоМесяцам.Ставка,
	|		ДоходыПоМесяцам.ДатаПолученияДохода,
	|		ДоходыПоМесяцам.РегистрацияВНалоговомОргане
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДоходыПоМесяцам.ГоловнаяОрганизация,
	|		ДоходыПоМесяцам.ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА ДоходыПоМесяцам.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				ТОГДА ""Раздел4_Ставка30""
	|			ИНАЧЕ ""Раздел4_Ставка35_15_9""
	|		КОНЕЦ,
	|		ДоходыПоМесяцам.Месяц,
	|		0,
	|		0,
	|		0,
	|		ДоходыПоМесяцам.ДатаНачисления,
	|		ДоходыПоМесяцам.МесяцНачисления,
	|		ДоходыПоМесяцам.ДатаВыплаты,
	|		ДоходыПоМесяцам.ДатаПолученияДохода,
	|		ДоходыПоМесяцам.КодДохода,
	|		ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0),
	|		ДоходыПоМесяцам.КодВычета,
	|		ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0),
	|		ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0) - ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0),
	|		(ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0) - ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0)) * ДоходыПоМесяцам.СтавкаЧислом,
	|		ДоходыПоМесяцам.КодДоходаКодДляОтчетности,
	|		ДоходыПоМесяцам.КодВычетаКодДляОтчетности,
	|		ДоходыПоМесяцам.Ставка,
	|		ДоходыПоМесяцам.РегистрацияВНалоговомОргане
	|	ИЗ
	|		ВТДоходыПоСтавкам КАК ДоходыПоМесяцам
	|	ГДЕ
	|		НЕ ДоходыПоМесяцам.ДоходРазделаПо13
	|		И НЕ ДоходыПоМесяцам.ДатаПолученияДоходаФиксирована) КАК Доходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.ФизическоеЛицо,
	|	Доходы.ГоловнаяОрганизация,
	|	Доходы.Раздел,
	|	Доходы.МесяцНалоговогоПериода,
	|	Доходы.ДатаНачисления,
	|	Доходы.МесяцНачисления,
	|	Доходы.ДатаВыплаты,
	|	Доходы.ДатаПолученияДохода,
	|	Доходы.КодДоходаСсылка,
	|	Доходы.КодВычетаСсылка,
	|	Доходы.Ставка,
	|	Доходы.КодВычета,
	|	Доходы.РегистрацияВНалоговомОргане,
	|	Доходы.КодДохода
	|
	|ИМЕЮЩИЕ
	|	(МАКСИМУМ(Доходы.СуммаДоходаДоРезидентства) <> 0
	|		ИЛИ МАКСИМУМ(Доходы.СуммаВычетаДоРезидентства) <> 0
	|		ИЛИ МАКСИМУМ(Доходы.ОблагаемаяСуммаДоходаДоРезидентства) <> 0
	|		ИЛИ СУММА(Доходы.СуммаДохода) <> 0
	|		ИЛИ СУММА(Доходы.СуммаВычета) <> 0
	|		ИЛИ СУММА(Доходы.ОблагаемаяСуммаДохода) <> 0
	|		ИЛИ СУММА(Доходы.НалогНачислено) <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоходыПоМесяцам.ГоловнаяОрганизация,
	|	ДоходыПоМесяцам.ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ДоходыПоМесяцам.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|			ТОГДА ""Раздел4_Ставка30""
	|		ИНАЧЕ ""Раздел4_Ставка35_15_9""
	|	КОНЕЦ,
	|	ДоходыПоМесяцам.Месяц,
	|	0,
	|	0,
	|	0,
	|	ДоходыПоМесяцам.ДатаНачисления,
	|	ДоходыПоМесяцам.МесяцНачисления,
	|	ДоходыПоМесяцам.ДатаВыплаты,
	|	ДоходыПоМесяцам.ДатаПолученияДохода,
	|	ДоходыПоМесяцам.КодДохода,
	|	ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0),
	|	ДоходыПоМесяцам.КодВычета,
	|	ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0),
	|	ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0) - ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0),
	|	(ЕСТЬNULL(ДоходыПоМесяцам.СуммаДохода, 0) - ЕСТЬNULL(ДоходыПоМесяцам.СуммаВычета, 0)) * ДоходыПоМесяцам.СтавкаЧислом,
	|	ДоходыПоМесяцам.КодДоходаКодДляОтчетности,
	|	ДоходыПоМесяцам.КодВычетаКодДляОтчетности,
	|	ДоходыПоМесяцам.Ставка,
	|	ДоходыПоМесяцам.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТДоходыПоСтавкам КАК ДоходыПоМесяцам
	|ГДЕ
	|	НЕ ДоходыПоМесяцам.ДоходРазделаПо13
	|	И ДоходыПоМесяцам.ДатаПолученияДоходаФиксирована
	|	И ДоходыПоМесяцам.СуммаДохода <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Раздел,
	|	МесяцНалоговогоПериода,
	|	МесяцНачисления,
	|	ДатаНачисления,
	|	ДатаВыплаты,
	|	КодДохода,
	|	КодВычета
	|ИТОГИ
	|	МАКСИМУМ(СуммаДоходаДоРезидентства),
	|	МАКСИМУМ(СуммаВычетаДоРезидентства),
	|	МАКСИМУМ(ОблагаемаяСуммаДоходаДоРезидентства),
	|	СУММА(СуммаДохода),
	|	СУММА(СуммаВычета),
	|	СУММА(ОблагаемаяСуммаДохода),
	|	МАКСИМУМ(НалогНачислено)
	|ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Раздел,
	|	МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВычетыПоСтавкам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВычетыПоСтавкам.Месяц КАК Месяц,
	|	ВычетыПоСтавкам.МесяцПериодаПредоставленияВычета КАК МесяцПериодаПредоставленияВычета,
	|	ВычетыПоСтавкам.КодВычета КАК КодВычетаСсылка,
	|	СУММА(ВычетыПоСтавкам.СуммаВычета) КАК СуммаВычета,
	|	МАКСИМУМ(ЕСТЬNULL(ДоходыПоМесяцамПоСтавке13Итог.СуммаДохода, 0)) КАК СуммаДохода,
	|	ВычетыПоСтавкам.КодВычетаКодДляОтчетности КАК КодВычета,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТВычетыПоСтавкам КАК ВычетыПоСтавкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцамПоСтавке13Итог КАК ДоходыПоМесяцамПоСтавке13Итог
	|		ПО (ДоходыПоМесяцамПоСтавке13Итог.ГоловнаяОрганизация = ВычетыПоСтавкам.ГоловнаяОрганизация)
	|			И (ДоходыПоМесяцамПоСтавке13Итог.ФизическоеЛицо = ВычетыПоСтавкам.ФизическоеЛицо)
	|			И (ДоходыПоМесяцамПоСтавке13Итог.Месяц = ВычетыПоСтавкам.Месяц)
	|			И (ДоходыПоМесяцамПоСтавке13Итог.РегистрацияВНалоговомОргане = ВычетыПоСтавкам.РегистрацияВНалоговомОргане)
	|			И (ВычетыПоСтавкам.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВычетыПоСтавкам.КодВычета,
	|	ВычетыПоСтавкам.КодВычетаКодДляОтчетности,
	|	ВычетыПоСтавкам.ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо,
	|	ВычетыПоСтавкам.Месяц,
	|	ВычетыПоСтавкам.МесяцПериодаПредоставленияВычета,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	МесяцПериодаПредоставленияВычета,
	|	КодВычета
	|ИТОГИ
	|	СУММА(СуммаВычета),
	|	МАКСИМУМ(СуммаДохода)
	|ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.Месяц КАК Месяц,
	|	ДанныеОНалогах.Период КАК Дата,
	|	ДанныеОНалогах.Ставка КАК Ставка,
	|	СУММА(ДанныеОНалогах.НалогНачислено) КАК НалогНачислено,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ДанныеОНалогах.СтавкаПоИтогамГода КАК СтавкаПоИтогамГода
	|ИЗ
	|	ВТДанныеОНалогах КАК ДанныеОНалогах
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	ДанныеОНалогах.Месяц,
	|	ДанныеОНалогах.Период,
	|	ДанныеОНалогах.Ставка,
	|	ДанныеОНалогах.ГоловнаяОрганизация,
	|	ДанныеОНалогах.СтавкаПоИтогамГода,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеОНалогах.НалогНачислено) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	Дата,
	|	Ставка
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.Месяц КАК Месяц,
	|	ДанныеОНалогах.Период КАК Дата,
	|	ДанныеОНалогах.Ставка КАК Ставка,
	|	ВЫБОР
	|		КОГДА ДанныеОНалогах.Период < &ДатаИзмененияСроковУплатыНалога
	|			ТОГДА ДанныеОНалогах.Период
	|		ИНАЧЕ ДанныеОНалогах.СрокПеречисления
	|	КОНЕЦ КАК СрокПеречисления,
	|	СУММА(ДанныеОНалогах.НалогУдержано) КАК НалогУдержано,
	|	СУММА(ДанныеОНалогах.УдержаноКЗачетуПоСтавке13) КАК КЗачетуПоСтавке13,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТДанныеОНалогах КАК ДанныеОНалогах
	|ГДЕ
	|	ДанныеОНалогах.НалогУдержано <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОНалогах.ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	ДанныеОНалогах.Месяц,
	|	ДанныеОНалогах.Период,
	|	ДанныеОНалогах.Ставка,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА ДанныеОНалогах.Период < &ДатаИзмененияСроковУплатыНалога
	|			ТОГДА ДанныеОНалогах.Период
	|		ИНАЧЕ ДанныеОНалогах.СрокПеречисления
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	Дата,
	|	Ставка,
	|	СрокПеречисления
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.Месяц КАК Месяц,
	|	ДанныеОНалогах.Период КАК Дата,
	|	ДанныеОНалогах.Ставка КАК Ставка,
	|	ДанныеОНалогах.НалогВозвращено КАК НалогВозвращено,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТДанныеОНалогах КАК ДанныеОНалогах
	|ГДЕ
	|	ДанныеОНалогах.НалогВозвращено <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	Дата,
	|	Ставка
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеОНалогах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНалогах.Месяц КАК Месяц,
	|	ДанныеОНалогах.Период КАК Дата,
	|	ДанныеОНалогах.Ставка КАК Ставка,
	|	ДанныеОНалогах.НалогПереданоНаВзыскание КАК НалогПереданоНаВзыскание,
	|	ДанныеОНалогах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТДанныеОНалогах КАК ДанныеОНалогах
	|ГДЕ
	|	ДанныеОНалогах.НалогПереданоНаВзыскание <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	Дата,
	|	Ставка
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК Месяц,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Период КАК Дата,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Ставка КАК Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Сумма КАК Сумма,
	|	ЕСТЬNULL(ПеречисленоДоПолученияСтатусаРезидента.КЗачетуПоСтавке13, 0) КАК КЗачетуПоСтавке13,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.РеквизитыПП КАК РеквизитыПП,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТРасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПерваяСтрокаПеречисленногоНалогаЗаМесяц КАК ПерваяСтрокаПеречисленногоНалогаЗаМесяц
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКЗачетуПоПеречислениям КАК ПеречисленоДоПолученияСтатусаРезидента
	|			ПО ПерваяСтрокаПеречисленногоНалогаЗаМесяц.ГоловнаяОрганизация = ПеречисленоДоПолученияСтатусаРезидента.ГоловнаяОрганизация
	|				И ПерваяСтрокаПеречисленногоНалогаЗаМесяц.ФизическоеЛицо = ПеречисленоДоПолученияСтатусаРезидента.ФизическоеЛицо
	|				И ПерваяСтрокаПеречисленногоНалогаЗаМесяц.Месяц = ПеречисленоДоПолученияСтатусаРезидента.МесяцНалоговогоПериода
	|				И ПерваяСтрокаПеречисленногоНалогаЗаМесяц.РегистрацияВНалоговомОргане = ПеречисленоДоПолученияСтатусаРезидента.РегистрацияВНалоговомОргане
	|				И (ПерваяСтрокаПеречисленногоНалогаЗаМесяц.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ГоловнаяОрганизация = ПерваяСтрокаПеречисленногоНалогаЗаМесяц.ГоловнаяОрганизация
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ФизическоеЛицо = ПерваяСтрокаПеречисленногоНалогаЗаМесяц.ФизическоеЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода = ПерваяСтрокаПеречисленногоНалогаЗаМесяц.Месяц
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Ставка = ПерваяСтрокаПеречисленногоНалогаЗаМесяц.Ставка
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане = ПерваяСтрокаПеречисленногоНалогаЗаМесяц.РегистрацияВНалоговомОргане
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Период = ПерваяСтрокаПеречисленногоНалогаЗаМесяц.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	Месяц,
	|	Дата,
	|	Ставка
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВычетыПоСтавкам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВычетыПоСтавкам.КодВычетаКодДляОтчетности КАК КодВычета,
	|	СУММА(ВычетыПоСтавкам.СуммаВычета) КАК СуммаВычета,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТВычетыПоСтавкам КАК ВычетыПоСтавкам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВычетыПоСтавкам.ГоловнаяОрганизация,
	|	ВычетыПоСтавкам.ФизическоеЛицо,
	|	ВычетыПоСтавкам.КодВычетаКодДляОтчетности,
	|	ВычетыПоСтавкам.РегистрацияВНалоговомОргане
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВычетыПоСтавкам.СуммаВычета) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо,
	|	РегистрацияВНалоговомОргане,
	|	КодВычета
	|ИТОГИ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо";
		
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.Количество();
	
	ТаблицаРегистрацийВНО = Результат[КоличествоРезультатов - 15].Выгрузить();
	ТаблицаРегистрацийВНО.Колонки.Добавить("ОКАТО_КПП", Новый ОписаниеТипов("Строка"));
	ТаблицаРегистрацийВНО.Колонки.Добавить("ОКТМО_КПП", Новый ОписаниеТипов("Строка"));
	ТаблицаРегистрацийВНО.Индексы.Добавить("РегистрацияВНалоговомОргане");
	Для каждого СтрокаТЗ Из ТаблицаРегистрацийВНО Цикл
		СтрокаТЗ.ОКАТО_КПП = УчетНДФЛ.СуммаОКАТОиКПП(СтрокаТЗ.КодПоОКАТО, СтрокаТЗ.КПП);
		СтрокаТЗ.ОКТМО_КПП = УчетНДФЛ.СуммаОКАТОиКПП(СтрокаТЗ.КодПоОКТМО, СтрокаТЗ.КПП);
	КонецЦикла;
	
	ВыборкаАвансыЗачтенныеИтоги		= Результат[КоличествоРезультатов - 16].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаВычетовПоСтавке13		= Результат[КоличествоРезультатов - 14].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	ВыборкаСтатусов					= Результат[КоличествоРезультатов - 13].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаУведомления				= Результат[КоличествоРезультатов - 12].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	ВыборкаСправкиНДФЛ				= Результат[КоличествоРезультатов - 11].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	ВыборкаСтандартныеВычеты		= Результат[КоличествоРезультатов - 10].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаСоциальныеВычеты			= Результат[КоличествоРезультатов - 9].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаДоходовПоМесяцамПоРазделам = Результат[КоличествоРезультатов - 8].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаВычетыПоМесяцамПоВидам   = Результат[КоличествоРезультатов - 7].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаНалогиНачисленныеИтоги   = Результат[КоличествоРезультатов - 6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаНалогиУдержанныеИтоги    = Результат[КоличествоРезультатов - 5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаНалогиВозвращенныеИтоги  = Результат[КоличествоРезультатов - 4].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаНалогиВзысканиеИтоги     = Результат[КоличествоРезультатов - 3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаНалогиПеречисленныеИтоги = Результат[КоличествоРезультатов - 2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаВычетыИтоги				= Результат[КоличествоРезультатов - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

КонецПроцедуры

Процедура ПолучитьСпискиФизЛицИСотрудников(СтрокиДеревоДанных, СписокФизЛиц)
	
	Для Каждого СтрокаДанных Из СтрокиДеревоДанных Цикл
		Если СтрокаДанных.ФизическоеЛицо <> Неопределено  //СтрокаДанных.Организация <> Неопределено Тогда
			                                                                                       Тогда
			СтрокаСписка = СписокФизЛиц.Добавить();
			СтрокаСписка.ФизическоеЛицо     = СтрокаДанных.ФизическоеЛицо;
			СтрокаСписка.Организация = СтрокаДанных.Организация;
			СтрокаСписка.ГоловнаяОрганизация = СтрокаДанных.ГоловнаяОрганизация;
			СтрокаСписка.Год = Год(СтрокаДанных.Год);
			СтрокаСписка.НЕОтбиратьПоОП = (СтрокаДанных.ГоловнаяОрганизация = СтрокаДанных.Организация)
			
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ВывестиКарточки(ДокументРезультат, СтрокиДеревоДанных)
	СтатусРезидент = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент");
	ВывестиРазделитель = Ложь;
	Для каждого СтрокаДерева Из СтрокиДеревоДанных Цикл
		Если ВывестиРазделитель Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ВывестиРазделитель = Истина;
		СформироватьМакет(ДокументРезультат, СтрокаДерева.ФизическоеЛицо, СтрокаДерева.Год,  СтрокаДерева.ГоловнаяОрганизация, СтатусРезидент);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет табличный документ
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом.
//
Процедура СформироватьМакет(ДокументРезультат, ФизическоеЛицо, Год, ГоловнаяОрганизация, СтатусРезидент) Экспорт
		
	Ответственный = Справочники.Пользователи.ПустаяСсылка();
	
	Макет =	УправлениеПечатью.МакетПечатнойФормы("Отчет.РегистрНалоговогоУчетаПоНДФЛ.ПФ_MXL_ФормаРегистрНалоговогоУчетаПоНДФЛ");
		
	НалогПоСтавке13ИсчисленПомесячно = Год < УчетНДФЛ.ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев();
	
	// Области-разделы
    Раздел1 = Макет.ПолучитьОбласть("Раздел1");									    
	Раздел2 = Макет.ПолучитьОбласть("Раздел2");	
	Раздел3 = Макет.ПолучитьОбласть("Раздел3");
	Раздел7 = Макет.ПолучитьОбласть("Раздел7");
	РасчетыНалогаПоСтавке13Шапка = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13Шапка");
	РасчетыНалогаПоСтавке13Строка = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13Строка");
	РасчетыНалогаПоСтавке13МесяцГруппа = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13МесяцГруппа");
	РасчетыНалогаПоСтавке13БылоПоСтавке30 = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13БылоПоСтавке30");
	РасчетыНалогаПоСтавке13Итог = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13ИтогМесяц");
	ПредоставленныеВычетыШапка = Макет.ПолучитьОбласть("ПредоставленныеВычетыШапка");
	ПредоставленныеВычетыЗаголовок = Макет.ПолучитьОбласть("ПредоставленныеВычетыЗаголовок");
	ПредоставленныеВычетыШапкаМесяцГруппа = Макет.ПолучитьОбласть("ПредоставленныеВычетыШапкаМесяцГруппа");
	ПредоставленныеВычетыСтрока = Макет.ПолучитьОбласть("ПредоставленныеВычетыСтрока");
	ПредоставленныеВычетыИтог = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтог");
	НалогПоСтавке30Заголовок = Макет.ПолучитьОбласть("НалогПоСтавке30Заголовок");
	НалогПоСтавке30Шапка = Макет.ПолучитьОбласть("НалогПоСтавке30Шапка");
	НалогПоСтавке30Строка = Макет.ПолучитьОбласть("НалогПоСтавке30Строка");
	НалогПоСтавке30Итог = Макет.ПолучитьОбласть("НалогПоСтавке30Итог");
	НалогПоСтавке9Шапка = Макет.ПолучитьОбласть("НалогПоСтавке9Шапка");
	НалогПоСтавке9Строка = Макет.ПолучитьОбласть("НалогПоСтавке9Строка");
	НалогПоСтавке9Итог = Макет.ПолучитьОбласть("НалогПоСтавке9Итог");
	ИсчисленоЗаголовок = Макет.ПолучитьОбласть("ИсчисленоЗаголовок");
	ИсчисленоШапка = Макет.ПолучитьОбласть("ИсчисленоШапка" + ?(НалогПоСтавке13ИсчисленПомесячно,"","2015"));
	ИсчисленоСтрока = Макет.ПолучитьОбласть("ИсчисленоСтрока");
	ЗачтеноАвансовЗаголовок = Макет.ПолучитьОбласть("ЗачтеноАвансовЗаголовок");
	ЗачтеноАвансовШапка = Макет.ПолучитьОбласть("ЗачтеноАвансовШапка");
	ЗачтеноАвансовСтрока = Макет.ПолучитьОбласть("ЗачтеноАвансовСтрока");
	УдержаноЗаголовок = Макет.ПолучитьОбласть("УдержаноЗаголовок");
	УдержаноШапка = Макет.ПолучитьОбласть("УдержаноШапка" + ?(НалогПоСтавке13ИсчисленПомесячно,"","2015"));
	УдержаноСтрока = Макет.ПолучитьОбласть("УдержаноСтрока" + ?(НалогПоСтавке13ИсчисленПомесячно,"","2015"));
	УдержаноБылоПоСтавке30 = Макет.ПолучитьОбласть("УдержаноБылоПоСтавке30");
	ВозвращеноЗаголовок = Макет.ПолучитьОбласть("ВозвращеноЗаголовок");
	ВозвращеноШапка = Макет.ПолучитьОбласть("ВозвращеноШапка");
	ВозвращеноСтрока = Макет.ПолучитьОбласть("ВозвращеноСтрока");
	ПереданоНаВзысканиеЗаголовок = Макет.ПолучитьОбласть("ПереданоНаВзысканиеЗаголовок");
	ПереданоНаВзысканиеШапка = Макет.ПолучитьОбласть("ПереданоНаВзысканиеШапка");
	ПереданоНаВзысканиеСтрока = Макет.ПолучитьОбласть("ПереданоНаВзысканиеСтрока");
	НалогПеречисленныйЗаголовок = Макет.ПолучитьОбласть("НалогПеречисленныйЗаголовок");
	НалогПеречисленныйШапка = Макет.ПолучитьОбласть("НалогПеречисленныйШапка");
	НалогПеречисленныйСтрока = Макет.ПолучитьОбласть("НалогПеречисленныйСтрока");
	НалогПеречисленныйБылоПоСтавке30 = Макет.ПолучитьОбласть("НалогПеречисленныйБылоПоСтавке30");
	ПредоставленныеВычетыИтогиЗаголовок = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиЗаголовок");
	ПредоставленныеВычетыИтогиИтог = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиИтог");
	ОбщиеСуммыИтогЗаголовок = Макет.ПолучитьОбласть("ОбщиеСуммыИтогЗаголовок");
	СтандартныеВычетыЗаголовок = Макет.ПолучитьОбласть("СтандартныеВычетыЗаголовок");
	СтандартныеВычетыШапка = Макет.ПолучитьОбласть("СтандартныеВычетыШапка");
	СтандартныеВычетыСтрока = Макет.ПолучитьОбласть("СтандартныеВычетыСтрока");
	СоциальныеВычетыЗаголовок = Макет.ПолучитьОбласть("СоциальныеВычетыЗаголовок");
	СоциальныеВычетыШапка = Макет.ПолучитьОбласть("СоциальныеВычетыШапка");
	СоциальныеВычетыСтрока = Макет.ПолучитьОбласть("СоциальныеВычетыСтрока");
	ИмущественныйВычетУведомление = Макет.ПолучитьОбласть("ИмущественныйВычетУведомление");
	Подписи = Макет.ПолучитьОбласть("Подписи");
	ОбластьПустыеВычеты = Макет.ПолучитьОбласть("ОбластьПустыеВычеты");
	
	ДатаПереходаНаКодыОКТМО = УчетНДФЛ.ДатаПереходаНаКодыОКТМО();
	ДатаИзмененияНалогообложенияДивидендов = УчетНДФЛ.ДатаИзмененияНалогообложенияДивидендов();
	Если Год < ДобавитьМесяц(ДатаПереходаНаКодыОКТМО, -12) Тогда
		Раздел4 = Макет.ПолучитьОбласть("Раздел4");
		ОбщиеСуммыИтогШапка = Макет.ПолучитьОбласть("ОбщиеСуммыИтогШапка");
		ОбщиеСуммыСтроки = Макет.ПолучитьОбласть("ОбщиеСуммыСтроки");
		ПредоставленныеВычетыИтогиШапка = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиШапка");
		ПредоставленныеВычетыИтогиСтрока = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиСтрока");
	ИначеЕсли Год < ДатаПереходаНаКодыОКТМО Тогда
		Раздел4 = Макет.ПолучитьОбласть("Раздел4_2013");
		ОбщиеСуммыИтогШапка = Макет.ПолучитьОбласть("ОбщиеСуммыИтогШапка2013");
		ОбщиеСуммыСтроки = Макет.ПолучитьОбласть("ОбщиеСуммыСтроки2013");
		ПредоставленныеВычетыИтогиШапка = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиШапка2013");
		ПредоставленныеВычетыИтогиСтрока = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиСтрока2013");
	Иначе 
		Раздел4 = Макет.ПолучитьОбласть("Раздел4_2014");
		ПредоставленныеВычетыИтогиШапка = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиШапка2014");
		ПредоставленныеВычетыИтогиСтрока = Макет.ПолучитьОбласть("ПредоставленныеВычетыИтогиСтрока2014");
		Если Год < ДатаИзмененияНалогообложенияДивидендов Тогда
			ОбщиеСуммыИтогШапка = Макет.ПолучитьОбласть("ОбщиеСуммыИтогШапка2014");
			ОбщиеСуммыСтроки = Макет.ПолучитьОбласть("ОбщиеСуммыСтроки2014");
		Иначе	
			ОбщиеСуммыИтогШапка = Макет.ПолучитьОбласть("ОбщиеСуммыИтогШапка2015");
			ОбщиеСуммыСтроки = Макет.ПолучитьОбласть("ОбщиеСуммыСтроки2015");
		КонецЕсли;
	КонецЕсли;
	
	Если Год < ДатаИзмененияНалогообложенияДивидендов Тогда
		РасчетыНалогаПоСтавке13Заголовок = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13Заголовок");
		НалогПоСтавке9Заголовок = Макет.ПолучитьОбласть("НалогПоСтавке9Заголовок");
	Иначе
		РасчетыНалогаПоСтавке13Заголовок = Макет.ПолучитьОбласть("РасчетыНалогаПоСтавке13Заголовок2015");
		НалогПоСтавке9Заголовок = Макет.ПолучитьОбласть("НалогПоСтавке9Заголовок2015");
	КонецЕсли;
	
	// -----------------------------------------------------------------------------                                 
	// РАЗДЕЛ 1 и 2
	
	ТаблицаПоНалогамИтоги = Новый ТаблицаЗначений();	
	ТаблицаПоНалогамИтоги.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ТаблицаПоНалогамИтоги.Колонки.Добавить("Ставка", Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
	ТаблицаПоНалогамИтоги.Колонки.Добавить("ОбщаяСуммаДохода", Новый ОписаниеТипов("Число"));
    ТаблицаПоНалогамИтоги.Колонки.Добавить("НалоговаяБаза", Новый ОписаниеТипов("Число"));

	НалоговыйПериод = Год(Год);
	
	СтруктураОтбора = Новый Структура("ФизическоеЛицо, Год, ГоловнаяОрганизация", ФизическоеЛицо, НалоговыйПериод, ГоловнаяОрганизация);
	
	ДанныеОФизЛицеОрганизации.Сбросить();
	
	Если ДанныеОФизЛицеОрганизации.НайтиСледующий(СтруктураОтбора) Тогда

		АдресПоКлассификатору = Ложь;
		Если Не ПустаяСтрока(ДанныеОФизЛицеОрганизации.АдресРегистрации) Тогда
			
			СтруктураАдреса = ЗарплатаКадры.СтруктураАдресаИзXML(
					ДанныеОФизЛицеОрганизации.АдресРегистрации, Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица);
			АдресПоКлассификатору = СтруктураАдреса.Свойство("Индекс");
			
		Иначе
			СтруктураАдреса = Неопределено;
		КонецЕсли;
		
		// Заполнение данных раздела 1.
		Раздел1.Параметры.ГодНП = "" + Формат(Год, "ЧГ=0; ДФ=yyyy");
		Раздел1.Параметры.ИНН = "" + ДанныеОФизЛицеОрганизации.Организация_ИНН +"\" + ДанныеОФизЛицеОрганизации.Организация_КПП;
		Раздел1.Параметры.КодНалоговогоОргана = ДанныеОФизЛицеОрганизации.Организация_КодНалоговогоОргана;
		Раздел1.Параметры.НазваниеОрганизации = ДанныеОФизЛицеОрганизации.Организация_Наименование;
		
		// Заполнение данных раздела 2.
		Раздел2.Параметры.ИНН = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_ИНН;
		Раздел2.Параметры.ФИО = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_ФИО;
		Раздел2.Параметры.ВидДокумента = ДанныеОФизЛицеОрганизации.ВидДокумента;
		Раздел2.Параметры.КодДокумента = ДанныеОФизЛицеОрганизации.КодДокументаУдЛичн;
		Раздел2.Параметры.СерияНомерДокумента = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_СерияНомерДокументаУдЛичн;
		Раздел2.Параметры.ДатаРождения = Формат(ДанныеОФизЛицеОрганизации.ФизическоеЛицо_ДатаРождения, "ДЛФ=D");
		Раздел2.Параметры.КодСтраны = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_КодСтраны;
		Раздел2.Параметры.СтранаПроживания = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_АдресКодСтраны;
		Если АдресПоКлассификатору Тогда
			Раздел2.Параметры.Индекс = СтруктураАдреса.Индекс;
			Раздел2.Параметры.Регион = АдресныйКлассификатор.КодРегионаПоНаименованию(СтруктураАдреса.Регион);
			Раздел2.Параметры.Район = СтруктураАдреса.Район;
			Раздел2.Параметры.Город = СтруктураАдреса.Город;
			Раздел2.Параметры.НаселенныйПункт =  СтруктураАдреса.НаселенныйПункт;
			Раздел2.Параметры.Улица = СтруктураАдреса.Улица;
			Раздел2.Параметры.Дом = СтруктураАдреса.Дом;
			Раздел2.Параметры.Корпус = СтруктураАдреса.Корпус;
			Раздел2.Параметры.Квартира = СтруктураАдреса.Квартира;
		КонецЕсли;	
		Раздел2.Параметры.СтранаПроживания = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_АдресКодСтраны;
		ПредставлениеАдресаПоСтруктуреПолей = "";
		Если ЗначениеЗаполнено(ДанныеОФизЛицеОрганизации.ФизическоеЛицо_ИностранныйАдрес) Тогда
			СтруктураПолейАдреса = ЗарплатаКадры.СтруктураАдресаИзXML(
					ДанныеОФизЛицеОрганизации.ФизическоеЛицо_ИностранныйАдрес, Справочники.ВидыКонтактнойИнформации.АдресЗаПределамиРФФизическиеЛица);
			Раздел2.Параметры.СтранаПроживания = ?(ЗначениеЗаполнено(СтруктураПолейАдреса.КодСтраны), СтруктураПолейАдреса.КодСтраны, Раздел2.Параметры.СтранаПроживания);
			ПредставлениеАдресаПоСтруктуреПолей = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(СтруктураПолейАдреса);
			Раздел2.Параметры.Адрес = ПредставлениеАдресаПоСтруктуреПолей;
		КонецЕсли; 
		
	КонецЕсли;
	
	ДокументРезультат.Вывести(Раздел1);
	
	ВыборкаСтатусов.Сбросить();
	
	СтатусНаКонецПериода = СтатусРезидент;
	Если ВыборкаСтатусов.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
		ВыборкаДетали = ВыборкаСтатусов.Выбрать();
		Пока ВыборкаДетали.Следующий() Цикл
			Раздел2.Параметры["Статус" + Строка(Месяц(ВыборкаДетали.Месяц))] = ВыборкаДетали.Статус;	
			СтатусНаКонецПериода = ВыборкаДетали.Статус;
		КонецЦикла;	
	КонецЕсли;	
	
	ДокументРезультат.Вывести(Раздел2);
	
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	
	Раздел3.Параметры.Доход = ДанныеОФизЛицеОрганизации.ФизическоеЛицо_ДоходыПредыдущегоМестаРаботы;
	ДокументРезультат.Вывести(Раздел3);

	ВыводимыеОбласти = Новый Массив;
	
	ТаблицаСтандартныхВычетов = Новый ТаблицаЗначений();
	ТаблицаСтандартныхВычетов.Колонки.Добавить("КодВычета");
	ТаблицаСтандартныхВычетов.Колонки.Добавить("СтандартныеВычетыРазмер");
	ТаблицаСтандартныхВычетов.Колонки.Добавить("СтандартныеВычетыПериодС");
	ТаблицаСтандартныхВычетов.Колонки.Добавить("СтандартныеВычетыПериодПо");
	ТаблицаСтандартныхВычетов.Колонки.Добавить("СтандартныеВычетыОснование");
	
	ПредыдущийВычет = Неопределено;
	ПредыдущийПериод = '00010101';
	ПредыдущийРазмер = Неопределено;
	ПредыдущаяСтрока = Неопределено;
	СтрокаТаблицы = Неопределено;
		
	ВыборкаСтандартныеВычеты.Сбросить();
		
	Если ВыборкаСтандартныеВычеты.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
	
		ВыборкаСтандартныеВычетыПоФизЛицу = ВыборкаСтандартныеВычеты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Если ВыборкаСтандартныеВычетыПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
			ВыборкаДетали = ВыборкаСтандартныеВычетыПоФизЛицу.Выбрать();
			
			ВыводимыеОбласти.Добавить(СтандартныеВычетыЗаголовок);
			ВыводимыеОбласти.Добавить(СтандартныеВычетыШапка);
			ВыводимыеОбласти.Добавить(СтандартныеВычетыСтрока);
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда 
	       		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;	
			
			ДокументРезультат.Вывести(СтандартныеВычетыШапка);
			
			Пока ВыборкаДетали.Следующий() Цикл
				Если ВыборкаДетали.КодВычета = ПредыдущийВычет И (КонецМесяца(ПредыдущийПериод) + 1) = ВыборкаДетали.Месяц И ПредыдущийРазмер = ВыборкаДетали.Размер Тогда
					ДатаЗавершения = КонецМесяца(ВыборкаДетали.Месяц);
				Иначе
					Если ПредыдущаяСтрока <> Неопределено Тогда 
						ПредыдущаяСтрока.СтандартныеВычетыПериодПо = ДатаЗавершения;
					КонецЕсли;	
					СтрокаТаблицы = ТаблицаСтандартныхВычетов.Добавить();
					СтрокаТаблицы.КодВычета = ВыборкаДетали.КодВычета;
					СтрокаТаблицы.СтандартныеВычетыПериодС = ВыборкаДетали.Месяц; 
				    СтрокаТаблицы.СтандартныеВычетыРазмер = ВыборкаДетали.Размер;
					СтрокаТаблицы.СтандартныеВычетыОснование = ВыборкаДетали.Основание;
					
					ДатаЗавершения = КонецМесяца(ВыборкаДетали.Месяц);
				КонецЕсли;	
				
				ПредыдущаяСтрока = СтрокаТаблицы;
				
				ПредыдущийВычет  = ВыборкаДетали.КодВычета;
				ПредыдущийПериод = ВыборкаДетали.Месяц;
				ПредыдущийРазмер = ВыборкаДетали.Размер;
			КонецЦикла;	
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.СтандартныеВычетыПериодПо = ДатаЗавершения;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	

	ЕстьПравоНаВычеты = Ложь;
	Для Каждого Вычет Из ТаблицаСтандартныхВычетов Цикл
		ВыводимыеОбласти.Очистить();
		СтандартныеВычетыСтрока.Параметры.КодВычета = Вычет.КодВычета;
		СтандартныеВычетыСтрока.Параметры.СтандартныеВычетыРазмер = Вычет.СтандартныеВычетыРазмер;
		СтандартныеВычетыСтрока.Параметры.СтандартныеВычетыПериодС = Формат(Вычет.СтандартныеВычетыПериодС, "ДФ=дд.ММ.гггг");
		СтандартныеВычетыСтрока.Параметры.СтандартныеВычетыПериодПо = Формат(Вычет.СтандартныеВычетыПериодПо, "ДФ=дд.ММ.гггг");
		СтандартныеВычетыСтрока.Параметры.СтандартныеВычетыОснование = Вычет.СтандартныеВычетыОснование;
		
		ВыводимыеОбласти.Добавить(СтандартныеВычетыСтрока);
		
		Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
       		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			ДокументРезультат.Вывести(СтандартныеВычетыШапка);
		КонецЕсли;
		
		ДокументРезультат.Вывести(СтандартныеВычетыСтрока);
		ЕстьПравоНаВычеты = Истина;
	КонецЦикла;	
	
	ВыборкаСоциальныеВычеты.Сбросить();
	
	ВыводимыеОбласти.Очистить();
	Если ВыборкаСоциальныеВычеты.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
		
		ВыборкаПоФизЛицу = ВыборкаСоциальныеВычеты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
			ВыборкаДетали = ВыборкаПоФизЛицу.Выбрать();
			
			ВыводимыеОбласти.Добавить(СоциальныеВычетыЗаголовок);
			ВыводимыеОбласти.Добавить(СоциальныеВычетыШапка);
			ВыводимыеОбласти.Добавить(СоциальныеВычетыСтрока);
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
       			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;	
			
			ДокументРезультат.Вывести(СоциальныеВычетыЗаголовок);
			ДокументРезультат.Вывести(СоциальныеВычетыШапка);
			
			Пока ВыборкаДетали.Следующий() Цикл
				ВыводимыеОбласти.Очистить();
				СоциальныеВычетыСтрока.Параметры.СоцВычетыКод = ВыборкаДетали.КодВычетаКод;
				СоциальныеВычетыСтрока.Параметры.СоцВычетыРазмер = ВыборкаДетали.Размер;
				СоциальныеВычетыСтрока.Параметры.СоцВычетыДата = Формат(ВыборкаДетали.Дата, "ДФ=дд.ММ.гггг");
				
				ВыводимыеОбласти.Добавить(СоциальныеВычетыСтрока);
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
		       		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					ДокументРезультат.Вывести(СоциальныеВычетыШапка);
				КонецЕсли;
				
				ДокументРезультат.Вывести(СоциальныеВычетыСтрока);
				ЕстьПравоНаВычеты = Истина;
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;	
	
	Если Не ЕстьПравоНаВычеты Тогда
		ДокументРезультат.Вывести(ОбластьПустыеВычеты);
	КонецЕсли;	
	
	Если ВыборкаУведомления.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
		
		ВыводимыеОбласти.Очистить();
		
		ВыводимыеОбласти.Добавить(ИмущественныйВычетУведомление);
		
		Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
       		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ВыборкаПоФизЛицу = ВыборкаУведомления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
			ВыборкаДетали = ВыборкаПоФизЛицу.Выбрать();
			
			Если ВыборкаДетали.Следующий() Тогда
				ИмущественныйВычетУведомление.Параметры.Заполнить(ВыборкаДетали);
				ИмущественныйВычетУведомление.Параметры.ДатаУведомления = Формат(ВыборкаДетали.ДатаУведомления, "ДФ=дд.ММ.гггг");
				ДокументРезультат.Вывести(ИмущественныйВычетУведомление);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	ВыборкаДоходовПоМесяцамПоРазделам.Сбросить();
	
	ЕстьДанныеПоДоходам = Ложь;
	Если ВыборкаДоходовПоМесяцамПоРазделам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
		ВыборкаФизЛиц  = ВыборкаДоходовПоМесяцамПоРазделам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
			
			ЕстьДанныеПоДоходам = Истина;
			ТаблицаПоНалогамИтоги.Очистить();

			ДанныеОНалогахНачисленныхПоРегистрации = Новый Соответствие;
			ДанныеОбАвансахЗачтенныхПоРегистрации = Новый Соответствие;
			ДанныеОНалогахУдержанныхПоРегистрации = Новый Соответствие;
			ДанныеОНалогахВозвращенныхПоРегистрации = Новый Соответствие;
			ДанныеОНалогахПереданныхНаВзысканиеПоРегистрации = Новый Соответствие;
			ДанныеОНалогахПеречисленныхПоРегистрации = Новый Соответствие;
			
			ВыборкаРегистраций = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаРегистраций.Следующий() Цикл
				
				СтрокаРегистрации = ТаблицаРегистрацийВНО.Найти(ВыборкаРегистраций.РегистрацияВНалоговомОргане,"РегистрацияВНалоговомОргане");
				Если СтрокаРегистрации <> Неопределено Тогда
					Раздел4.Параметры.Заполнить(СтрокаРегистрации);
				КонецЕсли;
				
				ВыводимыеОбласти.Очистить();
				
				ВыводимыеОбласти.Добавить(Раздел4);
				ВыводимыеОбласти.Добавить(РасчетыНалогаПоСтавке13Заголовок);				
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ДокументРезультат.Вывести(Раздел4);
				
				ВыборкаПоРазделам = ВыборкаРегистраций.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				СуммаДоходаДоРезидентстваНарастающийИтог = 0;
				ОблагаемаяСуммаДоходаДоРезидентстваНарастающийИтог = 0;
				
				Если ВыборкаПоРазделам.НайтиСледующий("Раздел4_Ставка13", "Раздел") Тогда
					
					ВычетыПоСтавке13 = Новый Соответствие;
					ВыборкаВычетовПоСтавке13.Сбросить();
					Если ВыборкаВычетовПоСтавке13.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
						ВыборкаВычетовПоСтавке13ПоФизЛицам = ВыборкаВычетовПоСтавке13.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаВычетовПоСтавке13ПоФизЛицам.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
							ВыборкаВычетовПоСтавке13Регистрация = ВыборкаВычетовПоСтавке13ПоФизЛицам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Если ВыборкаВычетовПоСтавке13Регистрация.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
								ВыборкаДетали = ВыборкаВычетовПоСтавке13Регистрация.Выбрать();
								СуммаВычетаИтог = 0;
								Пока ВыборкаДетали.Следующий() Цикл 
									СуммаВычетаИтог = ВыборкаДетали.СуммаВычета + СуммаВычетаИтог;
									ВычетыПоСтавке13.Вставить(ВыборкаДетали.Месяц, СуммаВычетаИтог);		
								КонецЦикла;	
							КонецЕсли;
						КонецЕсли;	
					КонецЕсли;	
					
					ДокументРезультат.Вывести(РасчетыНалогаПоСтавке13Заголовок);
					ДокументРезультат.Вывести(РасчетыНалогаПоСтавке13Шапка);
					
					ВыборкаПоМесяцамНалоговогоПериода = ВыборкаПоРазделам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					СуммаДоходаНарастающийИтог = 0;
					СуммаВычетовНарастающийИтог = 0;
					ОблагаемаяСуммаДоходаНарастающийИтог = 0;
					НалоговаяБаза = 0;
					СуммаВычетовСНачалаПериода = 0;
					СтавкаНалога = 0.13;
					
					Пока ВыборкаПоМесяцамНалоговогоПериода.Следующий() Цикл
						ВыводимыеОбласти.Очистить();
						
						ОбластьНалогПоСтавке13Группа = Новый ТабличныйДокумент;
						Если ЗначениеЗаполнено(ВыборкаПоМесяцамНалоговогоПериода.СуммаДоходаДоРезидентства) Тогда
							ЗаполнитьЗначенияСвойств(РасчетыНалогаПоСтавке13БылоПоСтавке30.Параметры,ВыборкаПоМесяцамНалоговогоПериода);
							РасчетыНалогаПоСтавке13БылоПоСтавке30.Параметры.НалоговаяБазаДоРезидентства = НалоговаяБаза + ВыборкаПоМесяцамНалоговогоПериода.ОблагаемаяСуммаДоходаДоРезидентства;
							РасчетыНалогаПоСтавке13БылоПоСтавке30.Параметры.НалогНачисленоДоРезидентства = Окр((НалоговаяБаза + ВыборкаПоМесяцамНалоговогоПериода.ОблагаемаяСуммаДоходаДоРезидентства) * СтавкаНалога);
							ОбластьНалогПоСтавке13Группа.Вывести(РасчетыНалогаПоСтавке13БылоПоСтавке30);
							СуммаДоходаДоРезидентстваНарастающийИтог = СуммаДоходаДоРезидентстваНарастающийИтог + ВыборкаПоМесяцамНалоговогоПериода.СуммаДоходаДоРезидентства;
						КонецЕсли;
						
						СуммаДоходаНарастающийИтог = СуммаДоходаНарастающийИтог + ВыборкаПоМесяцамНалоговогоПериода.СуммаДохода;
						СуммаВычетовНарастающийИтог = СуммаВычетовНарастающийИтог + ВыборкаПоМесяцамНалоговогоПериода.СуммаВычета;
						ОблагаемаяСуммаДоходаНарастающийИтог = ОблагаемаяСуммаДоходаНарастающийИтог + ВыборкаПоМесяцамНалоговогоПериода.ОблагаемаяСуммаДохода;
						ОблагаемаяСуммаДоходаДоРезидентстваНарастающийИтог = ОблагаемаяСуммаДоходаДоРезидентстваНарастающийИтог + ВыборкаПоМесяцамНалоговогоПериода.ОблагаемаяСуммаДоходаДоРезидентства;
						
						СуммаВычетовСНачалаПериода = ?(ВычетыПоСтавке13.Получить(ВыборкаПоМесяцамНалоговогоПериода.МесяцНалоговогоПериода) = Неопределено, СуммаВычетовСНачалаПериода, ВычетыПоСтавке13.Получить(ВыборкаПоМесяцамНалоговогоПериода.МесяцНалоговогоПериода));
						
						НалоговаяБаза = ОблагаемаяСуммаДоходаНарастающийИтог -  СуммаВычетовСНачалаПериода + ОблагаемаяСуммаДоходаДоРезидентстваНарастающийИтог;
						
						РасчетыНалогаПоСтавке13МесяцГруппа.Параметры.Месяц = Формат(ВыборкаПоМесяцамНалоговогоПериода.МесяцНалоговогоПериода, "ДФ=ММММ");
						
						ОбластьНалогПоСтавке13Группа.Вывести(РасчетыНалогаПоСтавке13МесяцГруппа);
						
						ВыводимыеОбласти.Добавить(ОбластьНалогПоСтавке13Группа);
						
						ВыборкаПоДатамПолученияДохода = ВыборкаПоМесяцамНалоговогоПериода.Выбрать();
						
						Пока ВыборкаПоДатамПолученияДохода.Следующий() Цикл
							РасчетыНалогаПоСтавке13Строка.Параметры.Заполнить(ВыборкаПоДатамПолученияДохода);
							РасчетыНалогаПоСтавке13Строка.Параметры.ДатаНачисления = Формат(ВыборкаПоДатамПолученияДохода.ДатаНачисления, "ДФ=дд.ММ.гггг");
							РасчетыНалогаПоСтавке13Строка.Параметры.МесяцНачисления = Формат(ВыборкаПоДатамПолученияДохода.МесяцНачисления, "ДФ=ММММ");
							РасчетыНалогаПоСтавке13Строка.Параметры.ДатаВыплаты = Формат(ВыборкаПоДатамПолученияДохода.ДатаВыплаты, "ДФ=дд.ММ.гггг");
							РасчетыНалогаПоСтавке13Строка.Параметры.ДатаПолученияДохода = Формат(ВыборкаПоДатамПолученияДохода.ДатаПолученияДохода, "ДФ=дд.ММ.гггг");
							
							ОбластьДоходыПоСтавке13 = Новый ТабличныйДокумент;
							ОбластьДоходыПоСтавке13.Вывести(РасчетыНалогаПоСтавке13Строка);
							
							ВыводимыеОбласти.Добавить(ОбластьДоходыПоСтавке13);		
							
						КонецЦикла;	
						
						РасчетыНалогаПоСтавке13Итог.Параметры.Заполнить(ВыборкаПоМесяцамНалоговогоПериода);
						РасчетыНалогаПоСтавке13Итог.Параметры.СуммаДоходаНарастающийИтог = СуммаДоходаНарастающийИтог;
						РасчетыНалогаПоСтавке13Итог.Параметры.СуммаВычетаНарастающийИтог = СуммаВычетовНарастающийИтог;
						РасчетыНалогаПоСтавке13Итог.Параметры.ОблагаемаяСуммаДоходаИтог = ОблагаемаяСуммаДоходаНарастающийИтог;
						РасчетыНалогаПоСтавке13Итог.Параметры.НалоговаяБаза = НалоговаяБаза;
						РасчетыНалогаПоСтавке13Итог.Параметры.НалогНачислено = Окр(НалоговаяБаза * СтавкаНалога);
						
						ВыводимыеОбласти.Добавить(РасчетыНалогаПоСтавке13Итог);
						
						Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
							ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
							ДокументРезультат.Вывести(РасчетыНалогаПоСтавке13Шапка);
						КонецЕсли;
						
						Для Каждого Область Из ВыводимыеОбласти Цикл
							ДокументРезультат.Вывести(Область);
						КонецЦикла;	
						
					КонецЦикла;
					
					СтрокаТаблицыИтогов = ТаблицаПоНалогамИтоги.Добавить();
					СтрокаТаблицыИтогов.РегистрацияВНалоговомОргане = ВыборкаРегистраций.РегистрацияВНалоговомОргане;
					СтрокаТаблицыИтогов.Ставка = Перечисления.НДФЛСтавки.Ставка13;
					СтрокаТаблицыИтогов.ОбщаяСуммаДохода = ВыборкаПоРазделам.СуммаДохода + СуммаДоходаДоРезидентстваНарастающийИтог;
					СтрокаТаблицыИтогов.НалоговаяБаза = НалоговаяБаза;
					
					ВыборкаВычетыПоМесяцамПоВидам.Сбросить();
					
					ВыводимыеОбласти.Очистить();
					Если ВыборкаВычетыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
						ВыборкаВычетыПоФизЛицу = ВыборкаВычетыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						
						Если ВыборкаВычетыПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
							ВыборкаВычетыПоРегистрации = ВыборкаВычетыПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Если ВыборкаВычетыПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда 
									
								ВыборкаВычетыПоМесяцам = ВыборкаВычетыПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								
								ВыводилиЗаголовок = Ложь;
								
								ДоходИтог = 0;
								СуммаВычетаНарастающийИтог = 0;
								Пока ВыборкаВычетыПоМесяцам.Следующий() Цикл
									ВыводимыеОбласти.Очистить();
									
									Если Не ВыводилиЗаголовок Тогда
										ВыводимыеОбласти.Добавить(ПредоставленныеВычетыЗаголовок);
										ВыводимыеОбласти.Добавить(ПредоставленныеВычетыШапка);
									КонецЕсли;
									
									ДоходИтог = ДоходИтог + ВыборкаВычетыПоМесяцам.СуммаДохода;
									
									ПредоставленныеВычетыШапкаМесяцГруппа.Параметры.Месяц = Формат(ВыборкаВычетыПоМесяцам.Месяц, "ДФ=ММММ");	
									ПредоставленныеВычетыШапкаМесяцГруппа.Параметры.Доход = Формат(ДоходИтог, "ЧДЦ=2");
									
									ВыводимыеОбласти.Добавить(ПредоставленныеВычетыШапкаМесяцГруппа);
									
									ВыборкаВычетыДетали = ВыборкаВычетыПоМесяцам.Выбрать();
									
									Пока ВыборкаВычетыДетали.Следующий() Цикл
										
										СуммаВычетаНарастающийИтог = СуммаВычетаНарастающийИтог + ВыборкаВычетыДетали.СуммаВычета;
										ПредоставленныеВычетыСтрока.Параметры.Заполнить(ВыборкаВычетыДетали);
										
										ОбластьПредоставленныеВычеты = Новый ТабличныйДокумент;
										ОбластьПредоставленныеВычеты.Вывести(ПредоставленныеВычетыСтрока);
										ВыводимыеОбласти.Добавить(ОбластьПредоставленныеВычеты);	
										
									КонецЦикла;	
									
									ПредоставленныеВычетыИтог.Параметры.СуммаВычета = ВыборкаВычетыПоМесяцам.СуммаВычета;
									ПредоставленныеВычетыИтог.Параметры.СуммаВычетаНарастающийИтог = СуммаВычетаНарастающийИтог;
									
									ВыводимыеОбласти.Добавить(ПредоставленныеВычетыИтог);
									
									Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
										ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
										Если ВыводилиЗаголовок Тогда
											ДокументРезультат.Вывести(ПредоставленныеВычетыШапка);
										КонецЕсли;
									КонецЕсли;
									
									Для Каждого Область Из ВыводимыеОбласти Цикл
										ДокументРезультат.Вывести(Область);
									КонецЦикла;
									
									ВыводилиЗаголовок = Истина;
									
								КонецЦикла;	
								
							КонецЕсли;
						КонецЕсли;		
					КонецЕсли;	
					
				КонецЕсли;	
				
				ВыборкаПоРазделам.Сбросить();
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаПоРазделам.НайтиСледующий("Раздел4_Ставка30", "Раздел") Тогда
					
					ВыводимыеОбласти.Добавить(НалогПоСтавке30Заголовок);
					ВыводимыеОбласти.Добавить(НалогПоСтавке30Шапка);
					ВыводимыеОбласти.Добавить(НалогПоСтавке30Строка);
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					ДокументРезультат.Вывести(НалогПоСтавке30Заголовок);
					ДокументРезультат.Вывести(НалогПоСтавке30Шапка);
					
					ВыборкаПоМесяцамНалоговогоПериода = ВыборкаПоРазделам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					СуммаДоходаИтог = 0;
					СуммаВычетовИтог = 0;
					ОблагаемаяСуммаДоходаИтог = 0;
					НалогНачисленоИтог = 0;
					
					Пока ВыборкаПоМесяцамНалоговогоПериода.Следующий() Цикл
						
						ВыборкаПоДатамПолученияДохода = ВыборкаПоМесяцамНалоговогоПериода.Выбрать();
						
						Пока ВыборкаПоДатамПолученияДохода.Следующий() Цикл
							ВыводимыеОбласти.Очистить();
							
							НалогПоСтавке30Строка.Параметры.Заполнить(ВыборкаПоДатамПолученияДохода);
							НалогПоСтавке30Строка.Параметры.ДатаПолученияДохода = Формат(ВыборкаПоДатамПолученияДохода.ДатаПолученияДохода, "ДФ=дд.ММ.гггг");
							НалогПоСтавке30Строка.Параметры.ДатаНачисления = Формат(ВыборкаПоДатамПолученияДохода.ДатаНачисления, "ДФ=дд.ММ.гггг");
							НалогПоСтавке30Строка.Параметры.МесяцНачисления = Формат(ВыборкаПоДатамПолученияДохода.МесяцНачисления, "ДФ=ММММ");
							НалогПоСтавке30Строка.Параметры.ДатаВыплаты = Формат(ВыборкаПоДатамПолученияДохода.ДатаВыплаты, "ДФ=дд.ММ.гггг");
							
							НалогНачислено = Окр(ВыборкаПоДатамПолученияДохода.НалогНачислено); 
							НалогПоСтавке30Строка.Параметры.НалогНачислено = НалогНачислено;
							
							ВыводимыеОбласти.Добавить(НалогПоСтавке30Строка);
							
							Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
								ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
								ВыводимыеОбласти.Добавить(НалогПоСтавке30Шапка);
							КонецЕсли;
							
							ДокументРезультат.Вывести(НалогПоСтавке30Строка);	
							
							СуммаДоходаИтог = СуммаДоходаИтог + ВыборкаПоДатамПолученияДохода.СуммаДохода;
							СуммаВычетовИтог = СуммаВычетовИтог + ВыборкаПоДатамПолученияДохода.СуммаВычета;
							ОблагаемаяСуммаДоходаИтог = ОблагаемаяСуммаДоходаИтог + ВыборкаПоДатамПолученияДохода.ОблагаемаяСуммаДохода;
							НалогНачисленоИтог = НалогНачисленоИтог + НалогНачислено;
							
						КонецЦикла;	
					КонецЦикла;
					НалогПоСтавке30Итог.Параметры.СуммаДохода = СуммаДоходаИтог;
					НалогПоСтавке30Итог.Параметры.СуммаВычета = СуммаВычетовИтог;
					НалогПоСтавке30Итог.Параметры.ОблагаемаяСуммаДохода = ОблагаемаяСуммаДоходаИтог;
					НалогПоСтавке30Итог.Параметры.НалогНачислено = НалогНачисленоИтог;
					
					ВыводимыеОбласти.Очистить();
					ВыводимыеОбласти.Добавить(НалогПоСтавке30Итог);
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
						ВыводимыеОбласти.Добавить(НалогПоСтавке30Шапка);
					КонецЕсли;
					
					ДокументРезультат.Вывести(НалогПоСтавке30Итог);
					
					Если СуммаДоходаИтог > СуммаДоходаДоРезидентстваНарастающийИтог Тогда
						СтрокаТаблицыИтогов = ТаблицаПоНалогамИтоги.Добавить();
						СтрокаТаблицыИтогов.РегистрацияВНалоговомОргане = ВыборкаРегистраций.РегистрацияВНалоговомОргане;
						СтрокаТаблицыИтогов.Ставка = Перечисления.НДФЛСтавки.Ставка30;
						СтрокаТаблицыИтогов.ОбщаяСуммаДохода = СуммаДоходаИтог - СуммаДоходаДоРезидентстваНарастающийИтог;
						СтрокаТаблицыИтогов.НалоговаяБаза = ОблагаемаяСуммаДоходаИтог - Мин(ОблагаемаяСуммаДоходаИтог,ОблагаемаяСуммаДоходаДоРезидентстваНарастающийИтог);
					КонецЕсли;
				КонецЕсли;	
				
				ВыборкаПоРазделам.Сбросить();
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаПоРазделам.НайтиСледующий("Раздел4_Ставка35_15_9", "Раздел") Тогда
					ВыводимыеОбласти.Добавить(НалогПоСтавке9Заголовок);
					ВыводимыеОбласти.Добавить(НалогПоСтавке9Шапка);
					ВыводимыеОбласти.Добавить(НалогПоСтавке9Строка);
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					ДокументРезультат.Вывести(НалогПоСтавке9Заголовок);
					ДокументРезультат.Вывести(НалогПоСтавке9Шапка);
					
					ВыборкаПоМесяцамНалоговогоПериода = ВыборкаПоРазделам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					СуммаДоходаИтог = 0;
					СуммаВычетовИтог = 0;
					ОблагаемаяСуммаДоходаИтог = 0;
					НалогНачисленоИтог = 0;
					
					Пока ВыборкаПоМесяцамНалоговогоПериода.Следующий() Цикл
						
						ВыборкаПоДатамПолученияДохода = ВыборкаПоМесяцамНалоговогоПериода.Выбрать();
						
						Пока ВыборкаПоДатамПолученияДохода.Следующий() Цикл
							ВыводимыеОбласти.Очистить();
							НалогПоСтавке9Строка.Параметры.Заполнить(ВыборкаПоДатамПолученияДохода);
							НалогПоСтавке9Строка.Параметры.ДатаНачисления = Формат(ВыборкаПоДатамПолученияДохода.ДатаНачисления, "ДФ=дд.ММ.гггг");
							НалогПоСтавке9Строка.Параметры.ДатаВыплаты = Формат(ВыборкаПоДатамПолученияДохода.ДатаВыплаты, "ДФ=дд.ММ.гггг");
							
							НалогНачислено = Окр(ВыборкаПоДатамПолученияДохода.НалогНачислено); 
							НалогПоСтавке9Строка.Параметры.НалогНачислено = НалогНачислено;
							
							ВыводимыеОбласти.Добавить(НалогПоСтавке9Строка);
							Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
								ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
								ВыводимыеОбласти.Добавить(НалогПоСтавке9Шапка);
							КонецЕсли;
							
							ДокументРезультат.Вывести(НалогПоСтавке9Строка);	
							
							СуммаДоходаИтог = СуммаДоходаИтог + ВыборкаПоДатамПолученияДохода.СуммаДохода;
							СуммаВычетовИтог = СуммаВычетовИтог + ВыборкаПоДатамПолученияДохода.СуммаВычета;
							ОблагаемаяСуммаДоходаИтог = ОблагаемаяСуммаДоходаИтог + ВыборкаПоДатамПолученияДохода.ОблагаемаяСуммаДохода;
							НалогНачисленоИтог = НалогНачисленоИтог + НалогНачислено;
							
							СтрокаТаблицыИтогов = ТаблицаПоНалогамИтоги.Добавить();
							СтрокаТаблицыИтогов.РегистрацияВНалоговомОргане = ВыборкаРегистраций.РегистрацияВНалоговомОргане;
							СтрокаТаблицыИтогов.Ставка = ВыборкаПоДатамПолученияДохода.Ставка;
							СтрокаТаблицыИтогов.ОбщаяСуммаДохода = ВыборкаПоДатамПолученияДохода.СуммаДохода;
							СтрокаТаблицыИтогов.НалоговаяБаза = ВыборкаПоДатамПолученияДохода.ОблагаемаяСуммаДохода;
							
						КонецЦикла;	
					КонецЦикла;
					НалогПоСтавке9Итог.Параметры.СуммаДохода = СуммаДоходаИтог;
					НалогПоСтавке9Итог.Параметры.СуммаВычета = СуммаВычетовИтог;
					НалогПоСтавке9Итог.Параметры.ОблагаемаяСуммаДохода = ОблагаемаяСуммаДоходаИтог;
					НалогПоСтавке9Итог.Параметры.НалогНачислено = НалогНачисленоИтог;
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, НалогПоСтавке9Итог) Тогда
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
						ВыводимыеОбласти.Добавить(НалогПоСтавке9Шапка);
					КонецЕсли;
					
					ДокументРезультат.Вывести(НалогПоСтавке9Итог);
					
				КонецЕсли;	
				
				ВыборкаНалогиНачисленныеИтоги.Сбросить();
				
				ДанныеОНалогахНачисленныхПоСтавкам = Новый Соответствие;
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаНалогиНачисленныеИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
					ВыборкаНалогиНачисленныеИтогиПоФизЛицу = ВыборкаНалогиНачисленныеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если ВыборкаНалогиНачисленныеИтогиПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
						ВыборкаНалогиНачисленныеИтогиПоРегистрации = ВыборкаНалогиНачисленныеИтогиПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаНалогиНачисленныеИтогиПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
							ВыборкаНалогиНачисленныеДетали = ВыборкаНалогиНачисленныеИтогиПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							
							ВыводимыеОбласти.Добавить(ИсчисленоЗаголовок);
							ВыводимыеОбласти.Добавить(ИсчисленоШапка);
							ВыводимыеОбласти.Добавить(ИсчисленоСтрока);
							
							Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
								ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
							КонецЕсли;
							
							ДокументРезультат.Вывести(ИсчисленоЗаголовок);
							ДокументРезультат.Вывести(ИсчисленоШапка);
							Пока ВыборкаНалогиНачисленныеДетали.Следующий() Цикл
								
								ИсчисленоСтрока.Параметры.Месяц = Формат(ВыборкаНалогиНачисленныеДетали.Месяц, ?(НалогПоСтавке13ИсчисленПомесячно,"ДФ=ММММ","ДФ=дд.ММ.гггг"));
								ИсчисленоСтрока.Параметры.Дата = Формат(ВыборкаНалогиНачисленныеДетали.Дата, "ДФ=дд.ММ.гггг");
								ИсчисленоСтрока.Параметры.Ставка = ВыборкаНалогиНачисленныеДетали.Ставка;
								ИсчисленоСтрока.Параметры.Сумма =  ВыборкаНалогиНачисленныеДетали.НалогНачислено;
								
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ИсчисленоСтрока) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
									ДокументРезультат.Вывести(ИсчисленоШапка);
								КонецЕсли;
								
								ДокументРезультат.Вывести(ИсчисленоСтрока);
								
								Если ВыборкаНалогиНачисленныеДетали.Ставка = Перечисления.НДФЛСтавки.Ставка30 И ВыборкаНалогиНачисленныеДетали.СтавкаПоИтогамГода = Перечисления.НДФЛСтавки.Ставка13 Тогда
									СтавкаИтогов = Перечисления.НДФЛСтавки.Ставка13
								Иначе
									СтавкаИтогов = ВыборкаНалогиНачисленныеДетали.Ставка
								КонецЕсли;
								Если ДанныеОНалогахНачисленныхПоСтавкам.Получить(СтавкаИтогов) = Неопределено Тогда
									ДанныеОНалогахНачисленныхПоСтавкам.Вставить(СтавкаИтогов, ВыборкаНалогиНачисленныеДетали.НалогНачислено);
								Иначе
									ДанныеОНалогахНачисленныхПоСтавкам[СтавкаИтогов] = ДанныеОНалогахНачисленныхПоСтавкам[СтавкаИтогов] + ВыборкаНалогиНачисленныеДетали.НалогНачислено;
								КонецЕсли;	
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;	
				
				ДанныеОНалогахНачисленныхПоРегистрации.Вставить(ВыборкаРегистраций.РегистрацияВНалоговомОргане, ДанныеОНалогахНачисленныхПоСтавкам); 
				
				ВыборкаАвансыЗачтенныеИтоги.Сбросить();
				
				ДанныеОбАвансахЗачтенныхПоСтавкам = Новый Соответствие;
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаАвансыЗачтенныеИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
					ВыборкаАвансыЗачтенныеИтогиПоФизЛицу = ВыборкаАвансыЗачтенныеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если ВыборкаАвансыЗачтенныеИтогиПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
						ВыборкаАвансыЗачтенныеИтогиПоРегистрации = ВыборкаАвансыЗачтенныеИтогиПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаАвансыЗачтенныеИтогиПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
							ВыборкаАвансыЗачтенныеДетали = ВыборкаАвансыЗачтенныеИтогиПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							
							ВыводимыеОбласти.Добавить(ЗачтеноАвансовЗаголовок);
							ВыводимыеОбласти.Добавить(ЗачтеноАвансовШапка);
							ВыводимыеОбласти.Добавить(ЗачтеноАвансовСтрока);
							
							Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
								ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
							КонецЕсли;
							
							ДокументРезультат.Вывести(ЗачтеноАвансовЗаголовок);
							ДокументРезультат.Вывести(ЗачтеноАвансовШапка);
							Пока ВыборкаАвансыЗачтенныеДетали.Следующий() Цикл
								
								ЗачтеноАвансовСтрока.Параметры.Месяц = Формат(ВыборкаАвансыЗачтенныеДетали.Месяц, "ДФ=ММММ");
								ЗачтеноАвансовСтрока.Параметры.Дата = Формат(ВыборкаАвансыЗачтенныеДетали.Дата, "ДФ=дд.ММ.гггг");
								ЗачтеноАвансовСтрока.Параметры.Ставка = ВыборкаАвансыЗачтенныеДетали.Ставка;
								ЗачтеноАвансовСтрока.Параметры.Сумма =  ВыборкаАвансыЗачтенныеДетали.ЗачтеноАвансов;
								
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ЗачтеноАвансовСтрока) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
									ДокументРезультат.Вывести(ЗачтеноАвансовШапка);
								КонецЕсли;
								
								ДокументРезультат.Вывести(ЗачтеноАвансовСтрока);
								
								Если ВыборкаАвансыЗачтенныеДетали.Ставка = Перечисления.НДФЛСтавки.Ставка30 И ВыборкаАвансыЗачтенныеДетали.СтавкаПоИтогамГода = Перечисления.НДФЛСтавки.Ставка13 Тогда
									СтавкаИтогов = Перечисления.НДФЛСтавки.Ставка13
								Иначе
									СтавкаИтогов = ВыборкаАвансыЗачтенныеДетали.Ставка
								КонецЕсли;
								Если ДанныеОбАвансахЗачтенныхПоСтавкам.Получить(СтавкаИтогов) = Неопределено Тогда
									ДанныеОбАвансахЗачтенныхПоСтавкам.Вставить(СтавкаИтогов, ВыборкаАвансыЗачтенныеДетали.ЗачтеноАвансов);
								Иначе
									ДанныеОбАвансахЗачтенныхПоСтавкам[СтавкаИтогов] = ДанныеОбАвансахЗачтенныхПоСтавкам[СтавкаИтогов] + ВыборкаАвансыЗачтенныеДетали.ЗачтеноАвансов;
								КонецЕсли;	
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;	
				
				ДанныеОбАвансахЗачтенныхПоРегистрации.Вставить(ВыборкаРегистраций.РегистрацияВНалоговомОргане, ДанныеОбАвансахЗачтенныхПоСтавкам); 
				
				ВыборкаНалогиУдержанныеИтоги.Сбросить();
				
				ДанныеОНалогахУдержанныхПоСтавкам = Новый Соответствие;
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаНалогиУдержанныеИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
					ВыборкаНалогиУдержанныеИтогиПоФизЛицу = ВыборкаНалогиУдержанныеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если ВыборкаНалогиУдержанныеИтогиПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
						ВыборкаНалогиУдержанныеИтогиПоРегистрации = ВыборкаНалогиУдержанныеИтогиПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаНалогиУдержанныеИтогиПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
							
							ВыборкаНалогиУдержанныеДетали = ВыборкаНалогиУдержанныеИтогиПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							
							ВыводимыеОбласти.Добавить(УдержаноЗаголовок);
							ВыводимыеОбласти.Добавить(УдержаноШапка);
							ВыводимыеОбласти.Добавить(УдержаноСтрока);
							
							Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
								ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
							КонецЕсли;
							
							ДокументРезультат.Вывести(УдержаноЗаголовок);
							ДокументРезультат.Вывести(УдержаноШапка);
							КЗачетуПоСтавке13 = 0;
							Пока ВыборкаНалогиУдержанныеДетали.Следующий() Цикл
								
								Если ВыборкаНалогиУдержанныеДетали.Ставка = Перечисления.НДФЛСтавки.Ставка13 И ЗначениеЗаполнено(КЗачетуПоСтавке13) Тогда
									УдержаноБылоПоСтавке30.Параметры.Сумма = КЗачетуПоСтавке13;
									Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, УдержаноБылоПоСтавке30) Тогда
										ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
										ДокументРезультат.Вывести(УдержаноШапка);
									КонецЕсли;
									ДокументРезультат.Вывести(УдержаноБылоПоСтавке30);
									КЗачетуПоСтавке13 = 0;
								КонецЕсли;
								
								УдержаноСтрока.Параметры.Месяц = Формат(ВыборкаНалогиУдержанныеДетали.Месяц, ?(НалогПоСтавке13ИсчисленПомесячно,"ДФ=ММММ","ДФ=дд.ММ.гггг"));
								УдержаноСтрока.Параметры.Дата = Формат(ВыборкаНалогиУдержанныеДетали.Дата, "ДФ=дд.ММ.гггг");
								УдержаноСтрока.Параметры.Ставка = ВыборкаНалогиУдержанныеДетали.Ставка;
								УдержаноСтрока.Параметры.Сумма =  ВыборкаНалогиУдержанныеДетали.НалогУдержано;
								Если Не НалогПоСтавке13ИсчисленПомесячно Тогда
									УдержаноСтрока.Параметры.СрокПеречисления =  ВыборкаНалогиУдержанныеДетали.СрокПеречисления;
								КонецЕсли;
								
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, УдержаноСтрока) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
									ДокументРезультат.Вывести(УдержаноШапка);
								КонецЕсли;
								
								ДокументРезультат.Вывести(УдержаноСтрока);
								
								Если ДанныеОНалогахУдержанныхПоСтавкам.Получить(ВыборкаНалогиУдержанныеДетали.Ставка) = Неопределено Тогда
									ДанныеОНалогахУдержанныхПоСтавкам.Вставить(ВыборкаНалогиУдержанныеДетали.Ставка, ВыборкаНалогиУдержанныеДетали.НалогУдержано);
								Иначе
									ДанныеОНалогахУдержанныхПоСтавкам[ВыборкаНалогиУдержанныеДетали.Ставка] = ДанныеОНалогахУдержанныхПоСтавкам[ВыборкаНалогиУдержанныеДетали.Ставка] + ВыборкаНалогиУдержанныеДетали.НалогУдержано;
								КонецЕсли;	
								
								Если ВыборкаНалогиУдержанныеДетали.Ставка = Перечисления.НДФЛСтавки.Ставка30 И ЗначениеЗаполнено(ВыборкаНалогиУдержанныеДетали.КЗачетуПоСтавке13) Тогда
									КЗачетуПоСтавке13 = КЗачетуПоСтавке13 + ВыборкаНалогиУдержанныеДетали.КЗачетуПоСтавке13;
									Если ДанныеОНалогахУдержанныхПоСтавкам.Получить(Перечисления.НДФЛСтавки.Ставка30) <> Неопределено Тогда
										ДанныеОНалогахУдержанныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка30] = ДанныеОНалогахУдержанныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка30] - ВыборкаНалогиУдержанныеДетали.КЗачетуПоСтавке13;
									КонецЕсли;
									Если ДанныеОНалогахУдержанныхПоСтавкам.Получить(Перечисления.НДФЛСтавки.Ставка13) = Неопределено Тогда
										ДанныеОНалогахУдержанныхПоСтавкам.Вставить(Перечисления.НДФЛСтавки.Ставка13, ВыборкаНалогиУдержанныеДетали.КЗачетуПоСтавке13);
									Иначе
										ДанныеОНалогахУдержанныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка13] = ДанныеОНалогахУдержанныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка13] + ВыборкаНалогиУдержанныеДетали.КЗачетуПоСтавке13;
									КонецЕсли;	
								КонецЕсли;
							КонецЦикла;
							
							Если ЗначениеЗаполнено(КЗачетуПоСтавке13) Тогда
								УдержаноБылоПоСтавке30.Параметры.Сумма = КЗачетуПоСтавке13;
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, УдержаноБылоПоСтавке30) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
									ДокументРезультат.Вывести(УдержаноШапка);
								КонецЕсли;
								ДокументРезультат.Вывести(УдержаноБылоПоСтавке30);
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;		
				
				ДанныеОНалогахУдержанныхПоРегистрации.Вставить(ВыборкаРегистраций.РегистрацияВНалоговомОргане, ДанныеОНалогахУдержанныхПоСтавкам);
				
				ВыборкаНалогиВозвращенныеИтоги.Сбросить();
				
				ДанныеОНалогахВозвращенныхПоСтавкам = Новый Соответствие;
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаНалогиВозвращенныеИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
					ВыборкаНалогиВозвращенныеИтогиПоФизЛицу = ВыборкаНалогиВозвращенныеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если ВыборкаНалогиВозвращенныеИтогиПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
						ВыборкаНалогиВозвращенныеИтогиПоРегистрации = ВыборкаНалогиВозвращенныеИтогиПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаНалогиВозвращенныеИтогиПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
								
								ВыборкаНалогиВозвращенныеДетали = ВыборкаНалогиВозвращенныеИтогиПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								
								ВыводимыеОбласти.Добавить(ВозвращеноЗаголовок);
								ВыводимыеОбласти.Добавить(УдержаноШапка);
								ВыводимыеОбласти.Добавить(ВозвращеноСтрока);
								
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
								КонецЕсли;
								
								ДокументРезультат.Вывести(ВозвращеноЗаголовок);
								ДокументРезультат.Вывести(ВозвращеноШапка);
								Пока ВыборкаНалогиВозвращенныеДетали.Следующий() Цикл
									ВозвращеноСтрока.Параметры.Месяц = Формат(ВыборкаНалогиВозвращенныеДетали.Месяц, "ДФ=ММММ");
									ВозвращеноСтрока.Параметры.Дата = Формат(ВыборкаНалогиВозвращенныеДетали.Дата, "ДФ=дд.ММ.гггг");
									ВозвращеноСтрока.Параметры.Ставка = ВыборкаНалогиВозвращенныеДетали.Ставка;
									ВозвращеноСтрока.Параметры.Сумма =  ВыборкаНалогиВозвращенныеДетали.НалогВозвращено;
									
									Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВозвращеноСтрока) Тогда
										ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
										ДокументРезультат.Вывести(ВозвращеноШапка);
									КонецЕсли;
									
									ДокументРезультат.Вывести(ВозвращеноСтрока);
									
									Если ДанныеОНалогахВозвращенныхПоСтавкам.Получить(ВыборкаНалогиВозвращенныеДетали.Ставка) = Неопределено Тогда
										ДанныеОНалогахВозвращенныхПоСтавкам.Вставить(ВыборкаНалогиВозвращенныеДетали.Ставка, ВыборкаНалогиВозвращенныеДетали.НалогВозвращено);
									Иначе
										ДанныеОНалогахВозвращенныхПоСтавкам[ВыборкаНалогиВозвращенныеДетали.Ставка] = ДанныеОНалогахВозвращенныхПоСтавкам[ВыборкаНалогиВозвращенныеДетали.Ставка] + ВыборкаНалогиВозвращенныеДетали.НалогВозвращено;
									КонецЕсли;	
									
								КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДанныеОНалогахВозвращенныхПоРегистрации.Вставить(ВыборкаРегистраций.РегистрацияВНалоговомОргане, ДанныеОНалогахВозвращенныхПоСтавкам);
				
				ВыборкаНалогиВзысканиеИтоги.Сбросить();
				
				ДанныеОНалогахПереданныхНаВзысканиеПоСтавкам = Новый Соответствие;
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаНалогиВзысканиеИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
					ВыборкаНалогиВзысканиеИтогиПоФизЛицу = ВыборкаНалогиВзысканиеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если ВыборкаНалогиВзысканиеИтогиПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
						ВыборкаНалогиВзысканиеИтогиПоРегистрации = ВыборкаНалогиВзысканиеИтогиПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаНалогиВзысканиеИтогиПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
							
							ВыборкаНалогиВзысканиеДетали = ВыборкаНалогиВзысканиеИтогиПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							
							ВыводимыеОбласти.Добавить(ПереданоНаВзысканиеЗаголовок);
							ВыводимыеОбласти.Добавить(ПереданоНаВзысканиеШапка);
							ВыводимыеОбласти.Добавить(ПереданоНаВзысканиеСтрока);
							
							Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
								ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
							КонецЕсли;
							
							ДокументРезультат.Вывести(ПереданоНаВзысканиеЗаголовок);
							ДокументРезультат.Вывести(ПереданоНаВзысканиеШапка);
							Пока ВыборкаНалогиВзысканиеДетали.Следующий() Цикл
								ПереданоНаВзысканиеСтрока.Параметры.Месяц = Формат(ВыборкаНалогиВзысканиеДетали.Месяц, "ДФ=ММММ");
								ПереданоНаВзысканиеСтрока.Параметры.Дата = Формат(ВыборкаНалогиВзысканиеДетали.Дата, "ДФ=дд.ММ.гггг");
								ПереданоНаВзысканиеСтрока.Параметры.Ставка = ВыборкаНалогиВзысканиеДетали.Ставка;
								ПереданоНаВзысканиеСтрока.Параметры.Сумма =  ВыборкаНалогиВзысканиеДетали.НалогПереданоНаВзыскание;
								
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ПереданоНаВзысканиеСтрока) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
									ДокументРезультат.Вывести(ПереданоНаВзысканиеШапка);
								КонецЕсли;
								
								ДокументРезультат.Вывести(ПереданоНаВзысканиеСтрока);
								
								Если ДанныеОНалогахПереданныхНаВзысканиеПоСтавкам.Получить(ВыборкаНалогиВзысканиеДетали.Ставка) = Неопределено Тогда
									ДанныеОНалогахПереданныхНаВзысканиеПоСтавкам.Вставить(ВыборкаНалогиВзысканиеДетали.Ставка, ВыборкаНалогиВзысканиеДетали.НалогПереданоНаВзыскание);
								Иначе
									ДанныеОНалогахПереданныхНаВзысканиеПоСтавкам[ВыборкаНалогиВзысканиеДетали.Ставка] = ДанныеОНалогахПереданныхНаВзысканиеПоСтавкам[ВыборкаНалогиВзысканиеДетали.Ставка] + ВыборкаНалогиВзысканиеДетали.НалогПереданоНаВзыскание;
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДанныеОНалогахПереданныхНаВзысканиеПоРегистрации.Вставить(ВыборкаРегистраций.РегистрацияВНалоговомОргане, ДанныеОНалогахПереданныхНаВзысканиеПоСтавкам);
				
				ВыборкаНалогиПеречисленныеИтоги.Сбросить();
				
				ДанныеОНалогахПеречисленныхПоСтавкам = Новый Соответствие;
				
				ВыводимыеОбласти.Очистить();
				Если ВыборкаНалогиПеречисленныеИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
					ВыборкаНалогиПеречисленныеИтогиПоФизЛицу = ВыборкаНалогиПеречисленныеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Если ВыборкаНалогиПеречисленныеИтогиПоФизЛицу.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
						ВыборкаНалогиПеречисленныеИтогиПоРегистрации = ВыборкаНалогиПеречисленныеИтогиПоФизЛицу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаНалогиПеречисленныеИтогиПоРегистрации.НайтиСледующий(ВыборкаРегистраций.РегистрацияВНалоговомОргане, "РегистрацияВНалоговомОргане") Тогда
							
								ВыборкаНалогиПеречисленныеДетали = ВыборкаНалогиПеречисленныеИтогиПоРегистрации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								
								ВыводимыеОбласти.Добавить(НалогПеречисленныйЗаголовок);
								ВыводимыеОбласти.Добавить(НалогПеречисленныйШапка);
								ВыводимыеОбласти.Добавить(НалогПеречисленныйСтрока);
								
								Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
									ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
								КонецЕсли;
								
								ДокументРезультат.Вывести(НалогПеречисленныйЗаголовок);
								ДокументРезультат.Вывести(НалогПеречисленныйШапка);
								КЗачетуПоСтавке13 = 0;
								ПредыдущийМесяц = "";
								Пока ВыборкаНалогиПеречисленныеДетали.Следующий() Цикл
									
									Если ВыборкаНалогиПеречисленныеДетали.Ставка = Перечисления.НДФЛСтавки.Ставка13 И ЗначениеЗаполнено(КЗачетуПоСтавке13) Тогда
										НалогПеречисленныйБылоПоСтавке30.Параметры.Сумма =  КЗачетуПоСтавке13;
										Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, НалогПеречисленныйБылоПоСтавке30) Тогда
											ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
											ДокументРезультат.Вывести(НалогПеречисленныйШапка);
										КонецЕсли;
										ДокументРезультат.Вывести(НалогПеречисленныйБылоПоСтавке30);
										КЗачетуПоСтавке13 = 0;
									КонецЕсли;
									
									НалогПеречисленныйСтрока.Параметры.Месяц = Формат(ВыборкаНалогиПеречисленныеДетали.Месяц, "ДФ=ММММ");
									НалогПеречисленныйСтрока.Параметры.Дата = Формат(ВыборкаНалогиПеречисленныеДетали.Дата, "ДФ=дд.ММ.гггг");
									НалогПеречисленныйСтрока.Параметры.Ставка = ВыборкаНалогиПеречисленныеДетали.Ставка;
									НалогПеречисленныйСтрока.Параметры.Сумма =  ВыборкаНалогиПеречисленныеДетали.Сумма;
									НалогПеречисленныйСтрока.Параметры.РеквизитыПП = ВыборкаНалогиПеречисленныеДетали.РеквизитыПП;
									
									Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, НалогПеречисленныйСтрока) Тогда
										ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
										ДокументРезультат.Вывести(НалогПеречисленныйШапка);
									КонецЕсли;
									
									ДокументРезультат.Вывести(НалогПеречисленныйСтрока);
									
									СтрокаТаблицыИтогов = ТаблицаПоНалогамИтоги.Добавить();
									СтрокаТаблицыИтогов.РегистрацияВНалоговомОргане = ВыборкаРегистраций.РегистрацияВНалоговомОргане;
									СтрокаТаблицыИтогов.Ставка = ВыборкаНалогиПеречисленныеДетали.Ставка;
									
									Если ДанныеОНалогахПеречисленныхПоСтавкам.Получить(ВыборкаНалогиПеречисленныеДетали.Ставка) = Неопределено Тогда
										ДанныеОНалогахПеречисленныхПоСтавкам.Вставить(ВыборкаНалогиПеречисленныеДетали.Ставка, ВыборкаНалогиПеречисленныеДетали.Сумма);
									Иначе
										ДанныеОНалогахПеречисленныхПоСтавкам[ВыборкаНалогиПеречисленныеДетали.Ставка] = ДанныеОНалогахПеречисленныхПоСтавкам[ВыборкаНалогиПеречисленныеДетали.Ставка] + ВыборкаНалогиПеречисленныеДетали.Сумма;
									КонецЕсли;
									
									Если ПредыдущийМесяц <> ВыборкаНалогиПеречисленныеДетали.Месяц И ВыборкаНалогиПеречисленныеДетали.Ставка = Перечисления.НДФЛСтавки.Ставка30 И ЗначениеЗаполнено(ВыборкаНалогиПеречисленныеДетали.КЗачетуПоСтавке13) Тогда
										ПредыдущийМесяц = ВыборкаНалогиПеречисленныеДетали.Месяц;
										КЗачетуПоСтавке13 = КЗачетуПоСтавке13 + ВыборкаНалогиПеречисленныеДетали.КЗачетуПоСтавке13;
										Если ДанныеОНалогахПеречисленныхПоСтавкам.Получить(Перечисления.НДФЛСтавки.Ставка30) <> Неопределено Тогда
											ДанныеОНалогахПеречисленныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка30] = ДанныеОНалогахПеречисленныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка30] - ВыборкаНалогиПеречисленныеДетали.КЗачетуПоСтавке13;
										КонецЕсли;
										Если ДанныеОНалогахПеречисленныхПоСтавкам.Получить(Перечисления.НДФЛСтавки.Ставка13) = Неопределено Тогда
											ДанныеОНалогахПеречисленныхПоСтавкам.Вставить(Перечисления.НДФЛСтавки.Ставка13, ВыборкаНалогиПеречисленныеДетали.КЗачетуПоСтавке13);
										Иначе
											ДанныеОНалогахПеречисленныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка13] = ДанныеОНалогахПеречисленныхПоСтавкам[Перечисления.НДФЛСтавки.Ставка13] + ВыборкаНалогиПеречисленныеДетали.КЗачетуПоСтавке13;
										КонецЕсли;	
									КонецЕсли;
									
								КонецЦикла;
								
								Если ЗначениеЗаполнено(КЗачетуПоСтавке13) Тогда
									НалогПеречисленныйБылоПоСтавке30.Параметры.Сумма = КЗачетуПоСтавке13;
									Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, НалогПеречисленныйБылоПоСтавке30) Тогда
										ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
										ДокументРезультат.Вывести(НалогПеречисленныйШапка);
									КонецЕсли;
									ДокументРезультат.Вывести(НалогПеречисленныйБылоПоСтавке30);
								КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДанныеОНалогахПеречисленныхПоРегистрации.Вставить(ВыборкаРегистраций.РегистрацияВНалоговомОргане, ДанныеОНалогахПеречисленныхПоСтавкам);
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЕстьДанныеПоДоходам Тогда
		ВыводимыеОбласти.Очистить();
		ВыводимыеОбласти.Добавить(Раздел4);
		
		Если СтатусНаКонецПериода = СтатусРезидент Тогда
			ВыводимыеОбласти.Добавить(РасчетыНалогаПоСтавке13Заголовок);
		Иначе
			ВыводимыеОбласти.Добавить(НалогПоСтавке30Заголовок);
		КонецЕсли;	
		
		Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, УдержаноСтрока) Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Для Каждого Область Из ВыводимыеОбласти Цикл
			ДокументРезультат.Вывести(Область);
		КонецЦикла;	
	КонецЕсли;	

	ВыборкаВычетыИтоги.Сбросить();
	
	ВыводимыеОбласти.Очистить();
	
	ВыводимыеОбласти.Добавить(ПредоставленныеВычетыИтогиЗаголовок);
	ВыводимыеОбласти.Добавить(ПредоставленныеВычетыИтогиШапка);
	ВыводимыеОбласти.Добавить(ПредоставленныеВычетыИтогиСтрока);
	
	Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
   		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;

	ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиЗаголовок);
	ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиШапка);
	
	Если ВыборкаВычетыИтоги.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") Тогда
		ВыборкаФизЛиц  = ВыборкаВычетыИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
			ВыборкаДетали = ВыборкаФизЛиц.Выбрать();
			СуммаВычетаИтог = 0; 
			Пока ВыборкаДетали.Следующий() Цикл
				
				ПредоставленныеВычетыИтогиСтрока.Параметры.Заполнить(ВыборкаДетали);	
				СтрокаРегистрации = ТаблицаРегистрацийВНО.Найти(ВыборкаДетали.РегистрацияВНалоговомОргане,"РегистрацияВНалоговомОргане");
				Если СтрокаРегистрации <> Неопределено Тогда
					ПредоставленныеВычетыИтогиСтрока.Параметры.Заполнить(СтрокаРегистрации);	
				КонецЕсли;
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ПредоставленныеВычетыИтогиСтрока) Тогда
	       			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиШапка);
				КонецЕсли;

				ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиСтрока);
				СуммаВычетаИтог = СуммаВычетаИтог + ВыборкаДетали.СуммаВычета;
			КонецЦикла;
			ПредоставленныеВычетыИтогиИтог.Параметры.СуммаВычета = СуммаВычетаИтог;
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ПредоставленныеВычетыИтогиИтог) Тогда
       			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиШапка);
			КонецЕсли;

			ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиИтог);
		Иначе
			ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиСтрока);
		КонецЕсли;
	Иначе 
		ДокументРезультат.Вывести(ПредоставленныеВычетыИтогиСтрока);	
	КонецЕсли;	
	
	ВыводимыеОбласти.Очистить();
	
	ВыводимыеОбласти.Добавить(ОбщиеСуммыИтогЗаголовок);
	ВыводимыеОбласти.Добавить(ОбщиеСуммыИтогШапка);
	ВыводимыеОбласти.Добавить(ОбщиеСуммыСтроки);
	
	Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
   		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;

	ДокументРезультат.Вывести(ОбщиеСуммыИтогЗаголовок);
	ДокументРезультат.Вывести(ОбщиеСуммыИтогШапка);
	
	ТаблицаПоНалогамИтоги.Свернуть("РегистрацияВНалоговомОргане, Ставка", "ОбщаяСуммаДохода, НалоговаяБаза");
	ТаблицаПоНалогамИтоги.Сортировать("РегистрацияВНалоговомОргане, Ставка");
	
	Для Каждого НалогиИтоги Из ТаблицаПоНалогамИтоги Цикл
		
		НалогиИсчисленныеПоРегистрации = ДанныеОНалогахНачисленныхПоРегистрации.Получить(НалогиИтоги.РегистрацияВНалоговомОргане);
		
		Если НалогиИсчисленныеПоРегистрации = Неопределено Тогда
			СуммаНалогаИсчисленная = 0;
		Иначе
			НалогиИсчисленныеПоСтавкам = НалогиИсчисленныеПоРегистрации.Получить(НалогиИтоги.Ставка);
			СуммаНалогаИсчисленная = ?(НалогиИсчисленныеПоСтавкам = Неопределено, 0, НалогиИсчисленныеПоСтавкам);
		КонецЕсли;	
		
		АвансыЗачтенныеПоРегистрации = ДанныеОбАвансахЗачтенныхПоРегистрации.Получить(НалогиИтоги.РегистрацияВНалоговомОргане);
		
		Если АвансыЗачтенныеПоРегистрации = Неопределено Тогда
			СуммаАвансовыхПлатежейЗачтенная = 0;
		Иначе
			АвансыЗачтенныеПоСтавкам = АвансыЗачтенныеПоРегистрации.Получить(НалогиИтоги.Ставка);
			СуммаАвансовыхПлатежейЗачтенная = ?(АвансыЗачтенныеПоСтавкам = Неопределено, 0, АвансыЗачтенныеПоСтавкам);
		КонецЕсли;	
		
		НалогиУдержанныеПоРегистрации = ДанныеОНалогахУдержанныхПоРегистрации.Получить(НалогиИтоги.РегистрацияВНалоговомОргане);
		
		Если НалогиУдержанныеПоРегистрации = Неопределено Тогда
			СуммаНалогаУдержанная = 0;
		Иначе
			НалогиУдержанныеПоСтавкам = НалогиУдержанныеПоРегистрации.Получить(НалогиИтоги.Ставка);
			СуммаНалогаУдержанная = ?(НалогиУдержанныеПоСтавкам = Неопределено, 0, НалогиУдержанныеПоСтавкам);
		КонецЕсли;
		
		НалогиВозвращенныеПоРегистрации = ДанныеОНалогахВозвращенныхПоРегистрации.Получить(НалогиИтоги.РегистрацияВНалоговомОргане);
		
		Если НалогиВозвращенныеПоРегистрации = Неопределено Тогда
			СуммаНалогаВозвращенная = 0;
		Иначе
			НалогиВозвращенныеПоСтавкам = НалогиВозвращенныеПоРегистрации.Получить(НалогиИтоги.Ставка);
			СуммаНалогаВозвращенная = ?(НалогиВозвращенныеПоСтавкам = Неопределено, 0, НалогиВозвращенныеПоСтавкам);
		КонецЕсли;
		
		СуммаНалогаУдержанная = СуммаНалогаУдержанная - СуммаНалогаВозвращенная;
		
		НалогиВзысканиеПоРегистрации = ДанныеОНалогахПереданныхНаВзысканиеПоРегистрации.Получить(НалогиИтоги.РегистрацияВНалоговомОргане);
		
		Если НалогиВзысканиеПоРегистрации = Неопределено Тогда
			СуммаНалогаВзыскание = 0;
		Иначе
			НалогиВзысканиеПоСтавкам = НалогиВзысканиеПоРегистрации.Получить(НалогиИтоги.Ставка);
			СуммаНалогаВзыскание = ?(НалогиВзысканиеПоСтавкам = Неопределено, 0, НалогиВзысканиеПоСтавкам);
		КонецЕсли;
		
		НалогиПеречисленныеПоРегистрации = ДанныеОНалогахПеречисленныхПоРегистрации.Получить(НалогиИтоги.РегистрацияВНалоговомОргане);
		
		Если НалогиПеречисленныеПоРегистрации = Неопределено Тогда
			СуммаНалогаПеречисленная = 0;
		Иначе
			НалогиПеречисленныеПоСтавкам = НалогиПеречисленныеПоРегистрации.Получить(НалогиИтоги.Ставка);
			СуммаНалогаПеречисленная = ?(НалогиПеречисленныеПоСтавкам = Неопределено, 0, НалогиПеречисленныеПоСтавкам);
		КонецЕсли;
		
		Если НалогиИтоги.ОбщаяСуммаДохода = 0 И НалогиИтоги.НалоговаяБаза = 0 И СуммаНалогаИсчисленная = 0 И СуммаНалогаУдержанная = 0 И СуммаНалогаПеречисленная = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаРегистрации = ТаблицаРегистрацийВНО.Найти(НалогиИтоги.РегистрацияВНалоговомОргане,"РегистрацияВНалоговомОргане");
		Если СтрокаРегистрации <> Неопределено Тогда
			ОбщиеСуммыСтроки.Параметры.Заполнить(СтрокаРегистрации);
		КонецЕсли;

		ОбщиеСуммыСтроки.Параметры.Заполнить(НалогиИтоги);
		ОбщиеСуммыСтроки.Параметры.СуммаНалогаИсчисленная = СуммаНалогаИсчисленная;
		ОбщиеСуммыСтроки.Параметры.СуммаНалогаУдержанная = СуммаНалогаУдержанная;
	    ОбщиеСуммыСтроки.Параметры.СуммаНалогаПеречисленная = СуммаНалогаПеречисленная;
		Если Год >= ДатаИзмененияНалогообложенияДивидендов Тогда
			ОбщиеСуммыСтроки.Параметры.СуммаАвансовыхПлатежейЗачтенная = СуммаАвансовыхПлатежейЗачтенная;
		КонецЕсли;
		СальдоПоНалогам = СуммаНалогаИсчисленная - СуммаАвансовыхПлатежейЗачтенная - СуммаНалогаУдержанная - СуммаНалогаВзыскание;
		
		Если СальдоПоНалогам > 0 Тогда 
			ОбщиеСуммыСтроки.Параметры.СуммаНалогаНеУдержанная = СальдоПоНалогам;	
			ОбщиеСуммыСтроки.Параметры.СуммаНалогаИзлишнеУдержанная = 0;
		Иначе	
			ОбщиеСуммыСтроки.Параметры.СуммаНалогаИзлишнеУдержанная = -СальдоПоНалогам;
			ОбщиеСуммыСтроки.Параметры.СуммаНалогаНеУдержанная = 0;
		КонецЕсли;	
		Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ОбщиеСуммыСтроки) Тогда
   			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			ДокументРезультат.Вывести(ОбщиеСуммыИтогШапка);
		КонецЕсли;

		ДокументРезультат.Вывести(ОбщиеСуммыСтроки);
		
	КонецЦикла;	
		
	ВыборкаСправкиНДФЛ.Сбросить();
	
	ВыводимыеОбласти.Очистить();
	ВыводимыеОбласти.Добавить(Раздел7);
	ВыводимыеОбласти.Добавить(Подписи);
	
	Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, ВыводимыеОбласти) Тогда
   		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;

	Если ВыборкаСправкиНДФЛ.НайтиСледующий(ФизическоеЛицо, "ФизическоеЛицо") Тогда
		ВыборкаПоСправкам = ВыборкаСправкиНДФЛ.Выбрать();
		
		Пока ВыборкаПоСправкам.Следующий() Цикл
			Раздел7.Параметры["НомерСправки" + Строка(ВыборкаПоСправкам.ТипСправки)] = ВыборкаПоСправкам.НомерСправки;
			Раздел7.Параметры["ДатаСправки" + Строка(ВыборкаПоСправкам.ТипСправки)] = Формат(ВыборкаПоСправкам.ДатаСправки, "ДФ=дд.ММ.гггг");
		КонецЦикла;
	КонецЕсли;	
	
	ДокументРезультат.Вывести(Раздел7);
	
	Подписи.Параметры.ДатаПодписи = Формат(ДатаПодписи, "ДЛФ=D");
	Подписи.Параметры.ФИООтветственного = ДанныеОФизЛицеОрганизации.Ответственный_ФИО;
	Подписи.Параметры.ДолжностьОтветственного = ДанныеОФизЛицеОрганизации.Ответственный_Должность;
	
	ДокументРезультат.Вывести(Подписи);

КонецПроцедуры

#КонецОбласти

#КонецЕсли