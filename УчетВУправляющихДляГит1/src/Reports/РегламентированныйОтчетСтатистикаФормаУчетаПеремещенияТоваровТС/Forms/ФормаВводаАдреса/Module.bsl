&НаКлиенте
Перем Оповестить;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	Параметры.Свойство("ОргИндекс", ОргИндекс);
	Параметры.Свойство("ОргНаселенныйПункт", ОргНаселенныйПункт);
	Параметры.Свойство("ОргОбласть", ОргОбласть);
	Параметры.Свойство("ОргСтранаКод", ОргСтранаКод);
	Параметры.Свойство("ОргСтранаНаименование", ОргСтранаНаименование);
	Параметры.Свойство("ОргУлицаНомерДома", ОргУлицаНомерДома);
	
	СтруктураРеквизитов = Новый Структура;
	МассивРеквизитов = ПолучитьРеквизиты();
	Для Каждого Реквизит Из МассивРеквизитов Цикл
		Если Реквизит.Имя <> "СтруктураРеквизитов" И Реквизит.Имя <> "ВидыУдостЛичности" Тогда
			СтруктураРеквизитов.Вставить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОргСтранаНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",                      "Выберите страну");
	ПараметрыФормы.Вставить("ТаблицаЗначений",                СтраныМира);
	ПараметрыФормы.Вставить("НаимКолонкиНазвание",            "Название");
	ПараметрыФормы.Вставить("СтруктураДляПоиска",             Новый Структура("Кратко", ОргСтранаНаименование));
	ПараметрыФормы.Вставить("ВключитьВидимостьКолонкиКратко", Ложь);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОргСтранаНаименованиеНачалоВыбораЗавершение", ЭтотОбъект);

	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОргСтранаНаименованиеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ОргСтранаКод = Результат["Код"];
		ОргСтранаНаименование = Результат["Кратко"];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура OK(Команда)
	ВыполняемоеОповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершениеПродолжение", ЭтотОбъект);
	
	Для Каждого РеквизитФормы Из СтруктураРеквизитов Цикл
		СтруктураРеквизитов[РеквизитФормы.Ключ] = ЭтаФорма[РеквизитФормы.Ключ];
	КонецЦикла; 
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершениеПродолжение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Истина Тогда
		
		Модифицированность = Ложь;
		Закрыть(СтруктураРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры