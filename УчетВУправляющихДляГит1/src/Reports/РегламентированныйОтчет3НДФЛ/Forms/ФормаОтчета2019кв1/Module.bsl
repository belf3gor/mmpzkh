#Область ОписаниеПеременных

&НаКлиенте
Перем ОбработкаКС Экспорт;

&НаСервере
Перем ОбъектЭтогоОтчета;

&НаКлиенте
Перем ФормаВыгрузкиРеглОтчета;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполняем обязательными ключами реквизит формы "СтруктураРеквизитовФормы"
	СтруктураРеквизитовФормы = Отчеты.РегламентированныйОтчет3НДФЛ.СтруктураРеквизитовФормы(
		Параметры.Организация, Параметры.мДатаКонцаПериодаОтчета, Параметры.мВыбраннаяФорма, Параметры);
	Если СтруктураРеквизитовФормы = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не определена СтруктураРеквизитовФормы для налогового периода ""%1""'"), Параметры.мДатаКонцаПериодаОтчета);
	КонецЕсли;
	
	// Установка значения прочих реквизитов
	мСтатусНалогоплательщика = 0;
	
	// количество строк многострочных разделов по "бумажной" форме отчета
	// (минимальное количество строк, которое должно присутствовать всегда)
	КолИсхСтрокИдРаздел1 = 1;
	КолИсхСтрокИдПриложение1 = 1;
	КолИсхСтрокИдПриложение1Расчет = 1;
	КолИсхСтрокИдПриложение8 = 1;
	
	// Структуры для работы с показателями уникальных страниц отчета (не многострочных и не многостраничных)
	СтруктураДанныхТитульный = Новый Структура;
	СтруктураДанныхПриложение4 = Новый Структура;
	СтруктураДанныхПриложение5 = Новый Структура;
	СтруктураДанныхПриложение6 = Новый Структура;
	
	// Ключи с именами реквизитов формы, соответствующих таблицам многострочных разделов
	мСтруктураМногострочныхРазделов = Отчеты.РегламентированныйОтчет3НДФЛ.СтруктураМногострочныхРазделов(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	// Структура содержит данные дополнительных страниц:
	// Ключ - имя раздела, значение - имя таблицы, каждая строка которой соответствует странице раздела
	мСтруктураМногостраничныхРазделов = Отчеты.РегламентированныйОтчет3НДФЛ.СтруктураМногостраничныхРазделов(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	// Добавляем первые страницы в многостраничные разделы
	ИнициализацияМногостраничныхРазделов();
	
	// Структура с описанием обязательного количества строк в многострочных разделах формы
	мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдРаздел1, КолИсхСтрокИдРаздел1);
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдПриложение1, КолИсхСтрокИдПриложение1);
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдПриложение1Расчет, КолИсхСтрокИдПриложение1Расчет);
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдПриложение8, КолИсхСтрокИдПриложение8);
	
	// Установка цветов для оформления ячеек отчета
	ЦветЗаполняетсяТолькоВручную     = Новый Цвет(255, 255, 225);
	ЦветЗаполняетсяАвтоКорректир     = Новый Цвет(255, 240, 200);
	ЦветЗаполняетсяАвтоБезКорректир  = Новый Цвет(230, 240, 220);
	СтруктураРеквизитовФормы.ЦветЗаполняетсяАвтоАрифметика = Новый Цвет(192, 220, 192); // ЗЕЛЕНЫЙ
	ЦветЗаполняетсяОднозначноВручную = Новый Цвет(255, 255, 192); // ЖЕЛТЫЙ
	ЦветЗаполняетсяАвтоматически     = Новый Цвет(255, 255, 255); // Белый, Ввод запрещен
	
	// структура цветов раскраски автозаполняемых ячеек
	мСтруктураЦветовРаскраски = Новый Структура;
	мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
	мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
	мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);
	мСтруктураЦветовРаскраски.Вставить("_4", СтруктураРеквизитовФормы.ЦветЗаполняетсяАвтоАрифметика);
	мСтруктураЦветовРаскраски.Вставить("_0", ЦветЗаполняетсяОднозначноВручную);
	мСтруктураЦветовРаскраски.Вставить("_5", ЦветЗаполняетсяАвтоматически);
	
	мСтруктураВариантыЗаполнения       = Новый Структура;
	мСтруктураВариантыЗаполненияЭталон = Новый Структура;
	
	// Установка начального раздела декларации
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный";
	
	ИмяМакетаТекущегоРаздел = ИмяМакета(ИмяФормы, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	ТабличныйДокумент.Вывести(ОтчетОбъект().ПолучитьМакет(ИмяМакетаТекущегоРаздел));
	
	Инициализация(Параметры.БезОткрытияФормы);
	
	Если ЗначениеЗаполнено(Параметры.мСохраненныйДок) Тогда
		ДанныеОтчета = Параметры.мСохраненныйДок.ДанныеОтчета.Получить();
		ОтчетСозданИзПомощника = ДанныеОтчета.Свойство("ДанныеПомощникаЗаполнения")
			И ДанныеОтчета.ДанныеПомощникаЗаполнения.Количество() > 0
			И Метаданные.Обработки.Найти("ПомощникЗаполнения3НДФЛ") <> Неопределено;
	Иначе
		ОтчетСозданИзПомощника = Ложь;
	КонецЕсли;
	Элементы.ГруппаОтчетИзПомощника.Видимость = ОтчетСозданИзПомощника;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовокФормы();
	
	ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьПроверкуПередОткрытием", 0.1, Истина);
	
	Если СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения <> Неопределено
		И СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения.Статус = "Ошибка" Тогда
		ВызватьИсключение СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения.ПодробноеПредставлениеОшибки;
	ИначеЕсли СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения <> Неопределено Тогда
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияФоновогоЗаданияАвтозаполнения", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения, Обработчик, НастройкиОжидания);
	Иначе
		ОбновитьДеревоРазделовПослеавтоЗаполнения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(
		ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтчетСформированВПомощникеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПомощникЗаполнения3НДФЛ" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыПомощника = Новый Структура;
		ПараметрыПомощника.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
		ПараметрыПомощника.Вставить("Период", СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
		ПараметрыПомощника.Вставить("ВыбраннаяФорма", СтруктураРеквизитовФормы.мВыбраннаяФорма);
		ПараметрыПомощника.Вставить("КонтекстныйВызов", Истина);
		
		ИмяФормыПомощника = "Обработка.ПомощникЗаполнения3НДФЛ.Форма.Форма";
		ОткрытьФорму(ИмяФормыПомощника, ПараметрыПомощника);
		
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьОтчетСформированВПомощникеНажатие(Элемент)
	
	Элементы.ГруппаОтчетИзПомощника.Видимость = Ложь;
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, КонтролирующийОрган());
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, КонтролирующийОрган());
	
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, КонтролирующийОрган());
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийНавигацииПоРазделам

&НаКлиенте
Процедура РазделыОтчетаПриАктивизацииСтроки(Элемент)
	
	Раздел = Элемент.ТекущиеДанные;
	Если Раздел <> Неопределено И Элемент.ТекущаяСтрока <> СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета Тогда
		РазделОтчетаПриИзменении(Раздел, Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоДокумента

&НаКлиенте
Процедура ОбработкаВыбораВПолеТабличногоДокумента(Элемент, Область, СтандартнаяОбработка)
	
	ИмяЯчейки = Область.Имя;
	
	Если РегламентированнаяОтчетностьКлиент.ВыбраннаяОбластьРегламентированногоОтчетаНеВерна(ЭтотОбъект, Область, СтандартнаяОбработка) Тогда
		Если СтрНачинаетсяС(Область.Имя, "ДобавитьСтраницу") Тогда
			ДобавитьСтраницу("");
		ИначеЕсли СтрНачинаетсяС(Область.Имя, "УдалитьСтраницу") И Область.Гиперссылка Тогда
			УдалитьСтраницу("");
		ИначеЕсли СтрНачинаетсяС(Область.Имя, "ДобавитьСтроку") Тогда
			ДобавитьСтроку("");
		ИначеЕсли СтрНачинаетсяС(Область.Имя, "УдалитьСтроку") И Область.Гиперссылка Тогда
			УдалитьСтроку("");
		ИначеЕсли СтрНачинаетсяС(Область.Имя, "СобратьДанные") И Область.Гиперссылка Тогда
			СобратьДанные("");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ИмяЯчейки = "КодИМНС" Тогда // Титульный лист
		
		ОбработкаВыбораИФНС(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли ИмяЯчейки = "КодСтраны" Тогда // Титульный лист
		
		ОбработкаВыбораСтраныМира(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли Лев(ИмяЯчейки, 13) = "П000300001060" Тогда // Приложение 1: Haимeнование источника выплаты дохода
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьВводСтроки(
			ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВводаСтрокиЗавершение", Элемент, Область),
			Область.Текст,
			НСтр("ru = 'Haимeнование источника выплаты дохода'"),
			,
			Истина);
		
	ИначеЕсли ИмяЯчейки = "П000400001010" Тогда // Приложение 2: Код страны по классификатору ОКСМ
		
		ОбработкаВыбораСтраныМира(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли ИмяЯчейки = "П000400001020" Тогда // Приложение 2: Haимeнование источника выплаты дохода
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьВводСтроки(
			ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВводаСтрокиЗавершение", Элемент, Область),
			Область.Текст,
			НСтр("ru = 'Haимeнование источника выплаты дохода'"),
			,
			Истина);
		
	ИначеЕсли ИмяЯчейки = "П000400001030" Тогда // Приложение 2: Код валюты
		
		ОбработкаВыбораВалюты(Элемент, Область, СтандартнаяОбработка);
		
	ИначеЕсли ИмяЯчейки = "П000700001031" Тогда // Приложение 7: Номер объекта
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьВводСтроки(
			ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВводаСтрокиЗавершение", Элемент, Область),
			Область.Текст,
			НСтр("ru = 'Номер объекта'"),
			,
			Истина);
		
	ИначеЕсли ИмяЯчейки = "П000700001032" Тогда // Приложение 7: Сведения о местонахождении объекта
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьВводСтроки(
			ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВводаСтрокиЗавершение", Элемент, Область),
			Область.Текст,
			НСтр("ru = 'Сведения о местонахождении объекта'"),
			,
			Истина);
		
	ИначеЕсли Лев(ИмяЯчейки, 13) = "П001900001010" Тогда // Расчет к Приложению 1: Кадастровый номер отчужденного объекта недвижимого имущества
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьВводСтроки(
			ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВводаСтрокиЗавершение", Элемент, Область),
			Область.Текст,
			НСтр("ru = 'Кадастровый номер отчужденного объекта недвижимого имущества'"),
			,
			Истина);
		
	ИначеЕсли ИмяЯчейки = "П001000001030" Тогда // Расчет к Приложению 5: Haимeнование негосударственного пенсионного фонда или страховой организации
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьВводСтроки(
			ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВводаСтрокиЗавершение", Элемент, Область),
			Область.Текст,
			НСтр("ru = 'Haимeнование негосударственного пенсионного фонда или страховой организации'"),
			,
			Истина);
		
	Иначе
		
		ПараметрВводаПоля = СвойствоПоказателя(ИмяЯчейки, АдресСвойствПоказателей);
		
		Если ПараметрВводаПоля <> Неопределено Тогда
			
			ВвестиПоказатель(
				Элемент,
				Область,
				ПараметрВводаПоля.Показатель,
				ПараметрВводаПоля.ТекстПриВыборе,
				ПараметрВводаПоля.АдресТаблицыЗначений,
				СтандартнаяОбработка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОповещенияВыбораВПолеТабличногоДокумента(ИмяПроцедуры, Элемент, Область)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРаздела", Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	ДополнительныеПараметры.Вставить("ИмяЯчейки", Область.Имя);
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("Область", Область);
	
	Возврат Новый ОписаниеОповещения(ИмяПроцедуры, ЭтотОбъект, ДополнительныеПараметры);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаИзмененияСодержимогоВПолеТабличногоДокумента(Элемент, Область)
	
	Модифицированность = Истина;
	
	ПараметрВводаПоля = СвойствоПоказателя(Область.Имя, АдресСвойствПоказателей);
	Если ПараметрВводаПоля<> Неопределено Тогда
		Если ПараметрВводаПоля.Тип = "КБК" И ЗначениеЗаполнено(Область.Значение) Тогда
			Область.Значение = СтрЗаменить(Область.Значение," ","");
		КонецЕсли;
		
		Если СтрДлина(СокрЛП(Область.Значение)) <> 0 И ПараметрВводаПоля.Длина <> 0 И СтрДлина(СокрЛП(Область.Значение)) <> ПараметрВводаПоля.Длина Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Длина значения показателя должна быть равна %1!'"), ПараметрВводаПоля.Длина);
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	
	ИмяПоказателя = Область.Имя;
	
	//Изменение "по месту нахождения (учета) (код)" на Титульном листе
	Если СтрНайти(ИмяПоказателя, "ПоМесту") > 0 Тогда
		
		НовыйПоМесту = РегламентированнаяОтчетностьКлиентСервер.ВернутьЗначениеПараметраСЛиста(ЭтотОбъект, "Титульный", "ПоМесту");
		
		СтруктураРеквизитовФормы.мПоМесту = НовыйПоМесту;
		ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		РасчетПослеИзмененияСодержания(ИмяПоказателя, ИмяРаздела, Область);
	ИначеЕсли ИмяПоказателя = "ДатаПодписи" Тогда
		
		ДатаПодписи = Область.Значение;
		
	ИначеЕсли ИмяПоказателя = "НомерКорректировки" Тогда
		
		НомерКорректировки = Область.Значение;
		СтруктураРеквизитовФормы.ВидДокумента = ?(НомерКорректировки > 0, 1, 0);
		
	ИначеЕсли ИмяПоказателя = "ОтчетГод" Тогда
		
		СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = Дата(Область.Значение, 1, 1);
		СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета = КонецГода(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
		
		// Задаем заголовок формы
		СтруктураРеквизитовФормы.СтрПериодОтчета = ПредставлениеПериода(
			НачалоДня(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета),
			КонецДня(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета),
			"ФП = Истина");
		
		УстановитьЗаголовокФормы();
		
		ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		РасчетПослеИзмененияСодержания(ИмяПоказателя, ИмяРаздела, Область);
		
	Иначе
		ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		РасчетПослеИзмененияСодержания(ИмяПоказателя, ИмяРаздела, Область);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриАктивизацииОбласти(Элемент)
	
	ПозицияРазделителя = 14;
	Разделитель = "_";
	
	Если Сред(Элемент.ТекущаяОбласть.Имя, ПозицияРазделителя, 1) = Разделитель Тогда
		ИмяОбластиВторойСтроки = Лев(Элемент.ТекущаяОбласть.Имя, ПозицияРазделителя - 1) + Разделитель + "2";
		Элементы.УдалитьСтроку.Доступность = РазделМногострочный(СтруктураРеквизитовФормы.НаимТекущегоРаздела, ЭтотОбъект)
			И (ТабличныйДокумент.Области.Найти(ИмяОбластиВторойСтроки) <> Неопределено);
	Иначе
		Элементы.УдалитьСтроку.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗагрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОтчет(Команда)
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСведенияОбОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьДанные(Команда)
	
	Если Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Раздел1" Тогда
		
		СобратьДанныеВРаздел1();
		
	ИначеЕсли Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим = "Раздел2" Тогда
		
		СобратьДанныеВРаздел2();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницу(Команда)
	
	ВставитьДополнительнуюСтраницуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	
	СтруктураРеквизитовФормы.НомерТекущейСтраницы = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницу(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтраницуЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Удалить текущую страницу?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	Перем МаксимальноеКоличествоСтрокГруппы, СтруктураГруппы;
	
	ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	// Исходное количество строк группы
	МаксимальноеКоличествоСтрокГруппы = МаксимальноеКоличествоСтрокНаСтранице(ЭтотОбъект, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	Если мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы) Тогда
		СтруктураГруппы = ЭтотОбъект[СтруктураГруппы];
	КонецЕсли;
	
	// Определяем последнюю строку многострочной группы
	Если СтруктураГруппы <> Неопределено Тогда
		Если СтруктураГруппы.Количество() = МаксимальноеКоличествоСтрокГруппы Тогда
			ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Количество строк на странице не может превышать %1. Добавить новую страницу?'"),
				МаксимальноеКоличествоСтрокГруппы);
			ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьСтрокуЗавершение", ЭтотОбъект, Новый Структура("Команда", Команда));
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ВставитьДополнительнуюСтроку(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	ТекТабличноеПоле = ЭтотОбъект["ТабличныйДокумент"];
	
	ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
	ИмяГруппы      = РегламентированнаяОтчетностьКлиент.РазделТекущейОбласти(ЭтотОбъект, ТекущаяОбласть);
	
	Если ИмяГруппы = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = НСтр("ru='Не выбрана удаляемая строка многострочного раздела.'");
		
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтрокуЗавершение", ЭтотОбъект);
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Удалить выбранную строку?%1'"), Символы.ПС);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчет(Команда)
	
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(ЭтотОбъект, "ОчиститьВесьОтчет");
	
	СтруктураРеквизитовФормы.НомерТекущейСтраницы = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекущуюСтраницу(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьТекущуюСтраницуЗавершение", ЭтотОбъект);
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(
		ЭтотОбъект, "ОчиститьТекущийЭкземплярМногостраничногоРаздела"
		+ Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроек(Команда)
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСДвухмернымШтрихкодомPDF417(Команда)
	
	Если ДоступнаПечатьPDF417(СтруктураРеквизитовФормы.мВыбраннаяФорма) Тогда
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФорму(ЭтотОбъект, Команда.Имя);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Печать формы со штрихкодом PDF417 будет реализована в будущих версиях программы.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоступнаПечатьPDF417(ВыбраннаяФорма)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.ДоступнаПечатьPDF417(ВыбраннаяФорма);
	
КонецФункции

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Отказ = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьБланкЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвто(Команда)
	
	Отказ = Ложь;
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ПроверитьВозможностьАвтоЗаполненияРеглОтчета(ИмяФормы, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗаполнениемРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Расшифровать(Команда)
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация",							СтруктураРеквизитовФормы.Организация);
	ПараметрыОтчета.Вставить("мДатаНачалаПериодаОтчета",			СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыОтчета.Вставить("мДатаКонцаПериодаОтчета",				СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыОтчета.Вставить("АдресВременногоХранилищаРасшифровки", СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки);
	
	ИДИменПоказателей = Новый Массив();
	ИДИменПоказателей.Добавить(Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя);
	
	РегламентированнаяОтчетностьКлиент.ОткрытьРасшифровкуОтчета(
		ИмяОтчета(ЭтотОбъект.ИмяФормы), ИмяФормыОтчета(ЭтотОбъект.ИмяФормы), ИДИменПоказателей, ПараметрыОтчета)
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителя(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезПредставителяЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВИнтернетеЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, КонтролирующийОрган());
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтотОбъект, КонтролирующийОрган());
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьНаКлиенте(, Новый ОписаниеОповещения("ЗаписатьИЗакрытьПродолжение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьОтчетИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияОтчета

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	
	// Заполняем показатели отчета из макета "Состав показателей"
	ЗаполнитьНачальныеНастройки();
	
	// Настройки показа и вывода на печать каждой страницы отчета
	ФормироватьСтруктуруСтраницОтчета();
	
	// Настройки свойств каждого раздела: многострочности, многостраничности, количества строк и страниц, параметров автозаполнения и т.д.
	ФормироватьСтруктуруСвойствСтраниц();
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	УстановитьМаскиВПоляхВвода(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	ТабличныйДокумент.Области.НомерКорректировки.Значение = 0;
	СтруктураРеквизитовФормы.ВидДокумента = 0;
	
	КлючиПоказателей = Отчеты.РегламентированныйОтчет3НДФЛ.КлючиПоказателейДекларации(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	АдресКлючейПоказателей = ПоместитьВоВременноеХранилище(КлючиПоказателей, УникальныйИдентификатор);
	
	Если Не БезОткрытияФормы Тогда
		СвойстваПоказателей = Отчеты.РегламентированныйОтчет3НДФЛ.СвойстваПоказателей(
			ОтчетОбъект(), СтруктураРеквизитовФормы.мВыбраннаяФорма, УникальныйИдентификатор);
		АдресСвойствПоказателей = ПоместитьВоВременноеХранилище(СвойстваПоказателей, УникальныйИдентификатор);
	КонецЕсли;
	
	// Заполняем многострочные разделы нужным количеством строк
	НачальныеОперацииСМногострочнымиБлоками();
	
	ЗаполнитьПустыеСтруктурыДанных();
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено
		И Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
		// Новый документ
		Модифицированность = Истина;
		КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
		ЗаполнитьДатуПодписи(ЭтотОбъект);
		ЗаполнитьСведенияОбОрганизации();
	ИначеЕсли ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
		// Копирование документа
		Модифицированность = Истина;
		ВосстановитьСохраненныеДанные();
		ЗаполнитьДатуПодписи(ЭтотОбъект);
		СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
	Иначе
		// Открытие существующего документа
		ВосстановитьСохраненныеДанные();
		Если Не БезОткрытияФормы И СтруктураРеквизитовФормы.мСохраненныйДок <> Неопределено Тогда
			ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	РегламентированнаяОтчетность.ПолучитьСведенияОПоказателяхОтчета(ЭтотОбъект);
	
	Если НЕ СтруктураРеквизитовФормы.ОтображатьКнопкуЗаполнить Тогда
		Элементы.Заполнить.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ СтруктураРеквизитовФормы.ОтображатьКнопкуРасшифровать Тогда
		Элементы.Расшифровать.Видимость = Ложь;
		Элементы.ТабличныйДокументКонтекстноеМенюРасшифровать.Видимость = Ложь;
	КонецЕсли;
	
	ПоказатьПериод();
	
	// формируем список элементов подменю, связанное с кнопкой <Печать>
	Если НЕ БезОткрытияФормы Тогда
		РегламентированнаяОтчетностьКлиентСервер.ПоказатьКнопкиПечати(ЭтотОбъект);
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.Организация <> Неопределено Тогда
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
			СтруктураРеквизитовФормы.Организация, ДатаПодписи, "НаимЮЛПол, НаимЮЛСокр, ОКТМО");
		Если СведенияОбОрганизации <> Неопределено Тогда
			СтруктураРеквизитовФормы.НаимОрганизации = СведенияОбОрганизации.НаимЮЛСокр;
			СтруктураРеквизитовФормы.НаимОрг = СведенияОбОрганизации.НаимЮЛПол;
			СтруктураРеквизитовФормы.ОКТМО = СведенияОбОрганизации.ОКТМО;
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтотОбъект);
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		ЗаполнитьСводныеТаблицы(ЭтотОбъект);
	Иначе
		Если Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
			// Заполняем итоговые разделы нового пустого отчета
			ПодготовитьДанныеРаздела2(ЭтотОбъект);
			СобратьДанныеВРаздел1Сервер();
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически Тогда
		СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения = ЗаполнитьАвтоНаСервере();
		Если СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения.Статус = "Выполнено" Тогда
			ЗагрузитьПодготовленныеДанные();
		ИначеЕсли СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения.Статус = "Ошибка" Тогда
			ВызватьИсключение СтруктураРеквизитовФормы.ДлительнаяОперацияАвтозаполнения.ПодробноеПредставлениеОшибки;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ БезОткрытияФормы Тогда
		ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет в таблицы многостраничных отчетов строки, соответствующие 1-й странице
&НаСервере
Процедура ИнициализацияМногостраничныхРазделов()
	
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		НоваяТаблицаДанных = Отчеты.РегламентированныйОтчет3НДФЛ.НоваяТаблицаДанныхМногостраничногоРаздела();
		ЭтотОбъект[Раздел.Значение].Загрузить(НоваяТаблицаДанных);
	КонецЦикла;
	
КонецПроцедуры

// Выполняет начальные настройки параметров отчета.
// По данным макета "СоствПоказателей" формирует:
//   - состав показателей отчета;
//   - варианты заполнения показателей отчета;
//   - дерево выводимых на печать листов отчета.
//
&НаСервере
Процедура ЗаполнитьНачальныеНастройки()
	
	СоставПоказателей = Отчеты.РегламентированныйОтчет3НДФЛ.СоставПоказателей(СтруктураРеквизитовФормы.мСоставПоказателей);
	
	мТаблицаСоставПоказателей.Загрузить(СоставПоказателей.СоставПоказателей);
	
	Для Каждого ВариантыЗаполнения Из СоставПоказателей.ДанныеВариантовАвтоЗаполнения Цикл
		Для каждого Варианты Из ВариантыЗаполнения.Значение Цикл
			НоваяСтрока = ЭтотОбъект["ТаблицаВариантыЗаполнения" + ВариантыЗаполнения.Ключ].Добавить();
			НоваяСтрока.ТаблицаВариантовЗаполнения.Загрузить(Варианты);
		КонецЦикла;
	КонецЦикла;
	
	мСтруктураВариантыЗаполнения = СоставПоказателей.СтруктураВариантовЗаполнения;
	мСтруктураВариантыЗаполненияЭталон = СоставПоказателей.СтруктураВариантовЗаполнения;
	
КонецПроцедуры

// Формирует дерево значений - структуру страниц отчета,
// содержащей настройки показа страниц, вывода на печать и выгрузки в ИМНС.
//
&НаСервере
Процедура ФормироватьСтруктуруСтраницОтчета() 
	
	ЗначениеВРеквизитФормы(
		Отчеты.РегламентированныйОтчет3НДФЛ.ДеревоСтраницОтчета(СтруктураРеквизитовФормы.мВыбраннаяФорма),
		"мДеревоСтраницОтчета");
	
КонецПроцедуры

// Формирует дерево значений - структуру страниц отчета,
// содержащей настройки многострочности, многостраничности, количества строк и страниц, параметров автозаполнения.
//
&НаСервере
Процедура ФормироватьСтруктуруСвойствСтраниц() Экспорт
	
	ЗначениеВРеквизитФормы(
		Отчеты.РегламентированныйОтчет3НДФЛ.СвойстваРазделовДекларации(СтруктураРеквизитовФормы.мВыбраннаяФорма),
		"мСвойстваРазделовДекларации");
	
КонецПроцедуры

// Добавляет нужное количество строк в таблицы,
// с помощью которых многострочные разделы редактируются в табличном документе
&НаСервере
Процедура НачальныеОперацииСМногострочнымиБлоками()
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	// создаем исходную структуру многострочных разделов
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = ЭтотОбъект[ЭлементСтруктуры.Значение];
		
		мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
		
		Для НСтр = 1 По ИсходноеКоличествоСтрокГруппы Цикл
			
			СтруктураГруппы.Добавить();
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокФормы()
	
	Заголовок = СтрШаблон(НСтр("ru = 'Декларация 3-НДФЛ %1 (%2)'"),
		СтруктураРеквизитовФормы.СтрПериодОтчета,
		СтруктураРеквизитовФормы.НаимОрганизации);
	
	СтруктураРеквизитовФормы.ТекстДляЗаголовочнойЧасти = Заголовок;
	
КонецПроцедуры

#КонецОбласти

#Область ДеревоРазделов

&НаСервере
Процедура СформироватьДеревоРазделовОтчетаНаСервере() Экспорт
	
	СформироватьДеревоРазделовОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДеревоРазделовОтчетаНаКлиенте() Экспорт
	
	СформироватьДеревоРазделовОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Формирует дерево для навигации по разделам и страницам отчета
&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьДеревоРазделовОтчета(Форма)
	Перем ИмяТаблицыСтраницыМногостраничногоРаздела;
	
	Форма["РазделыОтчета"].ПолучитьЭлементы().Очистить();
	
	Для Каждого ЭлементДереваСтраницОтчета Из Форма["мДеревоСтраницОтчета"].ПолучитьЭлементы() Цикл
		
		Если ЭлементДереваСтраницОтчета.ПоказатьСтраницу = 1 Тогда
			
			ЭлементРазделовОтчета = Форма["РазделыОтчета"].ПолучитьЭлементы().Добавить();
			ЭлементРазделовОтчета.КолонкаРазделыОтчета = ЭлементДереваСтраницОтчета.Представление;
			ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			ЭлементРазделовОтчета.РазделМногостраничный = РазделМногостраничный(ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим, Форма);
			ЭлементРазделовОтчета.РазделМногострочный = РазделМногострочный(ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим, Форма);
			ЭлементРазделовОтчета.ИндексКартинки = ?(ЭлементРазделовОтчета.РазделМногостраничный, 0, 1);
			
			Если ЭлементРазделовОтчета.РазделМногостраничный Тогда
				ИмяТаблицыСтраницыМногостраничногоРаздела = Форма.мСтруктураМногостраничныхРазделов[ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим];
				Для НомерСтраницы = 1 По Форма[ИмяТаблицыСтраницыМногостраничногоРаздела].Количество() Цикл
					СтраницаРазделаОтчета = ЭлементРазделовОтчета.ПолучитьЭлементы().Добавить();
					СтраницаРазделаОтчета.КолонкаРазделыОтчета              = СтрШаблон("Стр. %1", НомерСтраницы);
					СтраницаРазделаОтчета.КолонкаРазделыОтчетаСокрНаим      = ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим;
					СтраницаРазделаОтчета.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницы;
					СтраницаРазделаОтчета.ИндексКартинки                    = 1;
					СтраницаРазделаОтчета.РазделМногостраничный             = ЭлементРазделовОтчета.РазделМногостраничный;
					СтраницаРазделаОтчета.РазделМногострочный               = ЭлементРазделовОтчета.РазделМногострочный;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ТабличныйДокумент

&НаСервереБезКонтекста
Функция ИмяМакета(ИмяФормы, ИмяРаздела)
	
	Возврат Сред(ИмяФормы, СтрНайти(ИмяФормы, "ФормаОтчета")) + "_" + ИмяРаздела;
	
КонецФункции

&НаСервере
Процедура УстановитьМаскиВПоляхВвода(ИмяТекРаздела)
	
	ТипДата = Новый ОписаниеТипов("Дата");
	ТипЧисло = Новый ОписаниеТипов("Число");
	
	Число15_2 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	
	Для каждого Область Из ТабличныйДокумент.Области Цикл
		
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Область.СодержитЗначение
			И Не ЗначениеЗаполнено(Область.Формат) Тогда
			
			Если Область.ТипЗначения = ТипЧисло Тогда
				Область.ТипЗначения = Число15_2;
			ИначеЕсли Область.ТипЗначения = ТипДата Тогда
				Область.Формат = "ДФ=dd.MM.yyyy";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает формат, цвет, доступность и прочие свойства областей табличного документа на 
//	основании таблицы "мТаблицаСоставПоказателей". Таблица "мТаблицаСоставПоказателей" 
//	собирается по макету "СоставПоказателей..." при инициализации отчета
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматВыводаВПоляхВвода(Форма, НаимРаздела) Экспорт
	Перем СтруктураГруппы;
	
	Если Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1") Тогда
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = " + Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0";
	ИначеЕсли Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1000") Тогда
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = " + Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3";
	ИначеЕсли Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1000000") Тогда
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = " + Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6";
	КонецЕсли;
	
	Для Каждого СтрПоказатели Из Форма.мТаблицаСоставПоказателей Цикл
		
		Если НЕ НаимРаздела = СтрПоказатели.ИмяПоляТаблДокумента Тогда
			Продолжить;
		КонецЕсли;
		
		КодПоказателя				= СокрП(СтрПоказатели.КодПоказателяПоСоставу);
		ПризнМногострочность		= СокрП(СтрПоказатели.ПризнМногострочности);
		КодПоказателяПоСтруктуре	= СокрП(СтрПоказатели.КодПоказателяПоСтруктуре);
		КодПоказателяПоФорме		= СокрП(СтрПоказатели.КодПоказателяПоФорме);
		ТипДанныхПоказателя			= СокрП(СтрПоказатели.ТипДанныхПоказателя);
		ИмяПоказателя				= ?(ПустаяСтрока(СтрПоказатели.КодПоказателяПоФорме), КодПоказателя, КодПоказателяПоФорме);
		
		Если ПустаяСтрока(КодПоказателя) ИЛИ КодПоказателя = "===" ИЛИ КодПоказателя = "###" ИЛИ Лев(КодПоказателя, 2) = "//" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипДанныхПоказателя = "Е" И ПустаяСтрока(КодПоказателяПоСтруктуре) Тогда
			ТекТабличноеПоле = Форма.ТабличныйДокумент;
			Если ПризнМногострочность <> "/" Тогда
				ФорматируемаяОбласть = ТекТабличноеПоле.Области[ИмяПоказателя];
				ДопустимыйЗнакПоказателя = ФорматируемаяОбласть.ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак;
				ЧислоЕЗнак = Форма.СтруктураРеквизитовФормы.мОписаниеТиповЧислоЕ;
				
				Если ДопустимыйЗнакПоказателя = ДопустимыйЗнак.Неотрицательный Тогда
					ЧислоЕЗнак = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(ЧислоЕЗнак.КвалификаторыЧисла.Разрядность,ЧислоЕЗнак.КвалификаторыЧисла.РазрядностьДробнойЧасти, ДопустимыйЗнакПоказателя));
				КонецЕсли;
				ФорматируемаяОбласть.ТипЗначения = ЧислоЕЗнак;
				ФорматируемаяОбласть.Формат = "ЧЦ = " + ЧислоЕЗнак.КвалификаторыЧисла.Разрядность + Форма.СтруктураРеквизитовФормы.мСтрокаФормата;
			Иначе
				// определяем кол-во строк многострочного раздела и применяем процедуру форматирования к данной графе по всем строкам
				НСтр = 1;
				Пока ТекТабличноеПоле.Области.Найти(ИмяПоказателя + "_" + Формат(НСтр, "ЧГ=0")) <> Неопределено Цикл
					ФорматируемаяОбласть = ТекТабличноеПоле.Области[ИмяПоказателя + "_" + Формат(НСтр, "ЧГ=0")];
					ДопустимыйЗнакПоказателя = ФорматируемаяОбласть.ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак;
					ЧислоЕЗнак = Форма.СтруктураРеквизитовФормы.мОписаниеТиповЧислоЕ;
					Если ДопустимыйЗнакПоказателя = ДопустимыйЗнак.Неотрицательный Тогда
						ЧислоЕЗнак = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(ЧислоЕЗнак.КвалификаторыЧисла.Разрядность,ЧислоЕЗнак.КвалификаторыЧисла.РазрядностьДробнойЧасти, ДопустимыйЗнакПоказателя));
					КонецЕсли;
					ФорматируемаяОбласть.ТипЗначения = ЧислоЕЗнак;
					ФорматируемаяОбласть.Формат = "ЧЦ = " + ЧислоЕЗнак.КвалификаторыЧисла.Разрядность + Форма.СтруктураРеквизитовФормы.мСтрокаФормата;
					НСтр = НСтр + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказателиЗначениямиПоУмолчанию(ИмяРаздела)
	
	Если ИмяРаздела = "Приложение3" Тогда
		
		Если ТабличныйДокумент.Области.Найти("П000500001010") <> Неопределено И ТабличныйДокумент.Области["П000500001010"].Значение = 0 Тогда
			
			ТабличныйДокумент.Области["П000500001010"].Значение = 1;
			СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, "П000500001010", 1);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМакетСтраницыВТабличныйДокументНаСервере(ИмяТекРаздела, НомерСтраницыРазделаОтчета, ЗаголовокРаздела = Ложь)
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела, Ложь);
	
	ВывестиМакетВТаблДокНаСервере(ИмяТекРаздела, НомерСтраницыРазделаОтчета,,,ЗаголовокРаздела);
	
	СтруктураРеквизитовФормы.НаимТекущегоРаздела  = ИмяТекРаздела;
	СтруктураРеквизитовФормы.НомерТекущейСтраницы = НомерСтраницыРазделаОтчета;
	
	РасчетНаСервере(ИмяТекРаздела);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМакетВТаблДокНаСервере(ИмяТекРаздела, НомерСтраницыРазделаОтчета, УдалениеСтраницы = Ложь, ИндексУдаляемойСтраницы = Неопределено, ЗаголовокРаздела = Ложь)
	
	ТабличныйДокумент.Очистить();
	
	ЭтоПоследняяСтраница = Истина;
	ЭтоПерваяСтраница = Истина;
	
	КоличествоПодобныхСтраниц = 0;
	
	Если РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, ИмяТекРаздела, "МногостраничностьВРазделе") = Истина 
		И РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, ИмяТекРаздела, "ПоказыватьИтогиТолькоНаПоследнейСтранице") = Истина Тогда
			
			КоличествоСтраницРаздела = ЭтотОбъект["ТаблицаСтраницы" + ИмяТекРаздела].Количество();
			Если УдалениеСтраницы И ИндексУдаляемойСтраницы + 1 = КоличествоСтраницРаздела Тогда
				ЭтоПоследняяСтраница = Истина;
			Иначе
				ЭтоПоследняяСтраница = КоличествоСтраницРаздела - НомерСтраницыРазделаОтчета = 0;
			КонецЕсли;
			
			ЭтоПерваяСтраница =  НомерСтраницыРазделаОтчета = 1;
			КоличествоПодобныхСтраниц = ЭтотОбъект["ТаблицаСтраницы" + ИмяТекРаздела].Количество() - ?(УдалениеСтраницы ,1,0);
			
	КонецЕсли;
	
	Если ЭтоПерваяСтраница И УдалениеСтраницы И КоличествоПодобныхСтраниц = 0 Тогда
		
		ЭтоПоследняяСтраница = Истина;
		
	КонецЕсли;
	
	ИмяМакета = ИмяМакета(ИмяФормы, ИмяТекРаздела);
	Макет = ОтчетОбъект().ПолучитьМакет(ИмяМакета);
	ТабличныйДокумент.Вывести(Макет);
	
	Если РазделМногострочный(ИмяТекРаздела, ЭтотОбъект) Тогда
		
		НаимТаблицыСтраницы = мСтруктураМногостраничныхРазделов[ИмяТекРаздела];
		
		// Управление видимостью гиперссылки "Удалить страницу"
		КоличествоСтраницРаздела = ЭтотОбъект[НаимТаблицыСтраницы].Количество() - ?(УдалениеСтраницы,1,0);
		Для каждого ТаблицаСтраницы Из ЭтотОбъект[НаимТаблицыСтраницы] Цикл
			Если КоличествоСтраницРаздела = 1 ИЛИ ЗаголовокРаздела Тогда
				Для Каждого Область Из ТабличныйДокумент.Области Цикл
					Если СтрЧислоВхождений(Область.Имя, "УдалитьСтраницу") > 0 Тогда
						Область.Текст = "";
						Область.Гиперссылка = Ложь;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		СтруктураДанных = Новый Структура;
		
		НомерСтраницыРазделаОтчета = Мин(ЭтотОбъект[НаимТаблицыСтраницы].Количество(), НомерСтраницыРазделаОтчета);
		
		СписокДанных = ЭтотОбъект[НаимТаблицыСтраницы][НомерСтраницыРазделаОтчета - 1].Данные;
		
		// Сбрасываем активность всех страниц, активность новой выведенной страницы будет установлена ниже
		// Если выводится предыдущая страница перед удалением, то сбрасывать активность не нужно
		// Активная страница будет удалена, а активность перемещена на предыдущую стрнаицу, которая уже выведена
		Если НЕ УдалениеСтраницы Тогда
			Для каждого ТаблицаСтраницы Из ЭтотОбъект[НаимТаблицыСтраницы] Цикл
				ТаблицаСтраницы.АктивнаяСтраница = Ложь;
			КонецЦикла;
		КонецЕсли;
	
		// Устанавливаем признак активной страницы только если это не удаление страницы
		// При удалении страницы все происходит по другому, сначала мы показываем предыдущую
		// не активную страницу, и только затем удаляем активную, поэтому в режиме удаления страницы
		// не устанавливаем активность на выведенной странице, активность будет перемещена 
		// на выведенную сейчас страницу после удаления активной страницы (которую удаляем)
		ЭтотОбъект[НаимТаблицыСтраницы][НомерСтраницыРазделаОтчета - 1].АктивнаяСтраница = ?(ИндексУдаляемойСтраницы = 0 ,Истина ,НЕ УдалениеСтраницы);
		
		Если СписокДанных.Количество() > 0 Тогда
			СтруктураДанных = СписокДанных[0].Значение;
		КонецЕсли;
		
		Для Каждого ЭлСтруктуры Из СтруктураДанных Цикл
			
			Если ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ) <> Неопределено Тогда
				ТабличныйДокумент.Области[ЭлСтруктуры.Ключ].Значение = ЭлСтруктуры.Значение;
			КонецЕсли; 
			
		КонецЦикла;
		
		ИмяТаблицыСтраницРаздела = мСтруктураМногостраничныхРазделов[ИмяТекРаздела];
		
		ТаблицаСтраницРаздела = ЭтотОбъект[ИмяТаблицыСтраницРаздела];
		
		СтрСледующаяСтраница = ТаблицаСтраницРаздела[НомерСтраницыРазделаОтчета - 1];
		
		// Устанавливаем признак активной страницы только если это не удаление страницы
		СтрСледующаяСтраница.АктивнаяСтраница = ?(ИндексУдаляемойСтраницы = 0 ,Истина ,НЕ УдалениеСтраницы);
		
		ТаблицаВариантыЗаполнения = РегламентированнаяОтчетностьВызовСервера.ПолучитьТаблицуВариантовЗаполненияНаСервере(ЭтотОбъект, ИмяТекРаздела);
		
		НовДанныеДопСтрокРаздела = СтрСледующаяСтраница.ДанныеДопСтрок;
		
		Если (ТипЗнч(НовДанныеДопСтрокРаздела) = Тип("СписокЗначений"))
			И (НовДанныеДопСтрокРаздела.Количество() > 0)
			И (НовДанныеДопСтрокРаздела[0].Представление = "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела") Тогда
			
			НовДанныеДопСтрокРаздела = НовДанныеДопСтрокРаздела[0].Значение;
			
		Иначе
			
			НовДанныеДопСтрокРаздела = Неопределено;
			
		КонецЕсли;
		
		// Способ хранения многострочных частей
		СохранятьМногострКакТЗ = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, ИмяТекРаздела, "СохранятьМногострКакТЗ");
		
		Если НовДанныеДопСтрокРаздела <> Неопределено Тогда
			
			Если СохранятьМногострКакТЗ Тогда
				
				ИмяГруппыДопСтрок = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ИмяТекРаздела);
				
				ПромТаблицаДопСтрокРаздела = ЭтотОбъект["Хранилище" + ИмяГруппыДопСтрок].Добавить().Таблица;
				
				КопироватьДанныеФормы(НовДанныеДопСтрокРаздела, ПромТаблицаДопСтрокРаздела);
				
				// Заменяем таблицу значений с данными текущей страницы.
				КопироватьДанныеФормы(ПромТаблицаДопСтрокРаздела, ЭтотОбъект[ИмяГруппыДопСтрок]);
				
				// Выводим в табличный документ данные группы дополнительных строк и обновляем структуру многострочного раздела
				ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппыДопСтрок, ПромТаблицаДопСтрокРаздела);
				ОбновитьСтруктуруМногострочногоРазделаНаСервере(ИмяГруппыДопСтрок);
				
			Иначе
				
				Для Каждого ГруппаСтрок Из НовДанныеДопСтрокРаздела Цикл
					ИмяГруппыДопСтрок = ГруппаСтрок.Ключ;
					ДанныеГруппыСтрок = ГруппаСтрок.Значение;
					
					ПромТаблицаГруппыСтрок = ЭтотОбъект["Хранилище" + ИмяГруппыДопСтрок].Добавить().Таблица;
					
					КопироватьДанныеФормы(ДанныеГруппыСтрок, ПромТаблицаГруппыСтрок);
					
					// Заменяем таблицу значений с данными текущей страницы.
					КопироватьДанныеФормы(ПромТаблицаГруппыСтрок, ЭтотОбъект[ИмяГруппыДопСтрок]);
					
					ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппыДопСтрок, ПромТаблицаГруппыСтрок);
					ОбновитьСтруктуруМногострочногоРазделаНаСервере(ИмяГруппыДопСтрок);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ОтрисоватьЗначкиУдаленияСтрок(ТабличныйДокумент);
		
	ИначеЕсли РазделМногостраничный(ИмяТекРаздела, ЭтотОбъект) Тогда
		
		НаимТаблицыСтраницы = мСтруктураМногостраничныхРазделов[ИмяТекРаздела];
		
		// Управление видимостью гиперссылки "Удалить страницу"
		КоличествоСтраницРаздела = ЭтотОбъект[НаимТаблицыСтраницы].Количество() - ?(УдалениеСтраницы,1,0);
		Для каждого ТаблицаСтраницы Из ЭтотОбъект[НаимТаблицыСтраницы] Цикл
			Если КоличествоСтраницРаздела = 1 ИЛИ ЗаголовокРаздела Тогда
				Для Каждого Область Из ТабличныйДокумент.Области Цикл
					Если СтрЧислоВхождений(Область.Имя, "УдалитьСтраницу") > 0 Тогда
						Область.Текст = "";
						Область.Гиперссылка = Ложь;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		СтруктураДанных = Новый Структура;
		
		СписокДанных = ЭтотОбъект[НаимТаблицыСтраницы][НомерСтраницыРазделаОтчета - 1].Данные;
		
		// Сбрасываем активность всех страниц, активность новой выведенной страницы будет установлена ниже
		// Если выводится предыдущая страница перед удалением, то сбрасывать активность не нужно
		// Активная страница будет удалена, а активность перемещена на предыдущую страницу, которая уже выведена
		Если НЕ УдалениеСтраницы Тогда
			Для каждого ТаблицаСтраницы Из ЭтотОбъект[НаимТаблицыСтраницы] Цикл
				ТаблицаСтраницы.АктивнаяСтраница = Ложь;
			КонецЦикла;
		КонецЕсли;
		// Устанавливаем признак активной страницы только если это не удаление страницы
		// При удалении страницы все происходит по другому, сначала мы показываем предыдущую
		// не активную страницу, и только затем удаляем активную, поэтому в режиме удаления страницы
		// не устанавливаем активность на выведенной странице, активность будет перемещена 
		// на выведенную сейчас страницу после удаления активной страницы (которую удаляем)
		ЭтотОбъект[НаимТаблицыСтраницы][НомерСтраницыРазделаОтчета - 1].АктивнаяСтраница = ?(ИндексУдаляемойСтраницы = 0 ,Истина ,НЕ УдалениеСтраницы);
		
		Если СписокДанных.Количество() > 0 Тогда
			СтруктураДанных = СписокДанных[0].Значение;
		КонецЕсли;
		
		Для Каждого ЭлСтруктуры Из СтруктураДанных Цикл
			Если ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ) <> Неопределено Тогда
				ТабличныйДокумент.Области[ЭлСтруктуры.Ключ].Значение = ЭлСтруктуры.Значение;
			КонецЕсли; 
		КонецЦикла;
		
	Иначе
		
		Для Каждого ЭлСтруктуры Из ЭтотОбъект["СтруктураДанных" + ИмяТекРаздела] Цикл
			Если ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ) <> Неопределено Тогда
				ТабличныйДокумент.Области[ЭлСтруктуры.Ключ].Значение = ЭлСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	УстановитьМаскиВПоляхВвода(ИмяТекРаздела);
	
	УстановитьФорматВыводаВПоляхВводаНаСервере(ИмяТекРаздела);
	
	УстановитьЦветФонаЯчейкиНаСервере(,, ИмяТекРаздела);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтрисоватьЗначкиУдаленияСтрок(ТабДок)
	
	МассивОбластейСДопСтроками = Новый Массив;
	
	Для Каждого ОбластьТД Из ТабДок.Области Цикл
		Если СтрЧислоВхождений(ОбластьТД.Имя, "УдалитьСтроку") > 0 Тогда
			Если СтрЧислоВхождений(ОбластьТД.Имя, "_2") > 0 Тогда
				МассивОбластейСДопСтроками.Добавить(Лев(ОбластьТД.Имя, 26));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбластьТД Из ТабДок.Области Цикл
		Если СтрЧислоВхождений(ОбластьТД.Имя, "УдалитьСтроку") > 0 Тогда
			Если МассивОбластейСДопСтроками.Найти(Лев(ОбластьТД.Имя, 26)) = Неопределено Тогда
				ОбластьТД.Гиперссылка = Ложь;
				ОбластьТД.Текст = "";
			Иначе
				ОбластьТД.Гиперссылка = Истина;
				ОбластьТД.Текст = "х";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветФонаЯчейкиНаКлиенте(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	
	УстановитьЦветФонаЯчейки(ЭтотОбъект, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

// Процедура управляет цветом фона автозаполняемых ячеек при:
//  а) автозаполнении;
//  б) ручном выборе другого варианта заполнения;
//  в) добавлении/удалении и листании дополнительных страниц.
//
// Цвет фона устанавливается в зависимости от варианта заполнения ячейки.
//
// Параметры:
//  ИмяЯчейки                - строка, имя ячейки, для которой устанавливаем цвет фона;
//  ВариантЗаполнения        - число,  вариант заполнения ячейки;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели, на которой для всех автозаполняемых
//                             ячеек табличного документа следует установить цвет фона.
//
&НаСервере
Процедура УстановитьЦветФонаЯчейкиНаСервере(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	
	УстановитьЦветФонаЯчейки(ЭтотОбъект, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

// Процедура управляет цветом фона автозаполняемых ячеек при:
//  а) автозаполнении;
//  б) ручном выборе другого варианта заполнения;
//  в) добавлении/удалении и листании дополнительных страниц.
//
// Цвет фона устанавливается в зависимости от варианта заполнения ячейки.
//
// Параметры:
//  ИмяЯчейки                - строка, имя ячейки, для которой устанавливаем цвет фона;
//  ВариантЗаполнения        - число,  вариант заполнения ячейки;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели, на которой для всех автозаполняемых
//                             ячеек табличного документа следует установить цвет фона.
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЦветФонаЯчейки(Форма, ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	
	Перем ЦветФона;
	Перем Комментарий;
	
	// Не нужно разукрашивать табличные поля, если открывается без открытия формы (например печать или просмотр)
	Если Форма.СтруктураРеквизитовФормы.мБезОткрытияФормы ИЛИ Форма.СтруктураРеквизитовФормы.мРежимПечати Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИмяЯчейки = Неопределено Тогда
		// устанавливаем цвет фона конкретной ячейки
		#Если НаКлиенте Тогда
			ИмяСтраницыПанели = РегламентированнаяОтчетностьКлиент.ОпределитьСтраницуПанелиПоКодуПоказателяНаКлиенте(Форма, ИмяЯчейки);
		#Иначе
			ИмяСтраницыПанели = РегламентированнаяОтчетностьВызовСервера.ОпределитьСтраницуПанелиПоКодуПоказателяНаСервере(Форма, ИмяЯчейки);
		#КонецЕсли
		Если ИмяСтраницыПанели = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИмяПоляТаблДокумента = "ТабличныйДокумент";
		
		Ключ = "_" + ВариантЗаполнения;
		Форма.мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
		
		// КодПоказателяПоСтруктуре
		ЗначениеВКвадратах = РегламентированнаяОтчетностьКлиентСервер.ОбластьЯвляетсяЯчейкойСоставногоПоля(Форма, ИмяЯчейки);
		
		Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
			
			Если ЗначениеВКвадратах Тогда
				// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
				РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяЯчейки, "УстановитьЗащиту");
			Иначе
				Форма[ИмяПоляТаблДокумента].Области[ИмяЯчейки].Защита = Истина;
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеВКвадратах Тогда
				// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
				РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяЯчейки, "СнятьЗащиту");
			Иначе
				Форма[ИмяПоляТаблДокумента].Области[ИмяЯчейки].Защита = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		// установим цвет ячейки
		Если ЗначениеВКвадратах Тогда
			РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяЯчейки, "ПроставитьЦветРаскраски", ЦветФона);
		Иначе
			Форма[ИмяПоляТаблДокумента].Области[ИмяЯчейки].ЦветФона = ЦветФона;
		КонецЕсли;
		
	Иначе
		// устанавливаем цвет фона всех автозаполняемых ячеек активной страницы
		
		#Если НаКлиенте Тогда
			ТаблицаВариантыЗаполнения = РегламентированнаяОтчетностьКлиент.ПолучитьТаблицуВариантовЗаполненияНаКлиенте(Форма, ИмяТекущейСтраницыПанели);
		#Иначе
			ТаблицаВариантыЗаполнения = РегламентированнаяОтчетностьВызовСервера.ПолучитьТаблицуВариантовЗаполненияНаСервере(Форма, ИмяТекущейСтраницыПанели);
		#КонецЕсли
		
		Если ТаблицаВариантыЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого Строка Из ТаблицаВариантыЗаполнения Цикл
			ИмяПоказателя            =  Строка.КодПоказателя;
			ВариантЗаполнения        =  Строка.ВариантЗаполнения;
			Комментарий              =  Строка.Комментарий;
			КодПоказателяПоСтруктуре =  Строка.КодПоказателяПоСтруктуре;
			
			КолвоСтрокВМногострочнойЧасти = РегламентированнаяОтчетностьКлиентСервер.ПоказательВходитВМногострочнуюЧасть(Форма, ИмяПоказателя);

			ИмяГруппы = ИмяГруппы(ИмяПоказателя);
			ПоказательМногострочный = Ложь;
			Если Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы) Тогда
				Для Каждого СтруктураГруппы Из Форма[ИмяГруппы] Цикл
					ПоказательМногострочный = СтруктураГруппы.Свойство(ИмяПоказателя);
					Прервать;
				КонецЦикла;
			КонецЕсли;
			
			Если КолвоСтрокВМногострочнойЧасти > 0 И ПоказательМногострочный Тогда
				Многострочность = Истина;
				БазоваяЧасть = ИмяПоказателя;
				ОписанияБлоков = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(Форма, ИмяТекущейСтраницыПанели, "МногострочностьВРазделе");
				
				Если ТипЗнч(ОписанияБлоков) = Тип("Число") Тогда
					ВБлокеВсеПоказателиЗаполняются = Истина;
				ИначеЕсли ТипЗнч(ОписанияБлоков) = Тип("ДанныеФормыКоллекция") Тогда
					ИмяГруппы = ИмяГруппы(ИмяПоказателя);
					ВБлокеВсеПоказателиЗаполняются = НЕ ОписанияБлоков.НайтиСтроки(Новый Структура("ИдГруппы", ИмяГруппы))[0].НезаполняемыеПоказателиЕсть;
				КонецЕсли;
				
			Иначе
				Многострочность = Ложь;
				ВБлокеВсеПоказателиЗаполняются = Истина;
				КолвоСтрокВМногострочнойЧасти = 1;
			КонецЕсли;
			
			Для Инд = 1 по КолвоСтрокВМногострочнойЧасти Цикл
				
				Если Многострочность Тогда
					ИмяПоказателя = БазоваяЧасть + "_" + Формат(Инд, "ЧГ=0");
				КонецЕсли;
				
				Если Форма.ТабличныйДокумент.Области.Найти(ИмяПоказателя) <> Неопределено Тогда
					
					ТекОбластьЯчеек = Форма.ТабличныйДокумент.Области[ИмяПоказателя];
					Если (Не ПустаяСтрока(Комментарий)) ИЛИ (Не ПустаяСтрока(ТекОбластьЯчеек.Примечание.Текст)) Тогда
						ТекОбластьЯчеек.Примечание.Текст = Комментарий;
					КонецЕсли;
					
					Ключ = "_" + ВариантЗаполнения;
					Форма.мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
					
					ЗначениеВКвадратах = ?(ПустаяСтрока(КодПоказателяПоСтруктуре), Ложь, Истина);
					
					Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
						
						Если ЗначениеВКвадратах Тогда
							// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
							РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяПоказателя, "УстановитьЗащиту");
						Иначе
							Форма.ТабличныйДокумент.Области[ИмяПоказателя].Защита = Истина;
						КонецЕсли;
						
					Иначе
						
						Если ЗначениеВКвадратах Тогда
							// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
							РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяПоказателя, "СнятьЗащиту");
						Иначе
							Форма.ТабличныйДокумент.Области[ИмяПоказателя].Защита = Ложь;
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеВКвадратах Тогда
						РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяПоказателя, "ПроставитьЦветРаскраски", ЦветФона);
					Иначе
						Форма.ТабличныйДокумент.Области[ИмяПоказателя].ЦветФона = ЦветФона;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаИзмененияСодержимогоВПолеТабличногоДокументаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ИмяПоказателя = ДополнительныеПараметры.ИмяПоказателя;
	ИмяРаздела = ДополнительныеПараметры.ИмяРаздела;
	НовыйПоМесту = ДополнительныеПараметры.НовыйПоМесту;
	Область = ДополнительныеПараметры.Область;
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		//Пользователь отказался очистить отчет - возврат к старому значению
		ТабличныйДокумент.Области.ПоМесту.Значение = СтруктураРеквизитовФормы.мПоМесту;
		Возврат;
	Иначе 
		//Пользователь согласился очистить отчет
		СтруктураРеквизитовФормы.мПоМесту = НовыйПоМесту;	
		
		ОчиститьНаСервере();
		
		РегламентированнаяОтчетностьКлиент.ОбновитьДеревоРазделовВФормеОтчета(ЭтотОбъект);
		СтруктураРеквизитовФормы.НомерТекущейСтраницы = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
		
	КонецЕсли;
	РасчетПослеИзмененияСодержания(ИмяПоказателя, ИмяРаздела, Область);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДатуПодписи(Форма)
	
	ДатаПодписи = ТекущаяДатаСеанса();
	
	Форма.ДатаПодписи = ДатаПодписи;
	Форма.СтруктураДанныхТитульный.ДатаПодписи = ДатаПодписи;
	УстановитьЗначениеПоля(Форма.ТабличныйДокумент, "ДатаПодписи", ДатаПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетПослеИзмененияСодержания(ИмяПоказателя, ИмяРаздела, Область)
	
	Если (ИмяПоказателя = "П000400001040" Или ИмяПоказателя = "П000400001080")
		И Строка(ТабличныйДокумент.Области["П000400001031"].Значение) <> КодВидаДоходаЗаПределамиРФ("ПрибыльКИК", СтруктураРеквизитовФормы.мВыбраннаяФорма) Тогда
		
		Если ИмяПоказателя = "П000400001040" Тогда // Дата получения дохода
			Курс = ПолучитьКурсВалютыНаДату(ТабличныйДокумент.Области["П000400001030"].Значение, Область.Значение);
			ИмяПоказателяКурса = "П000400001050";
			ТабличныйДокумент.Области[ИмяПоказателяКурса].Значение = Курс;
			СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяПоказателяКурса, Курс);
		КонецЕсли;
		
		Если ИмяПоказателя = "П000400001080" Тогда // Дата уплаты налога
			Курс = ПолучитьКурсВалютыНаДату(ТабличныйДокумент.Области["П000400001030"].Значение, Область.Значение);
			ИмяПоказателяКурса = "П000400001090";
			ТабличныйДокумент.Области[ИмяПоказателяКурса].Значение = Курс;
			СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяПоказателяКурса, Курс);
		КонецЕсли;
	КонецЕсли;
	
	//действия, связанные с показателями, обладающими списками выбора,
	//на элементы которых наложены условия (макет "Списки...")
	
	Если ИмяПоказателя = "СтатусНалогоплательщика" Тогда
		ОбновитьПеременныеМодуляНаКлиенте("СтатусНалогоплательщика",Область.Значение);
	КонецЕсли;
	
	//расчет выполняется в структуре, обновляем каждое значение после ввода
	
	Если ИмяРаздела = "Титульный"
		И ИмяПоказателя = "Фамилия"
		Или ИмяПоказателя = "Имя"
		Или ИмяПоказателя = "Отчество"
		Или ИмяПоказателя = "ИНН"
		Тогда
		СохранитьЗначениеПоказателяВСтруктуреНаСервере(ИмяРаздела, ИмяПоказателя, Область.Значение);
	Иначе
		СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяПоказателя, Область.Значение);
	КонецЕсли;
	
	Если НЕ ИмяРаздела = "Титульный" Тогда
		
		РасчетНаКлиенте(ИмяРаздела, ИмяПоказателя);
		
		СтруктураРеквизитовФормы.НужноРассчитатьОтчетНаСервере = Истина; //Сделаем пересчет на сервере всех разделов
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.РазделыОтчета.ТекущиеДанные;
	
	КопияТекущиеДанные = ТекущиеДанные;
	
	ТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоСтраницТекущегоРаздела = ТекущиеДанные.ПолучитьЭлементы().Количество();
	
	Если КоличествоСтраницТекущегоРаздела = 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтраницаРазделаПослеУдаления = Неопределено;
	Если КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = КоличествоСтраницТекущегоРаздела Тогда
		ТекущаяСтраницаРазделаПослеУдаления = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(ТекущиеДанные.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета - 1);
		СтруктураРеквизитовФормы.НомерТекущейСтраницы = ТекущаяСтраницаРазделаПослеУдаления.КолонкаНомерСтраницыРазделаОтчета;
	КонецЕсли;
	
	НаимРаздела = КопияТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	//Для многостраничных разделов и разделов с итогами не на каждой странице, может понадобиться другой макет
	Если РазделМногостраничный(НаимРаздела, ЭтотОбъект) Тогда
		
		НомерВыводимойСтраницы = СтруктураРеквизитовФормы.НомерТекущейСтраницы;
		Если ТекущаяСтраницаРазделаПослеУдаления = Неопределено Тогда
			НомерВыводимойСтраницы = ?(СтруктураРеквизитовФормы.НомерТекущейСтраницы = КоличествоСтраницТекущегоРаздела, КоличествоСтраницТекущегоРаздела, СтруктураРеквизитовФормы.НомерТекущейСтраницы + 1);
		КонецЕсли;
	
		ВывестиМакетВТаблДокНаСервере(НаимРаздела, НомерВыводимойСтраницы, Истина, ТекущиеДанные.ПолучитьЭлементы().Индекс(КопияТекущиеДанные));
		РасчетНаКлиенте(НаимРаздела);
	КонецЕсли;
	
	ИндексУдаляемойСтраницы = ТекущиеДанные.ПолучитьЭлементы().Индекс(КопияТекущиеДанные);
	
	УдалитьДополнительнуюСтраницуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим, ИндексУдаляемойСтраницы);
	
	ТекущиеДанные.ПолучитьЭлементы().Удалить(ИндексУдаляемойСтраницы);
	
	НомерСтраницы = 1;
	
	Для Каждого СтраницаРаздела Из ТекущиеДанные.ПолучитьЭлементы() Цикл
		
		СтраницаРаздела.КолонкаРазделыОтчета              = "Стр. " + НомерСтраницы;
		СтраницаРаздела.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницы;
		
		НомерСтраницы = НомерСтраницы + 1;
		
	КонецЦикла;
	
	Если НЕ ТекущаяСтраницаРазделаПослеУдаления = Неопределено Тогда
		Элементы.РазделыОтчета.ТекущаяСтрока = ТекущаяСтраницаРазделаПослеУдаления.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Команда = ДополнительныеПараметры.Команда;
	
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДобавитьСтраницу(Команда);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьДополнительнуюСтроку(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОпределитьТабличноеПолеПоИдМногострочногоРазделаНаКлиенте(ИдГруппы) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдРаздел1", СтруктураРеквизитовФормы.мИдРаздел1);
	ИдГрупп.Вставить("мИдПриложение1", СтруктураРеквизитовФормы.мИдПриложение1);
	ИдГрупп.Вставить("мИдПриложение1Расчет", СтруктураРеквизитовФормы.мИдПриложение1Расчет);
	ИдГрупп.Вставить("мИдПриложение8", СтруктураРеквизитовФормы.мИдПриложение8);
	
	Возврат ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы, ИдГрупп);
	
КонецФункции

&НаСервере
Функция ОпределитьТабличноеПолеПоИдМногострочногоРазделаНаСервере(ИдГруппы) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдРаздел1", СтруктураРеквизитовФормы.мИдРаздел1);
	ИдГрупп.Вставить("мИдПриложение1", СтруктураРеквизитовФормы.мИдПриложение1);
	ИдГрупп.Вставить("мИдПриложение1Расчет", СтруктураРеквизитовФормы.мИдПриложение1Расчет);
	ИдГрупп.Вставить("мИдПриложение8", СтруктураРеквизитовФормы.мИдПриложение8);
	
	Возврат ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы, ИдГрупп);
	
КонецФункции

// Функция определяет поле табличного документа формы, содержащее 
// многострочный раздел.по имени ИдГруппы.
//
// Параметры:
//  ИдГруппы - строка, имя многострочного раздела.
//
// Возвращаемое значение:
//  Поле табличного документа.
//
&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы, ИдГрупп) Экспорт
	
	Перем ТекТабличноеПоле;
	
	Если ИдГруппы = ИдГрупп.мИдРаздел1 Тогда
		ТекТабличноеПоле = "Раздел1";
	ИначеЕсли ИдГруппы = ИдГрупп.мИдПриложение1 Тогда
		ТекТабличноеПоле = "Приложение1";
	ИначеЕсли ИдГруппы = ИдГрупп.мИдПриложение1Расчет Тогда
		ТекТабличноеПоле = "Приложение1Расчет";
	ИначеЕсли ИдГруппы = ИдГрупп.мИдПриложение8 Тогда
		ТекТабличноеПоле = "Приложение8";
	КонецЕсли;
	
	Возврат ТекТабличноеПоле;
	
КонецФункции

&НаСервере
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ИмяЛиста = Неопределено) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдРаздел1", СтруктураРеквизитовФормы.мИдРаздел1);
	ИдГрупп.Вставить("мИдПриложение1", СтруктураРеквизитовФормы.мИдПриложение1);
	ИдГрупп.Вставить("мИдПриложение1Расчет", СтруктураРеквизитовФормы.мИдПриложение1Расчет);
	ИдГрупп.Вставить("мИдПриложение8", СтруктураРеквизитовФормы.мИдПриложение8);
	
	Возврат ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста, , ИдГрупп);
	
КонецФункции

&НаКлиенте
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ИмяЛиста = Неопределено) Экспорт
	
	ИдГрупп = Новый Структура;
	ИдГрупп.Вставить("мИдРаздел1", СтруктураРеквизитовФормы.мИдРаздел1);
	ИдГрупп.Вставить("мИдПриложение1", СтруктураРеквизитовФормы.мИдПриложение1);
	ИдГрупп.Вставить("мИдПриложение1Расчет", СтруктураРеквизитовФормы.мИдПриложение1Расчет);
	ИдГрупп.Вставить("мИдПриложение8", СтруктураРеквизитовФормы.мИдПриложение8);
	
	Возврат ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста, , ИдГрупп);
	
КонецФункции

// Функция определяет многострочный раздел табличного документа
// на активной странице основной панели формы.
//
// Параметры:
//  Нет;
//
// Возвращаемое значение:
//  строка, имя многострочного раздела.
//
&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста = Неопределено, ТекущаяСтраница, ИдГрупп)
	Перем ИмяГруппы;
	
	Если ИмяЛиста = Неопределено Тогда
		ИмяТекущейСтраницыПанели = ТекущаяСтраница.Имя;
	Иначе
		ИмяТекущейСтраницыПанели = ИмяЛиста;
	КонецЕсли;
	
	Если ИмяТекущейСтраницыПанели = "Раздел1" Тогда
		ИмяГруппы = ИдГрупп.мИдРаздел1;
	ИначеЕсли ИмяТекущейСтраницыПанели = "Приложение1" Тогда
		ИмяГруппы = ИдГрупп.мИдПриложение1;
	ИначеЕсли ИмяТекущейСтраницыПанели = "Приложение1Расчет" Тогда
		ИмяГруппы = ИдГрупп.мИдПриложение1Расчет;
	ИначеЕсли ИмяТекущейСтраницыПанели = "Приложение8" Тогда
		ИмяГруппы = ИдГрупп.мИдПриложение8;
	КонецЕсли;
	
	Возврат ИмяГруппы;
	
КонецФункции

&НаСервере
Процедура ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппы, СтруктураГруппы) Экспорт
	
	ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРазделВТабличныйДокументНаКлиенте(ИмяГруппы, СтруктураГруппы) Экспорт
	
	ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы)
	Перем ИсходноеКоличествоСтрокГруппы;
	
	ТекущаяСтраница = ОпределитьТабличноеПолеПоИдМногострочногоРазделаНаСервере(ИмяГруппы);
	
	// определим исходное колво строк многострочного раздела,
	// соответствующее бумажной форме отчета
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	// реальное кол-во строк (колво заполненных строк)
	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();
	
	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = ЭтотОбъект[ТекТабличноеПолеИмя];
	
	ПерерисоватьСтроки = (НЕ ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы);
	
	Раздел = ЭтотОбъект[ТекТабличноеПолеИмя];
	
	НастройкиМнгЧ = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, ТекущаяСтраница, "МногострочностьВРазделе").НайтиСтроки(Новый Структура("ИдГруппы", ИмяГруппы))[0];
	ВысотаСтроки = НастройкиМнгЧ.ВысотаСтроки;
	
	// При восстановлении сохраненных данных отчета сравниваем исходное кол-во
	// отображаемых в форме строк многострочного раздела с реальным количеством
	// созданных строк в сохраненном отчете.
	Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда
		
		// При превышении реального кол-ва строк над исходным кол-вом (по шаблону),
		// автоматически добавляем необходимое колво строк.
		ДобавитьНеобходимоеКоличествоДопСтрокНаСервере(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИсходноеКоличествоСтрокГруппы, ТекущееКоличествоСтрокГруппы, ИмяГруппы, СтруктураГруппы, ВысотаСтроки);
		
		ЭтотОбъект[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
		
		ОтрисоватьЗначкиУдаленияСтрок(ТекТабличноеПоле);
		
	КонецЕсли;
	
	// Выводим значения строк
	СписокКолонок = СтруктураГруппы.Выгрузить().Колонки;
	Для НомерСтроки = 0 По СтруктураГруппы.Количество() - 1 Цикл
		СтрокаТаблицы = СтруктураГруппы[НомерСтроки];
		НомерСтрокиВМакете = Формат(НомерСтроки + 1, "ЧГ=0");
		
		Для Каждого Колонка Из СписокКолонок Цикл
			
			Если СтруктураГруппы[0].Свойство(Колонка.Имя) Тогда
				
				ЗначениеЯчейки = СтрокаТаблицы[Колонка.Имя];
				Если ТипЗнч(ЗначениеЯчейки) = Тип("СписокЗначений") Тогда
					Для НомерСтрокиСписка = 0 По ЗначениеЯчейки.Количество() - 1 Цикл
						ИмяЯчейки = СтрШаблон("%1_%2_%3", Колонка.Имя, НомерСтрокиВМакете, НомерСтрокиСписка + 1);
						УстановитьЗначениеПоля(ТекТабличноеПоле, ИмяЯчейки, ЗначениеЯчейки[НомерСтрокиСписка].Значение);
					КонецЦикла;
				Иначе
					ИмяЯчейки = СтрШаблон("%1_%2", Колонка.Имя, НомерСтрокиВМакете);
					УстановитьЗначениеПоля(ТекТабличноеПоле, ИмяЯчейки, ЗначениеЯчейки);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматВыводаВПоляхВводаНаСервере(НаимРаздела)
	
	УстановитьФорматВыводаВПоляхВвода(ЭтотОбъект, НаимРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФорматВыводаВПоляхВводаНаКлиенте(НаимРаздела)
	
	УстановитьФорматВыводаВПоляхВвода(ЭтотОбъект, НаимРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТабличноеПолеНаКлиенте(ВыбТабличноеПолеИмя) Экспорт
	
	ОчиститьТабличноеПоле(ЭтотОбъект, ВыбТабличноеПолеИмя);
	
	Если РазделМногострочный(ВыбТабличноеПолеИмя, ЭтотОбъект) Тогда
		ОтрисоватьЗначкиУдаленияСтрок(ЭтотОбъект.ТабличныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличноеПолеНаСервере(ВыбТабличноеПолеИмя) Экспорт
	
	ОчиститьТабличноеПоле(ЭтотОбъект, ВыбТабличноеПолеИмя);
	
	Если РазделМногострочный(ВыбТабличноеПолеИмя, ЭтотОбъект) Тогда
		ОтрисоватьЗначкиУдаленияСтрок(ЭтотОбъект.ТабличныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

// Процедура очищает содержимое редактируемых и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьТабличноеПоле(Форма, ВыбТабличноеПолеИмя)
	
	РегламентированнаяОтчетностьКлиентСервер.ОчиститьТабличноеПоле(Форма, ВыбТабличноеПолеИмя);
	
КонецПроцедуры

&НаСервере
Процедура ОчисткаРегламентированногоОтчетаНаСервере(ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста, РежимОчистки, Многостраничность, КолВоСтраницВРазделе) Экспорт
	
	УдалятьсяВсеДопЭкземпляры = Ложь;
	
	Если СтрНайти(РежимОчистки, "ОчиститьТекущийРаздел") > 0 Тогда
		
		Если (Многостраничность) И (КолВоСтраницВРазделе > 1) Тогда
			
			ОчиститьПоказателиОтчетаЗаполняемыеИзИБНаСервере(ИмяСтраницы);
			
			УдалятьсяВсеДопЭкземпляры = Истина;
			
		Иначе
			
			ОчиститьТабличноеПолеНаСервере(ИмяСтраницы);
			
		КонецЕсли;
		
		СформироватьДеревоРазделовОтчетаНаСервере();
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;

	ИначеЕсли СтрНайти(РежимОчистки, "ОчиститьТекущийЭкземплярМногостраничногоРаздела") > 0 Тогда
		
		ОчиститьТабличноеПолеСпец(ИмяСтраницы);
		
	КонецЕсли;
	
	Если ИмяСтраницы = "Титульный" Тогда
		Если Элементы.Найти("ДатаПодписи") = Неопределено Тогда
			Область = ТабличныйДокумент.Области.Найти("ДатаПодписи");
			Если НЕ Область = Неопределено Тогда
				Если ТипЗнч(Область.Значение) = Тип("Строка") Тогда
					ДатаПодписи = Лев(СтрЗаменить(Область.Значение, ".", ""), 8);
				ИначеЕсли ТипЗнч(Область.Значение) = Тип("Дата") Тогда
					Если ЗначениеЗаполнено(Область.Значение) Тогда
						ДатаПодписи = Лев(СтрЗаменить(Формат(Область.Значение, "ДФ = ""дд.ММ.гггг 'г.'"""), ".", ""), 8);
					Иначе
						ДатаПодписи = "        ";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиентСервер.ПослеОчисткиРегламентированногоОтчета(ЭтотОбъект, ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаРегламентированногоОтчетаНаКлиенте(ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста, РежимОчистки, Многостраничность, КолВоСтраницВРазделе) Экспорт
	
	УдалятьсяВсеДопЭкземпляры = Ложь;
	
	Если СтрНайти(РежимОчистки, "ОчиститьТекущийРаздел") > 0 Тогда
		
		Если (Многостраничность) И (КолВоСтраницВРазделе > 1) Тогда
			
			РегламентированнаяОтчетностьКлиентСервер.ОчиститьПоказателиОтчетаЗаполняемыеИзИБ(ЭтотОбъект, ИмяСтраницы);
			
			УдалятьсяВсеДопЭкземпляры = Истина;
			
		Иначе
			
			ОчиститьТабличноеПолеНаКлиенте(ИмяСтраницы);
			
		КонецЕсли;
		
	ИначеЕсли СтрНайти(РежимОчистки, "ОчиститьТекущийЭкземплярМногостраничногоРаздела") > 0 Тогда
		
		ОчиститьТабличноеПолеСпец(ИмяСтраницы);
		
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиентСервер.ПослеОчисткиРегламентированногоОтчета(ЭтотОбъект, ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста);
	
	СформироватьДеревоРазделовОтчетаНаКлиенте();
	
КонецПроцедуры

// СпециальныеОперацииПослеОчисткиЛистаНаСервере
// Процедура вызывается из модуля РО. При удалении листа любого раздела отчета.
// Вызов происходит в том случае, если в св-вах раздела установлен реквизит ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста = Истина
// если в св-вах такого реквизита нет, то после удаления листа запускается расчет этого листа.
//
// В подобных процедурах должны выполняться действия, которые следуют за удалением доп. страниц. пересчет номеров, закрашивание ячеек и т.д.
// Если в разделе в св-вах нет ключа СпециальныеОперацииПослеОчисткиЛиста = Ложь или нет для многостраничного раздела
// тогда не вызывается эта процедура.
//
// Параметры:
//  ИмяСтраницы    - имся страницы раздела декларации
//  УдалениДопЭкз  - Булево. Имеет смысл только для многостраничных разделов. Если Истина, то выбрана очистка всего раздела "насквозь" включая
//                 удаление всех доп страниц. Если Ложь - то очистка только текущего табличного поля.
//
&НаСервере
Процедура СпециальныеОперацииПослеОчисткиЛистаНаСервере(ИмяСтраницы) Экспорт
		
	РасчетНаСервере(ИмяСтраницы);
	
КонецПроцедуры

// СпециальныеОперацииПослеОчисткиЛистаНаКлиенте(
// Процедура вызывается из модуля РО. При удалении листа любого раздела отчета.
// Вызов происходит в том случае, если в св-вах раздела установлен реквизит ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста = Истина
// если в св-вах такого реквизита нет, то после удаления листа запускается расчет этого листа.
//
// В подобных процедурах должны выполняться действия, которые следуют за удалением доп. страниц. пересчет номеров, закрашивание ячеек и т.д.
// Если в разделе в св-вах нет ключа СпециальныеОперацииПослеОчисткиЛиста = Ложь или нет для многостраничного раздела
// тогда не вызывается эта процедура.
//
// Параметры:
//  ИмяСтраницы    - имся страницы раздела декларации
//  УдалениДопЭкз  - Булево. Имеет смысл только для многостраничных разделов. Если Истина, то выбрана очистка всего раздела "насквозь" включая
//                 удаление всех доп страниц. Если Ложь - то очистка только текущего табличного поля.
&НаКлиенте
Процедура СпециальныеОперацииПослеОчисткиЛистаНаКлиенте(ИмяСтраницы) Экспорт
		
	РасчетНаСервере();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеПоля(ТабличныйДокумент, ИмяПоля, ЗначениеПоля)
	
	Поле = ТабличныйДокумент.Области.Найти(ИмяПоля);
	Если Поле <> Неопределено Тогда
		Если Поле.СодержитЗначение = Истина Тогда
			Поле.Значение = ЗначениеПоля;
		Иначе
			Поле.Текст = ЗначениеПоля;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	РегламентированнаяОтчетностьКлиент.АктивизироватьЯчейку(ЭтотОбъект, Ячейка);
	
КонецПроцедуры

#КонецОбласти

#Область ДиалогиВыбораДляВводаЗначений

&НаКлиенте
Процедура ВвестиПоказатель(Элемент, Область, КодПоказателя, Знач ТекстВыбора, АдресТаблицыВыбора, СтандартнаяОбработка = Ложь, КолонкаПоиска = "Код") Экспорт
	
	Если СтруктураРеквизитовФормы.мАвтоВыборКодов Или Не ЭтоАдресВременногоХранилища(АдресТаблицыВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	//если показатель входит в состав многострочной части, тогда имя области не будет совпадать с переданным кодом показателя
	//необходимо это учесть
	//считаем, что в многострочной части не может быть показателя, состоящего из нескольких ячеек
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетностьКлиентСервер.ПоказательВходитВМногострочнуюЧасть(ЭтотОбъект, КодПоказателя) > 0 Тогда
		
		ИсходноеЗначение = ?(ТипЗнч(Область.Значение) = Тип("Строка"), СокрЛП(Область.Значение), Область.Значение);
	Иначе
		Разделитель = ?(Лев(Область.Имя, СтрДлина(КодПоказателя) + 1) = КодПоказателя + "_", "_", "");
		ИндексОбласти = 1;
		ИсходноеЗначение = "";
		Пока ЭтотОбъект[ТекущийЭлемент.Имя].Области.Найти(КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")) <> Неопределено Цикл
			ИсходноеЗначение = ИсходноеЗначение + Строка(ЭтотОбъект[ТекущийЭлемент.Имя].Области[КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")].Значение);
			ИндексОбласти = ИндексОбласти + 1;
		КонецЦикла;
		ИсходноеЗначение = СокрЛП(ИсходноеЗначение);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",          ТекстВыбора);
	ПараметрыФормы.Вставить("ТаблицаЗначений",    АдресТаблицыВыбора);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура(КолонкаПоиска, ИсходноеЗначение));
	
	
	ДополнительныеПараметры = Новый Структура("ИндексОбласти, КодПоказателя, КолонкаПоиска, Область, Разделитель, Элемент", ИндексОбласти, КодПоказателя, КолонкаПоиска, Область, Разделитель, Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиПоказательЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказательЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ИндексОбласти = ДополнительныеПараметры.ИндексОбласти;
	КодПоказателя = ДополнительныеПараметры.КодПоказателя;
	КолонкаПоиска = ДополнительныеПараметры.КолонкаПоиска;
	Область = ДополнительныеПараметры.Область;
	Разделитель = ДополнительныеПараметры.Разделитель;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетностьКлиентСервер.ПоказательВходитВМногострочнуюЧасть(ЭтотОбъект, КодПоказателя) > 0 Тогда
		Область.Значение = РезультатВыбора[КолонкаПоиска];
	Иначе
		СтрокаРезультатВыбора = Строка(РезультатВыбора[КолонкаПоиска]);
		Для Инд = СтрДлина(СтрокаРезультатВыбора) + 1 По ИндексОбласти - 1 Цикл
			СтрокаРезультатВыбора = " " + РезультатВыбора[КолонкаПоиска];
		КонецЦикла;
		Для Инд = 1 По ИндексОбласти - 1 Цикл
			ЭтотОбъект[ТекущийЭлемент.Имя].Области[КодПоказателя + Разделитель + Формат(Инд, "ЧГ=")].Значение = СокрЛП(Сред(РезультатВыбора[КолонкаПоиска], Инд, 1));
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ОбработкаИзмененияСодержимогоВПолеТабличногоДокумента(Элемент, Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСтраныМира(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = ПолучитьФорму("Справочник.СтраныМира.ФормаВыбора", Новый Структура("РежимВыбора", Истина), ЭтотОбъект);
	ФормаВыбора.Заголовок = НСтр("ru = 'Код страны по классификатору ОКСМ'");
	ФормаВыбора.ОписаниеОповещенияОЗакрытии = ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВыбораСтраныМираЗавершение", Элемент, Область);
	ФормаВыбора.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраныМираЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяЯчейки = ДополнительныеПараметры.ИмяЯчейки;
	Область = ДополнительныеПараметры.Область;
	Элемент = ДополнительныеПараметры.Элемент;
		
	Если Результат <> Неопределено Тогда
		
		КодЭлементаСправочника = КодЭлементаСправочника(Результат);
		
		Если Область.Значение <> КодЭлементаСправочника Тогда
			
			Область.Значение = КодЭлементаСправочника;
			Модифицированность = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрВводаПоля = СвойствоПоказателя(ИмяЯчейки, АдресСвойствПоказателей);
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ВвестиПоказатель(Элемент, Область, КодПоказателя, ПараметрВводаПоля.ТекстПриВыборе, ПараметрВводаПоля.АдресТаблицыЗначений);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораВалюты(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = ПолучитьФорму("Справочник.Валюты.ФормаВыбора", Новый Структура("РежимВыбора", Истина), ЭтотОбъект);
	ФормаВыбора.Заголовок = НСтр("ru = 'Код валюты'");
	ФормаВыбора.ОписаниеОповещенияОЗакрытии = ОписаниеОповещенияВыбораВПолеТабличногоДокумента("ОткрытьФормуВыбораКодаВалютыЗавершение", Элемент, Область);
	ФормаВыбора.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораКодаВалютыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяРаздела = ДополнительныеПараметры.ИмяРаздела;
	ИмяЯчейки = ДополнительныеПараметры.ИмяЯчейки;
	Область = ДополнительныеПараметры.Область;
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат <> Неопределено Тогда
		
		КодЭлементаСправочника = КодЭлементаСправочника(Результат);
		
		Если Область.Значение <> КодЭлементаСправочника Тогда
			
			Область.Значение = КодЭлементаСправочника;
			СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяЯчейки, КодЭлементаСправочника);
			
			КодВидаДохода = Строка(ТабличныйДокумент.Области["П000400001031"].Значение);
			ЭтоПрибыльКИК = (КодВидаДохода = КодВидаДоходаЗаПределамиРФ("ПрибыльКИК", СтруктураРеквизитовФормы.мВыбраннаяФорма));
			
			Если ЗначениеЗаполнено(ТабличныйДокумент.Области["П000400001040"].Значение) И Не ЭтоПрибыльКИК Тогда
				
				Курс = ПолучитьКурсВалютыНаДату(Область.Значение, ТабличныйДокумент.Области["П000400001040"].Значение);
				
				ИмяПоказателяКурса = "П000400001050";
				ТабличныйДокумент.Области[ИмяПоказателяКурса].Значение = Курс;
				СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяПоказателяКурса, Курс);
				
				РасчетПослеИзмененияСодержания(ИмяПоказателяКурса, ИмяРаздела, ТабличныйДокумент.Области[ИмяПоказателяКурса]);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТабличныйДокумент.Области["П000400001080"].Значение) И Не ЭтоПрибыльКИК Тогда
				
				Курс = ПолучитьКурсВалютыНаДату(Область.Значение, ТабличныйДокумент.Области["П000400001080"].Значение);
				
				ИмяПоказателяКурса = "П000400001090";
				ТабличныйДокумент.Области[ИмяПоказателяКурса].Значение = Курс;
				СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяПоказателяКурса, Курс);
				
				РасчетПослеИзмененияСодержания(ИмяПоказателяКурса, ИмяРаздела, ТабличныйДокумент.Области[ИмяПоказателяКурса]);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	ПараметрВводаПоля = СвойствоПоказателя(ИмяЯчейки, АдресСвойствПоказателей);
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		КодПоказателя = ПараметрВводаПоля.Показатель;
		ВвестиПоказатель(Элемент, Область, КодПоказателя, ПараметрВводаПоля.ТекстПриВыборе, ПараметрВводаПоля.АдресТаблицыЗначений);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаСтрокиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Область.Значение <> Результат Тогда
		ДополнительныеПараметры.Область.Значение = Результат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделОтчетаПриИзменении(Раздел, ЭлементРазделыОтчета)
	
	ЭтоЗаголовокРаздела = (Раздел.ПолучитьРодителя() = Неопределено);
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = ЭлементРазделыОтчета.ТекущаяСтрока;
	
	Элементы.ОчиститьТекущуюСтраницу.Доступность = Истина;
	
	Если СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере Тогда
		
		Если Раздел.РазделМногостраничный И НЕ ЗначениеЗаполнено(Раздел.КолонкаНомерСтраницыРазделаОтчета) Тогда
			Раздел.КолонкаНомерСтраницыРазделаОтчета = 1;
		КонецЕсли;
		
		ВывестиМакетСтраницыВТабличныйДокументНаСервере(Раздел.КолонкаРазделыОтчетаСокрНаим, Раздел.КолонкаНомерСтраницыРазделаОтчета, ЭтоЗаголовокРаздела);
		
	Иначе
		
		СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
		
	КонецЕсли;
	
	Элементы.ДобавитьСтраницу.Видимость = Раздел.РазделМногостраничный;
	Элементы.УдалитьСтраницу.Видимость = Раздел.РазделМногостраничный;
	Если Раздел.РазделМногостраничный Тогда
		
		ЭлементРазделыОтчета.Развернуть(ЭлементРазделыОтчета.ТекущаяСтрока);
		
		Если ЭтоЗаголовокРаздела Тогда
			
			Элементы.ОчиститьТекущуюСтраницу.Доступность = Ложь;
			
			Элементы.УдалитьСтраницу.Видимость = Ложь;
			
		ИначеЕсли Раздел.ПолучитьРодителя().ПолучитьЭлементы().Количество() = 1 Тогда
			
			Элементы.УдалитьСтраницу.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДобавитьСтроку.Видимость = Раздел.РазделМногострочный;
	Элементы.УдалитьСтроку.Видимость  = Раздел.РазделМногострочный;
	Элементы.ТабличныйДокументКонтекстноеМенюГруппаСтроки.Видимость = Раздел.РазделМногострочный;
	
	Элементы.Обновить.Доступность = (Раздел.КолонкаРазделыОтчетаСокрНаим = "Титульный");
	
	Элементы.СобратьДанные.Видимость = (Раздел.КолонкаРазделыОтчетаСокрНаим = "Раздел1" Или Раздел.КолонкаРазделыОтчетаСокрНаим = "Раздел2");
	
	Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Область(1, 1);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КодЭлементаСправочника(Элемент)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Элемент, "Код");
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораИФНСЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ТабличныйДокумент.Области["КодИМНС"].Значение = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(РезультатВыбора.КодНО, 4);
		ЗаполнитьСведенияОПодписантах();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораИФНС(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КодИМНС = СокрЛП(ТабличныйДокумент.Области.КодИМНС.Значение);
	
	ЗначенияДляОтбора = Новый Массив;
	ЗначенияДляОтбора.Добавить(Новый Структура("КодНО, КПП", КодИМНС));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыФормы.Вставить("ЗначенияДляОтбора", ЗначенияДляОтбора);
	
	ФормаВыбораНалоговогоОргана = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ФормаВыбораНалоговогоОргана", ПараметрыФормы, ЭтотОбъект);
	
	Если ФормаВыбораНалоговогоОргана.ТаблицаНО.Количество() <> 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораИФНСЗавершение", ЭтотОбъект);
		ФормаВыбораНалоговогоОргана.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВыбораНалоговогоОргана.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВыбораНалоговогоОргана.Открыть();
	ИначеЕсли Не ЗначениеЗаполнено(КодИМНС) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'В реквизитах организации не указан налоговый орган'");
		Сообщение.КлючДанных = СтруктураРеквизитовФормы.Организация;
		Сообщение.Сообщить();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Для выбора инспекции, необходимо завести соответствующую запись в справочнике ""Регистрации в налоговом органе"".'");
		Сообщение.КлючДанных = СтруктураРеквизитовФормы.Организация;
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныйФункционал

&НаСервереБезКонтекста
Функция СвойствоПоказателя(Знач КодПоказателя, Знач АдресСвойствПоказателей)
	
	СвойстваПоказателя = Неопределено;
	
	Если Не ЭтоАдресВременногоХранилища(АдресСвойствПоказателей) Тогда
		Возврат СвойстваПоказателя;
	КонецЕсли;
	
	СвойстваПоказателей = ПолучитьИзВременногоХранилища(АдресСвойствПоказателей);
	Если ТипЗнч(СвойстваПоказателей) <> Тип("ТаблицаЗначений") Тогда
		Возврат СвойстваПоказателя;
	КонецЕсли;
	
	СтруктураКодаПоказателя = СтрРазделить(КодПоказателя, "_");
	БазаКодаПоказателя = СтруктураКодаПоказателя[0];
	
	НайденнаяСтрока  = СвойстваПоказателей.Найти(БазаКодаПоказателя, "Показатель");
	Если НайденнаяСтрока <> Неопределено Тогда
		СвойстваПоказателя = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(НайденнаяСтрока);
	КонецЕсли;
	
	Возврат СвойстваПоказателя;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МаксимальноеКоличествоСтрокНаСтранице(Форма, ИмяСтраницы)
	
	МаксимальноеКоличествоСтрок = 0;
	Для Каждого Раздел Из Форма.мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл
		Если Раздел.ИмяСтраницы = ИмяСтраницы Тогда
			Если Раздел.МногострочностьВРазделе.Количество() <> 0 Тогда
				МаксимальноеКоличествоСтрок = Раздел.МногострочностьВРазделе[0].МаксимальноеКоличествоСтрокНаСтранице;
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат МаксимальноеКоличествоСтрок;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ВыполнитьПроверкуПередОткрытием()
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОткрытиемФормыРегламентированногоОтчета(
		Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект),
		ЭтотОбъект,
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизации()
	
	Отчеты.РегламентированныйОтчет3НДФЛ.ЗаполнитьТитульныйЛист(СтруктураДанныхТитульный, СтруктураРеквизитовФормы.Организация, СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	
	ЗаполнитьСведенияОПодписантах();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОПодписантах() Экспорт
	
	Отчеты.РегламентированныйОтчет3НДФЛ.ЗаполнитьСведенияОПодписантах(
		СтруктураДанныхТитульный, СтруктураРеквизитовФормы.Организация);
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		Для Каждого РеквизитТабличногоДокумента Из ТабличныйДокумент.Области Цикл
			Если РеквизитТабличногоДокумента.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
				И Не РеквизитТабличногоДокумента.Защита Тогда
				СтруктураДанныхТитульный.Свойство(РеквизитТабличногоДокумента.Имя, РеквизитТабличногоДокумента.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьПеременныеМодуляНаСервере(ИмяПоказателя, ЗначениеПоказателя) Экспорт
	
	ПеременныеМодуля = Новый Структура;
	ПеременныеМодуля.Вставить("мПредставительФизЛицо",  СтруктураРеквизитовФормы.мПредставительФизЛицо);
	ПеременныеМодуля.Вставить("мНазваниеПредставителя", СтруктураРеквизитовФормы.мНазваниеПредставителя);
	ПеременныеМодуля.Вставить("мДокументПредставителя", СтруктураРеквизитовФормы.мДокументПредставителя);
	ПеременныеМодуля.Вставить("мФамилияПредставителя",  СтруктураРеквизитовФормы.мФамилияПредставителя);
	ПеременныеМодуля.Вставить("мИмяПредставителя",      СтруктураРеквизитовФормы.мИмяПредставителя);
	ПеременныеМодуля.Вставить("мОтчествоПредставителя", СтруктураРеквизитовФормы.мОтчествоПредставителя);
	ПеременныеМодуля.Вставить("мСтатусНалогоплательщика", СтруктураРеквизитовФормы.мСтатусНалогоплательщика);
	
	флОбновитьПеременныеМодуля = ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля);
	
	СтруктураРеквизитовФормы.мПредставительФизЛицо  = ПеременныеМодуля.мПредставительФизЛицо;
	СтруктураРеквизитовФормы.мНазваниеПредставителя = ПеременныеМодуля.мНазваниеПредставителя;
	СтруктураРеквизитовФормы.мДокументПредставителя = ПеременныеМодуля.мДокументПредставителя;
	СтруктураРеквизитовФормы.мФамилияПредставителя  = ПеременныеМодуля.мФамилияПредставителя;
	СтруктураРеквизитовФормы.мИмяПредставителя      = ПеременныеМодуля.мИмяПредставителя;
	СтруктураРеквизитовФормы.мОтчествоПредставителя = ПеременныеМодуля.мОтчествоПредставителя;
	СтруктураРеквизитовФормы.мСтатусНалогоплательщика = ПеременныеМодуля.мСтатусНалогоплательщика;
	
	Возврат флОбновитьПеременныеМодуля;
	
КонецФункции

&НаКлиенте
Функция ОбновитьПеременныеМодуляНаКлиенте(ИмяПоказателя, ЗначениеПоказателя) Экспорт
	
	ПеременныеМодуля = Новый Структура;
	ПеременныеМодуля.Вставить("мПредставительФизЛицо",  СтруктураРеквизитовФормы.мПредставительФизЛицо);
	ПеременныеМодуля.Вставить("мНазваниеПредставителя", СтруктураРеквизитовФормы.мНазваниеПредставителя);
	ПеременныеМодуля.Вставить("мДокументПредставителя", СтруктураРеквизитовФормы.мДокументПредставителя);
	ПеременныеМодуля.Вставить("мФамилияПредставителя",  СтруктураРеквизитовФормы.мФамилияПредставителя);
	ПеременныеМодуля.Вставить("мИмяПредставителя",      СтруктураРеквизитовФормы.мИмяПредставителя);
	ПеременныеМодуля.Вставить("мОтчествоПредставителя", СтруктураРеквизитовФормы.мОтчествоПредставителя);
	ПеременныеМодуля.Вставить("мСтатусНалогоплательщика", СтруктураРеквизитовФормы.мСтатусНалогоплательщика);
	
	флОбновитьПеременныеМодуля = ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля);
	
	СтруктураРеквизитовФормы.мПредставительФизЛицо  = ПеременныеМодуля.мПредставительФизЛицо;
	СтруктураРеквизитовФормы.мНазваниеПредставителя = ПеременныеМодуля.мНазваниеПредставителя;
	СтруктураРеквизитовФормы.мДокументПредставителя = ПеременныеМодуля.мДокументПредставителя;
	СтруктураРеквизитовФормы.мФамилияПредставителя  = ПеременныеМодуля.мФамилияПредставителя;
	СтруктураРеквизитовФормы.мИмяПредставителя      = ПеременныеМодуля.мИмяПредставителя;
	СтруктураРеквизитовФормы.мОтчествоПредставителя = ПеременныеМодуля.мОтчествоПредставителя;
	СтруктураРеквизитовФормы.мСтатусНалогоплательщика = ПеременныеМодуля.мСтатусНалогоплательщика;
	
	Возврат флОбновитьПеременныеМодуля;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля)
	
	ИзмененияПроизошли = Ложь;
	
	Если Лев(ИмяПоказателя, 1) = "_" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ИмяПоказателя = "ПредставительФизЛицо" Тогда
		ПеременныеМодуля.мПредставительФизЛицо = ЗначениеПоказателя;
		ИзмененияПроизошли = Истина;
	ИначеЕсли ИмяПоказателя = "НазваниеПредставителя" Тогда
		ПеременныеМодуля.мНазваниеПредставителя = ЗначениеПоказателя;
		ИзмененияПроизошли = Истина;
	ИначеЕсли ИмяПоказателя = "ДокументПредставителя" Тогда
		ПеременныеМодуля.мДокументПредставителя = ЗначениеПоказателя;
		ИзмененияПроизошли = Истина;
	ИначеЕсли ИмяПоказателя = "ФамилияПредставителя" Тогда
		ПеременныеМодуля.мФамилияПредставителя = ЗначениеПоказателя;
		ИзмененияПроизошли = Истина;
	ИначеЕсли ИмяПоказателя = "ИмяПредставителя" Тогда
		ПеременныеМодуля.мИмяПредставителя = ЗначениеПоказателя;
		ИзмененияПроизошли = Истина;
	ИначеЕсли ИмяПоказателя = "ОтчествоПредставителя" Тогда
		ПеременныеМодуля.мОтчествоПредставителя = ЗначениеПоказателя;
		ИзмененияПроизошли = Истина;
	ИначеЕсли ИмяПоказателя = "СтатусНалогоплательщика" Тогда
		Если ЗначениеПоказателя = "1" Тогда
			ПеременныеМодуля.мСтатусНалогоплательщика = 1;
		ИначеЕсли ЗначениеПоказателя = "2" Тогда
			ПеременныеМодуля.мСтатусНалогоплательщика = 2;
		Иначе
			ПеременныеМодуля.мСтатусНалогоплательщика = 0;
		КонецЕсли;
	КонецЕсли;

	Возврат ИзмененияПроизошли;
	
КонецФункции

//Преобразовывает некоторые значения структуры из посимвольного представления в одно поле
//предназначена для совместимости с формами сохраненными в версии 2.0
&НаСервере
Процедура ПреобразоватьПосимвольныеДанныеВЗначения(Данные)
	
	Если Данные.Свойство("ИНН_1") Тогда
		
		ИНН = "";
		Для Поз = 1 По 12 Цикл
			ИНН = ИНН + Данные["ИНН_" + Поз];
			Данные.Удалить("ИНН_" + Поз);
		КонецЦикла;	
		Данные.Вставить("ИНН",ИНН);
	КонецЕсли;
	
	Если Данные.Свойство("ДатаПодписи1") Тогда
		
		мДатаПодписи = "" + Данные["ДатаПодписи5"]
		+ Данные["ДатаПодписи6"]
		+ Данные["ДатаПодписи7"]
		+ Данные["ДатаПодписи8"]
		+ Данные["ДатаПодписи3"]
		+ Данные["ДатаПодписи4"]
		+ Данные["ДатаПодписи1"]
		+ Данные["ДатаПодписи2"];
		
		Для Поз = 1 По 8 Цикл
			Данные.Удалить("ДатаПодписи" + Поз);
		КонецЦикла;
		
		Данные.Вставить("ДатаПодписи", ?(ЗначениеЗаполнено(мДатаПодписи), Дата(мДатаПодписи), РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"))));
		
	КонецЕсли;
	Если Данные.Свойство("НомСтр1_1") Тогда
			
		Для Поз = 1 По 3 Цикл
			Данные.Удалить("НомСтр1_" + Поз);
		КонецЦикла;	
		
	КонецЕсли;
	Если Данные.Свойство("НомерКорректировки1") Тогда	
		
		НомерКорректировки = "";
		Для Поз = 1 По 3 Цикл
			НомерКорректировки = НомерКорректировки + Данные["НомерКорректировки" + Поз];
			Данные.Удалить("НомерКорректировки" + Поз);
		КонецЦикла;	
		Данные.Вставить("НомерКорректировки", НомерКорректировки);
	
	КонецЕсли;
	Если Данные.Свойство("ОтчетГод1") Тогда
		
		ОтчетГод = "";
		Для Поз = 1 По 4 Цикл
			ОтчетГод = ОтчетГод + Данные["ОтчетГод" + Поз];
			Данные.Удалить("ОтчетГод" + Поз);
		КонецЦикла;	
		Данные.Вставить("ОтчетГод", ОтчетГод);
		
	КонецЕсли;
	Если Данные.Свойство("КодИМНС1") Тогда
		
		КодИМНС = "";
		Для Поз = 1 По 4 Цикл
			КодИМНС = КодИМНС + Данные["КодИМНС" + Поз];
			Данные.Удалить("КодИМНС" + Поз);
		КонецЦикла;	
		Данные.Вставить("КодИМНС", КодИМНС);
		
	КонецЕсли;
	Если Данные.Свойство("ПоМесту1") Тогда
		
		ПоМесту = "";
		Для Поз = 1 По 3 Цикл
			ПоМесту = ПоМесту + Данные["ПоМесту" + Поз];
			Данные.Удалить("ПоМесту" + Поз);
		КонецЦикла;	
		Данные.Вставить("ПоМесту", ПоМесту);
		
	КонецЕсли;
	Если Данные.Свойство("Период1") Тогда
		
		Период = "";
		Для Поз = 1 По 2 Цикл
			Период = Период + Данные["Период" + Поз];
			Данные.Удалить("Период" + Поз);
		КонецЦикла;	
		Данные.Вставить("Период", Период);
		
	КонецЕсли;
	Если Данные.Свойство("СоставленаНа1") Тогда
		
		СоставленаНа = "";
		Для Поз = 1 По 3 Цикл
			СоставленаНа = СоставленаНа + Данные["СоставленаНа" + Поз];
			Данные.Удалить("СоставленаНа" + Поз);
		КонецЦикла;	
		Данные.Вставить("СоставленаНа", СоставленаНа);
		
	КонецЕсли;
	Если Данные.Свойство("ДатаВыдачиДокумента1") Тогда	
		
		ДатаВыдачиДокумента = "" + Данные["ДатаВыдачиДокумента5"]
		+ Данные["ДатаВыдачиДокумента6"]
		+ Данные["ДатаВыдачиДокумента7"]
		+ Данные["ДатаВыдачиДокумента8"]
		+ Данные["ДатаВыдачиДокумента3"]
		+ Данные["ДатаВыдачиДокумента4"]
		+ Данные["ДатаВыдачиДокумента1"]
		+ Данные["ДатаВыдачиДокумента2"];

		Для Поз = 1 По 8 Цикл
			Данные.Удалить("ДатаВыдачиДокумента" + Поз);
		КонецЦикла;	
		
		Данные.Вставить("ДатаВыдачиДокумента", ?(ЗначениеЗаполнено(ДатаВыдачиДокумента), Дата(ДатаВыдачиДокумента), РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"))));
		
	КонецЕсли;
	Если Данные.Свойство("ДатаРождения1") Тогда
		
		ДатаРождения = "" + Данные["ДатаРождения5"]
		+ Данные["ДатаРождения6"]
		+ Данные["ДатаРождения7"]
		+ Данные["ДатаРождения8"]
		+ Данные["ДатаРождения3"]
		+ Данные["ДатаРождения4"]
		+ Данные["ДатаРождения1"]
		+ Данные["ДатаРождения2"];
		
		Для Поз = 1 По 8 Цикл
			Данные.Удалить("ДатаРождения" + Поз);
		КонецЦикла;	
		Данные.Вставить("ДатаРождения", ?(ЗначениеЗаполнено(ДатаРождения) , Дата(ДатаРождения), РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"))));
		
	КонецЕсли;	

КонецПроцедуры

// Процедура управляет показом в форме периода построения отчета.
//
&НаСервере
Процедура ПоказатьПериод()
	
	ПолеТабличногоДокументаТитульный = ТабличныйДокумент;
	
	// Выводим год на титульный лист
	Если СтруктураДанныхТитульный <> Неопределено
		И СтруктураДанныхТитульный.Свойство("ОтчетГод")
		И ЗначениеЗаполнено(СтруктураДанныхТитульный.ОтчетГод) Тогда
		ОтчетныйГод = Формат(СтруктураДанныхТитульный.ОтчетГод, "ЧГ=");
	Иначе
		ОтчетныйГод = Формат(Год(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета), "ЧГ=");
	КонецЕсли;
	
	ПолеТабличногоДокументаТитульный.Области.ОтчетГод.Значение = ОтчетныйГод;
	
	// Задаем заголовок формы
	СтруктураРеквизитовФормы.СтрПериодОтчета = ПредставлениеПериода(
		НачалоДня(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета),
		КонецДня(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета),
		"ФП = Истина");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСменыЕдИзмеренияИТочности()
	
	ПроверитьТочность();
	
	УстановитьФорматВыводаВПоляхВводаНаКлиенте(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	РасчетНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

// Процедура контролирует корректность установленной точности указания
// суммовых (денежных) показателей при выборе единицы измерения.
//
&НаКлиенте
Процедура ПроверитьТочность()
	
	Если СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1") Тогда
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения > 2 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: при выводе сумм в рублях точность не может превышать 2 знака'"), ЭтотОбъект.Заголовок);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 2;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1000") Тогда
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения > 3 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: при выводе сумм в тысячах рублей точность не может превышать 3 знака'"), ЭтотОбъект.Заголовок);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 3;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураРеквизитовФормы.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1000000") Тогда
		
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения > 6 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: при выводе сумм в миллионах рублей точность не может превышать 6 знаков'"), ЭтотОбъект.Заголовок);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 6;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКурсВалютыНаДату(КодВалюты,ДатаКурса)
	
	Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
	Если Валюта <> Неопределено Тогда
		Результат = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта,ДатаКурса);
		Возврат Результат.Курс;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Очистить(ВыполняемоеОповещение = Неопределено) Экспорт
	
	РегламентированнаяОтчетностьКлиент.Очистить(ЭтотОбъект, ВыполняемоеОповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраницЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ЗначениеАвторасчета = ДополнительныеПараметры.ЗначениеАвторасчета;
	ЗначениеЕдИзм = ДополнительныеПараметры.ЗначениеЕдИзм;
	ЗначениеЗпт = ДополнительныеПараметры.ЗначениеЗпт;
	НомерСтраницыРазделаОтчета = ДополнительныеПараметры.НомерСтраницыРазделаОтчета;
	ТекущийРазделОтчетаСокрНаим = ДополнительныеПараметры.ТекущийРазделОтчетаСокрНаим;
	
	РегламентированнаяОтчетностьКлиентСервер.ПоказатьКнопкиПечати(ЭтотОбъект);
	Модифицированность = Истина;
	
	флЗначенияИдентичны = Истина;
	
	Для Индекс = 0 По мДеревоСтраницОтчета.ПолучитьЭлементы().Количество() - 1 Цикл
		
		Если НЕ мДеревоСтраницОтчета.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу = мДеревоВыбранныхСтраниц.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу Тогда
			флЗначенияИдентичны = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеАвторасчета <> СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
		
		#Если ВебКлиент Тогда
			ИзменитьТаблицуВариантовЗаполненияНаСервере();
		#Иначе
			РегламентированнаяОтчетностьКлиентСервер.ИзменитьТаблицуВариантовЗаполнения(ЭтотОбъект, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
		#КонецЕсли	
		
	КонецЕсли;
	
	Если ЗначениеЕдИзм <> СтруктураРеквизитовФормы.ЕдиницаИзмерения
		ИЛИ ЗначениеЗпт <> СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения Тогда
		ПослеСменыЕдИзмеренияИТочности();
	КонецЕсли;
	
	// Управляем видимостью страниц основной панели формы
	РегламентированнаяОтчетностьКлиент.ПоказатьСтраницыОтчетаНаКлиенте(ЭтотОбъект);
	
	Если НЕ флЗначенияИдентичны Тогда
		
		СформироватьДеревоРазделовОтчетаНаКлиенте();
		
		Если НЕ ТекущийРазделОтчетаСокрНаим = Неопределено Тогда
			
			// Пытаемся найти ранее запомненный раздел.
			НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(РазделыОтчета.ПолучитьЭлементы(), "КолонкаРазделыОтчетаСокрНаим", ТекущийРазделОтчетаСокрНаим);
			
			Если НЕ НайденнаяСтрока = Неопределено 
				И НЕ НомерСтраницыРазделаОтчета = Неопределено 
				И НайденнаяСтрока.ПолучитьЭлементы().Количество() > 0 Тогда
				
				НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(НайденнаяСтрока.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", НомерСтраницыРазделаОтчета);
				
			КонецЕсли;
			
			// В случае успешного поиска, устанавливаем курсор на найденный раздел.
			Если НЕ НайденнаяСтрока = Неопределено Тогда
				Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРазделОтчетаСокрНаим = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		НомерСтраницыРазделаОтчета  = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтотОбъект);
	
	ДокументыОснования = Новый СписокЗначений;
	ДокументыОснования.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.СформироватьИЗаписать(ДокументыОснования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтотОбъект);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.Проверить(СпДокОсн);
	
КонецПроцедуры

&НаКлиенте
Функция ЗаголовокРегламентированногоОтчета(ДатаНач, ДатаКон, Периодичность = Неопределено) Экспорт
		
	СтрПериодОтчета = ПредставлениеПериода( НачалоДня(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета), КонецДня(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета), "ФП = Истина" );
		
	Если СтрНайти(ЭтотОбъект.Заголовок, СтрПериодОтчета) = 0 Тогда
		Возврат ЭтотОбъект.Заголовок + " за " + СтрПериодОтчета;
	Иначе
		Возврат ЭтотОбъект.Заголовок;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОтчетОбъект()

	Если ОбъектЭтогоОтчета = Неопределено Тогда
		ОбъектЭтогоОтчета = РеквизитФормыВЗначение("Отчет");
	КонецЕсли; 
	
	Возврат ОбъектЭтогоОтчета;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РазделМногострочный(ИмяРаздела, Форма)
	
	МногострочностьВРазделе = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(Форма, ИмяРаздела, "МногострочностьВРазделе");
	КоличествоМногострочныхЧастей = МногострочностьВРазделе.Количество();
	
	Возврат КоличествоМногострочныхЧастей > 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РазделМногостраничный(ИмяРаздела, Форма)
	
	МногостраничностьВРазделе = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(Форма, ИмяРаздела, "МногостраничностьВРазделе");
	
	Возврат МногостраничностьВРазделе;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяГруппы(ИмяЯчейки)
	
	Возврат Лев(ИмяЯчейки, 10);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяОтчета(ПолноеИмяФормы)
	ИмяФормы = СтрЗаменить(ПолноеИмяФормы, "Внешний", "");
	Возврат Сред(Лев(ИмяФормы, СтрНайти(ИмяФормы, ".Форма.") - 1), 7);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяФормыОтчета(ИмяФормы)
	Возврат Сред(ИмяФормы, СтрНайти(ИмяФормы, ".Форма.") + 7);
КонецФункции

#КонецОбласти

#Область РаботаСДаннымиВСтруктуреОтчета

&НаСервере
Процедура ЗаполнитьПустыеСтруктурыДанных()
	
	СохрТабличныйДокумент = Новый ТабличныйДокумент;
	СохрТабличныйДокумент.Вывести(ТабличныйДокумент);
	
	Для Каждого Раздел Из мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл
		
		ИмяМакета = ИмяМакета(ИмяФормы, Раздел.ИмяСтраницы);
		
		Если Не Раздел.МногостраничностьВРазделе
			И ЭтотОбъект["СтруктураДанных" + Раздел.ИмяСтраницы].Количество() = 0 Тогда
			
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(ОтчетОбъект().ПолучитьМакет(ИмяМакета));
			
			СохранитьДанныеРаздела(Раздел.ИмяСтраницы, Ложь);
			
		ИначеЕсли Раздел.МногостраничностьВРазделе
			И ЭтотОбъект[мСтруктураМногостраничныхРазделов[Раздел.ИмяСтраницы]][0].Данные.Количество() = 0 Тогда
			
			
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(ОтчетОбъект().ПолучитьМакет(ИмяМакета));
			
			СохранитьДанныеРаздела(Раздел.ИмяСтраницы, Ложь, Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(СохрТабличныйДокумент);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеРаздела(НаимРаздела, ОчищатьРазделЕслиЕстьМногострочность, ИзменятьАктивнуюСтраницу = Истина)
	
	Если РазделМногострочный(НаимРаздела, ЭтотОбъект) Тогда
		
		ТаблицаСтраницРаздела = Неопределено;
		
		мСтруктураМногостраничныхРазделов.Свойство(НаимРаздела, ТаблицаСтраницРаздела);
		
		ТаблицаСтраницРаздела = ЭтотОбъект[ТаблицаСтраницРаздела];
		
		СтрТаблицыСтраниц = ТаблицаСтраницРаздела.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
		
		Если СтрТаблицыСтраниц.Количество() > 0 Тогда
			
			СтрТаблицаСтраницы = СтрТаблицыСтраниц[0];
			
			// Сохраним данные активной страницы, с присвоением Представления страницы и с очистной многострочных частей (Истина)
			РегламентированнаяОтчетностьКлиентСервер.СохранитьДанныеМногостраничногоРазделаВактивнойСтранице(ЭтотОбъект, НаимРаздела, ОчищатьРазделЕслиЕстьМногострочность);
			
			Для каждого СтрТаблица Из СтрТаблицыСтраниц Цикл
				СтрТаблица.АктивнаяСтраница = Ложь;
			КонецЦикла;
			
			СтрТаблицаСтраницы.АктивнаяСтраница = НЕ ИзменятьАктивнуюСтраницу;
			
		КонецЕсли;
		
	ИначеЕсли РазделМногостраничный(НаимРаздела, ЭтотОбъект) Тогда
		
		СтруктураДанных = Новый Структура;
		
		Для Каждого Область Из ТабличныйДокумент.Области Цикл
			
			Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И Область.СодержитЗначение Тогда
				СтруктураДанных.Вставить(Область.Имя, Область.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		НаимТаблицыСтраницы = мСтруктураМногостраничныхРазделов[НаимРаздела];
		
		АктивныеСтраницы = ЭтотОбъект[НаимТаблицыСтраницы].НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
		
		Если АктивныеСтраницы.Количество() > 0 Тогда
			
			АктивнаяСтраница = АктивныеСтраницы[0];
			АктивнаяСтраница.Данные.Очистить();
			АктивнаяСтраница.Данные.Добавить(СтруктураДанных);
			
			Для каждого Страница Из АктивныеСтраницы Цикл
				Страница.АктивнаяСтраница = Ложь;
			КонецЦикла;
			
			Страница.АктивнаяСтраница = НЕ ИзменятьАктивнуюСтраницу;
			
		КонецЕсли; 
		
		
	Иначе
		
		ЭтотОбъект["СтруктураДанных" + НаимРаздела] = СобратьДанныеТекущегоТаблПоля(ЭтотОбъект, "ТабличныйДокумент");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СобратьДанныеТекущегоТаблПоляНаСервере(ИмяТабличногоПоля) Экспорт
	
	Возврат СобратьДанныеТекущегоТаблПоля(ЭтотОбъект, ИмяТабличногоПоля);
	
КонецФункции

&НаКлиенте
Функция СобратьДанныеТекущегоТаблПоляНаКлиенте(ИмяТабличногоПоля) Экспорт
	
	Возврат СобратьДанныеТекущегоТаблПоля(ЭтотОбъект, ИмяТабличногоПоля);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СобратьДанныеТекущегоТаблПоля(Форма, ТабличноеПоле) Экспорт
	
	СтруктураДанныхПоля = Новый Структура;
	
	Для Каждого ТекущаяОбласть Из Форма[ТабличноеПоле].Области Цикл
		
		Если ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И ТекущаяОбласть.СодержитЗначение = Истина Тогда
			
			СтруктураДанныхПоля.Вставить(ТекущаяОбласть.Имя, ТекущаяОбласть.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураДанныхПоля;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СохранитьДополнительныеСтрокиВСтруктуре(Форма, ИмяРаздела, Очищать = Ложь)
	
	Если РазделМногострочный(ИмяРаздела, Форма) Тогда
		// Сохраним данные активной страницы, с присвоением Представления страницы и с очистной многострочных частей (Истина)
		РегламентированнаяОтчетностьКлиентСервер.СохранитьДанныеМногостраничногоРазделаВактивнойСтранице(Форма, ИмяРаздела, Очищать);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет структуру многострочнго раздела, заданного
// уникальным идентификатором. Заполняет таблицу значений
// многострочного раздела значениями соответствующих ячеек.
//
// Параметры:
//  ИмяГруппы - строка - идентификатор многострочного раздела.
//
&НаСервере
Процедура ОбновитьСтруктуруМногострочногоРазделаНаСервере(ИмяГруппы) Экспорт
	
	ОбновитьСтруктуруМногострочногоРаздела(ЭтотОбъект, ИмяГруппы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтруктуруМногострочногоРазделаНаКлиенте(ИмяГруппы) Экспорт
	
	ОбновитьСтруктуруМногострочногоРаздела(ЭтотОбъект, ИмяГруппы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтруктуруМногострочногоРаздела(Форма, ИмяГруппы)
	
	Перем ИмяСтруктурыГруппы;
	
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, ИмяСтруктурыГруппы);
	
	СтруктураГруппы = Форма[ИмяСтруктурыГруппы];
	СписокКолонок = СтруктураГруппы.Выгрузить().Колонки;
	Для Индекс = 0 По СтруктураГруппы.Количество() - 1 Цикл
		
		Для Каждого Колонка Из СписокКолонок Цикл
			
			Если Не СтруктураГруппы[0].Свойство(Колонка.Имя) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(СтруктураГруппы[Индекс][Колонка.Имя]) = Тип("СписокЗначений") Тогда
				ЗначениеКолонки = СтруктураГруппы[Индекс][Колонка.Имя];
				ЗначениеКолонки.Очистить();
				НомерСтроки = 1;
				Пока Истина Цикл
					ИмяЯчейки = СтрШаблон("%1_%2_%3", Колонка.Имя, Формат(Индекс + 1, "ЧГ=0"), Формат(НомерСтроки, "ЧГ=0"));
					ОбластьЯчейки = Форма.ТабличныйДокумент.Области.Найти(ИмяЯчейки);
					Если ОбластьЯчейки = Неопределено Тогда
						Прервать;
					КонецЕсли;
					
					ЗначениеКолонки.Добавить(?(ОбластьЯчейки.СодержитЗначение, ОбластьЯчейки.Значение, ОбластьЯчейки.Текст));
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;
			Иначе
				ИмяЯчейки = СтрШаблон("%1_%2", Колонка.Имя, Формат(Индекс + 1, "ЧГ=0"));
				ОбластьЯчейки = Форма.ТабличныйДокумент.Области.Найти(ИмяЯчейки);
				Если ОбластьЯчейки <> Неопределено Тогда
					СтруктураГруппы[Индекс][Колонка.Имя] = ?(ОбластьЯчейки.СодержитЗначение, ОбластьЯчейки.Значение, ОбластьЯчейки.Текст);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНеобходимоеКоличествоДопСтрокНаСервере(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИсходноеКоличествоСтрокГруппы, ТекущееКоличествоСтрокГруппы, ИмяГруппы, СтруктураГруппы, ВысотаСтроки) Экспорт
	
	ОбластьДопСтроки = ТекТабличноеПоле.ПолучитьОбласть("МногострочнаяЧасть");
	
	КоординатыОбластей = Новый Структура();
	
	Для Каждого Область Из ТекТабличноеПоле.Области Цикл
		
		Если СтрНайти(Область.Имя, "_") Тогда
			
			КоординатыОбластей.Вставить(Область.Имя, Область.Верх);
			
		КонецЕсли;
		
	КонецЦикла;
	
	МногострочнаяЧастьВерх = ТекТабличноеПоле.Области.МногострочнаяЧасть.Верх;
	
	МногострочнаяЧастьНиз = ТекТабличноеПоле.Области.МногострочнаяЧасть.Низ;
	
	ТекТабличноеПоле.ВставитьОбласть(ОбластьДопСтроки.Область(),
		ТекТабличноеПоле.Область(МногострочнаяЧастьНиз + 1,
		,
		МногострочнаяЧастьНиз + (ТекущееКоличествоСтрокГруппы - 1) * ВысотаСтроки),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	Для НомНовойСтроки = ИсходноеКоличествоСтрокГруппы По ТекущееКоличествоСтрокГруппы Цикл
		
		Для Каждого Область Из ОбластьДопСтроки.Области Цикл
			
			Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				
				ОбластьТаблДок = ТекТабличноеПоле.Область(
					КоординатыОбластей[Область.Имя] + (ТекущееКоличествоСтрокГруппы - НомНовойСтроки) * ВысотаСтроки,
					Область.Лево,
					КоординатыОбластей[Область.Имя] + (ТекущееКоличествоСтрокГруппы - НомНовойСтроки) * ВысотаСтроки,
					Область.Право);
				
				Если ОбластьТаблДок.СодержитЗначение Тогда
					ОбластьТаблДок.Очистить();
				КонецЕсли;
				
				СоставляющиеИмени = СтрРазделить(Область.Имя, "_");
				Если СоставляющиеИмени.Количество() > 1 Тогда
					СоставляющиеИмени[1] = Формат(ТекущееКоличествоСтрокГруппы - НомНовойСтроки + 1, "ЧГ=0");
				КонецЕсли;
				
				ОбластьТаблДок.Имя = СтрСоединить(СоставляющиеИмени, "_");
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекТабличноеПоле.Область(ТекТабличноеПоле.Области.МногострочнаяЧасть.Верх, , ТекТабличноеПоле.Области.МногострочнаяЧасть.Низ).Имя = "";
	
	ТекТабличноеПоле.Область(МногострочнаяЧастьВерх, , МногострочнаяЧастьНиз).Имя = "МногострочнаяЧасть";
	
КонецПроцедуры

&НаСервере
Процедура ВставитьДополнительнуюСтраницуНаСервере(ИмяТекущейСтраницыПанели = Неопределено)
	
	Перем ТаблицаСтраницРаздела;
	Перем СтруктураДанныхДопСтрокРаздела;
	
	Если ИмяТекущейСтраницыПанели = Неопределено Тогда
		ИмяТекущейСтраницыПанели = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
	Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяТекущейСтраницыПанели, ТаблицаСтраницРаздела) Тогда
		Возврат;
	Иначе
		МаксКолвоЭкз = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, ИмяТекущейСтраницыПанели, "МаксКолвоЭкз", 0);
		Если МаксКолвоЭкз <> 0 И ЭтотОбъект[мСтруктураМногостраничныхРазделов[ИмяТекущейСтраницыПанели]].Количество() >= МаксКолвоЭкз Тогда
			ЗаголовокСтраницы = ИмяТекущейСтраницыПанели;
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В разделе ""%1"" максимальное количество экземпляров страниц равно %2.'"), ЗаголовокСтраницы, МаксКолвоЭкз);
			
			Сообщение.Сообщить();
			
			СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Истина;
			
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	
	ИмяТекТабличногоПоля     = "ТабличныйДокумент";
	ТекТабличноеПоле         = ЭтотОбъект[ИмяТекТабличногоПоля];
	
	СохранитьДанныеМногостраничногоРазделаВактивнойСтраницеНаСервере(ИмяТекущейСтраницыПанели);
	
	ТаблицаСтраницРаздела = ЭтотОбъект[ТаблицаСтраницРаздела];
	
	// Найдем активную страницу
	СтрТаблицаСтраницы = ТаблицаСтраницРаздела.НайтиСтроки(Новый Структура ("АктивнаяСтраница", Истина));
	
	Если СтрТаблицаСтраницы.Количество() > 0 Тогда
		
		СтрТаблицаСтраницы = СтрТаблицаСтраницы[0];
		
		// Убираем активность у текущей страницы
		СтрТаблицаСтраницы.АктивнаяСтраница = Ложь;
		
	КонецЕсли;
	
	// Добавляем новую страницу
	СтрНоваяСтраница = ТаблицаСтраницРаздела.Добавить();
	
	// Присваиваем представление по умолчанию
	СтрНоваяСтраница.Представление      = "Страница N " + ТаблицаСтраницРаздела.Количество();
	СтрНоваяСтраница.АвтоматическоеПредставление      = Истина;
	
	// Ставим признак активной страницы
	СтрНоваяСтраница.АктивнаяСтраница = Истина;
	
	ПослеДобавленияДопСтрНаСервере(ИмяТекТабличногоПоля, ИмяТекущейСтраницыПанели);
	
	ВывестиМакетВТаблДокНаСервере(ИмяТекущейСтраницыПанели, ТаблицаСтраницРаздела.Индекс(СтрНоваяСтраница) + 1);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПослеДобавленияДопСтрНаСервере(ИмяТекТабличногоПоля, ИмяТекущейСтраницыПанели)
	
	ОчиститьТабличноеПолеНаСервере(ИмяТекущейСтраницыПанели);
	
	СохранитьДанныеМногостраничногоРазделаВактивнойСтраницеНаСервере(ИмяТекущейСтраницыПанели);
	
	ПослеВставкиДополнительнойСтраницыНаСервере(ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

&НаСервере
Процедура ПослеВставкиДополнительнойСтраницыНаСервере(ИмяТекущейСтраницы)
	
	РасчетНаСервере(ИмяТекущейСтраницы);
	
	РегламентированнаяОтчетность.ПослеВставкиДополнительнойСтраницыНаСервере(ЭтотОбъект, ИмяТекущейСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеМногостраничногоРазделаВактивнойСтраницеНаСервере(ИмяТекущейСтраницыПанели)
	
	//итоговые ячейки не очистились - необходимо провести расчет
	//для этого сохраним данные страницы
	РегламентированнаяОтчетностьКлиентСервер.СохранитьДанныеМногостраничногоРазделаВактивнойСтранице(ЭтотОбъект, ИмяТекущейСтраницыПанели, Истина);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтраницуНаСервере(ИмяТекущейСтраницыПанели = Неопределено, НомерУдаляемойСтраницы = Неопределено)
	
	Перем ТаблицаСтраницРаздела;
	Перем МассивВариантовЗаполненияСтраниц;
	
	РегламентированнаяОтчетность.УдалитьДополнительнуюСтраницуНаСервере(ЭтотОбъект, ИмяТекущейСтраницыПанели);
	
	Если НомерУдаляемойСтраницы <> Неопределено
		И мСтруктураМногостраничныхРазделов.Свойство(ИмяТекущейСтраницыПанели, ТаблицаСтраницРаздела)
		И мСтруктураВариантыЗаполнения.Свойство(ИмяТекущейСтраницыПанели, МассивВариантовЗаполненияСтраниц) Тогда
		
		ТаблицаСтраницРаздела = ЭтотОбъект[ТаблицаСтраницРаздела];
		МассивВариантовЗаполненияСтраниц = ЭтотОбъект["ТаблицаВариантыЗаполнения" + ИмяТекущейСтраницыПанели];
		
		МассивВариантовЗаполненияСтраниц.Удалить(НомерУдаляемойСтраницы);
		
		КолСтраниц = ТаблицаСтраницРаздела.Количество();
		КолВариантов = МассивВариантовЗаполненияСтраниц.Количество();
		
		Для Инд = КолСтраниц + 1 По КолВариантов Цикл
			
			МассивВариантовЗаполненияСтраниц.Удалить(МассивВариантовЗаполненияСтраниц.Количество() - 1);
			
		КонецЦикла;
		
	КонецЕсли;
	
	РасчетНаСервере();
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ВставитьДополнительнуюСтроку(ТекущаяСтраница)
	Перем СтруктураГруппы;
	Перем ИсходноеКоличествоСтрокГруппы;
	
	// Сначала выполним проверку на активную страницу панели формы,
	// которая содержит многострочный раздел
	
	ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ТекущаяСтраница);
	
	Если ИмяГруппы = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбран многострочный раздел, в который необходимо добавить строку.'"));
		Возврат;
	КонецЕсли;
	
	// Исходное количество строк группы
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = ЭтотОбъект[ТекТабличноеПолеИмя];
	
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	СтруктураГруппы = ЭтотОбъект[СтруктураГруппы];
	
	// определяем последнюю строку многострочной группы
	КолСтрок = СтруктураГруппы.Количество();
	
	КолСтрокСтр      = Формат(КолСтрок, "ЧГ=0");
	КолСтрокСтрНовое = Формат(КолСтрок + 1, "ЧГ=0");
	
	Для НомерПервойЯчейки = 1 По 10 Цикл
		ИмяПервойЯчейкиПоследнейСтроки = СтрШаблон("%1%2_%3", ИмяГруппы, Формат(НомерПервойЯчейки, "ЧЦ=3; ЧВН="), КолСтрокСтр);
		ОбластьПервойЯчейки = ТекТабличноеПоле.Области.Найти(ИмяПервойЯчейкиПоследнейСтроки);
		Если ОбластьПервойЯчейки <> Неопределено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОбластьПервойЯчейки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Добавляем в структуру многострочной группы новую строку
	СтруктураГруппы.Добавить();
	
	НастройкиМнгЧ = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, СтруктураРеквизитовФормы.НаимТекущегоРаздела, "МногострочностьВРазделе").НайтиСтроки(Новый Структура("ИдГруппы", ИмяГруппы))[0];
	ВысотаСтроки = НастройкиМнгЧ.ВысотаСтроки;
	
	// Номера строк исходной области
	НомерВерхЯчейкиОбласти = ОбластьПервойЯчейки.Верх - 1;
	
	// Если над ячейкой со значением есть ячейки с текстом, то сдвинем номер границы еще выше.
	Пока Истина Цикл
		ОбластьЯчейки = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, ОбластьПервойЯчейки.Лево, НомерВерхЯчейкиОбласти, ОбластьПервойЯчейки.Право);
		Если Не ПустаяСтрока(ОбластьЯчейки.Текст) Тогда
			НомерВерхЯчейкиОбласти = НомерВерхЯчейкиОбласти - 1;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НомерНижнЯчейкиОбласти = НомерВерхЯчейкиОбласти + ВысотаСтроки - 1;
	СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;
	
	// Вставляем новую область в табличный документ
	РегламентированнаяОтчетностьВызовСервера.СпециальногоДобавленияСтроки(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти);
	
	ЭтотОбъект[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
	
	СписокКолонок = СтруктураГруппы.Выгрузить().Колонки;
	Для Каждого Колонка Из СписокКолонок Цикл
		
		Если ТипЗнч(СтруктураГруппы[0][Колонка.Имя]) = Тип("СписокЗначений") Тогда
			Для НомерЭлементаСписка = 0 По СтруктураГруппы[0][Колонка.Имя].Количество() - 1 Цикл
				ИмяОбластиЯчейки    = СтрШаблон("%1_%2_%3", Колонка.Имя, КолСтрокСтр, Формат(НомерЭлементаСписка + 1, "ЧГ=0"));
				ИмяНовОбластиЯчейки = СтрШаблон("%1_%2_%3", Колонка.Имя, КолСтрокСтрНовое, Формат(НомерЭлементаСписка + 1, "ЧГ=0"));
				
				ОтрисоватьЯчейкуНовойДополнительнойСтроки(
					ТекТабличноеПоле, ИмяОбластиЯчейки, ИмяНовОбластиЯчейки, ИмяПервойЯчейкиПоследнейСтроки, СдвигСтрок, ИсходноеКоличествоСтрокГруппы);
			КонецЦикла;
		Иначе
			ИмяОбластиЯчейки    = СтрШаблон("%1_%2", Колонка.Имя, КолСтрокСтр);
			ИмяНовОбластиЯчейки = СтрШаблон("%1_%2", Колонка.Имя, КолСтрокСтрНовое);
			
			ОтрисоватьЯчейкуНовойДополнительнойСтроки(
				ТекТабличноеПоле, ИмяОбластиЯчейки, ИмяНовОбластиЯчейки, ИмяПервойЯчейкиПоследнейСтроки, СдвигСтрок, ИсходноеКоличествоСтрокГруппы);
		КонецЕсли;
		
	КонецЦикла;
	
	// Назначим текущей областью таблицы первую редактируемую ячейку добавленной строки.
	// Определяем текущую область таблицы
	ИмяТекущейОбласти = СтрШаблон("%1%2_%3", ИмяГруппы, Формат(НомерПервойЯчейки, "ЧЦ=3; ЧВН="), КолСтрокСтрНовое);
	
	Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	
	// установим фокус на поле табличн. документа
	ТекущийЭлемент = Элементы[ТекТабличноеПолеИмя];
	
	// Заполним значениями по умолчанию
	ЗаполнитьПоказателиЗначениямиПоУмолчанию(ТекущаяСтраница);
	
	ОтрисоватьЗначкиУдаленияСтрок(ТекТабличноеПоле);
	
	СохранитьДополнительныеСтрокиВСтруктуре(ЭтотОбъект, ТекущаяСтраница, Ложь);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтрисоватьЯчейкуНовойДополнительнойСтроки(ТабличныйДокумент, ИмяОбластиЭталона, ИмяНовойОбласти, ИмяПервойЯчейкиПоследнейСтроки, СдвигСтрок, ИсходноеКоличествоСтрокГруппы)
	
	ОбластьЯчейки = ТабличныйДокумент.Области.Найти(ИмяОбластиЭталона);
	Если ОбластьЯчейки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьЯчейки.Имя = ИмяНовойОбласти;
	
	// Определяем границы ячейки исходной области
	НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
	НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
	НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
	НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
	
	// Назначаем имя ячейки исходной области,
	// (т.к. в результате вставки новой области оно было очищено)
	ОбластьЯчейкиИсходнойОбласти = ТабличныйДокумент.Область(
		НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
	ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЭталона;
	
	Если Найти(ИмяПервойЯчейкиПоследнейСтроки, Лев(ИмяОбластиЭталона, 13)) = 1 Тогда
		ОбластьУдалитьСтроку = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + ИмяОбластиЭталона);
		Если ОбластьУдалитьСтроку <> Неопределено Тогда
			ОбластьУдалитьСтроку.Имя = "УдалитьСтроку" + ИмяНовойОбласти;
			ТабличныйДокумент.Область(
				ОбластьУдалитьСтроку.Верх - СдвигСтрок,
				ОбластьУдалитьСтроку.Лево,
				ОбластьУдалитьСтроку.Низ - СдвигСтрок,
				ОбластьУдалитьСтроку.Право).Имя  = "УдалитьСтроку" + ИмяОбластиЭталона;
		КонецЕсли;
	КонецЕсли;
	
	// очищаем значения ячеек вставленной области
	Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
		ОбластьЯчейки.Очистить();
	Иначе
		// порядковый номер последней строки перед вставкой
		ИсходныйНомер        = Число("0" + ОбластьЯчейки.Текст);
		НовыйПорядковыйНомер = ИсходныйНомер + 1;
		
		ОбластьЯчейки.Текст  = ? (НЕ НовыйПорядковыйНомер > ИсходноеКоличествоСтрокГруппы, Строка(НовыйПорядковыйНомер), "");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтроку(ТекущаяСтраница)
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем ИдентификаторСтруктурыГруппы;
	
	// Сначала выполним проверку на активную страницу панели формы,
	// которая содержит многострочный раздел
	ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ТекущаяСтраница);
	
	// исходное количество строк группы
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, ИдентификаторСтруктурыГруппы);
	
	ТекТабличноеПолеИмя = "ТабличныйДокумент";
	
	ТекТабличноеПоле = ЭтотОбъект[ТекТабличноеПолеИмя];
	ТекущаяОбласть = Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть;
	ИмяЯчейки      = ТекущаяОбласть.Имя;
	
	ИмяГруппы = РегламентированнаяОтчетностьВызовСервера.РазделТекущейОбластиНаСервере(ЭтотОбъект, ТекущаяОбласть);
	
	// Предварительно обновим структуру многострочных разделов
	ОбновитьСтруктуруМногострочногоРазделаНаСервере(ИмяГруппы);
	
	// номер строки многострочного раздела определим по имени выделенной ячейки
	НомУдаляемойСтроки = Число( Сред( ИмяЯчейки, СтрНайти( ИмяЯчейки, "_") + 1));
	НомерСтрокиТаблицы = НомУдаляемойСтроки - 1; // нумерация строк в таблице значений начинается с нуля
	СтруктураГруппы = ЭтотОбъект[ИдентификаторСтруктурыГруппы];
	Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы И СтруктураГруппы.Количество() > НомерСтрокиТаблицы Тогда
		// удаляется добавленная строка
		ИмяПервойЯчейкиУдаляемойСтроки = ИмяГруппы + "010_" + НомУдаляемойСтроки;
		
		// область первой ячейки удаляемой строки многострочного раздела
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
		
		НастройкиМнгЧ = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(ЭтотОбъект, ТекущаяСтраница, "МногострочностьВРазделе").НайтиСтроки(Новый Структура("ИдГруппы", ИмяГруппы))[0];
		ВысотаСтроки = НастройкиМнгЧ.ВысотаСтроки;
		
		// номера строк удаляемой области
		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх - 1;
		НомерНижнЯчейкиОбласти = НомерВерхЯчейкиОбласти + ВысотаСтроки - 1;  // первая область - контрол удаления строки
		
		РегламентированнаяОтчетностьВызовСервера.СпециальногоУдаленияСтроки(ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти);
		
		ЭтотОбъект[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
		
		КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;
		
		// обновим имена ячеек сдвинутых строк
		СписокКолонок = СтруктураГруппы.Выгрузить().Колонки;
		Если КолвоСдвигаемыхСтрок > 0 Тогда
			
			Для НомерСтроки = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
				
				СтрокаТаблицы = СтруктураГруппы[НомерСтроки - 1];
				НомерСдвигаемойСтроки = Формат(НомерСтроки, "ЧГ=0");
				НовыйНомерСтроки = Формат(НомерСтроки - 1, "ЧГ=0");
				
				// Сдвигаем ячейку с командой удаления строки
				ИмяКолонки = "УдалитьСтроку" + ИмяГруппы + Формат(1, "ЧЦ=2; ЧВН=") + "0";
				СтароеИмяЯчейки = СтрШаблон("%1_%2", ИмяКолонки, НомерСдвигаемойСтроки);
				НовоеИмяЯчейки = СтрШаблон("%1_%2", ИмяКолонки, НовыйНомерСтроки);
				
				Ячейка = ТекТабличноеПоле.Области[СтароеИмяЯчейки];
				Ячейка.Имя = НовоеИмяЯчейки;
				
				// Сдвигаем ячейки содержащие значения
				Для каждого Колонка Из СписокКолонок Цикл
				
					Если ТипЗнч(СтрокаТаблицы[Колонка.Имя]) = Тип("СписокЗначений") Тогда
						Для НомерЭлементаСписка = 0 По СтрокаТаблицы[Колонка.Имя].Количество() - 1 Цикл
							СтароеИмяЯчейки = СтрШаблон("%1_%2_%3", Колонка.Имя, НомерСдвигаемойСтроки, Формат(НомерЭлементаСписка + 1, "ЧГ=0"));
							НовоеИмяЯчейки = СтрШаблон("%1_%2_%3", Колонка.Имя, НовыйНомерСтроки, Формат(НомерЭлементаСписка + 1, "ЧГ=0"));
							
							Ячейка = ТекТабличноеПоле.Области[СтароеИмяЯчейки];
							Ячейка.Имя = НовоеИмяЯчейки;
						КонецЦикла;
					Иначе
						СтароеИмяЯчейки = СтрШаблон("%1_%2", Колонка.Имя, НомерСдвигаемойСтроки);
						НовоеИмяЯчейки = СтрШаблон("%1_%2", Колонка.Имя, НовыйНомерСтроки);
						
						Ячейка = ТекТабличноеПоле.Области[СтароеИмяЯчейки];
						Ячейка.Имя = НовоеИмяЯчейки;
					КонецЕсли;
				
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
		// обновляем структуру многострочного раздела
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
		
	КонецЕсли;
	
	// назначим текущей областью таблицы первую редактируемую ячейку
	// удаленной строки, если ее порядковый номер меньше кол-ва строк
	// многострочного раздела, или последней строки раздела в противном случае
	
	// определяем текущую область таблицы
	НомТекущейСтроки  = Мин( НомУдаляемойСтроки, СтруктураГруппы.Количество() );
	ИмяТекущейОбласти = ИмяГруппы + "010_" + НомТекущейСтроки;
	
	Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	
	ОтрисоватьЗначкиУдаленияСтрок(ТекТабличноеПоле);
	
	// установим фокус на поле табличн. документа
	ТекущийЭлемент = Элементы[ТекТабличноеПолеИмя];
	
	СохранитьДополнительныеСтрокиВСтруктуре(ЭтотОбъект, ТекущаяСтраница, Ложь);
	
	Расчет(ЭтотОбъект, ТекущаяСтраница);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекущуюСтраницуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		
		ЗначениеКорректировки = ТабличныйДокумент.Области.НомерКорректировки.Значение;
		
		Если НЕ ЗначениеЗаполнено(ЗначениеКорректировки) Тогда
			ТабличныйДокумент.Области.НомерКорректировки.Значение = "0";
			ЗначениеКорректировки = 0;
		КонецЕсли;
		
		НомерКорректировки = ЗначениеКорректировки;
		СтруктураРеквизитовФормы.ВидДокумента = ?(ЗначениеКорректировки = 0, 0, 1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере() Экспорт
	
	СтруктураДанныхПриложение4.Очистить();
	СтруктураДанныхПриложение5.Очистить();
	СтруктураДанныхПриложение6.Очистить();
	
	НомерКорректировки = 0;
	ДатаПодписи = ТекущаяДатаСеанса();
	
	Для каждого ЭлементСтруктурыТаблицаСтраницы Из мСтруктураМногостраничныхРазделов Цикл
		ЭтотОбъект[ЭлементСтруктурыТаблицаСтраницы.Значение].Очистить();
	КонецЦикла;
	
	ИнициализацияМногостраничныхРазделов();
	
	Для Каждого мнгРаздел Из мСтруктураМногострочныхРазделов Цикл
		ЭтотОбъект[мнгРаздел.Ключ].Очистить();
	КонецЦикла;
	
	НачальныеОперацииСМногострочнымиБлоками();
	
	ЗаполнитьПустыеСтруктурыДанных();
	
	Если НЕ СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		ОчиститьТабличноеПолеНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	КонецЕсли;
	
	ВывестиМакетСтраницыВТабличныйДокументНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела, 1);
	
	СпециальныеОперацииПослеОчисткиЛистаНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
	Модифицированность = Истина;

КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеПоказателяВСтруктуреНаСервере(ИмяРаздела, ИмяПоказателя, Значение);
	
	СохранитьЗначениеПоказателяВСтруктуре(ЭтотОбъект, ИмяРаздела, ИмяПоказателя, Значение);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, ИмяПоказателя, ЗначениеПоказателя)
	
	Если Не ЗначениеЗаполнено(ИмяРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	МногостраничностьВРазделе = РазделМногостраничный(ИмяРаздела, Форма);
	КолМногострочныхЧастей = ?(РазделМногострочный(ИмяРаздела, Форма), 1, 0);
	СохранятьМногострКакТЗ = Истина;
	ИмяГруппы = ?(КолМногострочныхЧастей = 0, "", Форма.СтруктураРеквизитовФормы[СтрШаблон("мИд%1", ИмяРаздела)]);
	
	ДанныеРаздела = Неопределено;
	ДанныеМногострочногоРаздела = Неопределено;
	
	Если МногостраничностьВРазделе Тогда  //Для многостраничных разделов
		
		ИмяТаблицыСтраницы = Форма.мСтруктураМногостраничныхРазделов[ИмяРаздела];
			
		ТаблицаСтраницы = Форма[ИмяТаблицыСтраницы];
		АктивныеСтраницы = ТаблицаСтраницы.НайтиСтроки(Новый Структура("АктивнаяСтраница", Истина));
		
		Если АктивныеСтраницы.Количество() > 0 Тогда
			
			ДанныеРазделаСписок = АктивныеСтраницы[0].Данные;
			Если ДанныеРазделаСписок.Количество() > 0 Тогда
				
				ДанныеРаздела = ДанныеРазделаСписок[0].Значение;
				ДанныеРазделаСписок[0].Пометка = Ложь; //Регистрируем для передачи на сервер (при редактировании таблицы значения не передаются)
				
			КонецЕсли;
			
			Если КолМногострочныхЧастей > 0 Тогда  // И многострочных разделов
				
				ДанныеДопСтрокРазделаСписок = АктивныеСтраницы[0].ДанныеДопСтрок;
				Если ДанныеДопСтрокРазделаСписок.Количество() > 0 Тогда
					
					ДанныеДопСтрокРаздела = ДанныеДопСтрокРазделаСписок[0].Значение;
					ДанныеДопСтрокРазделаСписок[0].Пометка = Ложь;
					
					СтруктураГруппы = Неопределено;
					Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
					
					ДанныеМногострочногоРаздела = Форма[СтруктураГруппы];
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе  //Для обычных разделов
		
		ДанныеРаздела = Форма["СтруктураДанных" + ИмяРаздела];
		
		Если КолМногострочныхЧастей > 0 Тогда  // И многострочных разделов
			
			СтруктураГруппы = Неопределено;
			Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
			
			ДанныеМногострочногоРаздела = Форма[СтруктураГруппы];
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Если ДанныеМногострочногоРаздела = Неопределено Тогда
		Если ДанныеРаздела <> Неопределено Тогда
			Если ДанныеРаздела.Свойство(ИмяПоказателя) Тогда
				ДанныеРаздела[ИмяПоказателя] = ЗначениеПоказателя;
			Иначе
				ДанныеРаздела.Вставить(ИмяПоказателя, ЗначениеПоказателя);
			КонецЕсли
		КонецЕсли; 
	Иначе
		Если ИмяГруппы = ИмяГруппы(ИмяПоказателя) Тогда //Показатель входит в многострочную часть
			МассивСтрок = СтрРазделить(ИмяПоказателя, "_");
			Показатель = МассивСтрок[0];
			НомерСтроки = ?(МассивСтрок.Количество() > 1 И Не ПустаяСтрока(МассивСтрок[1]), Число(МассивСтрок[1]), 0);
			ДопНомерСтроки = ?(МассивСтрок.Количество() > 2 И Не ПустаяСтрока(МассивСтрок[2]), Число(МассивСтрок[2]), 0);
			
			Если НомерСтроки > 0 И ДопНомерСтроки > 0 И ДанныеМногострочногоРаздела.Количество() >= НомерСтроки Тогда
				Если ДанныеМногострочногоРаздела[НомерСтроки - 1][Показатель].Количество() < ДопНомерСтроки Тогда
					ДанныеМногострочногоРаздела[НомерСтроки - 1][Показатель].Добавить(ЗначениеПоказателя);
				Иначе
					ДанныеМногострочногоРаздела[НомерСтроки - 1][Показатель][ДопНомерСтроки - 1].Значение = ЗначениеПоказателя;
				КонецЕсли;
			ИначеЕсли (НомерСтроки > 0) И ДанныеМногострочногоРаздела.Количество() >= НомерСтроки Тогда
				ДанныеМногострочногоРаздела[НомерСтроки - 1][Показатель] = ЗначениеПоказателя;
			КонецЕсли;
			
			Если (НомерСтроки > 0) И ДанныеДопСтрокРаздела.Количество() >= НомерСтроки Тогда
				ДанныеДопСтрокРаздела[НомерСтроки - 1][Показатель] = ЗначениеПоказателя;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеРаздела <> Неопределено Тогда
			Если ДанныеРаздела.Свойство(ИмяПоказателя) Тогда
				ДанныеРаздела[ИмяПоказателя] = ЗначениеПоказателя;
			Иначе
				ДанныеРаздела.Вставить(ИмяПоказателя, ЗначениеПоказателя);
			КонецЕсли
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтруктуруПоказателей()
	
	мДанныеОтчета = Новый Структура;
	
	СохрСтруктура = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		мДанныеОтчета.Вставить(СтрЗаменить(Раздел.Ключ, "ПолеТабличногоДокумента", ""), РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
	КонецЦикла;
	
	Для Каждого ЭлементСтруктуры Из СохрСтруктура.ДанныеМногостраничныхРазделов Цикл
		
		НайденноеЗначение = Неопределено;
		
		мСтруктураМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, НайденноеЗначение);
		
		Если НЕ НайденноеЗначение = Неопределено Тогда
			
			НайденноеЗначение = ЭтотОбъект[НайденноеЗначение];
			
			ЗначениеВДанныеФормы(ЭлементСтруктуры.Значение, НайденноеЗначение);
			
			Если НайденноеЗначение[0].Свойство("Данные") Тогда
				
				Для Каждого СтрокаТаблЗнач Из НайденноеЗначение Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.Данные) = Тип("СписокЗначений") Тогда
						
						СтрокаТаблЗнач.Данные.Очистить();
						СтрокаТаблЗнач.Данные.Добавить(ЭлементСтруктуры.Значение[НайденноеЗначение.Индекс(СтрокаТаблЗнач)].Данные);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если НайденноеЗначение[0].Свойство("ДанныеДопСтрок") Тогда
				
				Ном = 0;
				
				Для Каждого СтрокаТаблЗнач Из ЭлементСтруктуры.Значение Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("Структура") Тогда
						
						Для Каждого ЭлСтруктуры Из СтрокаТаблЗнач.ДанныеДопСтрок Цикл
							
							ТаблЗнач = ЭлСтруктуры.Значение;
							
							ЭлСтруктуры.Значение = ЭтотОбъект["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
							
							ЗначениеВДанныеФормы(ТаблЗнач, ЭлСтруктуры.Значение);
							
							НайденноеЗначение[Ном].ДанныеДопСтрок.Добавить(ЭлСтруктуры.Значение);
							
						КонецЦикла;
						
					ИначеЕсли ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("ТаблицаЗначений") Тогда
						
						СтрокаТаблЗначДанныеДопСтрок = ЭтотОбъект["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
						
						ЗначениеВДанныеФормы(СтрокаТаблЗнач.ДанныеДопСтрок, СтрокаТаблЗначДанныеДопСтрок);
						
						НайденноеЗначение[Ном].ДанныеДопСтрок.Добавить(СтрокаТаблЗначДанныеДопСтрок, "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела");
						
					КонецЕсли;
					
					Ном = Ном + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		ТаблицаЛистовРаздела = ЭтотОбъект[Раздел.Значение];
		Для Каждого Лист Из ТаблицаЛистовРаздела Цикл
			Лист.Данные[0].Значение = РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Лист.Данные[0].Значение);
		КонецЦикла;
		мДанныеОтчета.Вставить(Раздел.Ключ, ЭтотОбъект[Раздел.Значение]);
	КонецЦикла;
	
	Для Каждого ДанныеЛиста Из мДанныеОтчета.Приложение7 Цикл
		СтраницаЛиста = ДанныеЛиста.Данные[0].Значение;
		Если СтраницаЛиста.Свойство("П000700001070_1") И СтраницаЛиста.Свойство("П000700001070_3")
			И ЗначениеЗаполнено(СтраницаЛиста.П000700001070_1) И ЗначениеЗаполнено(СтраницаЛиста.П000700001070_3) Тогда
			СтраницаЛиста.Вставить("П000700001070", СокрЛП(СтраницаЛиста.П000700001070_1) + "/" + СокрЛП(СтраницаЛиста.П000700001070_3));
		Иначе
			СтраницаЛиста.Вставить("П000700001070", "");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьРазделыНаНаличиеИнформации(ДеревоВыгрузки, парамРазделы)
	
	Разделы = Новый Массив;
	Если ТипЗнч(парамРазделы) = Тип("Строка") Тогда
		Если СтрНайти(парамРазделы, ",") <> 0 Тогда
			СтрРазделы = Новый Структура(парамРазделы);
			Для Каждого Эл Из СтрРазделы Цикл
				Разделы.Добавить(Эл.Ключ);
			КонецЦикла;
		Иначе
			Разделы.Добавить(парамРазделы);
		КонецЕсли;
	КонецЕсли;
	
	РазделыПусты = Ложь;
	Для Каждого Раздел Из Разделы Цикл
		Если РазделПуст(ДеревоВыгрузки, Раздел) Тогда
			РазделыПусты = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат НЕ РазделыПусты;
	
КонецФункции

&НаСервере
Функция ПолучитьПоказателиРаздела(ДеревоВыгрузки, Раздел)
	
	ПоказателиРаздела = ДеревоВыгрузки.Строки.НайтиСтроки(Новый Структура("Раздел", Раздел), Истина);
	КоличествоСтрокРаздела = ПоказателиРаздела.Количество();
	Для ОбрИнд = 1 По КоличествоСтрокРаздела Цикл
		Инд = КоличествоСтрокРаздела - ОбрИнд;
		ТекСтр = ПоказателиРаздела[Инд];
		Если НЕ ЗначениеЗаполнено(ТекСтр.Ключ) Тогда
			ПоказателиРаздела.Удалить(Инд);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПоказателиРаздела;
	
КонецФункции

&НаСервере
Функция ПоказательЗаполнен(Показатель, Значение)
	
	Возврат ЗначениеЗаполнено(Значение) И НЕ (СокрЛП(Показатель.Формат) = "N" И Показатель.МаксРазмерность = 15 И ТипЗнч(Значение) = Тип("Число") И Окр(Значение) = 0);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоказателиНеанализируемыеНаЗаполненность()
	
	ПоказателиНеанализируемыеНаЗаполненность= Новый Массив;
	
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001020");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001030");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001040");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001050");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001060");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001070");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001110");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001130");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001150");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000200001160");
	
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000500001050");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000500001060");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000500001070");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000500001080");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000500001090");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000500001100");
	
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000700001070");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000700001140");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000700001170");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000700001180");
	
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000900001080");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000900001120");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000900001190");
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П000900001200");
	
	ПоказателиНеанализируемыеНаЗаполненность.Добавить("П001000001080");
	
	Возврат ПоказателиНеанализируемыеНаЗаполненность;
	
КонецФункции

&НаСервере
Функция СтраницаПуста(ДанныеСтраницы, Показатели, ДеревоВыгрузки, РодМногостр = Ложь)
	
	Если ТипЗнч(Показатели) = Тип("Строка") Тогда
		ПоказателиРаздела = ПолучитьПоказателиРаздела(ДеревоВыгрузки, Показатели);
	Иначе
		ПоказателиРаздела = Показатели;
	КонецЕсли;
	
	ПоказателиНеанализируемыеНаЗаполненность = ПоказателиНеанализируемыеНаЗаполненность();
	
	Для Каждого Пок Из ПоказателиРаздела Цикл
		Если ПоказателиНеанализируемыеНаЗаполненность.Найти(Пок.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Пок.Многострочность Или (РодМногостр И Пок.Родитель.Многострочность) Тогда
			ИндексСтроки = 1;
			ЗначениеПоказателя = Неопределено;
			Пока ДанныеСтраницы.Свойство(Пок.Ключ + "_" + Формат(ИндексСтроки, "ЧГ="), ЗначениеПоказателя) Цикл
				Если ПоказательЗаполнен(Пок, ЗначениеПоказателя) Тогда
					Возврат Ложь;
				КонецЕсли;
				ИндексСтроки = ИндексСтроки + 1;
			КонецЦикла;
		Иначе
			ЗначениеПоказателя = Неопределено;
			ДанныеСтраницы.Свойство(СтрЗаменить(Пок.Ключ,"_Ч",""), ЗначениеПоказателя);
			Если ПоказательЗаполнен(Пок, ЗначениеПоказателя) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция РазделПуст(ДеревоВыгрузки, Раздел)
	
	ПоказателиРаздела = ПолучитьПоказателиРаздела(ДеревоВыгрузки, Раздел);
	
	ДанныеРаздела = мДанныеОтчета[Раздел];
	РазделПуст = Истина;
	Если ТипЗнч(ДанныеРаздела) <> Тип("Структура") Тогда
		Для Каждого Стр Из ДанныеРаздела Цикл
			Если НЕ СтраницаПуста(Стр.Данные[0].Значение, ПоказателиРаздела, ДеревоВыгрузки) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если НЕ СтраницаПуста(ДанныеРаздела, ПоказателиРаздела, ДеревоВыгрузки) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки)
	
	ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ДеревоВыгрузки); // формируем точную структуру XML-макета
	ЗаполнитьДаннымиУзел(ДеревоВыгрузки, ПараметрыВыгрузки); // заполняем дерево данными
	
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьУсловныеЭлементы(Знач ПараметрыВыгрузки, Узел)
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
			Возврат;
		КонецЕсли;
		РегламентированнаяОтчетность.ДополнитьСтруктуруПараметров(мДанныеОтчета, ПараметрыВыгрузки, Узел);
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ РегламентированнаяОтчетность.УсловиеВыполнено(ЭтотОбъект, ПараметрыВыгрузки, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

// Процедура рассчитывает итоги по указанному листу (если лист не указан, рассчитываются итоги на всех листах)
//  Если лист помечен активным и в табличном документе на форме есть соответствующие итогам области, их значения будут обновлены
&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогиЛиста(Форма, ИмяЛиста = Неопределено)

	Если ИмяЛиста = "Приложение3" Тогда
		
		ИтоговыеПоказатели = Новый Структура();
		ИтоговыеПоказатели.Вставить("СуммаДоходаИП", 0); // Итого: Общая сумма дохода
		ИтоговыеПоказатели.Вставить("СуммаВычетаИП", 0); // Итого: Сумма профессионального налогового вычета
		ИтоговыеПоказатели.Вставить("СуммаАвансовыхПлатежейИП", 0); // Итого: Сумма фактически уплаченных авансовых платежей по налогу
		ИтоговыеПоказатели.Вставить("ГодРегистрацииКФХ", 0); // Итого: Год регистрации крестьянского хозяйства
		ИтоговыеПоказатели.Вставить("СуммаКорректировкиНалоговойБазы", 0); // Итого: Сумма корректировки налоговой базы
		ИтоговыеПоказатели.Вставить("СуммаКорректировкиНалога", 0); // Итого: Сумма корректировки налога
		
		СтраницаИтоговПриложения3 = Неопределено;
		
		Для Каждого Лист Из Форма.ТаблицаСтраницыПриложение3 Цикл
			
			Если Лист.Данные.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаЛиста = Лист.Данные[0].Значение;
			
			ИтоговыеПоказатели.СуммаДоходаИП = ИтоговыеПоказатели.СуммаДоходаИП + СтрокаЛиста.П000500001030; // Сумма дохода
			ИтоговыеПоказатели.СуммаВычетаИП = ИтоговыеПоказатели.СуммаВычетаИП + СтрокаЛиста.П000500001040; // Сумма фактически произведенных расходов
			Если ИтоговыеПоказатели.СуммаАвансовыхПлатежейИП = 0 И СтрокаЛиста.П000500001070 <> 0 Тогда
				ИтоговыеПоказатели.СуммаАвансовыхПлатежейИП = СтрокаЛиста.П000500001070;
			КонецЕсли;
			Если ИтоговыеПоказатели.ГодРегистрацииКФХ = 0 И СтрокаЛиста.П000500001080 <> 0 Тогда
				ИтоговыеПоказатели.ГодРегистрацииКФХ = СтрокаЛиста.П000500001080;
			КонецЕсли;
			Если ИтоговыеПоказатели.СуммаКорректировкиНалоговойБазы = 0 И СтрокаЛиста.П000500001090 <> 0 Тогда
				ИтоговыеПоказатели.СуммаКорректировкиНалоговойБазы = СтрокаЛиста.П000500001090;
			КонецЕсли;
			Если ИтоговыеПоказатели.СуммаКорректировкиНалога = 0 И СтрокаЛиста.П000500001100 <> 0 Тогда
				ИтоговыеПоказатели.СуммаКорректировкиНалога = СтрокаЛиста.П000500001100;
			КонецЕсли;
			
			// Очищаем итоговые показатели
			СтрокаЛиста.П000500001050 = 0; // Итого: Общая сумма дохода
			СтрокаЛиста.П000500001060 = 0; // Итого: Сумма профессионального налогового вычета
			СтрокаЛиста.П000500001070 = 0; // Итого: Сумма фактически уплаченных авансовых платежей по налогу
			СтрокаЛиста.П000500001080 = 0; // Итого: Год регистрации крестьянского хозяйства
			СтрокаЛиста.П000500001090 = 0; // Итого: Сумма корректировки налоговой базы
			СтрокаЛиста.П000500001100 = 0; // Итого: Сумма корректировки налога
			
			СтраницаИтоговПриложения3 = Лист;
			
		КонецЦикла;
		
		Если ИтоговыеПоказатели.СуммаВычетаИП = 0 Тогда
			// Вычет в размере 20% процентов от общей суммы доходов
			ИтоговыеПоказатели.СуммаВычетаИП =
				Окр(ИтоговыеПоказатели.СуммаДоходаИП * ПроцентПрофессиональногоВычета(Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма) / 100, 2);
		КонецЕсли;
		
		Если СтраницаИтоговПриложения3 <> Неопределено Тогда
			
			СтрокаЛиста = СтраницаИтоговПриложения3.Данные[0].Значение;
			
			СтрокаЛиста.П000500001050 = ИтоговыеПоказатели.СуммаДоходаИП; // Итого: Общая сумма дохода
			СтрокаЛиста.П000500001060 = ИтоговыеПоказатели.СуммаВычетаИП; // Итого: Сумма профессионального налогового вычета
			СтрокаЛиста.П000500001070 = ИтоговыеПоказатели.СуммаАвансовыхПлатежейИП; // Итого: Сумма фактически уплаченных авансовых платежей по налогу
			СтрокаЛиста.П000500001080 = ИтоговыеПоказатели.ГодРегистрацииКФХ; // Итого: Год регистрации крестьянского хозяйства
			СтрокаЛиста.П000500001090 = ИтоговыеПоказатели.СуммаКорректировкиНалоговойБазы; // Итого: Сумма корректировки налоговой базы
			СтрокаЛиста.П000500001100 = ИтоговыеПоказатели.СуммаКорректировкиНалога; // Итого: Сумма корректировки налога
			
			Если СтраницаИтоговПриложения3.АктивнаяСтраница И Форма.ТабличныйДокумент.Области.Найти("П000500001050") <> Неопределено Тогда
				Форма.ТабличныйДокумент.Области["П000500001050"].Значение = ИтоговыеПоказатели.СуммаДоходаИП;
				Форма.ТабличныйДокумент.Области["П000500001060"].Значение = ИтоговыеПоказатели.СуммаВычетаИП;
				Форма.ТабличныйДокумент.Области["П000500001070"].Значение = ИтоговыеПоказатели.СуммаАвансовыхПлатежейИП;
				Форма.ТабличныйДокумент.Области["П000500001080"].Значение = ИтоговыеПоказатели.ГодРегистрацииКФХ;
				Форма.ТабличныйДокумент.Области["П000500001090"].Значение = ИтоговыеПоказатели.СуммаКорректировкиНалоговойБазы;
				Форма.ТабличныйДокумент.Области["П000500001100"].Значение = ИтоговыеПоказатели.СуммаКорректировкиНалога;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяЛиста = "Приложение4" Тогда
		
		ПоказателиЛиста = Форма.СтруктураДанныхПриложение4;
		ПоказателиЛиста.П000600001120 = 
			ПоказателиЛиста.П000600001010 +
			ПоказателиЛиста.П000600001020 +
			ПоказателиЛиста.П000600001030 +
			ПоказателиЛиста.П000600001040 +
			ПоказателиЛиста.П000600001050 +
			ПоказателиЛиста.П000600001060 +
			ПоказателиЛиста.П000600001070 +
			ПоказателиЛиста.П000600001080 +
			ПоказателиЛиста.П000600001090 +
			ПоказателиЛиста.П000600001100 +
			ПоказателиЛиста.П000600001110;
			
		Если Форма.ТабличныйДокумент.Области.Найти("П000600001120") <> Неопределено Тогда
			Форма.ТабличныйДокумент.Области["П000600001120"].Значение = ПоказателиЛиста.П000600001120;
		КонецЕсли; 
		
	ИначеЕсли ИмяЛиста = "Приложение6" Тогда
		
		ПоказателиЛиста = Форма.СтруктураДанныхПриложение6;
		
		ПоказателиЛиста.П000800001160 = ПоказателиЛиста.П000800001010
			+ ПоказателиЛиста.П000800001020
			+ ПоказателиЛиста.П000800001030
			+ ПоказателиЛиста.П000800001040
			+ ПоказателиЛиста.П000800001050
			+ ПоказателиЛиста.П000800001060
			+ ПоказателиЛиста.П000800001070
			+ ПоказателиЛиста.П000800001080
			+ ПоказателиЛиста.П000800001090
			+ ПоказателиЛиста.П000800001100
			+ ПоказателиЛиста.П000800001110
			+ ПоказателиЛиста.П000800001120
			+ ПоказателиЛиста.П000800001130
			+ ПоказателиЛиста.П000800001140
			+ ПоказателиЛиста.П000800001150;
		
		Если Форма.ТабличныйДокумент.Области.Найти("П000800001160") <> Неопределено Тогда
			Форма.ТабличныйДокумент.Области["П000800001160"].Значение = ПоказателиЛиста.П000800001160;
		КонецЕсли;
		
	ИначеЕсли ИмяЛиста = "Приложение7" Тогда
		
		// Формула определения суммы налоговой базы для определения имущественного вычета
		//ВсегоДоходПоСтавке13 (строка 010 Раздела 2) 
		// - пункт 12 Приложения 4
		// - пункт 4 Приложения 5
		// - пункт 4.1 Приложения 5
		// - подпункт 3.2 Приложения 3
		// - подпункт 6.5 Приложения 3
		// - подпункт 10.1 Приложения 6
		// - подпункт 2.3 Приложения 7
		// - подпункт 2.4 Приложения 7
		// - итог строк 060 Приложения 8
		
		// Превышение предела вычета на проценты не вычисляем, поскольку не можем определить год оформления кредита.
		// Для кредитов полученных до 2014 года предел вычета не применяется.
		// Считаем, что пользователь указал в строке 090 правильную сумму, доступную к вычету.
		
		ЗаполнитьСводныеТаблицы(Форма);
		
		СуммаДохода = СуммаДоходаДляИмущественногоВычета(Форма);
		
		СтраницаИтогов = Неопределено;
		
		ПределВычетаНаПриобретение = ПределВычета("НаПриобретениеНедвижимостиСтоимость", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
		ВсегоНаПриобретение   = 0;
		ОстатокВычетаНаПриобретение = ПределВычетаНаПриобретение;
		
		ВсегоПоПроцентам = 0;
		
		// Собираем информацию по вычетам по всем листам
		Для Каждого СтраницаЛиста Из Форма.ТаблицаСтраницыПриложение7 Цикл
			
			Если СтраницаЛиста.Данные.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтраницаИтогов  = СтраницаЛиста;
			ПоказателиЛиста = СтраницаЛиста.Данные[0].Значение;
			
			Если ПоказателиЛиста.П000700001080 > ОстатокВычетаНаПриобретение Тогда
				ПоказателиЛиста.П000700001080 = ОстатокВычетаНаПриобретение;
				Если СтраницаЛиста.АктивнаяСтраница И Форма.ТабличныйДокумент.Области.Найти("П000700001080") <> Неопределено Тогда
					Форма.ТабличныйДокумент.Области["П000700001080"].Значение = ПоказателиЛиста.П000700001080;
				КонецЕсли;
			КонецЕсли;
			
			ВсегоНаПриобретение = ВсегоНаПриобретение + ПоказателиЛиста.П000700001080;
			ОстатокВычетаНаПриобретение = Макс(0, ОстатокВычетаНаПриобретение - ПоказателиЛиста.П000700001080);
			
			ВсегоПоПроцентам = ВсегоПоПроцентам + ПоказателиЛиста.П000700001090;
			
		КонецЦикла;
		
		ВсегоНаПриобретение = Мин(ВсегоНаПриобретение, ПределВычетаНаПриобретение);
		
		Если СтраницаИтогов <> Неопределено Тогда
			
			ПоказателиЛиста = СтраницаИтогов.Данные[0].Значение;
			
			// Сумма дохода - налоговая база, вычисленная по формуле
			ПоказателиЛиста.Вставить("П000700001140", СуммаДохода);
			
			// Рассчитываем вычет на приобретение
			Если НЕ ПоказателиЛиста.Свойство("П000700001100") Тогда
				ПоказателиЛиста.Вставить("П000700001100", 0);
			КонецЕсли;
			
			Если НЕ ПоказателиЛиста.Свойство("П000700001120") Тогда
				ПоказателиЛиста.Вставить("П000700001120", 0);
			КонецЕсли;
			
			Если НЕ ПоказателиЛиста.Свойство("П000700001150") Тогда
				ПоказателиЛиста.Вставить("П000700001150", 0);
			КонецЕсли;
			
			ОстатокВычетаНаПриобретение = Макс(ВсегоНаПриобретение - ПоказателиЛиста.П000700001100 - ПоказателиЛиста.П000700001120, 0);
			
			ПринимаемыйВычетНаПриобретение = Мин(СуммаДохода, ОстатокВычетаНаПриобретение);
			ПоказательП000700001150 = 0;
			Если ПоказателиЛиста.Свойство("П000700001150", ПоказательП000700001150) Тогда
				ПринимаемыйВычетНаПриобретение = Мин(ПринимаемыйВычетНаПриобретение, ПоказательП000700001150);
			КонецЕсли;
			ПоказателиЛиста.Вставить("П000700001150", ПринимаемыйВычетНаПриобретение);
			
			ПоказателиЛиста.Вставить("П000700001170", Макс(ОстатокВычетаНаПриобретение - ПринимаемыйВычетНаПриобретение, 0));
			
			// Рассчитываем вычет по процентам
			Если НЕ ПоказателиЛиста.Свойство("П000700001110") Тогда
				ПоказателиЛиста.Вставить("П000700001110", 0);
			КонецЕсли;
			
			Если НЕ ПоказателиЛиста.Свойство("П000700001130") Тогда
				ПоказателиЛиста.Вставить("П000700001130", 0);
			КонецЕсли;
			
			// Сумму дохода нужно уменьшить на сумму рассчитанного вычета по расходам на приобретение
			СуммаДохода = СуммаДохода - ПринимаемыйВычетНаПриобретение;
			
			ОстатокВычетаПоПроцентам = Макс(ВсегоПоПроцентам - ПоказателиЛиста.П000700001110 - ПоказателиЛиста.П000700001130, 0);
			
			ПринимаемыйВычетПоПроцентам = Мин(СуммаДохода, ОстатокВычетаПоПроцентам);
			ПоказательП000700001160 = 0;
			Если ПоказателиЛиста.Свойство("П000700001160", ПоказательП000700001160) Тогда
				ПринимаемыйВычетПоПроцентам = Мин(ПринимаемыйВычетПоПроцентам, ПоказательП000700001160);
			КонецЕсли;
			
			ПоказателиЛиста.Вставить("П000700001160", ПринимаемыйВычетПоПроцентам);
			
			ПоказателиЛиста.Вставить("П000700001180", Макс(ОстатокВычетаПоПроцентам - ПринимаемыйВычетПоПроцентам, 0));
			
			Если СтраницаИтогов.АктивнаяСтраница И Форма.ТабличныйДокумент.Области.Найти("П000700001140") <> Неопределено Тогда
				Форма.ТабличныйДокумент.Области["П000700001140"].Значение = ПоказателиЛиста.П000700001140;
				Форма.ТабличныйДокумент.Области["П000700001150"].Значение = ПоказателиЛиста.П000700001150;
				Форма.ТабличныйДокумент.Области["П000700001160"].Значение = ПоказателиЛиста.П000700001160;
				Форма.ТабличныйДокумент.Области["П000700001170"].Значение = ПоказателиЛиста.П000700001170;
				Форма.ТабличныйДокумент.Области["П000700001180"].Значение = ПоказателиЛиста.П000700001180;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяЛиста = "Приложение5" Тогда
		
		ПоказателиЛиста = Форма.СтруктураДанныхПриложение5;
		
		// Общая сумма стандартных налоговых вычетов
		ПоказателиЛиста.П000900001080 = Макс(0,
			  ПоказателиЛиста.П000900001010
			+ ПоказателиЛиста.П000900001020
			+ ПоказателиЛиста.П000900001030
			+ ПоказателиЛиста.П000900001040
			+ ПоказателиЛиста.П000900001050
			+ ПоказателиЛиста.П000900001060
			- ПоказателиЛиста.П000900001070);
		
		// Итого по п. 2
		ПоказателиЛиста.П000900001120 =
			  ПоказателиЛиста.П000900001090
			+ ПоказателиЛиста.П000900001100
			+ ПоказателиЛиста.П000900001110;
		
		// Общая сумма расходов
		ПоказателиЛиста.П000900001180 = Мин(120000,
			  ПоказателиЛиста.П000900001130
			+ ПоказателиЛиста.П000900001140
			+ ПоказателиЛиста.П000900001150
			+ ПоказателиЛиста.П000900001160
			+ ПоказателиЛиста.П000900001170);
		
		// Общая сумма социальных налоговых вычетов
		ПоказателиЛиста.П000900001190 = Макс(ПоказателиЛиста.П000900001120 + ПоказателиЛиста.П000900001180 - ПоказателиЛиста.П000900001181, 0);
		
		// Общая сумма стандартных и социальных налоговых вычетов
		ПоказателиЛиста.П000900001200 = ПоказателиЛиста.П000900001080 + ПоказателиЛиста.П000900001190;
		
		Если Форма.ТабличныйДокумент.Области.Найти("П000900001200") <> Неопределено Тогда
			
			Форма.ТабличныйДокумент.Области["П000900001080"].Значение = ПоказателиЛиста.П000900001080;
			Форма.ТабличныйДокумент.Области["П000900001120"].Значение = ПоказателиЛиста.П000900001120;
			Форма.ТабличныйДокумент.Области["П000900001180"].Значение = ПоказателиЛиста.П000900001180;
			Форма.ТабличныйДокумент.Области["П000900001190"].Значение = ПоказателиЛиста.П000900001190;
			Форма.ТабличныйДокумент.Области["П000900001200"].Значение = ПоказателиЛиста.П000900001200;
			
		КонецЕсли; 
		
	ИначеЕсли ИмяЛиста = "Приложение5Расчет" Тогда
		
		ОбщаяСуммаВзносовПенсионныйФонд = 0;
		
		СтраницаИтоговПриложение5Расчет = Неопределено;
		
		Для Каждого Лист Из Форма.ТаблицаСтраницыПриложение5Расчет Цикл
			
			Если Лист.Данные.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			СтрокаЛиста = Лист.Данные[0].Значение;
			
			ОбщаяСуммаВзносовПенсионныйФонд = ОбщаяСуммаВзносовПенсионныйФонд + СтрокаЛиста.П001000001060 + СтрокаЛиста.П001000001070;
			
			// Очищаем итоговые показатели
			СтрокаЛиста.П001000001080 = 0; // Общая сумма взносов, принимаемая к вычету
			
			СтраницаИтоговПриложение5Расчет = Лист;
			
		КонецЦикла;
		
		// Обновим связанный показатель Приложения 5
		Форма.СтруктураДанныхПриложение5.П000900001160 = ОбщаяСуммаВзносовПенсионныйФонд;
		
		Если СтраницаИтоговПриложение5Расчет <> Неопределено Тогда
			
			СтрокаЛиста = СтраницаИтоговПриложение5Расчет.Данные[0].Значение;
			СтрокаЛиста.П001000001080 = ОбщаяСуммаВзносовПенсионныйФонд;
			
			Если СтраницаИтоговПриложение5Расчет.АктивнаяСтраница И Форма.ТабличныйДокумент.Области.Найти("П001000001080") <> Неопределено Тогда
				Форма.ТабличныйДокумент.Области["П001000001080"].Значение = ОбщаяСуммаВзносовПенсионныйФонд;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Не ЗначениеЗаполнено(ИмяЛиста) Тогда
		
		Для Каждого СтрокаДерева Из Форма.мДеревоСтраницОтчета.ПолучитьЭлементы() Цикл
			РассчитатьИтогиЛиста(Форма, СтрокаДерева.ИмяСтраницы);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСводныеТаблицы(Форма)
	
	Форма.СводДоходов.Очистить();
	Форма.СводВычетов.Очистить();
	
	ВидыДоходов = ВидыДоходов(Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
	КодыВидовДоходовРФ = КодыВидовДоходовРФ(Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
	КодыБК = КБК(Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	Для Каждого Лист Из Форма.ТаблицаСтраницыПриложение1 Цикл
		
		Если Лист.Данные.Количество() = 0 ИЛИ Лист.ДанныеДопСтрок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		Для Каждого СтрокаЛиста Из Лист.ДанныеДопСтрок[0].Значение Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаЛиста.П000300001070) Тогда
				Продолжить;
			КонецЕсли; 
			
			СтрокаДохода = Форма.СводДоходов.Добавить();
			Если СтрокаЛиста.П000300001020 = КодыВидовДоходовРФ.ОплатаТруда
				ИЛИ СтрокаЛиста.П000300001020 = КодыВидовДоходовРФ.ОперацииСЦеннымиБумагами
				ИЛИ СтрокаЛиста.П000300001080 <> 0 Тогда
				СтрокаДохода.КБК = КодыБК.ДоходыПолученныеОтНалоговыхАгентов;
			Иначе
				СтрокаДохода.КБК = КодыБК.ДоходыПриПолученииКоторыхНеУдержанНалог;
			КонецЕсли; 
			СтрокаДохода.ОКТМО = СтрокаЛиста.П000300001050;
			СтрокаДохода.Ставка = СтрокаЛиста.П000300001010;
			
			Если СтрокаЛиста.П000300001020 = КодыВидовДоходовРФ.Дивиденды Тогда
				СтрокаДохода.ВидДохода = ВидыДоходов.Дивиденды;
			Иначе
				СтрокаДохода.ВидДохода = ВидыДоходов.Иное;
			КонецЕсли;
			
			СтрокаДохода.СуммаДохода      = СтрокаЛиста.П000300001070;
			СтрокаДохода.НачисленоНалога  = Окр(СтрокаЛиста.П000300001070 * СтрокаДохода.Ставка / 100, 0);
			СтрокаДохода.УдержаноНалога   = СтрокаЛиста.П000300001080;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого Лист Из Форма.ТаблицаСтраницыПриложение2 Цикл
		
		Если Лист.Данные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ПоказателиЛиста = Лист.Данные[0].Значение;
		СтавкаНалога = ПоказателиЛиста.П000400001001;
		
		Если НЕ ЗначениеЗаполнено(ПоказателиЛиста.П000400001070) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДохода        = Форма.СводДоходов.Добавить();
		СтрокаДохода.КБК    = КодыБК.ДоходыПриПолученииКоторыхНеУдержанНалог;
		СтрокаДохода.ОКТМО  =  Форма.СтруктураРеквизитовФормы.ОКТМО;
		СтрокаДохода.Ставка = СтавкаНалога;
		
		Если Строка(ПоказателиЛиста.П000400001031) = КодВидаДоходаЗаПределамиРФ("ПрибыльКИК", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма) Тогда
			СтрокаДохода.ВидДохода = ВидыДоходов.ПрибыльКИК;
		Иначе
			СтрокаДохода.ВидДохода = ВидыДоходов.Иное;
		КонецЕсли;
		
		СтрокаДохода.СуммаДохода     = ПоказателиЛиста.П000400001070;
		СтрокаДохода.НачисленоНалога = ПоказателиЛиста.П000400001120;
		
		СтрокаДохода.НалогПринимаемыйКУчету   = ПоказателиЛиста.П000400001130;
		
		Если ПоказателиЛиста.П000400001071 <> 0 Тогда
			
			СтрокаВычета                        = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета              = "СуммаРубСтоимостиИмуществаПриЛиквидацииКИК";
			СтрокаВычета.Ставка                 = СтавкаНалога;
			СтрокаВычета.СуммаВычетаДляРаздела2 = ПоказателиЛиста.П000400001071;
			
			Если Строка(ПоказателиЛиста.П000400001031) = КодВидаДоходаЗаПределамиРФ("ПрибыльКИК", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма) Тогда
				СтрокаВычета.ВидДохода = ВидыДоходов.ПрибыльКИК;
			Иначе
				СтрокаВычета.ВидДохода = ВидыДоходов.Иное;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПоказателиЛиста.П000400001072 <> 0 Тогда
			
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "СуммаРубДивидендовКИК";
			СтрокаВычета.Ставка                  = СтавкаНалога;
			СтрокаВычета.СуммаВычетаДляРаздела2  = ПоказателиЛиста.П000400001072;
			Если Строка(ПоказателиЛиста.П000400001031) = КодВидаДоходаЗаПределамиРФ("ПрибыльКИК", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма) Тогда
				СтрокаВычета.ВидДохода = ВидыДоходов.ПрибыльКИК;
			Иначе
				СтрокаВычета.ВидДохода = ВидыДоходов.Иное;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	СтраницаИтоговПриложения3 = Неопределено;
	Для Каждого Лист Из Форма.ТаблицаСтраницыПриложение3 Цикл
		
		Если Лист.Данные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаЛиста = Лист.Данные[0].Значение;
		
		Если ЗначениеЗаполнено(СтрокаЛиста.П000500001110) Тогда // Код источника дохода
			СтрокаВычета = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета = "ОбщаяСуммаПрофессиональныхВычетов";
			СтрокаВычета.Ставка = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2 = СтрокаЛиста.П000500001150; // Расчет общей суммы расходов, принимаемых к вычету
			СтрокаВычета.СуммаВычетаДляД1 = СтрокаЛиста.П000500001150;
			СтрокаВычета.ВидДохода = ВидыДоходов.Иное;
		КонецЕсли;
		
		СтраницаИтоговПриложения3 = Лист;
		
	КонецЦикла;
	
	Если СтраницаИтоговПриложения3 <> 0 Тогда
		
		СтрокаЛиста = СтраницаИтоговПриложения3.Данные[0].Значение;
		
		Если СтрокаЛиста.П000500001050 <> 0 Тогда
			СтрокаДохода = Форма.СводДоходов.Добавить();
			СтрокаДохода.КБК = КодыБК.ДоходыОтПредпринимательскойДеятельности;
			СтрокаДохода.ОКТМО =  Форма.СтруктураРеквизитовФормы.ОКТМО;
			СтрокаДохода.Ставка = 13;
			СтрокаДохода.СуммаДохода = СтрокаЛиста.П000500001050;
			СтрокаДохода.НачисленоНалога = Окр((СтрокаЛиста.П000500001050 - СтрокаЛиста.П000500001060) * СтрокаДохода.Ставка / 100, 0);
			СтрокаДохода.УплаченоНалога = СтрокаЛиста.П000500001070;
			СтрокаДохода.ВидДохода = ВидыДоходов.Иное;
		КонецЕсли;
		
		Если СтрокаЛиста.П000500001060 <> 0 Тогда
			СтрокаВычета = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета = "ОбщаяСуммаВычетаИП";
			СтрокаВычета.Ставка = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2 = СтрокаЛиста.П000500001060;
			СтрокаВычета.СуммаВычетаДляД1 = СтрокаЛиста.П000500001060;
			СтрокаВычета.ВидДохода = ВидыДоходов.Иное;
		КонецЕсли;
		
	КонецЕсли;
	
	// Приложение 4
	Если Форма.СтруктураДанныхПриложение4.П000600001120 <> 0 Тогда
		СтрокаВычета                         = Форма.СводВычетов.Добавить();
		СтрокаВычета.ВидВычета               = "СуммаДоходовНеПодлежащаяНалогообложению";
		СтрокаВычета.Ставка                  = 13;
		СтрокаВычета.СуммаВычетаДляРаздела2  = Форма.СтруктураДанныхПриложение4.П000600001120;
		СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
		СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
	КонецЕсли;
	
	// Общая сумма социальных налоговых вычетов, предоставленных в отчетном налоговом периоде налоговыми агентами
	Если Форма.СтруктураДанныхПриложение5.П000900001181 <> 0 Тогда
		СтрокаВычета                         = Форма.СводВычетов.Добавить();
		СтрокаВычета.ВидВычета               = "ОбщаяСуммаСоциальныхВычетовПредоставленныхНалоговымиАгентами";
		СтрокаВычета.Ставка                  = 13;
		СтрокаВычета.СуммаВычетаДляРаздела2  = Форма.СтруктураДанныхПриложение5.П000900001181;
		СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
		СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
	КонецЕсли;
	
	// Общая сумма стандартных и социальных налоговых вычетов
	Если Форма.СтруктураДанныхПриложение5.П000900001200 <> 0 Тогда
		СтрокаВычета                         = Форма.СводВычетов.Добавить();
		СтрокаВычета.ВидВычета               = "ОбщаяСуммаСтандартныхСоциальныхВычетов";
		СтрокаВычета.Ставка                  = 13;
		СтрокаВычета.СуммаВычетаДляРаздела2  = Форма.СтруктураДанныхПриложение5.П000900001200;
		СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
		СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
	КонецЕсли;
	
	// Сумма инвестиционного налогового вычета, предусмотренного
	// подпунктом 2 пункта 1 статьи 219.1 Налогового кодекса Российской Федерации
	Если Форма.СтруктураДанныхПриложение5.П000900001210 <> 0 Тогда
		СтрокаВычета = Форма.СводВычетов.Добавить();
		СтрокаВычета.ВидВычета = "ИнвестиционныйВычет";
		СтрокаВычета.Ставка = 13;
		СтрокаВычета.СуммаВычетаДляРаздела2 = Форма.СтруктураДанныхПриложение5.П000900001210;
		СтрокаВычета.СуммаВычетаДляД1 = СтрокаВычета.СуммаВычетаДляРаздела2;
		СтрокаВычета.ВидДохода = ВидыДоходов.Иное;
	КонецЕсли;
	
	// Сумма предоставленного в предшествующих налоговых периодах инвестиционного
	// налогового вычета, предусмотренного подпунктом 2 пункта 1 статьи 219.1 Налогового
	// кодекса Российской Федерации, подлежащая восстановлению
	Если Форма.СтруктураДанныхПриложение5.П000900001220 <> 0 Тогда
		СтрокаДохода = Форма.СводДоходов.Добавить();
		СтрокаДохода.КБК = КодыБК.ДоходыПриПолученииКоторыхНеУдержанНалог;
		СтрокаДохода.ОКТМО =  Форма.СтруктураРеквизитовФормы.ОКТМО;
		СтрокаДохода.Ставка = 13;
		СтрокаДохода.СуммаДохода = Форма.СтруктураДанныхПриложение5.П000900001220;
		СтрокаДохода.НачисленоНалога = Окр((СтрокаДохода.СуммаДохода) * СтрокаДохода.Ставка / 100, 0);
		СтрокаДохода.ВидДохода = ВидыДоходов.Иное;
	КонецЕсли;
	
	Для Каждого СтраницаЛиста Из Форма.ТаблицаСтраницыПриложение7 Цикл
		
		Если СтраницаЛиста.Данные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПоказателиЛиста = СтраницаЛиста.Данные[0].Значение;
		
		// Итоговые показатели есть только на последнем листе, если есть один,
		// то есть и остальные, в противном случае пропускаем эту страницу
		Если ПоказателиЛиста.Свойство("П000700001120") Тогда
			// - подпункт 2.3 Приложение 7
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "СуммаИмущественногоВычетаПредоставленногоНалоговымиАгентамиРасходы";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = ПоказателиЛиста.П000700001120;
			СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
			
			// - подпункт 2.4 Приложение 7
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "СуммаИмущественногоВычетаПредоставленногоНалоговымиАгентамиПроценты";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = ПоказателиЛиста.П000700001130;
			СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
			
			// - подпункт 2.6 Приложение 7
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "СуммаРасходовНаПриобретениеНедвижимостиКВычету";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = ПоказателиЛиста.П000700001150;
			СтрокаВычета.СуммаВычетаДляД1        = 0;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
			
			// - подпункт 2.7 Приложение 7
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "СуммаПроцентовПоКредитуНаНедвижимостьКВычету";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = ПоказателиЛиста.П000700001160;
			СтрокаВычета.СуммаВычетаДляД1        = 0;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
		КонецЕсли;
		
	КонецЦикла;
	
	// - пункт 4 Приложение 6
	Если Форма.СтруктураДанныхПриложение6.П000800001160 <> 0 Тогда
		СтрокаВычета                         = Форма.СводВычетов.Добавить();
		СтрокаВычета.ВидВычета               = "ОбщаяСуммаИмущественногоВычета";
		СтрокаВычета.Ставка                  = 13;
		СтрокаВычета.СуммаВычетаДляРаздела2  = Форма.СтруктураДанныхПриложение6.П000800001160;
		СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
		СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
	КонецЕсли;
	
	Для Каждого СтраницаЛиста Из Форма.ТаблицаСтраницыПриложение8 Цикл
		
		Если СтраницаЛиста.Данные.Количество() = 0 Или СтраницаЛиста.ДанныеДопСтрок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаЛиста Из СтраницаЛиста.ДанныеДопСтрок[0].Значение Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаЛиста.П001200001010) Тогда
				Продолжить;
			КонецЕсли;
			
			ДоступныйВычет = СтрокаЛиста.П001200001020;
			
			// Поле 040 приложения 8
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "ЦенныеБумагиСуммаРасходовУменьшающаяДоходы";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = Мин(ДоступныйВычет, СтрокаЛиста.П001200001040);
			СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
			
			ДоступныйВычет = Макс(0, ДоступныйВычет - СтрокаВычета.СуммаВычетаДляРаздела2);
			
			// Поле 052 приложения 8
			Для Каждого ЗначениеПоказателя Из СтрокаЛиста.П001200001052 Цикл
				СтрокаВычета                         = Форма.СводВычетов.Добавить();
				СтрокаВычета.ВидВычета               = "ЦенныеБумагиСуммаУбытка";
				СтрокаВычета.Ставка                  = 13;
				СтрокаВычета.СуммаВычетаДляРаздела2  = Мин(ДоступныйВычет, ЗначениеПоказателя.Значение);
				СтрокаВычета.СуммаВычетаДляД1        = СтрокаВычета.СуммаВычетаДляРаздела2;
				СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
				
				ДоступныйВычет = Макс(0, ДоступныйВычет - СтрокаВычета.СуммаВычетаДляРаздела2);
			КонецЦикла;
			
			// Поле 060 приложения 8
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "ЦенныеБумагиСуммаПрибылиКВычету";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = Мин(ДоступныйВычет, СтрокаЛиста.П001200001060);
			СтрокаВычета.СуммаВычетаДляД1        = 0;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
			
			ДоступныйВычет = Макс(0, ДоступныйВычет - СтрокаВычета.СуммаВычетаДляРаздела2);
			
			// Поле 070 приложения 8
			СтрокаВычета                         = Форма.СводВычетов.Добавить();
			СтрокаВычета.ВидВычета               = "ЦенныеБумагиСуммаПрибылиПоОперациямНаИИСкВычету";
			СтрокаВычета.Ставка                  = 13;
			СтрокаВычета.СуммаВычетаДляРаздела2  = Мин(ДоступныйВычет, СтрокаЛиста.П001200001070);
			СтрокаВычета.СуммаВычетаДляД1        = 0;
			СтрокаВычета.ВидДохода               = ВидыДоходов.Иное;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СуммаДоходаДляИмущественногоВычета(Форма)

	Результат = 0;
	
	// Определяем сумму обычного дохода по ставке 13%
	Отбор = Новый Структура("Ставка, ВидДохода", 13, ВидДохода("Иное", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма));
	СтрокиДохода = Форма.СводДоходов.НайтиСтроки(Отбор);
	Для Каждого СтрокаДохода Из СтрокиДохода Цикл
		Результат = Результат + СтрокаДохода.СуммаДохода;
	КонецЦикла;
	
	// Вычитаем суммы вычетов, принимаемых до имущественного вычета 
	Результат = Результат - Форма.СводВычетов.Итог("СуммаВычетаДляД1");

	Возврат Макс(Результат, 0);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СуммаОблагаемогоДоходаПоСтавке(Форма, Ставка)

	// Вычисляет и возвращает сумму, соответствующую п. 3 раздела 2
	Результат = 0;
	
	СтрокиДохода = Форма.СводДоходов.НайтиСтроки(Новый Структура("Ставка", Ставка));
	Для Каждого СтрокаДохода Из СтрокиДохода Цикл
		Результат = Результат + СтрокаДохода.СуммаДохода;
	КонецЦикла; 
	
	Если Ставка = 13 Тогда
		
		Для Каждого СтрокаВычета Из Форма.СводВычетов Цикл
			Если СтрокаВычета.ВидВычета = "СуммаДоходовНеПодлежащаяНалогообложению" Тогда
				Результат = Результат - СтрокаВычета.СуммаВычетаДляРаздела2;
			КонецЕсли; 
		КонецЦикла; 
		
	КонецЕсли; 
	
	Возврат Макс(Результат, 0);

КонецФункции

&НаКлиенте
Процедура РасчетНаКлиенте(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт
	
	Расчет(ЭтотОбъект, ИмяСтраницыПанели, ОбластьИмя);
	
КонецПроцедуры

&НаСервере
Процедура РасчетНаСервере(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт 
	
	Расчет(ЭтотОбъект, ИмяСтраницыПанели, ОбластьИмя);
	
КонецПроцедуры

// Выполняет провреку значений и пересчет зависимых показателей на заданной странице отчета
// Параметры:
//  Форма - управляемая форма - форма этого отчета
//	ИмяОбласти - Строка - имя раздела или листа декларации
//
&НаКлиентеНаСервереБезКонтекста
Процедура Расчет(Форма, ИмяРаздела = "", ИмяОбласти = "")
	
	Перем ТаблицаБлока, ТаблицаРаздела;
	
	Если (Форма.СтруктураРеквизитовФормы.мРежимПечати) Тогда
		Возврат; // служебный вызов в процессе печати
	КонецЕсли;
	
	Если (Форма.СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет) Тогда
		Возврат; // отключен режим авторасчета
	КонецЕсли;
	
	ИмяОбласти13 = Лев(ИмяОбласти, 13); // имя области без номера строки
	
	// Проверки значений и пересчет связанных НЕ итоговых реквизитов
	Если ИмяРаздела = "Приложение2" Тогда
		
		// Проверка необходимости пересчитать сумму дохода из валюты в рубли
		ПересчитыватьДоходВруб = ИмяОбласти = "П000400001030" // Код валюты
			Или ИмяОбласти = "П000400001040" // дата получения дохода
			Или ИмяОбласти = "П000400001050" // курс валюты на дату получения дохода
			Или ИмяОбласти = "П000400001060"; // сумма дохода в валюте
			
		Если ПересчитыватьДоходВруб Тогда
			
			Курс         = Форма.ТабличныйДокумент.Области["П000400001050"].Значение;
			СуммаВВалюте = Форма.ТабличныйДокумент.Области["П000400001060"].Значение;
			
			Если ЗначениеЗаполнено(Курс) И ЗначениеЗаполнено(СуммаВВалюте) Тогда
				ДоходВРублях = Окр(СуммаВВалюте * Курс, 2);
				Форма.ТабличныйДокумент.Области["П000400001070"].Значение = ДоходВРублях;
				СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000400001070", ДоходВРублях);
			КонецЕсли; 
			
		КонецЕсли; 
		
		// Проверка необходимости пересчитать сумму налога из валюты в рубли
		ПересчитыватьНалогВРуб = ИмяОбласти = "П000400001030" // Код валюты
			Или ИмяОбласти = "П000400001080" // дата уплаты налога
			Или ИмяОбласти = "П000400001090" // курс валюты на дату уплаты налога
			Или ИмяОбласти = "П000400001100"; // сумма налога в валюте
			
		Если ПересчитыватьНалогВРуб Тогда
			
			Курс         = Форма.ТабличныйДокумент.Области["П000400001090"].Значение;
			СуммаВВалюте = Форма.ТабличныйДокумент.Области["П000400001100"].Значение;
			
			Если ЗначениеЗаполнено(Курс) И ЗначениеЗаполнено(СуммаВВалюте) Тогда
				НалогВРублях = Окр(СуммаВВалюте * Курс, 2); 
				Форма.ТабличныйДокумент.Области["П000400001110"].Значение = НалогВРублях;
				СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000400001110", НалогВРублях);
			КонецЕсли; 
			
		КонецЕсли; 
		
		// Расчет суммы налога, начисленного в РФ.
		СтавкаНалога = Форма.ТабличныйДокумент.Области["П000400001001"].Значение;
		
		Если ИмяОбласти = "П000400001001" Тогда // Ставка налога 
			
			НалогВРФ = Окр(Форма.ТабличныйДокумент.Области["П000400001070"].Значение * СтавкаНалога / 100, 2);
			Форма.ТабличныйДокумент.Области["П000400001120"].Значение = НалогВРФ;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000400001120", НалогВРФ);
			
			// Сумма принимаемого налога не может превышать сумму уплаченного
			Если Форма.ТабличныйДокумент.Области["П000400001130"].Значение > 0 Тогда
				ПринимаемаяСумма = Мин(Форма.ТабличныйДокумент.Области["П000400001120"].Значение, Форма.ТабличныйДокумент.Области["П000400001110"].Значение);
				Форма.ТабличныйДокумент.Области["П000400001130"].Значение = ПринимаемаяСумма;
				СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000400001130", ПринимаемаяСумма);
			КонецЕсли; 
			
		ИначеЕсли ПересчитыватьДоходВруб ИЛИ ИмяОбласти = "П000400001070" Тогда
			
			НалогВРФ = Окр(Форма.ТабличныйДокумент.Области["П000400001070"].Значение * СтавкаНалога / 100, 2);
			Форма.ТабличныйДокумент.Области["П000400001120"].Значение = НалогВРФ;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000400001120", НалогВРФ);
			
		КонецЕсли; 

		Если (ПересчитыватьДоходВруб ИЛИ ПересчитыватьНалогВРуб ИЛИ ИмяОбласти = "П000400001130" ИЛИ ИмяОбласти = "П000400001120" ИЛИ ИмяОбласти = "П000400001070")
				И Форма.ТабличныйДокумент.Области["П000400001130"].Значение > 0 Тогда
			
			Значение = Мин(Форма.ТабличныйДокумент.Области["П000400001130"].Значение, Форма.ТабличныйДокумент.Области["П000400001120"].Значение);
			
			Форма.ТабличныйДокумент.Области["П000400001130"].Значение = Значение;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000400001130", Значение);
			
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Приложение3" Тогда
		
		Если ИмяОбласти = "П000500001040" И Форма.ТабличныйДокумент.Области["П000500001040"].Значение = 0 Тогда 
			// очищаем фактические расходы
			Форма.ТабличныйДокумент.Области["П000500001041"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001041", 0);
			Форма.ТабличныйДокумент.Области["П000500001042"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001042", 0);
			Форма.ТабличныйДокумент.Области["П000500001043"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001043", 0);
			Форма.ТабличныйДокумент.Области["П000500001044"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001044", 0);
			ОбновлятьФактическиеРасходы = Истина;
		Иначе
			ОбновлятьФактическиеРасходы = ИмяОбласти = "П000500001041" // Сумма материальных расходов
				Или ИмяОбласти = "П000500001042" // Сумма амортизационных начислений
				Или ИмяОбласти = "П000500001043" // Сумма расходов на выплаты и вознаграждения в пользу физических лиц
				Или ИмяОбласти = "П000500001044"; // Сумма прочих расходов
		КонецЕсли;
		
		ВсегоФактРасходы = Форма.ТабличныйДокумент.Области["П000500001041"].Значение
			+ Форма.ТабличныйДокумент.Области["П000500001042"].Значение
			+ Форма.ТабличныйДокумент.Области["П000500001043"].Значение
			+ Форма.ТабличныйДокумент.Области["П000500001044"].Значение;
		
		Если ОбновлятьФактическиеРасходы
				Или ИмяОбласти = "П000500001040"
					И Форма.ТабличныйДокумент.Области["П000500001040"].Значение < ВсегоФактРасходы Тогда
			
			Форма.ТабличныйДокумент.Области["П000500001040"].Значение = ВсегоФактРасходы;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001040", ВсегоФактРасходы);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Форма.ТабличныйДокумент.Области["П000500001110"].Значение) Тогда // Код источника дохода
			Форма.ТабличныйДокумент.Области["П000500001150"].Значение =
				  Форма.ТабличныйДокумент.Области["П000500001120"].Значение
				+ Форма.ТабличныйДокумент.Области["П000500001130"].Значение
				+ Форма.ТабличныйДокумент.Области["П000500001140"].Значение;
		Иначе
			Форма.ТабличныйДокумент.Области["П000500001120"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001120", 0);
			Форма.ТабличныйДокумент.Области["П000500001130"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001130", 0);
			Форма.ТабличныйДокумент.Области["П000500001140"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001140", 0);
			Форма.ТабличныйДокумент.Области["П000500001150"].Значение = 0;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000500001150", 0);
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Приложение7" Тогда
		
		ПределВычетаНаПриобретение = ПределВычета("НаПриобретениеНедвижимостиСтоимость", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
		// Превышение предела вычета на проценты не вычисляем, поскольку не можем определить год оформления кредита.
		// Для кредитов полученных до 2014 года предел вычета не применяется.
		
		// Для определения допустимых пределов вычетов подсчитаем расходы, указанные на других (неактивных) страницах
		РасходыНаПриобретение = 0;
		Для Каждого СтраницаЛиста Из Форма.ТаблицаСтраницыПриложение7 Цикл
			
			Если СтраницаЛиста.АктивнаяСтраница Тогда
				Продолжить;
			КонецЕсли;
			
			ПоказателиЛиста = СтраницаЛиста.Данные[0].Значение;
			
			РасходыНаПриобретение = РасходыНаПриобретение + ПоказателиЛиста.П000700001080;
			
		КонецЦикла;
		
		ОстатокПределаНаПриобретение = Макс(ПределВычетаНаПриобретение - РасходыНаПриобретение, 0);
		
		Если ИмяОбласти = "П000700001080"  // Сумма раходов на приобретение
				И Форма.ТабличныйДокумент.Области["П000700001080"].Значение > ОстатокПределаНаПриобретение Тогда
			
			// Сумма фактических расходов не может превышать установленный законом предел
			Форма.ТабличныйДокумент.Области["П000700001080"].Значение = ОстатокПределаНаПриобретение;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000700001080", Форма.ТабличныйДокумент.Области["П000700001080"].Значение);
			
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Приложение6" Тогда
		
		ПределНормыВычетаНедвижимости     = ПределВычета("ПродажаЖилойНедвижимости", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);;
		ПределНормыВычетаПрочегоИмущества = ПределВычета("ПродажаИногоИмущества", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);;
		
		Если ИмяОбласти = "П000800001010"  // Вычет из доходов от продажи недвижимости, находившейся в собственности менее 3 лет
			И Форма.ТабличныйДокумент.Области["П000800001010"].Значение > ПределНормыВычетаНедвижимости Тогда 
			// п. 1.1
			
			Форма.ТабличныйДокумент.Области["П000800001010"].Значение = ПределНормыВычетаНедвижимости;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000800001010", ПределНормыВычетаНедвижимости);
			
		ИначеЕсли ИмяОбласти = "П000800001030"  // Вычет из доходов от продажи долей недвижимости, находившейся в собственности менее 3 лет
				И Форма.ТабличныйДокумент.Области["П000800001030"].Значение > ПределНормыВычетаНедвижимости Тогда
			// п. 1.3
			
			Форма.ТабличныйДокумент.Области["П000800001030"].Значение = ПределНормыВычетаНедвижимости;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000800001030", ПределНормыВычетаНедвижимости);
			
		ИначеЕсли ИмяОбласти = "П000800001050"  // Иное недвижимое имущество для расчета вычета в 250000 руб
				И Форма.ТабличныйДокумент.Области["П000800001050"].Значение > ПределНормыВычетаПрочегоИмущества Тогда
			// п. 2.1
			
			Форма.ТабличныйДокумент.Области["П000800001050"].Значение = ПределНормыВычетаПрочегоИмущества;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000800001050", ПределНормыВычетаПрочегоИмущества);
			
		ИначеЕсли ИмяОбласти = "П000800001070"  // Вычет по норме из доходов от продажи прочего имущества 
				И Форма.ТабличныйДокумент.Области["П000800001070"].Значение > ПределНормыВычетаПрочегоИмущества Тогда
			// п. 3.1 
			
			Форма.ТабличныйДокумент.Области["П000800001070"].Значение = ПределНормыВычетаПрочегоИмущества;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П000800001070", ПределНормыВычетаПрочегоИмущества);
			
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Приложение5" Тогда
		
		Если ИмяОбласти = "П000900001010" Тогда // Сумма стандартного налогового вычета в соответствии с пп.1 п.1 ст.218 НК РФ
			МаксимальноеЗначение = ПределВычета("СтандартныйВычетПо218_1_1", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
		ИначеЕсли ИмяОбласти = "П000900001020" Тогда // Сумма стандартного налогового вычета в соответствии с пп.2 п.1 ст.218 НК РФ
			МаксимальноеЗначение = ПределВычета("СтандартныйВычетПо218_1_2", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
		ИначеЕсли ИмяОбласти = "П000900001210" Тогда// Сумма инвестиционного налогового вычета
			МаксимальноеЗначение = ПределВычета("ИнвестиционныйВычет", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
		Иначе
			МаксимальноеЗначение = Неопределено;
		КонецЕсли;
		
		Если МаксимальноеЗначение <> Неопределено И Форма.ТабличныйДокумент.Области[ИмяОбласти].Значение > МаксимальноеЗначение Тогда
			Форма.ТабличныйДокумент.Области[ИмяОбласти].Значение = МаксимальноеЗначение;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, ИмяОбласти, МаксимальноеЗначение);
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Приложение1Расчет" Тогда
		
		НомерСтрокиСтрокой  = Сред(ИмяОбласти, 15);
		
		// При изменении суммы дохода исходя из цены договора или исходя из кадастровой стоимости
		Если ИмяОбласти13 = "П001900001030" ИЛИ ИмяОбласти13 = "П001900001040" Тогда
			
			П001900001050 = Макс(
				Форма.ТабличныйДокумент.Области["П001900001030_" + НомерСтрокиСтрокой].Значение,
				Форма.ТабличныйДокумент.Области["П001900001040_" + НомерСтрокиСтрокой].Значение);
			П001900001050 = Макс(П001900001050, 0);
			
			Форма.ТабличныйДокумент.Области["П001900001050_" + НомерСтрокиСтрокой].Значение = П001900001050;
			СохранитьЗначениеПоказателяВСтруктуре(Форма, ИмяРаздела, "П001900001050_" + НомерСтрокиСтрокой, П001900001050);
			
		КонецЕсли;
		
	ИначеЕсли ИмяРаздела = "Раздел2" Тогда
	
		// Пересчет выполняется при выводе листа или при изменении ставки
		
		ЗаполнитьСводныеТаблицы(Форма);
		
		ТаблицаСтраницыРаздел2 = Форма.ТаблицаСтраницыРаздел2;
		Для Каждого ЛистСтраницы Из ТаблицаСтраницыРаздел2 Цикл
			Если ЗначениеЗаполнено(ЛистСтраницы.Данные[0].Значение.П000200001001) Тогда
				РасчитатьИтогиСтраницыРаздела2(ЛистСтраницы.Данные[0].Значение);
			КонецЕсли;
			Если ЛистСтраницы.АктивнаяСтраница Тогда
				АктивнаяСтраница = ЛистСтраницы;
			КонецЕсли;
		КонецЦикла;
		
		Ставка = Форма.ТабличныйДокумент.Области.П000200001001.Значение;
		ВидДохода = Форма.ТабличныйДокумент.Области.П000200001002.Значение;
		ОчиститьЗначенияСтраницы(Форма, АктивнаяСтраница.Данные[0].Значение, "Раздел2", Ложь);
		ЗаполнитьРасчетныеДанныеРаздела2(Форма, АктивнаяСтраница, ?(ЗначениеЗаполнено(Ставка), Число(Ставка), 0), ВидДохода, Истина);
		
	КонецЕсли;
	
	// Пересчет итогов листа
	РассчитатьИтогиЛиста(Форма, ИмяРаздела);
	
	// Обновляем сводные таблицы
	ЗаполнитьСводныеТаблицы(Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьДанныеВРаздел2()
	
	//Пересчитываем итоги всех листов
	РассчитатьИтогиЛиста(ЭтотОбъект, Неопределено);
	
	// Заполняем сводные таблицы итогов
	ЗаполнитьСводныеТаблицы(ЭтотОбъект);
	
	// Сначала в разделе 2 рассчитываются результаты в разрезе ставок
	ПодготовитьДанныеРаздела2(ЭтотОбъект);
	
	// Обновляем дерево разделов
	СформироватьДеревоРазделовОтчета(ЭтотОбъект);
	
	// Выводим макет в табличный документ
	ВывестиМакетВТаблДокНаСервере("Раздел2", 1, , , Истина);
	
	// Активизируем строку раздела в дереве
	Для Каждого СтрокаРаздела Из РазделыОтчета.ПолучитьЭлементы() Цикл
		Если СтрокаРаздела.КолонкаРазделыОтчетаСокрНаим = "Раздел2" Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = СтрокаРаздела.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

// Заполняет страницу расчетными данными по указанной ставке
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРасчетныеДанныеРаздела2(Форма, СтраницаРаздела, Ставка, ВидДохода, ОбновлятьТабличныйДокумент = Ложь)

	ДанныеСтраницы = СтраницаРаздела.Данные[0].Значение;
	
	ОКТМОНАлогоплательщика = Форма.СтруктураРеквизитовФормы.Октмо;
	
	// Устанавливаем ставку налога и вид дохода
	ДанныеСтраницы.П000200001001 = Число(Ставка);
	ДанныеСтраницы.П000200001002 = ВидДохода;
	
	Если ЗначениеЗаполнено(Ставка) Тогда
		
		Если ЗначениеЗаполнено(ВидДохода) Тогда
			
			// Находим строки доходов по указанной ставке
			СтрокиДоходов = Форма.СводДоходов.НайтиСтроки(Новый Структура("Ставка, ВидДохода", Число(Ставка), ВидДохода));
			
			Если СтрокиДоходов.Количество() > 0 Тогда
				
				// Заполняем суммами полученного дохода и уплаченных (удержанных) налогов
				Для Каждого СтрокаДохода Из СтрокиДоходов Цикл
					
					Если Не ОбновлятьТабличныйДокумент Тогда
						// поля, доступные для редактирования обновляем только при автоматическом заполнении (без обновления табличного документа)
					КонецЕсли;
					ДанныеСтраницы.П000200001080 = ДанныеСтраницы.П000200001080 + СтрокаДохода.УдержаноНалога;
					ДанныеСтраницы.П000200001110 = ДанныеСтраницы.П000200001110 + СтрокаДохода.УплаченоНалога;
					ДанныеСтраницы.П000200001130 = ДанныеСтраницы.П000200001130 + СтрокаДохода.НалогПринимаемыйКУчету;
					
					Если СтрокаДохода.ВидДохода = ВидДохода("ПрибыльКИК", Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма) Тогда
						ДанныеСтраницы.П000200001051 = ДанныеСтраницы.П000200001051 + СтрокаДохода.СуммаДохода;
					Иначе
						ДанныеСтраницы.П000200001010 = ДанныеСтраницы.П000200001010 + СтрокаДохода.СуммаДохода;
					КонецЕсли;
					
				КонецЦикла;
				
				// Заполняем суммами вычетов
				СтрокиВычетов = Форма.СводВычетов.НайтиСтроки(Новый Структура("Ставка, ВидДохода", Число(Ставка), ВидДохода));
				Для Каждого СтрокаВычета Из СтрокиВычетов Цикл
					
					Если СтрокаВычета.ВидВычета = "СуммаРубСтоимостиИмуществаПриЛиквидацииКИК"
						Или СтрокаВычета.ВидВычета = "СуммаРубДивидендовКИК"
						Или СтрокаВычета.ВидВычета = "СуммаДоходовНеПодлежащаяНалогообложению" Тогда
						
						ДанныеСтраницы.П000200001020 = ДанныеСтраницы.П000200001020 + СтрокаВычета.СуммаВычетаДляРаздела2;
						
					ИначеЕсли СтрокаВычета.ВидВычета = "ОбщаяСуммаВычетаИП"
						Или СтрокаВычета.ВидВычета = "ОбщаяСуммаПрофессиональныхВычетов"
						Или СтрокаВычета.ВидВычета = "ОбщаяСуммаСоциальныхВычетовПредоставленныхНалоговымиАгентами"
						Или СтрокаВычета.ВидВычета = "ОбщаяСуммаСтандартныхСоциальныхВычетов"
						Или СтрокаВычета.ВидВычета = "ИнвестиционныйВычет"
						Или СтрокаВычета.ВидВычета = "ОбщаяСуммаИмущественногоВычета"
						Или СтрокаВычета.ВидВычета = "СуммаИмущественногоВычетаПредоставленногоНалоговымиАгентамиРасходы"
						Или СтрокаВычета.ВидВычета = "СуммаИмущественногоВычетаПредоставленногоНалоговымиАгентамиПроценты"
						Или СтрокаВычета.ВидВычета = "СуммаРасходовНаПриобретениеНедвижимостиКВычету"
						Или СтрокаВычета.ВидВычета = "СуммаПроцентовПоКредитуНаНедвижимостьКВычету"
						Или СтрокаВычета.ВидВычета = "ЦенныеБумагиСуммаПрибылиКВычету"
						Или СтрокаВычета.ВидВычета = "ЦенныеБумагиСуммаПрибылиПоОперациямНаИИСкВычету" Тогда
						
						ДанныеСтраницы.П000200001040 = ДанныеСтраницы.П000200001040 + СтрокаВычета.СуммаВычетаДляРаздела2;
						
					ИначеЕсли СтрокаВычета.ВидВычета = "ЦенныеБумагиСуммаРасходовУменьшающаяДоходы"
						Или СтрокаВычета.ВидВычета = "ЦенныеБумагиСуммаУбытка" Тогда
						
						ДанныеСтраницы.П000200001050 = ДанныеСтраницы.П000200001050 + СтрокаВычета.СуммаВычетаДляРаздела2;
						
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		// Итоги и результаты рассчитываем независимо от наличия доходов и вычетов, т.к. в любом случае могли быть налоговые платежи по патентам и т.п. 
		РасчитатьИтогиСтраницыРаздела2(ДанныеСтраницы);
	
	КонецЕсли;
	
	// Если страница активна, заполняем ячейки табличного документа
	ТабличныйДокумент = Форма.ТабличныйДокумент;
	Если ОбновлятьТабличныйДокумент И ТабличныйДокумент.Области.Найти("П000200001001") <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ДанныеСтраницы Цикл
			ТабличныйДокумент.Области[КлючИЗначение.Ключ].Значение = КлючИЗначение.Значение;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РасчитатьИтогиСтраницыРаздела2(ДанныеСтраницы)
	
	// Общая сумма дохода, подлежащая налогообложению
	ДанныеСтраницы.П000200001030 = Макс(0, ДанныеСтраницы.П000200001010 - ДанныеСтраницы.П000200001020);
	
	// Проверяем: Общая сумма налоговых вычетов (строка 040) и сумма расходов, принимаемая в уменьшение полученных доходов (строка 050), 
	// в совокупности не может превышать значение показателя строки 030
	СуммаРазницы = ДанныеСтраницы.П000200001040 + ДанныеСтраницы.П000200001050 - ДанныеСтраницы.П000200001030;
	Если СуммаРазницы > 0 Тогда
		
		// Сначала уменьшаем сумму вычета по листам З и И - убытки могут быть перенесены на следующие годы
		ДанныеСтраницы.П000200001050 = Макс(0, ДанныеСтраницы.П000200001050 - СуммаРазницы);
		СуммаРазницы = ДанныеСтраницы.П000200001040 + ДанныеСтраницы.П000200001050 - ДанныеСтраницы.П000200001030;
		
		Если СуммаРазницы > 0 Тогда
			// Если вычетов все равно больше, уменьшаем сумму вычетов по строке 040
			ДанныеСтраницы.П000200001040 = Макс(0, ДанныеСтраницы.П000200001040 - СуммаРазницы);
		КонецЕсли;
		
	КонецЕсли;
	
	// Налоговая база для исчисления налога
	ДанныеСтраницы.П000200001060 = Макс(0,
		ДанныеСтраницы.П000200001030
		+ ДанныеСтраницы.П000200001051 
		- ДанныеСтраницы.П000200001040
		- ДанныеСтраницы.П000200001050);
	
	// Общая сумма налога, исчисленная к уплате
	ДанныеСтраницы.П000200001070 = Окр(ДанныеСтраницы.П000200001060 * ДанныеСтраницы.П000200001001 / 100, 0); // Начислено налога
	
	// Сумма налога, исчисленная к уплате (доплате) в бюджет
	СуммаНалогаИсчисленная = Макс(0, ДанныеСтраницы.П000200001070
		- ДанныеСтраницы.П000200001080
		- ДанныеСтраницы.П000200001090
		- ДанныеСтраницы.П000200001100
		- ДанныеСтраницы.П000200001110
		- ДанныеСтраницы.П000200001120
		- ДанныеСтраницы.П000200001130);
	
	// Сумма налога, подлежащая уплате (доплате) в бюджет
	ДанныеСтраницы.П000200001150 = Макс(0, СуммаНалогаИсчисленная - ДанныеСтраницы.П000200001140);
	
	// Сумма налога, подлежащая возврату из бюджета
	МаксимумСтроки160 = ДанныеСтраницы.П000200001080 + ДанныеСтраницы.П000200001090 + ДанныеСтраницы.П000200001110;
	
	РасчетнаяСуммаСтроки160 = ДанныеСтраницы.П000200001080
		+ ДанныеСтраницы.П000200001090
		+ ДанныеСтраницы.П000200001100
		+ ДанныеСтраницы.П000200001110
		+ ДанныеСтраницы.П000200001120
		+ ДанныеСтраницы.П000200001130
		- ДанныеСтраницы.П000200001070;
	
	ДанныеСтраницы.П000200001160 = Макс(0, Мин(МаксимумСтроки160, РасчетнаяСуммаСтроки160));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьЗначенияСтраницы(Форма, СтраницаРаздела, ИмяСтраницы, ОчиститьВсе = Ложь)

	Если ОчиститьВсе Тогда
		
		// обнуляем значения всех показателей страницы
		Для каждого КлючИЗначение Из СтраницаРаздела Цикл
			ОчиститьПоказательСтраницы(СтраницаРаздела[КлючИЗначение.Ключ]);
		КонецЦикла;
		
	Иначе
		
		// Обнуляем только показатели, рассчтываемые автоматически
		#Если НаКлиенте Тогда
			ТаблицаВариантыЗаполнения = РегламентированнаяОтчетностьКлиент.ПолучитьТаблицуВариантовЗаполненияНаКлиенте(Форма, ИмяСтраницы);
		#Иначе
			ТаблицаВариантыЗаполнения = РегламентированнаяОтчетностьВызовСервера.ПолучитьТаблицуВариантовЗаполненияНаСервере(Форма, ИмяСтраницы);
		#КонецЕсли
		Для Каждого СтрокаПоказателя Из ТаблицаВариантыЗаполнения Цикл
			
			Если СтрокаПоказателя.ВариантЗаполнения = 4 И СтраницаРаздела.Свойство(СтрокаПоказателя.КодПоказателя) Тогда
				ОчиститьПоказательСтраницы(СтраницаРаздела[СтрокаПоказателя.КодПоказателя]);
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьПоказательСтраницы(ЗначениеПоказателя)

	Если ТипЗнч(ЗначениеПоказателя) = Тип("Число") Тогда
		ЗначениеПоказателя = 0;
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
		ЗначениеПоказателя = '00010101';
	ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Строка") Тогда
		ЗначениеПоказателя = "";
	Иначе
		ЗначениеПоказателя = Неопределено;
	КонецЕсли; 

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидыДоходов(Знач ВыбраннаяФорма)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.ВидыДоходов(ВыбраннаяФорма);
	
КонецФункции

&НаСервереБезКонтекста
Функция ВидДохода(Знач КлючВидаДохода, Знач ВыбраннаяФорма)
	
	Перем ВидДохода;
	
	ВидыДоходов = Отчеты.РегламентированныйОтчет3НДФЛ.ВидыДоходов(ВыбраннаяФорма);
	Если ВидыДоходов.Свойство(КлючВидаДохода, ВидДохода) Тогда
		Возврат ВидДохода;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция КодыВидовДоходовРФ(Знач ВыбраннаяФорма)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.КодыВидовДоходовРФ(ВыбраннаяФорма);
	
КонецФункции

&НаСервереБезКонтекста
Функция КодВидаДоходаЗаПределамиРФ(Знач КлючВидаДохода, Знач ВыбраннаяФорма)
	
	Перем КодВидаДохода;
	
	КодыВидовДоходовЗаПределамиРФ = Отчеты.РегламентированныйОтчет3НДФЛ.КодыВидовДоходовЗаПределамиРФ(ВыбраннаяФорма);
	Если КодыВидовДоходовЗаПределамиРФ.Свойство(КлючВидаДохода, КодВидаДохода) Тогда
		Возврат КодВидаДохода;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция КБК(Знач ВыбраннаяФорма)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.КБК(ВыбраннаяФорма);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроцентПрофессиональногоВычета(Знач ВыбраннаяФорма)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.ПроцентПрофессиональногоВычета(ВыбраннаяФорма);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПределВычета(Знач КлючПоказателя, Знач ВыбраннаяФорма)
	
	Перем ЗначениеПоказателя;
	
	ПределыВычетов = Отчеты.РегламентированныйОтчет3НДФЛ.ПределыВычетов(ВыбраннаяФорма);
	Если ПределыВычетов.Свойство(КлючПоказателя, ЗначениеПоказателя) Тогда
		Возврат ЗначениеПоказателя;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Перезаполняет страницы Раздела 2
//	Для каждой ставки в сводной таблице доходов создаем отдельную страницу раздела 2, в которой вычисляем 
//	результат (к доплате или к возмещению).
//	Полученный результат распределяем в колонке "РезультатРасчетов" таблицы "СводДоходов" по строкам с рассчитанной ставкой,
//	т.к. Раздел 2 должен заполняться уже не по ставкам, а по сочетанию КБК и ОКТМО
&НаКлиентеНаСервереБезКонтекста
Процедура ПодготовитьДанныеРаздела2(Форма)
	
	ТаблицаСтраницыРаздел2 = Форма.ТаблицаСтраницыРаздел2;
	
	ОбразецСтруктуры = Новый Структура(Новый ФиксированнаяСтруктура(ТаблицаСтраницыРаздел2[0].Данные[0].Значение));
	Для Каждого ЭлементСтруктуры Из ОбразецСтруктуры Цикл
		Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Число") Тогда
			ОбразецСтруктуры[ЭлементСтруктуры.Ключ] = 0;
		Иначе
			ОбразецСтруктуры[ЭлементСтруктуры.Ключ] = "";
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСтраницыРаздел2.Очистить();
	
	ВидыДоходов = ВидыДоходов(Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	СтавкиНалога = СтавкиНалога(Форма.АдресСвойствПоказателей, Форма.АдресКлючейПоказателей);
	
	Для Каждого ВидДохода Из ВидыДоходов Цикл
		
		Для Каждого Ставка Из СтавкиНалога Цикл
			
			Если ПустаяСтрока(Ставка) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокиДохода = Форма.СводДоходов.НайтиСтроки(Новый Структура("Ставка, ВидДохода", Число(Ставка), ВидДохода.Значение));
			
			Если СтрокиДохода.Количество() > 0 Тогда
				
				СтраницаРаздела = ТаблицаСтраницыРаздел2.Добавить();
				СтраницаРаздела.Представление               = Ставка + " %";
				СтраницаРаздела.АвтоматическоеПредставление = Истина;
				СтраницаРаздела.Данные.Добавить(Новый Структура(Новый ФиксированнаяСтруктура(ОбразецСтруктуры)));
				
				ЗаполнитьРасчетныеДанныеРаздела2(Форма, СтраницаРаздела, Число(Ставка), ВидДохода.Значение);
				
			КонецЕсли;
			
		КонецЦикла; 
			
	КонецЦикла;
	
	Если ТаблицаСтраницыРаздел2.Количество() = 0 Тогда
		// Добавляем пустую страницу со значениями по умолчанию.
		
		СтраницаРаздела = ТаблицаСтраницыРаздел2.Добавить();
		СтраницаРаздела.Представление               = "Новая страница";
		СтраницаРаздела.АктивнаяСтраница            = Истина;
		СтраницаРаздела.АвтоматическоеПредставление = Истина;
		
		Ставка13 = "13";
		Если СтавкиНалога(Форма.АдресСвойствПоказателей, Форма.АдресКлючейПоказателей).Найти(Ставка13) <> Неопределено Тогда
			СтраницаРаздела.Представление               = Ставка13 + " %";
			СтраницаРаздела.АвтоматическоеПредставление = Истина;
			СтраницаРаздела.Данные.Добавить(Новый Структура(Новый ФиксированнаяСтруктура(ОбразецСтруктуры)));
			ЗаполнитьРасчетныеДанныеРаздела2(Форма, СтраницаРаздела, Число(Ставка13), ВидыДоходов.Иное);
		Иначе
			СтраницаРаздела.Данные.Добавить(Новый Структура(Новый ФиксированнаяСтруктура(ОбразецСтруктуры)));
		КонецЕсли;
		
	Иначе
		
		ТаблицаСтраницыРаздел2[0].АктивнаяСтраница  = Истина;
		
	КонецЕсли; 
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтавкиНалога(Знач АдресСвойствПоказателей, Знач АдресКлючейПоказателей)
	
	Возврат МассивЗначенийПоказателя("НалоговаяСтавка", АдресСвойствПоказателей, АдресКлючейПоказателей);
	
КонецФункции

&НаСервереБезКонтекста
Функция МассивЗначенийПоказателя(ИмяПоказателя, АдресСвойствПоказателей, АдресКлючейПоказателей)
	
	МассивЗначений = Новый Массив;
	
	ИмяПоля = ИмяПоля(ИмяПоказателя, АдресКлючейПоказателей);
	Если Не ЗначениеЗаполнено(ИмяПоля) Тогда
		ВызватьИсключение СтрШаблон("Не найдено поле показателя ""%1""", ИмяПоказателя);
	КонецЕсли;
	
	ПараметрВводаПоля = СвойствоПоказателя(ИмяПоля, АдресСвойствПоказателей);
	
	Если ПараметрВводаПоля <> Неопределено
		И ЭтоАдресВременногоХранилища(ПараметрВводаПоля.АдресТаблицыЗначений) Тогда
		
		ТаблицаЗначений = ПолучитьИзВременногоХранилища(ПараметрВводаПоля.АдресТаблицыЗначений);
		
	КонецЕсли;
	
	Если ТаблицаЗначений <> Неопределено И ТипЗнч(ТаблицаЗначений) = Тип("ТаблицаЗначений") Тогда
		МассивЗначений = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗначений, "Код", Истина);
	КонецЕсли;
	
	Возврат МассивЗначений;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяПоля(Знач КлючПоказателя, Знач АдресКлючейПоказателей)
	
	Если Не ЭтоАдресВременногоХранилища(АдресКлючейПоказателей) Тогда
		Возврат "";
	КонецЕсли;
	
	КлючиПоказателейДекларации = ПолучитьИзВременногоХранилища(АдресКлючейПоказателей);
	Если ТипЗнч(КлючиПоказателейДекларации) <> Тип("ТаблицаЗначений") Тогда
		Возврат "";
	КонецЕсли;
	
	КлючПоказателя = КлючиПоказателейДекларации.Найти(КлючПоказателя, "Ключ");
	Если КлючПоказателя = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Не найден ключ показателя ""%1""", КлючПоказателя);
	КонецЕсли;
	
	Возврат КлючПоказателя.ИмяПоля;
	
КонецФункции

&НаКлиенте
Процедура СобратьДанныеВРаздел1()
	
	СобратьДанныеВРаздел1Сервер();
	
	// Обновляем дерево разделов
	СформироватьДеревоРазделовОтчета(ЭтотОбъект);
	
	// Выводим макет в табличный документ
	ВывестиМакетВТаблДокНаСервере("Раздел1", 1, , , Истина);
	
	// Активизируем строку раздела в дереве
	Для Каждого СтрокаРаздела Из РазделыОтчета.ПолучитьЭлементы() Цикл
		Если СтрокаРаздела.КолонкаРазделыОтчетаСокрНаим = "Раздел1" Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = СтрокаРаздела.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СобратьДанныеВРаздел1Сервер()
	
	// Итоги пересчитываем только в том случае, если не заполнена таблица свода доходов
	Если СводДоходов.Количество() = 0 Тогда
		РассчитатьИтогиЛиста(ЭтотОбъект, Неопределено);
		ЗаполнитьСводныеТаблицы(ЭтотОбъект);
	КонецЕсли;
	
	ОКТМОНАлогоплательщика = СтруктураРеквизитовФормы.Октмо;
	
	// Сначала распределим результаты расчета Раздела 2 по своду доходов
	Для Каждого СтраницаРаздела Из ТаблицаСтраницыРаздел2 Цикл
		
		Если СтраницаРаздела.Данные.Количество() = 0
			ИЛИ СтраницаРаздела.Данные[0].Значение.П000200001001 = 0
			ИЛИ СтраницаРаздела.Данные[0].Значение.П000200001010 
				+ СтраницаРаздела.Данные[0].Значение.П000200001051 = 0
			Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеСтраницы = СтраницаРаздела.Данные[0].Значение;
		
		ОстатокКУплате     = Число(ДанныеСтраницы.П000200001150);
		ОстатокКВозмещению = Число(ДанныеСтраницы.П000200001160);
		ПоследняяСтрока    = Неопределено;
		
		// Находим строки доходов по указанной ставке
		СтрокиДоходов = СводДоходов.НайтиСтроки(Новый Структура("Ставка", СтраницаРаздела.Данные[0].Значение.П000200001001));
		Для Каждого СтрокаДохода Из СтрокиДоходов Цикл
		
			Если ОстатокКУплате > 0 Тогда
				СтрокаДохода.ОКТМО             = ОКТМОНАлогоплательщика; // доплата налога всегда по месту регистрации налогоплательщика
				СтрокаДохода.РезультатРасчетов = Мин(ОстатокКУплате, СтрокаДохода.НачисленоНалога - СтрокаДохода.УдержаноНалога);
				ОстатокКУплате                 = Макс(0, ОстатокКУплате - СтрокаДохода.РезультатРасчетов);
			КонецЕсли;
			
			Если ОстатокКВозмещению > 0 Тогда
				НалогУплачен = СтрокаДохода.УплаченоНалога + СтрокаДохода.УдержаноНалога + СтрокаДохода.НалогПринимаемыйКУчету; // возмещается только то, что уплачено или удержано
				СтрокаДохода.РезультатРасчетов = - Мин(ОстатокКВозмещению, НалогУплачен); // пишем со знаком минус для получения итоговой суммы при сворачивании по КБК и ОКТМО
				ОстатокКВозмещению             = Макс(0, ОстатокКВозмещению + СтрокаДохода.РезультатРасчетов);
			КонецЕсли;
			
			Если СтрокаДохода.РезультатРасчетов <> 0 Тогда
				ПоследняяСтрока = СтрокаДохода;
			КонецЕсли;
			
		КонецЦикла;
		
		// Убеждаемся, что распределен весь результат расчетов
		Если ПоследняяСтрока <> Неопределено Тогда
			Если ОстатокКУплате > 0 Тогда
				ПоследняяСтрока.РезультатРасчетов = ПоследняяСтрока.РезультатРасчетов + ОстатокКУплате;
			КонецЕсли;
			Если ОстатокКВозмещению > 0 Тогда
				ПоследняяСтрока.РезультатРасчетов = ПоследняяСтрока.РезультатРасчетов - ОстатокКВозмещению;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Свернем таблицу доходов по КБК и ОКТМО, чтобы получить итоги расчетов
	ТаблицаРасчетов = СводДоходов.Выгрузить();
	ТаблицаРасчетов.Свернуть("КБК,ОКТМО", "РезультатРасчетов");
	
	// Удаляем пустые строки, если есть строки с налогом к уплате или к возврату
	ПустыеСтроки = ТаблицаРасчетов.НайтиСтроки(Новый Структура("РезультатРасчетов", 0));
	Если ПустыеСтроки.Количество() <> 0 И ПустыеСтроки.Количество() < ТаблицаРасчетов.Количество() Тогда
		Для Каждого СтрокаТаблицыРасчетов Из ПустыеСтроки Цикл
			ТаблицаРасчетов.Удалить(СтрокаТаблицыРасчетов);
		КонецЦикла;
	КонецЕсли;
	
	ПризнакиУплатыВозврата = Отчеты.РегламентированныйОтчет3НДФЛ.ПризнакиУплатыВозврата(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	// Каждую пару КБК+ОКТМО выводим на отдельной строке страницы раздела 1
	ТаблицаСтраницыРаздел1.Очистить();
	МаксимальноеКоличествоСтрок = МаксимальноеКоличествоСтрокНаСтранице(ЭтотОбъект, "Раздел1");
	НомерСтроки = 1;
	Для Каждого СтрокаРасчета Из ТаблицаРасчетов Цикл
		
		Если ТаблицаСтраницыРаздел1.Количество() = 0 Или НомерСтроки > МаксимальноеКоличествоСтрок Тогда
			СтраницаРасчета = ТаблицаСтраницыРаздел1.Добавить();
			СтраницаРасчета.Представление               = "Страница N " + ТаблицаСтраницыРаздел1.Количество();
			СтраницаРасчета.АвтоматическоеПредставление = Истина;
			СтраницаРасчета.Данные.Добавить(Новый Структура);
			
			ИмяГруппыДопСтрок = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере("Раздел1");
			
			ТаблицаДопСтрокРаздела = ЭтотОбъект["Хранилище" + ИмяГруппыДопСтрок].Добавить().Таблица;
			
			СтраницаРасчета.ДанныеДопСтрок.Добавить(ТаблицаДопСтрокРаздела, "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела");
			
			НомерСтроки = 1;
		КонецЕсли;
		
		ДанныеСтраницыРасчета = СтраницаРасчета.ДанныеДопСтрок[0].Значение.Добавить();
		
		ДанныеСтраницыРасчета.П000100001020 = СтрокаРасчета.КБК;
		
		Если ЗначениеЗаполнено(СтрокаРасчета.ОКТМО) Тогда
			ДанныеСтраницыРасчета.П000100001030 = СтрокаРасчета.ОКТМО;
		Иначе
			ДанныеСтраницыРасчета.П000100001030 = ОКТМОНАлогоплательщика;
		КонецЕсли;
		
		Если СтрокаРасчета.РезультатРасчетов > 0 Тогда
			
			ДанныеСтраницыРасчета.П000100001010 = ПризнакиУплатыВозврата.Уплата;
			ДанныеСтраницыРасчета.П000100001040 = СтрокаРасчета.РезультатРасчетов;
			ДанныеСтраницыРасчета.П000100001050 = 0;
			
		ИначеЕсли СтрокаРасчета.РезультатРасчетов < 0 Тогда
			
			ДанныеСтраницыРасчета.П000100001010 = ПризнакиУплатыВозврата.Возврат;
			ДанныеСтраницыРасчета.П000100001040 = 0;
			ДанныеСтраницыРасчета.П000100001050 = -СтрокаРасчета.РезультатРасчетов;
			
		Иначе
			
			ДанныеСтраницыРасчета.П000100001010 = ПризнакиУплатыВозврата.ОтсутствиеУплатыВозврата;
			ДанныеСтраницыРасчета.П000100001040 = 0;
			ДанныеСтраницыРасчета.П000100001050 = 0;
			
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	// Если не оказалось новых страниц раздела, добавим пустую
	Если ТаблицаСтраницыРаздел1.Количество() = 0 Тогда
		СтраницаРаздела = ТаблицаСтраницыРаздел1.Добавить();
		СтраницаРаздела.Представление               = "Новая страница";
		СтраницаРаздела.АктивнаяСтраница            = Истина;
		СтраницаРаздела.АвтоматическоеПредставление = Истина;
		
		СтраницаРаздела.Данные.Добавить(Новый Структура);
		
		ИмяГруппыДопСтрок = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере("Раздел1");
		ТаблицаДопСтрокРаздела = ЭтотОбъект["Хранилище" + ИмяГруппыДопСтрок].Добавить().Таблица;
		
		СтраницаРаздела.ДанныеДопСтрок.Добавить(ТаблицаДопСтрокРаздела, "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела");
		ДанныеСтраницыРасчета = СтраницаРаздела.ДанныеДопСтрок[0].Значение.Добавить();
		
		ДанныеСтраницыРасчета.П000100001030 = ОКТМОНАлогоплательщика;
		ДанныеСтраницыРасчета.П000100001010 = ПризнакиУплатыВозврата.ОтсутствиеУплатыВозврата;
		ДанныеСтраницыРасчета.П000100001020 = КБК(СтруктураРеквизитовФормы.мВыбраннаяФорма).ДоходыОтПредпринимательскойДеятельности;
	Иначе
		
		ТаблицаСтраницыРаздел1[0].АктивнаяСтраница  = Истина;
		
	КонецЕсли;
	
	// Обновим значения показателей в структуре "Данные" каждой страницы раздела 1
	
	ИмяСтруктурыГруппы = Неопределено;
	мСтруктураМногострочныхРазделов.Свойство(ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере("Раздел1"), ИмяСтруктурыГруппы);
	
	СтруктураГруппы = ЭтотОбъект[ИмяСтруктурыГруппы];
	СписокКолонок = СтруктураГруппы.Выгрузить().Колонки;
	Для Каждого СтраницаРаздела1 Из ТаблицаСтраницыРаздел1 Цикл
		Для Индекс = 0 По СтраницаРаздела1.ДанныеДопСтрок[0].Значение.Количество() - 1 Цикл
			СтрокаТаблицы = СтраницаРаздела1.ДанныеДопСтрок[0].Значение[Индекс];
			Для Каждого Колонка Из СписокКолонок Цикл
				СтраницаРаздела1.Данные[0].Значение.Вставить(СтрШаблон("%1_%2", Колонка.Имя, Индекс + 1), СтрокаТаблицы[Колонка.Имя]);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаКС

&НаСервере
Функция ПроверитьКонтрольныеСоотношенияВОтчете() Экспорт
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.НовыйРезультатПроверкиКонтрольныхСоотношений();
	
КонецФункции

#КонецОбласти

#Область ЧтениеСохранениеВыводОтчета

// Восстанавливает сохраненные данные отчета.
//
&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеМногостраничныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем Автозаполнение;
	Перем ТаблицаМногостраничногоДокумента;
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		СтруктураРеквизитовФормы.Организация =	СтруктураРеквизитовФормы.мСохраненныйДок.Организация;
	КонецЕсли;
	
	ДатаПодписи = СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи;
	Комментарий = СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий;
	
	// восстанавливаем сохраненные данные отчета
	СписокСохранения = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	
	// список листов, выводимых на печать
	ДеревоНастройкиСтраниц = СтруктураРеквизитовФормы.мСохраненныйДок.ДеревоНастройкиСтраниц.Получить();
	
	ЗначениеВРеквизитФормы(ДеревоНастройкиСтраниц.Скопировать(), "мДеревоСтраницОтчета");
	
	СписокСохранения.Свойство("ПоказателиОтчета", ПоказателиОтчета);
	
	// восстанавливаем сохраненные данные многострочных разделов
	Если СписокСохранения.Свойство( "ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов) Тогда
		
		Для Каждого ЭлементСтруктуры Из ДанныеМногострочныхРазделов Цикл
			
			ИдентификаторМногострочнойГруппы = "";
			Если Не мСтруктураМногострочныхРазделов.Свойство(ЭлементСтруктуры.Ключ, ИдентификаторМногострочнойГруппы) Тогда
				Продолжить;
			КонецЕсли;
			
			МногострочнаяГруппа = ЭтотОбъект[ИдентификаторМногострочнойГруппы];
			МногострочнаяГруппа.Очистить();
			Для каждого СтрокаДанных Из ЭлементСтруктуры.Значение Цикл
				ЗаполнитьЗначенияСвойств(МногострочнаяГруппа.Добавить(), СтрокаДанных);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// восстанавливаем сохраненные данные вариантов автозаполнения ячеек
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) И ДанныеВариановАвтоЗаполнения <> Неопределено Тогда
		
		мСтруктураВариантыЗаполнения.Очистить();
		
		Для Каждого ЭлементСтруктуры Из ДанныеВариановАвтоЗаполнения Цикл
			
			мСтруктураВариантыЗаполнения.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Ключ);
			
			ЭтотОбъект["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Очистить();
			
			Для Каждого ЭлементМассива Из ЭлементСтруктуры.Значение Цикл
				
				ТаблВарЗаполнения = ЭтотОбъект["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Добавить().ТаблицаВариантовЗаполнения;
				
				ЗначениеВДанныеФормы(ЭлементМассива, ТаблВарЗаполнения);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// восстанавливаем сохраненные данные многостраничных разделов
	Если СписокСохранения.Свойство("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов) Тогда
		
		Для Каждого ЭлементСтруктуры Из ДанныеМногостраничныхРазделов Цикл
			
			ИдентификаторМногостраничногоРаздела = "";
			Если Не мСтруктураМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, ИдентификаторМногостраничногоРаздела) Тогда
				Продолжить;
			КонецЕсли;
			
			МногостраничныйРаздел = ЭтотОбъект[ИдентификаторМногостраничногоРаздела];
			
			Если ЭлементСтруктуры.Значение.Колонки.Найти("ДанныеДопСтрок") = Неопределено Тогда
				ЭлементСтруктуры.Значение.Колонки.Добавить("ДанныеДопСтрок");
			КонецЕсли;
			
			МногостраничныйРаздел.Очистить();
			Для Каждого СтрокаДанных Из ЭлементСтруктуры.Значение Цикл
				ЗаполнитьЗначенияСвойств(МногостраничныйРаздел.Добавить(), СтрокаДанных);
			КонецЦикла;
			
			Если МногостраничныйРаздел.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если МногостраничныйРаздел[0].Свойство("Данные") Тогда
				
				Для Каждого СтрокаТаблЗнач Из МногостраничныйРаздел Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.Данные) = Тип("СписокЗначений") Тогда
						
						ПреобразоватьПосимвольныеДанныеВЗначения(ЭлементСтруктуры.Значение[МногостраничныйРаздел.Индекс(СтрокаТаблЗнач)].Данные);
						СтрокаТаблЗнач.Данные.Очистить();
						СтрокаТаблЗнач.Данные.Добавить(ЭлементСтруктуры.Значение[МногостраничныйРаздел.Индекс(СтрокаТаблЗнач)].Данные);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если МногостраничныйРаздел[0].Свойство("ДанныеДопСтрок") Тогда
				
				Ном = 0;
				
				Для Каждого СтрокаТаблЗнач Из ЭлементСтруктуры.Значение Цикл
					
					Если ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("Структура") Тогда
						
						Для Каждого ЭлСтруктуры Из СтрокаТаблЗнач.ДанныеДопСтрок Цикл
							
							ТаблЗнач = ЭлСтруктуры.Значение;
							
							ЭлСтруктуры.Значение = ЭтотОбъект["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
							
							ЗначениеВДанныеФормы(ТаблЗнач, ЭлСтруктуры.Значение);
							
							МногостраничныйРаздел[Ном].ДанныеДопСтрок.Добавить(ЭлСтруктуры.Значение);
							
						КонецЦикла;
						
					ИначеЕсли ТипЗнч(СтрокаТаблЗнач.ДанныеДопСтрок) = Тип("ТаблицаЗначений") Тогда
						
						СтрокаТаблЗначДанныеДопСтрок = ЭтотОбъект["Хранилище" + ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ЭлементСтруктуры.Ключ)].Добавить().Таблица;
						
						ЗначениеВДанныеФормы(СтрокаТаблЗнач.ДанныеДопСтрок, СтрокаТаблЗначДанныеДопСтрок);
						
						МногостраничныйРаздел[Ном].ДанныеДопСтрок.Добавить(СтрокаТаблЗначДанныеДопСтрок, "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела");
						
					КонецЕсли;
					
					Ном = Ном + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		Для Каждого Страница Из ЭтотОбъект[Раздел.Значение] Цикл
			Страница.АктивнаяСтраница = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	СписокСохранения.Свойство("ФлПодсчетСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	
	Если НЕ СписокСохранения.Свойство("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов) Тогда
		СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	КонецЕсли;
		
	Если НЕ СписокСохранения.Свойство("мПроверятьСоотношенияПриПечатиИВыгрузки", СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки) Тогда
		СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки = Ложь;
	КонецЕсли;
	
	СтатусНалогоплательщика = Неопределено;
	Если СписокСохранения.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаТитульный")
		И СписокСохранения.ПоказателиОтчета.ПолеТабличногоДокументаТитульный.Свойство("СтатусНалогоплательщика", СтатусНалогоплательщика) Тогда
		ОбновитьПеременныеМодуляНаСервере("СтатусНалогоплательщика", СтатусНалогоплательщика);
	КонецЕсли;

	СписокСохранения.Свойство("Автозаполнение", Автозаполнение);
	СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Автозаполнение;
	
	Если НЕ СписокСохранения.Свойство("ПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена) Тогда
		СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена = Ложь;
	КонецЕсли;
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	Префикс = "ПолеТабличногоДокумента";
	
	Для Каждого Раздел Из мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл	
		
		Если НЕ Раздел.МногостраничностьВРазделе Тогда
			ПреобразоватьПосимвольныеДанныеВЗначения(ПоказателиОтчета[Префикс + Раздел.ИмяСтраницы]);
			ЭтотОбъект["СтруктураДанных" + Раздел.ИмяСтраницы] = ПоказателиОтчета[Префикс + Раздел.ИмяСтраницы];
		КонецЕсли;
		
	КонецЦикла;
	
	ИмяТекРаздела = "Титульный";
	
	Для Каждого ЭлСтруктуры Из ЭтотОбъект["СтруктураДанных" + ИмяТекРаздела] Цикл
		Если ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ) <> Неопределено Тогда
			ТабличныйДокумент.Области[ЭлСтруктуры.Ключ].Значение = ЭлСтруктуры.Значение;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураРеквизитовФормы.мПоМесту = ТабличныйДокумент.Области.ПоМесту.Значение;
	
	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтотОбъект);
	
	КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПередСохранением(КодИФНС, КПП, ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаимТекРаздела, УдалитьДанныеПомощника)
	
	Если РегламентированнаяОтчетность.БылиИзмененыКлючевыеРеквизитыОтчета(ЭтотОбъект, КодИФНС, КПП)
		И РегламентированнаяОтчетность.СуществуетДокументСАналогичнымиРеквизитами(ЭтотОбъект, КодИФНС) Тогда
		
		ПодобныйОтчетСуществует = Истина;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мЗаписываетсяНовыйДокумент = (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	Если Вариант <> Неопределено И ((СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) ИЛИ (СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено) ИЛИ (Вариант <> СтруктураРеквизитовФормы.мВариант)) Тогда
		
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			
		ИначеЕсли Вариант <> СтруктураРеквизитовФормы.мВариант Тогда
			
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПодобныйОтчетСуществует
		ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Сохранить(Автосохранение, КодИФНС, КПП, Вариант, Ложь, НаимТекРаздела, УдалитьДанныеПомощника);
	
КонецФункции

// Формирует структуру данных отчета и помещает в реквизит типа ХранилищеЗначения
// документа РегламентированныйОтчет.
// 
// При первом сохранении данных отчета создает новый документ этого вида.
// Структура данных отчета состоит из элементов:
//   - значения редактируемых реквизитов отчета;
//   - данные по многострочным разделам отчета;
//   - данные по  многостраничным разделам отчета;
//   - данные по вариантам заполнения реквизитов,
//     участвующих в автозаполнении.
//
&НаСервере
Функция Сохранить(Автосохранение = Ложь, КодИФНС, КПП, Вариант, ВидДокументаНомерКорректировкиИзменен = Ложь, НаимТекРаздела, УдалитьДанныеПомощника = Ложь) Экспорт
	
	Если ВидДокументаНомерКорректировкиИзменен Тогда
		РазблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, УникальныйИдентификатор);
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.НужноРассчитатьОтчетНаСервере Тогда
		РасчетНаСервере();
	КонецЕсли;
	
	СтруктураРеквизитовФормы.Вставить("мВариант", Вариант);
	СтруктураРеквизитовФормы.Вставить("ДатаПодписи", ДатаПодписи);
	СтруктураРеквизитовФормы.Вставить("Комментарий", Комментарий);
	СтруктураРеквизитовФормы.Вставить("УдалитьДанныеПомощника", УдалитьДанныеПомощника);
	
	СохранитьДанныеРаздела(НаимТекРаздела, Ложь, Ложь);
	
	ЗаполнитьПустыеСтруктурыДанных();
	
	СвойстваРазделовДекларации = РеквизитФормыВЗначение("мСвойстваРазделовДекларации", Тип("ДеревоЗначений"));
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("ПоказателиОтчета", Отчеты.РегламентированныйОтчет3НДФЛ.ПоказателиОтчета(
		СвойстваРазделовДекларации, ЭтотОбъект));
	ДанныеФормы.Вставить("ДанныеМногострочныхРазделов", Отчеты.РегламентированныйОтчет3НДФЛ.ДанныеМногострочныхРазделов(
		мСтруктураМногострочныхРазделов, ЭтотОбъект));
	ДанныеФормы.Вставить("ДанныеМногостраничныхРазделов", Отчеты.РегламентированныйОтчет3НДФЛ.ДанныеМногостраничныхРазделов(
		СвойстваРазделовДекларации, мСтруктураМногостраничныхРазделов, ЭтотОбъект));
	ДанныеФормы.Вставить("ДеревоНастройкиСтраниц", РеквизитФормыВЗначение("мДеревоСтраницОтчета"));
	ДанныеФормы.Вставить("ДанныеВариантовАвтоЗаполнения",
		Отчеты.РегламентированныйОтчет3НДФЛ.ДанныеВариантовАвтоЗаполнения(мСтруктураВариантыЗаполнения, ЭтотОбъект));
	
	ОтчетЗаписан = Отчеты.РегламентированныйОтчет3НДФЛ.ЗаписатьДанныеФормы(
		ДанныеФормы, СтруктураРеквизитовФормы, КодИФНС, Автосохранение, ВидДокументаНомерКорректировкиИзменен);
	
	Если ОтчетЗаписан Тогда
		РегламентированнаяОтчетность.СохранитьСтатусОтправкиРеглОтчета(ЭтотОбъект);
		Модифицированность = Ложь;
	КонецЕсли;
	
	Возврат ОтчетЗаписан;
	
КонецФункции

&НаСервере
Функция ПоказателиОтчета()
	
	ПоказателиОтчета = Новый Структура();
	
	Префикс = "ПолеТабличногоДокумента";
	
	Для Каждого Раздел Из мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл
		
		Если Не Раздел.МногостраничностьВРазделе Тогда
			ПоказателиОтчета.Вставить(Префикс + Раздел.ИмяСтраницы, ЭтотОбъект["СтруктураДанных" + Раздел.ИмяСтраницы]);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПоказателиОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьИЗакрытьПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделуНаКлиенте(Знач ИмяРаздела) Экспорт
	
	ПеременныеМодуля = Новый Структура;
	
	ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(ИмяРаздела, ПеременныеМодуля);
	
КонецПроцедуры

&НаСервере
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделуНаСервере(Знач ИмяРаздела) Экспорт
	
	ПеременныеМодуля = Новый Структура;
	
	ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(ИмяРаздела, ПеременныеМодуля);
	
КонецПроцедуры

// ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу()
// Процедура обнуляем переменные модуля, используемые для отображения в квадратиках
// в разделе 1
// запускается при добавлении новой страницы, а также при инициализации формы
//
&НаКлиентеНаСервереБезКонтекста
Процедура ОбнулитьПеременныеМодуляХранящиеЗначенияКвадратногоПредставленияПоРазделу(Знач ИмяРаздела, ПеременныеМодуля)
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеЗначенийВКвадратикахНаКлиенте(Знач ИмяСтраницы) Экспорт
	
	ЗаполнениеЗначенийВКвадратиках(ЭтотОбъект, ИмяСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеЗначенийВКвадратикахНаСервере(Знач ИмяСтраницы) Экспорт
	
	ЗаполнениеЗначенийВКвадратиках(ЭтотОбъект, ИмяСтраницы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнениеЗначенийВКвадратиках(Форма, Знач ИмяСтраницы)
	
	Возврат;
	
КонецПроцедуры

// Процедура очищает содержимое редактируемых и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
&НаСервере
Процедура ОчиститьТабличноеПолеСпец(ИмяТекущегоРаздела, ОбновитьСтавки = Истина) Экспорт
	
	//для очистки защищенных областей итоговых ячеек
	//получим список итоговых ячеек, снимая с них защиту), а после очищения вернем защиту
	СписокИтоговыхЯчеек = Новый СписокЗначений;
	
	Для каждого Область Из ТабличныйДокумент.Области Цикл
		Если Область.ЦветФона = СтруктураРеквизитовФормы.ЦветЗаполняетсяАвтоАрифметика И Область.Защита Тогда
			Область.Защита = Ложь;
			СписокИтоговыхЯчеек.Добавить(Область.Имя);
		КонецЕсли;
	КонецЦикла;
	
	РегламентированнаяОтчетностьКлиентСервер.ОчиститьТабличноеПоле(ЭтотОбъект, ИмяТекущегоРаздела);
	
	НомерКорректировки = 0;
	ДатаПодписи = ТекущаяДатаСеанса();
	
	СохранитьДанныеРаздела(ИмяТекущегоРаздела, Истина, Ложь);
	
	Для каждого КодИтоговогоПоказателя Из СписокИтоговыхЯчеек Цикл
		ОбластьПоказателя=ТабличныйДокумент.Области.Найти(КодИтоговогоПоказателя.Значение);
		Если ОбластьПоказателя <> Неопределено Тогда
			ОбластьПоказателя.Защита = Истина;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ОчиститьПоказателиОтчетаЗаполняемыеИзИБНаСервере(ИмяСтраницы)
	
	ОчиститьПоказателиОтчетаЗаполняемыеИзИБ(ЭтотОбъект, ИмяСтраницы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьПоказателиОтчетаЗаполняемыеИзИБ(Форма, НазванияМногостраничныхРазделовДляУдаления = "") Экспорт
	
	Если НЕ ПустаяСтрока(НазванияМногостраничныхРазделовДляУдаления) Тогда
		НазванияМнгостраничныхРазделов = Новый Структура(НазванияМногостраничныхРазделовДляУдаления);
	Иначе
		НазванияМнгостраничныхРазделов = Новый Структура;
	КонецЕсли;
	
	Для каждого СтрокаУровня1 из Форма.мСвойстваРазделовДекларации.ПолучитьЭлементы() Цикл
		
		НазваниеСтраницы           = СтрокаУровня1.ИмяСтраницы;
		МногостраничностьВРазделе  = СтрокаУровня1.МногостраничностьВРазделе;
		РазделАвтозаполняется = СтрокаУровня1.РазделЯвляетсяАвтозаполняемым;
		
		Если НазваниеСтраницы = "Титульный" Тогда
			Продолжить;
		КонецЕсли;
		
		мРазделИмя = "ТабличныйДокумент";
		мРаздел    = Форма[мРазделИмя];
		Если НазванияМнгостраничныхРазделов.Свойство(НазваниеСтраницы) Тогда
			// Очистим текущий лист.
			#Если НаКлиенте Тогда
				Форма.ОчиститьТабличноеПолеНаКлиенте(НазваниеСтраницы);
			#Иначе
				Форма.ОчиститьТабличноеПолеНаСервере(НазваниеСтраницы);
			#КонецЕсли
			
			ТаблицаСтраницРаздела = Неопределено;
			Форма.мСтруктураМногостраничныхРазделов.Свойство(НазваниеСтраницы, ТаблицаСтраницРаздела);
			
			ТаблицаСтраницРаздела = Форма[ТаблицаСтраницРаздела];
			
			ИндексСтраницы = 0;
			Пока Не (ИндексСтраницы > (ТаблицаСтраницРаздела.Количество()-1)) Цикл
				СтрокаТаблицы = ТаблицаСтраницРаздела[ИндексСтраницы];
				Если НЕ СтрокаТаблицы.АктивнаяСтраница Тогда
					// удаляем дополнительную страницу
					ТаблицаСтраницРаздела.Удалить(СтрокаТаблицы);
					Продолжить;
				КонецЕсли;
				ИндексСтраницы = ИндексСтраницы + 1;
			КонецЦикла;
			
			// Варианты заполнения установим в один массив. Все варианты возьмем из Эталона.
			// Цвет ячеек перерисуется в показе статика многостраничного раздела, ниже.
			РегламентированнаяОтчетностьКлиентСервер.ОчиститьВариантыЗаполнения(Форма, НазваниеСтраницы, Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Открывает форму документа ФормаНастройкиОтчета с целью выбора 
// выводимых на печать листов отчета.
//
// Параметры:
//   ВариантВыбора - строка, назначение выбора страниц.
//
&НаКлиенте
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)
	Перем ТекущийРазделОтчетаСокрНаим;
	Перем НомерСтраницыРазделаОтчета;
	
	// Запоминаем текущий раздел, установленный в дереве разделов отчета.
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРазделОтчетаСокрНаим = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		НомерСтраницыРазделаОтчета  = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	КонецЕсли;
	
	мПараметры = Новый Структура;
	мПараметры.Вставить("ПроверкаСоотношений", Ложь);
	мПараметры.Вставить("Автосохранение", Ложь);
	мПараметры.Вставить("ИмеетсяРеквизитДеревоВыбранныхСтраниц", Истина);
	мПараметры.Вставить("РазрешитьВстроеннуюПечать", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	ПараметрыФормы.Вставить("мПечатьБезШтрихкодаРазрешена", СтруктураРеквизитовФормы.мПечатьБезШтрихкодаРазрешена);
	ПараметрыФормы.Вставить("мСчетчикСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	ПараметрыФормы.Вставить("мАвтоВыборКодов", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	ПараметрыФормы.Вставить("мПроверятьСоотношенияПриПечатиИВыгрузки", СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки);
	ПараметрыФормы.Вставить("мПараметры", мПараметры);
	
	ФормаНастройкиОтчета = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("НастройкиОтчета", ПараметрыФормы, ЭтотОбъект);
	
	мДеревоВыбранныхСтраниц.ПолучитьЭлементы().Очистить();
	
	КопироватьДанныеФормы(мДеревоСтраницОтчета, ФормаНастройкиОтчета.Дерево);
	
	ЗначениеАвторасчета = СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет;
	ЗначениеЕдИзм       = СтруктураРеквизитовФормы.ЕдиницаИзмерения;
	ЗначениеЗпт         = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	
	ДополнительныеПараметры = Новый Структура("ЗначениеАвторасчета, ЗначениеЕдИзм, ЗначениеЗпт, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим", ЗначениеАвторасчета, ЗначениеЕдИзм, ЗначениеЗпт, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраницЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ФормаНастройкиОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаНастройкиОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаНастройкиОтчета.Открыть();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТаблицуВариантовЗаполненияНаСервере()
	
	РегламентированнаяОтчетностьКлиентСервер.ИзменитьТаблицуВариантовЗаполнения(ЭтотОбъект, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	
	РегламентированнаяОтчетность.ПолучитьСведенияОПоказателяхОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузкаОтчетаИзФайла(ДанныеДляЗагрузки) Экспорт
	
	Если Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(НСтр("ru='Отчет ""%1"" не сохранен.'"), ЭтотОбъект.Заголовок);
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки)
	
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = Отчеты.РегламентированныйОтчет3НДФЛ.ПараметрыВыгрузки(
		СтруктураРеквизитовФормы.мВыбраннаяФорма, мДанныеОтчета.Титульный);
	ДеревоДляЗагрузки = ИзвлечьСтруктуруXML();
	
	Если НЕ РегламентированнаяОтчетностьЗагрузка.ЗагрузкаОтчетаИзФайла(
		ЭтотОбъект, ПараметрыВыгрузки, ДанныеДляЗагрузки, ДеревоДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьВыгрузкуОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчета()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуОтчетаЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПоказатели() Экспорт
	
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = Отчеты.РегламентированныйОтчет3НДФЛ.ПараметрыВыгрузки(
		СтруктураРеквизитовФормы.мВыбраннаяФорма, мДанныеОтчета.Титульный);
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	Если НЕ ВыгрузкаВозможна(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция УниверсальнаяВыгрузка(ТекстВыгрузки = Неопределено, ПроверятьБлокировки = Истина) Экспорт
	
	Перем ПараметрыВыгрузки;
	
	Если Модифицированность Тогда

		ТекстЗаголовка = ЗаголовокРегламентированногоОтчета(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета, СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
		
		ОргСтр = " (" + СтруктураРеквизитовФормы.Организация + ")";
		Если СтрНайти(ТекстЗаголовка, ОргСтр) = 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + ОргСтр;
		КонецЕсли;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Отчет """ + ТекстЗаголовка + """ не сохранен.";
		Сообщение.Сообщить();
		
		Возврат Ложь;
	КонецЕсли;
	
	Если Открыта() Тогда
		Если СтруктураРеквизитовФормы.ЕдиницаИзмерения <> ПредопределенноеЗначение("Перечисление.ПорядкиОкругленияОтчетности.Окр1") Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = НСтр("ru='Перед выгрузкой необходимо установить единицу измерения ""в рублях"".'");
			
			Сообщение.Сообщить();
			
			Возврат Ложь;
			
		КонецЕсли;
		Если СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения <> 0 Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = НСтр("ru='Перед выгрузкой необходимо установить точность ""0"".'");
			
			Сообщение.Сообщить();
			
			Возврат Ложь;
			
		КонецЕсли;
	КонецЕсли;
	Если СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки <> Неопределено И СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки Тогда
		РегламентированнаяОтчетностьКлиент.ПроверитьКонтрольныеСоотношения(ЭтотОбъект, ОбработкаКС);
	КонецЕсли;
	Если НЕ УниверсальнаяВыгрузкаXMLНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLНаСервере(ТекстВыгрузки, ПараметрыВыгрузки)
	
	СформироватьСтруктуруПоказателей();
	ПараметрыВыгрузки = Отчеты.РегламентированныйОтчет3НДФЛ.ПараметрыВыгрузки(
		СтруктураРеквизитовФормы.мВыбраннаяФорма, мДанныеОтчета.Титульный);
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	
	Если НЕ ВыгрузкаВозможна(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстВыгрузки = ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ИзвлечьСтруктуруXML()
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ОтчетОбъект().ПолучитьМакет("СтруктураXML513");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость = Истина И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

&НаСервере
Функция ВыгрузкаВозможна(ДеревоВыгрузки)
	
	Если Не ПроверитьРазделыНаНаличиеИнформации(ДеревоВыгрузки, "Раздел1, Раздел2") Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			НСтр("ru = 'Должны быть заполнены разделы 1 и 2.'"),
			"Раздел1");
		Возврат Ложь;
	КонецЕсли;
	
	Для Индекс = 0 По мДанныеОтчета.Раздел1.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Раздел1[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Раздел1", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуРаздела1(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Для Индекс = 0 По мДанныеОтчета.Приложение1.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Приложение1[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Приложение1", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуПриложения1(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Для Индекс = 0 По мДанныеОтчета.Приложение2.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Приложение2[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Приложение2", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуПриложения2(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Если Не СтраницаПуста(мДанныеОтчета.Приложение5, "Приложение5", ДеревоВыгрузки) Тогда
		ПроверитьПриложение5(мДанныеОтчета.Приложение5);
	КонецЕсли;
	
	Для Индекс = 0 По мДанныеОтчета.Приложение7.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Приложение7[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Приложение7", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуПриложения7(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Для Индекс = 0 По мДанныеОтчета.Приложение8.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Приложение8[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Приложение8", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуПриложения8(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Для Индекс = 0 По мДанныеОтчета.Приложение5Расчет.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Приложение5Расчет[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Приложение5Расчет", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуПриложение5Расчет(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Для Индекс = 0 По мДанныеОтчета.Приложение1Расчет.Количество() - 1 Цикл
		ДанныеСтраницы = мДанныеОтчета.Приложение1Расчет[Индекс].Данные[0].Значение;
		Если Не СтраницаПуста(ДанныеСтраницы, "Приложение1Расчет", ДеревоВыгрузки) Тогда
			ПроверитьСтраницуПриложение1Расчет(ДанныеСтраницы, Индекс + 1);
		КонецЕсли;
	КонецЦикла;
	
	Возврат (ТаблицаСообщений.Количество() = 0);
	
КонецФункции

&НаСервере
Процедура ПроверитьСтраницуРаздела1(ДанныеСтраницы, НомерСтраницы)
	
	НомерСтроки = 1;
	
	Пока ДанныеСтраницы.Свойство(СтрШаблон("П000100001030_%1", НомерСтроки)) Цикл
		
		// Код ОКТМО
		Если ПустаяСтрока(ДанныеСтраницы[СтрШаблон("П000100001030_%1", НомерСтроки)]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Раздела 1 в строке %2 не указан код ОКТМО!'"), НомерСтраницы, НомерСтроки),
				"Раздел1",
				"П000100001030",
				НомерСтраницы);
		Иначе
			ДлинаОКТМО = СтрДлина(СокрЛП(ДанныеСтраницы[СтрШаблон("П000100001030_%1", НомерСтроки)]));
			Если ДлинаОКТМО <> 8 И ДлинаОКТМО <> 11 Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
					СтрШаблон(НСтр("ru = 'На странице %1 Раздела 1 в строке %2 указан код ОКТМО некорректной длины!'"), НомерСтраницы, НомерСтроки),
					"Раздел1",
					"П000100001030",
					НомерСтраницы);
			КонецЕсли;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСтраницуПриложения1(ДанныеСтраницы, НомерСтраницы)
	
	НомерСтроки = 1;
	
	Пока ДанныеСтраницы.Свойство("П000300001030_" + НомерСтроки) Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеСтраницы["П000300001010_" +НомерСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект, 
				СтрШаблон("На странице %1 Приложения 1 в строке %2 не указана ставка налога", НомерСтраницы, НомерСтроки),
				"Приложение1",
				"П000300001010_" + НомерСтроки,
				НомерСтраницы);
		КонецЕсли;
		
		ДлинаИНН = СтрДлина(СокрЛП(ДанныеСтраницы["П000300001030_" +НомерСтроки])); //ИНН
		ДлинаКПП = СтрДлина(СокрЛП(ДанныеСтраницы["П000300001040_" + НомерСтроки])); //КПП
		
		Если ДлинаИНН > 0 И ДлинаИНН <> 12 И  ДлинаИНН <> 10  Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект, 
				СтрШаблон("На странице %1 Приложения 1 в строке %2 указан ИНН некорректной длины!", НомерСтраницы, НомерСтроки),
				"Приложение1",
				"П000300001030_" + НомерСтроки,
				НомерСтраницы);
		КонецЕсли;
		
		Если ДлинаИНН = 10 И ДлинаКПП <> 9  Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект, 
				СтрШаблон("На странице %1 Приложения 1 в строке %2 указан КПП некорректной длины!", НомерСтраницы, НомерСтроки),
				"Приложение1",
				"П000300001040_" + НомерСтроки,
				НомерСтраницы);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СокрЛП(ДанныеСтраницы["П000300001060_" + НомерСтроки])) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект, 
				СтрШаблон("На странице %1 Приложения 1 в строке %2 не указано Наименование источника выплаты дохода", НомерСтраницы, НомерСтроки),
				"Приложение1",
				"П000300001060_" + НомерСтроки,
				НомерСтраницы);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000300001070_" + НомерСтроки]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект, 
				СтрШаблон("На странице %1 Приложения 1 в строке %2 не указана сумма дохода", НомерСтраницы, НомерСтроки),
				"Приложение1",
				"П000300001070_" + НомерСтроки,
				НомерСтраницы);
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСтраницуПриложения2(ДанныеСтраницы, НомерСтраницы)
	
	Если Не ЗначениеЗаполнено(ДанныеСтраницы.П000400001001) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2: не указана ставка налога", НомерСтраницы),
			"Приложение2",
			"П000400001001",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000400001010"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указан код страны по классификатору ОКСМ", НомерСтраницы),
			"Приложение2",
			"П000400001010",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(ДанныеСтраницы["П000400001020"])) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указано Наименование источника выплаты дохода", НомерСтраницы),
			"Приложение2",
			"П000400001020",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000400001030"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указан код валюты", НомерСтраницы),
			"Приложение2",
			"П000400001030",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000400001040"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указана дата получения дохода", НомерСтраницы),
			"Приложение2",
			"П000400001040",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000400001050"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указан курс иностранной валюты", НомерСтраницы),
			"Приложение2",
			"П000400001050",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000400001060"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указана сумма дохода, полученная в иностранной валюте", НомерСтраницы),
			"Приложение2",
			"П000400001060",
			НомерСтраницы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтраницы["П000400001070"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон("На странице %1 Приложения 2 не указана сумма дохода, полученная в иностранной валюте в пересчете в рубли", НомерСтраницы),
			"Приложение2",
			"П000400001070",
			НомерСтраницы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСтраницуПриложения7(ДанныеСтраницы, НомерСтраницы)
	
	Если ПустаяСтрока(ДанныеСтраницы.П000700001030) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
			СтрШаблон(НСтр("ru = 'На странице %1 Приложения 7 не указан код номера объекта (30)'"), НомерСтраницы),
			"Приложение7",
			"П000700001030",
			НомерСтраницы);
	ИначеЕсли НомерОбъектаНедвижимостиОтсутствует(ДанныеСтраницы.П000700001030) Тогда
		// Если отсутствует номер объекта, обязательно указывается его адрес
		Если ПустаяСтрока(ДанныеСтраницы.П000700001032) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 7 не указаны сведения о местонахождении объекта (32)'"), НомерСтраницы),
				"Приложение7",
				"П000700001032",
				НомерСтраницы);
		КонецЕсли;
	Иначе
		Если ПустаяСтрока(ДанныеСтраницы.П000700001031) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 7 не указан номер объекта (31)'"), НомерСтраницы),
				"Приложение7",
				"П000700001031",
				НомерСтраницы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСтраницуПриложения8(ДанныеСтраницы, НомерСтраницы)
	
	ПризнакиУчетаУбытков = Отчеты.РегламентированныйОтчет3НДФЛ.ПризнакиУчетаУбытковПоЦеннымБумагам(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	НомерСтроки = 1;
	Пока ДанныеСтраницы.Свойство(СтрШаблон("П001200001010_%1", НомерСтроки)) Цикл
		ИмяПоля = СтрШаблон("П001200001010_%1", НомерСтроки);
		Если Не ЗначениеЗаполнено(ДанныеСтраницы[ИмяПоля]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 8 в строке %2 не указан код вида операции (10)'"), НомерСтраницы, НомерСтроки),
				"Приложение8",
				"П001200001010",
				НомерСтраницы);
		КонецЕсли;
		
		ИмяПоля = СтрШаблон("П001200001020_%1", НомерСтроки);
		Если Не ЗначениеЗаполнено(ДанныеСтраницы[ИмяПоля]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 8 в строке %2 не указана сумма дохода (20)'"), НомерСтраницы, НомерСтроки),
				"Приложение8",
				"П001200001020",
				НомерСтраницы);
		КонецЕсли;
		
		Строка051Заполнена = Ложь;
		СуммаСтрок052 = 0;
		ЕстьОшибкиЗаполненияСтрок = Ложь;
		НомерДопСтроки = 1;
		Пока ДанныеСтраницы.Свойство(СтрШаблон("П001200001051_%1_%2", НомерСтроки, НомерДопСтроки)) Цикл
			Если Не ПустаяСтрока(ДанныеСтраницы[СтрШаблон("П001200001051_%1_%2", НомерСтроки, НомерДопСтроки)]) Тогда
				Строка051Заполнена = Истина;
			ИначеЕсли ЗначениеЗаполнено(ДанныеСтраницы[СтрШаблон("П001200001052_%1_%2", НомерСтроки, НомерДопСтроки)]) Тогда
				ЕстьОшибкиЗаполненияСтрок = Истина;
			КонецЕсли;
			
			СуммаСтрок052 = СуммаСтрок052 + ДанныеСтраницы[СтрШаблон("П001200001052_%1_%2", НомерСтроки, НомерДопСтроки)];
			
			НомерДопСтроки = НомерДопСтроки + 1;
		КонецЦикла;
		
		ИмяПоля = СтрШаблон("П001200001050_%1", НомерСтроки);
		Если Не ЗначениеЗаполнено(ДанныеСтраницы[ИмяПоля]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 8 в строке %2 не указан признак учета убытков (50)'"), НомерСтраницы, НомерСтроки),
				"Приложение8",
				"П001200001050",
				НомерСтраницы);
		ИначеЕсли ДанныеСтраницы[ИмяПоля] = ПризнакиУчетаУбытков.УбытокНеУчитывается
			И (Строка051Заполнена Или СуммаСтрок052 <> 0) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 8 в строке %2 в поле (50) указан признак учета убытков %3. Поля (51) и (52) в таком случае не заполняются'"), НомерСтраницы, НомерСтроки, ДанныеСтраницы[ИмяПоля]),
				"Приложение8",
				"П001200001050",
				НомерСтраницы);
		КонецЕсли;
		
		ОбщаяСуммаРасходов =
			  ДанныеСтраницы[СтрШаблон("П001200001040_%1", НомерСтроки)]
			+ ДанныеСтраницы[СтрШаблон("П001200001060_%1", НомерСтроки)]
			+ ДанныеСтраницы[СтрШаблон("П001200001070_%1", НомерСтроки)]
			+ СуммаСтрок052;
		
		Если ОбщаяСуммаРасходов > ДанныеСтраницы[СтрШаблон("П001200001020_%1", НомерСтроки)] Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Приложения 8 в строке %2 сумма полей (40), (52), (60), (70) больше суммы в поле (20)'"), НомерСтраницы, НомерСтроки),
				"Приложение8",
				"П001200001020",
				НомерСтраницы);
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НомерОбъектаНедвижимостиОтсутствует(КодНомераОбъекта)
	
	Возврат (КодНомераОбъекта = "4");
	
КонецФункции

&НаСервере
Процедура ПроверитьПриложение5(ДанныеСтраницы)
	
	Если ДанныеСтраницы.П000900001090 <> 0 Тогда
		СуммаОблагаемогоДоходаПоСтавке13 = СуммаОблагаемогоДоходаПоСтавке(ЭтотОбъект, 13);
		Если ДанныеСтраницы.П000900001090 > Окр(СуммаОблагаемогоДоходаПоСтавке13 * 0.25, 2) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'Сумма вычета в виде пожертвований превышает 25%% от общей суммы дохода %1'"), СуммаОблагаемогоДоходаПоСтавке13),
				"Приложение5",
				"П000900001090");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСтраницуПриложение5Расчет(ДанныеСтраницы, НомерСтраницы)
	
	Если ДанныеСтраницы.П001000001060 <> 0 Тогда
		
		ДлинаИНН = СтрДлина(СокрЛП(ДанныеСтраницы.П001000001010)); //ИНН
		ДлинаКПП = СтрДлина(СокрЛП(ДанныеСтраницы.П001000001020)); //КПП
		ДлинаНомераДоговора = СтрДлина(СокрЛП(ДанныеСтраницы.П001000001050));
		
		Если ДлинаИНН <> 10 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Расчета к Приложению 5 указан ИНН некорректной длины!'"), НомерСтраницы),
				"Приложение5Расчет",
				"П001000001010",
				НомерСтраницы);
		КонецЕсли;
		
		Если ДлинаКПП <> 9 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Расчета к Приложению 5 указан КПП некорректной длины!'"), НомерСтраницы),
				"Приложение5Расчет",
				"П001000001020",
				НомерСтраницы);
		КонецЕсли;
		
		Если ДлинаНомераДоговора = 0 Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон(НСтр("ru = 'На странице %1 Расчета к Приложению 5 указан номер договора некорректной длины!'"), НомерСтраницы),
				"Приложение5Расчет",
				"П001000001050",
				НомерСтраницы);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСтраницуПриложение1Расчет(ДанныеСтраницы, НомерСтраницы)
	
	НомерСтроки = 1;
	Пока ДанныеСтраницы.Свойство(СтрШаблон("П001900001010_%1", НомерСтроки)) Цикл
		
		ИмяПоля = СтрШаблон("П001900001010_%1", НомерСтроки);
		Если Не ЗначениеЗаполнено(ДанныеСтраницы[ИмяПоля]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
				СтрШаблон("На странице %1 Расчета к Приложению 1 в строке %2 не указан кадастровый номер", НомерСтраницы, НомерСтроки),
				"Приложение1Расчет",
				ИмяПоля,
				НомерСтраницы);
		КонецЕсли;
		
		ИмяПоля = СтрШаблон("П001900001030_%1", НомерСтроки);
		Если Не ЗначениеЗаполнено(ДанныеСтраницы[ИмяПоля]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект, 
				СтрШаблон("На странице %1 Расчета к Приложению 1 в строке %2 не указана сумма дохода исходя из цены договора", НомерСтраницы, НомерСтроки),
				"Приложение1Расчет",
				ИмяПоля,
				НомерСтраницы);
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение = Неопределено) Экспорт
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" Тогда
		СтруктураДанныхТитульный.Вставить("КодИМНС", ТабличныйДокумент.Области.КодИМНС.Значение);
		Если ТабличныйДокумент.Области.НомерКорректировки.Значение = "" Тогда
			ТабличныйДокумент.Области.НомерКорректировки.Значение = "0";
		КонецЕсли;
		Вариант = Число(ТабличныйДокумент.Области.НомерКорректировки.Значение);
	Иначе
		Вариант = Число(СтруктураДанныхТитульный.НомерКорректировки);
	КонецЕсли;
	
	КодИФНС = СтруктураДанныхТитульный.КодИМНС;
	КПП 	= "";

	Если НЕ РегламентированнаяОтчетностьКлиент.ПриЗаписиРегламентированногоОтчетаНаКлиенте(ЭтотОбъект, КодИФНС, Автосохранение, Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
			
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		НаименованиеТекущегоРаздела = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	Иначе
		НаименованиеТекущегоРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	КонецЕсли;
	
	УдалитьДанныеПомощника = Модифицированность И ОтчетСозданИзПомощника;
	Если УдалитьДанныеПомощника Тогда
		// Скрываем плашку про отчет из помощника при сохранении в случае, 
		// если отчет был модифицирован.
		Элементы.ГруппаОтчетИзПомощника.Видимость = Ложь;
		Оповестить("ЗакрытьПомощникЗаполнения3НДФЛ", СтруктураРеквизитовФормы.мСохраненныйДок);
	КонецЕсли;
	
	РезультатСохранения = ПередСохранением(
		КодИФНС, 
		КПП,
		ПодобныйОтчетСуществует,
		Вариант,
		ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,
		Автосохранение,
		НаименованиеТекущегоРаздела,
		УдалитьДанныеПомощника);
		
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением = (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	Если НуженВопросПередСохранением Тогда
		
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС, КПП);
		
	Иначе
		
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС, КПП)
	
	Если ПодобныйОтчетСуществует Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчет с видом %1 уже существует.
			|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП", Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,КодИФНС, КПП);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,КодИФНС, КПП);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, КодИФНС, КПП", Автосохранение, Вариант, ВыполняемоеОповещение,КодИФНС, КПП );
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.Открыть();
	Иначе
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтотОбъект.Заголовок), , , БиблиотекаКартинок.Записать);
		РезультатСохранения = Сохранить(Автосохранение, КодИФНС, КПП, Вариант, Ложь, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
	Иначе
		Возврат;
	КонецЕсли;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтотОбъект.Заголовок), , , БиблиотекаКартинок.Записать);
	РезультатСохранения = Сохранить(Автосохранение, КодИФНС, КПП, Вариант, ВидДокументаНомерКорректировкиИзменен, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения)
	
	КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если РезультатСохранения Тогда
		
		РегламентированнаяОтчетностьКлиент.ПослеЗаписиРегламентированногоОтчета(ЭтотОбъект);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителяЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиЧерезПредставителяВРегламентированномОтчете(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Код = "ОКВЭД" Тогда
		// Для ОКВЭД нужно убрать точки в конце
		ЗначениеПоказателя = СтрЗаменить(СокрЛП(СтрЗаменить(ЗначениеПоказателя, ".", " ")) ," ", ".");
	ИначеЕсли Узел.Код = "Тлф" Тогда
		ЗначениеПоказателя = Отчеты.РегламентированныйОтчет3НДФЛ.ТелефонДляОтчета(ЗначениеПоказателя);
	ИначеЕсли Узел.Код = "НомОбъект" Или Узел.Код = "СвМестОбъект" Или Узел.Код = "НомКадОтчуждОНИ" Тогда
		ЗначениеПоказателя = СтрЗаменить(ЗначениеПоказателя, Символы.ПС, " ");
	КонецЕсли;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Если ЗначениеЗаполнено(ЗначениеПоказателя) Тогда
			Узел.Значение = ?(МаксШирина < СтрДлина(СокрЛП(ЗначениеПоказателя)), СокрЛП(Лев(СокрЛП(ЗначениеПоказателя), МаксШирина)), СокрЛП(ЗначениеПоказателя));
		Иначе
			Узел.Значение = Неопределено;
		КонецЕсли;
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.; ЧН=0; ЧГ=";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	ИначеЕсли Узел.Формат = "gYear" Тогда
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Если ЗначениеЗаполнено(ЗначениеПоказателя) Тогда
				Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, "ДФ=гггг"));
			Иначе
				Узел.Значение = Неопределено;
			КонецЕсли;
		ИначеЕсли Не ЗначениеЗаполнено(ЗначениеПоказателя) Тогда
			// Пустое значение для gYear
			Узел.Значение = "0001";
		ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Число") Тогда
			
			Узел.Значение = Прав(СокрЛП(Формат(ЗначениеПоказателя, "ЧГ=")), 4);
			
		Иначе
			Узел.Значение = Прав(СокрЛП(ЗначениеПоказателя), 4);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДаннымиУзел(Узел, ДанныеРаздела = Неопределено, НомерСтроки = Неопределено, ТолькоАтрибуты = Ложь)
	
	Если Узел = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем данные конкретного раздела
	Если ДанныеРаздела = Неопределено Тогда
		ДанныеРаздела = мДанныеОтчета[Узел.Раздел];
	КонецЕсли;
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		Если (Стр.Тип = "С" ИЛИ Стр.Тип = "C") И Не ТолькоАтрибуты Тогда // учтем оба варианта: кириллицу и латиницу
			Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ДанныеРаздела, "ИмяКлючевогоУзлаСодержательнойЧасти")
				И Стр.Код = ДанныеРаздела.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				ЗаполнитьСодержательнуюЧасть(Стр);
			ИначеЕсли Стр.Многострочность Тогда
				//Особенность декларации, многострочные подузлы
				Если Стр.Родитель.Многострочность Тогда
					Если Стр.Код = "ИстЮЛ" Или Стр.Код = "ИстФЛИн" Или Стр.Код = "СведИст" Или Стр.Код = "СвОрг" Или Стр.Код = "СвФЛ" Тогда
						Если УсловиеЮр_Физ(ДанныеРаздела, Стр, НомерСтроки) Тогда
							ЗаполнитьДаннымиУзел(Стр, ДанныеРаздела, НомерСтроки);
						КонецЕсли;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				НомСтр = 1;
				ПодчиненныйЭлемент = ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Стр);
				КлючПодчиненногоЭлемента = ПодчиненныйЭлемент.Ключ;
				Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ДанныеРаздела, КлючПодчиненногоЭлемента + "_" + Формат(НомСтр, "ЧГ=")) Цикл
					УзелСоответствующийСтроке = СкопироватьУзел(Узел, Стр);
					ЗаполнитьДаннымиУзел(УзелСоответствующийСтроке, ДанныеРаздела, НомСтр);
					НомСтр = НомСтр + 1;
				КонецЦикла;
			Иначе
				Если Стр.Код = "ИстЮЛ" Или Стр.Код = "ИстФЛИн" Или Стр.Код = "СведИст" Или Стр.Код = "СвОрг" Или Стр.Код = "СвФЛ" Тогда
					Если УсловиеЮр_Физ(ДанныеРаздела, Стр, НомерСтроки) Тогда
						ЗаполнитьДаннымиУзел(Стр, ДанныеРаздела, НомерСтроки);
					КонецЕсли;
				ИначеЕсли ЗначениеЗаполнено(Стр.Раздел) И Не Стр.Многостраничность Тогда
					ЗаполнитьДаннымиУзел(Стр, , НомерСтроки);
				Иначе
					ЗаполнитьДаннымиУзел(Стр, ДанныеРаздела, НомерСтроки);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
					ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
					Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ДанныеРаздела, ПолныйКодПоказателя) Тогда
						ЗначениеПоказателя = ?(ДанныеРаздела[ПолныйКодПоказателя] = 0, Стр.ЗначениеПоУмолчанию, ДанныеРаздела[ПолныйКодПоказателя]);
						Если ЗначениеПоказателя = "" И Стр.Формат = "N" Тогда
							ЗначениеПоказателя = ЗначениеПоказателяПустогоПоказателя(Стр);
						КонецЕсли;
						ВывестиПоказательВXML(Стр, ЗначениеПоказателя);
					Иначе
						ВывестиПоказательВXML(Стр, "");
					КонецЕсли;
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				ВывестиПоказательВXML(Стр, ДанныеРаздела[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УсловиеЮр_Физ(Параметры, Узел, НомерСтроки = Неопределено)
	
	//проверка на длину ИНН для заполнения соответствующей ветки
	Результат = Ложь;
	ЗначениеПоказателя = Неопределено;
	Если Узел.Код = "ИстЮЛ" ИЛИ Узел.Код = "СведИст" ИЛИ Узел.Код = "СвОрг" Тогда
		КлючПодчиненногоЭлемента = Узел.Строки.Найти("ИННЮЛ").Ключ;
		ПолныйКодПоказателя = КлючПодчиненногоЭлемента + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
		Если Параметры.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
			Если НЕ ЗначениеЗаполнено(ЗначениеПоказателя) ИЛИ СтрДлина(СокрЛП(ЗначениеПоказателя)) > 10 Тогда
				РегламентированнаяОтчетность.УдалитьУзел(Узел);
				Результат = Ложь;
			Иначе
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Узел.Код = "ИстФЛИн" ИЛИ Узел.Код = "СвФЛ" Тогда
		КлючПодчиненногоЭлемента = Узел.Строки.Найти("ИННФЛ").Ключ;
		ПолныйКодПоказателя = КлючПодчиненногоЭлемента + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
		Если Параметры.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
			Если ЗначениеЗаполнено(ЗначениеПоказателя) И СтрДлина(СокрЛП(ЗначениеПоказателя)) < 12 Тогда
				РегламентированнаяОтчетность.УдалитьУзел(Узел);
				Результат = Ложь;
			Иначе
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗначениеСоответствуетФормату(Стр)
	
	СтатусПроверки = Истина;
	
	Если Стр.Формат = "Т" ИЛИ Стр.Формат = "T"  ИЛИ Стр.Формат = "S" Тогда // учтем оба варианта: кириллицу и латиницу
		ДлинаЗначения = СтрДлина(Стр.Значение);
		Если ДлинаЗначения < Стр.МинРазмерность Тогда
			СтатусПроверки = Ложь;
			Если ЗначениеЗаполнено(Стр.Раздел) И ЗначениеЗаполнено(Стр.Ключ) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
					"Длина значения показателя """
					+ РегламентированнаяОтчетность.ПредставлениеПоказателя(Стр)
					+ """ (" + Стр.Код+ ") меньше минимально возможной (" + ДлинаЗначения
					+ " < " + Стр.МинРазмерность + ") (" + Стр.Раздел + ")", Стр.Раздел, Стр.Ключ, "");
			Иначе
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
					"Длина значения показателя """
					+ РегламентированнаяОтчетность.ПредставлениеПоказателя(Стр)
					+ """ (" + Стр.Код+ ") меньше минимально возможной (" + ДлинаЗначения
					+ " < " + Стр.МинРазмерность + ") (" + Стр.Раздел + ")", "Титульный", "");
			КонецЕсли;
		КонецЕсли;
		Если ДлинаЗначения > Стр.МаксРазмерность Тогда
			СтатусПроверки = Ложь;
			Если ЗначениеЗаполнено(Стр.Раздел) И ЗначениеЗаполнено(Стр.Ключ) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
					"Длина значения показателя """
					+ РегламентированнаяОтчетность.ПредставлениеПоказателя(Стр)
					+ """  (" + Стр.Код+ ") больше максимально возможной (" + ДлинаЗначения
					+ " > " + Стр.МаксРазмерность + ") (" + Стр.Раздел + ")", Стр.Раздел, Стр.Ключ, "");
			Иначе
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтотОбъект,
					"Длина значения показателя """
					+ РегламентированнаяОтчетность.ПредставлениеПоказателя(Стр)
					+ """  (" + Стр.Код+ ") больше максимально возможной (" + ДлинаЗначения
					+ " > " + Стр.МаксРазмерность + ") (" + Стр.Раздел + ")", "Титульный", "");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Функция ПроверитьДанные(Узел)
	
	СтатусПроверки = Истина;
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если НЕ ЗначениеСоответствуетФормату(Узел) Тогда
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	Для Каждого Стр из Узел.Строки Цикл
		СтатусПроверки = (ПроверитьДанные(Стр) И СтатусПроверки); // именно в указанном порядке для выдачи всех ошибок
	КонецЦикла;
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки)
	
	ПотокXML = РегламентированнаяОтчетность.СоздатьНовыйПотокXML("windows-1251"); // создаем новый поток для записи
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, ПараметрыВыгрузки); // пишем дерево в поток
	ТекстДляЗаписи = ПотокXML.Закрыть(); // получаем текст XML
	Возврат ТекстДляЗаписи;
	
КонецФункции

&НаСервере
Функция СкопироватьУзел(Родитель, Узел)
	
	// находим узел с максимальным индексом и с тем же кодом, что и у копируемого, и добавляем новый сразу после найденного
	СтрокиСТемЖеКодом = Родитель.Строки.НайтиСтроки(Новый Структура("Код", Узел.Код), Ложь);
	МаксИндекс = -1;
	Для Каждого СтрокаСТемЖеКодом Из СтрокиСТемЖеКодом Цикл
		ТекИндекс = Родитель.Строки.Индекс(СтрокаСТемЖеКодом);
		Если ТекИндекс > МаксИндекс Тогда
			МаксИндекс = ТекИндекс;
		КонецЕсли;
	КонецЦикла;
	
	ИндексСоздаваемогоУзла = МаксИндекс + 1;
	Если ИндексСоздаваемогоУзла >= Родитель.Строки.Количество() ИЛИ ИндексСоздаваемогоУзла = 0 Тогда // если аналогичный узел не найден или найденный узел - последний
		НовыйУзел = Родитель.Строки.Добавить();
	Иначе
		НовыйУзел = Родитель.Строки.Вставить(ИндексСоздаваемогоУзла);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	Возврат НовыйУзел;
	
КонецФункции

&НаСервере
Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	ЧислоВместоКодаЭлемента = (ТипЗнч(КодЭлемента) = Тип("Число"));
	Если ТипЗнч(КодЭлемента) = Тип("Число") Тогда
		Если Узел.Строки.Количество() > КодЭлемента Тогда
			Возврат Узел.Строки.Получить(КодЭлемента);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Для Каждого Стр Из Узел.Строки Цикл
			Если Стр.Код = КодЭлемента Тогда
				Возврат Стр;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСодержательнуюЧасть(УзелСодержательнойЧасти)
	
	Если УзелСодержательнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Создаем Родительский узел - эталон, структура будет определеяться по эталонному узлу.
	// После цикла по всем элементам эталонного узла, он должен быть удален.
	Эталон = СкопироватьУзел(УзелСодержательнойЧасти.Родитель, УзелСодержательнойЧасти);
	
	// Очищаем все подчиненные узлы родительского узла.
	// По мере необходимости эти узлы будут создаваться.
	УзелСодержательнойЧасти.Строки.Очистить();
	
	Для Каждого ПодчиненныйУзел Из Эталон.Строки Цикл
		
		Если ПодчиненныйУзел.Многостраничность Тогда
			ЗаполнитьДаннымиУзелМногостраничногоРаздела(УзелСодержательнойЧасти, ПодчиненныйУзел);
		Иначе
			ЗаполнитьДаннымиУзелРаздела(УзелСодержательнойЧасти, ПодчиненныйУзел);
		КонецЕсли;
		
	КонецЦикла;
	
	// Удаляем эталон из дерева выгрузки.
	Эталон.Родитель.Строки.Удалить(Эталон);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелРаздела(Родитель, Узел)
	
	Если СтраницаПуста(мДанныеОтчета[Узел.Раздел], Узел.Раздел, Родитель.Владелец(), Узел.Многострочность) Тогда
		Возврат;
	КонецЕсли;
	
	ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
	ЗаполнитьДаннымиУзел(ЭкземплярУзла);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелМногостраничногоРаздела(Родитель, Узел)
	
	Если Узел.Код = "ДоходИстИно" Тогда
		ЗаполнитьДаннымиУзелМногостраничногоРаздела_ДоходИстИно(Родитель, Узел);
		Возврат;
	КонецЕсли;
	
	Если Узел.Код = "ДоходПредпр" Тогда
		ЗаполнитьДаннымиУзелМногостраничногоРаздела_ДоходПредпр(Родитель, Узел);
		Возврат;
	КонецЕсли;
	
	Если Узел.Код = "ВычСоцИнв219" Тогда
		ЗаполнитьДаннымиУзелМногостраничногоРаздела_ВычСоцИнв219(Родитель, Узел);
		Возврат;
	КонецЕсли;
	
	Если Узел.Код = "ИмущНалВычНов" Тогда
		ЗаполнитьДаннымиУзелМногостраничногоРаздела_ИмущНалВычНов(Родитель, Узел);
		Возврат;
	КонецЕсли;
	
	Если Узел.Код = "ДохОперЦБ" Тогда
		ЗаполнитьДаннымиУзелМногостраничногоРаздела_ДохОперЦБ(Родитель, Узел);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтраницаРаздела Из мДанныеОтчета[Узел.Раздел] Цикл
		
		ДанныеСтраницы = СтраницаРаздела.Данные[0].Значение;
		
		// Проверим заполненность страницы раздела.
		Если СтраницаПуста(ДанныеСтраницы, Узел.Раздел, Родитель.Владелец(), Узел.Многострочность) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Узел.Многострочность Тогда
			// Требуется особая обработка, т.к. каждая строка листа соответствует отдельному элементу в структуре выгрузки.
			Для Каждого СтрокаМногострочнойЧасти Из СтраницаРаздела.ДанныеДопСтрок[0].Значение Цикл
				ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
				ЗаполнитьДаннымиУзел(ЭкземплярУзла, СтрокаМногострочнойЧасти);
			КонецЦикла;
		Иначе
			ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
			ЗаполнитьДаннымиУзел(ЭкземплярУзла, ДанныеСтраницы);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелМногостраничногоРаздела_ДоходИстИно(Родитель, Узел)
	
	ДеревоВыгрузки = Родитель.Владелец();
	
	ДанныеРаздела = мДанныеОтчета[Узел.Раздел];
	
	СтавкиИУзлы = Новый Соответствие;
	
	ЭталонСтроки = ПолучитьПодчиненныйЭлемент(Узел, "РасчДохНалИно");
	
	Для Каждого СтраницаЛиста Из ДанныеРаздела Цикл
		
		ПоказателиЛиста = СтраницаЛиста.Данные[0].Значение;
		
		Если СтраницаПуста(ПоказателиЛиста, Узел.Раздел, ДеревоВыгрузки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтавкаЛиста = Неопределено;
		Если ПоказателиЛиста.Свойство("П000400001001", СтавкаЛиста) Тогда
			
			ЭкземплярУзла = СтавкиИУзлы.Получить(СтавкаЛиста);
			Если ЭкземплярУзла = Неопределено Тогда
				ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
				СтавкиИУзлы.Вставить(СтавкаЛиста, ЭкземплярУзла);
				ЗаполнитьДаннымиУзел(ЭкземплярУзла, ПоказателиЛиста, , Истина);
			КонецЕсли;
			
			ЭкземплярСтроки = СкопироватьУзел(ЭкземплярУзла, ЭталонСтроки);
			ЗаполнитьДаннымиУзел(ЭкземплярСтроки, ПоказателиЛиста);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелМногостраничногоРаздела_ДоходПредпр(Родитель, Узел)
	
	// Приложение 3 обратываем по особенному
	// Итоги на последнем листе являются атрибутами всего раздела
	// а данные о видах деятельности подразделами
	// отдельным разделом является информация о крестьянском фермерском хозяйстве
	
	ДеревоВыгрузки = Родитель.Владелец();
	
	ДанныеРаздела = мДанныеОтчета[Узел.Раздел];
	
	// Создаем узел раздела, в него будем добавлять подузлы для видов деятельности
	УзелПриложение3 = СкопироватьУзел(Родитель, Узел);
	
	УзелВидДеятельности = ПолучитьПодчиненныйЭлемент(Узел, "РасчВидДеят");
	Если УзелВидДеятельности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УзелПрофВычет = ПолучитьПодчиненныйЭлемент(Узел, "ПрофНалВыч");
	
	Для Каждого СтраницаПриложение3 Из ДанныеРаздела Цикл
		
		ПоказателиЛиста = СтраницаПриложение3.Данные[0].Значение;
		
		Если СтраницаПуста(ПоказателиЛиста, Узел.Раздел, ДеревоВыгрузки) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭкземплярУзла = СкопироватьУзел(УзелПриложение3, УзелВидДеятельности);
		ЗаполнитьДаннымиУзел(ЭкземплярУзла, ПоказателиЛиста);
		
		ЭкземплярУзла = СкопироватьУзел(УзелПриложение3, УзелПрофВычет);
		ЗаполнитьДаннымиУзел(ЭкземплярУзла, ПоказателиЛиста);
		
	КонецЦикла;
	
	// Последняя страница - итоговая, на ней находятся итоговые данные по всему узлу
	// данные о крестьянском фермерском хозяйстве и о корректировке
	УзелКФХ = ПолучитьПодчиненныйЭлемент(УзелПриложение3, "СведКФХ");
	Если УзелКФХ <> Неопределено И ПоказателиЛиста <> Неопределено Тогда
		ЗаполнитьДаннымиУзел(УзелКФХ, ПоказателиЛиста);
	КонецЕсли;
	
	УзелНБКор105_3 = ПолучитьПодчиненныйЭлемент(УзелПриложение3, "НБКор105.3");
	Если УзелНБКор105_3 <> Неопределено И ПоказателиЛиста <> Неопределено Тогда
		ЗаполнитьДаннымиУзел(УзелНБКор105_3, ПоказателиЛиста);
	КонецЕсли;
	
	Если ПоказателиЛиста <> Неопределено Тогда
		ЗаполнитьДаннымиУзел(УзелПриложение3, ПоказателиЛиста, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелМногостраничногоРаздела_ВычСоцИнв219(Родитель, Узел)
	
	ДеревоВыгрузки = Родитель.Владелец();
	
	ДанныеРаздела = мДанныеОтчета[Узел.Раздел];
	
	// Создаем новый Лист
	ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
	
	// Получаем подчиненные элементы листа п. 1 и 3
	Экземпляр_РасчПенсВзнос = ПолучитьПодчиненныйЭлемент(ЭкземплярУзла, "РасчПенсВзнос");
	
	// Получаем эталонный элемент пункта 1, будем использовать его для получения эталонов элементов 1.1 и 1.2
	Эталон_РасчПенсВзнос = ПолучитьПодчиненныйЭлемент(Узел, "РасчПенсВзнос");
	
	// Получаем эталонный элемент для п. 1.1
	Эталон_РасчПСВВыч = ПолучитьПодчиненныйЭлемент(Эталон_РасчПенсВзнос, "РасчПСВВыч");
	
	// Получаем элемент для п. 1.2
	Экземпляр_РасчДопСВВыч = ПолучитьПодчиненныйЭлемент(Экземпляр_РасчПенсВзнос, "РасчДопСВВыч");
	
	ИтогиЛиста = Новый ТаблицаЗначений;
	ИменаКолонок = "";
	Для Каждого СтраницаЛиста Из ДанныеРаздела Цикл
		
		// Проверим заполненность страницы раздела
		Если СтраницаПуста(СтраницаЛиста.Данные[0].Значение, Узел.Раздел, ДеревоВыгрузки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИтогиЛиста.Колонки.Количество() = 0 Тогда
			Для Каждого КлючИЗначение Из СтраницаЛиста.Данные[0].Значение Цикл
				ИтогиЛиста.Колонки.Добавить(КлючИЗначение.Ключ);
				ИменаКолонок = ИменаКолонок + КлючИЗначение.Ключ + ",";
			КонецЦикла;
		КонецЕсли;
		
		СтрокаИтогов = ИтогиЛиста.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаИтогов, СтраницаЛиста.Данные[0].Значение);
		
		// Создаем подчиненный узел п 1.1 из эталона
		Экземпляр_РасчПСВВыч = СкопироватьУзел(Экземпляр_РасчПенсВзнос, Эталон_РасчПСВВыч);
		
		// Заполняем созданные узлы даннымм
		ЗаполнитьДаннымиУзел(Экземпляр_РасчПСВВыч, СтраницаЛиста.Данные[0].Значение);
		
	КонецЦикла;
	
	Если ИтогиЛиста.Количество() > 0 Тогда
		ИтогиЛиста.Колонки.Добавить("КолонкаСвертки");
		ИтогиЛиста.Свернуть("КолонкаСвертки", ИменаКолонок);
		ЗаполнитьДаннымиУзел(Экземпляр_РасчПенсВзнос, ИтогиЛиста[0], , Истина);
		ЗаполнитьДаннымиУзел(Экземпляр_РасчДопСВВыч, ИтогиЛиста[0], , Истина);
	КонецЕсли;
	
	Если СтраницаЛиста <> Неопределено Тогда
		ЗаполнитьДаннымиУзел(ЭкземплярУзла, СтраницаЛиста.Данные[0].Значение, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелМногостраничногоРаздела_ИмущНалВычНов(Родитель, Узел)
	
	ДеревоВыгрузки = Родитель.Владелец();
	
	ДанныеРаздела = мДанныеОтчета[Узел.Раздел];
	
	ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
	
	Экземпляр_РасчИмущВыч = ПолучитьПодчиненныйЭлемент(ЭкземплярУзла, "РасчИмущВыч");
	Эталон_РасчИмущВыч = ПолучитьПодчиненныйЭлемент(Узел, "РасчИмущВыч");
	Эталон_СвОбъектРасх = ПолучитьПодчиненныйЭлемент(Эталон_РасчИмущВыч, "СвОбъектРасх");
	Экземпляр_СвОбъектРасх = ПолучитьПодчиненныйЭлемент(Экземпляр_РасчИмущВыч, "СвОбъектРасх");
	Экземпляр_СвОбъектРасх.Родитель.Строки.Удалить(Экземпляр_СвОбъектРасх);
	
	НужноЗаполнитьИтог = Ложь;
	Для Каждого СтраницаЛиста Из ДанныеРаздела Цикл
		// Проверим заполненность страницы раздела
		Если НЕ СтраницаПуста(СтраницаЛиста.Данные[0].Значение, Узел.Раздел, ДеревоВыгрузки) Тогда
			
			Экземпляр_СвОбъектРасх = СкопироватьУзел(Экземпляр_РасчИмущВыч, Эталон_СвОбъектРасх);
			ЗаполнитьДаннымиУзел(Экземпляр_СвОбъектРасх, СтраницаЛиста.Данные[0].Значение);
			НужноЗаполнитьИтог = Истина;
		КонецЕсли;
	КонецЦикла;
	Если НужноЗаполнитьИтог Тогда
		ЗаполнитьДаннымиУзел(Экземпляр_РасчИмущВыч, СтраницаЛиста.Данные[0].Значение, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзелМногостраничногоРаздела_ДохОперЦБ(Родитель, Узел)
	
	// Приложение 8 особенное - многостраничное, многострочное и имеет многострочную часть в каждой строке.
	
	ПризнакиУчетаУбытков = Отчеты.РегламентированныйОтчет3НДФЛ.ПризнакиУчетаУбытковПоЦеннымБумагам(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	ДеревоВыгрузки = Родитель.Владелец();
	
	Для Каждого СтраницаЛиста Из мДанныеОтчета[Узел.Раздел] Цикл
		
		Для Каждого СтрокаЛиста Из СтраницаЛиста.ДанныеДопСтрок[0].Значение Цикл
			
			ЭкземплярУзла = СкопироватьУзел(Родитель, Узел);
			ЗаполнитьДаннымиУзел(ЭкземплярУзла, СтрокаЛиста);
			
			Если СтрокаЛиста.П001200001050 = ПризнакиУчетаУбытков.УбытокУчитывается Тогда
				УчетУбытковЭталон = ПолучитьПодчиненныйЭлемент(ЭкземплярУзла, "УчетУбыт");
				Для Индекс = 0 По СтрокаЛиста.П001200001051.Количество() - 1 Цикл
					
					СтруктураЗаполнения = Новый Структура;
					СтруктураЗаполнения.Вставить("П001200001051", СтрокаЛиста.П001200001051[Индекс].Значение);
					СтруктураЗаполнения.Вставить("П001200001052", СтрокаЛиста.П001200001052[Индекс].Значение);
					
					ПодчиненныйУзел = СкопироватьУзел(ЭкземплярУзла, УчетУбытковЭталон);
					
					ЗаполнитьДаннымиУзел(ПодчиненныйУзел, СтруктураЗаполнения);
					
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНулямиНезаполненныеАтрибуты(Узел, Знач Исключения = Неопределено)
	
	Если Исключения = Неопределено Тогда
		Исключения = Новый Структура;
	КонецЕсли;
	
	Если (Узел.Тип = "А" ИЛИ Узел.Тип = "A") И НЕ ЗначениеЗаполнено(Узел.Значение) Тогда
		ЗначениеАтрибута = Неопределено;
		Узел.Значение = ?(Исключения.Свойство(Узел.Код, ЗначениеАтрибута), ЗначениеАтрибута, "0");
	КонецЕсли;
	
	Для Каждого Стр Из Узел.Строки Цикл
		ЗаполнитьНулямиНезаполненныеАтрибуты(Стр, Исключения);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьПустойУзелРаздела(УзелРодитель, СтруктураПустогоРаздела, ТолькоПервыйУровень = Ложь, ИндексУзла = Неопределено, УзелРаздела = Неопределено)
	
	Если ТолькоПервыйУровень Тогда
		
		Если УзелРаздела = Неопределено Тогда
			Если ИндексУзла = Неопределено Тогда
				УзелРаздела = УзелРодитель.Строки.Добавить();
			Иначе
				УзелРаздела = УзелРодитель.Строки.Вставить(ИндексУзла);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(УзелРаздела, СтруктураПустогоРаздела);
		КонецЕсли;
		
		Для Каждого Стр Из СтруктураПустогоРаздела.Строки Цикл
			Если (Стр.Тип = "А" ИЛИ Стр.Тип = "A") И УзелРаздела.Строки.Найти(Стр.Код, "Код") = Неопределено Тогда
				
				// ищем первое вхождение сложного элемента и вставляем на его место новый атрибут
				СложныйЭлемент = УзелРаздела.Строки.Найти("С", "Тип");
				Если СложныйЭлемент = Неопределено Тогда
					НовСтр = УзелРаздела.Строки.Добавить();
				Иначе
					НовСтр = УзелРаздела.Строки.Вставить(УзелРаздела.Строки.Индекс(СложныйЭлемент));
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НовСтр, Стр);
				
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Если УзелРаздела = Неопределено Тогда
			Если ИндексУзла = Неопределено Тогда
				УзелРаздела = УзелРодитель.Строки.Добавить();
			Иначе
				УзелРаздела = УзелРодитель.Строки.Вставить(ИндексУзла);
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(УзелРаздела, СтруктураПустогоРаздела);
		
		Для Каждого Стр Из СтруктураПустогоРаздела.Строки Цикл
			ЗагрузитьПустойУзелРаздела(УзелРаздела, Стр, ТолькоПервыйУровень);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат УзелРаздела;
	
КонецФункции

&НаСервере
Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (СтрНайти(Узел.Обязательность, "Н") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		ИначеЕсли (СтрНайти(Узел.Обязательность, "М") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
			И УзелПуст(Узел)
			И ?(СтрНайти(Узел.Обязательность, "О") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "O") <> 0, РегламентированнаяОтчетность.ИмеютсяАналогичныеСоседниеУзлыКлюч(Узел), Истина) Тогда
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УзелПуст(Узел)

	Для Каждого Стр из Узел.Строки Цикл
		Если Не УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если Узел.Формат = "S" Тогда
		УзелПуст = Истина;
	ИначеЕсли Узел.Формат = "N" Тогда
		УзелПуст = Не ЗначениеЗаполнено(Узел.Значение)
			Или Узел.Значение = ЗначениеПоказателяПустогоПоказателя(Узел);
	ИначеЕсли Узел.Формат = "gYear" Тогда
		УзелПуст = Узел.Значение = ЗначениеПоказателяПустогоПоказателя(Узел);
	Иначе
		УзелПуст = Не ЗначениеЗаполнено(Узел.Значение);
	КонецЕсли;
	
	Возврат УзелПуст;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеПоказателяПустогоПоказателя(Узел)
	
	Если Узел.Формат = "N" Тогда
		Если 0 < Узел.МинРазмерность И Узел.МинРазмерность <= 128 Тогда
			ЧРД = ".";
			ЧЦ  = Формат(Узел.МинРазмерность + 1, "ЧГ=0");
			ЧДЦ = Формат(Узел.МинРазмерность, "ЧГ=0");
			ШаблонФорматирования = СтрШаблон("ЧРД=%1; ЧЦ=%2; ЧДЦ=%3;", ЧРД, ЧЦ, ЧДЦ);
			ЗначениеПоказателя = "0" + Сред(Формат(1, ШаблонФорматирования), 2);
		Иначе
			ЗначениеПоказателя = "0";
		КонецЕсли;
	ИначеЕсли Узел.Формат = "gYear" Тогда
		ЗначениеПоказателя = "0001";
	Иначе
		ЗначениеПоказателя = "";
	КонецЕсли;
	
	Возврат ЗначениеПоказателя;
	
КонецФункции

&НаСервере
Функция ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, ПараметрыВыгрузки) Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, ПараметрыВыгрузки);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда 
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, ПараметрыВыгрузки);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьБланкЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Команда = ДополнительныеПараметры.Команда;
		
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1. Формируется печатная форма...'"), ЭтотОбъект.Заголовок), , , БиблиотекаКартинок.Печать);
	
	Если СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки <> Неопределено И СтруктураРеквизитовФормы.мПроверятьСоотношенияПриПечатиИВыгрузки Тогда
		РегламентированнаяОтчетностьКлиент.ПроверитьКонтрольныеСоотношения(ЭтотОбъект, ОбработкаКС);
	КонецЕсли;
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтотОбъект, ВидПечати, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
	
	// Запомним текущий раздел и страницу, чтобы к ним вернутся
	НаимТекущегоРаздела = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	НомерТекущейСтраницы = СтруктураРеквизитовФормы.НомерТекущейСтраницы;
	
	Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = НСтр("ru='Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");
		
		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ТипЗнч(мДеревоВыбранныхСтраниц) <> Тип("ДанныеФормыДерево") Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
	СтруктураРеквизитовФормы.мРежимПечати = Истина;
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела, Ложь, Ложь);
	
	Если СтруктураРеквизитовФормы.НужноРассчитатьОтчетНаСервере Тогда
		РасчетНаСервере();
	КонецЕсли; 
	
	сохрТаблДок = ТабличныйДокумент.ПолучитьОбласть();
	
	СтрПоля = "ПолеСверху, ПолеСправа, ПолеСнизу, ПолеСлева";
	ВремПоля = Новый Структура(СтрПоля);
	
	ЗаполнитьЗначенияСвойств(ВремПоля, ТабличныйДокумент, СтрПоля);
	
	ВывестиМакетВТаблДокНаСервере("Титульный", 0);
	
	ОбщиеРеквизитыРазделов = ПолучитьОбщиеРеквизитыСТитульногоЛиста();
	
	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.ПолучитьЭлементы() Цикл
		
		Если СтрокаУровня1.ВыводНаПечать = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("НаименованиеЛиста",  СтрЗаменить(СтрокаУровня1.Представление, Символы.ПС, " "));
		СтруктураПараметров.Вставить("ОриентацияЛиста",    СтрокаУровня1.ОриентацияСтраницы);
		СтруктураПараметров.Вставить("ИмяСтраницы",        СтрокаУровня1.ИмяСтраницы);
		СтруктураПараметров.Вставить("ИмяОбластиТаблПоля", СтрокаУровня1.ИмяОбластиТабличногоПоля);
		СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
		СтруктураПараметров.Вставить("НомСтр", 				1);
		СтруктураПараметров.Вставить("Количество",          1);
		
		Если НЕ мСтруктураМногостраничныхРазделов.Свойство(СтрокаУровня1.ИмяСтраницы) Тогда
			
			ВывестиМакетВТаблДокНаСервере(СтрокаУровня1.ИмяСтраницы, 0);
			ВывестиНаПечатьЛист(СтруктураПараметров, ОбщиеРеквизитыРазделов, СтрПоля);
			
		Иначе
			
			СтруктураПараметров.Вставить("ИмяСтраницыПанели", СтрокаУровня1.ИмяСтраницы);
			
			ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметров, ОбщиеРеквизитыРазделов);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Возвращаем текущий раздел и страницу
	ВывестиМакетВТаблДокНаСервере(НаимТекущегоРаздела, НомерТекущейСтраницы);
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(сохрТаблДок);
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ВремПоля, СтрПоля);
		
	РегламентированнаяОтчетностьКлиентСервер.ПроставлениеНомеровЛистов(ЭтотОбъект, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	КопироватьДанныеФормы(мДеревоВыбранныхСтраниц, мДеревоСтраницОтчета);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ВывестиНаПечатьЛист(ПараметрыПечати, ОбщиеРеквизитыРазделов, СтрПоля = Неопределено)
	
	СоответствиеРазделовМакетам =
		Отчеты.РегламентированныйОтчет3НДФЛ.СоответствиеРазделовМакетамМЧБ(СтруктураРеквизитовФормы.мВыбраннаяФорма);
	
	МЧБРаздела = СоответствиеРазделовМакетам[ПараметрыПечати.ИмяСтраницы];
	
	МакетМЧБ = ОтчетОбъект().ПолучитьМакет(МЧБРаздела);
	МакетМЧБ.ПолеСверху = 5;
	МакетМЧБ.ПолеСправа = 5;
	МакетМЧБ.ПолеСнизу = 5;
	МакетМЧБ.ПолеСлева = 5;
	
	ВремПоля = Новый Структура(СтрПоля);
	
	//Специальный алгоритм для многострочных разделов
	//Определение: является ли раздел многострочным 
	ИдГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ПараметрыПечати.ИмяСтраницы);
	
	Если ИдГруппы <> Неопределено Тогда
		
		//определение исходного количества строк на листе МЧБ
		ИсхКолвоСтрокБлока = МаксимальноеКоличествоСтрокНаСтранице(ЭтотОбъект, ПараметрыПечати.ИмяСтраницы);
		
		СохрТабличныйДокумент = Новый ТабличныйДокумент;
		СохрТабличныйДокумент.Вывести(ТабличныйДокумент);
		
		ТекКолвоСтрокБлока = ЭтотОбъект[мСтруктураМногострочныхРазделов[ИдГруппы]].Количество();
		Если ТекКолвоСтрокБлока > ИсхКолвоСтрокБлока И ИсхКолвоСтрокБлока > 0 Тогда
			//Добавляли строки - значит будут доп. листы МЧБ
			//Определение общего количества листов МЧБ
			КолвоЛистовМЧБ = Цел(ТекКолвоСтрокБлока / ИсхКолвоСтрокБлока);
			КолвоСтрокНаПоследнемЛисте = ТекКолвоСтрокБлока % ИсхКолвоСтрокБлока;
			Если КолвоСтрокНаПоследнемЛисте <> 0 Тогда
				КолвоЛистовМЧБ = КолвоЛистовМЧБ + 1;	
			КонецЕсли;
			
			//заполнение строки имен показателей (для проверки перед переименованием)
			СтрИменаПоказателей = "";
			
			Колонки = ДанныеФормыВЗначение(ЭтотОбъект[мСтруктураМногострочныхРазделов[ИдГруппы]], Тип("ТаблицаЗначений")).Колонки;
			
			Для каждого КолонкаСтруктуры Из Колонки Цикл
				СтрИменаПоказателей = СтрИменаПоказателей + КолонкаСтруктуры.Имя;
			КонецЦикла;
			
			//Заполнение списка листов МЧБ
			СписокЛистовМЧБ = Новый СписокЗначений;
			Для Сч = 1 По КолвоЛистовМЧБ Цикл
				//переименование (начиная со второго листа)
				Если Сч > 1 Тогда
					СписокОчищаемыхОбластей = Новый Массив;
					СписокОбластейПереименования = Новый СписокЗначений;
					Для каждого ТекущаяОбласть Из МакетМЧБ.Области Цикл
						ИмяЯчейки = ТекущаяОбласть.Имя;
						
						Если СтрДлина(ИмяЯчейки) < 13 ИЛИ СтрНайти(СтрИменаПоказателей, Лев(ИмяЯчейки, 13)) = 0 Тогда
							//проверка на вхождение в данную многострочную часть
							Продолжить;
						КонецЕсли;
						
						//определение и обрасывание НомерЯчейки
						ПоказательИНомерСтрокиСРазделителем = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСмысловуюЧастьИмениЯчейки(ИмяЯчейки, Истина); // ВМЕСТЕ с разделителем
						ПоказательИНомерСтроки = Лев(ПоказательИНомерСтрокиСРазделителем,СтрДлина(ПоказательИНомерСтрокиСРазделителем) - 1);
						НомерЯчейки = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСмысловуюЧастьИмениЯчейки(ИмяЯчейки, Ложь);
						//определение СмысловаяЧасть и ТекущийСчетчик
						СмысловаяЧастьСРазделителем = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСмысловуюЧастьИмениЯчейки(ПоказательИНомерСтроки, Истина); // ВМЕСТЕ с разделителем
						СмысловаяЧасть = Лев(СмысловаяЧастьСРазделителем,СтрДлина(СмысловаяЧастьСРазделителем) - 1);
						ТекущийСчетчик = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСмысловуюЧастьИмениЯчейки(ПоказательИНомерСтроки, Ложь);
						
						СмысловаяЧастьСРазделителем = ?(ЗначениеЗаполнено(СмысловаяЧастьСРазделителем),СмысловаяЧастьСРазделителем, ПоказательИНомерСтрокиСРазделителем);
						СмысловаяЧасть = ?(ЗначениеЗаполнено(СмысловаяЧасть),СмысловаяЧасть, ПоказательИНомерСтроки);
						ТекущийСчетчик = ?(ЗначениеЗаполнено(ТекущийСчетчик),ТекущийСчетчик, НомерЯчейки);
						
						//Окончательная проверка на вхождение в данную многострочную часть
						Если СтрНайти(СтрИменаПоказателей,СмысловаяЧасть) = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						НовыйСчетчик = Число(ТекущийСчетчик) + ИсхКолвоСтрокБлока;
						Если НовыйСчетчик > ТекКолвоСтрокБлока Тогда
							//очищение происходит после этого цикла
							СписокОчищаемыхОбластей.Добавить(ТекущаяОбласть);
							Продолжить;
						КонецЕсли;
						
						//Подготовка к переименованию
						//заполнение списка: значение - область, представление - будущее имя
						СписокОбластейПереименования.Добавить(ТекущаяОбласть, СмысловаяЧасть + "_" + НовыйСчетчик + "_" + НомерЯчейки);
					КонецЦикла;
					
					//Очищение
					Для каждого ОбластьСписка Из СписокОчищаемыхОбластей Цикл
						ОбластьСписка.Имя = "";
						ОбластьСписка.Текст = "-";
					КонецЦикла;
					
					//Переименование
					Для каждого ЭлСпискаОбластей Из СписокОбластейПереименования Цикл
						ЭлСпискаОбластей.Значение.Имя = ЭлСпискаОбластей.Представление;
					КонецЦикла;
					
				КонецЕсли; 
				//переименование
				//
				СписокЛистовМЧБ.Добавить(МакетМЧБ.ПолучитьОбласть());
			КонецЦикла;
			
			Для каждого элЛистМЧБ Из СписокЛистовМЧБ Цикл
				
				ТабличныйДокумент.Очистить();
				ТабличныйДокумент.Вывести(СохрТабличныйДокумент);
				
				ЛистМЧБ = элЛистМЧБ.Значение;
				
				ЗаполнитьМакетМЧБ(ПараметрыПечати, ЛистМЧБ, ОбщиеРеквизитыРазделов);
				
				Если СписокЛистовМЧБ.Индекс(элЛистМЧБ)<СписокЛистовМЧБ.Количество() - 1 Тогда
					//Выводится не последний лист многострочной серии листов МЧБ
					//Необходимо обнулить итоговые показатели
					ОбнулитьИтоговыеПоказателиНаЛистеМЧБ(ЛистМЧБ);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ВремПоля, ТабличныйДокумент, СтрПоля);
				ТабличныйДокумент.Очистить();
				ТабличныйДокумент.Вывести(ЛистМЧБ);
				ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ЛистМЧБ, СтрПоля, "Макет");
				
				РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая(ЭтотОбъект.Заголовок, ПараметрыПечати, ТабличныйДокумент, СтруктураРеквизитовФормы.мПечатныеФормы);
				
				ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ВремПоля, СтрПоля);
			КонецЦикла;
			Возврат;
			
			
			//Не добавляли строки - продолжение выполнения
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьМакетМЧБ(ПараметрыПечати, МакетМЧБ, ОбщиеРеквизитыРазделов);
	
	ЗаполнитьЗначенияСвойств(ВремПоля, ТабличныйДокумент, СтрПоля);
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(МакетМЧБ);
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, МакетМЧБ, СтрПоля, "Макет");
	
	РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая(ЭтотОбъект.Заголовок, ПараметрыПечати, ТабличныйДокумент, СтруктураРеквизитовФормы.мПечатныеФормы);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметровСтраницы, ОбщиеРеквизитыРазделов)
	Перем ИмяСтраницы;
	Перем ТаблицаСтраницРаздела;
	
	СтруктураПараметровСтраницы.Свойство("ИмяСтраницыПанели", ИмяСтраницы);
	
	Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницы, ТаблицаСтраницРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСтраницРаздела = ЭтотОбъект[ТаблицаСтраницРаздела];
	СтруктураПараметровСтраницы.Вставить("Количество", ТаблицаСтраницРаздела.Количество());
	Для Каждого Страница Из ТаблицаСтраницРаздела Цикл
		// выводим на печать каждую страницу многостраничного раздела
		ТекущийИндекс = ТаблицаСтраницРаздела.Индекс(Страница);
		СохрАктивнаяСтраница = ТаблицаСтраницРаздела[ТекущийИндекс].АктивнаяСтраница;
		ВывестиМакетВТаблДокНаСервере(ИмяСтраницы, ТекущийИндекс + 1);
		ТаблицаСтраницРаздела[ТекущийИндекс].АктивнаяСтраница = СохрАктивнаяСтраница;
		СтруктураПараметровСтраницы.Вставить("НомСтр", ТекущийИндекс + 1);
		ВывестиНаПечатьЛист(СтруктураПараметровСтраницы, ОбщиеРеквизитыРазделов);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМакетМЧБ(Параметры, МакетМЧБ, ОбщиеРеквизитыРазделов)
	
	Если МакетМЧБ.Области.Найти("ИНН") <> Неопределено Тогда
		МакетМЧБ.Области.ИНН.Текст = ОбщиеРеквизитыРазделов.СтрИНН;
	КонецЕсли;
	
	Если МакетМЧБ.Области.Найти("Фамилия") <> Неопределено Тогда
		МакетМЧБ.Области.Фамилия.Текст = ОбщиеРеквизитыРазделов.СтрФамилия;
	КонецЕсли;
	Если МакетМЧБ.Области.Найти("ИнициалИмя") <> Неопределено Тогда
		МакетМЧБ.Области.ИнициалИмя.Текст = Лев(СокрЛП(ОбщиеРеквизитыРазделов.СтрИмя),1);
	КонецЕсли; 
	Если МакетМЧБ.Области.Найти("ИнициалОтчество") <> Неопределено Тогда
		МакетМЧБ.Области.ИнициалОтчество.Текст = Лев(СокрЛП(ОбщиеРеквизитыРазделов.СтрОтчество),1);
	КонецЕсли; 
	
	Если МакетМЧБ.Области.Найти("ДатаПодписи1") <> Неопределено Тогда
		Для Счетчик = 1 По 8 Цикл
			МакетМЧБ.Области["ДатаПодписи" + Счетчик].Текст = ОбщиеРеквизитыРазделов.ДатаПодписи[Счетчик - 1];
		КонецЦикла; 
	КонецЕсли;
	
	// Аккуратно переносим значения из табличного документа в макет МЧБ
	ИсходныйМакет = ТабличныйДокумент;
	
	Для Каждого Область Из ИсходныйМакет.Области Цикл
		
		Если Область.ТипОбласти <> ТипОбластиЯчеекТабличногоДокумента.Прямоугольник ИЛИ НЕ Область.СодержитЗначение Тогда
			Продолжить;
		КонецЕсли; 
		
		ИмяОбластиМЧБ = Область.Имя; // По умолчанию имена областей в МЧБ и в макете заполнения совпадают
		
		// Сначала обработаем нестандартные ситуации определеня значений
		Если Параметры.ИмяСтраницы = "Титульный" И Область.Имя = "МестоРождения" Тогда 
			
			ОбластьНазначения = МакетМЧБ.Области.Найти("МестоРождения");
			Если ОбластьНазначения <> Неопределено Тогда
				ОбластьНазначения.Текст = ОбщиеРеквизитыРазделов.МестоРождения.МестоРождения;
			КонецЕсли; 
			
			ОбластьНазначения = МакетМЧБ.Области.Найти("МестоРождения2");
			Если ОбластьНазначения <> Неопределено Тогда
				ОбластьНазначения.Текст = ОбщиеРеквизитыРазделов.МестоРождения.МестоРождения2;
			КонецЕсли; 
			
			Продолжить;
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение1" И Лев(Область.Имя, 13) = "П000300001060" Тогда
			
			// Haимeнование источника выплаты дохода
			ВывестиМногострочныйТекстОбластиВМакетМЧБ(Область, МакетМЧБ);
			Продолжить;
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение2" И Область.Имя = "П000400001020" Тогда
			
			// Haимeнование источника выплаты дохода
			ВывестиМногострочныйТекстОбластиВМакетМЧБ(Область, МакетМЧБ);
			Продолжить;
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение3" И Лев(Область.Имя, 13) = "П000500001080" Тогда
			
			ЗначениеОбласти = ?(Область.Значение = 0, "", Область.Значение);
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение7" И Лев(Область.Имя, 15) = "П000700001070_1" Тогда
			
			Если НЕ ЗначениеЗаполнено(ИсходныйМакет.Области.П000700001070_1.Значение) И НЕ ЗначениеЗаполнено(ИсходныйМакет.Области.П000700001070_3.Значение)  Тогда
				ЗначениеОбласти = "";
			Иначе
				ЗначениеОбласти = СокрЛП(ИсходныйМакет.Области.П000700001070_1.Значение) + "/" + СокрЛП(ИсходныйМакет.Области.П000700001070_3.Значение);
			КонецЕсли;
			ИмяОбластиМЧБ = "П000700001070";
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение7" И (Область.Имя = "П000700001031" Или Область.Имя = "П000700001032") Тогда
			
			// Номер объекта или Сведения о местонахождении объекта
			ВывестиМногострочныйТекстОбластиВМакетМЧБ(Область, МакетМЧБ);
			Продолжить;
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение1Расчет" И Лев(Область.Имя, 13) = "П001900001010" Тогда
			
			// Кадастровый номер отчужденного объекта недвижимого имущества
			ВывестиМногострочныйТекстОбластиВМакетМЧБ(Область, МакетМЧБ);
			Продолжить;
			
		ИначеЕсли Параметры.ИмяСтраницы = "Приложение5Расчет" И Лев(Область.Имя, 13) = "П001000001030" Тогда
			
			// Haимeнование негосударственного пенсионного фонда или страховой организации
			ВывестиМногострочныйТекстОбластиВМакетМЧБ(Область, МакетМЧБ);
			Продолжить;
			
		Иначе
			ЗначениеОбласти = Область.ТипЗначения.ПривестиЗначение(Область.Значение);
		КонецЕсли; 
		
		ТипЗначенияОбласти = ТипЗнч(ЗначениеОбласти);
		
		ОбластьНазначения = МакетМЧБ.Области.Найти(ИмяОбластиМЧБ);
		Если ОбластьНазначения <> Неопределено Тогда
			
			Если ТипЗначенияОбласти = Тип("Число") Тогда
				
				Если Параметры.ИмяСтраницы = "Приложение1Расчет" 
					И (Лев(Область.Имя, 13) = "П001900001020" Или Лев(Область.Имя, 13) = "П001900001040") Тогда
					ПредставлениеНуля = "ЧН=-";
				Иначе
					ПредставлениеНуля = "ЧН=0" + 
						?(Область.ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти = 0, 
							"", 
							Лев(".00000000", Область.ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти + 1));
				КонецЕсли;
				
				// выводим число с учетом разрядности
				СтрокаФормата = "ЧЦ=" +  Область.ТипЗначения.КвалификаторыЧисла.Разрядность + 
								"; ЧДЦ=" + Область.ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти + 
								"; ЧГ=; ЧРД=.; " + ПредставлениеНуля;
				ОбластьНазначения.Текст = Формат(ЗначениеОбласти, СтрокаФормата); 
				
			ИначеЕсли ТипЗначенияОбласти = Тип("Дата") Тогда
				
				ОбластьНазначения.Текст = Формат(ЗначениеОбласти, "ДФ=дд.ММ.гггг"); 
				
			Иначе
				
				ОбластьНазначения.Текст = ЗначениеОбласти; 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМногострочныйТекстОбластиВМакетМЧБ(Область, МакетМЧБ, МаксимальноеЧислоСтрокВМакете = 5)
	
	// В исходном макете значение в одной ячейке, но в макете назначения строка разбита на несколько
	
	ЗначениеОбласти = СокрЛП(Область.Значение);
	Для НомерСтроки = 1 По Мин(СтрЧислоСтрок(ЗначениеОбласти), МаксимальноеЧислоСтрокВМакете) Цикл
		ОбластьНазначения = МакетМЧБ.Области.Найти(СтрШаблон(Область.Имя + "_%1", НомерСтроки));
		Если ОбластьНазначения <> Неопределено Тогда
			ОбластьНазначения.Текст = СтрПолучитьСтроку(ЗначениеОбласти, НомерСтроки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОбщиеРеквизитыСТитульногоЛиста()
	
	МассивИНН         = Новый Массив(12);
	МассивКПП         = Новый Массив(9);
	МассивДатаПодписи = Новый Массив(8);
	МассивФамилия     = Новый Массив(32);
	МассивИмя         = Новый Массив(32);
	МассивОтчество    = Новый Массив(32);
	
	ИсходныйМакет = ТабличныйДокумент;
	
	Область = ИсходныйМакет.Области.Найти("ИНН");
	СтрИНН = "";
	Если НЕ Область = Неопределено Тогда
		
		СтрИНН = Область.Значение;
		
		Если СтрДлина(СтрИНН) = 10 Тогда
			СтрИНН = СтрИНН + "--";
		КонецЕсли;
		
		Для Ном = 1 По СтрДлина(СтрИНН) Цикл
			
			МассивИНН[Ном - 1] = Сред(СтрИНН, Ном, 1);
			
		КонецЦикла;
		
	КонецЕсли;
		
	Область = ИсходныйМакет.Области.Найти("ДатаПодписи");
	
	Если НЕ Область = Неопределено Тогда
		
		Если ЗначениеЗаполнено(Область.Значение) Тогда
			СтрДатаПодписи = Лев(СтрЗаменить(Область.Значение, ".", ""), 8);
		Иначе
			// Если дата подписи не заполнена подставим пустую строку соответствующей длины
			СтрДатаПодписи = "        ";
		КонецЕсли;
		
		Для Ном = 1 По СтрДлина(СтрДатаПодписи) Цикл
			
			МассивДатаПодписи[Ном - 1] = Сред(СтрДатаПодписи, Ном, 1);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Область = ИсходныйМакет.Области.Найти("Фамилия");
	СтрФамилия = "";
	Если НЕ Область = Неопределено Тогда
		СтрФамилия = Область.Значение;
		ДлинаФамилии = СтрДлина(СтрФамилия);
		
		Для Ном = 1 По Мин(ДлинаФамилии, МассивФамилия.Количество()) Цикл
			
			МассивФамилия[Ном - 1] = Сред(СтрФамилия, Ном, 1);
			
		КонецЦикла;
		
		Для Ном = Макс(ДлинаФамилии, 1) По МассивФамилия.Количество() Цикл
			
			МассивФамилия[Ном - 1] = "-";
			
		КонецЦикла;
		
	КонецЕсли;

	Область = ИсходныйМакет.Области.Найти("Имя");
	СтрИмя = "";
	Если НЕ Область = Неопределено Тогда
		СтрИмя = Область.Значение;
		ДлинаИмени = СтрДлина(СтрИмя);
		Для Ном = 1 По Мин(ДлинаИмени, МассивИмя.Количество()) Цикл
			
			МассивИмя[Ном - 1] = Сред(СтрИмя, Ном, 1);
			
		КонецЦикла;
		
		Для Ном = Макс(ДлинаИмени, 1) По МассивИмя.Количество() Цикл
			
			МассивИмя[Ном - 1] = "-";
			
		КонецЦикла;
		
	КонецЕсли;
	
	Область = ИсходныйМакет.Области.Найти("Отчество");
	СтрОтчество = "";
	Если НЕ Область = Неопределено Тогда
		СтрОтчество = Область.Значение;
		ДлинаОтчества = СтрДлина(СтрОтчество);
		Для Ном = 1 По Мин(ДлинаОтчества, МассивОтчество.Количество()) Цикл
			
			МассивОтчество[Ном - 1] = Сред(СтрОтчество, Ном, 1);
			
		КонецЦикла;
		
		Для Ном = Макс(ДлинаОтчества, 1) По МассивОтчество.Количество() Цикл
			
			МассивОтчество[Ном - 1] = "-";
			
		КонецЦикла;
		
	КонецЕсли;
	
	//Место рождения
	Область = ИсходныйМакет.Области.Найти("МестоРождения");
	МестоРожденияПоСловам = СтрРазделить(Область.Значение, " ", Ложь);
	
	ДлинаВСимволах = 0;
	МестоРожденияВ21Строки = Новый Структура("МестоРождения, МестоРождения2", "", "");
	Для Каждого Слово Из МестоРожденияПоСловам Цикл
		
		ДлинаВСимволах = ДлинаВСимволах + СтрДлина(Слово);
		
		Если ДлинаВСимволах > 18 Тогда
			
			МестоРожденияВ21Строки.МестоРождения2 = МестоРожденияВ21Строки.МестоРождения2+ Слово+ " ";
			
		Иначе
			
			МестоРожденияВ21Строки.МестоРождения = МестоРожденияВ21Строки.МестоРождения + Слово + " ";
			
		КонецЕсли;
		
	КонецЦикла;

	МестоРожденияВ21Строки.МестоРождения = СокрЛП(МестоРожденияВ21Строки.МестоРождения);
	МестоРожденияВ21Строки.МестоРождения2 = СокрЛП(МестоРожденияВ21Строки.МестоРождения2);
	
	ОбщиеРеквизитыРазделов = Новый Структура;
	
	ОбщиеРеквизитыРазделов.Вставить("ИНН", МассивИНН);
	ОбщиеРеквизитыРазделов.Вставить("КПП", МассивКПП);
	ОбщиеРеквизитыРазделов.Вставить("ДатаПодписи", МассивДатаПодписи);
	ОбщиеРеквизитыРазделов.Вставить("Фамилия", МассивФамилия);
	ОбщиеРеквизитыРазделов.Вставить("Имя", МассивИмя);
	ОбщиеРеквизитыРазделов.Вставить("Отчество", МассивОтчество);
	ОбщиеРеквизитыРазделов.Вставить("СтрИНН", СтрИНН);
	
	ОбщиеРеквизитыРазделов.Вставить("СтрФамилия", СтрФамилия);
	ОбщиеРеквизитыРазделов.Вставить("СтрИмя", СтрИмя);
	ОбщиеРеквизитыРазделов.Вставить("СтрОтчество", СтрОтчество);
	ОбщиеРеквизитыРазделов.Вставить("МестоРождения", МестоРожденияВ21Строки);
	Возврат ОбщиеРеквизитыРазделов;
	
КонецФункции

&НаСервере
Процедура ОбнулитьИтоговыеПоказателиНаЛистеМЧБ(ТаблДок)
	
	ОбластьИтоговыеПоказатели = ТаблДок.Области.Найти("ИтоговыеПоказатели");
	Если ОбластьИтоговыеПоказатели <> Неопределено Тогда
		
		ОбластьИтоговыеПоказателиВерх = ОбластьИтоговыеПоказатели.Верх;
		ОбластьИтоговыеПоказателиНиз = ОбластьИтоговыеПоказатели.Низ;
		
		Для Каждого Обл Из ТаблДок.Области Цикл
			Если Обл.Верх >= ОбластьИтоговыеПоказателиВерх
				И Обл.Низ <= ОбластьИтоговыеПоказателиНиз
				И Обл.ГраницаСлева.ТипЛинии = ТипЛинииЯчейкиТабличногоДокумента.Точечная
				Тогда
				Обл.Текст = "-";
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ЗаполнитьАвтоНаСервере();
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.ПодробноеПредставлениеОшибки;
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияФоновогоЗаданияАвтозаполнения", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияФоновогоЗаданияАвтозаполнения(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Задание отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ЗагрузитьПодготовленныеДанные();
		ОбновитьДеревоРазделовПослеавтоЗаполнения();
	Иначе
		ВызватьИсключение ДлительнаяОперация.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДеревоРазделовПослеавтоЗаполнения()

	СтруктураРеквизитовФормы.ВыводитьСтраницуВТаблДокНаСервере = Ложь;
	
	РегламентированнаяОтчетностьКлиент.ОбновитьДеревоРазделовВФормеОтчета(ЭтотОбъект);
	
	Если Элементы.РазделыОтчета.ТекущиеДанные <> Неопределено И Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя() <> Неопределено Тогда
		Элементы.РазделыОтчета.ТекущаяСтрока = Элементы.РазделыОтчета.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы()[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьАвтоНаСервере()
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела, Ложь);
	
	ЗаполнитьПустыеСтруктурыДанных();
	
	СтруктураРеквизитовФормы.АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыОтчета.Вставить("мДатаНачалаПериодаОтчета", СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыОтчета.Вставить("мДатаКонцаПериодаОтчета", СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыОтчета.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыОтчета.Вставить("ИНН", СтруктураДанныхТитульный.ИНН);
	ПараметрыОтчета.Вставить("АдресВоВременномХранилище", СтруктураРеквизитовФормы.АдресВоВременномХранилище);
	ПараметрыОтчета.Вставить("АдресВременногоХранилищаРасшифровки");
	
	Контейнер = Новый Структура();
	
	// Из базы данных берем данные о доходах и расходах предпринимателя
	Контейнер.Вставить("Приложение3", СведенияМногостраничногоРазделаВТаблице("ТаблицаСтраницыПриложение3"));
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ИДОтчета", ИмяОтчета(ЭтотОбъект.ИмяФормы));
	ПараметрыЗаполнения.Вставить("ИДРедакцииОтчета", ИмяФормыОтчета(ЭтотОбъект.ИмяФормы));
	ПараметрыЗаполнения.Вставить("ПараметрыОтчета", ПараметрыОтчета);
	ПараметрыЗаполнения.Вставить("Контейнер", Контейнер);
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Автоматическое заполнение по данным информационной базы'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"РегламентированнаяОтчетность.ЗаполнитьОтчетВФоне",
		ПараметрыЗаполнения,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Функция СведенияМногостраничногоРазделаВТаблице(ИмяСтруктурыДанных)
	
	ОписаниеТиповНеопределено = Новый ОписаниеТипов("Неопределено");
	
	ТаблицаЗначенийСДанными = ДанныеФормыВЗначение(ЭтотОбъект[ИмяСтруктурыДанных], Тип("ТаблицаЗначений"));
	
	Если ТаблицаЗначенийСДанными <> Неопределено Тогда
		
		Если ТаблицаЗначенийСДанными.Колонки.Найти("Данные") <> Неопределено Тогда
			ТаблицаЗначенийСДанными.Колонки.Удалить("Данные");
			ТаблицаЗначенийСДанными.Колонки.Добавить("Данные", ОписаниеТиповНеопределено);
			
			Ном = 0;
			Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗначенийСДанными Цикл
				СтрокаТаблицыЗначений.Данные = ЭтотОбъект[ИмяСтруктурыДанных][Ном].Данные;
				Ном = Ном + 1;
			КонецЦикла;
		КонецЕсли;
		
		Если ТаблицаЗначенийСДанными.Колонки.Найти("ДанныеДопСтрок") <> Неопределено Тогда
			ТаблицаЗначенийСДанными.Колонки.Удалить("ДанныеДопСтрок");
			ТаблицаЗначенийСДанными.Колонки.Добавить("ДанныеДопСтрок", ОписаниеТиповНеопределено);
			
			Ном = 0;
			Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗначенийСДанными Цикл
				
				Если ЭтотОбъект[ИмяСтруктурыДанных][Ном].ДанныеДопСтрок.Количество() > 0
				   И ЭтотОбъект[ИмяСтруктурыДанных][Ном].ДанныеДопСтрок[0].Представление = "ДанныеФормыКоллекцияТаблицаДопСтрокРаздела" Тогда
					СтрокаТаблицыЗначений.ДанныеДопСтрок = ДанныеФормыВЗначение(ЭтотОбъект[ИмяСтруктурыДанных][Ном].ДанныеДопСтрок[0].Значение, Тип("ТаблицаЗначений"));
					
					Ном = Ном + 1;
					Продолжить;
				КонецЕсли;
				
				СтруктураДанныхДопСтрок = Новый Структура;
				
				Для Каждого ЭлСтруктуры Из ЭтотОбъект[ИмяСтруктурыДанных][Ном].ДанныеДопСтрок Цикл
					СтруктураДанныхДопСтрок.Вставить(ДанныеФормыВЗначение(ЭлСтруктуры.Значение, Тип("ТаблицаЗначений")));
				КонецЦикла;
				
				СтрокаТаблицыЗначений.ДанныеДопСтрок = СтруктураДанныхДопСтрок;
				
				Ном = Ном + 1;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаЗначенийСДанными;
	
КонецФункции

&НаСервере
Процедура ВосстановитьМногостраничныйРазделИзТаблицы(ИмяРаздела, ТаблицаСДанными)
	
	ИмяРеквизитаФормы = Неопределено;
	Если НЕ мСтруктураМногостраничныхРазделов.Свойство(ИмяРаздела, ИмяРеквизитаФормы) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитФормы = ЭтотОбъект[ИмяРеквизитаФормы];
	ЗначениеВДанныеФормы(ТаблицаСДанными, РеквизитФормы);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные()
	
	Контейнер = ПолучитьИзВременногоХранилища(СтруктураРеквизитовФормы.АдресВоВременномХранилище);
	
	Если ТипЗнч(Контейнер) = Тип("Структура") Тогда
		Для Каждого ЭлементСтруктуры Из Контейнер Цикл
			ИмяЭлемента = ЭлементСтруктуры.Ключ;
			
			Если мСтруктураМногостраничныхРазделов.Свойство(ИмяЭлемента) Тогда
				ВосстановитьМногостраничныйРазделИзТаблицы(ИмяЭлемента, ЭлементСтруктуры.Значение);
			Иначе
				ЭтотОбъект["СтруктураДанных" + ИмяЭлемента] = ЭлементСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РасчетНаСервере();
	
	ПодготовитьДанныеРаздела2(ЭтотОбъект);
	
	СобратьДанныеВРаздел1Сервер();
	
	// Обновляем дерево разделов
	СформироватьДеревоРазделовОтчета(ЭтотОбъект);
	
	ВывестиМакетВТаблДокНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела, 1);
	
	// Активизируем строку раздела в дереве
	Для Каждого СтрокаРаздела Из РазделыОтчета.ПолучитьЭлементы() Цикл
		Если СтрокаРаздела.КолонкаРазделыОтчетаСокрНаим = СтруктураРеквизитовФормы.НаимТекущегоРаздела Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = СтрокаРаздела.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Родитель)
	
	Для Каждого Стр из Родитель.Строки Цикл
		Если ЗначениеЗаполнено(Стр.Ключ) Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект, КонтролирующийОрган());
	
КонецПроцедуры

#КонецОбласти

#Область РасширенныйНалоговыйПериод

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьПоясненияКПериодуОтчета(Форма)
	
	Элементы = Форма.Элементы;
	СтруктураРеквизитовФормы = Форма.СтруктураРеквизитовФормы;
	
	Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация)
		И ЗначениеЗаполнено(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета) Тогда
		ТекстПояснения = ТекстПоясненияРасширенныйНалоговыйПериод(
			СтруктураРеквизитовФормы.Организация,
			СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПояснения) Тогда
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = Истина;
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = ТекстПояснения;
	Иначе
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстПоясненияРасширенныйНалоговыйПериод(Знач Организация, Знач КонецПериода)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.ТекстПоясненияРасширенныйНалоговыйПериод(Организация, КонецПериода);
	
КонецФункции

#КонецОбласти

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиентеНаСервереБезКонтекста
Функция КонтролирующийОрган()
	
	Возврат "ФНС";
	
КонецФункции

#КонецОбласти

#КонецОбласти