// Область "Настройки" описывает параметры инициализации механизма загрузки
// регламентированных отчетов из файлов выгрузки. Формат области настроек - JSON.
//
// Описание параметров:
//  * Установкой параметра "РазрешитьЗагрузку" в "true" или "false" регулируем видимость
//    кнопки "Загрузить" и возможность загрузки из файла в форме отчета.
//  * Параметр "ФорматыСхемВыгрузки" описывает соответствие имен макетов схем выгрузки
//    поддерживаемым форматам загрузки. Если у формы единственная схема выгрузки или
//    нужно всегда использовать схему по умолчанию - указываем значение "null".
//  * Параметр "ОбластиОбработчиков" содержит список имен используемых обработчиков
//    загружаемых данных. Имена соответствуют областям макета, в которых можно размещать
//    произвольные алгоритмы на языке "1С:Предприятие" для обработки доступных объектов.
//  * Параметр "ИмяФайлаНастроек" содержит путь к файлу настроек для быстрого описания
//    правил загрузки без сохранения конфигурации. После отладки правил загрузки нужно
//    обязательно перенести содержимое файла в макет и удалить параметр или присвоить ему
//    значение "null". В целях безопасности нельзя выпускать конфигурации с заполненным
//    параметром "ИмяФайлаНастроек".
//
// Примечание:
//  Сопоставление имен узлов из файла XML и имен показателей отчета выполняется по данным схемы выгрузки,
//  поэтому обработчики используются для получения значений показателей не описанных в схеме выгрузки.
//
#Область Настройки
{
	"РазрешитьЗагрузку"   : true,
	
	"ФорматыСхемВыгрузки" : null,
	"ОбластиОбработчиков" : {
		"ПередЗагрузкой"  : "ПредОбработкаСхемыЗагрузки",
		"ПослеЗагрузки"   : "ПостОбработкаДокументаОтчета",
	}
}
#КонецОбласти

// Доступны параметры:
//
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПредОбработкаСхемыЗагрузки
	
	ДобавитьКолонкуВДеревоЕслиНеНайдена(П.ДеревоДляЗагрузки, "МнУр"); // для определения многоуровневых разделов
	
	НайденныеУзлы = УзлыПоXPath(П.ДеревоДляЗагрузки, "//Документ/РазрПолВБР");
	Для Каждого НайденныйУзел Из НайденныеУзлы Цикл
		
		НайденныйУзел.МнУр = Истина;
		
		НайденныеУзлыИерархия = УзлыПоXPath(НайденныйУзел, "//node()"); // вся иерархия подчиненных узлов любого типа
		Для Каждого ТекущийУзел Из НайденныеУзлыИерархия Цикл
			ТекущийУзел.МнУр = Истина;
		КонецЦикла;
		
	КонецЦикла;
	
#КонецОбласти

// Доступны параметры:
//
//  П.ДанныеОтчета      - заполненные данные документа регламентированного отчета;
//  П.ДеревоДляЗагрузки - выбранное дерево схемы загрузки, не заполненное данными;
//  П.ПараметрыОтчета   - параметры, сформированные в форме отчета, используются для выгрузки.
//
#Область ПостОбработкаДокументаОтчета
	
	Титульный = Неопределено;
	
	П.ДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаТитульный", Титульный);
	Если Титульный <> Неопределено Тогда
		
		Титульный.НомерДокумента = П.ПараметрыОтчета.НомПор;
		Титульный.ПоМесту        = П.ПараметрыОтчета.ПоМесту;
		
	КонецЕсли;
	
	Раздел2 = Неопределено;
	
	П.ДанныеОтчета.ДанныеМногоуровневыхРазделов.Свойство("Раздел2", Раздел2);
	Если Раздел2 <> Неопределено Тогда
	
		Для Каждого СтраницаПервогоУровня Из Раздел2.Строки Цикл
			Для Каждого ЭлементМногострочнойЧасти Из СтраницаПервогоУровня.ДанныеМногострочныхЧастей Цикл
				
				Для Каждого СтрокаМногострочнойЧасти Из ЭлементМногострочнойЧасти.Значение.Строки Цикл
					ЗначениеПоказателяСум = Неопределено;
					ЗначениеПоказателяКол = Неопределено;
					Если СтрокаМногострочнойЧасти.Данные.Свойство("П00002М110004", ЗначениеПоказателяСум)
					   И СтрокаМногострочнойЧасти.Данные.Свойство("П00002М109003", ЗначениеПоказателяКол)
					   И ТипЗнч(ЗначениеПоказателяКол) = Тип("Число") И ЗначениеПоказателяКол <> 0 Тогда
						СтрокаМногострочнойЧасти.Данные.Вставить("П00002М108002", Окр((0 + ЗначениеПоказателяСум) / ЗначениеПоказателяКол, 3));
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
#КонецОбласти