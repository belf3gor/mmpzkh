
#Область СлужебныеПроцедурыИФункции

// Процедура инициализирует переменные контекста.
&НаСервере
Процедура ИнициализироватьПеременныеКонтекста(ПеременныеКонтекста)
	
	ШаблонГруппыВарианта = Новый Структура(
		"Вид, РастягиватьПоГоризонтали,
		|Отображение, Группировка, 
		|ОтображатьЗаголовок"
	);
	ШаблонГруппыВарианта.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ШаблонГруппыВарианта.РастягиватьПоГоризонтали = Истина;
	ШаблонГруппыВарианта.Отображение = ОтображениеОбычнойГруппы.Нет;
	ШаблонГруппыВарианта.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ШаблонГруппыВарианта.ОтображатьЗаголовок = Ложь;
	
	// Шаблоны заполнения создаваемых элементов управления
	ШаблонНадписиВарианта = Новый Структура(
		"Вид, Гиперссылка, Высота, ЦветТекста,
		|РастягиватьПоГоризонтали, РастягиватьПоВертикали"
	);
	ШаблонНадписиВарианта.Вид = ВидДекорацииФормы.Надпись;
	ШаблонНадписиВарианта.Гиперссылка = Истина;
	ШаблонНадписиВарианта.РастягиватьПоГоризонтали = Истина;
	ШаблонНадписиВарианта.РастягиватьПоВертикали = Ложь;
	ШаблонНадписиВарианта.Высота = 1;
	ШаблонНадписиВарианта.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
	ПеременныеКонтекста.Вставить("Шаблоны", Новый Структура);
	ПеременныеКонтекста.Шаблоны.Вставить("ГруппаВарианта", ШаблонГруппыВарианта);
	ПеременныеКонтекста.Шаблоны.Вставить("НадписьВарианта", ШаблонНадписиВарианта);
	
КонецПроцедуры

// Процедура выводит список форм отчета на форму.
&НаСервере
Процедура ВывестиВариантыБезГруппы(СписокФормОтчета, ПеременныеКонтекста)
	
	КоличествоФорм = СписокФормОтчета.Количество();
	
	Если КоличествоФорм = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаУровня2 = Элементы.Найти("БезГруппы");
	
	КоличествоКолонок = ГруппаУровня2.ПодчиненныеЭлементы.Количество();;
	ГруппаУровня3Отсечка = Цел(КоличествоФорм / КоличествоКолонок); // Количество форм для вывода в одной колонке
	ГруппаУровня3Добавка = КоличествоФорм - ГруппаУровня3Отсечка * КоличествоКолонок; // Остаток распределения вариантов по колонкам
	
	ГруппаУровня3Индекс = 0; // Индекс текущей колонки
	ГруппаУровня3НомерЭлемента = 0; // Номер текущего варианта в текущей колонке
	ГруппаУровня3 = ГруппаУровня2.ПодчиненныеЭлементы.Получить(ГруппаУровня3Индекс); // Текущая колонка
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого тФорма Из СписокФормОтчета Цикл
		ГруппаУровня3НомерЭлемента = ГруппаУровня3НомерЭлемента + 1;
		
		// Переход к следующей группе верхнего уровня
		Если ГруппаУровня3НомерЭлемента > ГруппаУровня3Отсечка + ?(ГруппаУровня3Добавка > 0, 1, 0) Тогда
			ГруппаУровня3НомерЭлемента = 1;
			ГруппаУровня3Индекс        = ГруппаУровня3Индекс + 1;
			ГруппаУровня3              = ГруппаУровня2.ПодчиненныеЭлементы.Получить(ГруппаУровня3Индекс);
			ГруппаУровня3Добавка       = ГруппаУровня3Добавка - 1;
		КонецЕсли;
		
		ДобавитьЭлементыСпискаОтчета(ПеременныеКонтекста, тФорма, ГруппаУровня3);
		
	КонецЦикла;
	
	Если КоличествоФорм > 1 И КоличествоФорм < КоличествоКолонок Тогда
		Для ГруппаУровня3Индекс = КоличествоФорм По КоличествоКолонок - 1 Цикл
			ГруппаУровня3 = ГруппаУровня2.ПодчиненныеЭлементы.Получить(ГруппаУровня3Индекс);
			ДобавитьПустуюДекорацию(ПеременныеКонтекста, ГруппаУровня3);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Функция добавляет элемент списка отчетов на форму
&НаСервере
Функция ДобавитьЭлементыСпискаОтчета(ПеременныеКонтекста, Вариант, ВГруппу)
	
	// Уникальное имя добавляемого элемента
	ДобавленоЭлементов = ДобавленоЭлементов + 1;
	НадписьИмя = Вариант.НомерФормы;
	
	ГруппаВариантаИмя = "Группа_" + НадписьИмя;
	ГруппаВарианта = Элементы.Вставить(ГруппаВариантаИмя, Тип("ГруппаФормы"), ВГруппу);
	ЗаполнитьЗначенияСвойств(ГруппаВарианта, ПеременныеКонтекста.Шаблоны.ГруппаВарианта);
	
	// Добавление надписи-гиперссылки формы отчета
	Надпись = Элементы.Вставить(НадписьИмя, Тип("ДекорацияФормы"), ГруппаВарианта);
	ЗаполнитьЗначенияСвойств(Надпись, ПеременныеКонтекста.Шаблоны.НадписьВарианта);
	Надпись.Заголовок = СокрЛП(Вариант.Наименование);
	
	Надпись.УстановитьДействие("Нажатие", "ВариантНажатие");
	
	Возврат Надпись;
	
КонецФункции

// Функция добавляет пустую декорацию.
&НаСервере
Функция ДобавитьПустуюДекорацию(ПеременныеКонтекста, ВГруппу)
	
	ДобавленоЭлементов = ДобавленоЭлементов + 1;
	ДекорацияИмя = "ПустаяДекорация_" + Формат(ДобавленоЭлементов, "ЧГ=0");
	
	Декорация = Элементы.Вставить(ДекорацияИмя, Тип("ДекорацияФормы"), ВГруппу);
	Декорация.Вид = ВидДекорацииФормы.Надпись;
	Декорация.Заголовок = " ";
	
	Возврат Декорация;
	
КонецФункции

// Функция формирует таблицу значений форм отчета.
&НаСервере
Функция ПолучитьТаблицуФормОтчета(СписокФормОтчета)
	
	тФорм = новый ТаблицаЗначений;
	тФорм.Колонки.Добавить("НомерФормы");
	тФорм.Колонки.Добавить("Наименование");
	
	Для Каждого СтрокаСписка Из СписокФормОтчета Цикл
		СтрокатФорм = тФорм.Добавить();
		СтрокатФорм.НомерФормы   = СтрокаСписка.Значение;
		СтрокатФорм.Наименование = СтрокаСписка.Представление;
	КонецЦикла;
	
	Возврат тФорм;
	
КонецФункции

// Процедура заполняет панель отчетов.
&НаСервере
Процедура ЗаполнитьПанельОтчетов(СписокФормОтчета)
	
	ТаблицаФормОтчета = ПолучитьТаблицуФормОтчета(СписокФормОтчета);
	
	ПеременныеКонтекста = Новый Структура("ИмяГруппы, ДобавляемыеРеквизиты", "", Новый Массив);
	ИнициализироватьПеременныеКонтекста(ПеременныеКонтекста);
	
	ВывестиВариантыБезГруппы(ТаблицаФормОтчета, ПеременныеКонтекста);
	
	Элементы.БезГруппыДекорация.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Обработчик события "Нажатие" на названии формы отчета.
&НаКлиенте
Процедура ВариантНажатие(Элемент)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("НомерФормы", Элемент.Имя);
	ОткрытьФорму("Отчет.КВП_ФормыРегистрационногоУчета.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

// Обработчик события "ПриСозданииНаСервере" формы.
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокФормОтчета = Отчеты.КВП_ФормыРегистрационногоУчета.ПолучитьСписокФормОтчета();
	ЗаполнитьПанельОтчетов(СписокФормОтчета);
	
КонецПроцедуры

#КонецОбласти
