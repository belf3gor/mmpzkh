#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       См. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); 
// Отчет
//   поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	// Основной
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Основной");
	
	НастройкиВарианта.ФункциональныеОпции.Добавить("ВестиСведенияДляДекларацийПоАлкогольнойПродукции");
	НастройкиВарианта.ВидимостьПоУмолчанию = Истина;
	НастройкиВарианта.Описание = НСтр("ru='Сокращенный отчет по остаткам алкогольной продукции в ЕГАИС.'");
	
	НастройкиВарианта.Размещение.Вставить(Метаданные.Подсистемы.ГосИС.Подсистемы.ЕГАИС.Подсистемы.ОтчетыЕГАИС);
	
	// ОстаткиВРегистре1
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ОстаткиВРегистре1");
	
	НастройкиВарианта.ФункциональныеОпции.Добавить("ВестиСведенияДляДекларацийПоАлкогольнойПродукции");
	НастройкиВарианта.ВидимостьПоУмолчанию = Истина;
	НастройкиВарианта.Описание = НСтр("ru='Остатки в регистре № 1.'");
	
	НастройкиВарианта.Размещение.Вставить(Метаданные.Подсистемы.ГосИС.Подсистемы.ЕГАИС.Подсистемы.ОтчетыЕГАИС);
	
	// ОстаткиВРегистре2
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ОстаткиВРегистре2");
	
	НастройкиВарианта.ФункциональныеОпции.Добавить("ВестиСведенияДляДекларацийПоАлкогольнойПродукции");
	НастройкиВарианта.ВидимостьПоУмолчанию = Истина;
	НастройкиВарианта.Описание = НСтр("ru='Остатки в регистре № 2.'");
	
	НастройкиВарианта.Размещение.Вставить(Метаданные.Подсистемы.ГосИС.Подсистемы.ЕГАИС.Подсистемы.ОтчетыЕГАИС);
	
КонецПроцедуры

// Возвращает внешний набор данных для построения отчета.
//
// Параметры:
//  СписокОтчетов - Массив - массив документов ОтчетЕГАИС.
//
// Возвращаемое значение:
//  ТаблицаЗначений - внешний набор данных.
//
Функция ДанныеИнформационнойБазы(СписокОтчетов) Экспорт
	
	Результат = ДанныеОтчетовЕГАИС(СписокОтчетов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтчетЕГАИС", СписокОтчетов);
	Запрос.УстановитьПараметр("ВидыДокументовРегистр1", ВидыДокументовРегистр1());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	МАКСИМУМ(ВложенныйЗапрос.Дата) КАК Дата
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтчетЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|		ОтчетЕГАИС.Дата КАК Дата
	|	ИЗ
	|		Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|	ГДЕ
	|		ОтчетЕГАИС.Ссылка В(&ОтчетЕГАИС)
	|		И ОтчетЕГАИС.ВидДокумента В(&ВидыДокументовРегистр1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиЕГАИС.ОрганизацияЕГАИС,
	|		ОстаткиЕГАИС.Дата
	|	ИЗ
	|		Документ.ОстаткиЕГАИС КАК ОстаткиЕГАИС
	|	ГДЕ
	|		ОстаткиЕГАИС.Ссылка В(&ОтчетЕГАИС)
	|		И ОстаткиЕГАИС.ВидДокумента В(&ВидыДокументовРегистр1)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ОрганизацияЕГАИС";
	
	ТекстЗапроса = "";
	
	Сч = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ИмяПараметраДатаОстатков = "ДатаОстатков" + Формат(Сч, "ЧГ=0");
		ИмяПараметраОрганизацияЕГАИС = "ОрганизацияЕГАИС" + Формат(Сч, "ЧГ=0");
		
		Запрос.УстановитьПараметр(ИмяПараметраДатаОстатков, Выборка.Дата);
		Запрос.УстановитьПараметр(ИмяПараметраОрганизацияЕГАИС, Выборка.ОрганизацияЕГАИС);
		
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	&" + ИмяПараметраОрганизацияЕГАИС + " КАК ОрганизацияЕГАИС,
		|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.АлкогольнаяПродукция.Код КАК КодАлкогольнойПродукции,
		|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2 КАК Справка2,
		|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2.РегистрационныйНомер КАК НомерСправки2,
		|	1 КАК НомерРегистра,
		|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.КоличествоОстаток КАК КоличествоИБ,
		|	0 КАК КоличествоЕГАИС
		|ИЗ
		|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.Остатки(&" + ИмяПараметраДатаОстатков
		+ ", ОрганизацияЕГАИС = &" + ИмяПараметраОрганизацияЕГАИС + ") КАК ОстаткиАлкогольнойПродукцииЕГАИСОстатки";
		
		Сч = Сч + 1;
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаТаблицы = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		КонецЦикла;
		
		Результат.Свернуть("ОрганизацияЕГАИС, АлкогольнаяПродукция, КодАлкогольнойПродукции, Справка2, НомерСправки2, НомерРегистра", "КоличествоИБ, КоличествоЕГАИС");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает Истина в случае наличия расхождений между полученными данными и данными ИБ. Ложь - в противном случае.
//
Функция ЕстьРасхожденияВПолученныхДанных(ДокументСсылка) Экспорт
	
	Возврат ОтчетыЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументСсылка, Метаданные.Отчеты.ОстаткиАлкогольнойПродукцииЕГАИС.Имя);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает таблицу остатков, полученную из ЕГАИС.
//
Функция ДанныеОтчетовЕГАИС(СписокОтчетов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтчетЕГАИС", СписокОтчетов);
	Запрос.УстановитьПараметр("ВидыДокументовРегистр1", ВидыДокументовРегистр1());
	Запрос.УстановитьПараметр("ВидыДокументовРегистр2", ВидыДокументовРегистр2());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	МАКСИМУМ(ВложенныйЗапрос.Дата) КАК Дата,
	|	ВложенныйЗапрос.НомерРегистра КАК НомерРегистра
	|ПОМЕСТИТЬ ДатыОтчетовЕГАИС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтчетЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|		ОтчетЕГАИС.Дата КАК Дата,
	|		ВЫБОР
	|			КОГДА ОтчетЕГАИС.ВидДокумента В (&ВидыДокументовРегистр1)
	|				ТОГДА 1
	|			КОГДА ОтчетЕГАИС.ВидДокумента В (&ВидыДокументовРегистр2)
	|				ТОГДА 2
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НомерРегистра
	|	ИЗ
	|		Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|	ГДЕ
	|		ОтчетЕГАИС.Ссылка В(&ОтчетЕГАИС)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиЕГАИС.ОрганизацияЕГАИС,
	|		ОстаткиЕГАИС.Дата,
	|		ВЫБОР
	|			КОГДА ОстаткиЕГАИС.ВидДокумента В (&ВидыДокументовРегистр1)
	|				ТОГДА 1
	|			КОГДА ОстаткиЕГАИС.ВидДокумента В (&ВидыДокументовРегистр2)
	|				ТОГДА 2
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ОстаткиЕГАИС КАК ОстаткиЕГАИС
	|	ГДЕ
	|		ОстаткиЕГАИС.Ссылка В(&ОтчетЕГАИС)) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.НомерРегистра <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ОрганизацияЕГАИС,
	|	ВложенныйЗапрос.НомерРегистра
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОрганизацияЕГАИС,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетЕГАИС.Ссылка КАК Ссылка,
	|	ОтчетЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ДатыОтчетовЕГАИС.НомерРегистра КАК НомерРегистра
	|ПОМЕСТИТЬ ОтчетыЕГАИС
	|ИЗ
	|	ДатыОтчетовЕГАИС КАК ДатыОтчетовЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|		ПО ДатыОтчетовЕГАИС.ОрганизацияЕГАИС = ОтчетЕГАИС.ОрганизацияЕГАИС
	|			И ДатыОтчетовЕГАИС.Дата = ОтчетЕГАИС.Дата
	|			И (ВЫБОР
	|				КОГДА ДатыОтчетовЕГАИС.НомерРегистра = 1
	|					ТОГДА ОтчетЕГАИС.ВидДокумента В (&ВидыДокументовРегистр1)
	|				КОГДА ДатыОтчетовЕГАИС.НомерРегистра = 2
	|					ТОГДА ОтчетЕГАИС.ВидДокумента В (&ВидыДокументовРегистр2)
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	ОтчетЕГАИС.Ссылка В(&ОтчетЕГАИС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиЕГАИС.Ссылка,
	|	ОстаткиЕГАИС.ОрганизацияЕГАИС,
	|	ДатыОтчетовЕГАИС.НомерРегистра
	|ИЗ
	|	ДатыОтчетовЕГАИС КАК ДатыОтчетовЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОстаткиЕГАИС КАК ОстаткиЕГАИС
	|		ПО ДатыОтчетовЕГАИС.ОрганизацияЕГАИС = ОстаткиЕГАИС.ОрганизацияЕГАИС
	|			И ДатыОтчетовЕГАИС.Дата = ОстаткиЕГАИС.Дата
	|			И (ВЫБОР
	|				КОГДА ДатыОтчетовЕГАИС.НомерРегистра = 1
	|					ТОГДА ОстаткиЕГАИС.ВидДокумента В (&ВидыДокументовРегистр1)
	|				КОГДА ДатыОтчетовЕГАИС.НомерРегистра = 2
	|					ТОГДА ОстаткиЕГАИС.ВидДокумента В (&ВидыДокументовРегистр2)
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	ОстаткиЕГАИС.Ссылка В(&ОтчетЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОтчетЕГАИСОстаткиАлкогольнойПродукции.Ссылка.ВидДокумента В (&ВидыДокументовРегистр1)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК НомерРегистра,
	|	ОтчетЕГАИСОстаткиАлкогольнойПродукции.Ссылка.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ОтчетЕГАИСОстаткиАлкогольнойПродукции.КодАлкогольнойПродукции КАК КодАлкогольнойПродукции,
	|	ЕСТЬNULL(Справки2ЕГАИС.Ссылка, ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)) КАК Справка2,
	|	ОтчетЕГАИСОстаткиАлкогольнойПродукции.НомерСправки2 КАК НомерСправки2,
	|	ОтчетЕГАИСОстаткиАлкогольнойПродукции.Количество КАК КоличествоЕГАИС,
	|	0 КАК КоличествоИБ
	|ИЗ
	|	Документ.ОтчетЕГАИС.ОстаткиАлкогольнойПродукции КАК ОтчетЕГАИСОстаткиАлкогольнойПродукции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|		ПО ОтчетЕГАИСОстаткиАлкогольнойПродукции.КодАлкогольнойПродукции = КлассификаторАлкогольнойПродукцииЕГАИС.Код
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|		ПО ОтчетЕГАИСОстаткиАлкогольнойПродукции.НомерСправки2 = Справки2ЕГАИС.РегистрационныйНомер
	|			И (ОтчетЕГАИСОстаткиАлкогольнойПродукции.НомерСправки2 <> """")
	|ГДЕ
	|	ОтчетЕГАИСОстаткиАлкогольнойПродукции.Ссылка В
	|			(ВЫБРАТЬ
	|				ОтчетыЕГАИС.Ссылка
	|			ИЗ
	|				ОтчетыЕГАИС КАК ОтчетыЕГАИС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка.ВидДокумента В (&ВидыДокументовРегистр1)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка.ОрганизацияЕГАИС,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция.Код,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Справка2,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Справка2.РегистрационныйНомер,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество,
	|	0
	|ИЗ
	|	Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	|ГДЕ
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка В
	|			(ВЫБРАТЬ
	|				ОтчетыЕГАИС.Ссылка
	|			ИЗ
	|				ОтчетыЕГАИС КАК ОтчетыЕГАИС)";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает виды документов запросов остатков по регистру №1.
//
Функция ВидыДокументовРегистр1()
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыДокументовЕГАИС.ЗапросОстатковВРегистре1);
	Результат.Добавить(Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаОстаткиВРегистре1);
	
	Возврат Результат;
	
КонецФункции

// Возвращает виды документов запросов остатков по регистру №2.
//
Функция ВидыДокументовРегистр2()
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыДокументовЕГАИС.ЗапросОстатковВРегистре2);
	Результат.Добавить(Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаОстаткиВРегистре2);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли