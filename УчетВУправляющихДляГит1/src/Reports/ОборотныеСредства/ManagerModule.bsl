#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьВнешниеНаборыДанных",    Истина);
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Результат.Вставить("ИспользоватьПривилегированныйРежим", Истина);
	Результат.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);

	Возврат Результат;

КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт 
	
	Возврат "Оборотные средства" + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	ПС = ПланыСчетов["Хозрасчетный"];
	
	Счет10      = ПС.Материалы;
	Счет11      = ПС.ЖивотныеНаВыращиванииИОткорме;
	Счет14      = ПС.РезервыПодСнижениеСтоимостиМЦ;
	Счет14_01   = ПС.РезервыПодСнижениеСтоимостиМатериалов;
	Счет14_02   = ПС.РезервыПодСнижениеСтоимостиТоваров;
	Счет14_03   = ПС.РезервыПодСнижениеСтоимостиГотовойПродукции;
	Счет14_04   = ПС.РезервыПодСнижениеСтоимостиНезавершенногоПроизводства;
	Счет15      = ПС.ЗаготовлениеИПриобретениеМЦ;
	Счет15_01   = ПС.ЗаготовлениеИПриобретениеМатериалов;
	Счет15_02   = ПС.ПриобретениеТоваров;
	Счет16      = ПС.ОтклонениеВСтоимостиМЦ;
	Счет16_01   = ПС.ОтклонениеВСтоимостиМатериалов;
	Счет16_02   = ПС.ОтклонениеВСтоимостиТоваров;
	
	Счет20      = ПС.ОсновноеПроизводство_;
	Счет21      = ПС.Полуфабрикаты;
	Счет23      = ПС.ВспомогательныеПроизводства;
	Счет28      = ПС.БракВПроизводстве;
	Счет29      = ПС.ОбслуживающиеПроизводства;
	Счет41      = ПС.Товары;
	Счет42      = ПС.ТорговаяНаценка;
	Счет43      = ПС.ГотоваяПродукция;
	Счет44      = ПС.РасходыНаПродажу;
	Счет45      = ПС.ТоварыОтгруженные;
	Счет46      = ПС.ВыполненныеЭтапыПоНезавершеннымРаботам;
	
	Счет58      = ПС.ФинансовыеВложения;
	Счет58_01_2 = ПС.Акции;
	Счет58_02   = ПС.ДолговыеЦенныеБумаги;
	Счет58_03   = ПС.ПредоставленныеЗаймы;
	Счет58_04   = ПС.ВкладыПоДоговоруПростогоТоварищества;
	Счет58_05   = ПС.ПриобретенныеПрава;
	Счет59      = ПС.РезервыПодОбесценениеФинансовыхВложений;
		
	Счет94      = ПС.НедостачиИПотериОтПорчиЦенностей;
	
	Счета = Новый Массив;
	Счета.Добавить(Счет10);
	Счета.Добавить(Счет11);
	Счета.Добавить(Счет14);
	Счета.Добавить(Счет15);
	Счета.Добавить(Счет16);
	Счета.Добавить(Счет20);
	Счета.Добавить(Счет21);
	Счета.Добавить(Счет23);
	Счета.Добавить(Счет28);
	Счета.Добавить(Счет29);
	Счета.Добавить(Счет41);
	Счета.Добавить(Счет42);
	Счета.Добавить(Счет43);
	Счета.Добавить(Счет44);
	Счета.Добавить(Счет45);
	Счета.Добавить(Счет46);
	Счета.Добавить(Счет58);
	Счета.Добавить(Счет59);
	Счета.Добавить(Счет94);
	
	СписокДоступныхОрганизаций = ОбщегоНазначенияБП.СписокДоступныхОрганизаций(
									ПараметрыОтчета.Организация,
									ПараметрыОтчета.ВключатьОбособленныеПодразделения);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Счета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&Счета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Период КАК Период,
	|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
	|	ХозрасчетныйОстаткиИОбороты.Организация КАК Организация,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.Подразделение, ""<...>"") КАК Подразделение,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт КАК СуммаНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Месяц,
	|			,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Счета.Ссылка
	|				ИЗ
	|					Счета КАК Счета),
	|			,
	|			Организация В (&СписокОрганизаций)) КАК ХозрасчетныйОстаткиИОбороты
	|ГДЕ
	|	(ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт <> 0
	|			ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт <> 0
	|			ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт <> 0
	|			ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт <> 0)
	|ИТОГИ
	|	СУММА(СуммаНачальныйОстатокДт),
	|	СУММА(СуммаНачальныйОстатокКт),
	|	СУММА(СуммаКонечныйОстатокДт),
	|	СУММА(СуммаКонечныйОстатокКт)
	|ПО
	|	Организация,
	|	Подразделение,
	|	Период,
	|	Счет ИЕРАРХИЯ";
	
	Периодичность = БухгалтерскиеОтчетыКлиентСервер.ПолучитьЗначениеПериодичности(
		ПараметрыОтчета.Периодичность, ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	Если Периодичность = 6 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", "День");
	ИначеЕсли Периодичность = 7 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", "Неделя");
	ИначеЕсли Периодичность = 8 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", "Декада");
	ИначеЕсли Периодичность = 10 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", "Квартал");
	ИначеЕсли Периодичность = 11 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", "Полугодие");
	ИначеЕсли Периодичность = 12 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", "Год");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счета", Счета);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокДоступныхОрганизаций);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоПериода", "");
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		Запрос.УстановитьПараметр("КонецПериода",  КонецДня(ПараметрыОтчета.КонецПериода));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КонецПериода", "");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	МассивСтрокаДляУдаления = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаРезультат Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
			МассивСтрокаДляУдаления.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаДляУдаления Из МассивСтрокаДляУдаления Цикл
		ТаблицаРезультат.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	ТаблицаРезультат.Свернуть("Период,Счет,Организация,Подразделение,СуммаНачальныйОстатокДт,СуммаНачальныйОстатокКт,СуммаКонечныйОстатокДт,СуммаКонечныйОстатокКт");
	
	
	ТаблицаРазделы = Новый ТаблицаЗначений;
	ТаблицаРазделы.Колонки.Добавить("Раздел");
	ТаблицаРазделы.Колонки.Добавить("Порядок");
	ТаблицаРазделы.Колонки.Добавить("Родитель");
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Денежные средства'");
	НоваяСтрока.Порядок = 1;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Краткосрочные финансовые вложения'");
	НоваяСтрока.Порядок = 2;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Задолженность покупателей'");
	НоваяСтрока.Порядок = 3;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Сырье и материалы'");
	НоваяСтрока.Родитель = НСтр("ru = 'Запасы'");
	НоваяСтрока.Порядок = 4;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Животные на выращивании и откорме'");
	НоваяСтрока.Порядок = 5;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Незавершенное производство'");
	НоваяСтрока.Порядок = 6;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Готовая продукция'");
	НоваяСтрока.Порядок = 7;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Товары'");
	НоваяСтрока.Порядок = 8;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Товары в пути'");
	НоваяСтрока.Порядок = 9;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Брак в производстве'");
	НоваяСтрока.Порядок = 10;
	
	НоваяСтрока = ТаблицаРазделы.Добавить();
	НоваяСтрока.Раздел  = НСтр("ru = 'Недостачи к взысканию'");
	НоваяСтрока.Порядок = 11;
	
	
	ТаблицаОборотныеСредства = Новый ТаблицаЗначений;
	ТаблицаОборотныеСредства.Колонки.Добавить("Период"               , Новый ОписаниеТипов("Дата"));
	ТаблицаОборотныеСредства.Колонки.Добавить("Организация"          , Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОборотныеСредства.Колонки.Добавить("Подразделение"        , БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	ТаблицаОборотныеСредства.Колонки.Добавить("СуммаНачальныйОстаток", Новый ОписаниеТипов("Число"));
	ТаблицаОборотныеСредства.Колонки.Добавить("СуммаКонечныйОстаток" , Новый ОписаниеТипов("Число"));
	ТаблицаОборотныеСредства.Колонки.Добавить("Порядок"              , Новый ОписаниеТипов("Число"));
	ТаблицаОборотныеСредства.Колонки.Добавить("Раздел"               , Новый ОписаниеТипов("Строка"));
	ТаблицаОборотныеСредства.Колонки.Добавить("Родитель"             , Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаТаблицы Из ТаблицаРезультат Цикл
		Счет = СтрокаТаблицы.Счет;
		
		// Краткосрочные финансовые вложения
		Если Счет = Счет58_01_2 ИЛИ Счет = Счет58_02 
			ИЛИ Счет = Счет58_03 ИЛИ Счет = Счет58_04
			ИЛИ Счет = Счет58_05 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[1], "Дт", 1);
		ИначеЕсли Счет = Счет59 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[1], "Кт", -1);
			
			// Запасы (расчет составных частей)  
			// Сырье и материалы	
		ИначеЕсли Счет = Счет10 ИЛИ Счет = Счет14_01
			ИЛИ Счет = Счет15_01 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[3], "Дт", 1);
		ИначеЕсли Счет = Счет14_01 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[3], "Дт", -1);
		ИначеЕсли Счет = Счет16_01 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[3], "Дт", 1);
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[3], "Кт", -1);
			
			// Животные на выращивании и откорме
		ИначеЕсли Счет = Счет11 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[4], "Дт", 1);
			
			// НЗП	
		ИначеЕсли Счет = Счет20 ИЛИ Счет = Счет21
			ИЛИ Счет = Счет23 ИЛИ Счет = Счет29
			ИЛИ Счет = Счет46 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[5], "Дт", 1);
		ИначеЕсли Счет = Счет14_04 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[5], "Кт", -1);
			
			// Готовая продукция
		ИначеЕсли Счет = Счет43 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[6], "Дт", 1);
			
			// Товары
		ИначеЕсли Счет = Счет41 ИЛИ Счет = Счет15_02 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[7], "Дт", 1);
		ИначеЕсли Счет = Счет14_02 ИЛИ Счет = Счет14_03 ИЛИ Счет = Счет42 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[7], "Кт", -1);
		ИначеЕсли Счет = Счет16_02 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[7], "Дт", 1);
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[7], "Кт", -1);
			
			// Товары в пути
		ИначеЕсли Счет = Счет45 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[8], "Дт", 1);
			
			// Брак в производстве
		ИначеЕсли Счет = Счет28 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[9], "Дт", 1);
			
			// Недостачи к взысканию
		ИначеЕсли Счет = Счет94 Тогда
			ДобавитьСтрокуВТаблицу(ТаблицаОборотныеСредства, СтрокаТаблицы, ТаблицаРазделы[10], "Дт", 1);
			
		КонецЕсли;
	КонецЦикла;
	
	ВнешниеНаборыДанных = Новый Структура("ОборотныеСредства", ТаблицаОборотныеСредства);
	
	Возврат ВнешниеНаборыДанных;

КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	ВидыСубконтоКД = Новый СписокЗначений;
	ВидыСубконтоКД.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконтоКД.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	СчетаУчетаРасчетов = БухгалтерскиеОтчеты.СчетаУчетаРасчетовПокупателей();
	
	СчетаКД = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаКД, СчетаУчетаРасчетов.СчетаСДокументомРасчетов);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаКД, СчетаУчетаРасчетов.СчетаБезДокументаРасчетов);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СчетаКД", СчетаКД);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВидыДоговоров", БухгалтерскиеОтчеты.ВидыДоговоровПокупателей());
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВидыСубконтоКД", ВидыСубконтоКД);
	
	СчетаДС = Новый Массив;
	СчетаДС.Добавить(ПланыСчетов.Хозрасчетный.Касса);                       // 50
	СчетаДС.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);              // 51
	СчетаДС.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);               // 52
	СчетаДС.Добавить(ПланыСчетов.Хозрасчетный.СпециальныеСчета);            // 55
	СчетаДС.Добавить(ПланыСчетов.Хозрасчетный.ПереводыВПути_);              // 57
	СчетаДС.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами_); // 71
	СчетаДС = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаДС);
	
	СчетаДС = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДС, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ДенежныеДокументы)); // 50.03
	СчетаДС = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДС, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ДенежныеДокументыВал)); // 50.23
	СчетаДС = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДС, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПриобретениеИностраннойВалюты)); // 57.02
	СчетаДС = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДС, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПродажиПоПлатежнымКартам)); // 57.03
	СчетаДС = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДС, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РеализацияИностраннойВалюты)); // 57.22
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СчетаДС", СчетаДС);
	
	Периодичность = БухгалтерскиеОтчетыКлиентСервер.ПолучитьЗначениеПериодичности(ПараметрыОтчета.Периодичность, ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Периодичность", Периодичность);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	ВыводитьДиаграмму = Неопределено;
	
	Если НЕ ПараметрыОтчета.Свойство("ВыводитьДиаграмму", ВыводитьДиаграмму) Тогда
		
		ВыводитьДиаграмму = Истина;
		
	КонецЕсли;
	
	Таблица   = Неопределено;
	Диаграмма = Неопределено;
	Для Каждого ЭлементСтруктуры Из КомпоновщикНастроек.Настройки.Структура Цикл		
		Если ЭлементСтруктуры.Имя = "Таблица" Тогда
			Таблица = ЭлементСтруктуры;
		ИначеЕсли ЭлементСтруктуры.Имя = "Диаграмма" Тогда
			Диаграмма = ЭлементСтруктуры;
		КонецЕсли;		
	КонецЦикла;
	
	Если Диаграмма <> Неопределено Тогда
		
		Если ВыводитьДиаграмму Тогда
			
			Диаграмма.Точки.Очистить();
			ГруппировкаПериод = Диаграмма.Точки.Добавить();
			ПолеГруппировки = ГруппировкаПериод.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Использование = Истина;
			ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("Период");
			ПолеГруппировки.ТипДополнения = БухгалтерскиеОтчетыВызовСервера.ПолучитьТипДополненияПоИнтервалу(Периодичность);
			ПолеГруппировки.НачалоПериода =	НачалоДня(ПараметрыОтчета.НачалоПериода);
			ПолеГруппировки.КонецПериода  = КонецДня(ПараметрыОтчета.КонецПериода);
			
			ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			ГруппировкаПериод.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			// Группировка
			Диаграмма.Серии.Очистить();
			Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
				Если ПолеВыбраннойГруппировки.Использование Тогда
					Группировка = Диаграмма.Серии.Добавить();
					ПолеГруппировкиДиаграммы = Новый Структура("Использование, ИсходныйНомерСтроки, НомерСтроки, Поле, Представление");
					ЗаполнитьЗначенияСвойств(ПолеГруппировкиДиаграммы, ПолеВыбраннойГруппировки);
					ПолеГруппировкиДиаграммы.Вставить("ТипГруппировки", 0);
					БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(ПолеГруппировкиДиаграммы, Группировка);
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			
		Иначе
			
			Диаграмма.Использование = ВыводитьДиаграмму;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Таблица <> Неопределено Тогда
		Таблица.Колонки.Очистить();
		ГруппировкаПериод = Таблица.Колонки.Добавить();
		ПолеГруппировки = ГруппировкаПериод.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Использование = Истина;
		ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("Период");
		ПолеГруппировки.ТипДополнения = БухгалтерскиеОтчетыВызовСервера.ПолучитьТипДополненияПоИнтервалу(Периодичность);
		ПолеГруппировки.НачалоПериода = НачалоДня(ПараметрыОтчета.НачалоПериода);
		ПолеГруппировки.КонецПериода  = КонецДня(ПараметрыОтчета.КонецПериода);
		
		ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		ГруппировкаПериод.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		
		// Группировка
		Таблица.Строки.Очистить();
		Группировка = Таблица.Строки;
		Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
			Если ПолеВыбраннойГруппировки.Использование Тогда
				Если ТипЗнч(Группировка) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
					Группировка = Группировка.Добавить();
				Иначе
					Группировка = Группировка.Структура.Добавить();
				КонецЕсли;
				
				БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(ПолеВыбраннойГруппировки, Группировка);
				
				Если ПолеВыбраннойГруппировки.Поле = "Раздел" Тогда
					
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группировка.Отбор, "СуммаКонечныйОстаток", 0, ВидСравненияКомпоновкиДанных.Заполнено);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
КонецПроцедуры

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОборотныеСредства").Размещение.Вставить(Метаданные.Подсистемы.Руководителю.Подсистемы.ОбщиеПоказатели, "");
	
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
КонецПроцедуры

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	Схема = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Для Каждого Вариант из Схема.ВариантыНастроек Цикл
		 Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;	
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(Адрес);
	
	ОтчетОбъект       = ДанныеОбъекта.Объект;
	ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;

	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("Организация", ОтчетОбъект.Организация);
	ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
	
	Раздел        = Неопределено;
	Период        = Неопределено;
	Периодичность = БухгалтерскиеОтчетыКлиентСервер.ПолучитьЗначениеПериодичности(ОтчетОбъект.Периодичность, ОтчетОбъект.НачалоПериода, ОтчетОбъект.КонецПериода);
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(ДанныеРасшифровки.Настройки);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ДанныеОбъекта.Объект.СхемаКомпоновкиДанных));
	
	МассивПолей = БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровки, КомпоновщикНастроек, Истина);

	Для Каждого Отбор Из МассивПолей Цикл
		Если ТипЗнч(Отбор) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") тогда
			Если Отбор.Значение = NULL тогда
				Продолжить;
			КонецЕсли;
			
			Если Отбор.Поле = "Организация" Тогда
				ДополнительныеСвойства.Вставить("Организация", Отбор.Значение);
			ИначеЕсли Отбор.Поле = "Период" Тогда
				Период = Отбор.Значение;
			ИначеЕсли  Отбор.Поле = "Раздел" Тогда
				Раздел = Отбор.Значение;
			Иначе
				Если Отбор.Иерархия Тогда
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение, ВидСравненияКомпоновкиДанных.ВИерархии);
				Иначе
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение);
				КонецЕсли;
			КонецЕсли;	
		ИначеЕсли ТипЗнч(Отбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Если Отбор.Представление = "###ОтборПоОрганизацииСОП###" Тогда
				Для Каждого ЭлементОтбора Из Отбор.Элементы Цикл
					Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
						ДополнительныеСвойства.Вставить("Организация"                      , ЭлементОтбора.ПравоеЗначение);
						ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", Истина);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если Отбор.Представление = "###ОтборПоОрганизации###" Тогда
				ДополнительныеСвойства.Вставить("Организация"                      , Отбор.ПравоеЗначение);
				ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", Ложь);
			ИначеЕсли Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") 
				И Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				ДополнительныеСвойства.Вставить("Подразделение", Отбор.ПравоеЗначение);
			ИначеЕсли Отбор.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.ЛевоеЗначение, Отбор.ПравоеЗначение, Отбор.ВидСравнения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	НастройкиРасшифровки = Новый Структура;
	
	Если НРег(Раздел) = "денежные средства" Тогда
		Если Период <> Неопределено Тогда
			ДополнительныеСвойства.Вставить("Период", БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность));
		Иначе
			ДополнительныеСвойства.Вставить("Период", ОтчетОбъект.КонецПериода);
		КонецЕсли;
		ДополнительныеСвойства.Вставить("КлючВарианта", "ОстаткиДенежныхСредств");
		
		СписокПунктовМеню = Новый СписокЗначений;
		СписокПунктовМеню.Добавить("ОстаткиДенежныхСредств", "Остатки денежных средств");
		
		НастройкиРасшифровки.Вставить("ОстаткиДенежныхСредств", ПользовательскиеНастройки);
		
	ИначеЕсли НРег(Раздел) = "задолженность покупателей" Тогда
		Если Период <> Неопределено Тогда
			ДополнительныеСвойства.Вставить("Период", БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность));
		Иначе
			ДополнительныеСвойства.Вставить("Период", ОтчетОбъект.КонецПериода);
		КонецЕсли;
		ДополнительныеСвойства.Вставить("КлючВарианта", "ЗадолженностьПокупателейПоСрокамДолга");
		
		СписокПунктовМеню = Новый СписокЗначений;
		СписокПунктовМеню.Добавить("ЗадолженностьПокупателейПоСрокамДолга", "Задолженность покупателей по срокам долга");
		
		НастройкиРасшифровки.Вставить("ЗадолженностьПокупателейПоСрокамДолга", ПользовательскиеНастройки);
		
	ИначеЕсли НРег(Раздел) = "сырье и материалы" Тогда
		Если Период <> Неопределено Тогда
			ДополнительныеСвойства.Вставить("КонецПериода", КонецДня(БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность)));
			ДополнительныеСвойства.Вставить("НачалоПериода", БухгалтерскиеОтчетыКлиентСервер.НачалоПериода(Период, Периодичность));
		Иначе
			ДополнительныеСвойства.Вставить("КонецПериода", КонецДня(ОтчетОбъект.КонецПериода));
			ДополнительныеСвойства.Вставить("НачалоПериода", ОтчетОбъект.КонецПериода);
		КонецЕсли;
		
		СписокПунктовМеню = Новый СписокЗначений;
		СписокПунктовМеню.Добавить("ОстаткиТоваров", "Остатки товаров");
		
		ДополнительныеСвойства.Вставить("КлючВарианта", "ОстаткиТоваров");
		
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", ПланыСчетов.Хозрасчетный.СырьеИМатериалы, ВидСравненияКомпоновкиДанных.ВИерархии,,Истина);
		
		НастройкиРасшифровки.Вставить("ОстаткиТоваров", ПользовательскиеНастройки);
		
	ИначеЕсли НРег(Раздел) = "готовая продукция" Тогда
		Если Период <> Неопределено Тогда
			ДополнительныеСвойства.Вставить("КонецПериода", КонецДня(БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность)));
			ДополнительныеСвойства.Вставить("НачалоПериода", БухгалтерскиеОтчетыКлиентСервер.НачалоПериода(Период, Периодичность));
		Иначе
			ДополнительныеСвойства.Вставить("КонецПериода", КонецДня(ОтчетОбъект.КонецПериода));
			ДополнительныеСвойства.Вставить("НачалоПериода", ОтчетОбъект.КонецПериода);
		КонецЕсли;
		
		СписокПунктовМеню = Новый СписокЗначений;
		СписокПунктовМеню.Добавить("ОстаткиТоваров", "Остатки товаров");
		
		ДополнительныеСвойства.Вставить("КлючВарианта", "ОстаткиТоваров");
		
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", ПланыСчетов.Хозрасчетный.ГотоваяПродукция, ВидСравненияКомпоновкиДанных.ВИерархии,,Истина);
		
		НастройкиРасшифровки.Вставить("ОстаткиТоваров", ПользовательскиеНастройки);
		
	ИначеЕсли НРег(Раздел) = "товары" Тогда
		Если Период <> Неопределено Тогда
			ДополнительныеСвойства.Вставить("КонецПериода", КонецДня(БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность)));
			ДополнительныеСвойства.Вставить("НачалоПериода", БухгалтерскиеОтчетыКлиентСервер.НачалоПериода(Период, Периодичность));
		Иначе
			ДополнительныеСвойства.Вставить("КонецПериода", КонецДня(ОтчетОбъект.КонецПериода));
			ДополнительныеСвойства.Вставить("НачалоПериода", ОтчетОбъект.КонецПериода);
		КонецЕсли;
		
		СписокПунктовМеню = Новый СписокЗначений;
		СписокПунктовМеню.Добавить("ОстаткиТоваров", "Остатки товаров");
		
		ДополнительныеСвойства.Вставить("КлючВарианта", "ОстаткиТоваров");
		
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", ПланыСчетов.Хозрасчетный.Товары, ВидСравненияКомпоновкиДанных.ВИерархии,,Истина);
		
		НастройкиРасшифровки.Вставить("ОстаткиТоваров", ПользовательскиеНастройки);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	Адрес = ПоместитьВоВременноеХранилище(ДанныеОбъекта, Адрес);
	ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
	ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Ложь);
	
КонецПроцедуры

// Возвращает набор параметров, которые необходимо сохранять в рассылке отчетов.
// Значения параметров используются при формировании отчета в рассылке.
//
// Возвращаемое значение:
//   Структура - структура настроек, сохраняемых в рассылке с неинициализированными значениями.
//
Функция НастройкиОтчетаСохраняемыеВРассылке() Экспорт
	
	КоллекцияНастроек = Новый Структура;
	КоллекцияНастроек.Вставить("Организация"                      , Справочники.Организации.ПустаяСсылка());
	КоллекцияНастроек.Вставить("Периодичность"                    , 0);
	КоллекцияНастроек.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	КоллекцияНастроек.Вставить("РазмещениеДополнительныхПолей"    , 0);
	КоллекцияНастроек.Вставить("Группировка"                      , Неопределено);
	КоллекцияНастроек.Вставить("ДополнительныеПоля"               , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьДиаграмму"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьЗаголовок"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьПодвал"                   , Ложь);
	КоллекцияНастроек.Вставить("МакетОформления"                  , Неопределено);
	КоллекцияНастроек.Вставить("НастройкиКомпоновкиДанных"        , Неопределено);
	
	Возврат КоллекцияНастроек;
	
КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Часть параметров компоновки отчета используется так же и в рассылке отчета.
	ПараметрыОтчета = НастройкиОтчетаСохраняемыеВРассылке();
	
	// Дополним параметрами, влияющими на формирование отчета.
	ПараметрыОтчета.Вставить("ПериодОтчета"         , Неопределено);
	ПараметрыОтчета.Вставить("НачалоПериода"        , Дата(1,1,1));
	ПараметрыОтчета.Вставить("КонецПериода"         , Дата(1,1,1));
	ПараметрыОтчета.Вставить("РежимРасшифровки"     , Ложь);
	ПараметрыОтчета.Вставить("ДанныеРасшифровки"    , Неопределено);
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных", Неопределено);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета"  , "");
	
	Возврат ПараметрыОтчета;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ДобавитьСтрокуВТаблицу(Таблица, СтрокаТаблицы, Раздел, СторонаПроводки, Знак)
	
	НоваяСтрока = Таблица.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	НоваяСтрока.СуммаНачальныйОстаток = Знак * СтрокаТаблицы["СуммаНачальныйОстаток" + СторонаПроводки];
	НоваяСтрока.СуммаКонечныйОстаток  = Знак * СтрокаТаблицы["СуммаКонечныйОстаток" + СторонаПроводки];
	НоваяСтрока.Раздел    = Раздел.Раздел;
	НоваяСтрока.Родитель  = Раздел.Родитель;
	НоваяСтрока.Порядок   = Раздел.Порядок;
	
КонецПроцедуры
#КонецЕсли