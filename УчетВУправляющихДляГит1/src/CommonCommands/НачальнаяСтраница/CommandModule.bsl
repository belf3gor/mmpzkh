
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ПростойИнтерфейсНачальнаяСтраница");

	// Всегда сначала переходим на начальную или пустую страницу. 
	// В большинстве страница есть, поэтому не создаем задержку в ее отображении из-за дополнительного серверного вызова.
	ПерейтиПоНавигационнойСсылке("e1cib/navigationpoint/startpage");
	
	// Но если по каким-то причинам начальная страница отключена, то выведем сообщение пользователю об этом.
	СведенияОНачальнойСтранице = СведенияОНачальнойСтранице();
	
	Если НЕ СведенияОНачальнойСтранице.ЕстьНачальнаяСтраница Тогда
		СтрокиСообщения = Новый Массив;
		СтрокиСообщения.Добавить(НСтр("ru = 'Для настройки начальной страницы выберите пункт Главное меню'") + " ");
		СтрокиСообщения.Добавить(БиблиотекаКартинок.ГлавноеМеню);
		СтрокиСообщения.Добавить(" " + НСтр("ru = '- Вид - Настройка начальной страницы.'"));
		
		Если СведенияОНачальнойСтранице.ЭтоМедленныйРежимРаботы Тогда
			СтрокиСообщения.Добавить(Символы.ПС);
			СтрокиСообщения.Добавить(НСтр("ru = 'При работе на медленных компьютерах отображение информации на начальной странице может увеличить время запуска программы.'"));
		КонецЕсли;
		
		ПоказатьПредупреждение(, Новый ФорматированнаяСтрока(СтрокиСообщения), , НСтр("ru = 'Настройте начальную страницу'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СведенияОНачальнойСтранице()

	Результат = Новый Структура();
	Результат.Вставить("ЕстьНачальнаяСтраница",   Истина);
	Результат.Вставить("ЭтоМедленныйРежимРаботы", ОбщегоНазначенияБП.ЭтоМедленныйРежимРаботы());

	Настройки = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиНачальнойСтраницы");
	Если ТипЗнч(Настройки) = Тип("НастройкиНачальнойСтраницы") Тогда
		СоставФорм = Настройки.ПолучитьСоставФорм();
		Если СоставФорм.ЛеваяКолонка.Количество() = 0 
			И СоставФорм.ПраваяКолонка.Количество() = 0 Тогда
			// Начальная страница отключена.
			Результат.ЕстьНачальнаяСтраница = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции
