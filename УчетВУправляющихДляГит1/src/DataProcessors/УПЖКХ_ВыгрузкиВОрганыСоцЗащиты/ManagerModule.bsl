
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыМодуляМенеджераОбработки

// Предназначена для актуализации списка услуг, по которым производится выгрузка данных.
//
Функция АктуализироватьУслуги(ТаблицаУслуг) Экспорт
	
	// Данная проверка добавлена в следствии того, что ранее в таблице услуг не содержалась колонка "КодПерерасчета", "КодСОИ" и "КодПерерасчетаСОИ",
	// которая в настоящее время используется в выгрузке.
	// При отсутствии данной проверки в результате выполнения запроса появляются конфигураторские ошибки.
	Если ТаблицаУслуг.Колонки.Найти("КодПерерасчета") = Неопределено Тогда
		ТаблицаУслуг.Колонки.Добавить("КодПерерасчета", УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьОписаниеТиповЧисла(3));
	КонецЕсли;
	Если ТаблицаУслуг.Колонки.Найти("КодСОИ") = Неопределено Тогда
		ТаблицаУслуг.Колонки.Добавить("КодСОИ", УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьОписаниеТиповЧисла(3));
	КонецЕсли;
	Если ТаблицаУслуг.Колонки.Найти("КодПерерасчетаСОИ") = Неопределено Тогда
		ТаблицаУслуг.Колонки.Добавить("КодПерерасчетаСОИ", УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьОписаниеТиповЧисла(3));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаУслуг.Код,
	|	ТаблицаУслуг.КодПерерасчета,
	|	ТаблицаУслуг.КодСОИ,
	|	ТаблицаУслуг.КодПерерасчетаСОИ,
	|	ТаблицаУслуг.Услуга
	|ПОМЕСТИТЬ втСохраненыеУслуги
	|ИЗ
	|	&ТаблицаУслуг КАК ТаблицаУслуг
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСохраненыеУслуги.Код,
	|	втСохраненыеУслуги.КодПерерасчета,
	|	втСохраненыеУслуги.КодСОИ,
	|	втСохраненыеУслуги.КодПерерасчетаСОИ,
	|	втСохраненыеУслуги.Услуга
	|ИЗ
	|	втСохраненыеУслуги КАК втСохраненыеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КВП_Услуги КАК КВП_Услуги
	|		ПО втСохраненыеУслуги.Услуга = КВП_Услуги.Ссылка
	|ГДЕ
	|	НЕ КВП_Услуги.ЭтоГруппа
	|	И НЕ КВП_Услуги.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТаблицаУслуг", ТаблицаУслуг);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // АктуализироватьУслуги

// Предназначена для получения всех не помеченных элементов справочника "Услуги".
//
Функция ПолучитьВсеНепомеченныеУслуги() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КВП_Услуги.Ссылка КАК Услуга
	|ИЗ
	|	Справочник.КВП_Услуги КАК КВП_Услуги
	|ГДЕ
	|	НЕ КВП_Услуги.ЭтоГруппа
	|	И НЕ КВП_Услуги.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Услуга";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьВсеНепомеченныеУслуги()

#КонецОбласти

#КонецЕсли