
//////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Формирует массив сокращений для Томской области для выбранного вида сокращения.
//
Функция СформироватьМассивСокращениеДляВолгоградскойОблПоВиду(ВидСокращение)
		
	МассивСокращений = Новый Массив;
	// для регионов
	Если ВидСокращение = 1 Тогда
		МассивСокращений.Добавить("обл");
		// для районов
	ИначеЕсли ВидСокращение = 2 Тогда
		МассивСокращений.Добавить("р-н");
		МассивСокращений.Добавить("р-н.");
		// для городов
	ИначеЕсли ВидСокращение = 3 Тогда
		МассивСокращений.Добавить("г");
		МассивСокращений.Добавить("г.");
		// для нас.пунктов
	ИначеЕсли ВидСокращение = 4 Тогда
		МассивСокращений.Добавить("рп");
		МассивСокращений.Добавить("п");
		МассивСокращений.Добавить("с");
		МассивСокращений.Добавить("д");
		МассивСокращений.Добавить("х");
		МассивСокращений.Добавить("ст");
		МассивСокращений.Добавить("пгт");
		МассивСокращений.Добавить("дп");
		МассивСокращений.Добавить("рзд");
		МассивСокращений.Добавить("мкр");
		МассивСокращений.Добавить("нп");
		МассивСокращений.Добавить("тер");
		МассивСокращений.Добавить("п/ст");
		МассивСокращений.Добавить("промзона");
		МассивСокращений.Добавить("снт");
		МассивСокращений.Добавить("п/о");
		МассивСокращений.Добавить("городок");
		МассивСокращений.Добавить("заимка");
		МассивСокращений.Добавить("кп");
		МассивСокращений.Добавить("ж/д_казарм");
		МассивСокращений.Добавить("г");
		МассивСокращений.Добавить("обл");
		
		МассивСокращений.Добавить("рп.");
		МассивСокращений.Добавить("п.");
		МассивСокращений.Добавить("с.");
		МассивСокращений.Добавить("д.");
		МассивСокращений.Добавить("х.");
		МассивСокращений.Добавить("ст.");
		МассивСокращений.Добавить("пгт.");
		МассивСокращений.Добавить("дп.");
		МассивСокращений.Добавить("рзд.");
		МассивСокращений.Добавить("мкр.");
		МассивСокращений.Добавить("нп.");
		МассивСокращений.Добавить("тер.");
		МассивСокращений.Добавить("п/ст.");
		МассивСокращений.Добавить("промзона.");
		МассивСокращений.Добавить("снт.");
		МассивСокращений.Добавить("п/о.");
		МассивСокращений.Добавить("городок.");
		МассивСокращений.Добавить("заимка.");
		МассивСокращений.Добавить("кп.");
		МассивСокращений.Добавить("ж/д_казарм.");
		МассивСокращений.Добавить("г.");
		МассивСокращений.Добавить("обл.");
		// для улиц
	ИначеЕсли ВидСокращение = 5 Тогда
		МассивСокращений.Добавить("ул");
		МассивСокращений.Добавить("тер");
		МассивСокращений.Добавить("пл");
		МассивСокращений.Добавить("пер");
		МассивСокращений.Добавить("снт");
		МассивСокращений.Добавить("проезд");
		МассивСокращений.Добавить("б-р");
		МассивСокращений.Добавить("туп");
		МассивСокращений.Добавить("п");
		МассивСокращений.Добавить("ш");
		МассивСокращений.Добавить("пр-кт");
		МассивСокращений.Добавить("тракт");
		МассивСокращений.Добавить("сквер");
		МассивСокращений.Добавить("ст");
		МассивСокращений.Добавить("наб");
		МассивСокращений.Добавить("км");
		МассивСокращений.Добавить("ж/д_ст");
		МассивСокращений.Добавить("сад");
		МассивСокращений.Добавить("парк");
		МассивСокращений.Добавить("уч-к");
		МассивСокращений.Добавить("мкр");
		МассивСокращений.Добавить("городок");
		МассивСокращений.Добавить("кв-л");
		МассивСокращений.Добавить("нп");
		МассивСокращений.Добавить("остров");
		МассивСокращений.Добавить("гск");
		МассивСокращений.Добавить("линия");
		МассивСокращений.Добавить("рзд");
		МассивСокращений.Добавить("стр");
		МассивСокращений.Добавить("аллея");
		МассивСокращений.Добавить("казарма");
		МассивСокращений.Добавить("ж/д_рзд");
		МассивСокращений.Добавить("д");
		МассивСокращений.Добавить("ж/д_казарм");
		МассивСокращений.Добавить("ж/д_будка");
		МассивСокращений.Добавить("высел");
		
		МассивСокращений.Добавить("ул.");
		МассивСокращений.Добавить("тер.");
		МассивСокращений.Добавить("пл.");
		МассивСокращений.Добавить("пер.");
		МассивСокращений.Добавить("снт.");
		МассивСокращений.Добавить("проезд.");
		МассивСокращений.Добавить("б-р.");
		МассивСокращений.Добавить("туп.");
		МассивСокращений.Добавить("п.");
		МассивСокращений.Добавить("ш.");
		МассивСокращений.Добавить("пр-кт.");
		МассивСокращений.Добавить("тракт.");
		МассивСокращений.Добавить("сквер.");
		МассивСокращений.Добавить("ст.");
		МассивСокращений.Добавить("наб.");
		МассивСокращений.Добавить("км.");
		МассивСокращений.Добавить("ж/д_ст.");
		МассивСокращений.Добавить("сад.");
		МассивСокращений.Добавить("парк.");
		МассивСокращений.Добавить("уч-к.");
		МассивСокращений.Добавить("мкр.");
		МассивСокращений.Добавить("городок.");
		МассивСокращений.Добавить("кв-л.");
		МассивСокращений.Добавить("нп.");
		МассивСокращений.Добавить("остров.");
		МассивСокращений.Добавить("гск.");
		МассивСокращений.Добавить("линия.");
		МассивСокращений.Добавить("рзд.");
		МассивСокращений.Добавить("стр.");
		МассивСокращений.Добавить("аллея.");
		МассивСокращений.Добавить("казарма.");
		МассивСокращений.Добавить("ж/д_рзд.");
		МассивСокращений.Добавить("д.");
		МассивСокращений.Добавить("ж/д_казарм.");
		МассивСокращений.Добавить("ж/д_будка.");
		МассивСокращений.Добавить("высел.");
	КонецЕсли;
	
	Возврат МассивСокращений;
	
КонецФункции // СформироватьМассивСокращениеДляСвердловскойОблПоВиду()

&НаСервере
// Функция сокращает название города или улицы: убирает сокращения,
// определяющие тип данного элемента адреса: "г.", "ул." и так далее.
//
// Параметры:
//  Ресурс       - Строка - элемент адреса.
//
// Возвращаемое значение:
//  Строка - элемент адреса в сокращенном виде.
//
Функция НормализацияПолейАдресаОбласти(Ресурс, Массив)
	
	Найден = Истина;
	
	Для Каждого Элемент Из Массив Цикл
		
		Если Прав(СокрЛП(Ресурс), СтрДлина(Элемент)) = Элемент Тогда
			ПредСимвол = Сред(СокрЛП(Ресурс), СтрДлина(СокрЛП(Ресурс)) - СтрДлина(Элемент), 1);
			
			Если ПредСимвол = " " Тогда
				Возврат СокрЛП(Лев(СокрЛП(Ресурс), СтрДлина(СокрЛП(Ресурс)) - СтрДлина(Элемент)));
			КонецЕсли;
		ИначеЕсли Лев(СокрЛП(Ресурс), СтрДлина(Элемент)) = Элемент Тогда
			СледСимвол = Сред(СокрЛП(Ресурс), СтрДлина(Элемент) + 1, 1);
			
			Если СледСимвол = " " Тогда
				Возврат СокрЛП(Сред(СокрЛП(Ресурс), СтрДлина(Элемент) + 1));
			КонецЕсли;
		Иначе
			
			Найден = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Найден = Ложь Тогда
		Возврат Ресурс;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// СОХРАНЯЕМЫЕ НАСТРОЙКИ

#Область РаботаСНастройками

&НаСервере
// Процедура восстановления настроек.
Процедура ВосстановитьНастройки()
	
	СтруктураПараметров = УПЖКХ_ТиповыеМетодыВызовСервера.ХранилищеОбщихНастроекЗагрузить("ВолгоградскаяОбласть_ВыгрузкаВОСЗН", "ПараметрыВыгрузки");
	
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		СтруктураПараметров.Свойство("ИмяФайлаЗагрузки", ИмяФайлаЗагрузки);
		СтруктураПараметров.Свойство("КаталогВыгрузки",  КаталогВыгрузки);
		СтруктураПараметров.Свойство("КодировкаФайла",   КодировкаФайла);
		
		врТаблицаУслугВОСЗН = Новый ТаблицаЗначений;
		врТаблицаУслугВБазе = Новый ТаблицаЗначений;
		
		СтруктураПараметров.Свойство("ВолгоградскаяОбласть_ЖКУ", врТаблицаУслугВОСЗН);
		Если НЕ врТаблицаУслугВОСЗН = Неопределено Тогда
			Объект.ВолгоградскаяОбласть_ТаблицаУслуг.Загрузить(врТаблицаУслугВОСЗН);
		КонецЕсли;
		
		СтруктураПараметров.Свойство("ВолгоградскаяОбласть_ЖКУ_ВБазе", врТаблицаУслугВБазе);
		Если НЕ врТаблицаУслугВБазе = Неопределено Тогда
			Объект.ВолгоградскаяОбласть_ТаблицаУслугДляВыгрузки.Загрузить(врТаблицаУслугВБазе);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Процедура сохранения настроек.
Процедура СохранитьНастройки()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяФайлаЗагрузки",               ИмяФайлаЗагрузки);
	СтруктураПараметров.Вставить("КаталогВыгрузки",                КаталогВыгрузки);
	СтруктураПараметров.Вставить("КодировкаФайла",                 КодировкаФайла);
	СтруктураПараметров.Вставить("ВолгоградскаяОбласть_ЖКУ",       Объект.ВолгоградскаяОбласть_ТаблицаУслуг.Выгрузить());
	СтруктураПараметров.Вставить("ВолгоградскаяОбласть_ЖКУ_ВБазе", Объект.ВолгоградскаяОбласть_ТаблицаУслугДляВыгрузки.Выгрузить());
	
	УПЖКХ_ТиповыеМетодыВызовСервера.ХранилищеОбщихНастроекСохранить("ВолгоградскаяОбласть_ВыгрузкаВОСЗН", "ПараметрыВыгрузки", СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// РАБОТА С ВРЕМЕННЫМ ХРАНИЛИЩЕМ

#Область РаботаСВременнымХранилищем

&НаСервере
// Помещает настройки во временное хранилище.
Функция ПоместитьНастройкиВХранилище()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ВолгоградскаяОбласть_ЖКУ",       Объект.ВолгоградскаяОбласть_ТаблицаУслуг.Выгрузить());
	СтруктураНастроек.Вставить("ВолгоградскаяОбласть_ЖКУ_ВБазе", Объект.ВолгоградскаяОбласть_ТаблицаУслугДляВыгрузки.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураНастроек, Новый УникальныйИдентификатор);
	
КонецФункции

&НаСервере
// Получает настройки из временного хранилища.
Функция ПолучитьНастройкиИзХранилища(АдресНастроекВХранилище)
	
	СтруктураНастроек = ПолучитьИзВременногоХранилища(АдресНастроекВХранилище);
	
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
		
		Если СтруктураНастроек.Свойство("ВолгоградскаяОбласть_ЖКУ") Тогда
			Объект.ВолгоградскаяОбласть_ТаблицаУслуг.Загрузить(СтруктураНастроек.ВолгоградскаяОбласть_ЖКУ);
		КонецЕсли;
		
		Если СтруктураНастроек.Свойство("ВолгоградскаяОбласть_ЖКУ_ВБазе") Тогда
			Объект.ВолгоградскаяОбласть_ТаблицаУслугДляВыгрузки.Загрузить(СтруктураНастроек.ВолгоградскаяОбласть_ЖКУ_ВБазе);
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ЗАПОЛНЕНИЕ ДАННЫХ

#Область ЗаполнениеДанных

&НаКлиенте
// Проверяет наличие всех необходимых данных для заполнения таблиц выгрузки.
//
Функция ПроверкаУсловийДляЗаполненияТаблиц()
	
	ТекстОшибки = "";
	
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		ТекстОшибки = ТекстОшибки + "Не указан период выгрузки!";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстОшибки = ?(НЕ ПустаяСтрока(ТекстОшибки), ТекстОшибки + Символы.ПС + "Не указана организация!", "Не указан организация!");
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяФайлаЗагрузки) Тогда
		ТекстОшибки = ?(НЕ ПустаяСтрока(ТекстОшибки), ТекстОшибки + Символы.ПС + "Не указан файл загрузки!", "Не указан файл загрузки!");
	КонецЕсли;
	
	Если Объект.ВолгоградскаяОбласть_ТаблицаУслугДляВыгрузки.Количество() = 0 Тогда
		ТекстОшибки = ?(НЕ ПустаяСтрока(ТекстОшибки), ТекстОшибки + Символы.ПС + "Не заполнена таблица соответствия услуг!", "Не заполнена таблица соответствия услуг!");
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции // ПроверкаУсловийДляЗаполненияТаблиц

&НаСервере
// Процедура загрузки данных из файла.
Процедура ЗагрузитьНаСервере(ПутьКФайлу)
	
	// Запись временных данных во временный файл.
	врДанныеФайла = ПолучитьИзВременногоХранилища(ПутьКФайлу);
	ВременныйФайл = ПолучитьИмяВременногоФайла("DBF");
	
	// Для чтения *.dbf формата урежем имя временного файла.
	ФайлДБФ = Новый Файл(ВременныйФайл);
	СтароеИмяФайла = ФайлДБФ.ИмяБезРасширения + ".DBF";
	НовоеИмяФайла = Лев(ФайлДБФ.ИмяБезРасширения, 8) + ".DBF";
	ВременныйФайл = СтрЗаменить(ФайлДБФ.ПолноеИмя, СтароеИмяФайла, НовоеИмяФайла);
	
	врДанныеФайла.Записать(ВременныйФайл);
	
	//загрузка
	ФайлДБФ = Новый XBase;
	ФайлДБФ.Кодировка = ?(КодировкаФайла = 0, КодировкаXBase.OEM, КодировкаXBase.ANSI);
	ФайлДБФ.ОткрытьФайл(ВременныйФайл, , Истина);
	
	МассивСокрНасПункт = СформироватьМассивСокращениеДляВолгоградскойОблПоВиду(4);
	МассивСокрУлицы    = СформироватьМассивСокращениеДляВолгоградскойОблПоВиду(5);
	
	ТаблицаДанныхФайла = Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Выгрузить();
	
	Если ФайлДБФ.Открыта() = Истина Тогда
		
			ТаблицаДанныхФайла.Очистить();
		
		Пока Не ФайлДБФ.ВКонце() Цикл
			
			НоваяСтрока = ТаблицаДанныхФайла.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ФайлДБФ);
			НоваяСтрока.SURNAME = ВРег(СокрЛП(ФайлДБФ.SURNAME));
			НоваяСтрока.NAME    = ВРег(СокрЛП(ФайлДБФ.NAME));
			НоваяСтрока.SECNAME = ВРег(СокрЛП(ФайлДБФ.SECNAME));
			НоваяСтрока.TOWN    = ВРег(НормализацияПолейАдресаОбласти(СокрЛП(ФайлДБФ.TOWN), МассивСокрНасПункт));
			НоваяСтрока.STREET  = ВРег(НормализацияПолейАдресаОбласти(СокрЛП(ФайлДБФ.STREET), МассивСокрУлицы));
			НоваяСтрока.KORPUS  = СокрЛП(ФайлДБФ.KORPUS);
			НоваяСтрока.HOUSE   = СокрЛП(Строка(ФайлДБФ.HOUSE));
			
			НоваяСтрока.FLAT    = ?(ФайлДБФ.FLAT = 0, 1, ФайлДБФ.FLAT);
			
			ФайлДБФ.Следующая();
			
		КонецЦикла;
		
	КонецЕсли;
	
	ФайлДБФ.ЗакрытьФайл();
	
	Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Загрузить(ТаблицаДанныхФайла);
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
// Процедура заполнения всех данных.
Процедура ЗаполнитьНаСервере()
	
	ТаблицаДанныхФайлаОНачислениях    = Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Выгрузить();
	ТаблицаДанныхФайлаОЗадолженностях = Объект.ВолгоградскаяОбласть_ТаблицаЗадолженностей.Выгрузить();
	
	ОбработкаОбъект = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.УПЖКХ_ВыгрузкиВОрганыСоцЗащиты"));
	ОбработкаОбъект.СформироватьТаблицуДанныхВолгоградскаяОбласть(ТаблицаДанныхФайлаОНачислениях, ТаблицаДанныхФайлаОЗадолженностях);
	ЗначениеВДанныеФормы(ОбработкаОбъект, Объект);
	
	Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Загрузить(ТаблицаДанныхФайлаОНачислениях);
	Объект.ВолгоградскаяОбласть_ТаблицаЗадолженностей.Загрузить(ТаблицаДанныхФайлаОЗадолженностях);
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ВЫГРУЗКА ФАЙЛОВ

#Область ВыгрузкаФайлов

&НаКлиенте
// Предназначена для выгрузки данных из таблицы данных в dbf-файл.
//
Процедура ВыгрузитьДанныеВФайлВолгоградскаяОбласть()
	
	Если Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Количество() = 0
	 И Объект.ВолгоградскаяОбласть_ТаблицаЗадолженностей.Количество() = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("В таблицах выгружаемых данных отсутствуют данные для выгрузки в dbf-файлы.");
		Возврат;
	КонецЕсли;
	
	#Если Не ВебКлиент Тогда
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Началась выгрузка таблиц в файлы!");
	
	ФайлВыгрузки           = Новый XBase;
	ФайлВыгрузки.Кодировка = ?(КодировкаФайла = 0, КодировкаXBase.OEM, КодировкаXBase.ANSI);
	ФайлВыгрузки.Поля.Добавить("SURNAME",    "S", 25);
	ФайлВыгрузки.Поля.Добавить("NAME",       "S", 20);
	ФайлВыгрузки.Поля.Добавить("SECNAME",    "S", 20);
	ФайлВыгрузки.Поля.Добавить("BIRTHDAY",   "D", 8);
	ФайлВыгрузки.Поля.Добавить("POST",       "S", 6);
	ФайлВыгрузки.Поля.Добавить("TOWN",       "S", 100);
	ФайлВыгрузки.Поля.Добавить("STREET",     "S", 100);
	ФайлВыгрузки.Поля.Добавить("HOUSE",      "N", 4);
	ФайлВыгрузки.Поля.Добавить("KORPUS",     "S", 3);
	ФайлВыгрузки.Поля.Добавить("FRACTION",   "S", 3);
	ФайлВыгрузки.Поля.Добавить("FLAT",       "N", 4);
	ФайлВыгрузки.Поля.Добавить("LITERA",     "S", 3);
	ФайлВыгрузки.Поля.Добавить("MONTH",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("YEAR",       "N", 4);
	ФайлВыгрузки.Поля.Добавить("NUMBER",     "N", 2);
	ФайлВыгрузки.Поля.Добавить("SQ_METR",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("HOUSING",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("WATER",      "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ODN_W",      "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("NORM_W",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("TARIF_W",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("MES_W",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("SEVERAGE",   "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ODN_S",      "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("NORM_S",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("TARIF_S",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("MES_S",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("HEAT",       "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ODN_H",      "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("NORM_H",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("TARIF_H",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("MES_H",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("HOT_WAT",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ODN_HW",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("NORM_HW",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("TARIF_HW",   "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("MES_HW",     "N", 2);
	ФайлВыгрузки.Поля.Добавить("ENERGY",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ODN_E",      "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("NORM_E",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("TARIF_E",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("MES_E",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("GAS",        "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("NORM_G",     "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("TARIF_G",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("MES_G",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("GARBAGE",    "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ENERGY_H",   "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("GAS_HEAT",   "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ULICA",      "N", 3);
	ФайлВыгрузки.Поля.Добавить("KOD",        "N", 7);
	ФайлВыгрузки.Поля.Добавить("KODRC",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("NOM_LC",     "S", 15);
	ФайлВыгрузки.Поля.Добавить("DAT_BEG",    "D", 8);
	ФайлВыгрузки.Поля.Добавить("DAT_END",    "D", 8);
	ФайлВыгрузки.Поля.Добавить("ARREST",     "N", 10);
	ФайлВыгрузки.Поля.Добавить("ACC_ORG",    "S", 20);
	ФайлВыгрузки.Поля.Добавить("REPAIR",     "N", 10, 2);
	
	ФайлВыгрузки.СоздатьФайл(КаталогВыгрузки + "\uszn3.dbf",);
	ФайлВыгрузки.АвтоСохранение = Истина;
	
	Для каждого ТекСтрока Из Объект.ВолгоградскаяОбласть_ТаблицаНачислений Цикл
		ФайлВыгрузки.Добавить();
		ЗаполнитьЗначенияСвойств(ФайлВыгрузки, ТекСтрока);
		ФайлВыгрузки.HOUSE = Число(СокрЛП(ТекСтрока.HOUSE));
	КонецЦикла;
	
	ФайлВыгрузки.ЗакрытьФайл();
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Таблица начислений выгружена в файл " + КаталогВыгрузки + "\uszn3.dbf");
	
	ФайлВыгрузки           = Новый XBase;
	ФайлВыгрузки.Кодировка = ?(КодировкаФайла = 0, КодировкаXBase.OEM, КодировкаXBase.ANSI);
	ФайлВыгрузки.Поля.Добавить("SURNAME",    "S", 25);
	ФайлВыгрузки.Поля.Добавить("NAME",       "S", 20);
	ФайлВыгрузки.Поля.Добавить("SECNAME",    "S", 20);
	ФайлВыгрузки.Поля.Добавить("NOM_LC",     "S", 15);
	ФайлВыгрузки.Поля.Добавить("SUMMA",      "N", 10, 2);
	ФайлВыгрузки.Поля.Добавить("ACC_ORG",    "S", 20);
	ФайлВыгрузки.Поля.Добавить("MONTH",      "N", 2);
	ФайлВыгрузки.Поля.Добавить("YEAR",       "N", 4);
	
	ФайлВыгрузки.СоздатьФайл(КаталогВыгрузки + "\zuszn3.dbf",);
	ФайлВыгрузки.АвтоСохранение = Истина;
	
	Для каждого ТекСтрока Из Объект.ВолгоградскаяОбласть_ТаблицаЗадолженностей Цикл
		ФайлВыгрузки.Добавить();
		ЗаполнитьЗначенияСвойств(ФайлВыгрузки, ТекСтрока);
	КонецЦикла;
	
	ФайлВыгрузки.ЗакрытьФайл();
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Таблица задолженностей выгружена в файл " + КаталогВыгрузки + "\zuszn3.dbf");
	УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Выгрузка таблиц в файлы завершена!");
	
	#КонецЕсли
	
КонецПроцедуры // ВыгрузитьДанныеВФайлВолгоградскаяОбласть()

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Организация = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	ТекРабДата = УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьРабочуюДату();
	Объект.Период = ТекРабДата;
	
	ВосстановитьНастройки();
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.Период", "ПериодСтрокой");
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗакрытием" формы.
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "КомандаНастройка".
Процедура КомандаНастройка(Команда)
	
	АдресНастроекВХранилище = ПоместитьНастройкиВХранилище();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаНастройкаЗавершение", ЭтаФорма);
	ОткрытьФорму("Обработка.УПЖКХ_ВыгрузкиВОрганыСоцЗащиты.Форма.ВолгоградскаяОбласть_ФормаНастройки", Новый Структура("АдресНастроекВХранилище", АдресНастроекВХранилище), , , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
// Обработчик результата опроса команды "КомандаНастройка".
Процедура КомандаНастройкаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		ПолучитьНастройкиИзХранилища(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "КомандаЗагрузитьИзФайла".
Процедура КомандаЗагрузитьИзФайла(Команда)
	
	ТекстОшибки = ПроверкаУсловийДляЗаполненияТаблиц();
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ФайлНаДиске = Новый Файл(ИмяФайлаЗагрузки);
	ФайлНаДиске.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ПроверкаСуществованияФайлаЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСуществованияФайлаЗавершение(Существует, ДополнительныеПараметры) Экспорт
	
	Если НЕ Существует Тогда
		ПоказатьПредупреждение(, "Файл не найден!");
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = "";
	Описание = Новый ОписаниеОповещения("ОбработатьРезультатПомещенияФайлаВХранилище", ЭтаФорма);
	НачатьПомещениеФайла(Описание, ПутьКФайлу, ИмяФайлаЗагрузки, Ложь);

КонецПроцедуры

&НаКлиенте
// Обработчик результата помещения файла в хранилище.
Процедура ОбработатьРезультатПомещенияФайлаВХранилище(Результат, Адрес, ВыбранноеИмяФайла, ДопПараметры) Экспорт
	
	Если Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Количество() > 0
	 ИЛИ Объект.ВолгоградскаяОбласть_ТаблицаЗадолженностей.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("КомандаЗагрузитьИзФайлаЗавершение", ЭтотОбъект, Новый Структура("ПутьКФайлу", Адрес));
		
		ТекстВопроса = "Перед заполнением таблицы выгрузки будут очищены. Заполнить?";
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Началось заполнение таблиц выгрузки!");
		
		ЗагрузитьНаСервере(Адрес);
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Заполнение таблиц выгрузки завершено!");
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьРезультатПомещенияФайлаВХранилище()

&НаКлиенте
// Обработчик результата опроса команды "ЗагрузитьИзФайла".
Процедура КомандаЗагрузитьИзФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.ВолгоградскаяОбласть_ТаблицаНачислений.Очистить();
		Объект.ВолгоградскаяОбласть_ТаблицаЗадолженностей.Очистить();
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Началось заполнение таблиц выгрузки!");
		
		ЗагрузитьНаСервере(ДополнительныеПараметры.ПутьКФайлу);
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Заполнение таблиц выгрузки завершено!");
		
	КонецЕсли;
	
КонецПроцедуры // КомандаЗаполнитьВсеЗавершение()

&НаКлиенте
// Обработчик команды "КомандаВыгрузить".
Процедура КомандаВыгрузить(Команда)
	
	Если ПустаяСтрока(КаталогВыгрузки) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не указан каталог выгрузки!");
		Возврат;
	КонецЕсли;
	
	КаталогНаДиске = Новый Файл(КаталогВыгрузки);
	КаталогНаДиске.НачатьПроверкуСуществования(Новый ОписаниеОповещения("КомандаВыгрузитьЗавершение", ЭтотОбъект, Новый Структура("КаталогНаДиске", КаталогНаДиске)));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьЗавершение(Существует, ДополнительныеПараметры) Экспорт
	
	КаталогНаДиске = ДополнительныеПараметры.КаталогНаДиске;
	
	
	Если НЕ Существует Тогда
		ПоказатьПредупреждение(Неопределено, "Каталог выгрузки не найден!");
		Возврат;
	КонецЕсли;
	
	КаталогНаДиске.НачатьПроверкуЭтоКаталог(Новый ОписаниеОповещения("ПроверкаНаКаталогЗавершение", ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура ПроверкаНаКаталогЗавершение(ЭтоКаталог, ДополнительныеПараметры) Экспорт
	
	
	Если НЕ ЭтоКаталог Тогда
		ПоказатьПредупреждение(Неопределено, "Каталог выгрузки не найден!");
		Возврат;
	КонецЕсли;
	
	ВыгрузитьДанныеВФайлВолгоградскаяОбласть();

КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

////////////////////////
// Диалог выбора файла

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля ввода "Файл загрузки".
//
Процедура ИмяФайлаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр                      = "Файл данных (*.dbf)|*.dbf";
	ДиалогВыбораФайла.Расширение                  = "dbf";
	ДиалогВыбораФайла.Заголовок                   = "Выберите файл загрузки";
	ДиалогВыбораФайла.ПолноеИмяФайла              = ИмяФайлаЗагрузки;
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.ИндексФильтра               = 0;
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ИмяФайлаЗагрузкиНачалоВыбораЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаЗагрузкиНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		ИмяФайлаЗагрузки = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля ввода "Файл загрузки".
//
Процедура ИмяФайлаЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УПЖКХ_ОбщегоНазначенияКлиент.ОткрытьСтраницуВБраузере("explorer " + ИмяФайлаЗагрузки);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля ввода "Имя каталога".
//
Процедура ИмяКаталогаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок                   = "Выбор каталога файлов";
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.ИндексФильтра               = 0;
	ДиалогВыбораФайла.Каталог                     = КаталогВыгрузки;
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ИмяКаталогаЗагрузкиНачалоВыбораЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяКаталогаЗагрузкиНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		КаталогВыгрузки = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля ввода "Имя каталога".
//
Процедура ИмяКаталогаЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УПЖКХ_ОбщегоНазначенияКлиент.ОткрытьСтраницуВБраузере(КаталогВыгрузки);
	
КонецПроцедуры

////////////////////////
// Период

&НаКлиенте
// Обработчик события "ПриИзменении" поля "ПериодСтрокой".
Процедура ПериодСтрокойПриИзменении(Элемент)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.Период", "ПериодСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "НачалоВыбора" поля "ПериодСтрокой".
Процедура ПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.Период", "ПериодСтрокой");
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Регулирование" поля "ПериодСтрокой".
Процедура ПериодСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.Период", "ПериодСтрокой", Направление,
														Модифицированность);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "АвтоПодбор" поля "ПериодСтрокой".
Процедура ПериодСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ОкончаниеВводаТекста" поля "ПериодСтрокой".
Процедура ПериодСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
