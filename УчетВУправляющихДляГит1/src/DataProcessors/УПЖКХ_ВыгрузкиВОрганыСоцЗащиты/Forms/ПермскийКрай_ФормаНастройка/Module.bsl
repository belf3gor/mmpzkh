
//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получаем настройки из хранилища.
	Если Параметры.Свойство("АдресНастроекВХранилище") И Не Параметры.АдресНастроекВХранилище = Неопределено Тогда
		
		СтруктураНастроек = ПолучитьИзВременногоХранилища(Параметры.АдресНастроекВХранилище);
		Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
			
			// Сохраненная ранее таблица.
			врНастройкиТаблицаУслуг = СтруктураНастроек.ПермскийКрай_НастройкиТаблицаУслуг;
			Если ТипЗнч(врНастройкиТаблицаУслуг) = Тип("ТаблицаЗначений") Или врНастройкиТаблицаУслуг.Количество() <> 0 Тогда
				Объект.ПермскийКрай_НастройкиТаблицаУслуг.Загрузить(врНастройкиТаблицаУслуг);
			КонецЕсли;
			
			// Данные для правой колоки таблицы соответствий услуг.
			врТаблицаУслуг = СтруктураНастроек.ПермскийКрай_ТаблицаУслуг;
			Если ТипЗнч(врТаблицаУслуг) = Тип("ТаблицаЗначений") Тогда
				Элементы.НастройкиТаблицаУслугКолонка1.СписокВыбора.ЗагрузитьЗначения(врТаблицаУслуг.ВыгрузитьКолонку("NAME"));
			КонецЕсли;
			
			// Услуги кап.ремонта.
			врМассивЗначенийУслугиКапРемонта = СтруктураНастроек.МассивЗначенийУслугиКапРемонта;
			Если ТипЗнч(врМассивЗначенийУслугиКапРемонта) = Тип("Массив") Тогда
				УслугиКапРемонта.ЗагрузитьЗначения(врМассивЗначенийУслугиКапРемонта);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УслугиКапРемонта.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.КВП_Услуги");
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

////////////////////////
// Загрузить

&НаКлиенте
// Обработчик команды "КомандаЗагрузить".
Процедура КомандаЗагрузить(Команда)
	
	Если НЕ Объект.ПермскийКрай_НастройкиТаблицаУслуг.Количество() = 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("КомандаЗагрузитьЗавершение", ЭтаФорма);
		
		ТекстВопроса = "Перед заполением таблица будет очищена. Заполнить?";
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		КомандаЗагрузитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик результата вопроса команды "КомандаЗагрузить".
Процедура КомандаЗагрузитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	Иначе
		КомандаЗагрузитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Продолжение общей части обработчика команды "КомандаЗагрузить" на сервере.
Процедура КомандаЗагрузитьНаСервере()
	
	Объект.ПермскийКрай_НастройкиТаблицаУслуг.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КВП_Услуги.Ссылка
	|ИЗ
	|	Справочник.КВП_Услуги КАК КВП_Услуги";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Результат Цикл
		НоваяСтрока = Объект.ПермскийКрай_НастройкиТаблицаУслуг.Добавить();
		НоваяСтрока.Колонка2 = Строка.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////
// Сохранить

&НаКлиенте
// Обработчик команды "КомандаСохранить".
Процедура КомандаСохранить(Команда)
	
	АдресНастроекВХранилище = ПоместитьНастройкиВХранилище(); 
	Закрыть(АдресНастроекВХранилище);
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// РАБОТА С ВРЕМЕННЫМ ХРАНИЛИЩЕМ

#Область РаботаСВременнымХранилищем

&НаСервере
// Помещает таблицу услуг во временное хранилище.
//
// Возвращаемое значение:
//  Строка - адрес настроек во временном хранилище.
//
Функция ПоместитьНастройкиВХранилище()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПермскийКрай_НастройкиТаблицаУслуг", Объект.ПермскийКрай_НастройкиТаблицаУслуг.Выгрузить());
	СтруктураПараметров.Вставить("МассивЗначенийУслугиКапРемонта",     УслугиКапРемонта.ВыгрузитьЗначения());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураПараметров, Новый УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти