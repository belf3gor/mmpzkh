#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет в переданной таблице ПараметрыЗадач колонку статус.
// Добавляет колонку СведенияОРасчетеСуммы и заполняет ее.
//
// Параметры:
//  ПараметрыЗадачи - ТаблицаЗначений - Состав колонок см. НовыеПараметрыЗадачи()
//
Процедура ЗаполнитьСтатусыЗадач(ТаблицаЗадач) Экспорт
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		ПараметрыЗадачи = НовыеПараметрыЗадачи();
		ЗаполнитьЗначенияСвойств(ПараметрыЗадачи, Задача);
		
		ПараметрыЗадачи.ПериодОтчета = ПериодОтчетаПоПравилуУплаты(Задача.ПравилоУплаты, Задача.ПериодСобытия);
		ПараметрыЗадачи.ПравилоОтчета =
			ПравилоОтчетаПоПравилуУплаты(Задача.ПравилоУплаты, Задача.Организация, ПараметрыЗадачи.ПериодОтчета);
		
		СведенияОРасчетеСуммы  = СведенияОРасчетеСуммы(ПараметрыЗадачи);
		Задача.Статус = СтатусУплатыНалога(СведенияОРасчетеСуммы);
		
	КонецЦикла;
	
КонецПроцедуры

// Подготавливает сведения о расчете, уплате и состоянии сверки по задаче
//
// Параметры:
//  ПараметрыЗадачи - Структура - см. НовыеПараметрыЗадачи()
//  АдресХранилища  - Строка - адрес хранилища результата
//
Процедура СведенияПоЗадаче(ПараметрыЗадачи, АдресХранилища) Экспорт
	
	СоставРазделов  = ПараметрыЗадачи.СоставРазделов;
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("СоставРазделов", СоставРазделов);
	
	Если СоставРазделов.РасчетУплата Тогда
		
		СведенияОРасчетеСуммы = СведенияОРасчетеСуммы(ПараметрыЗадачи);
		
		Если ЗначениеЗаполнено(ПараметрыЗадачи.ПравилоУплаты) Тогда
			
			КлючЗадачиУплаты = Новый Структура; // Имитирует ключ записи ЗадачиБухгалтера
			КлючЗадачиУплаты.Вставить("Организация",   ПараметрыЗадачи.Организация);
			КлючЗадачиУплаты.Вставить("Правило",       ПараметрыЗадачи.ПравилоУплаты); 
			КлючЗадачиУплаты.Вставить("ПериодСобытия", ПараметрыЗадачи.ПериодСобытия); // период, за который уплачивается налог
			КлючЗадачиУплаты.Вставить("РегистрацияВНалоговомОргане",
			                                           ПараметрыЗадачи.РегистрацияВНалоговомОргане);
			
			ВыполнениеЗадачБухгалтера.ПроверитьАктуальностьСтатуса(КлючЗадачиУплаты, СтатусУплатыНалога(СведенияОРасчетеСуммы));
			
		КонецЕсли;
		
		РезультатВыполнения.Вставить("СведенияОРасчетеСуммы", СведенияОРасчетеСуммы);
		
	КонецЕсли;
	
	Если СоставРазделов.Сверка Тогда
		ПараметрыСверки = ВыполнениеЗадачБухгалтера.НовыйПараметрыЗадачиДляСверки();
		ЗаполнитьЗначенияСвойств(ПараметрыСверки, ПараметрыЗадачи);
		ПараметрыСверки.Правило = ПараметрыЗадачи.ПравилоУплаты;// если нет правила уплаты, то сверка заведомо не нужна
		СведенияОСверке = ВыполнениеЗадачБухгалтера.СведенияОСверке(ПараметрыСверки);
		РезультатВыполнения.Вставить("СведенияОСверке", СведенияОСверке);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатВыполнения, АдресХранилища);
	
КонецПроцедуры

// Функция-конструктор структуры, описывающей параметры задачи
//
Функция НовыеПараметрыЗадачи() Экспорт
	
	ПараметрыЗадачи = Новый Структура();
	
	ПараметрыЗадачи.Вставить("Организация",                 Справочники.Организации.ПустаяСсылка());
	ПараметрыЗадачи.Вставить("ПравилоУплаты",               Неопределено); // СправочникСсылка.Патенты, СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
	ПараметрыЗадачи.Вставить("ПравилоОтчета",               Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	ПараметрыЗадачи.Вставить("ПериодСобытия",               '00010101');
	ПараметрыЗадачи.Вставить("ПериодОтчета",                '00010101');
	ПараметрыЗадачи.Вставить("Срок",                        '00010101');
	ПараметрыЗадачи.Вставить("Описание",                    "");
	ПараметрыЗадачи.Вставить("РегистрацияВНалоговомОргане", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ПараметрыЗадачи.Вставить("СоставРазделов",              Новый Структура("РасчетУплата, Сверка", Ложь, Ложь));
	ПараметрыЗадачи.Вставить("ИдентификаторЗадачи",         "");
	ПараметрыЗадачи.Вставить("Периодичность",               Перечисления.Периодичность.ПустаяСсылка());
	ПараметрыЗадачи.Вставить("Статус",                      "");
	
	Возврат ПараметрыЗадачи;
	
КонецФункции

// Функция-конструктор таблицы, описывающей параметры задач
//
Функция НовыеПараметрыЗадач() Экспорт
	
	ПараметрыЗадач = Новый ТаблицаЗначений;
	
	Для Каждого ЭлементСтруктуры Из НовыеПараметрыЗадачи() Цикл
		ПараметрыЗадач.Колонки.Добавить(ЭлементСтруктуры.Ключ,
			Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗнч(ЭлементСтруктуры.Значение))));
	КонецЦикла;
	
	Возврат ПараметрыЗадач;
	
КонецФункции

// Возвращает правило уплаты налога по правилу представления отчета.
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета
//  Организация - СправочникСсылка.Организации - отбор правил по организации
//  ПериодСобытия - Дата - отбор правил по периоду
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога или пустая ссылка,
//  если оплата не требуется
//
Функция ПравилоУплатыПоПравилуОтчета(ПравилоОтчета, Организация, ПериодСобытия) Экспорт
	
	Если Не ОтчетыИспользуютсяДляУплатыНалога(ПравилоОтчета) Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяЗадачи",     ПравилоОтчета.Владелец.Код);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ПериодСобытия", ПериодСобытия);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Правило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &ИмяЗадачи
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Правило;
	
КонецФункции

// Возвращает правило представления отчета по правилу уплаты налога.
//
// Параметры:
//  Правило       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога
//  Организация   - СправочникСсылка.Организации - отбор правил по организации
//  ПериодОтчета  - Дата - дата, обозначающая период, по данным которого уплачивается налог
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета; 
//                                                              если не найдено, возвращается пустая ссылка
//
Функция ПравилоОтчетаПоПравилуУплаты(ПравилоУплаты, Организация, ПериодОтчета) Экспорт
	
	Если Не ОтчетыИспользуютсяДляУплатыНалога(ПравилоУплаты) Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	// Ищем задачу по представлению этого отчета, а из нее - подходящее правило
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задача",       ПравилоУплаты.Владелец);
	Запрос.УстановитьПараметр("ПериодОтчета", ПериодОтчета);
	Запрос.УстановитьПараметр("Организация",  Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаОтчета
	|		ПО ЗадачиБухгалтера.Правило = ПравилаОтчета.Ссылка
	|ГДЕ
	|	ПравилаОтчета.Владелец = &Задача
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодОтчета
	|	И ПравилаОтчета.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Правило";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТаблицаЗадач = Результат.Выгрузить();
		ПравилоОтчета = ПодобратьРелевантноеПравилоОтчета(ТаблицаЗадач.ВыгрузитьКолонку("Правило"), ПравилоУплаты);
		Возврат ПравилоОтчета;
	КонецЕсли;
	
	// Задачи за базовый период еще могут быть не созданы
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяЗадачи", ПравилоУплаты.Владелец.Код);
	Отбор.Вставить("Действие",  Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
	
	// Принцип составления расписания: в течение периода обзора период должен завершиться.
	НачалоСоставленияРасписания = ПериодОтчета; 
	КонецСоставленияРасписания  = КонецДня(ПериодОтчета) + 1;
	Расписание = РегистрыСведений.ЗадачиБухгалтера.РасписаниеПоНалогамОтчетамЗаПериод(
		Организация,
		НачалоСоставленияРасписания,
		КонецСоставленияРасписания,
		Отбор);
	
	Если ЗначениеЗаполнено(Расписание) Тогда
		Расписание.Сортировать("РегистрацияВНалоговомОргане, Правило", Новый СравнениеЗначений);
		ПравилоОтчета = ПодобратьРелевантноеПравилоОтчета(Расписание.ВыгрузитьКолонку("Правило"), ПравилоУплаты);
		Возврат ПравилоОтчета;
	КонецЕсли;
	
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	
КонецФункции

// Определяет период отчета, по данным которого уплачивается налог.
// В общем случае налог может уплачиваться на основании отчета, представленного в другом (базовом, более раннем) периоде.
// 
// Параметры:
//  Правило       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога
//  Организация   - СправочникСсылка.Организации - отбор правил по организации
//  ПериодСобытия - Дата - дата, обозначающая период, за который уплачивается налог
//
// Возвращаемое значение:
//  Дата - последний день периода, по данным которого определена сумма налога
//         (более строго: по данным которого представляется отчет, содержащий данные о налоге, подлежащем уплате за ПериодСобытия)
//
Функция ПериодОтчетаПоПравилуУплаты(ПравилоУплаты, ПериодСобытия) Экспорт
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПравилоУплаты.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПравилоУплаты.Периодичность, ПериодСобытия);
	
	ПоказателиБазовогоПериода = ВыполнениеЗадачБухгалтера.ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, ПравилоУплаты);
	Возврат НачалоДня(ПоказателиБазовогоПериода.КонецБазовогоПериода);// Периоды событий в информационной базе - без учета времени
	
КонецФункции

// Определяет представление декларации на форме (для пользователя).
//
// Параметры:
//  Декларация	 - ДокументСсылка.РегламентированныйОтчет  - декларация.
// 
// Возвращаемое значение:
//  Строка - представление декларации на форме (для пользователя).
//
Функция ПредставлениеДекларации(Декларация) Экспорт
	
	ТипДокумента = ТипЗнч(Декларация);
	
	Если ТипДокумента <> Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Возврат "";
	КонецЕсли;
	
	СостояниеОтправки = ИнтерфейсыВзаимодействияБРО.ПредставлениеСостоянияДокумента(Декларация);
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Декларация,
		"НаименованиеОтчета, ПредставлениеПериода");
	
	ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 за %2 (%3)'"),
		РеквизитыДокумента.НаименованиеОтчета,
		РеквизитыДокумента.ПредставлениеПериода,
		СостояниеОтправки);
	
	Возврат ПредставлениеДокумента;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СведенияОРасчетеСуммы

Функция СведенияОРасчетеСуммы(ПараметрыЗадачи)
	
	СведенияОРасчете = НовыйСведенияОРасчете();
	
	Если Не ЗначениеЗаполнено(ПараметрыЗадачи.ПравилоУплаты) Тогда
		
		// только отчет
		ЗаполнитьСведенияПодготовкаОтчета(ПараметрыЗадачи, СведенияОРасчете);
		
	Иначе
		
		СпособВыполненияЗадачи = ВыполнениеЗадачБухгалтера.СпособВыполненияЗадачи(ПараметрыЗадачи.ПравилоУплаты);
		
		Если СпособВыполненияЗадачи = "УплатаНДФЛ" Тогда
			ЗаполнитьСведенияУплатаНДФЛ(ПараметрыЗадачи, СведенияОРасчете);
		ИначеЕсли СпособВыполненияЗадачи = "УплатаСтраховыхВзносов" Тогда
			ЗаполнитьСведенияУплатаСтраховыхВзносов(ПараметрыЗадачи, СведенияОРасчете);
		ИначеЕсли СпособВыполненияЗадачи = "УплатаАвансовЗемельныйТранспортныйНалог" Тогда
			ЗаполнитьСведенияУплатаАвансовПоИмущественнымНалогам(ПараметрыЗадачи, СведенияОРасчете);
		ИначеЕсли СпособВыполненияЗадачи = "УплатаТорговогоСбора" Тогда
			ЗаполнитьСведенияУплатаТорговогоСбора(ПараметрыЗадачи, СведенияОРасчете);
		ИначеЕсли СпособВыполненияЗадачи = "УплатаНалогаНаПрофессиональныйДоход" Тогда
			ЗаполнитьСведенияУплатаНалогаНаПрофессиональныйДоход(ПараметрыЗадачи, СведенияОРасчете);
		ИначеЕсли ЗначениеЗаполнено(ПараметрыЗадачи.ПравилоОтчета) Тогда
			ЗаполнитьСведенияУплатаПоДекларации(ПараметрыЗадачи, СведенияОРасчете);
		Иначе
			ЗаполнитьСведенияНетСведений(ПараметрыЗадачи, СведенияОРасчете); // заполнение платежки стандартными реквизитами, без сумм
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СведенияОРасчете;
	
КонецФункции

Процедура ЗаполнитьСведенияУплатаНДФЛ(ПараметрыЗадачи, СведенияОРасчете)
	
	Организация                 = ПараметрыЗадачи.Организация;
	Правило                     = ПараметрыЗадачи.ПравилоУплаты;
	ПериодСобытия               = ПараметрыЗадачи.ПериодСобытия;
	РегистрацияВНалоговомОргане = ПараметрыЗадачи.РегистрацияВНалоговомОргане;
	Налог                       = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.НДФЛ);
	КБК                         = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, , ПериодСобытия);
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Перечисления.Периодичность.Месяц, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Перечисления.Периодичность.Месяц, ПериодСобытия);
	СчетУчета     = Справочники.ВидыНалоговИПлатежейВБюджет.СчетУчета(Налог, ПериодСобытия);
	УчетВРазрезеНалоговыхОрганов = НалоговыйУчет.УчетВРазрезеНалоговыхОрганов();
	КодТерритории                = Справочники.Организации.КодТерриторииМестаРегистрации(Организация);
	ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(КодТерритории, ПериодСобытия);
	ВидГосударственногоОргана = ПлатежиВБюджетПереопределяемый.ВидГосударственногоОргана(КБК);
	КодГосударственногоОргана = ДанныеГосударственныхОрганов.КодГосударственногоОрганаОрганизации(
		Организация,
		ВидГосударственногоОргана);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("СчетУчета", СчетУчета);
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет);
	НалоговыйУчетОбособленныхПодразделений.ДобавитьВидСубконтоРегистрацияВНалоговомОргане(ВидыСубконто);
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	ТекстЗапроса = // модифицируется при помощи СхемаЗапроса
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Счет КАК СчетУчета,
	|	ХозрасчетныйОбороты.Субконто1 КАК ВидНалоговогоОбязательства,
	|	ХозрасчетныйОбороты.Субконто2 КАК РегистрацияВНалоговомОргане,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.РегистрацииВНалоговомОргане).Код КАК КодНалоговогоОргана,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.РегистрацииВНалоговомОргане).КодПоОКТМО КАК ОКАТО,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Счет = &СчетУчета,
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И Субконто1 = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	НЕ(ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.СписаниеСРасчетногоСчета
	|				ИЛИ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Субконто2,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.РегистрацииВНалоговомОргане).Код,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.РегистрацииВНалоговомОргане).КодПоОКТМО
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетУчета,
	|	ВидНалоговогоОбязательства,
	|	ХозрасчетныйОбороты.Субконто2,
	|	Сумма";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Если НЕ УчетВРазрезеНалоговыхОрганов Тогда
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Удалить(2); // РегистрацияВНалоговомОргане
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Удалить(2); // КодНалоговогоОргана
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Удалить(2); // ОКАТО
		СхемаЗапроса.ПакетЗапросов[0].Порядок.Удалить(2);// Упорядочивание по Субконто2
	КонецЕсли;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Платежи   = ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи();
	Пока Выборка.Следующий() Цикл
		Платеж = Платежи.Добавить();
		ЗаполнитьЗначенияСвойств(Платеж, Выборка);
		Платеж.Налог = Налог;
		Платеж.КБК   = КБК;
		Платеж.Наименование = Строка(Платеж.Налог);
		Если НЕ УчетВРазрезеНалоговыхОрганов Тогда
			Платеж.ОКАТО               = КодТерритории;
			Платеж.КодНалоговогоОргана = КодГосударственногоОргана;
		КонецЕсли;
	КонецЦикла;
	
	Если Платежи.Количество() > 0 Тогда
		ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация);
	КонецЕсли;
		
	// Для уплаты НДФЛ таблица расчета совпадает с таблицей платежей.
	// Заполним таблицу расчета
	ТаблицаРасчета = НовыйТаблицаРасчета();
	Для Каждого Платеж Из Платежи Цикл
		Расчет = ТаблицаРасчета.Добавить();
		Расчет.НаименованиеПоказателя = Платеж.Наименование;
		Расчет.ЗначениеПоказателя     = Платеж.Сумма;
		Расчет.ОКТМО                  = Платеж.ОКАТО;
	КонецЦикла;
	
	Если Платежи.Количество() > 1 Тогда 
		ИтогРасчета = ТаблицаРасчета.Добавить();
		ИтогРасчета.НаименованиеПоказателя = НСтр("ru = 'Итого'");
		ИтогРасчета.ЗначениеПоказателя     = Платежи.Итог("Сумма");
		ИтогРасчета.ЭтоИтоговаяСтрока      = Истина;
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежных документов'");
	Иначе
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежного документа'");
	КонецЕсли;
	
	СведенияОРасчете.РасчетСуммыВыполнен    = Истина;
	СведенияОРасчете.ИнформацияРасчетСуммы  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сумма платежа рассчитывается на основании данных по счету %1'"), СчетУчета);
	СведенияОРасчете.ТаблицаПлатежей        = Платежи;
	СведенияОРасчете.ТаблицаРасчета         = ТаблицаРасчета;
	СведенияОРасчете.ПоказательПериода      = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(
		ПериодСобытия, ПлатежиВБюджетКлиентСервер.ПериодичностьМесяц());
	СведенияОРасчете.ЕстьОплата             = ЕстьОплата(Платежи);
	СведенияОРасчете.ИнформацияУплатаНалога = ИнформацияУплатаНалога;
	СведенияОРасчете.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.НДФЛ);
	СведенияОРасчете.КБК = КБК;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУплатаСтраховыхВзносов(ПараметрыЗадачи, СведенияОРасчете)
	
	Организация   = ПараметрыЗадачи.Организация;
	ПериодСобытия = ПараметрыЗадачи.ПериодСобытия;
	ИмяДокумента  = ЗакрытиеМесяца.ВидДокументаНачисленияЗарплаты(Организация);
	
	// Убедимся, что выполнена регламентная операция начисления взносов.
	ДокументЗарплаты    = Неопределено;
	НесколькоДокументов = Ложь;
	ОперацияВыполнена   = ВыполнениеЗадачБухгалтера.ОперацияНачисленияВзносовВыполнена(Организация, ПериодСобытия, ДокументЗарплаты, НесколькоДокументов);
	
	Если ОперацияВыполнена Тогда
		СведенияПоУплатеСтраховыхВзносов(ПараметрыЗадачи, СведенияОРасчете);
		Возврат;
	КонецЕсли;
	
	// Операция не выполнена.
	// Предложим выполнить регламентную операцию.
	ОписаниеДействияРасчет = СведенияОРасчете.ОписаниеДействияРасчет;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Организация",       Организация);
	Отбор.Вставить("МесяцНачисления",   НачалоМесяца(ПериодСобытия));
	Отбор.Вставить("ПериодРегистрации", НачалоМесяца(ПериодСобытия));
	
	Если ИмяДокумента = "ОтражениеЗарплатыВУчете" Тогда
		// Откроем форму обработки загрузки.
		ОписаниеДействияРасчет.ИмяФормы       = "Обработка.ЗагрузкаДанныхИзЗУП.Форма";
		ОписаниеДействияРасчет.ПараметрыФормы = Отбор;
		ОписаниеДействияРасчет.Наименование   = НСтр("ru = 'Загрузить данные о зарплате и взносах'");
	Иначе
		
		Если ИмяДокумента = "НачислениеЗарплаты" Тогда
			ОписаниеДействияРасчет.Наименование = НСтр("ru = 'Начислить зарплату и взносы'");
		Иначе
			ОписаниеДействияРасчет.Наименование = НСтр("ru = 'Ввести данные о зарплате и взносах'");
		КонецЕсли;
		
		// Откроем форму документа или списка (если документов несколько).
		Если НесколькоДокументов Тогда
			ОписаниеДействияРасчет.ИмяФормы = "Документ."+ИмяДокумента+".ФормаСписка";
			ОписаниеДействияРасчет.ПараметрыФормы = Отбор;
		ИначеЕсли ЗначениеЗаполнено(ДокументЗарплаты) Тогда
			ОписаниеДействияРасчет.ИмяФормы = "Документ."+ИмяДокумента+".ФормаОбъекта";
			ОписаниеДействияРасчет.ПараметрыФормы = Новый Структура("Ключ", ДокументЗарплаты);
		Иначе
			// Документ НачислениеЗарплаты принимает месяц нового документа через специальный параметр формы.
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("МесяцНачисленияНовогоДокумента", НачалоМесяца(ПериодСобытия)); 
			ПараметрыФормы.Вставить("ЗначенияЗаполнения",             Отбор);
			
			ОписаниеДействияРасчет.ИмяФормы = "Документ."+ИмяДокумента+".ФормаОбъекта";
			ОписаниеДействияРасчет.ПараметрыФормы = ПараметрыФормы;
		КонецЕсли;
	КонецЕсли;
	
	СведенияОРасчете.ИнформацияРасчетСуммы  =
		НСтр("ru = 'Сумма платежа рассчитывается на основании данных на счетах страховых взносов'");
	СведенияОРасчете.ПоказательПериода      = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПериодСобытия,
		ПлатежиВБюджетКлиентСервер.ПериодичностьМесяц());
	СведенияОРасчете.ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежного документа'");
	
КонецПроцедуры

Процедура СведенияПоУплатеСтраховыхВзносов(ПараметрыЗадачи, СведенияОРасчете)
	
	Платежи = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыСтраховыхВзносов(
		ПараметрыЗадачи.ПравилоУплаты,
		ПараметрыЗадачи.Организация,
		ПараметрыЗадачи.ПериодСобытия);
		
	// Для уплаты страховых взносов таблица расчета совпадает с таблицей платежей.
	// Заполним таблицу расчета
	ТаблицаРасчета = НовыйТаблицаРасчета();
	Для каждого Платеж Из Платежи Цикл
		Расчет = ТаблицаРасчета.Добавить();
		Расчет.НаименованиеПоказателя = Платеж.Наименование;
		Расчет.ЗначениеПоказателя     = Платеж.Сумма;
		Расчет.ОКТМО                  = Платеж.ОКАТО;
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Платеж.Налог, "ВидНалога") = Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ Тогда
			Платеж.НалоговыйПериод = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
		Иначе
			Платеж.НалоговыйПериод = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПараметрыЗадачи.ПериодСобытия,
				ПлатежиВБюджетКлиентСервер.ПериодичностьМесяц());
		КонецЕсли;
	КонецЦикла;
	
	Если Платежи.Количество() > 1 Тогда 
		ИтогРасчета = ТаблицаРасчета.Добавить();
		ИтогРасчета.НаименованиеПоказателя = НСтр("ru = 'Итого'");
		ИтогРасчета.ЗначениеПоказателя     = Платежи.Итог("Сумма");
		ИтогРасчета.ЭтоИтоговаяСтрока      = Истина;
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежных документов'");
	Иначе
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежного документа'");
	КонецЕсли;
	
	СведенияОРасчете.РасчетСуммыВыполнен    = Истина;
	СведенияОРасчете.ИнформацияРасчетСуммы  = 
		НСтр("ru = 'Сумма платежа рассчитывается на основании данных на счетах страховых взносов'");
	СведенияОРасчете.ТаблицаПлатежей        = Платежи;
	СведенияОРасчете.ТаблицаРасчета         = ТаблицаРасчета;
	СведенияОРасчете.ЕстьОплата             = ЕстьОплата(Платежи);
	СведенияОРасчете.ИнформацияУплатаНалога = ИнформацияУплатаНалога;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУплатаАвансовПоИмущественнымНалогам(ПараметрыЗадачи, СведенияОРасчете)
	
	Организация                 = ПараметрыЗадачи.Организация;
	Правило                     = ПараметрыЗадачи.ПравилоУплаты;
	ПериодСобытия               = ПараметрыЗадачи.ПериодСобытия;
	
	ИмяЗадачи     = ПараметрыЗадачи.ИдентификаторЗадачи;
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);
	ВидНалога     = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(ИмяЗадачи, Организация, КонецПериода);
	Налог         = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
	КБК           = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, , КонецПериода);
	
	Платежи = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыИмущественныхНалогов(
		Правило,
		Организация,
		ПериодСобытия,
		ПараметрыЗадачи.Срок,
		ПараметрыЗадачи.РегистрацияВНалоговомОргане);
	
	// Для уплаты имущественных налогов таблица расчета совпадает с таблицей платежей.
	// Заполним таблицу расчета
	ТаблицаРасчета = НовыйТаблицаРасчета();
	Для Каждого Платеж Из Платежи Цикл
		Расчет = ТаблицаРасчета.Добавить();
		Расчет.НаименованиеПоказателя = Платеж.Наименование;
		Расчет.ЗначениеПоказателя     = Платеж.Сумма;
		Расчет.ОКТМО                  = Платеж.ОКАТО;
	КонецЦикла;
	
	Если Платежи.Количество() > 1 Тогда
		ИтогРасчета = ТаблицаРасчета.Добавить();
		ИтогРасчета.НаименованиеПоказателя = НСтр("ru = 'Итого'");
		ИтогРасчета.ЗначениеПоказателя     = Платежи.Итог("Сумма");
		ИтогРасчета.ЭтоИтоговаяСтрока      = Истина;
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежных документов'");
	Иначе
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежного документа'");
	КонецЕсли;
	
	// Определим период, к которому относится платеж,
	// в соответствии с классификатором, используемым в платежных поручениях.
	НалоговыйПериодДляПлатежейВБюджет = Правило.Периодичность;
	ПериодичностьПоКлассификатору =
		ПлатежиВБюджетПереопределяемый.ПериодичностьПоКлассификатору(НалоговыйПериодДляПлатежейВБюджет);
	Если ПериодичностьПоКлассификатору = ПлатежиВБюджетКлиентСервер.ПлатежПоКонкретнойДате()
		Или НалоговыйПериодДляПлатежейВБюджет <> Правило.Периодичность Тогда
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПараметрыЗадачи.Срок, ПериодичностьПоКлассификатору);
	Иначе
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПериодСобытия, ПериодичностьПоКлассификатору);
	КонецЕсли;
	
	СведенияОРасчете.РасчетСуммыВыполнен    = Истина;
	СведенияОРасчете.ИнформацияРасчетСуммы  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сумма платежа рассчитывается на основании данных регистра 
		|%1'"), 
		?(ВидНалога = Перечисления.ВидыНалогов.ТранспортныйНалог,
		НСтр("ru = '""Расчет транспортного налога""'"),
		НСтр("ru = '""Расчет земельного налога""'")));
	СведенияОРасчете.ТаблицаПлатежей        = Платежи;
	СведенияОРасчете.ТаблицаРасчета         = ТаблицаРасчета;
	СведенияОРасчете.ПоказательПериода      = ПоказательПериода;
	СведенияОРасчете.ИнформацияУплатаНалога = ИнформацияУплатаНалога;
	СведенияОРасчете.Налог                  = Налог;
	СведенияОРасчете.КБК                    = КБК;
	СведенияОРасчете.ЕстьОплата             = ЕстьОплата(Платежи);
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУплатаТорговогоСбора(ПараметрыЗадачи, СведенияОРасчете)
	
	Организация   = ПараметрыЗадачи.Организация;
	Правило       = ПараметрыЗадачи.ПравилоУплаты;
	Срок          = ПараметрыЗадачи.Срок;
	ПериодСобытия = ПараметрыЗадачи.ПериодСобытия;
	Описание      = ПараметрыЗадачи.Описание;
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);
	ВидНалога     = Перечисления.ВидыНалогов.ТорговыйСбор;
	Налог         = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
	КБК           = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, , КонецПериода);
	
	ПериодичностьПоКлассификатору = ПлатежиВБюджетПереопределяемый.ПериодичностьПоКлассификатору(Правило.Периодичность);
	Если ПериодичностьПоКлассификатору = ПлатежиВБюджетКлиентСервер.ПлатежПоКонкретнойДате() Тогда
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(Срок, ПериодичностьПоКлассификатору);
	Иначе
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПериодСобытия, ПериодичностьПоКлассификатору);
	КонецЕсли;
	
	СуммаТорговогоСбора = ТорговыйСбор.СуммаТорговогоСбора(Организация, НачалоПериода, КонецПериода);
	
	// Для торгового сбора сумма к уплате разбивается по торговым точкам.
	СуммаТорговогоСбора.Свернуть("ТорговаяТочка, КодНалоговогоОргана, ОКАТО", "Сумма");
	ТаблицаРасчета = НовыйТаблицаРасчета();
	Для каждого СтрокаСбора Из СуммаТорговогоСбора Цикл
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(СтрокаСбора.ОКАТО, ПериодСобытия);
		Расчет = ТаблицаРасчета.Добавить();
		Расчет.НаименованиеПоказателя = СтрокаСбора.ТорговаяТочка;
		Расчет.ЗначениеПоказателя     = СтрокаСбора.Сумма;
		Расчет.ОКТМО                  = СтрокаСбора.ОКАТО;
	КонецЦикла;
	Если ТаблицаРасчета.Количество() > 1 Тогда 
		ИтогРасчета = ТаблицаРасчета.Добавить();
		ИтогРасчета.НаименованиеПоказателя = НСтр("ru = 'Итого'");
		ИтогРасчета.ЗначениеПоказателя     = ТаблицаРасчета.Итог("ЗначениеПоказателя");
		ИтогРасчета.ЭтоИтоговаяСтрока      = Истина;
	КонецЕсли;
	
	// Получаем таблицу платежей
	СуммаТорговогоСбора.Свернуть("КодНалоговогоОргана, ОКАТО", "Сумма");
	Платежи = ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи();
	Для каждого СтрокаСбора Из СуммаТорговогоСбора Цикл
		Платеж = Платежи.Добавить();
		ЗаполнитьЗначенияСвойств(Платеж, СтрокаСбора);
		Платеж.КБК          = КБК;
		Платеж.Налог        = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(Платеж.КБК);
		Платеж.Наименование = Строка(Платеж.Налог);
	КонецЦикла;
	
	Если Платежи.Количество() > 0 Тогда
		ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация);
	КонецЕсли;
	
	УдалитьСтрокиБезПлатежежей(Платежи);
	
	Если Платежи.Количество() > 1 Тогда
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежных документов'");
	Иначе
		ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежного документа'");
	КонецЕсли;
	
	СведенияОРасчете.РасчетСуммыВыполнен    = Истина;
	СведенияОРасчете.ИнформацияРасчетСуммы  =
		НСтр("ru = 'Сумма платежа рассчитывается на основании данных регистра ""Параметры торговых точек""'");
	СведенияОРасчете.ТаблицаПлатежей        = Платежи;
	СведенияОРасчете.ТаблицаРасчета         = ТаблицаРасчета;
	СведенияОРасчете.ПоказательПериода      = ПоказательПериода;
	СведенияОРасчете.ИнформацияУплатаНалога = ИнформацияУплатаНалога;
	СведенияОРасчете.Налог                  = Налог;
	СведенияОРасчете.КБК                    = КБК;
	СведенияОРасчете.ЕстьОплата             = ЕстьОплата(Платежи);
	
КонецПроцедуры

Процедура ЗаполнитьСведенияПодготовкаОтчета(ПараметрыЗадачи, СведенияОРасчете)
	
	ОписаниеДействияДекларация = ВыполнениеЗадачБухгалтера.ОписаниеДействияДекларация(
		ПараметрыЗадачи.ПравилоОтчета,
		ПараметрыЗадачи.ПериодСобытия,
		ПараметрыЗадачи.Организация,
		ПараметрыЗадачи.РегистрацияВНалоговомОргане);
		
	Если ОписаниеДействияДекларация = Неопределено Тогда
		СведенияОРасчете.РасчетСуммыВыполнен      = Истина; // Чтобы не показывать кнопку расчета
		СведенияОРасчете.ДопИнформацияРасчетСуммы = НСтр("ru = 'Подготовка отчета не автоматизирована'");
		СведенияОРасчете.ТребуетсяУплата          = Ложь;
		СведенияОРасчете.ИнформацияУплатаНалога   = НСтр("ru = 'Уплата не требуется'");
		СведенияОРасчете.ТребуетсяСверка          = Ложь;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СведенияОРасчете.ОписаниеДействияРасчет, ОписаниеДействияДекларация);
	СведенияОРасчете.ОписаниеДействияРасчет.Наименование = НСтр("ru = 'Сформировать отчет'");
	Если ОписаниеДействияДекларация.ПараметрыФормы.Свойство("Ключ") Тогда
		СведенияОРасчете.РасчетСуммыВыполнен    = Истина;
		СведенияОРасчете.УплатаПоДекларации     = Истина;
		СведенияОРасчете.Декларация             = ОписаниеДействияДекларация.ПараметрыФормы.Ключ;
		СведенияОРасчете.ДекларацияНаименование = ПредставлениеДекларации(ОписаниеДействияДекларация.ПараметрыФормы.Ключ);
	КонецЕсли;
	
	СведенияОРасчете.УплатаПоДекларации     = Истина;
	
	Если ПараметрыЗадачи.ИдентификаторЗадачи = "БухгалтерскаяОтчетность" Тогда
		
		СведенияОРасчете.ДопИнформацияРасчетСуммы = 
			НСтр("ru = 'Сформированная отчетность заполнена и готова к сдаче в налоговую инспекцию и
			|территориальное отделение Росстата.
			|Самый удобный способ - подключить 1С-Отчетность и отправить в электронном виде. 
			|Другой способ - это распечатать 2 экземпляра для налоговой инспекции и 2 экземпляра 
			|для Росстата (для ФНС и Росстата используются разные формы). На каждом экземпляре 
			|укажите дату, подпишите отчет и отнесите в свою налоговую инспекцию и Росстат.'");
			
	ИначеЕсли ПараметрыЗадачи.ИдентификаторЗадачи = "СведенияОСреднесписочнойЧисленности" Тогда 
		
		СведенияОРасчете.ДопИнформацияРасчетСуммы = 
			НСтр("ru = 'Сформированная отчетность заполнена и готова к сдаче в налоговую инспекцию.
			|Самый удобный способ - подключить 1С-Отчетность и отправить в электронном виде.
			|Другой способ - это распечатать 2 экземпляра. На каждом экземпляре укажите дату,
			|подпишите отчет и отнесите в свою налоговую инспекцию.'");
		
	КонецЕсли;
		
	СведенияОРасчете.ИнформацияУплатаНалога = НСтр("ru = 'Уплата не требуется'");
	СведенияОРасчете.ТребуетсяУплата        = Ложь;
	СведенияОРасчете.ТребуетсяСверка        = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУплатаПоДекларации(ПараметрыЗадачи, СведенияОРасчете)
	
	ЗаполнитьСведенияУплатаНалога(ПараметрыЗадачи, СведенияОРасчете);
	
	СведенияОРасчете.УплатаПоДекларации = Истина;
	
	Если ПараметрыЗадачи.ИдентификаторЗадачи = "НалогНаПрибыль" 
		ИЛИ ПараметрыЗадачи.ИдентификаторЗадачи = "НДФЛ_Предприниматель" Тогда
		СведенияОРасчете.ДопИнформацияРасчетСуммы = 
			НСтр("ru = 'Сформированная отчетность заполнена и готова к сдаче в налоговую инспекцию.
			|Самый удобный способ - подключить 1С-Отчетность и отправить в электронном виде.
			|Другой способ - это распечатать 2 экземпляра. На каждом экземпляре укажите дату,
			|подпишите отчет и отнесите в свою налоговую инспекцию.'");
		
	КонецЕсли;
	
	ДекларацияПодготовлена = Ложь;
	
	ОписаниеДействияДекларация = ВыполнениеЗадачБухгалтера.ОписаниеДействияДекларация(
		ПараметрыЗадачи.ПравилоОтчета,
		ПараметрыЗадачи.ПериодОтчета,
		ПараметрыЗадачи.Организация,
		ПараметрыЗадачи.РегистрацияВНалоговомОргане);
		
	Если ОписаниеДействияДекларация <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СведенияОРасчете.ОписаниеДействияРасчет, ОписаниеДействияДекларация);
		СведенияОРасчете.ОписаниеДействияРасчет.Наименование = НСтр("ru = 'Сформировать декларацию'");
		ДекларацияПодготовлена = ОписаниеДействияДекларация.ПараметрыФормы.Свойство("Ключ");
	КонецЕсли;
		
	Если Не ДекларацияПодготовлена Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДекларации = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыНалогаПоДекларации(
		ПараметрыЗадачи.ПравилоУплаты,
		ПараметрыЗадачи.Организация,
		ПараметрыЗадачи.РегистрацияВНалоговомОргане,
		ПараметрыЗадачи.ПериодСобытия,
		ПараметрыЗадачи.Срок,
		СведенияОРасчете.ПоказательПериода);
		
	// Заполним таблицу расчета
	ТаблицаРасчета = НовыйТаблицаРасчета();
	ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежного документа'");
	Если ДанныеДекларации.Свойство("СуммаНДСПоДекларации") Тогда
		// Платеж по декларации НДС
		// Сумма по декларации
		Расчет = ТаблицаРасчета.Добавить();
		Расчет.НаименованиеПоказателя = НСтр("ru = 'Сумма налога'");
		Расчет.ЗначениеПоказателя     = ДанныеДекларации.СуммаНДСПоДекларации;
		// Сумма платежа
		Расчет = ТаблицаРасчета.Добавить();
		Расчет.НаименованиеПоказателя = ПорядокРассчетаСуммыНДС(ДанныеДекларации);
		Расчет.ЗначениеПоказателя     = ДанныеДекларации.Платежи[0].Сумма;
		Расчет.ОКТМО                  = ДанныеДекларации.Платежи[0].ОКАТО;
		Расчет.ЭтоИтоговаяСтрока      = Истина;
	Иначе
		Для Каждого Платеж Из ДанныеДекларации.Платежи Цикл
			Расчет = ТаблицаРасчета.Добавить();
			Расчет.НаименованиеПоказателя = Платеж.Наименование;
			Расчет.ЗначениеПоказателя     = Платеж.Сумма;
			Расчет.ОКТМО                  = Платеж.ОКАТО;
		КонецЦикла;
		Если ДанныеДекларации.Платежи.Количество() > 1 Тогда
			ИтогРасчета = ТаблицаРасчета.Добавить();
			ИтогРасчета.НаименованиеПоказателя = НСтр("ru = 'Итого'");
			ИтогРасчета.ЗначениеПоказателя     = ДанныеДекларации.Платежи.Итог("Сумма");
			ИтогРасчета.ЭтоИтоговаяСтрока      = Истина;
			ИнформацияУплатаНалога = НСтр("ru = 'Подготовка платежных документов'");
		КонецЕсли;
	КонецЕсли;
	
	СведенияОРасчете.РасчетСуммыВыполнен = Истина;
	СведенияОРасчете.Декларация             = ДанныеДекларации.Ссылка;
	СведенияОРасчете.ДекларацияНаименование = ПредставлениеДекларации(ДанныеДекларации.Ссылка);
	СведенияОРасчете.ТаблицаПлатежей        = ДанныеДекларации.Платежи;
	СведенияОРасчете.ТаблицаРасчета         = ТаблицаРасчета;
	СведенияОРасчете.ДатаДекларации         = ДанныеДекларации.ДатаПодписи;
	СведенияОРасчете.ИнформацияУплатаНалога = ИнформацияУплатаНалога;
	СведенияОРасчете.ЕстьОплата             = ЕстьОплата(ДанныеДекларации.Платежи);
	СведенияОРасчете.КодНалоговогоОргана    = ДанныеДекларации.КодНалоговогоОргана;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияНетСведений(ПараметрыЗадачи, СведенияОРасчете)
	
	ЗаполнитьСведенияУплатаНалога(ПараметрыЗадачи, СведенияОРасчете);
	
	СведенияОРасчете.ИнформацияРасчетСуммы = "";
	
	СведенияОРасчете.РасчетСуммыВыполнен = Истина; // Не предоставляем пользователю инструменты расчета суммы
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУплатаНалога(ПараметрыЗадачи, СведенияОРасчете)
	
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(
		ПараметрыЗадачи.ИдентификаторЗадачи,
		ПараметрыЗадачи.Организация,
		ПараметрыЗадачи.ПериодСобытия);
	
	СведенияОРасчете.Налог              = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
	СведенияОРасчете.КБК                = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(СведенияОРасчете.Налог, , ПараметрыЗадачи.ПериодСобытия);
	СведенияОРасчете.ПоказательПериода  = ВыполнениеЗадачБухгалтера.ПоказательПериодаПлатежаПоДекларации(
		ПараметрыЗадачи.ПравилоУплаты,
		ПараметрыЗадачи.ПериодСобытия,
		ПараметрыЗадачи.Срок);
	
	ДокументыУплаты = ДокументыУплатыНалога(ПараметрыЗадачи.Организация, ПараметрыЗадачи.ПериодСобытия, ПараметрыЗадачи, ВидНалога);
	
	ТаблицаПлатежей = ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи();
	
	МассивТиповПлатежныхДокументов = Новый Массив;
	МассивТиповПлатежныхДокументов.Добавить(Тип("ДокументСсылка.ПлатежноеПоручение"));
	МассивТиповПлатежныхДокументов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	
	ТаблицаПлатежей.Колонки.Добавить("ПлатежноеПоручение", Новый ОписаниеТипов(МассивТиповПлатежныхДокументов));
	ТаблицаПлатежей.Колонки.Добавить("СуммаОплачено",      Новый ОписаниеТипов("Число"));
	ТаблицаПлатежей.Колонки.Добавить("Оплачено",           Новый ОписаниеТипов("Булево"));
	
	Для каждого ДокументУплаты Из ДокументыУплаты Цикл
		Платеж = ТаблицаПлатежей.Добавить();
		Платеж.Организация = ПараметрыЗадачи.Организация;
		Платеж.Период = ПараметрыЗадачи.ПериодСобытия;
		Платеж.КБК = СведенияОРасчете.КБК;
		Платеж.Сумма = ДокументУплаты.Сумма;
		Платеж.Наименование = Строка(СведенияОРасчете.Налог);
		Платеж.Налог = СведенияОРасчете.Налог;
		Платеж.ВидНалоговогоОбязательства = ДокументУплаты.ВидНалоговогоОбязательства;
		Платеж.НалоговыйПериод = ДокументУплаты.ПоказательПериода;
		
		Платеж.ПлатежноеПоручение = ДокументУплаты.Ссылка;
		Платеж.СуммаОплачено = ДокументУплаты.Сумма;
		Платеж.Оплачено = ДокументУплаты.Оплачено;
	КонецЦикла;
	
	СведенияОРасчете.ТаблицаПлатежей        = ТаблицаПлатежей;
	СведенияОРасчете.ЕстьОплата             = ЕстьОплата(ТаблицаПлатежей);
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУплатаНалогаНаПрофессиональныйДоход(ПараметрыЗадачи, СведенияОРасчете)
	
	ЗаполнитьСведенияУплатаНалога(ПараметрыЗадачи, СведенияОРасчете);
	
	СведенияОРасчете.ТребуетсяРасчет = Ложь;
	СведенияОРасчете.РасчетСуммыВыполнен = Истина;
	СведенияОРасчете.ИнформацияРасчетСуммы  =
		НСтр("ru = 'Сумма налога исчисляется налоговым органом.
			|Налоговый орган уведомляет налогоплательщика о сумме налога, подлежащей уплате по итогам налогового периода.'");
	
КонецПроцедуры

Функция ДокументыУплатыНалога(Организация, Период, ПараметрыЗадачи, ВидНалога)
	
	// Подготовим таблицу платежей.
	ПараметрыУплатыНалогов = ПомощникиПоУплатеНалоговИВзносов.НовыеПараметрыУплатыНалогов();
	ПараметрыУплатыНалогов.КодыЗадач.Добавить(ПараметрыЗадачи.ИдентификаторЗадачи);
	
	ПараметрыУплатыНалогов.ВидыНалогов.Добавить(ВидНалога);
	ПараметрыУплатыНалогов.ВидыНалоговыхОбязательств.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог);
	ПараметрыУплатыНалогов.ВидыНалоговыхОбязательств.Добавить(Перечисления.ВидыПлатежейВГосБюджет.ПениАкт);
	ПараметрыУплатыНалогов.ВидыНалоговыхОбязательств.Добавить(Перечисления.ВидыПлатежейВГосБюджет.ПениСам);
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПараметрыЗадачи.Периодичность, Период);
	КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПараметрыЗадачи.Периодичность, Период);
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ДокументыУплатыНалогов(
		Организация, НачалоПериода, КонецПериода, ПараметрыУплатыНалогов);
	
КонецФункции

#КонецОбласти

#Область Статусы

Функция СтатусУплатыНалога(СведенияОРасчетеСуммы)
	
	Статус = "";
	
	Если СведенияОРасчетеСуммы.ТаблицаРасчета = Неопределено
		ИЛИ СведенияОРасчетеСуммы.ТаблицаПлатежей = Неопределено
		ИЛИ СведенияОРасчетеСуммы.ТаблицаРасчета.Количество() = 0 Тогда
		
		Возврат Статус;
		
	КонецЕсли;
	
	Если СведенияОРасчетеСуммы.ТаблицаРасчета.Количество() = 1 Тогда
		ИтоговаяСтрока = СведенияОРасчетеСуммы.ТаблицаРасчета[0];
	Иначе
		ИтоговаяСтрока = СведенияОРасчетеСуммы.ТаблицаРасчета.Найти(Истина, "ЭтоИтоговаяСтрока");
	КонецЕсли;
	
	Если ИтоговаяСтрока = Неопределено Тогда
		Возврат Статус;
	КонецЕсли;
	
	НеобходимоУплатить = ИтоговаяСтрока.ЗначениеПоказателя;
	
	Если НеобходимоУплатить = 0 Тогда
		Возврат Статус;
	КонецЕсли;
	
	Уплачено = СведенияОРасчетеСуммы.ТаблицаПлатежей.Скопировать(Новый Структура("Оплачено", Истина), "СуммаОплачено").Итог("СуммаОплачено");
	
	Если НеобходимоУплатить <= Уплачено Тогда
		Статус = "Оплачено";
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

#КонецОбласти

#Область ПрочиеВспомогательныеПроцедурыИФункции

Функция НовыйСведенияОРасчете()
	
	СтруктураСведенийОРасчете = Новый Структура();
	СтруктураСведенийОРасчете.Вставить("РасчетСуммыВыполнен",    Ложь);
	СтруктураСведенийОРасчете.Вставить("ЕстьОплата",             Ложь);
	СтруктураСведенийОРасчете.Вставить("УплатаПоДекларации",     Ложь);
	СтруктураСведенийОРасчете.Вставить("ИнформацияРасчетСуммы",
		НСтр("ru = 'Сумма платежа рассчитывается на основании данных отчета'"));
	СтруктураСведенийОРасчете.Вставить("ДопИнформацияРасчетСуммы", "");
	СтруктураСведенийОРасчете.Вставить("ИнформацияУплатаНалога",
		НСтр("ru = 'Подготовка платежного документа'"));
	СтруктураСведенийОРасчете.Вставить("ТаблицаПлатежей",        Неопределено);// см. ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи()
	СтруктураСведенийОРасчете.Вставить("ТаблицаРасчета",         Неопределено);// см. НовыйТаблицаРасчета()
	СтруктураСведенийОРасчете.Вставить("ОписаниеДействияРасчет", НовыйОписаниеДействияРасчет());
	СтруктураСведенийОРасчете.Вставить("Декларация",             Документы.РегламентированныйОтчет.ПустаяСсылка());
	СтруктураСведенийОРасчете.Вставить("ДатаДекларации",         '00010101');
	СтруктураСведенийОРасчете.Вставить("ДекларацияНаименование", "");
	СтруктураСведенийОРасчете.Вставить("ПоказательПериода",      "");// Для платежного документа
	СтруктураСведенийОРасчете.Вставить("Налог",                  Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка());
	СтруктураСведенийОРасчете.Вставить("КБК",                    "");
	СтруктураСведенийОРасчете.Вставить("КодНалоговогоОргана",    "");
	СтруктураСведенийОРасчете.Вставить("ТребуетсяРасчет",        Истина);
	СтруктураСведенийОРасчете.Вставить("ТребуетсяУплата",        Истина);
	СтруктураСведенийОРасчете.Вставить("ТребуетсяСверка",        Истина);

	Возврат СтруктураСведенийОРасчете;
	
КонецФункции

Функция НовыйТаблицаРасчета()
	
	Расчеты = Новый ТаблицаЗначений;
	
	Расчеты.Колонки.Добавить("НаименованиеПоказателя",  Новый ОписаниеТипов("Строка"));
	Расчеты.Колонки.Добавить("ЗначениеПоказателя", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	Расчеты.Колонки.Добавить("ОКТМО", Метаданные.Справочники.РегистрацииВНалоговомОргане.Реквизиты.КодПоОКТМО.Тип);
	Расчеты.Колонки.Добавить("ЭтоИтоговаяСтрока", Новый ОписаниеТипов("Булево"));
	
	Возврат Расчеты;
	
КонецФункции

Функция НовыйОписаниеДействияРасчет()
	
	ОписаниеДействияРасчет = Новый Структура;
	
	ОписаниеДействияРасчет.Вставить("ИмяФормы",       "");
	ОписаниеДействияРасчет.Вставить("ПараметрыФормы", Неопределено);
	ОписаниеДействияРасчет.Вставить("Наименование",   "");
	
	Возврат ОписаниеДействияРасчет;
	
КонецФункции

Функция ЕстьОплата(Платежи)
	
	Если Платежи.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВсеПлатежныеДокументы = Платежи.Скопировать(,"ПлатежноеПоручение");
	ВсеПлатежныеДокументы.Свернуть("ПлатежноеПоручение");
	
	Возврат ВсеПлатежныеДокументы.Количество() > 1 
		Или ВсеПлатежныеДокументы.Количество() = 1 И ЗначениеЗаполнено(ВсеПлатежныеДокументы[0].ПлатежноеПоручение);
	
КонецФункции

Функция ПорядокРассчетаСуммыНДС(ДанныеДекларации)
	
	// Существование в одной декларации сумм, предполагающих рассрочку и сумм, уплачиваемых единовременно в соответствии с п.5 ст. 173 НК,
	// считаем очень редким,в программе не автоматизирован и в помощнике не обслуживаем.
	
	ПорядокРассчета = "";
	Если ДанныеДекларации.СуммаНДСПоДекларации = 0 Тогда
		ПорядокРассчета = НСтр("ru = 'По декларации оплата не требуется'");
	ИначеЕсли ДанныеДекларации.Платежи[0].Сумма = 0 Тогда 
		// По декларации был рассчитан единовременный платеж, который был уплачен в другом налоговом периоде.
		ПорядокРассчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сумма платежа %1 - %1(единовременный платеж)'"), ДанныеДекларации.СуммаНДСПоДекларации);
	ИначеЕсли ДанныеДекларации.СуммаНДСПоДекларации = ДанныеДекларации.Платежи[0].Сумма Тогда
		// По декларации предполагается уплатить только единовременный платеж.
		ПорядокРассчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сумма платежа %1(единовременный платеж)'"), ДанныеДекларации.СуммаНДСПоДекларации);
	ИначеЕсли (ДанныеДекларации.СуммаНДСПоДекларации - ДанныеДекларации.Платежи[0].Сумма * 3) > 3
		ИЛИ (ДанныеДекларации.СуммаНДСПоДекларации - ДанныеДекларации.Платежи[0].Сумма * 3) < 0 Тогда
		// По декларации предполагается уплатить и единовременный платеж, и платеж, предполагающий рассрочку.
		ПорядокРассчета = НСтр("ru = 'Сумма платежа'");
	Иначе
		ПорядокРассчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Сумма платежа %1/3'"), ДанныеДекларации.СуммаНДСПоДекларации);
	КонецЕсли;
	
	Возврат ПорядокРассчета;
	
КонецФункции

Процедура УдалитьСтрокиБезПлатежежей(Платежи)
	
	КоличествоСтрок = Платежи.Количество();
	// Удалим строки, по которым нет платежей
	Если КоличествоСтрок > 1 Тогда
		Для НомерСтроки = 1 По КоличествоСтрок Цикл
			Платеж = Платежи[КоличествоСтрок - НомерСтроки];
			Если Платеж.Сумма = 0 И Не ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
				Платежи.Удалить(Платеж);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ОтчетыИспользуютсяДляУплатыНалога(Правило)
	
	// Как правило, налог уплачивается на основании отчета (декларации).
	// Здесь описаны исключения, когда представляемые отчеты заведомо не связаны с уплатой налога,
	// не содержат данных для уплаты налога.
	// Ситуации, когда никакие отчеты не представляются для уплаты налога, таким исключением не являются и здесь не описаны.
	
	Если ВыполнениеЗадачБухгалтера.ЭтоСтраховыеВзносы(Правило) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПодобратьРелевантноеПравилоОтчета(ПравилаПодготовкиОтчета, ПравилоУплаты)
	
	Если ПравилаПодготовкиОтчета.Количество() = 0 Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Если ПравилаПодготовкиОтчета.Количество() = 1 Тогда
		Возврат ПравилаПодготовкиОтчета[0];
	КонецЕсли;
	
	ПолноеИмяПравилаУплаты = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПравилоУплаты);
	ИсполнительПравилаУплаты = ИнтерфейсыВзаимодействияБРО.ИмяРеглОтчета(ПолноеИмяПравилаУплаты);
	
	УникальныеПравила = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПравилаПодготовкиОтчета);
	Для Каждого ПравилоОтчета Из УникальныеПравила Цикл
		
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПравилоОтчета);
		ИсполнительПравила = ИнтерфейсыВзаимодействияБРО.ИмяРеглОтчета(ПолноеИмяПравила);
		
		Если ИсполнительПравила = ИсполнительПравилаУплаты Тогда
			Возврат ПравилоОтчета;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПравилаПодготовкиОтчета[0];
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
