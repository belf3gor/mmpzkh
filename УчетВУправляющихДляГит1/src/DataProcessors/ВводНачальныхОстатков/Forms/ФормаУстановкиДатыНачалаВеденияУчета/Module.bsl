

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Установить(Команда)
	
	Если НЕ ЗначениеЗаполнено(НоваяДатаВводаОстатков) Тогда
		ТекстПредупреждения = НСтр("ru = 'Укажите дату начала ведения учета в программе!'"); 
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	КорректныйПериод = КорректныйПериодВводаДокументов();
	Если НоваяДатаВводаОстатков < КорректныйПериод.НачалоКорректногоПериода Тогда
		ГраницаКорректности = Формат(КорректныйПериод.НачалоКорректногоПериода, "ДФ=гггг");
		ТекстПредупреждения = НСтр("ru = 'Дата начала ведения учета в программе должна быть не ранее %1 года!'"); 
		ПоказатьПредупреждение( , СтрШаблон(ТекстПредупреждения, ГраницаКорректности));
		Возврат;
	ИначеЕсли НоваяДатаВводаОстатков > КорректныйПериод.КонецКорректногоПериода Тогда
		ГраницаКорректности = Формат(КорректныйПериод.КонецКорректногоПериода, "ДФ=гггг");
		ТекстПредупреждения = НСтр("ru = 'Дата начала ведения учета в программе должна быть не позже %1 года!'"); 
		ПоказатьПредупреждение( , СтрШаблон(ТекстПредупреждения, ГраницаКорректности));
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть(НоваяДатаВводаОстатков);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НоваяДатаВводаОстатков = Параметры.ДатаВводаОстатков;
	Заголовок = Параметры.ТекстЗаголовка;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция КорректныйПериодВводаДокументов()
	
	Возврат ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
	
КонецФункции
