
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекОбъект = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.УПЖКХ_ИнтеграцияССайтом"));
	
	ТекОбъект.ИнициализацияРеквизитов();
	
	ЗначениеВДанныеФормы(ТекОбъект, Объект);
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаКнопкаЗакрыть);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриОткрытии(Отказ)
	
	УправлениеВидимостьюГиперссылкиРешенияПроблемСJavaScript();
	
	ПодключитьсяКСтраницеСайта();
	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
// Обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СохранитьРеквизиты();
	
КонецПроцедуры // ПередЗакрытием()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

#Область ОбработчикиСобытийЭлементовУправления

&НаКлиенте
// Обработчик события "Нажатие" гиперссылки "НадписьСтраницаРедактированияНовостей".
//
Процедура НадписьСтраницаРедактированияНовостейНажатие(Элемент)
	
	ПодключитьсяКСтраницеСайта();
	
КонецПроцедуры // НадписьСтраницаРедактированияНовостейНажатие()

&НаКлиенте
// Обработчик события "Нажатие" гиперссылки "НадписьГлавнаяСтраницаСайта".
//
Процедура НадписьГлавнаяСтраницаСайтаНажатие(Элемент)
	
	ПодключитьсяКСтраницеСайта(Ложь);
	
КонецПроцедуры // НадписьГлавнаяСтраницаСайтаНажатие()

&НаКлиенте
// Обработчик события "Нажатие" поля выбора "НастройкаДоступа".
//
Процедура НастройкаДоступаПриИзменении(Элемент)
	
	ПодключитьсяКСтраницеСайта();
	
КонецПроцедуры // НастройкаДоступаПриИзменении()

&НаКлиенте
// Обработчик события "Нажатие" гиперссылки на описание решения проблем со сценариями JavaScript.
//
Процедура НадписьПерейтиНаСтраницуРешенияПроблемСJavaScriptНажатие(Элемент)
	
	УПЖКХ_ОбщегоНазначенияКлиент.ОткрытьСтраницуРешенияПроблемРежимаСовместимостиСIE7();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКоманд

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
// Сохраняет настройки пользователя в хранилище настроек.
//
Процедура СохранитьРеквизиты()
	
	СтруктураНастроек = Новый Структура;
	
	СтруктураНастроек.Вставить("НастройкаДоступа",          Объект.НастройкаДоступа);
	СтруктураНастроек.Вставить("ОбновлятьПараметрыДоступа", Объект.ОбновлятьПараметрыДоступа);
	
	ХранилищеОбщихНастроек.Сохранить("Обработка.УПЖКХ_ИнтеграцияССайтом", , СтруктураНастроек);
	
КонецПроцедуры // СохранитьРеквизиты()

&НаСервере
// Формирует структуру с параметрами подключения к сайту.
//
Процедура ЗаполнитьПараметрыПодключения()
	
	ПараметрыПодключения = Новый Структура;
	
	ПараметрыПодключения.Вставить("АдресСайта",    Объект.НастройкаДоступа.АдресСайта);
	ПараметрыПодключения.Вставить("Логин",         Объект.НастройкаДоступа.Логин);
	ПараметрыПодключения.Вставить("Пароль",        Объект.НастройкаДоступа.Пароль);
	ПараметрыПодключения.Вставить("ХранитьПароль", Объект.НастройкаДоступа.ХранитьПароль);
	
КонецПроцедуры // ЗаполнитьПараметрыПодключения()

&НаКлиенте
// Устанавливает в HTML-документ адрес необходимой страницы сайта.
//
// Параметры:
//  РедактироватьНовости - Булево - признак того, что требуется открыть страницу редактирования новостей.
//
Процедура ПодключитьсяКСтраницеСайта(РедактироватьНовости = Истина)
	
	Если Объект.НастройкаДоступа = ПредопределенноеЗначение("Справочник.УПЖКХ_НастройкиДоступаКСайту.ПустаяСсылка") Тогда
		АдресСтраницы = "";
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыПодключения();
	
	Если ПараметрыПодключения.ХранитьПароль Тогда
		Пароль = ПараметрыПодключения.Пароль;
	Иначе
		ДополнительныеПараметры = Новый Структура("РедактироватьНовости", РедактироватьНовости);
		ФормаВводаПароля = ПолучитьФорму("ОбщаяФорма.УПЖКХ_ВебИнтеграцияВводПароля");
		ФормаВводаПароля.ОписаниеОповещенияОЗакрытии   = Новый ОписаниеОповещения("ПолучитьПарольДоступа", ЭтаФорма, ДополнительныеПараметры);
		ФормаВводаПароля.ВладелецФормы                 = ЭтаФорма;
		ФормаВводаПароля.ЗакрыватьПриЗакрытииВладельца = Истина;
		ФормаВводаПароля.Открыть();
		Возврат;
	КонецЕсли;
	
	ПолучитьАдресСтраницы(Пароль, РедактироватьНовости);
	
КонецПроцедуры // ПодключитьсяКСтраницеСайта()

&НаКлиенте
// Устанавливает адрес страницы.
Процедура ПолучитьАдресСтраницы(Пароль, РедактироватьНовости)
	
	АдресСтраницы = УПЖКХ_ВебИнтеграцияВзаимодействиеССайтом.ПолучитьАдресСтраницы(	РедактироватьНовости,
																					ПараметрыПодключения.АдресСайта,
																					ПараметрыПодключения.Логин,
																					Пароль);
КонецПроцедуры // ПолучитьАдресСтраницы()

&НаКлиенте
// Открывает форму для ввода пароля.
Функция ПолучитьПарольДоступа(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Пароль               = РезультатЗакрытия;
	РедактироватьНовости = ДополнительныеПараметры.РедактироватьНовости;
	
	ПолучитьАдресСтраницы(Пароль, РедактироватьНовости);
	
КонецФункции // ПолучитьПарольДоступа()

&НаКлиенте
// Управляет видимостью гиперссылки с предложением решения проблем со сценариями JavaScript.
//
Процедура УправлениеВидимостьюГиперссылкиРешенияПроблемСJavaScript()
	
	Элементы.НадписьПерейтиНаСтраницуРешенияПроблемСJavaScript.Видимость = УПЖКХ_ОбщегоНазначенияКлиент.ПроверитьИспользованиеРежимаСовместимостиСIE7(Элементы.СтраницаНовостей.ИнформацияПрограммыПросмотра);
	
КонецПроцедуры

#КонецОбласти