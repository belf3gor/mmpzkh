
#Область ОбъявлениеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить() Тогда
		Элементы.ГруппаВариантыУчетаЗарплаты.ТекущаяСтраница = Элементы.СтраницаУчетЗарплатыИКадровВоВнешнейПрограмме;
		Возврат;
	КонецЕсли;
	
	ЭтоМедленныйРежимРаботы = ОбщегоНазначенияБП.ЭтоМедленныйРежимРаботы();
	ЭтоВебКлиент            = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	ТекущаяДатаРаботы = ТекущаяДатаСеанса();
	
	ПериодРегистрации = РасчетныйПериод(Организация, ТекущаяДатаРаботы);
	
	ПредставлениеПериодаРегистрации = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Перечисления.ДоступныеПериодыОтчета.Месяц, ПериодРегистрации, КонецМесяца(ПериодРегистрации));
	
	СпособВыплатыЗарплата = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СпособыВыплатыЗарплаты.Зарплата");
	СпособВыплатыАванс    = Справочники.СпособыВыплатыЗарплаты.НайтиПоРеквизиту("ХарактерВыплаты", Перечисления.ХарактерВыплатыЗарплаты.Аванс);
	
	ЭтоПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	РасчетЗарплатыДляНебольшихОрганизаций = ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций");
	Элементы.ГруппаКомандыДокументы.Видимость = РасчетЗарплатыДляНебольшихОрганизаций;
	Элементы.ДокументСправкаНДФЛ.Видимость = НЕ РасчетЗарплатыДляНебольшихОрганизаций;
	
	ИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	
	УстановитьУсловноеОформление();
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		УправлениеФормой(ЭтотОбъект);
	Иначе
		ПустойСписок = ПустойСписок(Организация);
		Если ПустойСписок Тогда
			УправлениеФормой(ЭтотОбъект);
		Иначе
			Если НЕ ЭтоМедленныйРежимРаботы И НЕ ЭтоВебКлиент Тогда
				ПолучитьДанныеРазделаНаСервере();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Организация) 
		И НЕ ПустойСписок Тогда
		
		Если ЭтоМедленныйРежимРаботы ИЛИ ЭтоВебКлиент Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьДанныеРаздела",0.5, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	ТекущийСотрудник = Неопределено;
	Если СтрокаТаблицы <> Неопределено Тогда
		ТекущийСотрудник = СтрокаТаблицы.Ссылка;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеДанныхМестаРаботы" Тогда
		
		ПолучитьДанныеРаздела(Параметр);
		
	ИначеЕсли ИмяСобытия = "СозданСотрудник"
		ИЛИ ИмяСобытия = "Запись_Сотрудники" Тогда
		
		ПолучитьДанныеРаздела(Параметр);
		
		Если ПустойСписок Тогда
			ПустойСписок = Ложь;
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_НачислениеЗарплаты"
		ИЛИ ИмяСобытия = "Запись_БольничныйЛист"
		ИЛИ ИмяСобытия = "Запись_Отпуск"
		ИЛИ ИмяСобытия = "Запись_ВедомостьНаВыплатуЗарплатыВБанк"
		ИЛИ ИмяСобытия = "Запись_ВедомостьНаВыплатуЗарплатыВКассу"
		ИЛИ ИмяСобытия = "Запись_ПриемНаРаботу"
		ИЛИ ИмяСобытия = "Запись_КадровыйПеревод"
		ИЛИ ИмяСобытия = "Запись_Увольнение" Тогда
		
		ПолучитьДанныеРаздела(Источник);
		
	ИначеЕсли ИмяСобытия = "Запись_НачислениеДивидендов" Тогда
		
		ПолучитьДанныеРаздела();
		
	ИначеЕсли ИмяСобытия = "Запись_СписаниеСРасчетногоСчета"
		ИЛИ ИмяСобытия = "Запись_РасходныйКассовыйОрдер" 
		ИЛИ ИмяСобытия = "ОбновитьФорму" Тогда
		
		Если ТребуетсяПолучитьДанныеОплаты(Источник) Тогда
			ПолучитьДанныеРаздела();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗарплатаРассчитана_ПомощникУчетаЗарплаты" Тогда
		Если Параметр.Организация = Организация Тогда
			ПолучитьДанныеРаздела();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // Незачем очищать
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииПриИзменении(Элемент)
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", ПериодРегистрации, КонецМесяца(ПериодРегистрации));
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, Элементы.ПредставлениеПериодаРегистрации, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода()
	
	Если ПериодРегистрации > ТекущаяДатаРаботы Тогда
		ТекущаяДатаРаботы = ПериодРегистрации;
	КонецЕсли;
	
	// Обновляем представление периода на форме.
	ПредставлениеПериодаРегистрации = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		ПериодРегистрации,
		КонецМесяца(ПериодРегистрации));
		
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПриемНаРаботуНажатие(Элемент)
	
	СоздатьДокументПриемНаРаботу();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСообщениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ПанельАдминистрированияБП.Форма.Интерфейс");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ВидНачисленияЗарплата"
		ИЛИ Поле.Имя = "Начислено"
		ИЛИ Поле.Имя = "Удержано"
		ИЛИ Поле.Имя = "Выплачено"
		ИЛИ Поле.Имя = "КВыплате" Тогда
		ОткрытьРасчетныйДокумент("НачислениеЗарплаты");
	ИначеЕсли Поле.Имя = "ВидНачисленияДивиденды"
		ИЛИ Поле.Имя = "НачисленоДивиденды"
		ИЛИ Поле.Имя = "НДФЛДивиденды"
		ИЛИ Поле.Имя = "ВыплаченоДивиденды"
		ИЛИ Поле.Имя = "КВыплатеДивиденды" Тогда
		ОткрытьРасчетныйДокумент("НачислениеДивидендов");
	Иначе
		ОткрытьКарточкуСотрудника();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УменьшитьПериод(Команда)
	
	ПериодРегистрации = ДобавитьМесяц(ПериодРегистрации,-1);
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьПериод(Команда)
	
	ПериодРегистрации = ДобавитьМесяц(ПериодРегистрации, 1);
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетЗарплаты(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",    ПериодРегистрации);
	ПараметрыФормы.Вставить("Правило",          "");
	
	ОткрытьФорму("Обработка.ПомощникУчетаЗарплаты.Форма.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыплатитьАванс(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",    ПериодРегистрации);
	ПараметрыФормы.Вставить("Аванс",            Истина);
	ПараметрыФормы.Вставить("Правило",          "");
	
	ОткрытьФорму("Обработка.ПомощникУчетаЗарплаты.Форма.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтрокаТаблицы.Работает Тогда
		
		ТекущийМесяц = Месяц(ПериодРегистрации);
		
		ТекстГод   = Прав(ПредставлениеПериодаРегистрации,4);
		ТекстМесяц = Лев(ПредставлениеПериодаРегистрации,
			СтрДлина(ПредставлениеПериодаРегистрации)-?(ТекущийМесяц = 3 ИЛИ ТекущийМесяц = 8,5,6));
		ТекстМесяц = СтрШаблон(НСтр("ru = '%1е'"), ТекстМесяц);
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сотрудник ""%1"" не работает в организации в %2 %3 г.'"),
			СтрокаТаблицы.Наименование,
			ТекстМесяц,
			ТекстГод);
	Иначе
		
		Если НЕ(Команда.Имя = "ДокументСправкаНДФЛ" ИЛИ Команда.Имя = "ДокументИсполнительныйЛист") Тогда
			ПараметрыДокумента = Новый Структура("Организация, ПериодРегистрации, Сотрудник", 
												Организация, НачалоМесяца(ПериодРегистрации), СтрокаТаблицы.Ссылка);
			ЗначенияЗаполнения = Новый Структура("ЗначенияЗаполнения", ПараметрыДокумента);
		Иначе
			ЗначенияЗаполнения = Новый Структура("Основание", СтрокаТаблицы.Ссылка);
		КонецЕсли;
		
		ОткрытьФорму("Документ." + СтрЗаменить(Команда.Имя, "Документ","") + ".ФормаОбъекта",
			ЗначенияЗаполнения,
			ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументПриемНаРаботу(Команда)
	
	СоздатьДокументПриемНаРаботу();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументКадровыйПеревод(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьКадровыйУчет Тогда
		СотрудникиКлиент.ОформитьНаОсновании(ЭтаФорма, СтрокаТаблицы.Ссылка, "Документ." + СтрЗаменить(Команда.Имя, "Документ",""));
	Иначе
		ПараметрыСотрудника = Новый Структура("Ключ", СтрокаТаблицы.Ссылка);
		ФормаСотрудника = ПолучитьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыСотрудника);
		ФормаСотрудника.ТекущийЭлемент = ФормаСотрудника.Элементы.ТекущаяТарифнаяСтавка;
		ФормаСотрудника.Открыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУвольнение(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьКадровыйУчет Тогда
		СотрудникиКлиент.ОформитьНаОсновании(ЭтаФорма, СтрокаТаблицы.Ссылка, "Документ." + СтрЗаменить(Команда.Имя, "Документ",""));
	Иначе
		ПараметрыСотрудника = Новый Структура("Ключ", СтрокаТаблицы.Ссылка);
		ФормаСотрудника = ПолучитьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыСотрудника);
		ФормаСотрудника.Элементы.ДатаУвольнения.Видимость = Истина;
		ФормаСотрудника.Элементы.Уволить.Видимость        = Ложь;
		ФормаСотрудника.ТекущийЭлемент = ФормаСотрудника.Элементы.ДатаУвольнения;
		ФормаСотрудника.Открыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументНачислениеДивидендов(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаДокумента = ?(КонецМесяца(ТекущаяДата()) = КонецМесяца(ПериодРегистрации),
		ТекущаяДата(),
		КонецМесяца(ПериодРегистрации));
		
	ПараметрыДокумента = Новый Структура("Организация, Дата, Учредитель", 
										Организация, ДатаДокумента, СтрокаТаблицы.ФизическоеЛицо);
	ЗначенияЗаполнения = Новый Структура("ЗначенияЗаполнения", ПараметрыДокумента);
		
	ОткрытьФорму("Документ.НачислениеДивидендов.ФормаОбъекта",
		ЗначенияЗаполнения,
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТ2(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивСотрудников = Новый Массив;
	МассивСотрудников.Добавить(СтрокаТаблицы.Ссылка);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Справочник.Сотрудники",
		"ПФ_MXL_Т2",
		МассивСотрудников,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетВзаиморасчеты(Команда)
	
	Вариант = ПолучитьВариантОтчета("АнализНачисленийИУдержаний", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетНДФЛ(Команда)
	
	Вариант = ПолучитьВариантОтчета("АнализНДФЛ", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСтраховыеВзносы(Команда)
	
	Вариант = ПолучитьВариантОтчета("АнализВзносовВФонды", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСотрудники(Команда)
	
	Вариант = ПолучитьВариантОтчета("ОтчетыПоСотрудникам", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетНалогиИВзносыКратко(Команда)
	
	Вариант = ПолучитьВариантОтчета("НалогиИВзносыКратко", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТ13(Команда)
	
	Вариант = ПолучитьВариантОтчета("УнифицированнаяФормаТ13", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетАнализРасходовНаОплатуТруда(Команда)
	
	ОткрытьФорму("Отчет.АнализРасходовНаОплатуТруда.ФормаОбъекта");
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаСотрудника(Команда)
	
	ОткрытьКарточкуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкаУдаления(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОСотруднике = Новый Структура("Ссылка, ДатаПриема, ДатаУвольнения, ДатаРождения, Работает, Картинка, ПометкаУдаления");
	ЗаполнитьЗначенияСвойств(СведенияОСотруднике, СтрокаТаблицы);
	
	УстановитьСнятьПометкуУдаления(СведенияОСотруднике);
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СведенияОСотруднике, "Работает, Картинка, ПометкаУдаления");
	Объект.Список.Сортировать("Работает Убыв, ПометкаУдаления Возр, Наименование Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Оформление таблицы Список
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
	ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ПризнакРаботника",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"Объект.Список.Работает", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"Объект.Список.ПометкаУдаления", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидНачисленияЗарплата");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидНачисленияДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачисленоДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НДФЛДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВыплаченоДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КВыплатеДивиденды");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Список.НачисленоДивиденды", ВидСравненияКомпоновкиДанных.Равно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Если НЕ ЗначениеЗаполнено(Форма.Организация) Тогда
		Форма.Элементы.ГруппаСписокСотрудников.ТекущаяСтраница = Форма.Элементы.СтраницаНеВыбранаОрганизация;
		Форма.Элементы.ГруппаКоманднаяПанель.Доступность = Ложь;
	ИначеЕсли Форма.ПустойСписок Тогда
		Форма.Элементы.ГруппаСписокСотрудников.ТекущаяСтраница = Форма.Элементы.СтраницаПустая;
		Форма.Элементы.ГруппаКоманднаяПанель.Доступность = Ложь;
	Иначе
		Форма.Элементы.ГруппаСписокСотрудников.ТекущаяСтраница = Форма.Элементы.СтраницаСписок;
		Форма.Элементы.ГруппаКоманднаяПанель.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонки();

	Для каждого Строка ИЗ Объект.Список Цикл
		Строка.ВидНачисленияЗарплата  = НСтр("ru = 'зарплата:'");
		Строка.ВидНачисленияДивиденды = НСтр("ru = 'дивиденды:'");
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтоги()
	
	ИтогоНачислено = Объект.Список.Итог("Начислено") + Объект.Список.Итог("НачисленоДивиденды");
	ИтогоУдержано  = Объект.Список.Итог("Удержано") + Объект.Список.Итог("НДФЛДивиденды");
	ИтогоВыплачено = Объект.Список.Итог("Выплачено") + Объект.Список.Итог("ВыплаченоДивиденды");
	ИтогоКВыплате  = Объект.Список.Итог("КВыплате") + Объект.Список.Итог("КВыплатеДивиденды");
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЭтоПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	ПустойСписок = ПустойСписок(Организация);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПустойСписок(Организация)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Сотрудники.Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ГоловнаяОрганизация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
	
КонецФункции

&НаСервере
Функция ВыполнитьПолучениеДанныхНаСервере(Параметр = Неопределено)
	
	ПараметрыОбработки = СформироватьПараметрыОбработки(Параметр);
	
	НаименованиеЗадания = НСтр("ru = 'Раздел ""Сотрудники"": получение данных'");
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.ПростойИнтерфейсРазделСотрудники.ПодготовитьДанные", 
			ПараметрыОбработки, 
			НаименованиеЗадания);
			
		АдресХранилища = Результат.АдресХранилища;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьРезультат()
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ОбновитьВесьСписок  = Ложь;
		ОбновитьСотрудников = Ложь;
		Результат.Свойство("ОбновитьВесьСписок",  ОбновитьВесьСписок);
		Результат.Свойство("ОбновитьСотрудников", ОбновитьСотрудников);
		
		Если ОбновитьВесьСписок ИЛИ ОбновитьСотрудников Тогда
			
			Если Результат.Свойство("Список") Тогда
				Если ТипЗнч(Результат.Список) = Тип("ТаблицаЗначений") Тогда
					
					Если ОбновитьВесьСписок Тогда
						Объект.Список.Очистить();
						Объект.Список.Загрузить(Результат.Список);
					КонецЕсли;
					
					Если ОбновитьСотрудников Тогда
						ОбновитьДанныеСотрудников(Результат.Список);
					КонецЕсли;
					
					Если Объект.Список.Итог("ИсполнительныйЛист")<> 0 Тогда
						Элементы.Удержано.Заголовок = НСтр("ru = 'НДФЛ и алименты'");
					Иначе
						Элементы.Удержано.Заголовок = НСтр("ru = 'НДФЛ'");
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонки();
	ЗаполнитьИтоги();
	Элементы.ГруппаНачисленоВидНачисления.Видимость = Объект.Список.Итог("НачисленоДивиденды") <> 0;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьДанныеРаздела()
	
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеРаздела(Параметр = Неопределено)
	
	Результат = ВыполнитьПолучениеДанныхНаСервере(Параметр);
	
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	Иначе
		ЗагрузитьРезультат();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеРазделаНаСервере()
	
	ПараметрыОбработки = СформироватьПараметрыОбработки();
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	Обработки.ПростойИнтерфейсРазделСотрудники.ПодготовитьДанные(ПараметрыОбработки, АдресХранилища);
	ЗагрузитьРезультат();
	
КонецПроцедуры

&НаСервере
Функция ТребуетсяПолучитьДанныеОплаты(Источник)
	
	Если ТипЗнч(Источник) <> Тип("ДокументСсылка.СписаниеСРасчетногоСчета")
		И ТипЗнч(Источник) <> Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, ВидОперации");
	
	Если РеквизитыДокумента.Организация = ЭтотОбъект.Организация Тогда
		Если РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаДивидендов
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику Тогда
			
			Возврат Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеСотрудников(ТаблицаДаных)
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаДаных Цикл
		
		МассивСтрок = Объект.Список.НайтиСтроки(Новый Структура("Ссылка", СтрокаТаблицы.Ссылка));
		Если МассивСтрок.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(МассивСтрок[0], СтрокаТаблицы);
		Иначе
			НоваяСтрока = Объект.Список.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Объект.Список.Сортировать("Работает Убыв, ПометкаУдаления Возр, Наименование Возр");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПериодРегистрации = РезультатВыбора.НачалоПериода;
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРасчетныйДокументЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", РезультатВыбора.Значение);
	ОткрытьФорму("Документ." + ДополнительныеПараметры.ТипРасчета + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументПриемНаРаботу()
	
	Если ИспользоватьКадровыйУчет Тогда
		ОткрытьФорму("Документ.ПриемНаРаботу.Форма.ФормаДокумента");
	Иначе
		ОткрытьФорму("Справочник.Сотрудники.Форма.ФормаЭлемента");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВариантОтчета(ИмяОтчета, ИмяВарианта)
	
	Отчет         = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Отчет." + ИмяОтчета);
	ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, ИмяВарианта);
	Возврат ВариантОтчета;
	
КонецФункции

&НаКлиенте
Процедура ОтчетДругиеОтчеты(Команда)
	
	ВариантыОтчетовКлиент.ПоказатьПанельОтчетов(
		"Отчеты.Сотрудники", 
		Неопределено);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуСотрудника()
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыЭлементаСправочникаСотрудники");
	
	ПараметрыФормы = Новый Структура("Ключ", СтрокаТаблицы.Ссылка);
	ОткрытьФорму("Справочник.Сотрудники.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасчетныйДокумент(ТипРасчета)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасчетныйДокумент = РасчетныйДокументНаСервере(СтрокаТаблицы.Ссылка, ТипРасчета);
	Если РасчетныйДокумент <> Неопределено Тогда
		Если ТипЗнч(РасчетныйДокумент) = Тип("СписокЗначений") Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьРасчетныйДокументЗавершение", ЭтотОбъект, Новый Структура("ТипРасчета", ТипРасчета));
			ПоказатьВыборИзМеню(ОписаниеОповещения, РасчетныйДокумент);
		Иначе
			ПараметрыФормы = Новый Структура("Ключ", РасчетныйДокумент);
			ОткрытьФорму("Документ." + ТипРасчета + ".ФормаОбъекта", ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РасчетныйДокументНаСервере(Сотрудник, ТипРасчета)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("Сотрудник",               Сотрудник);
	Запрос.УстановитьПараметр("ФизическоеЛицо",          ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо"));
	Запрос.УстановитьПараметр("ПериодРегистрацииНачало", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ПериодРегистрацииКонец",  КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Зарплата",                ТипРасчета = "НачислениеЗарплаты");
	Запрос.УстановитьПараметр("Дивиденды",               ТипРасчета = "НачислениеДивидендов");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачислениеЗарплатыСотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.НачислениеЗарплаты.Сотрудники КАК НачислениеЗарплатыСотрудники
	|ГДЕ
	|	НачислениеЗарплатыСотрудники.Ссылка.Организация = &Организация
	|	И НачислениеЗарплатыСотрудники.Ссылка.Проведен
	|	И НачислениеЗарплатыСотрудники.Сотрудник = &Сотрудник
	|	И НачислениеЗарплатыСотрудники.Ссылка.МесяцНачисления МЕЖДУ &ПериодРегистрацииНачало И &ПериодРегистрацииКонец
	|	И &Зарплата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачислениеДивидендов.Ссылка
	|ИЗ
	|	Документ.НачислениеДивидендов КАК НачислениеДивидендов
	|ГДЕ
	|	НачислениеДивидендов.Организация = &Организация
	|	И НачислениеДивидендов.Проведен
	|	И НачислениеДивидендов.ТипУчредителя = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И НачислениеДивидендов.Учредитель = &ФизическоеЛицо
	|	И НачислениеДивидендов.Дата МЕЖДУ &ПериодРегистрацииНачало И &ПериодРегистрацииКонец
	|	И &Дивиденды";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Возврат Неопределено;
	ИначеЕсли Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		СписокДокументов = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			СписокДокументов.Добавить(Выборка.Ссылка);
		КонецЦикла;
		Возврат СписокДокументов;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьСнятьПометкуУдаления(СведенияОСотруднике)
	
	СотрудникОбъект = СведенияОСотруднике.Ссылка.ПолучитьОбъект();
	СотрудникОбъект.УстановитьПометкуУдаления(НЕ СотрудникОбъект.ПометкаУдаления);
	
	СведенияОСотруднике.ПометкаУдаления = СотрудникОбъект.ПометкаУдаления;
	
	Обработки.ПростойИнтерфейсРазделСотрудники.УстановитьСтатусСотрудника(СведенияОСотруднике, ПериодРегистрации);
	
КонецПроцедуры

Функция РасчетныйПериод(Организация, ТекущаяДатаРаботы);
	
	ТекущийПериод       = НачалоМесяца(ТекущаяДатаРаботы);
	ПериодЗадолженности = ТекущийПериод;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		НастройкиУчетаЗарплаты = РегистрыСведений.НастройкиУчетаЗарплаты.СоздатьМенеджерЗаписи();
		НастройкиУчетаЗарплаты.Организация = Организация;
		НастройкиУчетаЗарплаты.Прочитать();
		ДатаВыплатыЗарплаты = НастройкиУчетаЗарплаты.ДатаВыплатыЗарплаты;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗарплатаКВыплатеОстатки.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов
		|ИЗ
		|	РегистрНакопления.ЗарплатаКВыплате.Остатки(, Организация = &Организация) КАК ЗарплатаКВыплатеОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодВзаиморасчетов";
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ПериодЗадолженности = НачалоМесяца(Выборка.ПериодВзаиморасчетов);
		КонецЕсли;
	Иначе
		ДатаВыплатыЗарплаты = День(ТекущаяДатаРаботы);
	КонецЕсли;
	
	Если ПериодЗадолженности < ТекущийПериод Тогда
		РасчетныйПериод = ПериодЗадолженности;
	Иначе
		Если ДатаВыплатыЗарплаты >= День(ТекущаяДатаРаботы) Тогда
			РасчетныйПериод = НачалоМесяца(ДобавитьМесяц(ТекущийПериод, -1));
		Иначе
			РасчетныйПериод = ТекущийПериод;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РасчетныйПериод;
	
КонецФункции

#Область ДлительныеОперации

&НаСервере
Функция СформироватьПараметрыОбработки(Параметр = Неопределено)
	
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("Организация",             Организация);
	ПараметрыОбработки.Вставить("ПериодРегистрации",       ПериодРегистрации);
	ПараметрыОбработки.Вставить("ТекущаяДатаРаботы",       ТекущаяДатаРаботы);
	ПараметрыОбработки.Вставить("Параметр",                Параметр);
	
	Возврат ПараметрыОбработки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
				ЗагрузитьРезультат();
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания", 
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
					Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

