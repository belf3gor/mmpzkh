#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияКомплектовОтчетности

Процедура ДанныеКомплектаВОбъектПоСсылке(КомплектСсылка, ИнициализироватьДокумент = Ложь) Экспорт
	Если ЗначениеЗаполнено(КомплектСсылка) Тогда
		Комплект = КомплектСсылка.ПолучитьОбъект();
		
		ДанныеКомплектаВОбъект(Комплект, ИнициализироватьДокумент);
	Иначе
		ДокументыКомплекта.Очистить();
	КонецЕсли;	
КонецПроцедуры	

Процедура ДанныеКомплектаВОбъект(Комплект, ИнициализироватьДокумент = Ложь) Экспорт
	ОчиститьДанные();
	
	ДокументыКомплекта.Очистить();
	
	КомплектОбъект = Комплект;
	
	ПоместитьОписаниеРСВВТаблицуДокументов();
	
	ОписаниеАДВ62 = ВыборкаПоШапкеАДВ62(КомплектОбъект.ОписьПачекСЗВ);

	Если ОписаниеАДВ62.Следующий() Тогда
		ПоместитьОписаниеАДВ62ВТаблицуДокументов(ОписаниеАДВ62);
	КонецЕсли;	
	
	ПачкиСЗВ = Новый Массив;
	
	Для Каждого СтрокаПачка Из КомплектОбъект.СписокПачекСЗВ Цикл
		ПачкиСЗВ.Добавить(СтрокаПачка.ПачкаДокументов);
	КонецЦикла;	
	
	ПачкиСЗВ63 = Новый Массив;
	
	Для Каждого СтрокаПачка Из КомплектОбъект.СписокПачекСЗВ6_3 Цикл
		ПачкиСЗВ63.Добавить(СтрокаПачка.ПачкаДокументов);
	КонецЦикла;	

	ОписаниеПачекСЗВ6 = ВыборкаПоШапкеПачекСЗВ6(ПачкиСЗВ);	
	
	Пока ОписаниеПачекСЗВ6.Следующий() Цикл
		ПоместитьОписаниеСЗВ6ВТаблицуДокументов(ОписаниеПачекСЗВ6);			
	КонецЦикла;	
	
	ОписаниеПачекСЗВ63 = ВыборкаПоШапкеПачекСЗВ63(ПачкиСЗВ63);	
	
	Пока ОписаниеПачекСЗВ63.Следующий() Цикл
		ПоместитьОписаниеСЗВ63ВТаблицуДокументов(ОписаниеПачекСЗВ63);			
	КонецЦикла;	
	
	Если ИнициализироватьДокумент
		И ДокументыКомплекта.Количество() > 0 Тогда
		
		ДанныеДокументаВОбъектПоСсылке(ДокументыКомплекта[0].Ссылка);
	Иначе 
		СсылкаНаРедактируемыйДокумент = Неопределено;
	КонецЕсли;
	
	ДокументыКомплекта.Сортировать("ПорядокСортировки, КорректируемыйПериод, ТипСведенийСЗВ, КатегорияЗастрахованныхЛиц, ТипДоговора, Ссылка");
	
	Организация = Комплект.Организация;
	ОтчетныйПериод = Комплект.ОтчетныйПериод;
КонецПроцедуры	

Процедура ПоместитьАктивныйКомплектПериодаВОбъект() Экспорт
	ОписаниеКомплекта = Справочники.КомплектыОтчетностиПерсучета.ОписаниеАктивногоКомплектаПериода(Организация, ОтчетныйПериод);
	
	Если ОписаниеКомплекта = Неопределено Тогда
		КомплектОбъект = Неопределено;
		
		УстановитьПривилегированныйРежим(Истина);
		
		КомплектОбъект = Справочники.КомплектыОтчетностиПерсучета.СоздатьЭлемент();
		КомплектОбъект.Организация = Организация;
		КомплектОбъект.ОтчетныйПериод = ОтчетныйПериод;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		КомплектОбъект.СостояниеКомплекта = Перечисления.СостояниеКомплектаОтчетностиПерсучета.СведенияНеСформированы;
		
		ДанныеКомплектаВОбъект(КомплектОбъект, Истина);
	Иначе
		ДанныеКомплектаВОбъект(ОписаниеКомплекта.Ссылка.ПолучитьОбъект(), Истина);
	КонецЕсли;	
КонецПроцедуры	

Процедура ЗаполнитьДанныеКомплектаИзВременногоХранилища(АдресХранилища) Экспорт
	
	ДанныеКомплекта = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(ДанныеКомплекта) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если КомплектОбъект.Ссылка <> ДанныеКомплекта.Ссылка Тогда
		Если Не КомплектОбъект.Ссылка.Пустая() Тогда
			КомплектОбъект.Записать();
		КонецЕсли;	
		
		Комплект = Справочники.КомплектыОтчетностиПерсучета.СоздатьЭлемент();
	Иначе
		Комплект = КомплектОбъект;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Комплект, ДанныеКомплекта);
	
	Комплект.СписокПачекСЗВ.Загрузить(ДанныеКомплекта.СписокПачекСЗВ);
	Комплект.СписокПачекСЗВ6_3.Загрузить(ДанныеКомплекта.СписокПачекСЗВ6_3);
	
	Комплект.ПериодФормирования = ДанныеКомплекта.ПериодФормирования;
	
	Комплект.Записать();
	
	МодифицированностьКомплекта = Ложь;
	
	ДанныеКомплектаВОбъект(Комплект, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьКорректирующиеСведенияИзВременногоХранилища(АдресХранилища) Экспорт
	КорректирующиеДокументы = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(КорректирующиеДокументы) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	КомплектОбъект.ПоместитьКорректирующиеСведения(КорректирующиеДокументы);
	
	МодифицированностьКомплекта = Истина;
	
	ДанныеКомплектаВОбъект(КомплектОбъект, Истина);
КонецПроцедуры	

Процедура ЗаполнитьСписокКорректирующихДокументовЗаПериод(КорректируемыйПериод, ОписаниеКорректирующихДокументов) Экспорт
	СсылкиНаКорректирующиеДокументы = Новый Массив;
	
	Для Каждого ОписаниеДокумента Из ОписаниеКорректирующихДокументов Цикл
		Если ОписаниеДокумента.Ссылка.Пустая() Тогда
			Ссылка = КомплектОбъект.НовыйПачкаСЗВ6(
						ТипЗнч(ОписаниеДокумента.Ссылка), 
						ОписаниеДокумента.ТипСведенийСЗВ, 
						ОписаниеДокумента.КатегорияЗастрахованныхЛиц, 
						КорректируемыйПериод, 
						ОписаниеДокумента.ТипДоговора,
						Истина,
						ОписаниеДокумента.РегистрационныйНомерПФРвКорректируемыйПериод);
						
			СсылкиНаКорректирующиеДокументы.Добавить(Ссылка);	
			
			МодифицированностьКомплекта = Истина;
			МодифицированностьОписи = Истина;
		Иначе 
			СсылкиНаКорректирующиеДокументы.Добавить(ОписаниеДокумента.Ссылка);
			
			ДанныеШапкиДокумента = ДокументыКомплекта.Найти(ОписаниеДокумента.Ссылка);
			
			Если ДанныеШапкиДокумента.КатегорияЗастрахованныхЛиц <> ОписаниеДокумента.КатегорияЗастрахованныхЛиц
				Или ДанныеШапкиДокумента.ТипДоговора <> ОписаниеДокумента.ТипДоговора
				Или ДанныеШапкиДокумента.ТипСведенийСЗВ <> ОписаниеДокумента.ТипСведенийСЗВ Тогда
				
				ДокументОбъект = ДанныеШапкиДокумента.Ссылка.ПолучитьОбъект();
				ЗаполнитьЗначенияСвойств(ДокументОбъект, ОписаниеДокумента, ,"Ссылка");
				ДокументОбъект.Записать();
			КонецЕсли;		
		КонецЕсли;		
	КонецЦикла;
	
	ОписаниеКорректируемогоПериода = КомплектОбъект.КорректируемыеПериоды.Найти(КорректируемыйПериод, "Период");
	
	Если ОписаниеКорректируемогоПериода = Неопределено 
		И СсылкиНаКорректирующиеДокументы.Количество() > 0 Тогда
		
		ОписаниеКорректируемогоПериода = КомплектОбъект.КорректируемыеПериоды.Добавить();
		ОписаниеКорректируемогоПериода.Период = КорректируемыйПериод;
		ОписаниеКорректируемогоПериода.ВариантФормирования = Перечисления.ВариантыФормированияКорректирующихСведенийВПФР.РучнойВводДокументов;
	ИначеЕсли ОписаниеКорректируемогоПериода <> Неопределено
		И СсылкиНаКорректирующиеДокументы.Количество() > 0 Тогда 
		
		ОписаниеКорректируемогоПериода.ВариантФормирования = Перечисления.ВариантыФормированияКорректирующихСведенийВПФР.РучнойВводДокументов;	
	ИначеЕсли ОписаниеКорректируемогоПериода <> Неопределено Тогда 
		КомплектОбъект.КорректируемыеПериоды.Удалить(ОписаниеКорректируемогоПериода);
	КонецЕсли;

	УдаляемыеДокументы = Новый Массив;
	Для Каждого ДокументКомплекта Из ДокументыКомплекта Цикл
		Если ПерсонифицированныйУчетКлиентСервер.ЭтоДокументСведенийОВзносахИСтаже(ДокументКомплекта.Ссылка)
			И ДокументКомплекта.ТипСведенийСЗВ <> Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ
			И ДокументКомплекта.КорректируемыйПериод = КорректируемыйПериод
			И СсылкиНаКорректирующиеДокументы.Найти(ДокументКомплекта.Ссылка) = Неопределено Тогда
			
			УдаляемыеДокументы.Добавить(ДокументКомплекта.Ссылка);	
		КонецЕсли;	
	КонецЦикла;	
	
	Если УдаляемыеДокументы.Количество() > 0 Тогда
		КомплектОбъект.УдалитьДокументыКомплекта(УдаляемыеДокументы);
		МодифицированностьКомплекта = Истина;
		МодифицированностьОписи = Истина;
	КонецЕсли;
	
	ДанныеКомплектаВОбъект(КомплектОбъект, Истина);	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияПараметровФормированияСведений

Функция ПараметрыФормированияКорректирующихСведений() Экспорт
	Возврат КомплектОбъект.ПараметрыФормированияКорректирующихСведений();	
КонецФункции	

#КонецОбласти

#Область ИнициализацияДокументовПерсОтчетности

Процедура ДанныеДокументаВОбъектПоСсылке(Ссылка, СохранятьИзмененияВТекущемДокументе = Ложь, ОбновлятьДанныеШапки = Ложь) Экспорт
	Если СохранятьИзмененияВТекущемДокументе Тогда
		СохранитьДанныеТекущегоДокумента();
	КонецЕсли;	
	
	ОчиститьДанные();
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		СсылкаНаРедактируемыйДокумент = Неопределено;
		
		Возврат;		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		СсылкаНаРедактируемыйДокумент = Ссылка;
		ДанныеРСВ_1ВОбъект();
		Если ОбновлятьДанныеШапки Тогда
			ОбновляемыеДокументы = Новый Массив;
			ОбновляемыеДокументы.Добавить(Ссылка);
			ОбновитьДанныеШапкиДокументов(ОбновляемыеДокументы);
		КонецЕсли;	
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОписьПачекСЗВ_6") Тогда
		СсылкаНаРедактируемыйДокумент = Ссылка;
		СводныеДанныеПоКомплектуВОбъект();	
		Если ОбновлятьДанныеШапки Тогда
			ОбновляемыеДокументы = Новый Массив;
			ОбновляемыеДокументы.Добавить(Ссылка);
			ОбновитьДанныеШапкиДокументов(ОбновляемыеДокументы)
		КонецЕсли;	
	Иначе
		ДанныеДокументаВОбъект(Ссылка.ПолучитьОбъект(), ОбновлятьДанныеШапки);		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ДанныеДокументаВОбъект(ДокументОбъект, ОбновлятьДанныеШапки = Ложь) Экспорт
	ОчиститьДанные();
	
	ДокументОбъект.Прочитать();
	
	СсылкаНаРедактируемыйДокумент = ДокументОбъект.Ссылка;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РегламентированныйОтчет") Тогда
		ДанныеРСВ_1ВОбъект();
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОписьПачекСЗВ_6") Тогда
		СводныеДанныеПоКомплектуВОбъект();	
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПачкаДокументовСЗВ_6_1") 
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеестрСЗВ_6_2") Тогда
		
		ДанныеСЗВ_6ВОбъект(ДокументОбъект);
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПачкаДокументовСЗВ_6_3") Тогда
		ДанныеСЗВ_6_3ВОбъект(ДокументОбъект);	
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПачкаДокументовСЗВ_6_4") Тогда
    	ДанныеСЗВ_6_4ВОбъект(ДокументОбъект);	
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПачкаРазделов6РасчетаРСВ_1") Тогда
		ДанныеПачкиРазделов6ВОбъект(ДокументОбъект);
	КонецЕсли;	
	
	Если ОбновлятьДанныеШапки Тогда
		ОбновляемыеДокументы = Новый Массив;
		ОбновляемыеДокументы.Добавить(ДокументОбъект.Ссылка);
		ОбновитьДанныеШапкиДокументов(ОбновляемыеДокументы)
	КонецЕсли;	
КонецПроцедуры	

Процедура ОбновитьДанныеШапкиДокументов(ОбновляемыеДокументы) Экспорт
	СтрокиТаблицыПоСсылка = Новый Соответствие;
	
	Для Каждого СтрокаТаблицыДокументов Из ДокументыКомплекта Цикл 
		СтрокиТаблицыПоСсылка.Вставить(СтрокаТаблицыДокументов.Ссылка, СтрокаТаблицыДокументов);
	КонецЦикла;	
	
	ПачкиСЗВ6 = Новый Массив;
	ПачкиСЗВ63 = Новый Массив;
	РСВ_1 = Документы.РегламентированныйОтчет.ПустаяСсылка();
	АДВ_6_2 = Документы.ОписьПачекСЗВ_6.ПустаяСсылка();
	
	Для Каждого СсылкаНаОбновляемыйДокумент Из ОбновляемыеДокументы Цикл
		Если СтрокиТаблицыПоСсылка.Получить(СсылкаНаОбновляемыйДокумент) = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_1")
			Или ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_4")
			Или ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.РеестрСЗВ_6_2")
			Или ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.ПачкаРазделов6РасчетаРСВ_1") Тогда
			
			ПачкиСЗВ6.Добавить(СсылкаНаОбновляемыйДокумент);
		ИначеЕсли ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3") Тогда 
	    	ПачкиСЗВ63.Добавить(СсылкаНаОбновляемыйДокумент);
		ИначеЕсли ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.ОписьПачекСЗВ_6") Тогда
			АДВ_6_2 = СсылкаНаОбновляемыйДокумент;
		ИначеЕсли ТипЗнч(СсылкаНаОбновляемыйДокумент) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			РСВ_1 = СсылкаНаОбновляемыйДокумент;
		КонецЕсли;	
	КонецЦикла;	
		
	Если ЗначениеЗаполнено(РСВ_1) Тогда
		СтрокаТаблицыДокументов = СтрокиТаблицыПоСсылка[РСВ_1];
		ЗаполнитьОписаниеРСВВТаблицеДокументов(СтрокаТаблицыДокументов);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АДВ_6_2) Тогда
		СтрокаТаблицыДокументов = СтрокиТаблицыПоСсылка[АДВ_6_2];
		
		ОписаниеАДВ62 = ВыборкаПоШапкеАДВ62(АДВ_6_2);
		
		Если ОписаниеАДВ62.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокументов, ОписаниеАДВ62);
		КонецЕсли;
	КонецЕсли;	
		
	Если ПачкиСЗВ6.Количество() > 0 Тогда
		ОписаниеПачкиСЗВ6 = ВыборкаПоШапкеПачекСЗВ6(ПачкиСЗВ6);	
		
		Пока ОписаниеПачкиСЗВ6.Следующий() Цикл 
			СтрокаТаблицыДокументов = СтрокиТаблицыПоСсылка[ОписаниеПачкиСЗВ6.Ссылка];
			
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокументов, ОписаниеПачкиСЗВ6);	
	
			Если СтрокаТаблицыДокументов.ТипСведенийСЗВ <> Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ Тогда
				СтрокаТаблицыДокументов.КорректируемыйПериодСтрокой = ПерсонифицированныйУчетКлиентСервер.ПредставлениеОтчетногоПериода(СтрокаТаблицыДокументов.КорректируемыйПериод);		
			КонецЕсли;				
		КонецЦикла;	
	КонецЕсли;	
	
	Если ПачкиСЗВ63.Количество() > 0 Тогда
		
		ОписаниеПачкиСЗВ63 = ВыборкаПоШапкеПачекСЗВ63(ПачкиСЗВ63);	
		
		Пока ОписаниеПачкиСЗВ63.Следующий() Цикл 
			СтрокаТаблицыДокументов = СтрокиТаблицыПоСсылка[ОписаниеПачкиСЗВ63.Ссылка];

			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокументов, ОписаниеПачкиСЗВ6);
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область РедактированиеСпискаДокументов

Процедура ДобавитьПачкуСЗВ(ТипДокумента, КорректирующиеСведений = Ложь) Экспорт
	Если КорректирующиеСведений Тогда
		ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ;	
		
		КорректируемыйПериод = ПерсонифицированныйУчетКлиентСервер.ПредшествующийОтчетныйПериодПерсУчета(ОтчетныйПериод);
		
		Если (ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_1")
			Или ТипДокумента = Тип("ДокументСсылка.РеестрСЗВ_6_2"))
			И Год(КорректируемыйПериод) >= 2013 Тогда
			
			КорректируемыйПериод = '20121001';
		КонецЕсли;	
		
		КатегорииЗастрахованныхЛиц = ПерсонифицированныйУчет.КатегорииЗастрахованныхЛицОрганизации(
										Организация, 
										КорректируемыйПериод, , 
										Истина);		
			
	Иначе	
		ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ;
		
		КатегорииЗастрахованныхЛиц = ПерсонифицированныйУчет.КатегорииЗастрахованныхЛицОрганизации(
										Организация, 
										ОтчетныйПериод);
										
		КорректируемыйПериод = '00010101';									
	КонецЕсли;	
			
	ПачкаСЗВ = КомплектОбъект.НовыйПачкаСЗВ6(ТипДокумента, 
					ТипСведенийСЗВ, 
					КатегорииЗастрахованныхЛиц[0].Значение,  
					КорректируемыйПериод,
					Перечисления.ТипыДоговоровСЗВ63.Трудовой);
	
	МодифицированностьКомплекта = Истина;
	
	ПачкиСЗВ = Новый Массив;
	ПачкиСЗВ.Добавить(ПачкаСЗВ.Ссылка);
	
	ОписаниеПачекСЗВ6 = ВыборкаПоШапкеПачекСЗВ6(ПачкиСЗВ);
	
	Если ОписаниеПачекСЗВ6.Следующий() Тогда 
		ПоместитьОписаниеСЗВ6ВТаблицуДокументов(ОписаниеПачекСЗВ6);			
	КонецЕсли;
		
	ДокументыКомплекта.Сортировать("ПорядокСортировки, КорректируемыйПериод, ТипСведенийСЗВ, КатегорияЗастрахованныхЛиц, ТипДоговора, Ссылка");
	
	ДанныеДокументаВОбъектПоСсылке(ПачкаСЗВ.Ссылка);
КонецПроцедуры	

Процедура ДобавитьСуществующиеДокументы(ДобавляемыеДокументы) Экспорт 	
	КомплектОбъект.ДобавитьДокументыВКомплект(ДобавляемыеДокументы);
	
	МодифицированностьКомплекта = Истина;
	
	ДанныеКомплектаВОбъект(КомплектОбъект, Истина);
КонецПроцедуры	

Процедура УдалитьДокументы(СписокДокументов) Экспорт
	КомплектОбъект.УдалитьДокументыКомплекта(СписокДокументов);	
	
	МодифицированностьКомплекта = Истина;
	
	ДанныеКомплектаВОбъект(КомплектОбъект, Истина);
КонецПроцедуры	

#КонецОбласти

#Область СохранениеИзмененийВДанныхДокументов

Процедура СохранитьИзмененияВКомплекте() Экспорт
	Если МодифицированностьОписи 
		И ЗначениеЗаполнено(КомплектОбъект.ОписьПачекСЗВ) Тогда
		
		ОписьОбъект = КомплектОбъект.ОписьПачекСЗВ.ПолучитьОбъект();
		ОписьОбъект.Записать();
		
		МодифицированностьОписи = Ложь;
	КонецЕсли;	
	
	КомплектОбъект.Записать();
	МодифицированностьКомплекта = Ложь;
	
КонецПроцедуры	

Процедура СохранитьДанныеТекущегоДокумента() Экспорт
	Если СсылкаНаРедактируемыйДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ТипДокумента = ТипЗнч(СсылкаНаРедактируемыйДокумент);
	
	Если ТипДокумента = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		ИнтерфейсыВзаимодействияБРО.УстановитьВидФормыРСВ_1(СсылкаНаРедактируемыйДокумент, КомплектОбъект.КорректирующаяФормаРСВ_1);
	Иначе
		ДокументОбъект = СсылкаНаРедактируемыйДокумент.ПолучитьОбъект();
			
		ДанныеШапки = ДанныеШапкиТекущегоДокумента();
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеШапки);
		
		СотрудникиСФиксСтажем = Новый Массив;
		ИзменившиесяДанныеСотрудников = Новый Соответствие;
		
		Если ТипДокумента = Тип("ДокументСсылка.ОписьПачекСЗВ_6") Тогда 
			ДанныеОбъектаВДокументАДВ_6_2(ДокументОбъект);
			МодифицированностьОписи = Истина;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_1")
			Или ТипДокумента = Тип("ДокументСсылка.РеестрСЗВ_6_2") Тогда
			
			ДанныеОбъектаВДокументСЗВ_6(ДокументОбъект, СотрудникиСФиксСтажем, ИзменившиесяДанныеСотрудников);
			МодифицированностьОписи = Истина;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3") Тогда  	
			ДанныеОбъектаВДокументСЗВ_6_3(ДокументОбъект, ИзменившиесяДанныеСотрудников);
			МодифицированностьОписи = Истина;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_4") Тогда
			ДанныеОбъектаВДокументСЗВ_6_4(ДокументОбъект, СотрудникиСФиксСтажем, ИзменившиесяДанныеСотрудников);
			МодифицированностьОписи = Истина;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПачкаРазделов6РасчетаРСВ_1") Тогда
			ДанныеОбъектаВПачкуРазделов6(ДокументОбъект, СотрудникиСФиксСтажем, ИзменившиесяДанныеСотрудников);
			МодифицированностьОписи = Истина;	
		КонецЕсли;
		
		ДокументОбъект.Записать();
		
		Если СотрудникиСФиксСтажем.Количество() > 0 Тогда 
			Если ДокументОбъект.ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ Тогда 
				РасчетныйПериод = ДокументОбъект.ОтчетныйПериод
			Иначе
				РасчетныйПериод	= ДокументОбъект.КорректируемыйПериод;
			КонецЕсли;	
			
			КомплектОбъект.ЗафиксироватьСтрокиСтажаПоСотрудникам(
				СотрудникиСФиксСтажем, 
				ДокументОбъект.ТипСведенийСЗВ,
				РасчетныйПериод);
		КонецЕсли;		
			
		Если ИзменившиесяДанныеСотрудников.Количество() > 0 Тогда
			КомплектОбъект.ИзменитьЛичныеДанныеУСотрудниковВКомплекте(ИзменившиесяДанныеСотрудников);	
		КонецЕсли;	
		
		Если МодифицированностьОписи 
			И Не КомплектОбъект.ОписьПачекСЗВ.Пустая() Тогда
			
			ОписьОбъект = КомплектОбъект.ОписьПачекСЗВ.ПолучитьОбъект();
			ОписьОбъект.Записать();
			
			МодифицированностьОписи = Ложь;
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры	

Процедура ОчиститьДанные() Экспорт
	Сотрудники.Очистить();
	ЗаписиОСтаже.Очистить();
	СведенияОЗаработке.Очистить();
	СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Очистить();
	РСВ_1ИтогиПоТарифам.Очистить();
	ИтогиПоКатегориям.Очистить();
	КорректирующиеСведения.Очистить();
	
	РСВ_1КоличествоПрименяемыхТарифов = 0;
	РСВ_1НачисленоСтраховая = 0;
	РСВ_1НачисленоНакопительная = 0;
	РСВ_1УплаченоСтраховая = 0;
	РСВ_1УплаченоНакопительная = 0;
	РСВ_1ОблагаемаяБаза = 0;
	РСВ_1ОблагаемаяБазаПоЛьготнымТарифам = 0;
	РСВ_1ОбъектОбложения = 0;
	РСВ_1ОбъектОбложенияПоЛьготнымТарифам = 0;
	РСВ_1УменьшенныйОбъектОбложения = 0;
	РСВ_1УменьшенныйОбъектОбложенияПоЛьготнымТарифам = 0;
	РСВ_1ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах = 0;
	РСВ_1ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах = 0;
	
	ОписьКоличествоИсходныхФорма = 0;
	ОписьКоличествоКорректирующихФорм = 0;
	ОписьНачисленоСтраховая = 0;
	ОписьНачисленоНакопительная = 0;
	ОписьУплаченоСтраховая = 0;
	ОписьУплаченоНакопительная = 0;
	ОписьЗаработок = 0;
	ОписьОблагаетсяВзносами = 0;
	ОписьЗаработокПоЛьготнымКатегориям = 0;
	ОписьОблагаетсяВзносамиПоЛьготнымКатегориям = 0;
	ОписьОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах = 0;
	ОписьОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах = 0;
КонецПроцедуры	

#КонецОбласти

#Область ИнициализацияРСВ1

Процедура ДанныеРСВ_1ВОбъект()
	
	ДанныеРСВ = КомплектОбъект.ДанныеРСВКомплекта();	
	
	Если ДанныеРСВ = Неопределено 
		Или (Не ДанныеРСВ.ФормаРеглОтчетаДействуетВТекущемОтчетномПериоде
		И КомплектОбъект.ОтчетныйПериод < '20150101') Тогда
		
		Возврат;
	КонецЕсли;	
	
	РеквизитыИтоговРСВ = СоответствиеИтоговРСВРеквизитамОбъекта();
	
	Для Каждого ИтогиРСВ Из ДанныеРСВ Цикл
		ИмяРеквизитаОбъекта = РеквизитыИтоговРСВ[ИтогиРСВ.Ключ];
		
		Если ИмяРеквизитаОбъекта <> Неопределено Тогда
			Если ТипЗнч(ИтогиРСВ.Значение) = Тип("ТаблицаЗначений") Тогда
				ЭтотОбъект[ИмяРеквизитаОбъекта].Загрузить(ИтогиРСВ.Значение);
			Иначе
				ЭтотОбъект[ИмяРеквизитаОбъекта] = ИтогиРСВ.Значение;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

Функция СоответствиеИтоговРСВРеквизитамОбъекта()
	РеквизитыИтоговРСВ = Новый Соответствие;
	РеквизитыИтоговРСВ.Вставить("КоличествоПрименяемыхТарифов", "РСВ_1КоличествоПрименяемыхТарифов");
	РеквизитыИтоговРСВ.Вставить("НачисленоРСВ1НаСтраховую", "РСВ_1НачисленоСтраховая");
	РеквизитыИтоговРСВ.Вставить("НачисленоРСВ1НаНакопительную", "РСВ_1НачисленоНакопительная");
	РеквизитыИтоговРСВ.Вставить("УплаченоРСВ1НаСтраховую", "РСВ_1УплаченоСтраховая");
	РеквизитыИтоговРСВ.Вставить("УплаченоРСВ1НаНакопительную", "РСВ_1УплаченоНакопительная");
	РеквизитыИтоговРСВ.Вставить("ОблагаемаяБазаРСВ1", "РСВ_1ОблагаемаяБаза");
	РеквизитыИтоговРСВ.Вставить("ОблагаемаяБазаРСВ1Льготная", "РСВ_1ОблагаемаяБазаПоЛьготнымТарифам");
	РеквизитыИтоговРСВ.Вставить("ОбъектОбложенияРСВ1", "РСВ_1ОбъектОбложения");
	РеквизитыИтоговРСВ.Вставить("ОбъектОбложенияРСВ1Льготный", "РСВ_1ОбъектОбложенияПоЛьготнымТарифам");
	РеквизитыИтоговРСВ.Вставить("УменьшенныйОбъектОбложенияРСВ1", "РСВ_1УменьшенныйОбъектОбложения");
	РеквизитыИтоговРСВ.Вставить("УменьшенныйОбъектОбложенияРСВ1Льготный", "РСВ_1УменьшенныйОбъектОбложенияПоЛьготнымТарифам");
	РеквизитыИтоговРСВ.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах", "РСВ_1ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	РеквизитыИтоговРСВ.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах", "РСВ_1ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");
	РеквизитыИтоговРСВ.Вставить("ИтогиПоТарифам", "РСВ_1ИтогиПоТарифам");
	
	РеквизитыИтоговРСВ.Вставить("ОбъектОбложенияРСВ1За1Месяц", "РСВ_1ОбъектОбложенияЗа1Месяц");
	РеквизитыИтоговРСВ.Вставить("ОблагаемаяБазаРСВ1За1Месяц", "РСВ_1ОблагаемаяБазаЗа1Месяц");
	РеквизитыИтоговРСВ.Вставить("БазаСверхПредельнойРСВ1За1Месяц", "РСВ_1БазаСверхПредельнойЗа1Месяц");
	РеквизитыИтоговРСВ.Вставить("ОбъектОбложенияРСВ1За2Месяц", "РСВ_1ОбъектОбложенияЗа2Месяц");
	РеквизитыИтоговРСВ.Вставить("ОблагаемаяБазаРСВ1За2Месяц", "РСВ_1ОблагаемаяБазаЗа2Месяц");
	РеквизитыИтоговРСВ.Вставить("БазаСверхПредельнойРСВ1За2Месяц", "РСВ_1БазаСверхПредельнойЗа2Месяц");
	РеквизитыИтоговРСВ.Вставить("ОбъектОбложенияРСВ1За3Месяц", "РСВ_1ОбъектОбложенияЗа3Месяц");
	РеквизитыИтоговРСВ.Вставить("ОблагаемаяБазаРСВ1За3Месяц", "РСВ_1ОблагаемаяБазаЗа3Месяц");
	РеквизитыИтоговРСВ.Вставить("БазаСверхПредельнойРСВ1За3Месяц", "РСВ_1БазаСверхПредельнойЗа3Месяц");
	РеквизитыИтоговРСВ.Вставить("КоличествоЗастрахованных", "РСВ_1КоличествоЗастрахованныхЛиц");

	РеквизитыИтоговРСВ.Вставить("ИтогиРСВПоВредномуЗаработку", "РСВ_1ИтогиПоКлассамУсловийТруда");
	
	Возврат РеквизитыИтоговРСВ;
КонецФункции	

#КонецОбласти

#Область ИнициализацияАДВ62

Процедура СводныеДанныеПоКомплектуВОбъект()
	ИтогиПоДокументамКомплекта = КомплектОбъект.СводныеДанныеПоДокументамКомплекта();
	
	РеквизитыИтоговОписи = СоответствиеИтоговПоКомплектуРеквизитамОбъекта();
	
	Для Каждого ПолеИтогов Из ИтогиПоДокументамКомплекта Цикл
		ИмяРеквизитаОбъекта = РеквизитыИтоговОписи[ПолеИтогов.Ключ];
		
		Если ИмяРеквизитаОбъекта <> Неопределено Тогда
			Если ТипЗнч(ПолеИтогов.Значение) = Тип("ТаблицаЗначений") Тогда
				ЭтотОбъект[ИмяРеквизитаОбъекта].Загрузить(ПолеИтогов.Значение);
			Иначе
				ЭтотОбъект[ИмяРеквизитаОбъекта] = ПолеИтогов.Значение;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

Функция СоответствиеИтоговПоКомплектуРеквизитамОбъекта()
	РеквизитыИтоговРСВ = Новый Соответствие;
	РеквизитыИтоговРСВ.Вставить("НачисленоСтраховая", "ОписьНачисленоСтраховая");
	РеквизитыИтоговРСВ.Вставить("НачисленоНакопительная", "ОписьНачисленоНакопительная");
	РеквизитыИтоговРСВ.Вставить("УплаченоСтраховая", "ОписьУплаченоСтраховая");
	РеквизитыИтоговРСВ.Вставить("УплаченоНакопительная", "ОписьУплаченоНакопительная");
	РеквизитыИтоговРСВ.Вставить("Заработок", "ОписьЗаработок");
	РеквизитыИтоговРСВ.Вставить("ОблагаетсяВзносами", "ОписьОблагаетсяВзносами");
	РеквизитыИтоговРСВ.Вставить("ЗаработокПоЛьготнымКатегориям", "ОписьЗаработокПоЛьготнымКатегориям");
	РеквизитыИтоговРСВ.Вставить("ОблагаетсяВзносамиПоЛьготнымКатегориям", "ОписьОблагаетсяВзносамиПоЛьготнымКатегориям");
	РеквизитыИтоговРСВ.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах", "ОписьОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");
	РеквизитыИтоговРСВ.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах", "ОписьОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	РеквизитыИтоговРСВ.Вставить("ИтогиПоКатегориям", "ИтогиПоКатегориям");
	
	Возврат РеквизитыИтоговРСВ;
КонецФункции	

#КонецОбласти

#Область ИнициализацияСЗВ612

Процедура ДанныеСЗВ_6ВОбъект(ДокументОбъект)
	Для Каждого СтрокаСотрудникДокумента Из ДокументОбъект.Сотрудники Цикл
		СтрокаСотрудник = Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудник, СтрокаСотрудникДокумента);
	КонецЦикла;	
	
	Для Каждого СтрокаСтажДокумента Из ДокументОбъект.ЗаписиОСтаже Цикл 
		СтрокаСтаж = ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтаж, СтрокаСтажДокумента);
	КонецЦикла;	
	
	ДополнитьДанныеОВзносах(ДокументОбъект);
КонецПроцедуры	

Процедура ДополнитьДанныеОВзносах(ДокументОбъект)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Организация", ДокументОбъект.Организация);
	
	ПачкиСЗВ = Новый Массив;
	Для Каждого ДокументСЗВ Из КомплектОбъект.СписокПачекСЗВ Цикл
		ПачкиСЗВ.Добавить(ДокументСЗВ.ПачкаДокументов);	
	КонецЦикла;	
		
	Запрос.УстановитьПараметр("СписокДокументов", ПачкиСЗВ);
	Если ДокументОбъект.ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ Тогда
		ПредшествущийПериод = ПерсонифицированныйУчетКлиентСервер.ПредшествующийОтчетныйПериодПерсУчета(ДокументОбъект.ОтчетныйПериод);
		Запрос.УстановитьПараметр("ОкончаниеПериода", ПерсонифицированныйУчетКлиентСервер.ОкончаниеОтчетногоПериодаПерсУчета(ПредшествущийПериод));
	Иначе
		Запрос.УстановитьПараметр("ОкончаниеПериода", ПерсонифицированныйУчетКлиентСервер.ОкончаниеОтчетногоПериодаПерсУчета(ДокументОбъект.КорректируемыйПериод));
	КонецЕсли;	
	
	РасчетныйПериод = ?(ДокументОбъект.ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ, ДокументОбъект.ОтчетныйПериод, ДокументОбъект.КорректируемыйПериод);
	
	Запрос.УстановитьПараметр("РасчетныйПериод", РасчетныйПериод);
	Запрос.УстановитьПараметр("КатегорииМоряков", ПерсонифицированныйУчет.КатегорииЧленовЭкипажаМорскихСудов());	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеТекущегоДокумента.Ссылка.КатегорияЗастрахованныхЛиц КАК КатегорияЗастрахованныхЛиц,
	|	ДанныеТекущегоДокумента.НомерСтроки,
	|	ДанныеТекущегоДокумента.Сотрудник,
	|	ДанныеТекущегоДокумента.НачисленоСтраховая,
	|	ДанныеТекущегоДокумента.НачисленоНакопительная,
	|	ДанныеТекущегоДокумента.УплаченоСтраховая,
	|	ДанныеТекущегоДокумента.УплаченоНакопительная
	|ПОМЕСТИТЬ ВТДанныеТекущегоДокумента
	|ИЗ
	|	#ДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента
	|ГДЕ
	|	ДанныеТекущегоДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеТекущегоДокумента.Сотрудник
	|ПОМЕСТИТЬ ВТСотрудникиТекущегоДокумента
	|ИЗ
	|	ВТДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОВзносах.ФизическоеЛицо,
	|	ДанныеОВзносах.ПФРСтраховая КАК НачисленоСтраховая,
	|	ДанныеОВзносах.ПФРНакопительная КАК НачисленоНакопительная,
	|	ДанныеОВзносах.УплаченоСтраховая КАК УплаченоСтраховая,
	|	ДанныеОВзносах.УплаченоНакопительная КАК УплаченоНакопительная,
	|	ДанныеОВзносах.КатегорияЗастрахованныхЛиц,
	|	ДанныеОВзносах.ТипДоговора,
	|	ДанныеОВзносах.КорректируемыйПериод,
	|	ДанныеОВзносах.ТипСведенийСЗВ
	|ПОМЕСТИТЬ ВТДанныеОВзносахИзДокументов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеОВзносахДокумента.Сотрудник КАК ФизическоеЛицо,
	|		ДанныеОВзносахДокумента.Ссылка.КатегорияЗастрахованныхЛиц КАК КатегорияЗастрахованныхЛиц,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка) КАК ТипДоговора,
	|		ДанныеОВзносахДокумента.НачисленоСтраховая КАК ПФРСтраховая,
	|		ДанныеОВзносахДокумента.НачисленоНакопительная КАК ПФРНакопительная,
	|		ДанныеОВзносахДокумента.УплаченоСтраховая КАК УплаченоСтраховая,
	|		ДанныеОВзносахДокумента.УплаченоНакопительная КАК УплаченоНакопительная,
	|		ДанныеОВзносахДокумента.Ссылка.ТипСведенийСЗВ КАК ТипСведенийСЗВ,
	|		ДанныеОВзносахДокумента.Ссылка.КорректируемыйПериод КАК КорректируемыйПериод
	|	ИЗ
	|		Документ.ПачкаДокументовСЗВ_6_1.Сотрудники КАК ДанныеОВзносахДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиТекущегоДокумента КАК ДанныеТекущегоДокумента
	|			ПО ДанныеОВзносахДокумента.Сотрудник = ДанныеТекущегоДокумента.Сотрудник
	|				И (ДанныеОВзносахДокумента.Ссылка В (&СписокДокументов))
	|				И (ВЫБОР
	|					КОГДА ДанныеОВзносахДокумента.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|						ТОГДА ДанныеОВзносахДокумента.Ссылка.ОтчетныйПериод <= &РасчетныйПериод
	|					ИНАЧЕ ДанныеОВзносахДокумента.Ссылка.КорректируемыйПериод <= &РасчетныйПериод
	|				КОНЕЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеОВзносахДокумента.Сотрудник,
	|		ДанныеОВзносахДокумента.Ссылка.КатегорияЗастрахованныхЛиц,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка),
	|		ДанныеОВзносахДокумента.НачисленоСтраховая,
	|		ДанныеОВзносахДокумента.НачисленоНакопительная,
	|		ДанныеОВзносахДокумента.УплаченоСтраховая,
	|		ДанныеОВзносахДокумента.УплаченоНакопительная,
	|		ДанныеОВзносахДокумента.Ссылка.ТипСведенийСЗВ,
	|		ДанныеОВзносахДокумента.Ссылка.КорректируемыйПериод
	|	ИЗ
	|		Документ.РеестрСЗВ_6_2.Сотрудники КАК ДанныеОВзносахДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиТекущегоДокумента КАК ДанныеТекущегоДокумента
	|			ПО ДанныеОВзносахДокумента.Сотрудник = ДанныеТекущегоДокумента.Сотрудник
	|				И (ДанныеОВзносахДокумента.Ссылка В (&СписокДокументов))
	|				И (ВЫБОР
	|					КОГДА ДанныеОВзносахДокумента.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|						ТОГДА ДанныеОВзносахДокумента.Ссылка.ОтчетныйПериод <= &РасчетныйПериод
	|					ИНАЧЕ ДанныеОВзносахДокумента.Ссылка.КорректируемыйПериод <= &РасчетныйПериод
	|				КОНЕЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеОВзносахДокумента.Сотрудник,
	|		ДанныеОВзносахДокумента.Ссылка.КатегорияЗастрахованныхЛиц,
	|		ДанныеОВзносахДокумента.Ссылка.ТипДоговора,
	|		ДанныеОВзносахДокумента.НачисленоСтраховая,
	|		ДанныеОВзносахДокумента.НачисленоНакопительная,
	|		ДанныеОВзносахДокумента.УплаченоСтраховая,
	|		ДанныеОВзносахДокумента.УплаченоНакопительная,
	|		ДанныеОВзносахДокумента.Ссылка.ТипСведенийСЗВ,
	|		ДанныеОВзносахДокумента.Ссылка.КорректируемыйПериод
	|	ИЗ
	|		Документ.ПачкаДокументовСЗВ_6_4.Сотрудники КАК ДанныеОВзносахДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиТекущегоДокумента КАК ДанныеТекущегоДокумента
	|			ПО ДанныеОВзносахДокумента.Сотрудник = ДанныеТекущегоДокумента.Сотрудник
	|				И (ДанныеОВзносахДокумента.Ссылка В (&СписокДокументов))
	|				И (ВЫБОР
	|					КОГДА ДанныеОВзносахДокумента.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|						ТОГДА ДанныеОВзносахДокумента.Ссылка.ОтчетныйПериод <= &РасчетныйПериод
	|					ИНАЧЕ ДанныеОВзносахДокумента.Ссылка.КорректируемыйПериод <= &РасчетныйПериод
	|				КОНЕЦ)) КАК ДанныеОВзносах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОВзносахВПФРСрезПоследних.Период,
	|	СведенияОВзносахВПФРСрезПоследних.ФизическоеЛицо,
	|	СведенияОВзносахВПФРСрезПоследних.НачисленоСтраховая,
	|	СведенияОВзносахВПФРСрезПоследних.НачисленоНакопительная,
	|	СведенияОВзносахВПФРСрезПоследних.УплаченоСтраховая,
	|	СведенияОВзносахВПФРСрезПоследних.УплаченоНакопительная
	|ПОМЕСТИТЬ ВТСведенияОВзносахВПФР
	|ИЗ
	|	РегистрСведений.СведенияОВзносахВПФР.СрезПоследних(
	|			&ОкончаниеПериода,
	|			Организация = &Организация
	|				И ФизическоеЛицо В
	|					(ВЫБРАТЬ
	|						СотрудникиТекущегоДокумента.Сотрудник
	|					ИЗ
	|						ВТСотрудникиТекущегоДокумента КАК СотрудникиТекущегоДокумента)) КАК СведенияОВзносахВПФРСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеОВзносахИзДокументов.ФизическоеЛицо,
	|	ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц,
	|	ДанныеОВзносахИзДокументов.ТипДоговора,
	|	ДанныеОВзносахИзДокументов.КорректируемыйПериод
	|ПОМЕСТИТЬ ВТКорректирующиеСведения
	|ИЗ
	|	ВТДанныеОВзносахИзДокументов КАК ДанныеОВзносахИзДокументов
	|ГДЕ
	|	ДанныеОВзносахИзДокументов.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОВзносахЗаПериод.ФизическоеЛицо,
	|	СУММА(ДанныеОВзносахЗаПериод.СтраховаяПриход) КАК СтраховаяПриход,
	|	СУММА(ДанныеОВзносахЗаПериод.НакопительнаяПриход) КАК НакопительнаяПриход,
	|	СУММА(ДанныеОВзносахЗаПериод.СтраховаяРасход) КАК СтраховаяРасход,
	|	СУММА(ДанныеОВзносахЗаПериод.НакопительнаяРасход) КАК НакопительнаяРасход,
	|	СУММА(ДанныеОВзносахЗаПериод.СтраховаяЧленамЭкипажейМорскихСудов) КАК СтраховаяЧленамЭкипажейМорскихСудов,
	|	СУММА(ДанныеОВзносахЗаПериод.НакопительнаяЧленамЭкипажейМорскихСудов) КАК НакопительнаяЧленамЭкипажейМорскихСудов
	|ПОМЕСТИТЬ ДанныеОВзносахС2010Года
	|ИЗ
	|	(ВЫБРАТЬ
	|		СотрудникиТекущегоДокумента.Сотрудник КАК ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период >  СведенияОВзносахВПФР.Период)
	|				ТОГДА СтраховыеВзносыПереданныеВПФР.Страховая
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СтраховаяПриход,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА СтраховыеВзносыПереданныеВПФР.Накопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НакопительнаяПриход,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА СтраховыеВзносыПереданныеВПФР.Страховая
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СтраховаяРасход,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА СтраховыеВзносыПереданныеВПФР.Накопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НакопительнаяРасход,
	|		СтраховыеВзносыПереданныеВПФР.СтраховаяЧленамЭкипажейМорскихСудов КАК СтраховаяЧленамЭкипажейМорскихСудов,
	|		СтраховыеВзносыПереданныеВПФР.НакопительнаяЧленамЭкипажейМорскихСудов КАК НакопительнаяЧленамЭкипажейМорскихСудов
	|	ИЗ
	|		ВТСотрудникиТекущегоДокумента КАК СотрудникиТекущегоДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыПереданныеВПФР КАК СтраховыеВзносыПереданныеВПФР
	|			ПО (СтраховыеВзносыПереданныеВПФР.Организация = &Организация)
	|				И (СтраховыеВзносыПереданныеВПФР.Период <= &ОкончаниеПериода)
	|				И СотрудникиТекущегоДокумента.Сотрудник = СтраховыеВзносыПереданныеВПФР.ФизическоеЛицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОВзносахВПФР КАК СведенияОВзносахВПФР
	|			ПО СотрудникиТекущегоДокумента.Сотрудник = СведенияОВзносахВПФР.ФизическоеЛицо
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтраховыеВзносыПереданныеВПФР.ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА -СтраховыеВзносыПереданныеВПФР.Страховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА -СтраховыеВзносыПереданныеВПФР.Накопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА -СтраховыеВзносыПереданныеВПФР.Страховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СтраховыеВзносыПереданныеВПФР.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И (СведенияОВзносахВПФР.Период ЕСТЬ NULL 
	|						ИЛИ СтраховыеВзносыПереданныеВПФР.Период > СведенияОВзносахВПФР.Период)
	|				ТОГДА -СтраховыеВзносыПереданныеВПФР.Накопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		-СтраховыеВзносыПереданныеВПФР.СтраховаяЧленамЭкипажейМорскихСудов,
	|		-СтраховыеВзносыПереданныеВПФР.НакопительнаяЧленамЭкипажейМорскихСудов
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыПереданныеВПФР КАК СтраховыеВзносыПереданныеВПФР
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКорректирующиеСведения КАК КорректирующиеСведения
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОВзносахВПФР КАК СведенияОВзносахВПФР
	|				ПО КорректирующиеСведения.ФизическоеЛицо = СведенияОВзносахВПФР.ФизическоеЛицо
	|			ПО СтраховыеВзносыПереданныеВПФР.Период = КорректирующиеСведения.КорректируемыйПериод
	|				И СтраховыеВзносыПереданныеВПФР.ФизическоеЛицо = КорректирующиеСведения.ФизическоеЛицо
	|				И СтраховыеВзносыПереданныеВПФР.КатегорияЗастрахованныхЛиц = КорректирующиеСведения.КатегорияЗастрахованныхЛиц
	|				И СтраховыеВзносыПереданныеВПФР.ТипДоговора = КорректирующиеСведения.ТипДоговора
	|				И (СтраховыеВзносыПереданныеВПФР.Организация = &Организация)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеОВзносахИзДокументов.ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|				ТОГДА 0
	|			ИНАЧЕ ДанныеОВзносахИзДокументов.НачисленоСтраховая
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|				ТОГДА 0
	|			ИНАЧЕ ДанныеОВзносахИзДокументов.НачисленоНакопительная
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|				ТОГДА 0
	|			ИНАЧЕ ДанныеОВзносахИзДокументов.УплаченоСтраховая
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|				ТОГДА 0
	|			ИНАЧЕ ДанныеОВзносахИзДокументов.УплаченоНакопительная
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|				ТОГДА ДанныеОВзносахИзДокументов.НачисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДанныеОВзносахИзДокументов.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|				ТОГДА ДанныеОВзносахИзДокументов.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТДанныеОВзносахИзДокументов КАК ДанныеОВзносахИзДокументов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СведенияОВзносахВПФР.ФизическоеЛицо,
	|		СведенияОВзносахВПФР.НачисленоСтраховая,
	|		СведенияОВзносахВПФР.НачисленоНакопительная,
	|		СведенияОВзносахВПФР.УплаченоСтраховая,
	|		СведенияОВзносахВПФР.УплаченоНакопительная,
	|		0,
	|		0
	|	ИЗ
	|		ВТСведенияОВзносахВПФР КАК СведенияОВзносахВПФР) КАК ДанныеОВзносахЗаПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОВзносахЗаПериод.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТекущегоДокумента.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|			ТОГДА ДанныеОВзносахС2010Года.СтраховаяПриход
	|		ИНАЧЕ ДанныеОВзносахС2010Года.СтраховаяПриход - ДанныеТекущегоДокумента.НачисленоСтраховая
	|	КОНЕЦ КАК НачисленоСтраховаяБезУчетаДанныхПачки,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|			ТОГДА ДанныеОВзносахС2010Года.НакопительнаяПриход
	|		ИНАЧЕ ДанныеОВзносахС2010Года.НакопительнаяПриход - ДанныеТекущегоДокумента.НачисленоНакопительная
	|	КОНЕЦ КАК НачисленоНакопительнаяБезУчетаДанныхПачки,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|			ТОГДА ДанныеОВзносахС2010Года.СтраховаяРасход
	|		ИНАЧЕ ДанныеОВзносахС2010Года.СтраховаяРасход - ДанныеТекущегоДокумента.УплаченоСтраховая
	|	КОНЕЦ КАК УплаченоСтраховаяБезУчетаДанныхПачки,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|			ТОГДА ДанныеОВзносахС2010Года.НакопительнаяРасход - ДанныеТекущегоДокумента.УплаченоНакопительная
	|		ИНАЧЕ ДанныеОВзносахС2010Года.НакопительнаяРасход - ДанныеТекущегоДокумента.УплаченоНакопительная
	|	КОНЕЦ КАК УплаченоНакопительнаяБезУчетаДанныхПачки,
	|	ДанныеОВзносахС2010Года.СтраховаяПриход КАК НачисленоСтраховаяВсего,
	|	ДанныеОВзносахС2010Года.НакопительнаяПриход КАК НачисленоНакопительнаяВсего,
	|	ДанныеОВзносахС2010Года.СтраховаяРасход КАК УплаченоСтраховаяВсего,
	|	ДанныеОВзносахС2010Года.НакопительнаяРасход КАК УплаченоНакопительнаяВсего,
	|	ВЫБОР
	|		КОГДА ДанныеОВзносахС2010Года.СтраховаяПриход = 0
	|			ТОГДА 0
	|		ИНАЧЕ ДанныеОВзносахС2010Года.СтраховаяРасход / ДанныеОВзносахС2010Года.СтраховаяПриход
	|	КОНЕЦ КАК КоэффициентУплатыСтраховая,
	|	ВЫБОР
	|		КОГДА ДанныеОВзносахС2010Года.НакопительнаяПриход = 0
	|			ТОГДА 0
	|		ИНАЧЕ ДанныеОВзносахС2010Года.НакопительнаяРасход / ДанныеОВзносахС2010Года.НакопительнаяПриход
	|	КОНЕЦ КАК КоэффициентУплатыНакопительная,
	|	ДанныеОВзносахС2010Года.СтраховаяЧленамЭкипажейМорскихСудов КАК НачисленоСтраховаяПоКатегорииЧЭСВсего,
	|	ДанныеОВзносахС2010Года.НакопительнаяЧленамЭкипажейМорскихСудов КАК НачисленоНакопительнаяПоКатегорииЧЭСВсего,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|			ТОГДА ДанныеОВзносахС2010Года.СтраховаяЧленамЭкипажейМорскихСудов - ДанныеТекущегоДокумента.НачисленоСтраховая
	|		ИНАЧЕ ДанныеОВзносахС2010Года.СтраховаяЧленамЭкипажейМорскихСудов
	|	КОНЕЦ КАК НачисленоСтраховаяПоКатегорииЧЭСБезУчетаДанныхПачки,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.КатегорияЗастрахованныхЛиц В (&КатегорииМоряков)
	|			ТОГДА ДанныеОВзносахС2010Года.НакопительнаяЧленамЭкипажейМорскихСудов - ДанныеТекущегоДокумента.НачисленоНакопительная
	|		ИНАЧЕ ДанныеОВзносахС2010Года.НакопительнаяЧленамЭкипажейМорскихСудов
	|	КОНЕЦ КАК НачисленоНакопительнаяПоКатегорииЧЭСБезУчетаДанныхПачки
	|ИЗ
	|	ВТДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОВзносахС2010Года КАК ДанныеОВзносахС2010Года
	|		ПО ДанныеТекущегоДокумента.Сотрудник = ДанныеОВзносахС2010Года.ФизическоеЛицо";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДанныеТекущегоДокумента", ДокументОбъект.Ссылка.Метаданные().ПолноеИмя() + ".Сотрудники");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаСотрудник = Сотрудники[Выборка.НомерСтроки - 1];
		ЗаполнитьЗначенияСвойств(СтрокаСотрудник, Выборка);
	КонецЦикла;	
КонецПроцедуры	

#КонецОбласти

#Область ИнициализацияСЗВ63

Процедура ДанныеСЗВ_6_3ВОбъект(ДокументОбъект)
	СтрокиПоСотрудникам = Новый Соответствие;
	
	Для Каждого СтрокаСотрудникДокумента Из ДокументОбъект.Сотрудники Цикл
		СтрокаСотрудник = Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудник, СтрокаСотрудникДокумента);
		
		СтрокиПоСотрудникам.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработокДокумента Из ДокументОбъект.СведенияОЗаработке Цикл 
		СтрокаЗаработок = СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработок, СтрокаЗаработокДокумента);
		
		СтрокаСотрудник = СтрокиПоСотрудникам[СтрокаЗаработок.Сотрудник];
		
		Если СтрокаСотрудник <> Неопределено Тогда
			СтрокаСотрудник.Заработок = СтрокаСотрудник.Заработок + СтрокаЗаработок.Заработок;
			СтрокаСотрудник.ОблагаетсяВзносами = СтрокаСотрудник.ОблагаетсяВзносами + СтрокаЗаработок.ОблагаетсяВзносами;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#Область ИнициализацияСЗВ64

Процедура ДанныеСЗВ_6_4ВОбъект(ДокументОбъект)
	СтрокиПоСотрудникам = Новый Соответствие;
	
	Для Каждого СтрокаСотрудникДокумента Из ДокументОбъект.Сотрудники Цикл
		СтрокаСотрудник = Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудник, СтрокаСотрудникДокумента);
		
		СтрокиПоСотрудникам.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
	КонецЦикла;	
	
	Для Каждого СтрокаСтажДокумента Из ДокументОбъект.ЗаписиОСтаже Цикл 
		СтрокаСтаж = ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтаж, СтрокаСтажДокумента);
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработокДокумента Из ДокументОбъект.СведенияОЗаработке Цикл 
		СтрокаЗаработок = СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработок, СтрокаЗаработокДокумента);
		
		СтрокаСотрудник = СтрокиПоСотрудникам[СтрокаЗаработок.Сотрудник];
		
		Если СтрокаСотрудник <> Неопределено Тогда
			СтрокаСотрудник.Заработок = СтрокаСотрудник.Заработок + СтрокаЗаработок.Заработок;
			СтрокаСотрудник.ОблагаетсяВзносамиДоПредельнойВеличины = СтрокаСотрудник.ОблагаетсяВзносамиДоПредельнойВеличины + СтрокаЗаработок.ОблагаетсяВзносамиДоПредельнойВеличины;
			СтрокаСотрудник.ОблагаетсяВзносамиСвышеПредельнойВеличины = СтрокаСотрудник.ОблагаетсяВзносамиСвышеПредельнойВеличины + СтрокаЗаработок.ОблагаетсяВзносамиСвышеПредельнойВеличины;
		КонецЕсли;	
	КонецЦикла;	
	
	ДополнитьДанныеОВзносах(ДокументОбъект);
КонецПроцедуры	

Процедура ДанныеПачкиРазделов6ВОбъект(ДокументОбъект)
	СтрокиПоСотрудникам = Новый Соответствие;
	
	Для Каждого СтрокаСотрудникДокумента Из ДокументОбъект.Сотрудники Цикл
		СтрокаСотрудник = Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудник, СтрокаСотрудникДокумента);
		
		СтрокиПоСотрудникам.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
	КонецЦикла;	
	
	Для Каждого СтрокаСтажДокумента Из ДокументОбъект.ЗаписиОСтаже Цикл 
		СтрокаСтаж = ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтаж, СтрокаСтажДокумента);
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработокДокумента Из ДокументОбъект.СведенияОЗаработке Цикл 
		СтрокаЗаработок = СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработок, СтрокаЗаработокДокумента);
		
		СтрокаСотрудник = СтрокиПоСотрудникам[СтрокаЗаработок.Сотрудник];
		
		Если СтрокаСотрудник <> Неопределено
			И СтрокаЗаработок.Месяц <> 0 Тогда
			
			СтрокаСотрудник.Заработок = СтрокаСотрудник.Заработок + СтрокаЗаработок.Заработок;
			СтрокаСотрудник.ОблагаетсяВзносамиДоПредельнойВеличины = СтрокаСотрудник.ОблагаетсяВзносамиДоПредельнойВеличины + СтрокаЗаработок.ОблагаетсяВзносамиДоПредельнойВеличины;
			СтрокаСотрудник.ОблагаетсяВзносамиСвышеПредельнойВеличины = СтрокаСотрудник.ОблагаетсяВзносамиСвышеПредельнойВеличины + СтрокаЗаработок.ОблагаетсяВзносамиСвышеПредельнойВеличины;
			СтрокаСотрудник.ПоДоговорамГПХДоПредельнойВеличины = СтрокаСотрудник.ПоДоговорамГПХДоПредельнойВеличины + СтрокаЗаработок.ПоДоговорамГПХДоПредельнойВеличины;
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработокДокумента Из ДокументОбъект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах Цикл 
		СтрокаЗаработок = СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработок, СтрокаЗаработокДокумента);
	КонецЦикла;
	
	ЗаполнитьДанныеРаздела66(ДокументОбъект.Ссылка);

КонецПроцедуры	

Процедура ЗаполнитьДанныеРаздела66(ДокументОбъект)
	Если ДокументОбъект.ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ Тогда	
		УчитываемыеДокументы = ДокументыКомплекта.ВыгрузитьКолонку("Ссылка");
		
		Выборка = Документы.ПачкаРазделов6РасчетаРСВ_1.СформироватьЗапросПоКорректирующимСведениям(УчитываемыеДокументы, ДокументОбъект.Ссылка).Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаРаздела66 = КорректирующиеСведения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРаздела66, Выборка);
		КонецЦикла;	
	Иначе
		Для Каждого СтрокаДанныхДокумента Из ДокументОбъект.КорректирующиеСведения Цикл 
			СтрокаДанныхРаздела66 = КорректирующиеСведения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанныхРаздела66, СтрокаДанныхДокумента);
		КонецЦикла;	
	КонецЕсли;	
КонецПроцедуры	

#КонецОбласти

#Область ИнициализацияСпискаДокументовКомплекта

Процедура ПоместитьОписаниеРСВВТаблицуДокументов()
	Если ЗначениеЗаполнено(КомплектОбъект.РСВ_1) Тогда
		СтрокаСпискаПачек = ДокументыКомплекта.Добавить();
		ЗаполнитьОписаниеРСВВТаблицеДокументов(СтрокаСпискаПачек);
	КонецЕсли;	
КонецПроцедуры	

Процедура ЗаполнитьОписаниеРСВВТаблицеДокументов(СтрокаСпискаПачек)
	СтрокаСпискаПачек.Ссылка = КомплектОбъект.РСВ_1;
	Если КомплектОбъект.ОтчетныйПериод >= '20140101' Тогда
		СтрокаСпискаПачек.ПредставлениеДокумента = "Разделы 1-5";			
	Иначе	
		СтрокаСпискаПачек.ПредставлениеДокумента = "РСВ-1";
	КонецЕсли;	
	СтрокаСпискаПачек.НомерПачки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КомплектОбъект.РСВ_1, "НомерПачки");
	СтрокаСпискаПачек.ПорядокСортировки = 1;
	СтрокаСпискаПачек.ТипСведенийСЗВ = ?(КомплектОбъект.КорректирующаяФормаРСВ_1, Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ, Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ);
	ДанныеРСВ = КомплектОбъект.ДанныеРСВКомплекта();
	
	Если ДанныеРСВ.ФормаРеглОтчетаДействуетВТекущемОтчетномПериоде Тогда
		СтрокаСпискаПачек.НачисленоСтраховая = ДанныеРСВ.НачисленоРСВ1НаСтраховую;
		СтрокаСпискаПачек.НачисленоНакопительная = ДанныеРСВ.НачисленоРСВ1НаНакопительную;
		СтрокаСпискаПачек.УплаченоСтраховая = ДанныеРСВ.УплаченоРСВ1НаСтраховую;
		СтрокаСпискаПачек.УплаченоНакопительная = ДанныеРСВ.УплаченоРСВ1НаНакопительную;		
	КонецЕсли;	
	
	СтрокаСпискаПачек.ЭтоФормаРСВ_1 = Истина;
КонецПроцедуры	

Функция ВыборкаПоШапкеАДВ62(Ссылка)
	ПачкиСЗВ = Новый Массив;
	Для Каждого СтрокаПачка Из КомплектОбъект.СписокПачекСЗВ Цикл
		ПачкиСЗВ.Добавить(СтрокаПачка.ПачкаДокументов);
	КонецЦикла;	
	
	ПачкиСЗВ63 = Новый Массив;
	
	Для Каждого СтрокаПачка Из КомплектОбъект.СписокПачекСЗВ6_3 Цикл
		ПачкиСЗВ63.Добавить(СтрокаПачка.ПачкаДокументов);
	КонецЦикла;	
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПачкиСЗВ", ПачкиСЗВ);
	Запрос.УстановитьПараметр("ПачкиСЗВ63", ПачкиСЗВ63);
    Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(Взносы.НачисленоСтраховая) КАК НачисленоСтраховая,
	|	СУММА(Взносы.НачисленоНакопительная) КАК НачисленоНакопительная,
	|	СУММА(Взносы.УплаченоСтраховая) КАК УплаченоСтраховая,
	|	СУММА(Взносы.УплаченоНакопительная) КАК УплаченоНакопительная,
	|	СУММА(Взносы.ДоНачисленоСтраховая) КАК ДоНачисленоСтраховая,
	|	СУММА(Взносы.ДоНачисленоНакопительная) КАК ДоНачисленоНакопительная,
	|	СУММА(Взносы.ДоУплаченоСтраховая) КАК ДоУплаченоСтраховая,
	|	СУММА(Взносы.ДоУплаченоНакопительная) КАК ДоУплаченоНакопительная
	|ПОМЕСТИТЬ ВТВзносы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачисленоСтраховая,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачисленоНакопительная,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.УплаченоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК УплаченоСтраховая,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.УплаченоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК УплаченоНакопительная,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоначисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ДоНачисленоСтраховая,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ДоНачисленоНакопительная,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоУплаченоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ДоУплаченоСтраховая,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоУплаченоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ДоУплаченоНакопительная
	|	ИЗ
	|		Документ.ПачкаДокументовСЗВ_6_1.Сотрудники КАК СведенияОВзносах
	|	ГДЕ
	|		СведенияОВзносах.Ссылка В(&ПачкиСЗВ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.УплаченоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.УплаченоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоначисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоУплаченоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоУплаченоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.РеестрСЗВ_6_2.Сотрудники КАК СведенияОВзносах
	|	ГДЕ
	|		СведенияОВзносах.Ссылка В(&ПачкиСЗВ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.УплаченоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.УплаченоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоначисленоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.НачисленоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоУплаченоСтраховая
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СведенияОВзносах.Ссылка.ТипСведенийСЗВ <> ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|				ТОГДА СведенияОВзносах.ДоУплаченоНакопительная
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ПачкаДокументовСЗВ_6_4.Сотрудники КАК СведенияОВзносах
	|	ГДЕ
	|		СведенияОВзносах.Ссылка В(&ПачкиСЗВ)) КАК Взносы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СведенияОЗаработке.Заработок) КАК Заработок,
	|	СУММА(СведенияОЗаработке.ОблагаетсяВзносами) КАК ОблагаетсяВзносами,
	|	СУММА(СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины) КАК ОблагаетсяВзносамиДоПредельнойВеличины,
	|	СУММА(СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины) КАК ОблагаетсяВзносамиСвышеПредельнойВеличины
	|ПОМЕСТИТЬ ВТСведенияОЗаработке
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Заработок КАК Заработок,
	|		ПачкаДокументовСЗВ_6_3СведенияОЗаработке.ОблагаетсяВзносами КАК ОблагаетсяВзносами,
	|		0 КАК ОблагаетсяВзносамиДоПредельнойВеличины,
	|		0 КАК ОблагаетсяВзносамиСвышеПредельнойВеличины
	|	ИЗ
	|		Документ.ПачкаДокументовСЗВ_6_3.СведенияОЗаработке КАК ПачкаДокументовСЗВ_6_3СведенияОЗаработке
	|	ГДЕ
	|		ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Ссылка В(&ПачкиСЗВ63)
	|		И ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Заработок,
	|		0,
	|		ПачкаДокументовСЗВ_6_4СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины,
	|		ПачкаДокументовСЗВ_6_4СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины
	|	ИЗ
	|		Документ.ПачкаДокументовСЗВ_6_4.СведенияОЗаработке КАК ПачкаДокументовСЗВ_6_4СведенияОЗаработке
	|	ГДЕ
	|		ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Ссылка В(&ПачкиСЗВ)
	|		И ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Ссылка.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)) КАК СведенияОЗаработке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОписьПачекСЗВ_6.Ссылка,
	|	""АДВ-6-2"" КАК ПредставлениеДокумента,
	|	Взносы.НачисленоСтраховая,
	|	Взносы.НачисленоНакопительная,
	|	Взносы.УплаченоСтраховая,
	|	Взносы.УплаченоНакопительная,
	|	Взносы.ДоНачисленоСтраховая,
	|	Взносы.ДоНачисленоНакопительная,
	|	Взносы.ДоУплаченоСтраховая,
	|	Взносы.ДоУплаченоНакопительная,
	|	СведенияОЗаработке.Заработок,
	|	СведенияОЗаработке.ОблагаетсяВзносами,
	|	СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины,
	|	СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины,
	|	3 КАК ПорядокСортировки,
	|	ОписьПачекСЗВ_6.НомерПачки
	|ИЗ
	|	Документ.ОписьПачекСЗВ_6 КАК ОписьПачекСЗВ_6
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВзносы КАК Взносы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОЗаработке КАК СведенияОЗаработке
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОписьПачекСЗВ_6.Ссылка = &Ссылка";
	
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции	

Процедура ПоместитьОписаниеАДВ62ВТаблицуДокументов(ОписаниеАДВ62)
	СтрокаСпискаПачек = ДокументыКомплекта.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаСпискаПачек, ОписаниеАДВ62);					
КонецПроцедуры	

Функция ВыборкаПоШапкеПачекСЗВ6(СписокПачек)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокСЗВ", СписокПачек);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Ссылка,
	|	СУММА(ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Заработок) КАК Заработок,
	|	СУММА(ПачкаДокументовСЗВ_6_4СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины) КАК ОблагаетсяВзносамиДоПредельнойВеличины,
	|	СУММА(ПачкаДокументовСЗВ_6_4СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины) КАК ОблагаетсяВзносамиСвышеПредельнойВеличины,
	|	0 КАК ПоДоговорамГПХДоПредельнойВеличины,
	|	СУММА(ПачкаДокументовСЗВ_6_4СведенияОЗаработке.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах) КАК ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	СУММА(ПачкаДокументовСЗВ_6_4СведенияОЗаработке.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах) КАК ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах
	|ПОМЕСТИТЬ ВТЗаработок
	|ИЗ
	|	Документ.ПачкаДокументовСЗВ_6_4.СведенияОЗаработке КАК ПачкаДокументовСЗВ_6_4СведенияОЗаработке
	|ГДЕ
	|	ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаДокументовСЗВ_6_4СведенияОЗаработке.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.Ссылка,
	|	СУММА(ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.Заработок),
	|	СУММА(ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины),
	|	СУММА(ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины),
	|	СУММА(ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.ПоДоговорамГПХДоПредельнойВеличины),
	|	0,
	|	0
	|ИЗ
	|	Документ.ПачкаРазделов6РасчетаРСВ_1.СведенияОЗаработке КАК ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке
	|ГДЕ
	|	ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.Ссылка В(&СписокСЗВ)
	|	И ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.Месяц <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаРазделов6РасчетаРСВ_1СведенияОЗаработке.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.НачисленоСтраховая) КАК НачисленоСтраховая,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.УплаченоСтраховая) КАК УплаченоСтраховая,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.НачисленоНакопительная) КАК НачисленоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.УплаченоНакопительная) КАК УплаченоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.ДоначисленоСтраховая) КАК ДоначисленоСтраховая,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.ДоначисленоНакопительная) КАК ДоначисленоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.ДоУплаченоНакопительная) КАК ДоУплаченоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_4Сотрудники.ДоУплаченоСтраховая) КАК ДоУплаченоСтраховая,
	|	ПачкаДокументовСЗВ_6_4Сотрудники.Ссылка
	|ПОМЕСТИТЬ ВТВзносыСЗВ_6_4
	|ИЗ
	|	Документ.ПачкаДокументовСЗВ_6_4.Сотрудники КАК ПачкаДокументовСЗВ_6_4Сотрудники
	|ГДЕ
	|	ПачкаДокументовСЗВ_6_4Сотрудники.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаДокументовСЗВ_6_4Сотрудники.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ПачкаРазделов6РасчетаРСВ_1Сотрудники.НачисленоСтраховая) КАК НачисленоСтраховая,
	|	СУММА(ПачкаРазделов6РасчетаРСВ_1Сотрудники.ДоначисленоСтраховая) КАК ДоначисленоСтраховая,
	|	ПачкаРазделов6РасчетаРСВ_1Сотрудники.Ссылка
	|ПОМЕСТИТЬ ВТВзносыРаздела6
	|ИЗ
	|	Документ.ПачкаРазделов6РасчетаРСВ_1.Сотрудники КАК ПачкаРазделов6РасчетаРСВ_1Сотрудники
	|ГДЕ
	|	ПачкаРазделов6РасчетаРСВ_1Сотрудники.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаРазделов6РасчетаРСВ_1Сотрудники.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПачкаДокументовСЗВ_6_1.Ссылка,
	|	ПачкаДокументовСЗВ_6_1.НомерПачки,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.НачисленоСтраховая) КАК НачисленоСтраховая,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.УплаченоСтраховая) КАК УплаченоСтраховая,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.НачисленоНакопительная) КАК НачисленоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.УплаченоНакопительная) КАК УплаченоНакопительная,
	|	ПачкаДокументовСЗВ_6_1.КатегорияЗастрахованныхЛиц КАК КатегорияЗастрахованныхЛиц,
	|	ПачкаДокументовСЗВ_6_1.ТипСведенийСЗВ КАК ТипСведенийСЗВ,
	|	ПачкаДокументовСЗВ_6_1.КорректируемыйПериод КАК КорректируемыйПериод,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка) КАК ТипДоговора,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.ДоначисленоСтраховая) КАК ДоначисленоСтраховая,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.ДоначисленоНакопительная) КАК ДоначисленоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.ДоУплаченоНакопительная) КАК ДоУплаченоНакопительная,
	|	СУММА(ПачкаДокументовСЗВ_6_1Сотрудники.ДоУплаченоСтраховая) КАК ДоУплаченоСтраховая,
	|	""СЗВ-6-1"" КАК ПредставлениеДокумента,
	|	ВЫБОР
	|		КОГДА ПачкаДокументовСЗВ_6_1.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|			ТОГДА 7
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК ПорядокСортировки,
	|	"""" КАК РегистрационныйНомерПФРвКорректируемыйПериод,
	|	0 КАК ПоДоговорамГПХДоПредельнойВеличины,
	|	0 КАК ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	0 КАК ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах,
	|	0 КАК Заработок,
	|	0 КАК ОблагаетсяВзносамиДоПредельнойВеличины,
	|	0 КАК ОблагаетсяВзносамиСвышеПредельнойВеличины
	|ИЗ
	|	Документ.ПачкаДокументовСЗВ_6_1 КАК ПачкаДокументовСЗВ_6_1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПачкаДокументовСЗВ_6_1.Сотрудники КАК ПачкаДокументовСЗВ_6_1Сотрудники
	|		ПО ПачкаДокументовСЗВ_6_1.Ссылка = ПачкаДокументовСЗВ_6_1Сотрудники.Ссылка
	|ГДЕ
	|	ПачкаДокументовСЗВ_6_1.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаДокументовСЗВ_6_1.Ссылка,
	|	ПачкаДокументовСЗВ_6_1.КатегорияЗастрахованныхЛиц,
	|	ПачкаДокументовСЗВ_6_1.ТипСведенийСЗВ,
	|	ПачкаДокументовСЗВ_6_1.КорректируемыйПериод,
	|	ПачкаДокументовСЗВ_6_1.НомерПачки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеестрСЗВ_6_2.Ссылка,
	|	РеестрСЗВ_6_2.НомерПачки,
	|	СУММА(РеестрСЗВ_6_2Сотрудники.НачисленоСтраховая),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.УплаченоСтраховая),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.НачисленоНакопительная),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.УплаченоНакопительная),
	|	РеестрСЗВ_6_2.КатегорияЗастрахованныхЛиц,
	|	РеестрСЗВ_6_2.ТипСведенийСЗВ,
	|	РеестрСЗВ_6_2.КорректируемыйПериод,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.ДоначисленоСтраховая),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.ДоначисленоНакопительная),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.ДоУплаченоНакопительная),
	|	СУММА(РеестрСЗВ_6_2Сотрудники.ДоУплаченоСтраховая),
	|	""СЗВ-6-2"",
	|	ВЫБОР
	|		КОГДА РеестрСЗВ_6_2.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСзв.Исходная)
	|			ТОГДА 7
	|		ИНАЧЕ 4
	|	КОНЕЦ,
	|	"""",
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	Документ.РеестрСЗВ_6_2 КАК РеестрСЗВ_6_2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрСЗВ_6_2.Сотрудники КАК РеестрСЗВ_6_2Сотрудники
	|		ПО РеестрСЗВ_6_2.Ссылка = РеестрСЗВ_6_2Сотрудники.Ссылка
	|ГДЕ
	|	РеестрСЗВ_6_2.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеестрСЗВ_6_2.Ссылка,
	|	РеестрСЗВ_6_2.КатегорияЗастрахованныхЛиц,
	|	РеестрСЗВ_6_2.ТипСведенийСЗВ,
	|	РеестрСЗВ_6_2.КорректируемыйПериод,
	|	РеестрСЗВ_6_2.НомерПачки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПачкаДокументовСЗВ_6_4.Ссылка,
	|	ПачкаДокументовСЗВ_6_4.НомерПачки,
	|	СУММА(ВзносыСЗВ_6_4.НачисленоСтраховая),
	|	СУММА(ВзносыСЗВ_6_4.УплаченоСтраховая),
	|	СУММА(ВзносыСЗВ_6_4.НачисленоНакопительная),
	|	СУММА(ВзносыСЗВ_6_4.УплаченоНакопительная),
	|	ПачкаДокументовСЗВ_6_4.КатегорияЗастрахованныхЛиц,
	|	ПачкаДокументовСЗВ_6_4.ТипСведенийСЗВ,
	|	ПачкаДокументовСЗВ_6_4.КорректируемыйПериод,
	|	ПачкаДокументовСЗВ_6_4.ТипДоговора,
	|	СУММА(ВзносыСЗВ_6_4.ДоначисленоСтраховая),
	|	СУММА(ВзносыСЗВ_6_4.ДоначисленоНакопительная),
	|	СУММА(ВзносыСЗВ_6_4.ДоУплаченоНакопительная),
	|	СУММА(ВзносыСЗВ_6_4.ДоУплаченоСтраховая),
	|	""СЗВ-6-4"",
	|	ВЫБОР
	|		КОГДА ПачкаДокументовСЗВ_6_4.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|			ТОГДА 7
	|		ИНАЧЕ 4
	|	КОНЕЦ,
	|	ПачкаДокументовСЗВ_6_4.РегистрационныйНомерПФРвКорректируемыйПериод,
	|	ЗаработокСЗВ_6_4.ПоДоговорамГПХДоПредельнойВеличины,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах,
	|	ЗаработокСЗВ_6_4.Заработок,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиДоПредельнойВеличины,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиСвышеПредельнойВеличины
	|ИЗ
	|	Документ.ПачкаДокументовСЗВ_6_4 КАК ПачкаДокументовСЗВ_6_4
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаработок КАК ЗаработокСЗВ_6_4
	|		ПО ПачкаДокументовСЗВ_6_4.Ссылка = ЗаработокСЗВ_6_4.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВзносыСЗВ_6_4 КАК ВзносыСЗВ_6_4
	|		ПО ПачкаДокументовСЗВ_6_4.Ссылка = ВзносыСЗВ_6_4.Ссылка
	|ГДЕ
	|	ПачкаДокументовСЗВ_6_4.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаДокументовСЗВ_6_4.Ссылка,
	|	ПачкаДокументовСЗВ_6_4.КатегорияЗастрахованныхЛиц,
	|	ПачкаДокументовСЗВ_6_4.ТипСведенийСЗВ,
	|	ПачкаДокументовСЗВ_6_4.КорректируемыйПериод,
	|	ПачкаДокументовСЗВ_6_4.НомерПачки,
	|	ПачкаДокументовСЗВ_6_4.ТипДоговора,
	|	ПачкаДокументовСЗВ_6_4.РегистрационныйНомерПФРвКорректируемыйПериод,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах,
	|	ЗаработокСЗВ_6_4.ПоДоговорамГПХДоПредельнойВеличины,
	|	ЗаработокСЗВ_6_4.Заработок,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиДоПредельнойВеличины,
	|	ЗаработокСЗВ_6_4.ОблагаетсяВзносамиСвышеПредельнойВеличины
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПачкаРазделов6РасчетаРСВ_1.Ссылка,
	|	ПачкаРазделов6РасчетаРСВ_1.НомерПачки,
	|	СУММА(ВзносыРаздела6.НачисленоСтраховая),
	|	0,
	|	0,
	|	0,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗастрахованныхЛицДляПФР.ПустаяСсылка),
	|	ПачкаРазделов6РасчетаРСВ_1.ТипСведенийСЗВ,
	|	ПачкаРазделов6РасчетаРСВ_1.КорректируемыйПериод,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка),
	|	СУММА(ВзносыРаздела6.ДоначисленоСтраховая),
	|	0,
	|	0,
	|	0,
	|	""Пачка документов раздела 6"",
	|	ВЫБОР
	|		КОГДА ПачкаРазделов6РасчетаРСВ_1.ТипСведенийСЗВ = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ.Исходная)
	|			ТОГДА 7
	|		ИНАЧЕ 4
	|	КОНЕЦ,
	|	ПачкаРазделов6РасчетаРСВ_1.РегистрационныйНомерПФРвКорректируемыйПериод,
	|	СУММА(ЗаработокРаздела6.ПоДоговорамГПХДоПредельнойВеличины),
	|	СУММА(ЗаработокРаздела6.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах),
	|	СУММА(ЗаработокРаздела6.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах),
	|	ЗаработокРаздела6.Заработок,
	|	ЗаработокРаздела6.ОблагаетсяВзносамиДоПредельнойВеличины,
	|	ЗаработокРаздела6.ОблагаетсяВзносамиСвышеПредельнойВеличины
	|ИЗ
	|	Документ.ПачкаРазделов6РасчетаРСВ_1 КАК ПачкаРазделов6РасчетаРСВ_1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаработок КАК ЗаработокРаздела6
	|		ПО ПачкаРазделов6РасчетаРСВ_1.Ссылка = ЗаработокРаздела6.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВзносыРаздела6 КАК ВзносыРаздела6
	|		ПО ПачкаРазделов6РасчетаРСВ_1.Ссылка = ВзносыРаздела6.Ссылка
	|ГДЕ
	|	ПачкаРазделов6РасчетаРСВ_1.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаРазделов6РасчетаРСВ_1.Ссылка,
	|	ПачкаРазделов6РасчетаРСВ_1.ТипСведенийСЗВ,
	|	ПачкаРазделов6РасчетаРСВ_1.КорректируемыйПериод,
	|	ПачкаРазделов6РасчетаРСВ_1.НомерПачки,
	|	ПачкаРазделов6РасчетаРСВ_1.РегистрационныйНомерПФРвКорректируемыйПериод,
	|	ЗаработокРаздела6.Заработок,
	|	ЗаработокРаздела6.ОблагаетсяВзносамиДоПредельнойВеличины,
	|	ЗаработокРаздела6.ОблагаетсяВзносамиСвышеПредельнойВеличины";
	
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции	

Процедура ПоместитьОписаниеСЗВ6ВТаблицуДокументов(ОписаниеСЗВ6)
	СтрокаСпискаПачек = ДокументыКомплекта.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаСпискаПачек, ОписаниеСЗВ6);	
	
	Если СтрокаСпискаПачек.ТипСведенийСЗВ <> Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ Тогда
		СтрокаСпискаПачек.КорректируемыйПериодСтрокой = ПерсонифицированныйУчетКлиентСервер.ПредставлениеОтчетногоПериода(СтрокаСпискаПачек.КорректируемыйПериод);		
	КонецЕсли;	
КонецПроцедуры	

Функция ВыборкаПоШапкеПачекСЗВ63(СписокПачек)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокСЗВ", СписокПачек);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Ссылка,
	|	СУММА(ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Заработок) КАК Заработок,
	|	СУММА(ПачкаДокументовСЗВ_6_3СведенияОЗаработке.ОблагаетсяВзносами) КАК ОблагаетсяВзносами
	|ПОМЕСТИТЬ ВТСведенияОЗаработке
	|ИЗ
	|	Документ.ПачкаДокументовСЗВ_6_3.СведенияОЗаработке КАК ПачкаДокументовСЗВ_6_3СведенияОЗаработке
	|ГДЕ
	|	ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Ссылка В(&СписокСЗВ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПачкаДокументовСЗВ_6_3СведенияОЗаработке.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПачкаДокументовСЗВ_6_3.Ссылка,
	|	ПачкаДокументовСЗВ_6_3.ТипСведенийСЗВ,
	|	ПачкаДокументовСЗВ_6_3.ТипДоговора,
	|	СведенияОЗаработкеСЗВ63.Заработок,
	|	СведенияОЗаработкеСЗВ63.ОблагаетсяВзносами,
	|	""СЗВ-6-3"" КАК ПредставлениеДокумента,
	|	9 КАК ПорядокСортировки,
	|	ПачкаДокументовСЗВ_6_3.НомерПачки,
	|	ПачкаДокументовСЗВ_6_3.КатегорияЗастрахованныхЛиц
	|ИЗ
	|	ВТСведенияОЗаработке КАК СведенияОЗаработкеСЗВ63
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПачкаДокументовСЗВ_6_3 КАК ПачкаДокументовСЗВ_6_3
	|		ПО (ПачкаДокументовСЗВ_6_3.Ссылка = СведенияОЗаработкеСЗВ63.Ссылка)
	|ГДЕ
	|	ПачкаДокументовСЗВ_6_3.Ссылка В(&СписокСЗВ)";
	
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции	

Процедура ПоместитьОписаниеСЗВ63ВТаблицуДокументов(ОписаниеСЗВ63)
	СтрокаСпискаПачек = ДокументыКомплекта.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаСпискаПачек, ОписаниеСЗВ63);	
КонецПроцедуры	

#КонецОбласти

#Область СохранениеДокументаВБазе

Функция ДанныеШапкиТекущегоДокумента()
	Возврат ДокументыКомплекта.Найти(СсылкаНаРедактируемыйДокумент, "Ссылка");
КонецФункции	

#КонецОбласти

#Область ЗаполнениеДанныхДокументаАДВ62ПоДаннымОбъекта

Процедура ДанныеОбъектаВДокументАДВ_6_2(ДокументОбъект)
	ДокументОбъект.ПачкиДокументов.Очистить();
	
	Для Каждого ОписаниеДокумента Из ДокументыКомплекта Цикл
		ТипДокумента = ТипЗнч(ОписаниеДокумента.Ссылка);
		
		Если ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_1")
			Или ТипДокумента = Тип("ДокументСсылка.РеестрСЗВ_6_2")
			Или ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_4") Тогда
			
			ДокументОписи = ДокументОбъект.ПачкиДокументов.Добавить();
			ДокументОписи.ПачкаДокументов = ОписаниеДокумента.Ссылка;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#Область ЗаполнениеДанныхДокументаСЗВ612ПоДаннымОбъекта

Процедура  ДанныеОбъектаВДокументСЗВ_6(ДокументОбъект, СотрудникиСФиксСтажем, ИзменившиесяДанныеСотрудников)
	ДокументОбъект.Сотрудники.Очистить();
	ДокументОбъект.ЗаписиОСтаже.Очистить();
	
	Для Каждого СтрокаСотрудник Из Сотрудники Цикл
		СтрокаСотрудникДокумента = ДокументОбъект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудникДокумента, СтрокаСотрудник);
		
		Если СтрокаСотрудник.ФиксироватьСтаж Тогда
			СтрокаСотрудник.ФиксироватьСтаж = Ложь;
			СотрудникиСФиксСтажем.Добавить(СтрокаСотрудник.Сотрудник);
		КонецЕсли;	
		
		Если СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица Тогда 
			ИзменившиесяДанныеСотрудников.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
			СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица = Ложь;
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого СтрокаСтажа Из ЗаписиОСтаже Цикл
		СтрокаСтажаДокумента = ДокументОбъект.ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтажаДокумента, СтрокаСтажа);
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#Область ЗаполнениеДанныхДокументаСЗВ63ПоДаннымОбъекта

Процедура  ДанныеОбъектаВДокументСЗВ_6_3(ДокументОбъект, ИзменившиесяДанныеСотрудников)	
	ДокументОбъект.Сотрудники.Очистить();
	ДокументОбъект.СведенияОЗаработке.Очистить();
	
	Для Каждого СтрокаСотрудник Из Сотрудники Цикл
		СтрокаСотрудникДокумента = ДокументОбъект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудникДокумента, СтрокаСотрудник);
		
		Если СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица Тогда 
			ИзменившиесяДанныеСотрудников.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
			СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица = Ложь;
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого СтрокаЗаработок Из СведенияОЗаработке Цикл
		СтрокаЗаработокДокумента = ДокументОбъект.СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработокДокумента, СтрокаЗаработок);
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#Область ЗаполнениеДанныхДокументаСЗВ64ПоДаннымОбъекта

Процедура ДанныеОбъектаВДокументСЗВ_6_4(ДокументОбъект, СотрудникиСФиксСтажем, ИзменившиесяДанныеСотрудников)
	ДокументОбъект.Сотрудники.Очистить();
	ДокументОбъект.ЗаписиОСтаже.Очистить();
	ДокументОбъект.СведенияОЗаработке.Очистить();
		
	Для Каждого СтрокаСотрудник Из Сотрудники Цикл
		СтрокаСотрудникДокумента = ДокументОбъект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудникДокумента, СтрокаСотрудник);
		
		Если СтрокаСотрудник.ФиксироватьСтаж Тогда
			СтрокаСотрудник.ФиксироватьСтаж = Ложь;
			СотрудникиСФиксСтажем.Добавить(СтрокаСотрудник.Сотрудник);
		КонецЕсли;	
		
		Если СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица Тогда 
			ИзменившиесяДанныеСотрудников.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
			СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица = Ложь;
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого СтрокаСтажа Из ЗаписиОСтаже Цикл
		СтрокаСтажаДокумента = ДокументОбъект.ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтажаДокумента, СтрокаСтажа);
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработок Из СведенияОЗаработке Цикл
		СтрокаЗаработокДокумента = ДокументОбъект.СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработокДокумента, СтрокаЗаработок);
	КонецЦикла;	
	
КонецПроцедуры	

Процедура ДанныеОбъектаВПачкуРазделов6(ДокументОбъект, СотрудникиСФиксСтажем, ИзменившиесяДанныеСотрудников)
	ДокументОбъект.Сотрудники.Очистить();
	ДокументОбъект.ЗаписиОСтаже.Очистить();
	ДокументОбъект.СведенияОЗаработке.Очистить();
	ДокументОбъект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Очистить();
	ДокументОбъект.КорректирующиеСведения.Очистить();
		
	Для Каждого СтрокаСотрудник Из Сотрудники Цикл
		СтрокаСотрудникДокумента = ДокументОбъект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСотрудникДокумента, СтрокаСотрудник);
		
		Если СтрокаСотрудник.ФиксироватьСтаж Тогда
			СтрокаСотрудник.ФиксироватьСтаж = Ложь;
			СотрудникиСФиксСтажем.Добавить(СтрокаСотрудник.Сотрудник);
		КонецЕсли;	
		
		Если СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица Тогда 
			ИзменившиесяДанныеСотрудников.Вставить(СтрокаСотрудник.Сотрудник, СтрокаСотрудник);
			СтрокаСотрудник.ИзменилисьДанныеФизическогоЛица = Ложь;
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого СтрокаСтажа Из ЗаписиОСтаже Цикл
		СтрокаСтажаДокумента = ДокументОбъект.ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтажаДокумента, СтрокаСтажа);
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработок Из СведенияОЗаработке Цикл
		СтрокаЗаработокДокумента = ДокументОбъект.СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработокДокумента, СтрокаЗаработок);
	КонецЦикла;	
	
	Для Каждого СтрокаЗаработок Из СведенияОЗаработкеНаВредныхИТяжелыхРаботах Цикл
		СтрокаЗаработокДокумента = ДокументОбъект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаработокДокумента, СтрокаЗаработок);
	КонецЦикла;	
	
	Если ДокументОбъект.ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ Тогда 
		Для Каждого СтрокаДанныхРаздела66 Из КорректирующиеСведения Цикл
			СтрокаДанныхДокумента = ДокументОбъект.КорректирующиеСведения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанныхДокумента, СтрокаДанныхРаздела66);
		КонецЦикла;	
	КонецЕсли;	

КонецПроцедуры	

// Проверка заполнения.

Процедура ПроверитьДанныеКомплекта(ОшибкиРСВ, Отказ) Экспорт		
	// Сначала получим ошибки, которые нужно маппировать.
	
	СоответствиеДанныхОбъектаИтоговымДаннымОписи = СоответствиеДанныхОбъектаИтоговымДаннымОписи();
	СоответствиеДанныхОбъектаИтоговымДаннымРСВ = СоответствиеДанныхОбъектаИтоговымДаннымРСВ();
	
	КомплектОбъект.ПроверитьДанныеКомплекта(ОшибкиРСВ, Отказ);
	
	Ошибки = ПолучитьСообщенияПользователю(Истина);
	
	ОшибкиИтоговРСВ = Новый Массив;
	ОшибкиИтоговОписи = Новый Массив;	
	
	Для Каждого СообщениеПользователю Из Ошибки Цикл
		Если ТипЗнч(СообщениеПользователю.КлючДанных) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			ОшибкиИтоговРСВ.Добавить(СообщениеПользователю);		
		ИначеЕсли ТипЗнч(СообщениеПользователю.КлючДанных) = Тип("ДокументСсылка.ОписьПачекСЗВ_6") Тогда 
			ОшибкиИтоговОписи.Добавить(СообщениеПользователю);	
		Иначе	
			СообщениеПользователю.Сообщить();
		КонецЕсли;	
	КонецЦикла;	
	
	Если ОшибкиИтоговРСВ.Количество() > 0 Тогда
		ДанныеРСВ = КомплектОбъект.ДанныеРСВКомплекта();
		
		ЗарплатаКадрыОтображениеОшибок.ПреобразоватьПутиКДаннымВСообщенияхПользователю(
			ДанныеРСВ,
			ЭтотОбъект,
			ОшибкиИтоговРСВ,
			СоответствиеДанныхОбъектаИтоговымДаннымРСВ);		
	КонецЕсли;	
		
	Для Каждого СообщениеПользователю Из ОшибкиИтоговРСВ Цикл	
		СообщениеПользователю.Сообщить();
	КонецЦикла;
	
	Если ОшибкиИтоговОписи.Количество() > 0 Тогда
		ДанныеОписи = КомплектОбъект.СводныеДанныеПоДокументамКомплекта();
		
		ЗарплатаКадрыОтображениеОшибок.ПреобразоватьПутиКДаннымВСообщенияхПользователю(
			ДанныеОписи,
			ЭтотОбъект,
			ОшибкиИтоговОписи,
			СоответствиеДанныхОбъектаИтоговымДаннымОписи);		
	КонецЕсли;	
		
	Для Каждого СообщениеПользователю Из ОшибкиИтоговОписи Цикл	
		СообщениеПользователю.Сообщить();
	КонецЦикла;	
	
КонецПроцедуры

Функция СоответствиеДанныхОбъектаИтоговымДаннымОписи()
	ОписаниеСвязиДанных = ЗарплатаКадрыОтображениеОшибок.ОписаниеСвязиДанных();		
		
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "НачисленоСтраховая", "ОписьНачисленоСтраховая");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "НачисленоНакопительная", "ОписьНачисленоНакопительная");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "УплаченоСтраховая", "ОписьУплаченоСтраховая");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "УплаченоНакопительная", "ОписьУплаченоНакопительная");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "Заработок", "ОписьЗаработок");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаетсяВзносами", "ОписьОблагаетсяВзносами");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ЗаработокПоЛьготнымКатегориям", "ОписьЗаработокПоЛьготнымКатегориям");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаетсяВзносамиПоЛьготнымКатегориям", "ОписьОблагаетсяВзносамиПоЛьготнымКатегориям");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах", "ОписьОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах", "ОписьОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"ИтогиПоКатегориям", 
		"*",
		"ИтогиПоКатегориям",
		"");
		
	Возврат ОписаниеСвязиДанных;	
КонецФункции
	
Функция СоответствиеДанныхОбъектаИтоговымДаннымРСВ()	
	ОписаниеСвязиДанных = ЗарплатаКадрыОтображениеОшибок.ОписаниеСвязиДанных();
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "КоличествоПрименяемыхТарифов", "РСВ_1КоличествоПрименяемыхТарифов");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "НачисленоРСВ1НаСтраховую", "РСВ_1НачисленоСтраховая");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "НачисленоРСВ1НаНакопительную", "РСВ_1НачисленоНакопительная");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "УплаченоРСВ1НаСтраховую", "РСВ_1УплаченоСтраховая");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "УплаченоРСВ1НаНакопительную", "РСВ_1УплаченоНакопительная");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаемаяБазаРСВ1", "РСВ_1ОблагаемаяБаза");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаемаяБазаРСВ1Льготная", "РСВ_1ОблагаемаяБазаПоЛьготнымТарифам");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОбъектОбложенияРСВ1", "РСВ_1ОбъектОбложения");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОбъектОбложенияРСВ1Льготный", "РСВ_1ОбъектОбложенияПоЛьготнымТарифам");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "УменьшенныйОбъектОбложенияРСВ1", "РСВ_1УменьшенныйОбъектОбложения");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "УменьшенныйОбъектОбложенияРСВ1Льготный", "РСВ_1УменьшенныйОбъектОбложенияПоЛьготнымТарифам");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах", "РСВ_1ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах", "РСВ_1ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОбъектОбложенияРСВ1За1Месяц", "РСВ_1ОбъектОбложенияЗа1Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаемаяБазаРСВ1За1Месяц", "РСВ_1ОблагаемаяБазаЗа1Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "БазаСверхПредельнойРСВ1За1Месяц", "РСВ_1БазаСверхПредельнойЗа1Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОбъектОбложенияРСВ1За2Месяц", "РСВ_1ОбъектОбложенияЗа2Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаемаяБазаРСВ1За2Месяц", "РСВ_1ОблагаемаяБазаЗа2Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "БазаСверхПредельнойРСВ1За2Месяц", "РСВ_1БазаСверхПредельнойЗа2Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОбъектОбложенияРСВ1За3Месяц", "РСВ_1ОбъектОбложенияЗа3Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "ОблагаемаяБазаРСВ1За3Месяц", "РСВ_1ОблагаемаяБазаЗа3Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "БазаСверхПредельнойРСВ1За3Месяц", "РСВ_1БазаСверхПредельнойЗа3Месяц");
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "КоличествоЗастрахованных", "РСВ_1КоличествоЗастрахованныхЛиц");
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"ИтогиПоТарифам", 
		"*",
		"РСВ_1ИтогиПоТарифам",
		"");
		
	Возврат ОписаниеСвязиДанных;
КонецФункции	

#КонецОбласти

#КонецОбласти

#КонецЕсли
