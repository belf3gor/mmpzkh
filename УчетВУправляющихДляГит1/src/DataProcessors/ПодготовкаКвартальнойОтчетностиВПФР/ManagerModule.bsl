#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт	
	МассивАДВ11 = Новый Массив;
	ОписьАДВСсылка = Неопределено;
	ПачкиСЗВ61 = Новый Массив;
	ПачкиСЗВ62 = Новый Массив;
	ПачкиСЗВ63 = Новый Массив;
	ПачкиСЗВ64 = Новый Массив;
	ПачкиРазделов6 = Новый Массив;
	Комплект = Справочники.КомплектыОтчетностиПерсучета.ПустаяСсылка();
	
	Для Каждого ОбъектПечати Из МассивОбъектов Цикл
		Если ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ВедомостьУплатыАДВ_11") Тогда
			МассивАДВ11.Добавить(ОбъектПечати);
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ОписьПачекСЗВ_6") Тогда
			ОписьАДВСсылка = ОбъектПечати;
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_1") Тогда
			ПачкиСЗВ61.Добавить(ОбъектПечати);	
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.РеестрСЗВ_6_2") Тогда
			ПачкиСЗВ62.Добавить(ОбъектПечати);		
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3") Тогда
			ПачкиСЗВ63.Добавить(ОбъектПечати);			
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_4") Тогда
			ПачкиСЗВ64.Добавить(ОбъектПечати);		
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ПачкаРазделов6РасчетаРСВ_1") Тогда
			ПачкиРазделов6.Добавить(ОбъектПечати);	
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.КомплектыОтчетностиПерсучета") Тогда
			Комплект = ОбъектПечати;				
		КонецЕсли;	
	КонецЦикла;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаАДВ_11") И МассивАДВ11.Количество() > 0 Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаАДВ_11", "Форма АДВ-11", Документы.ВедомостьУплатыАДВ_11.СформироватьПечатнуюФормуАДВ_11(МассивАДВ11, ОбъектыПечати))	
	ИначеЕсли МассивАДВ11.Количество() = 0 Тогда 
		СтруктураОтбора = Новый Структура("ИмяМакета", "ФормаАДВ_11");
		УдаляемыеФормы = КоллекцияПечатныхФорм.НайтиСтроки(СтруктураОтбора);
		Для Каждого УдаляемаяСтрока Из УдаляемыеФормы Цикл
			КоллекцияПечатныхФорм.Удалить(УдаляемаяСтрока);				
		КонецЦикла;	
	КонецЕсли;	
	
	Если ОписьАДВСсылка <> Неопределено Тогда
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаАДВ_6_2") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаАДВ_6_2", "Форма АДВ-6-2", Документы.ОписьПачекСЗВ_6.СформироватьПечатнуюФормуАДВ6_2(ОписьАДВСсылка, ОбъектыПечати));	
		КонецЕсли;
	КонецЕсли;
	
	Если ПачкиСЗВ61.Количество() > 0 Тогда 
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСЗВ_6_1") Тогда 
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаСЗВ_6_1", "Форма СЗВ-6-1", Документы.ПачкаДокументовСЗВ_6_1.СформироватьПечатнуюФормуСЗВ_6_1(ПачкиСЗВ61, ОбъектыПечати));	
		КонецЕсли;
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СписокЗастрахованныхЛиц") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СписокЗастрахованныхЛиц", "Список застрахованных лиц", Документы.ПачкаДокументовСЗВ_6_1.СформироватьПечатнуюФормуСписокЗастрахованныхЛиц(ПачкиСЗВ61, ОбъектыПечати));	
		КонецЕсли;
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаАДВ_6_3") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаАДВ_6_3", "Форма АДВ-6-3", Документы.ПачкаДокументовСЗВ_6_1.СформироватьПечатнуюФормуАДВ_6_3(ПачкиСЗВ61, ОбъектыПечати));	
		КонецЕсли;
	КонецЕсли;

	Если ПачкиСЗВ62.Количество() > 0 Тогда 
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСЗВ_6_2") Тогда 
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаСЗВ_6_2", "Форма СЗВ-6-2", Документы.РеестрСЗВ_6_2.СформироватьПечатнуюФормуСЗВ_6_2(ПачкиСЗВ62, ОбъектыПечати));
		КонецЕсли;	
	КонецЕсли;
	
	Если ПачкиСЗВ63.Количество() > 0 Тогда
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСЗВ_6_3") Тогда 		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаСЗВ_6_3", "Форма СЗВ-6-3", Документы.ПачкаДокументовСЗВ_6_3.СформироватьПечатнуюФормуСВВ6_3(ПачкиСЗВ63, ОбъектыПечати));		
		КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаАДВ_6_4") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаАДВ_6_4", "Форма АДВ-6-4", Документы.ПачкаДокументовСЗВ_6_3.СформироватьПечатнуюФормуАДВ6_4(МассивОбъектов, ОбъектыПечати));	
		КонецЕсли;	
	КонецЕсли;
	
	Если ПачкиСЗВ64.Количество() > 0 Тогда
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСЗВ_6_4") Тогда 		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаСЗВ_6_4", "Форма СЗВ-6-4", Документы.ПачкаДокументовСЗВ_6_4.СформироватьПечатнуюФормуСЗВ_6_4(ПачкиСЗВ64, ОбъектыПечати));		
		КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаАДВ_6_5") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаАДВ_6_5", "Форма АДВ-6-5", Документы.ПачкаДокументовСЗВ_6_4.СформироватьПечатнуюФормуАДВ_6_5(МассивОбъектов, ОбъектыПечати));	
		КонецЕсли;	
	КонецЕсли;
		
КонецПроцедуры	

#КонецОбласти

#КонецЕсли