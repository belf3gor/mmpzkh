///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Текст сообщения пустой, вывести его на передний план.
	Элементы.СтраницыПрогресса.ТекущаяСтраница = Элементы.СтраницаГотово;

	лкКаталогВременныхФайлов = КаталогВременныхФайлов();
	Если (Прав(лкКаталогВременныхФайлов, 1) <> ПолучитьРазделительПутиСервера())
			И (Прав(лкКаталогВременныхФайлов, 1) <> ПолучитьРазделительПутиСервера()) Тогда
		лкКаталогВременныхФайлов = лкКаталогВременныхФайлов + ПолучитьРазделительПутиСервера();
	КонецЕсли;
	ЭтотОбъект.ИмяКаталогаЭкспорта = лкКаталогВременныхФайлов;

	ЭтотОбъект.ПериодДень = ТекущаяДатаСеанса();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЭкспорт(Команда)

	Если ПроверитьЗаполнение() Тогда

		Элементы.СтраницыПрогресса.ТекущаяСтраница = Элементы.СтраницаПрогресс;
		ЭтотОбъект.ОбновитьОтображениеДанных();

		ДатаСтрокой = Формат(ЭтотОбъект.ПериодДень,"ДФ=yyyy-MM-dd");

		ВсеСобытия = ОбработкаНовостейКлиентСервер.ПолучитьСписокВсехСобытийЖурналаРегистрации();
		Результат = ИнтернетПоддержкаПользователейВызовСервера.ВыгрузитьВсеСобытияЖурналаРегистрации(
			Новый Структура("ДатаНачала, ДатаОкончания, Событие",
				НачалоДня(ЭтотОбъект.ПериодДень),
				КонецДня(ЭтотОбъект.ПериодДень),
				ВсеСобытия),
			Новый Структура("Архивировать", Истина));

		Если ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Если ПустаяСтрока(Результат.АдресВременногоХранилищаФайла) Тогда
				ТекстСообщения = НСтр("ru='Не удалось экспортировать события журнала регистрации в файл.
					|Данные не удалось поместить во временное хранилище.'");
				ЭтотОбъект.РезультатЭкспорта = ТекстСообщения;
			Иначе
				// Попробовать записать файл.
				ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(Результат.АдресВременногоХранилищаФайла);
				Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
					лкИмяКаталогаЭкспорта = ИнтернетПоддержкаПользователейКлиентСервер.УдалитьПоследнийСимвол(
						ЭтотОбъект.ИмяКаталогаЭкспорта, "\/")
						+ ПолучитьРазделительПути();
					Если ЭтотОбъект.СжиматьФайл Тогда
						ИмяФайла = лкИмяКаталогаЭкспорта + ДатаСтрокой + ".zip";
					Иначе
						ИмяФайла = лкИмяКаталогаЭкспорта + ДатаСтрокой + ".xml";
					КонецЕсли;
					Попытка
						ДвоичныеДанныеФайла.Записать(ИмяФайла); // Метод "Записать" не работает в веб-клиенте.
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Журнал событий успешно экспортирован в файл с именем
								|%1'"),
							ИмяФайла);
						ЭтотОбъект.РезультатЭкспорта = ТекстСообщения;
					Исключение
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Не удалось записать файл по причине:
								|%1'"),
							ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						ЭтотОбъект.РезультатЭкспорта = ТекстСообщения;
					КонецПопытки;
				Иначе
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Не удалось экспортировать события журнала регистрации в файл.
							|Во временное хранилище помещены данные неправильного типа %1.'"),
						?(ДвоичныеДанныеФайла = Неопределено, "Неопределено", ТипЗнч(ДвоичныеДанныеФайла)));
					ЭтотОбъект.РезультатЭкспорта = ТекстСообщения;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЭтотОбъект.РезультатЭкспорта = Результат.ТекстОшибки;
		КонецЕсли;

		Элементы.СтраницыПрогресса.ТекущаяСтраница = Элементы.СтраницаГотово;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
