
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Обработки.ПроверкаРеквизитовПлатежныхПоручений.ПроверитьНаличиеНекорректныхПлатежей(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуДокументовНаСервере();
	Если ТаблицаДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаНеПроверятьПриНачалеРаботы = ХранилищеОбщихНастроек.Загрузить("ПроверкаРеквизитовПлатежныхПоручений", "НеПроверятьПриНачалеРаботы");
	НеПроверятьПриНачалеРаботы = (НастройкаНеПроверятьПриНачалеРаботы = Истина);
	
	НайденыОшибки = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не НайденыОшибки Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не найдено платежных поручений с ошибками!'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДекорацияОписаниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСверкуРасчетовСФНС" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если РазделениеВключено() Тогда
			ОткрытьФормуСверкаНалоговСФНС();
		Иначе
			ОткрытьФорму1СОтчетностьНаСверках();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТаблицаДокументов

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ПлатежноеПоручениеПредставление" Тогда
		
		ТекущаяСтрока = ТаблицаДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если ТекущаяСтрока <> Неопределено И ЗначениеЗаполнено(ТекущаяСтрока.ПлатежноеПоручениеСсылка) Тогда
			ПоказатьЗначение(, ТекущаяСтрока.ПлатежноеПоручениеСсылка);
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "ЗаявлениеОбУточненииПлатежа" Тогда
		
		ТекущаяСтрока = ТаблицаДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если ТекущаяСтрока = Неопределено Или Не ЗначениеЗаполнено(ТекущаяСтрока.АдресФайлаЗаявления) Тогда
			Возврат;
		КонецЕсли;
		
		ПолучитьФайл(ТекущаяСтрока.АдресФайлаЗаявления, ТекущаяСтрока.ЗаявлениеОбУточненииПлатежа, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьЗаявления(Команда)
	
	ЗаполнитьЗаявленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицуДокументов(Команда)
	ЗаполнитьТаблицуДокументовНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьФлажки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	УстановитьФлажки(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ХранилищеОбщихНастроек.Сохранить("ПроверкаРеквизитовПлатежныхПоручений", "НеПроверятьПриНачалеРаботы", НеПроверятьПриНачалеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Пометка)
	
	Для Каждого СтрокаТаблицы Из ТаблицаДокументов Цикл
		СтрокаТаблицы.Пометка = Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументовНаСервере()
	
	Обработки.ПроверкаРеквизитовПлатежныхПоручений.ЗаполнитьТаблицуДокументов(ТаблицаДокументов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаявленияНаСервере()
	
	Обработки.ПроверкаРеквизитовПлатежныхПоручений.ЗаполнитьЗаявленияОбУточненииПлатежа(ТаблицаДокументов, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазделениеВключено()
	Возврат ОбщегоНазначения.РазделениеВключено();
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуСверкаНалоговСФНС()
	
	ПараметрыФормы = Новый Структура;
	
	Если ТаблицаДокументов.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Организация", ТаблицаДокументов[0].Организация);
		ПараметрыФормы.Вставить("Год", Год(ТаблицаДокументов[0].ДатаПлатежа));
	КонецЕсли;
	
	ОткрытьФорму("Обработка.СверкаНалоговСФНС.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФорму1СОтчетностьНаСверках()
	
	ПараметрыФормы = Новый Структура;
	
	Если ТаблицаДокументов.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Организация", ТаблицаДокументов[0].Организация);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Раздел", ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки"));
	
	ОткрытьФорму("ОбщаяФорма.РегламентированнаяОтчетность",
		ПараметрыФормы, ,
		"1С-Отчетность");
	
	Оповестить("Открытие формы 1С-Отчетность", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти
