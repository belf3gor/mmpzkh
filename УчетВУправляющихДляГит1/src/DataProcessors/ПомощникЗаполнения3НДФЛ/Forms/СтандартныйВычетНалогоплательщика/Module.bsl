#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполнить месяцы в таблице СтандартныеВычета.
	ДобавитьМесяцы(НСтр("ru='Январь'"),  НСтр("ru='Июль'"));
	ДобавитьМесяцы(НСтр("ru='Февраль'"), НСтр("ru='Август'"));
	ДобавитьМесяцы(НСтр("ru='Март'"),    НСтр("ru='Сентябрь'"));
	ДобавитьМесяцы(НСтр("ru='Апрель'"),  НСтр("ru='Октябрь'"));
	ДобавитьМесяцы(НСтр("ru='Май'"),     НСтр("ru='Ноябрь'"));
	ДобавитьМесяцы(НСтр("ru='Июнь'"),    НСтр("ru='Декабрь'"));
	
	Если НЕ Параметры.Свойство("СтруктураДоходовВычетов") Тогда
		// Значения по умолчанию.
		ВычетПрименяется = "Год";
		РазмерВычета     = "Вычет500";
		УстановитьСуммуВычета(ЭтотОбъект);
	Иначе
		// Восстановим реквизиты формы.
		ДанныеФормы = Параметры.СтруктураДоходовВычетов.ДанныеФормы;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеФормы);
		
		// Восстановим суммы вычетов.
		СуммаВычетаПоМесяцам = ДанныеФормы.СуммаВычетаПоМесяцам;
		Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
			СтрокаВычета.СуммаВычета1 = СуммаВычетаПоМесяцам[СтрокаВычета.Месяц1];
			СтрокаВычета.СуммаВычета2 = СуммаВычетаПоМесяцам[СтрокаВычета.Месяц2];
		КонецЦикла;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РазмерВычетаПриИзменении(Элемент)
	
	УстановитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетПрименяетсяПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	Если ВычетПрименяется = "Год" Тогда
		УстановитьСуммуВычета(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураРезультата = Новый Структура;
	// Информация для формы помощника.
	СтруктураРезультата.Вставить("СуммаВычета", 0);
	СтруктураРезультата.Вставить("Вид", 
		ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.СтандартныйНаНалогоплательщика"));
	СтруктураРезультата.Вставить("Информация", НСтр("ru='Стандартный вычет на налогоплательщика'"));
	
	СуммаВычетаПоМесяцам = Новый Структура;
	СуммаВычета500  = 0;
	СуммаВычета3000 = 0;
	Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
		// Месяц 1
		СуммаВычетаПоМесяцам.Вставить(СтрокаВычета.Месяц1, СтрокаВычета.СуммаВычета1);
		Если СтрокаВычета.СуммаВычета1 = 500 Тогда
			СуммаВычета500 = СуммаВычета500 + 500;
		ИначеЕсли СтрокаВычета.СуммаВычета1 = 3000 Тогда
			СуммаВычета3000 = СуммаВычета3000 + 3000;
		КонецЕсли;
		// Месяц 2
		СуммаВычетаПоМесяцам.Вставить(СтрокаВычета.Месяц2, СтрокаВычета.СуммаВычета2);
		Если СтрокаВычета.СуммаВычета2 = 500 Тогда
			СуммаВычета500 = СуммаВычета500 + 500;
		ИначеЕсли СтрокаВычета.СуммаВычета2 = 3000 Тогда
			СуммаВычета3000 = СуммаВычета3000 + 3000;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураРезультата.СуммаВычета = СуммаВычета500 + СуммаВычета3000;
	
	// Данные для декларации.
	СтруктураДанныхДекларации = Новый Структура;
	СтруктураДанныхДекларации.Вставить("СуммаВычета500", СуммаВычета500);
	СтруктураДанныхДекларации.Вставить("СуммаВычета3000", СуммаВычета3000);
	СтруктураРезультата.Вставить("ДанныеДекларации", СтруктураДанныхДекларации);
	
	// Данные формы для восстановления.
	СтруктураДанныхФормы = Новый Структура;
	СтруктураДанныхФормы.Вставить("СуммаВычетаПоМесяцам", СуммаВычетаПоМесяцам);
	СтруктураДанныхФормы.Вставить("ВычетПрименяется",     ВычетПрименяется);
	СтруктураДанныхФормы.Вставить("РазмерВычета",         РазмерВычета);
	СтруктураРезультата.Вставить("ДанныеФормы",           СтруктураДанныхФормы);

	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Если Форма.ВычетПрименяется = "Год" Тогда
		Элементы.СтраницыПрименениеВычета.ТекущаяСтраница = Элементы.СтраницаВесьГод;
	Иначе
		Элементы.СтраницыПрименениеВычета.ТекущаяСтраница = Элементы.СтраницаНесколькоМесяцевВГоду;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьМесяцы(Месяц1, Месяц2)
	
	СтрокаВычета = СтандартныйВычет.Добавить();
	СтрокаВычета.Месяц1 = Месяц1;
	СтрокаВычета.Месяц2 = Месяц2;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСуммуВычета(Форма)
	
	Если Форма.РазмерВычета = "Вычет500" Тогда
		Сумма = 500;
	Иначе
		Сумма = 3000;
	КонецЕсли;
	
	Для Каждого СтрокаВычета Из Форма.СтандартныйВычет Цикл
		СтрокаВычета.СуммаВычета1 = Сумма;
		СтрокаВычета.СуммаВычета2 = Сумма;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти