
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("АдресТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДанных = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	
	Если Не ТипЗнч(ТаблицаДанных) = Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	СформирватьОтчетНаСервере(ТаблицаДанных);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Формирует отчет со сведениями о незаполненных данных.
Процедура СформирватьОтчетНаСервере(ТаблицаДанных)
	
	ТаблицаДанных.Сортировать("ВидОбмена, ОбъектСтрока");
	
	Макет = УПЖКХ_ТиповыеМетодыСервер.ПолучитьМакет("Обработка.УПЖКХ_ОбменДаннымиСГИСЖКХ.ОтчетОНезаполненныхОбъектах");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокОтчета");
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	ТаблицаВидовОбмена = ТаблицаДанных.Скопировать();
	ТаблицаВидовОбмена.Свернуть("ВидОбмена");
	
	ТаблицаДанных.Индексы.Добавить("ВидОбмена");
	
	Для Каждого СтрокаВидаОбмена Из ТаблицаВидовОбмена Цикл
		ОбластьВидОбмена = Макет.ПолучитьОбласть("ОбластьНазваниеОбмена");
		ОбластьВидОбмена.Параметры.НазваниеОбмена = СтрокаВидаОбмена.ВидОбмена;
		ТабличныйДокумент.Вывести(ОбластьВидОбмена);
		
		СтрокиВидаОбмена = ТаблицаДанных.НайтиСтроки(Новый Структура("ВидОбмена", СтрокаВидаОбмена.ВидОбмена));
		ТаблицаОбъектов  = ТаблицаДанных.Скопировать(СтрокиВидаОбмена);
		
		ТаблицаОбъектовДляСвертки = ТаблицаОбъектов.Скопировать();
		ТаблицаОбъектовДляСвертки.Свернуть("ОбъектСсылка, ОбъектСтрока");
		
		ВсегоОбъектов = ТаблицаОбъектовДляСвертки.Количество();
		ВсегоОшибок   = ТаблицаОбъектов.Количество();
		
		ОбластьСводка = Макет.ПолучитьОбласть("ОбластьСводкаПоОбмену");
		ОбластьСводка.Параметры.ВсегоОбъектов = ВсегоОбъектов;
		ОбластьСводка.Параметры.ВсегоОшибок   = ВсегоОшибок;
		ТабличныйДокумент.Вывести(ОбластьСводка);
		
		ТаблицаОбъектов.Индексы.Добавить("ОбъектСсылка");
		
		Для Каждого СтрокаОбъекта Из ТаблицаОбъектовДляСвертки Цикл
			СтрокиОшибок  = ТаблицаОбъектов.НайтиСтроки(Новый Структура("ОбъектСсылка", СтрокаОбъекта.ОбъектСсылка));
			ТаблицаОшибок = ТаблицаОбъектов.Скопировать(СтрокиОшибок);
			
			ТаблицаРазличныхОшибок = ТаблицаОшибок.Скопировать();
			ТаблицаРазличныхОшибок.Свернуть("ТипОшибки");
			
			Для Каждого СтрокаТипаОшибки Из ТаблицаРазличныхОшибок Цикл
				СтрокиТекТипаОшибки  = ТаблицаОшибок.НайтиСтроки(Новый Структура("ТипОшибки", СтрокаТипаОшибки.ТипОшибки));
				
				КоличествоОшибокТекТипа = СтрокиТекТипаОшибки.Количество();
				Если КоличествоОшибокТекТипа = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ОбластьОбъект = Макет.ПолучитьОбласть("ОбластьОбъект");
				ОбластьОбъект.Параметры.Заполнить(СтрокиТекТипаОшибки.Получить(0));
				ОбластьОбъект.Параметры.КоличествоОшибок = КоличествоОшибокТекТипа;
				ТабличныйДокумент.Вывести(ОбластьОбъект);
				
				ТабличныйДокумент.НачатьГруппуСтрок();
				
				Для Каждого СтрокаОшибки Из СтрокиТекТипаОшибки Цикл
					ОбластьОшибка = Макет.ПолучитьОбласть("ОбластьОписаниеОшибки");
					ОбластьОшибка.Параметры.Заполнить(СтрокаОшибки);
					ТабличныйДокумент.Вывести(ОбластьОшибка);
				КонецЦикла;
				
				ТабличныйДокумент.ЗакончитьГруппуСтрок();
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти