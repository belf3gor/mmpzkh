
#Область ОбработчикиФормы

&НаСервере
// Обработчик события формы "ПриСозданииНаСервере".
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СпособИдентификацииЛС = УПЖКХ_ИнтеграцияСГИСЖКХВажныеМеханизмы.ПолучитьСпособИдентификацииЛСДляГИСЖКХ();
	
	Если Параметры.Свойство("ОтображатьВсеДоступныеВидыОбмена") Тогда
		Параметры.Свойство("ОтображатьВсеДоступныеВидыОбмена", ОтображатьВсеДоступныеВидыОбмена);
	КонецЕсли;
	
	Если Параметры.Свойство("НеВыгружатьСведенияОКомнатахДляЖилыхДомов") Тогда
		Параметры.Свойство("НеВыгружатьСведенияОКомнатахДляЖилыхДомов", НеВыгружатьСведенияОКомнатахДляЖилыхДомов);
	КонецЕсли;
	
	Если Параметры.Свойство("КаталогВыгрузки") Тогда
		Параметры.Свойство("КаталогВыгрузки", КаталогВыгрузки);
	КонецЕсли;
	
	Если Параметры.Свойство("НастройкаСбораДанныхДляПД") Тогда
		Параметры.Свойство("НастройкаСбораДанныхДляПД", НастройкаСбораДанныхДляПД);
	КонецЕсли;
	
	Если Параметры.Свойство("ЕстьОшибкиЗаполнения") И Параметры.ЕстьОшибкиЗаполнения
		И Параметры.Свойство("СписокНезаполненныхПолейИзФормыНастроек") И Параметры.СписокНезаполненныхПолейИзФормыНастроек.Количество() > 0 Тогда
		Для каждого ТекЭлемент Из Параметры.СписокНезаполненныхПолейИзФормыНастроек Цикл
			Если ТекЭлемент.Значение = "КаталогВыгрузки" Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Не выбран каталог выгрузки.", , "КаталогВыгрузки");
			ИначеЕсли ТекЭлемент.Значение = "НастройкаСбораДанныхДляПД" Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Не выбрана настройка сбора данных для формирования платежных документов.", , "НастройкаСбораДанныхДляПД");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытияЭлементовФормы

&НаКлиенте
// Обработчик события "НачалоВыбора" поля "Каталог выгрузки".
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок                   = "Выбор каталога выгрузки";
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.ИндексФильтра               = 0;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		КаталогВыгрузки = ДиалогВыбораФайла.Каталог;
	КонецЕсли;
	
КонецПроцедуры // КаталогВыгрузкиНачалоВыбора()

&НаКлиенте
// Обработчик события "Открытие" поля "Каталог выгрузки".
Процедура КаталогВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	ЗапуститьПриложение("explorer " + Объект.КаталогВыгрузки);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // КаталогВыгрузкиОткрытие()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик события "КомандаОК".
Процедура КомандаОК(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОтображатьВсеДоступныеВидыОбмена",          ОтображатьВсеДоступныеВидыОбмена);
	СтруктураПараметров.Вставить("НеВыгружатьСведенияОКомнатахДляЖилыхДомов", НеВыгружатьСведенияОКомнатахДляЖилыхДомов);
	СтруктураПараметров.Вставить("КаталогВыгрузки",                           КаталогВыгрузки);
	СтруктураПараметров.Вставить("НастройкаСбораДанныхДляПД",                 НастройкаСбораДанныхДляПД);
	
	УстановитьСпособИдентификацииЛС();
	Закрыть(СтруктураПараметров);
	
КонецПроцедуры // КомандаОК()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Процедура устанавливает значение константы "УПЖКХ_СпособИдентификацииЛСДляГИСЖКХ".
Процедура УстановитьСпособИдентификацииЛС()
	
	Константы.УПЖКХ_СпособИдентификацииЛСДляГИСЖКХ.Установить(СпособИдентификацииЛС);
	
КонецПроцедуры // УстановитьСпособИдентификацииЛС()

&НаКлиенте
// Обработчик события формы "ПриПриОткрытии".
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Элементы.КаталогВыгрузки.Видимость            = Ложь;
		Элементы.КаталогВыгрузки.АвтоОтметкаНезаполненного = Ложь;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти
