
&НаКлиенте
Перем КонтекстЭДОКлиент Экспорт;

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииПослеПолученияКонтекстаЭДО", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииПослеПолученияКонтекстаЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	ДлительнаяОтправкаКлиент.ЗакрытьФормуОжиданияЗагрузкиМодуля();
	
	Оповещение = Новый ОписаниеОповещения(
			"ПриОткрытииЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
			
	ОпределятьГОСТ2001 = Истина;
	МодульДокументооборотСМинобороныКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДокументооборотСМинобороныКлиент");
	МодульДокументооборотСМинобороныКлиент.ОпределитьУстановленныеКрипторовайдеры(Оповещение, ОпределятьГОСТ2001);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(РезультатОпределенияКриптопровайдеров, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатОпределенияКриптопровайдеров.Выполнено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Ошибка при получении списка установленных криптопровайдеров.'"));
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаVipNet.Видимость = Ложь;
	Элементы.ГруппаCryptoPro.Видимость = Ложь;
	
	МодульДокументооборотСМинобороныКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ДокументооборотСМинобороныКлиентСервер");
	АлгоритмГОСТ2001 = МодульДокументооборотСМинобороныКлиентСервер.АлгоритмГОСТ2001();
	КриптопровайдерКриптоПро = МодульДокументооборотСМинобороныКлиентСервер.КриптопровайдерCryptoPro(АлгоритмГОСТ2001);
	КриптопровайдерВипнет = МодульДокументооборотСМинобороныКлиентСервер.КриптопровайдерViPNet(АлгоритмГОСТ2001);
	
	Криптопровайдеры = РезультатОпределенияКриптопровайдеров.УстановленныеКриптопровайдеры;
	Если Криптопровайдеры.Количество() > 0 Тогда
		Для каждого КриптоПровайдер Из Криптопровайдеры Цикл
			Если Криптопровайдер.Имя = КриптопровайдерКриптоПро.Имя Тогда
				Элементы.ГруппаCryptoPro.Видимость = Истина;
			КонецЕсли;
			Если Криптопровайдер.Имя = КриптопровайдерВипнет.Имя Тогда
				Элементы.ГруппаVipNet.Видимость = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкачатьViPNet(Команда)
	 
	Оповещение = Новый ОписаниеОповещения("СкачатьViPNetПослеУстановки", ЭтотОбъект);
	ОбщегоНазначенияЭДКОКлиент.УстановитьViPNetCSP(Оповещение, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СкачатьCryptoPro(Команда)
	
	Оповещение = Новый ОписаниеОповещения("СкачатьCryptoProПослеУстановки", ЭтотОбъект);
	ОбщегоНазначенияЭДКОКлиент.УстановитьCryptoProCSP(Оповещение, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодсказкаНетCSP3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("http://infotecs.ru/");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаНетCSP5ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("http://www.cryptopro.ru/");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СкачатьViPNetПослеУстановки(Результат, ВходящийКонтекст) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось обновить криптопровайдер.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкачатьCryptoProПослеУстановки(Результат, ВходящийКонтекст) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось обновить криптопровайдер.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти