
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНаОбъект 	= Параметры.СсылкаНаОбъект;
	Заявление 		= Параметры.Заявление;
	
	Параметры.Свойство("ЭтоПодключениеНаправления", ЭтоПодключениеНаправления);
	
	// Проставляем флаг, что заявление обработано и больше о нем напоминать не нужно.
	ОбработкаЗаявленийАбонентаВызовСервера.ОбновитьРеквизитыЗаявления(
		Заявление, Новый Структура("НастройкаЗавершена", Истина));
		
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пропустить(Команда)
	Закрыть("Пропустить");
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьНовоеЗаявление(Команда)
	Закрыть("ПодготовитьНовоеЗаявление");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	РазрешеноПропуститьПредупреждение = Заявление.ТипЗаявления = Перечисления.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение
		И НЕ ЭтоПодключениеНаправления;
	
	НазваниеОбъекта = ДлительнаяОтправкаКлиентСервер.НазваниеОбъектаВРодительномПадеже(СсылкаНаОбъект);
	
	Если РазрешеноПропуститьПредупреждение Тогда
		ШаблонЗаголовка = НСтр("ru = 'Перед отправкой <отчета> ознакомьтесь с результатами обработки заявления'");
		Элементы.ЧтоДелать.Заголовок = НСтр("ru = 'Вы можете подготовить исправленное заявление сейчас либо сделать это позже.'");
	Иначе
		ШаблонЗаголовка = НСтр("ru = 'Отправка <отчета> не может быть продолжена'");
		Элементы.ЧтоДелать.Заголовок = НСтр("ru = 'Для подключения к 1С-Отчетности устраните проблему и отправьте новое заявление.'");
	КонецЕсли;
	
	Заголовок = СтрЗаменить(ШаблонЗаголовка, "<отчета>", НазваниеОбъекта);
	
	Элементы.ПричинаОтклонения.Заголовок = Заявление.СтатусКомментарий;
		
	Элементы.ФормаПропустить.Видимость = РазрешеноПропуститьПредупреждение;
	Элементы.ФормаЗакрыть.Видимость = НЕ РазрешеноПропуститьПредупреждение;
	Элементы.ФормаПодготовитьНовоеЗаявление.КнопкаПоУмолчанию = НЕ РазрешеноПропуститьПредупреждение;
	
	// Представление заявления
	КонтекстЭДОСервер  =  ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.УстановитьПредставлениеЗаявленияВВидеФорматированнойСтроки(Заявление, Элементы.ЗаявлениеОтклонено);
	
КонецПроцедуры

#КонецОбласти