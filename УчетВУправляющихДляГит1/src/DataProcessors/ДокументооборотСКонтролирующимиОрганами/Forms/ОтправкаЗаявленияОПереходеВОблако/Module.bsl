&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Организация   = Параметры.Организация;
	УчетнаяЗапись = Организация.УчетнаяЗаписьОбмена;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	ДополнительныеРеквизиты = КонтекстЭДОСервер.ДополнительныеРеквизитыУчетнойЗаписи(УчетнаяЗапись);
	
	УправлениеФормой();
	
	ТелефонМобильный = ДополнительныеРеквизиты.ТелефонМобильный;
	ТелефонМобильный = ЭлектроннаяПодписьВМоделиСервисаКлиентСервер.ПолучитьПредставлениеТелефона(ТелефонМобильный);
	ЭлектроннаяПочта = ДополнительныеРеквизиты.ЭлектроннаяПочта;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Успешная отправка заявления на переход" Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОтправитьЗаявлениеЗавершение", 
			ЭтотОбъект);
			
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ОписаниеОповещения);
		ДополнительныеПараметры.Вставить("РезультатОтправки", 	  Истина);
			
		Оповестить("Длительная отправка. Выполняемое оповещение", ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Шаг1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"Шаг1ОбработкаНавигационнойСсылкиЗавершение", 
		ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ТелефонМобильный", ТелефонМобильный);
	ДополнительныеПараметры.Вставить("ЭлектроннаяПочта", ЭлектроннаяПочта);
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ПервичнаяНастройкаПолученияВременныхПаролей",
		ДополнительныеПараметры,
		,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьЗаявление(Команда)
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторПроверкиТелефонаДляПаролей) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите и подтвердите номер телефона, на который будут приходить 
                                                                |SMS с паролем для отправки отчетности.'"));
	КонецЕсли;
	
	ДополнительныеПараметры = ДлительнаяОтправкаКлиент.ПараметрыДлительнойОтправкиЗаявления();
	ДополнительныеПараметры.Вставить("Организация", 			Организация);
	ДополнительныеПараметры.Вставить("ЭтоПереходВКоробку", 		Ложь);
	ДополнительныеПараметры.Вставить("ТипКриптопровайдера", 	Неопределено);
	ДополнительныеПараметры.Вставить("ИдентификаторПроверкиТелефонаДляПаролей", 		ИдентификаторПроверкиТелефонаДляПаролей);
	ДополнительныеПараметры.Вставить("ИдентификаторПроверкиЭлектроннойПочтыДляПаролей", ИдентификаторПроверкиЭлектроннойПочтыДляПаролей);
	
	КонтекстЭДОКлиент.СформироватьИОтправитьЗаявлениеНаПереход(ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Шаг1ОбработкаНавигационнойСсылкиЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ТелефонМобильный = Результат.ТелефонМобильный;
		ЭлектроннаяПочта = Результат.ЭлектроннаяПочта;
		
		ИдентификаторПроверкиТелефонаДляПаролей = Результат.ИдентификаторПроверкиТелефонаДляПаролей;
		ИдентификаторПроверкиЭлектроннойПочтыДляПаролей = Результат.ИдентификаторПроверкиЭлектроннойПочтыДляПаролей;
		
		УправлениеФормой();
	
	КонецЕсли;
		
КонецПроцедуры
	
&НаСервере
Процедура УстановитьЗаголовок()
	
	ТекстЗаголовка = НСтр("ru = 'Перенос ключа в ""облако"" по %1'");
	Заголовок = СтрШаблон(ТекстЗаголовка, Организация);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	УстановитьЗаголовок();
	
	Подстрока1 = НСтр("ru = 'Номер телефона: '");
	Если НЕ ЗначениеЗаполнено(ИдентификаторПроверкиТелефонаДляПаролей) ИЛИ НЕ ЗначениеЗаполнено(ТелефонМобильный) Тогда
		Подстрока2 = Новый ФорматированнаяСтрока(НСтр("ru = 'не указан'"),,Новый Цвет(225, 40, 40),,"номер телефона");
	Иначе
		Подстрока2 = Новый ФорматированнаяСтрока(ТелефонМобильный,,,,"номер телефона");
	КонецЕсли;
	
	Элементы.Шаг2.Заголовок = Новый ФорматированнаяСтрока(Подстрока1, Подстрока2);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявлениеЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат = Истина Тогда
		Если Открыта() Тогда
			
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("Организация", 		Организация);
			ДополнительныеПараметры.Вставить("ЗаявлениеОтправлено", Истина);
		
			Закрыть(ДополнительныеПараметры);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент = Неопределено Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти