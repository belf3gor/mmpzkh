&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.СообщениеОНеподключенномНаправлении_ПриСозданииНаСервере(ЭтотОбъект);
	
	ЭтоЮрЛицо 		= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЭтоЮрЛицо(Организация);
	УчетнаяЗапись 	= КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
	ПодключенФНС 	= ЗначениеЗаполнено(УчетнаяЗапись) И УчетнаяЗапись.ПредназначенаДляДокументооборотаСФНС;
	СертификатНеДоступенИлиИстек = Параметры.СертификатНеДоступенИлиИстек;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Успешная отправка заявления. Закрыть форму владельца" 
		И Источник = ЗаявлениеАбонента.Ссылка Тогда
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьЗаявление(Команда)
	
	Если ДанныеЗаполнены И НЕ СертификатНеДоступенИлиИстек
		И ВидКонтролирующегоОргана <> ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС") Тогда
		ОтправитьЗаявлениеНаИзменениеВСкрытомРежиме();
	Иначе
		Закрыть(ВидКонтролирующегоОргана);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура УправлениеФормой()
	
	Если ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		
		Если ЭтоЮрЛицо Тогда
			Элементы.НовоеНаправление.Заголовок = НСтр("ru = 'Инспекция ФНС'");
			НовоеНаправление = КодКонтролирующегоОргана;
			ОпределитьКППДляЮрЛица();
		Иначе
			Элементы.НовоеНаправление.Заголовок = НСтр("ru = 'Новое направление'");
			НовоеНаправление = ОрганСтрокой + " " + КодКонтролирующегоОргана;
			Элементы.КПП.Видимость = Ложь;
		КонецЕсли;
			
	Иначе
		
		Элементы.НовоеНаправление.Заголовок = НСтр("ru = 'Новое направление'");
		НовоеНаправление = ОрганСтрокой + " " + КодКонтролирующегоОргана;
		Элементы.КПП.Видимость = Ложь;
		
	КонецЕсли;
	
	ДанныеЗаполнены = ДанныеЗаполненыПолностью();
	Если НЕ ДанныеЗаполнены ИЛИ СертификатНеДоступенИлиИстек Тогда
		
		// Направляем в мастер
		Элементы.ОтправитьЗаявление.Заголовок = НСтр("ru = 'Подготовить заявление'");
		 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеЗаполненыПолностью()
	
	КонтекстЭДОСервер  =  ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	ДанныеЗаполнения 	= КонтекстЭДОСервер.СоздатьЗаявлениеНаИзменениеВСкрытомРежиме_ДанныеЗаполнения(Организация);
	ДанныеОрганизации 	= ДанныеЗаполнения.СтруктураДанныхОрганизации;
	
	Если ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		
		Возврат УдалосьЗаполнитьНастройкиПФР(ЗаявлениеАбонента, ДанныеОрганизации, Истина);
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		
		Возврат УдалосьЗаполнитьНастройкиФСС(ЗаявлениеАбонента, ДанныеОрганизации, Истина);
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСРАР Тогда
		
		Возврат УдалосьЗаполнитьНастройкиФСРАР(ЗаявлениеАбонента, ДанныеОрганизации, ДанныеЗаполнения, Истина);
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСГС Тогда
		
		Возврат ЗначениеЗаполнено(КодКонтролирующегоОргана);
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОпределитьКППДляЮрЛица()
	
	СведенияОбОрганизации 	= РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, , "КППЮЛ");
	КППВОрганизации 		= СведенияОбОрганизации.КППЮЛ;
	
	ВсеКПП = РегистрацииВИФНСПоОрганизацииИКодуНО(Организация, КодКонтролирующегоОргана);
	
	Если ТипЗнч(СсылкаНаОбъект)  = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		// В отчете может быть не заполнен КПП, тогда берется тот, который указан в организации.
		КПП = СсылкаНаОбъект.КПП;
		Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект)  = Тип("ДокументСсылка.УведомлениеОПолучателеДокументов") Тогда
		
		КПП = СсылкаНаОбъект.КПППолучателяДокументов;
		Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		
		КПП = СсылкаНаОбъект.РегистрацияВИФНС.КПП;
		Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
		
		КПП = СсылкаНаОбъект.Получатель.КПП;
		Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
		
	Иначе
		
		Если ВсеКПП.Количество() > 1 Тогда
			
			Элементы.КПП.СписокВыбора.ЗагрузитьЗначения(ВсеКПП);
			Элементы.КПП.СписокВыбора.Добавить(КППВОрганизации);
			
			// Чтобы список выбора не был активным, фокус - на кнопку
			ТекущийЭлемент = Элементы.ОтправитьЗаявление;
			
		Иначе
			Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
		КонецЕсли;
		
		КПП = КППВОрганизации;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КПП) Тогда
		КПП = КППВОрганизации;
		Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КПП) Тогда
		КПП = НСтр("ru = '<не заполнен>'");
		Элементы.КПП.Вид = ВидПоляФормы.ПолеНадписи;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РегистрацииВИФНСПоОрганизацииИКодуНО(Организация, КодНО = Неопределено)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	РегистрацияВИФНС.КПП КАК КПП,
	                      |	РегистрацияВИФНС.Код КАК Код
	                      |ИЗ
	                      |	Справочник.РегистрацииВНалоговомОргане КАК РегистрацияВИФНС
	                      |ГДЕ
	                      |	(РегистрацияВИФНС.Владелец = &Организация
	                      |			ИЛИ РегистрацияВИФНС.Владелец = &ГоловнаяОрганизация)
	                      |	И (РегистрацияВИФНС.Код = &Код
	                      |				И &КодЗадан
	                      |			ИЛИ НЕ &КодЗадан)
	                      |	И РегистрацияВИФНС.ПометкаУдаления = ЛОЖЬ");
						  
	Запрос.УстановитьПараметр("Организация", 		 Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", РегламентированнаяОтчетность.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("Код", 				 КодНО);
	Запрос.УстановитьПараметр("КодЗадан", 			 КодНО <> Неопределено);
	
	ВсеКПП = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КПП");
	
	Возврат ВсеКПП;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьЗаявлениеНаИзменениеВСкрытомРежиме()
	
	ДополнительныеПараметры = ДлительнаяОтправкаКлиент.ПараметрыДлительнойОтправкиЗаявления();
	ДополнительныеПараметры.Вставить("Организация", Организация);
	
	Если НЕ ДлительнаяОтправкаКлиент.ПоказатьФормуДлительнойОтправкиЗаявления(ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОтправитьЗаявлениеНаИзменение", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтправитьЗаявлениеНаИзменение() Экспорт

	УдалосьСоздать = СоздатьНовоеЗаявлениеСНовымНаправлением();
	
	Если НЕ УдалосьСоздать Тогда
		ДлительнаяОтправкаКлиент.ОповеститьОНеудачнойОтправкеЗаявления();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СообщитьРезультатОтправки", 
		ЭтотОбъект);
		
	ИдентификаторАбонента = КонтекстЭДОКлиент.ИдентификаторАбонентаПоОрганизации(Организация);
	
	Контекст = КонтекстЭДОКлиент.ПараметрыПроцедурыСформироватьИОтправитьЗаявление();
	Контекст.ДокументЗаявление 							= ЗаявлениеАбонента;
	Контекст.ИдентификаторАбонента 						= ИдентификаторАбонента;
	Контекст.ВызовИзМастераПодключенияК1СОтчетности 	= Истина;
	Контекст.ВыполняемоеОповещение 						= ОписаниеОповещения;
	Контекст.ФормироватьЗакрытыйКлючИЗапросНаСертификат = Ложь;
	
	КонтекстЭДОКлиент.СформироватьИОтправитьЗаявление(Контекст);
	
КонецПроцедуры
	
&НаСервере
Функция СоздатьНовоеЗаявлениеСНовымНаправлением()
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	НовыйДокументЗаявление = КонтекстЭДОСервер.СоздатьЗаявлениеНаИзменениеВСкрытомРежиме(Организация);  
	Если НовыйДокументЗаявление = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ УдалосьДобавитьНовоеНаправление(НовыйДокументЗаявление) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		НовыйДокументЗаявление.Записать();
	Исключение
		ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ИнформацияОбОшибке().Описание);
		Возврат Ложь;
	КонецПопытки;
	
	ЗначениеВРеквизитФормы(НовыйДокументЗаявление, "ЗаявлениеАбонента");
	
	Возврат Истина;

КонецФункции

&НаСервере
Функция УдалосьДобавитьНовоеНаправление(НовыйДокументЗаявление)
	
	// см.СоздатьЗаявлениеНаИзменениеВСкрытомРежиме_ОпределитьНаправленияСдачиОтчетности
	НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Очистить();
	
	КонтекстЭДОСервер  =  ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	ДанныеЗаполнения 	= КонтекстЭДОСервер.СоздатьЗаявлениеНаИзменениеВСкрытомРежиме_ДанныеЗаполнения(Организация);
	ДанныеОрганизации 	= ДанныеЗаполнения.СтруктураДанныхОрганизации;
	
	ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.ПустаяСсылка();
	ТипПолучателя		= Перечисления.ТипыКонтролирующихОрганов.ПустаяСсылка();
	КодПолучателя 		= "";
	
	Если ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВФНС;
		ТипПолучателя 		= Перечисления.ТипыКонтролирующихОрганов.ФНС;
		КодПолучателя 		= КодКонтролирующегоОргана;
		
		Если ЭтоЮрЛицо Тогда
			Если НЕ ЗначениеЗаполнено(КПП) Тогда
				
				СведенияОбОрганизации 	= РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, , "КППЮЛ");
				КПП = СведенияОбОрганизации.КППЮЛ;
				
				Если НЕ ЗначениеЗаполнено(КПП) Тогда
					ТекстОшибки = НСтр("ru = 'Заполните КПП в настройках организации и повторите отправку'");
					ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
					Возврат Ложь;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		// Добавляем отдельно
		НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
		НоваяСтрока.ИзмененныйРеквизит = Перечисления.ПараметрыПодключенияК1СОтчетности.КодыФНС;
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВПФР;
		ТипПолучателя 		= Перечисления.ТипыКонтролирующихОрганов.ПФР;
		КодПолучателя 		= КодКонтролирующегоОргана;
		
		Если НЕ УдалосьЗаполнитьНастройкиПФР(НовыйДокументЗаявление, ДанныеОрганизации) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВФСС;
		ТипПолучателя 		= Перечисления.ТипыКонтролирующихОрганов.ФСС;
		
		Если НЕ УдалосьЗаполнитьНастройкиФСС(НовыйДокументЗаявление, ДанныеОрганизации) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСГС Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВРосстат;
		ТипПолучателя 		= Перечисления.ТипыКонтролирующихОрганов.ФСГС;
		КодПолучателя 		= КодКонтролирующегоОргана;
		
		// Добавляем отдельно
		НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
		НоваяСтрока.ИзмененныйРеквизит = Перечисления.ПараметрыПодключенияК1СОтчетности.КодРосстата;
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФСРАР Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВФСРАР;
		НовыйДокументЗаявление.ПодатьЗаявкуНаСертификатДляФСРАР = Истина;
		
		Если НЕ УдалосьЗаполнитьНастройкиФСРАР(НовыйДокументЗаявление, ДанныеОрганизации, ДанныеЗаполнения) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.РПН Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВРПН;
		НовыйДокументЗаявление.ПодатьЗаявкуНаПодключениеРПН = Истина;
		
	ИначеЕсли ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФТС Тогда
		
		ИзмененныйРеквизит 	= Перечисления.ПараметрыПодключенияК1СОтчетности.СдаватьВФТС;
		НовыйДокументЗаявление.ПодатьЗаявкуНаПодключениеФТС = Истина;
		
	КонецЕсли;
	
	// Изменившиеся реквизиты
	
	НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
	НоваяСтрока.ИзмененныйРеквизит = ИзмененныйРеквизит;

	// Сам изменившийся реквизит
	Если ВидКонтролирующегоОргана <> Перечисления.ТипыКонтролирующихОрганов.ФСРАР
		И ВидКонтролирующегоОргана <> Перечисления.ТипыКонтролирующихОрганов.РПН
		И ВидКонтролирующегоОргана <> Перечисления.ТипыКонтролирующихОрганов.ФТС Тогда
		
		НоваяСтрокаНаправления = НовыйДокументЗаявление.Получатели.Добавить();
		НоваяСтрокаНаправления.ТипПолучателя = ТипПолучателя;
		НоваяСтрокаНаправления.КодПолучателя = КодПолучателя;
		НоваяСтрокаНаправления.КПП 			 = КПП;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УдалосьЗаполнитьНастройкиФСРАР(НовыйДокументЗаявление, ДанныеОрганизации, ДанныеЗаполнения, ТолькоПроверка = Ложь)
	
	Если СтрокаПустая(НовыйДокументЗаявление.КодРегионаФСРАР) Тогда
		
		АдрЮР 			= ДанныеЗаполнения.АдресЮридический;
		КодРегионаФСРАР = РегламентированнаяОтчетностьКлиентСервер.РазложитьАдрес(АдрЮр).Регион;
		
		ЭтоЮридическоеЛицо	= ДанныеОрганизации.ТипОрганизации;
		
		Если НЕ ЗначениеЗаполнено(КодРегионаФСРАР) Тогда
			
			Если НЕ ТолькоПроверка Тогда
				
				Если ЭтоЮридическоеЛицо Тогда
					ТекстОшибки = НСтр("ru = 'Заполните регион в юридическом адресе в настройках организации'");
				Иначе
					ТекстОшибки = НСтр("ru = 'Заполните регион в адресе места жительства в настройках организации'");
				КонецЕсли;
					
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
				
			КонецЕсли;
			
			Возврат Ложь;
			
		КонецЕсли;
		
		НовыйДокументЗаявление.КодРегионаФСРАР = КодРегионаФСРАР;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УдалосьЗаполнитьНастройкиПФР(НовыйДокументЗаявление, ДанныеОрганизации, ТолькоПроверка = Ложь)
	
	Если СтрокаПустая(НовыйДокументЗаявление.РегНомерПФР) ИЛИ ТолькоПроверка Тогда
		
		РегНомерПФР = ДанныеОрганизации.РегНомПФР;

		// регистрационный номер в ПФР
		РегНомерПФР 	  = СокрЛП(РегНомерПФР);
		РегНомерПФРПустой = СтрокаПустая(РегНомерПФР);
		
		Если РегНомерПФРПустой Тогда 
			
			Если НЕ ТолькоПроверка Тогда
				ТекстОшибки = НСтр("ru = 'Заполните регистрационный номер в ПФР в настройках организации'");
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
			КонецЕсли;
			
			Возврат Ложь;
			
		ИначеЕсли НЕ ДокументооборотСКОКлиентСервер.ПроверитьРегистрационныйНомерПФР(РегНомерПФР, Истина) Тогда
			
			Если НЕ ТолькоПроверка Тогда
				ТекстОшибки = НСтр("ru = 'Регистрационный номер в ПФР в настройках организации должен состоять из 12 цифр (ХХХ-ХХХ-ХХХХХХ)'");
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
			КонецЕсли;
			
			Возврат Ложь;
			
		КонецЕсли;
		
		НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
		НоваяСтрока.ИзмененныйРеквизит = Перечисления.ПараметрыПодключенияК1СОтчетности.РегНомерПФР;
		
		НовыйДокументЗаявление.РегНомерПФР = РегНомерПФР;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УдалосьЗаполнитьНастройкиФСС(НовыйДокументЗаявление, ДанныеОрганизации, ТолькоПроверка = Ложь)
	
	РегНомерФСС			= ДанныеОрганизации.РегистрационныйНомерФСС;
	ЭтоЮридическоеЛицо	= ДанныеОрганизации.ТипОрганизации;

	Если ЭтоЮридическоеЛицо Тогда
		ПризнакОбособленногоПодразделения = ДанныеОрганизации.ПризнакОбособленногоПодразделения;
	Иначе
		ПризнакОбособленногоПодразделения = Ложь;
	КонецЕсли;
	
	Если ПризнакОбособленногоПодразделения Тогда
		ДополнительныйКодФСС = ДанныеОрганизации.РеквизитДопКодФСС;
	Иначе
		ДополнительныйКодФСС = "";
	КонецЕсли;
	
	Если СтрокаПустая(НовыйДокументЗаявление.РегНомерФСС) Тогда
		
		// регистрационный номер в ФСС
		Если ПустаяСтрока(РегНомерФСС) Тогда 
			
			Если НЕ ТолькоПроверка Тогда
				ТекстОшибки = НСтр("ru = 'Заполните регистрационный номер в ФСС в настройках организации'");
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
			КонецЕсли;
			
			Возврат Ложь;
			
		ИначеЕсли НЕ ДокументооборотСКОКлиентСервер.ПроверитьЦифровойКодЗаданнойДлины(РегНомерФСС, 10, Истина) Тогда
			
			Если НЕ ТолькоПроверка Тогда
				ТекстОшибки = НСтр("ru = 'Регистрационный номер в ФСС в настройках организации должен состоять из 10 цифр'");
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
			КонецЕсли;
			
			Возврат Ложь;

		КонецЕсли;

		НовыйДокументЗаявление.РегНомерФСС = РегНомерФСС;
		
		НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
		НоваяСтрока.ИзмененныйРеквизит = Перечисления.ПараметрыПодключенияК1СОтчетности.РегНомерФСС;
		
	КонецЕсли;
	
	Если СтрокаПустая(НовыйДокументЗаявление.ДополнительныйКодФСС) Тогда
		
		// дополнительный код ФСС
		Если ПризнакОбособленногоПодразделения И ЭтоЮридическоеЛицо Тогда
			
			Если ПустаяСтрока(ДополнительныйКодФСС) Тогда 
				
				Если НЕ ТолькоПроверка Тогда
					ТекстОшибки = НСтр("ru = 'Заполните дополнительный код ФСС в настройках организации'");
					ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
				КонецЕсли;
				
				Возврат Ложь;
				
			ИначеЕсли НЕ ДокументооборотСКОКлиентСервер.ПроверитьЦифровойКодЗаданнойДлины(ДополнительныйКодФСС, 10, Истина) Тогда
				
				Если НЕ ТолькоПроверка Тогда
					ТекстОшибки = НСтр("ru = 'Дополнительный код ФСС  в настройках организации должен состоять из 10 цифр'");
					ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
				КонецЕсли;
				
				Возврат Ложь;
				
			КонецЕсли;
			
			НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
			НоваяСтрока.ИзмененныйРеквизит = Перечисления.ПараметрыПодключенияК1СОтчетности.ДополнительныйКодФСС;
			
			НовыйДокументЗаявление.ДополнительныйКодФСС	= ДополнительныйКодФСС;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СтрокаПустая(ПроверяемаяСтрока)
	
	Возврат ПустаяСтрока(СтрЗаменить(ПроверяемаяСтрока,"-",""));
	
КонецФункции

&НаКлиенте
Процедура СообщитьРезультатОтправки(Результат, ВходящийКонтекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура")
		И (Результат.Свойство("ТекстОшибки") И ЗначениеЗаполнено(Результат.ТекстОшибки) 
		ИЛИ Результат.Свойство("ОписаниеОшибки") И ЗначениеЗаполнено(Результат.ОписаниеОшибки)) Тогда
		
		Если Результат.Свойство("ТекстОшибки") Тогда 
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(Результат.ТекстОшибки);
		Иначе
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(Результат.ОписаниеОшибки);
		КонецЕсли;
			
		ДлительнаяОтправкаКлиент.ОповеститьОНеудачнойОтправкеЗаявления();
		
	Иначе
		
		ДлительнаяОтправкаКлиент.ОповеститьОбУдачнойОтправкеЗаявления(Организация, ЗаявлениеАбонента.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент = Неопределено Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти