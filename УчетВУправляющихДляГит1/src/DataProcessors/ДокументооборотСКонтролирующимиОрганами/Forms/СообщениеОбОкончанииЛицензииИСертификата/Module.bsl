
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ЛицензияДатаОкончания, ЛицензияПросрочена, СертификатДатаОкончания, СертификатПросрочен, СсылкаНаОбъект");
	
	КонтекстЭДОСервер        = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	СведенияПоОбъекту        = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(СсылкаНаОбъект);
	Организация 			 = СведенияПоОбъекту.Организация;
	ВидКонтролирующегоОргана = СведенияПоОбъекту.ВидКонтролирующегоОргана;
	ОрганСтрокой 			 = ВидКонтролирующегоОргана;
	Заголовок                = КонтекстЭДОСервер.СообщениеОНеподключенномНаправлении_ЗаголовокФормы(СсылкаНаОбъект, ОрганСтрокой);
	
	ПоддерживаетсяВторичноеЗаявление = КонтекстЭДОСервер.ПоддерживаетсяВторичноеЗаявление(Организация);
	
	Элементы.ЧтоДелать.Заголовок = ЧтоДелать();
	
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		Элементы.ФормаОтправитьЗаявление.Видимость = Ложь;
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьЗаявление(Команда)
	Закрыть(Организация);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЧтоДелать()
	
	Возврат Новый ФорматированнаяСтрока(
		Предложение1(), 
		Предложение2(),
		Предложение3(),
		Предложение4());

КонецФункции

&НаСервере
Функция Предложение1()
	
	Предложение1 = НСтр("ru = 'Для отправки %1 необходимо продлить %2 на 1С-Отчетность.'");
	
	Текст1 = ДлительнаяОтправкаКлиентСервер.НазваниеОбъектаВРодительномПадеже(СсылкаНаОбъект);
	
	Текст2 = "";
	Если ЛицензияПросрочена И СертификатПросрочен Тогда 
		Текст2 = НСтр("ru = 'лицензию и сертификат'");
	ИначеЕсли ЛицензияПросрочена Тогда
		Текст2 = НСтр("ru = 'лицензию'");
	ИначеЕсли СертификатПросрочен Тогда
		Текст2 = НСтр("ru = 'сертификат'");
	КонецЕсли;
	
	Предложение1 = СтрШаблон(Предложение1, Текст1, Текст2);
	
	Возврат Предложение1 + Символы.ПС;
	
КонецФункции

&НаСервере
Функция Предложение2()
	
	Если ПоддерживаетсяВторичноеЗаявление Тогда
		Предложение2 = НСтр("ru = 'Для этого отправьте заявление на продление.'");
	Иначе
		Предложение2 = НСтр("ru = 'Для этого обратитесь к вашему оператору связи.'");
	КонецЕсли;
	
	Возврат Предложение2 + Символы.ПС + Символы.ПС;
	
КонецФункции

&НаСервере
Функция Предложение3()
	
	Предложение3 = "";
	Если ЛицензияПросрочена Тогда
		
		Предложение3 = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Лицензия истекла: '"),
			Новый ФорматированнаяСтрока(Строка(ЛицензияДатаОкончания), Новый Шрифт(,,Истина)),
			Символы.ПС);
			
	КонецЕсли;
	
	Возврат Предложение3;
	
КонецФункции

&НаСервере
Функция Предложение4()
	
	Предложение4 = "";
	Если СертификатПросрочен Тогда
		
		Предложение4 = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Сертификат истек:  '"),
			Новый ФорматированнаяСтрока(Строка(СертификатДатаОкончания), Новый Шрифт(,,Истина)));
			
	КонецЕсли;
	
	Возврат Предложение4;
	
КонецФункции

#КонецОбласти

