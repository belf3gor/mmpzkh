&НаКлиенте
Перем КонтекстЭДОКлиент;

&НаКлиенте
Перем ВыполняемоеОповещение;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаменитьБубликНаДинамичный();
	
	Ошибки = Новый ФиксированныйМассив(Новый Массив);
	
	ИспользуетсяОднаОрганизация = РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация();
	
	ОбработатьВходныеПараметры(Параметры);
	
	Элементы.ФормаЗакрыть.Видимость = Ложь;
	Элементы.Показать.Видимость 	= Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДлительнаяОтправкаКлиент.ИзменитьПараметрыДлительнойОтправкиКлиентСервер("ИдентификаторПолучателя", ИдентификаторПолучателя);
	Если ЗначениеЗаполнено(ТекущаяОрганизация) Тогда
		УстановитьТекущуюОрганизацию(ТекущаяОрганизация);
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РазборОбщихПараметровОповещения(ИмяСобытия, Параметр, Источник);
	
	Если ЭтоОбмен Тогда
		ОбработкаОповещенийОбновления(ИмяСобытия, Параметр, Источник);
	ИначеЕсли ЭтоОбновлениеМодуля Тогда
		ОбработкаОповещенийОбновленияМодуля(ИмяСобытия, Параметр, Источник);
	ИначеЕсли ЭтоОтправкаЗаявления Тогда
		ОбработкаОповещенийОтправкиЗаявления(ИмяСобытия, Параметр, Источник);
	Иначе
		ОбработкаОповещенийОтправки(ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Предотвращаем ручное закрытие формы длительной операции,
	// поскольку даже при закрытии формы отправка бы продолжилась.
	Если РазрешитьЗакрытиеФормы Тогда
		// Сброс параметров - это очистка списка ошибок на клиенте и сервере и очистка идентификатора
		// формы-получателя ошибок.
		Если НЕ СбросПараметровВыполнен Тогда
			Отказ = Истина;
			
			ОпределитьНеобходимостьЗакрытияБезДальнейшихДействий();
			
			ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьФорму", 0.1, Истина);
		КонецЕсли;
		
		Если ЭтоОтправкаЗаявления И НЕ ЕстьОшибки Тогда
			Оповестить("Успешная отправка заявления. Закрыть форму владельца", , Заявление);
		КонецЕсли;
		
	Иначе
		
		ПолучитьВсеОшибки();
		РазрешитьЗакрытиеФормы 		 = Истина;
		ЗакрытьБезДальнейшихДействий = Истина;
		Подключаемый_ЗакрытьФорму();
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)

	ОпределитьНеобходимостьЗакрытияБезДальнейшихДействий();
	
	// Пользователь нажал "Закрыть" вместо "Показать протокол", значит протокол ему не нужен.
	Если ЗначениеЗаполнено(ПротоколНесданогоОтчета) Тогда
		ПротоколНесданогоОтчета = Неопределено;
	КонецЕсли;
	
	Подключаемый_ЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура Показать(Команда)
	
	Подключаемый_ЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыводНадписей

&НаКлиенте
Функция ТекстПроНовыеИОшибкиПриРасшифровке(Параметр) Экспорт
	
	КоличествоНеРасшифрованных 	= Параметр.КоличествоНеРасшифрованных;
	КоличествоРасшифрованных 	= Параметр.КоличествоРасшифрованных;
	ВсегоСообщений 				= Параметр.ВсегоСообщений;
	
	РасшифрованыВсе 		= КоличествоРасшифрованных = ВсегоСообщений;
	РасшифрованаЧасть 		= КоличествоРасшифрованных <> ВсегоСообщений И КоличествоРасшифрованных > 0;
	НиОдноНеРасшифровано 	= КоличествоРасшифрованных = 0;
	
	ТекстПроНовыеСообщения = НСтр("ru = 'Получены новые сообщения от контролирующих органов.'");
	ТекстСообщенияГотовыКПросмотру = НСтр("ru = 'Сообщения готовы к просмотру.'");
	
	Если ЕстьНовые И ЕстьОшибки Тогда
		
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.РасшифрованныеСообщения;
		
		Если ЭтоАвторасшифровка Тогда
			ОсновнойТекст = ТекстПроНовыеСообщения;
		Иначе
			ОсновнойТекст = ТекстСообщенияГотовыКПросмотру;
		КонецЕсли;
		
		Если РасшифрованыВсе Тогда
		
			ПоясняющийТекст = Новый ФорматированнаяСтрока(
				ОсновнойТекст,
				Символы.ПС,
				Символы.ПС,
				СтрокаКрасным(НСтр("ru = 'Обратите внимание, при расшифровке и формировании квитанций возникли ошибки.'")));
		
		Иначе
			
			ПоясняющийТекст = Новый ФорматированнаяСтрока(
				ОсновнойТекст,
				Символы.ПС,
				Символы.ПС,
				СтрокаКрасным(НСтр("ru = 'Обратите внимание, часть сообщений расшифровать не удалось.'")));
				
		КонецЕсли;
			
	ИначеЕсли ЕстьНовые И НЕ ЕстьОшибки Тогда
		
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.РасшифрованныеСообщения;
		
		Если ЭтоАвторасшифровка Тогда
			ОсновнойТекст = ТекстПроНовыеСообщения;
		Иначе
			ОсновнойТекст = ТекстСообщенияГотовыКПросмотру;
		КонецЕсли;
		
		ПоясняющийТекст = Новый ФорматированнаяСтрока(ОсновнойТекст);
		
	ИначеЕсли НЕ ЕстьНовые И ЕстьОшибки Тогда
		
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ОшибкаОтправки;
		
		Если РасшифрованыВсе Тогда
			
			Если ЭтоАвторасшифровка Тогда
				ОсновнойТекст = ТекстПроНовыеСообщения;
			Иначе
				ОсновнойТекст = НСтр("ru = 'Все сообщения расшифрованы.'");
			КонецЕсли;
		
			ПоясняющийТекст = Новый ФорматированнаяСтрока(
				ОсновнойТекст,
				Символы.ПС,
				Символы.ПС,
				СтрокаКрасным(НСтр("ru = 'Обратите внимание, при расшифровке и формировании квитанций возникли ошибки.'")));
				
		ИначеЕсли РасшифрованаЧасть Тогда
				
			КрасныйТекстНекоторыеСообщенияРасшифроватьНеУдалось = СтрокаКрасным(НСтр("ru = 'Некоторые сообщения расшифровать не удалось.'"));
		
			Если ЭтоАвторасшифровка Тогда
				ПоясняющийТекст = Новый ФорматированнаяСтрока(
					ТекстПроНовыеСообщения,
					Символы.ПС,
					Символы.ПС,
					КрасныйТекстНекоторыеСообщенияРасшифроватьНеУдалось);
			Иначе
				ПоясняющийТекст = КрасныйТекстНекоторыеСообщенияРасшифроватьНеУдалось;
			КонецЕсли;
			
		ИначеЕсли НиОдноНеРасшифровано Тогда 
			
			КрасныйТекстНиОдноНеРасшифровано = СтрокаКрасным(НСтр("ru = 'Обратите внимание, при расшифровке и формировании квитанций возникли ошибки.'"));
			
			Если ЭтоАвторасшифровка Тогда
				ПоясняющийТекст = Новый ФорматированнаяСтрока(
					ТекстПроНовыеСообщения,
					Символы.ПС,
					Символы.ПС,
					КрасныйТекстНиОдноНеРасшифровано);
			Иначе
				ПоясняющийТекст = КрасныйТекстНиОдноНеРасшифровано;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли НЕ ЕстьНовые И НЕ ЕстьОшибки Тогда
		
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
		
		Если РасшифрованыВсе Тогда
			 ПоясняющийТекст = Новый ФорматированнаяСтрока(НСтр("ru = 'Все сообщения расшифрованы.'"));
		Иначе
			 ПоясняющийТекст = Новый ФорматированнаяСтрока(НСтр("ru = 'Некоторые сообщения не расшифрованы.'"));
		КонецЕсли; 
		
		Если ЭтоАвторасшифровка Тогда
			ПоясняющийТекст = Новый ФорматированнаяСтрока(
				ТекстПроНовыеСообщения,
				Символы.ПС,
				Символы.ПС,
				ПоясняющийТекст);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПоясняющийТекст;
	
КонецФункции

&НаКлиенте
Функция ТекстПроНовыеИОшибкиПриОбновлении() Экспорт
	
	// Новые сообщения.
	ТекстПроНовыеСообщения = НСтр("ru = 'Получены новые сообщения от контролирующих органов.'");
	ТекстПроОтсуствиеНовыхСообщений = НСтр("ru = 'Новых сообщений от контролирующих органов нет.'");
	
	// Ошибки (красным цветом).
	ТекстПроОшибки = НСтр("ru = 'Обратите внимание, возникли ошибки при обновлении.'");
	ТекстПроОшибки = СтрокаКрасным(ТекстПроОшибки);
	
	Если НетДоступаВИнтернет Тогда
		
		ТекстПроОтсуствиеИнтернета = НСтр("ru = 'Обновление выполнить не удалось в связи с ошибкой доступа в Интернет'");
		
		Если ЕстьОшибки Тогда
			
			ПоясняющийТекст = Новый ФорматированнаяСтрока(
				ТекстПроОтсуствиеИнтернета,
				Символы.ПС,
				Символы.ПС,
				ТекстПроОшибки);
			
		Иначе
			ПоясняющийТекст = Новый ФорматированнаяСтрока(ТекстПроОтсуствиеИнтернета);
		КонецЕсли;
		
	ИначеЕсли ЕстьНовые И ЕстьОшибки Тогда
		
		ПоясняющийТекст = Новый ФорматированнаяСтрока(
			ТекстПроНовыеСообщения,
			Символы.ПС,
			Символы.ПС,
			ТекстПроОшибки);
			
	ИначеЕсли ЕстьНовые И НЕ ЕстьОшибки Тогда
		
		ПоясняющийТекст = Новый ФорматированнаяСтрока(ТекстПроНовыеСообщения);
		
	ИначеЕсли НЕ ЕстьНовые И ЕстьОшибки Тогда
		
		ПоясняющийТекст = Новый ФорматированнаяСтрока(
			ТекстПроОтсуствиеНовыхСообщений,
			Символы.ПС,
			Символы.ПС,
			ТекстПроОшибки);
		
	Иначе
		
		// Нет новых, нет ошибок.
		ПоясняющийТекст = Новый ФорматированнаяСтрока(ТекстПроОтсуствиеНовыхСообщений);
		
	КонецЕсли;
	
	Возврат ПоясняющийТекст;
	
КонецФункции

&НаСервере
Процедура СформироватьТекстОРезультатеОбменаПоОтчету()
	
	ВидОбъекта = ДлительнаяОтправкаКлиентСервер.ВидОбъекта(ОтчетСсылка);
	
	НовоеСостояниеСдачиОтчетности = Неопределено;
	
	СостояниеОтчета = ТекущийЭтапОтправки(ОтчетСсылка);
	Если СостояниеОтчета <> Неопределено Тогда
		НовоеСостояниеОтчета 			= СостояниеОтчета.ТекстНадписи;
		НовоеСостояниеСдачиОтчетности 	= СостояниеОтчета.СостояниеСдачиОтчетности;
		ПротоколНесданогоОтчета			= СостояниеОтчета.Протокол;
	КонецЕсли;
	
	Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
	ПоясняющийТекст = "";
	
	СостояниеОтчетаИзменилось = ПредыдущееСостояниеОтчета <> НовоеСостояниеОтчета И ЗначениеЗаполнено(НовоеСостояниеОтчета);
	
	Если СостояниеОтчетаИзменилось Тогда
		
		Если ЗначениеЗаполнено(ОтчетСсылка) И (ЭтоОбменИзОтчета ИЛИ ЭтоОбменИзЭтаповОтправки) Тогда
			КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
			КонтекстЭДОСервер.ОтметитьСсылкуПоказаннойПользователю(ОтчетСсылка);
		КонецЕсли;
			
		Если ВидОбъекта.ЭтоОтчет И НовоеСостояниеОтчета = НСтр("ru = 'Сдан частично'") Тогда
			
			Картинка 		= БиблиотекаКартинок.СданоЧастично;
			ПоясняющийТекст = СтрШаблон(НСтр("ru = 'Отчет ""%1"" сдан частично.'"), НаименованиеОтчета);
				
			ПоказатьКнопкуОткрытияПротокола();
			
		ИначеЕсли НовоеСостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ОтрицательныйРезультатДокументооборота Тогда
			
			Картинка = БиблиотекаКартинок.ОшибкаОтправки;
			
			Если НовоеСостояниеОтчета = НСтр("ru = 'Не сдано'") Тогда
				ПоясняющийТекст = СтрШаблон(НСтр("ru = 'Отчет ""%1"" не сдан.'"), НаименованиеОтчета);
			Иначе
				ПоясняющийТекст = НадписьПриИзменившемсяСостоянии(НовоеСостояниеОтчета, СостояниеОтчетаИзменилось);
			КонецЕсли;
			
			ПоказатьКнопкуОткрытияПротокола();
			
		ИначеЕсли НовоеСостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота Тогда
			
			Картинка = БиблиотекаКартинок.УспешнаяОтправка;
			
			Если НовоеСостояниеОтчета = НСтр("ru = 'Сдано'") Тогда
				ПоясняющийТекст = СтрШаблон(НСтр("ru = 'Отчет ""%1"" сдан.'"), НаименованиеОтчета);
			Иначе
				ПоясняющийТекст = НадписьПриИзменившемсяСостоянии(НовоеСостояниеОтчета, СостояниеОтчетаИзменилось);
			КонецЕсли;
			
		Иначе
				
			Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
			ПоясняющийТекст = НадписьПриИзменившемсяСостоянии(НовоеСостояниеОтчета);
			
		КонецЕсли;
	Иначе
		
		// Состояние могло не измениться, а протокол есть.
		// При таком условии при закрытии формы откроется протокол, а это не нужно.
		ПротоколНесданогоОтчета = Неопределено;
		
		Картинка 		= БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
		ПоясняющийТекст = НадписьПриИзменившемсяСостоянии(НовоеСостояниеОтчета, СостояниеОтчетаИзменилось, НеУдалосьОбновитьСтатусОтчета);
		
	КонецЕсли;
	
	Элементы.ДекорацияДлительнаяОперация.Картинка 	= Картинка;
	Элементы.ПоясняющийТекст.Заголовок 				= ПоясняющийТекст;
		
КонецПроцедуры

&НаСервере
Функция НадписьПриИзменившемсяСостоянии(НовоеСостояние, СостояниеИзменилось = Истина, НеУдалосьОбновить = Ложь)
	
	ВидОбъекта = ДлительнаяОтправкаКлиентСервер.ВидОбъекта(ОтчетСсылка);
	
	Если ВидОбъекта.ЭтоПисьмо Тогда
		Представление = НСтр("ru = 'Состояние письма ""%Наименование""'");
	ИначеЕсли ВидОбъекта.ЭтоОтветНаТребование Тогда
		Представление = НСтр("ru = 'Состояние ответа на требование'");
	ИначеЕсли ВидОбъекта.ЭтоСверка Тогда
		Представление = НСтр("ru = 'Состояние запроса на сверку ""%Наименование""'");
	ИначеЕсли ВидОбъекта.ЭтоЕГРЮЛ Тогда
		Представление = НСтр("ru = 'Состояние запроса на выписку из ЕГРЮЛ'");
	ИначеЕсли ВидОбъекта.ЭтоМакетПФР Тогда
		Представление = НСтр("ru = 'Состояние макета пенсионных дел'");
	ИначеЕсли ВидОбъекта.ЭтоЗаявлениеОПенсии Тогда
		Представление = НСтр("ru = 'Состояние заявления о назначении и доставке пенсии'");
	Иначе
		Представление = НСтр("ru = 'Состояние отчета ""%Наименование""'");
	КонецЕсли;
	
	Если СостояниеИзменилось Тогда
		РезультатОбновления = НСтр("ru = 'изменилось на ""%Состояние"".'");
	ИначеЕсли НеУдалосьОбновить Тогда
		РезультатОбновления = НСтр("ru = 'обновить не удалось.'");
	Иначе
		РезультатОбновления = НСтр("ru = 'не изменилось.'");
	КонецЕсли;
	
	ПоясняющийТекст = Представление + " " + РезультатОбновления;
	ПоясняющийТекст = СтрЗаменить(ПоясняющийТекст, "%Наименование", НаименованиеОтчета);
	ПоясняющийТекст = СтрЗаменить(ПоясняющийТекст, "%Состояние", 	НовоеСостояние);
	
	Возврат ПоясняющийТекст;
	
КонецФункции

&НаКлиенте
Процедура ВывестиПоясняющийТекстИЗаголовокПриЗавершеннойОтправке(ЗавершенУспешно = Ложь, ДополнительноеСообщение = "")
	
	ВидОбъекта = ДлительнаяОтправкаКлиентСервер.ВидОбъекта(ОтчетСсылка);
	
	// Заголовок
	Если ЗавершенУспешно Тогда
		Заголовок = НСтр("ru = 'Успешно!'");
	Иначе
		Если ВидОбъекта.ЭтоПисьмо Тогда
			Заголовок = НСтр("ru = 'Письмо не отправлено'");
		ИначеЕсли ВидОбъекта.ЭтоОтветНаТребование Тогда
			Заголовок = НСтр("ru = 'Ответ на требование не отправлен'");
		ИначеЕсли ВидОбъекта.ЭтоСверка Тогда
			Заголовок = НСтр("ru = 'Запрос на сверку не отправлен'");
		ИначеЕсли ВидОбъекта.ЭтоЕГРЮЛ Тогда
			Заголовок = НСтр("ru = 'Запрос на выписку не отправлен'");
		ИначеЕсли ВидОбъекта.ЭтоМакетПФР Тогда
			Заголовок = НСтр("ru = 'Макет пенсионных дел не отправлен'");
		ИначеЕсли ВидОбъекта.ЭтоЗаявлениеОПенсии Тогда
			Заголовок = НСтр("ru = 'Заявление о назначении пенсии не отправлено'");
		Иначе
			Заголовок = НСтр("ru = 'Отчет не отправлен'");
		КонецЕсли;
	КонецЕсли;
	
	// Пояснение
	Если ВидОбъекта.ЭтоПисьмо Тогда
		ПоясняющийТекст = НСтр("ru = 'Письмо ""%Наименование"" %Отрицание отправлено в %Орган'");
	ИначеЕсли ВидОбъекта.ЭтоОтветНаТребование Тогда
		ПоясняющийТекст = НСтр("ru = '%Наименование %Отрицание отправлено в %Орган'");
	ИначеЕсли ВидОбъекта.ЭтоСверка Тогда
		ПоясняющийТекст = НСтр("ru = 'Запрос на сверку ""%Наименование"" %Отрицание отправлен в %Орган'");
	ИначеЕсли ВидОбъекта.ЭтоЕГРЮЛ Тогда
		ПоясняющийТекст = НСтр("ru = 'Запрос на выписку из ЕГРЮЛ %Отрицание отправлен'");
	ИначеЕсли ВидОбъекта.ЭтоМакетПФР Тогда
		ПоясняющийТекст = НСтр("ru = 'Макет пенсионных дел %Отрицание отправлен в %Орган'");
	ИначеЕсли ВидОбъекта.ЭтоЗаявлениеОПенсии Тогда
		ПоясняющийТекст = НСтр("ru = 'Заявление о назначении пенсии %Отрицание отправлено в %Орган'");
	Иначе
		ПоясняющийТекст = НСтр("ru = 'Отчет ""%Наименование"" %Отрицание отправлен в %Орган'");
	КонецЕсли;
	
	Если ЗавершенУспешно Тогда
		Отрицание = "";
	Иначе
		Отрицание = "не";
	КонецЕсли;
	
	// Пояснение
	ПоясняющийТекст = СтрЗаменить(ПоясняющийТекст, "%Наименование", НаименованиеОтчета);
	ПоясняющийТекст = СтрЗаменить(ПоясняющийТекст, "%Отрицание",    Отрицание);
	ПоясняющийТекст = СтрЗаменить(ПоясняющийТекст, "%Орган",        НаименованиеКонтролирующегоОргана);
	
	ПоясняющийТекст = ЗаменитьДвойнойПробел(ПоясняющийТекст);
	
	Если ЗначениеЗаполнено(ДополнительноеСообщение) Тогда
		ПоясняющийТекст = Новый ФорматированнаяСтрока(
			ПоясняющийТекст + ".",
			Символы.ПС,
			Символы.ПС,
			СтрокаКрасным(ДополнительноеСообщение));
	КонецЕсли;
	
	Элементы.ПоясняющийТекст.Заголовок = ПоясняющийТекст;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиПоясняющийТекстПриВыполняющейсяОперации(Форма)
	
	Если Форма.ЭтоОбновлениеМодуля ИЛИ Форма.ЭтоУниверсальноеОжидание Тогда
		Возврат;
	КонецЕсли;
	
	БезПроцентов = Форма.ОбщееКоличествоЭтапов = 0 ИЛИ НЕ Форма.ВыводитьПроценты;
	
	Если БезПроцентов Тогда
		Проценты = 0;
	Иначе
		Проценты = Мин(Макс(Цел(100*(Форма.КоличествоПройденныхЭтапов/Форма.ОбщееКоличествоЭтапов)), 1), 99);
	КонецЕсли;
	
	Если Форма.ЭтоОбмен Тогда
		ТекстНадписи = ПоясняющийТекстПриВыполняющемсяОбновлении(Форма, БезПроцентов, Проценты);
	ИначеЕсли Форма.ЭтоОтправкаЗаявления Тогда
		ТекстНадписи = ПоясняющийТекстПриОтправкеЗаявления(Форма);
	Иначе
		ТекстНадписи = ПоясняющийТекстПриВыполняющейсяОтправке(Форма, БезПроцентов, Проценты);
	КонецЕсли;
		
	Форма.Элементы.ПоясняющийТекст.Заголовок = ТекстНадписи;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоясняющийТекстПриОтправкеЗаявления(Форма)
	
	СоздаватьКлюч = Форма.СоздаватьКлюч;
	
	Если СоздаватьКлюч Тогда 
		// Вызывается из мастера для случая локальной учетки
		ТекстНадписи  = НСтр("ru = 'Выполняется создание закрытого ключа и отправка заявления
			                 |по организации %1 ...'");
	Иначе
		// Вызывается при скрытой отправке заявления без открытия формы мастера,
		// а так же из мастера при ЭП в облаке (когда пользователю не надо создавать ключ)
		ТекстНадписи  = НСтр("ru = 'Выполняется отправка заявления по организации
	                         |%1 ...'");
	КонецЕсли;
	
	ТекстНадписи  = СтрШаблон(ТекстНадписи, Форма.ТекущаяОрганизация);

	Возврат ТекстНадписи;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоясняющийТекстПриВыполняющейсяОтправке(Форма, БезПроцентов, Проценты)
	
	ТекстНадписи = "";
	
	ВидОбъекта = ДлительнаяОтправкаКлиентСервер.ВидОбъекта(Форма.ОтчетСсылка);

	// Пример: Отчет "Закупки спирта" отправляется в ФСРАР (25%)
	Если ВидОбъекта.ЭтоПисьмо Тогда
		ТекстНадписи = НСтр("ru = 'Письмо ""%Наименование"" отправляется в %Орган %Проценты'");
	ИначеЕсли ВидОбъекта.ЭтоОтветНаТребование Тогда
		ТекстНадписи = НСтр("ru = '%Наименование отправляется в %Орган %Проценты'");
	ИначеЕсли ВидОбъекта.ЭтоСверка Тогда
		ТекстНадписи = НСтр("ru = 'Запрос на сверку ""%Наименование"" отправляется в %Орган %Проценты'");
	ИначеЕсли ВидОбъекта.ЭтоЕГРЮЛ Тогда
		ТекстНадписи = НСтр("ru = 'Выполняется отправка запроса на выписку из ЕГРЮЛ %Проценты'");
	ИначеЕсли ВидОбъекта.ЭтоМакетПФР Тогда
		ТекстНадписи = НСтр("ru = 'Макет пенсионных дел отправляется в %Орган %Проценты'");
	ИначеЕсли ВидОбъекта.ЭтоЗаявлениеОПенсии Тогда
		ТекстНадписи = НСтр("ru = 'Заявление о назначении пенсии отправляется в %Орган %Проценты'");
	Иначе
		ТекстНадписи = НСтр("ru = 'Отчет ""%Наименование"" отправляется в %Орган %Проценты'");
	КонецЕсли;
	
	ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Наименование", Форма.НаименованиеОтчета);
	ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Орган",        Форма.НаименованиеКонтролирующегоОргана);
	
	Если БезПроцентов Тогда
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Проценты", "");
	Иначе
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Проценты", "("+ Строка(Проценты) + "%)");
	КонецЕсли;

	Возврат ТекстНадписи;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоясняющийТекстПриВыполняющемсяОбновлении(Форма, БезПроцентов, Проценты)
	
	ТекстНадписи = "";
	Если БезПроцентов Тогда
		
		// Пример: Выполняется обновление по организации Ромашка ...
		ТекстНадписи  = НСтр("ru = 'Выполняется обновление по организации %1 ...'");
		ТекстНадписи  = СтрШаблон(ТекстНадписи, Форма.ТекущаяОрганизация);
			
	Иначе
		
		Если Форма.ВыводитьОрганизациюВНадпись И ЗначениеЗаполнено(Форма.ТекущаяОрганизация) Тогда
			
			Если Форма.ЭтоРасшифровка Тогда
				ТекущаяОперация = НСтр("ru = 'расшифровка'");
			ИначеЕсли Форма.ЭтоОбмен Тогда
				ТекущаяОперация = НСтр("ru = 'обновление'");
			КонецЕсли;
			
			// Пример: Завершено: 15%. Текущая операция: обновление/расшифровка по организации Ромашка
			Подстрока1 = НСтр("ru = 'Завершено: %1%%'");
			Подстрока1 = СтрШаблон(Подстрока1, Проценты);
			
			Подстрока2 = НСтр("ru = 'Текущая операция: %1 по организации %2'");
			Подстрока2 = СтрШаблон(Подстрока2, ТекущаяОперация, Форма.ТекущаяОрганизация);
			Подстрока2 = СтрокаСерым(Подстрока2);
			
			ТекстНадписи = Новый ФорматированнаяСтрока(
				Подстрока1,
				Символы.ПС,
				Подстрока2);
			
		Иначе
			// Пример: Завершено: 15%
			ТекстНадписи = НСтр("ru = 'Завершено: %1%%'");
			ТекстНадписи = СтрШаблон(ТекстНадписи, Проценты);
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат ТекстНадписи;
	
КонецФункции

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаОповещенийОтправкиЗаявления(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Успешная отправка заявления на переход" Тогда
		
		Заявление = Источник;
		
		ЗакрытьБезДальнейшихДействий = Истина;
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.УспешнаяОтправка;
		
		Заголовок = НСтр("ru = 'Успешно!'");
		
		Если ЗначениеЗаполнено(Заявление) Тогда
			
			СсылкаНаЗаявление = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Заявление'"),
				,
				Новый Цвет(51, 51, 51),
				,
				ПолучитьНавигационнуюСсылку(Заявление));
				
		Иначе
			СсылкаНаЗаявление = НСтр("ru = 'Заявление'");
		КонецЕсли;
		
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ТекстПояснения")
			И ЗначениеЗаполнено(Параметр.ТекстПояснения) Тогда
			ТекстПояснения = Параметр.ТекстПояснения;
		Иначе
			ТекстПояснения = НСтр("ru = 'Мы уведомим вас о результате обработки заявления.
										|Обычно это занимает 1-2 дня.'");
		КонецЕсли;
		Подстрока2 = СтрокаСерым(ТекстПояснения);
		
		Элементы.ПоясняющийТекст.Заголовок = Новый ФорматированнаяСтрока(
			СсылкаНаЗаявление,
			НСтр("ru = ' по '"),
			Строка(ТекущаяОрганизация),
			НСтр("ru = ' отправлено.'"),
			Символы.ПС,
			Символы.ПС,
			Подстрока2);

		// Выполняем перед закрытием, поскольку при сбросе все ошибки очистятся. 
		ПолучитьВсеОшибки();
		РазрешитьЗакрыватьФорму();
		
	ИначеЕсли ИмяСобытия = "Неудачная отправка заявления на переход"  Тогда
		
		ВыполняемоеОповещение = Неопределено;
		РезультатОтправки = Ложь;
		
		// Выполняем перед закрытием, поскольку при сбросе все ошибки очистятся. 
		ПолучитьВсеОшибки();
		
		РазрешитьЗакрыватьФорму();
		
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ОшибкаОтправки;
		Заголовок = НСтр("ru = 'Заявление не отправлено'");
		ТекстНадписи  = НСтр("ru = 'Заявление по %1 не отправлено.'");
		ТекстНадписи  = СтрШаблон(ТекстНадписи, ТекущаяОрганизация);
		Элементы.ПоясняющийТекст.Заголовок = Новый ФорматированнаяСтрока(ТекстНадписи);
		
		Если ЕстьОшибки Тогда
			// Закрываем текущую форму и открываем форму с ошибками.
			Подключаемый_ЗакрытьФорму();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Завершить отправку без дальнейших действий" Тогда
		
		// При отправке в заявления ошибки выводятся в форме заявления.
		ЗавершитьБезДальнейшихДействий();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенийОбновленияМодуля(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Закрыть форму ожидания загрузки модуля" Тогда
		
		ЗавершитьБезДальнейшихДействий();
		
	ИначеЕсли ИмяСобытия = НСтр("ru = 'Длительное действие. Обновить надпись и заголовок'") Тогда
		
		Если ЗначениеЗаполнено(Параметр.Заголовок) Тогда
			Заголовок = Параметр.Заголовок;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметр.ОсновнойТекст) Тогда
			
			Если ЗначениеЗаполнено(Параметр.ДополнительныйТекст) Тогда
				
				Подстрока1 = Параметр.ОсновнойТекст;
				Подстрока2 = СтрокаСерым(Параметр.ДополнительныйТекст);
				
				Элементы.ПоясняющийТекст.Заголовок = Новый ФорматированнаяСтрока(
					Подстрока1,
					Символы.ПС,
					Символы.ПС,
					Подстрока2);
				
			Иначе
				Элементы.ПоясняющийТекст.Заголовок = Новый ФорматированнаяСтрока(Параметр.ОсновнойТекст);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОбновитьПанельОтправкиОтчета()

	Если ВладелецФормы <> Неопределено 
		И ЗначениеЗаполнено(ОтчетСсылка)
		И ВладелецФормы.Элементы.Найти("ГруппаПанельОтправки") <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(НаименованиеКонтролирующегоОргана) Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ОбновитьПанельСостоянияОтправкиВРегламентированномОтчете(
				ВладелецФормы, 
				НаименованиеКонтролирующегоОргана);
		Иначе
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ОбновитьПанельСостоянияОтправкиВРегламентированномОтчете(
				ВладелецФормы, 
				Неопределено);
		КонецЕсли;
				
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаменитьБубликНаДинамичный()
	
	// Это обход ошибки платформы.
	// В 8.3.8 в вебе не прорисовывается бублик длительной операции.
	
	ЭтоВеб = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияНиже8_3_9 = ОбщегоНазначенияКлиентСервер.СравнитьВерсии("8.3.9.1849", СистемнаяИнформация.ВерсияПриложения) > 0;
	
	Если НЕ (ЭтоВеб И ВерсияНиже8_3_9) Тогда
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДоопределитьПредыдущееСостояние()
	
	// Если форма отчета была долго открыта,
	// то в базе хранится уже новое состояние обмена, а в форме - все еще старое.
	// Поэтому за предыдущее состояние считаем то, что в открытой форме отчета, а не в базе.
	Если ЭтоОбменИзОтчета 
		И ВладелецФормы <> Неопределено
		И ВладелецФормы.Элементы.Найти("НаименованиеЭтапа") <> Неопределено
		И ЗначениеЗаполнено(ВладелецФормы.Элементы.НаименованиеЭтапа.Заголовок) Тогда
		
		ПредыдущееСостояниеОтчета = ВладелецФормы.Элементы.НаименованиеЭтапа.Заголовок;
		
	КонецЕсли;
	
	// Аналогично для формы этапов отправки.
	Если ЭтоОбменИзЭтаповОтправки 
		И ВладелецФормы <> Неопределено Тогда
		
		ПредыдущееСостояниеОтчета = ВладелецФормы.ТекущееСостояние;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьНеобходимостьЗакрытияБезДальнейшихДействий()
	
	// При этих видах обмена основная кнопка - Показать.
	// По Закрыть ничего происходить не должно.
	
	Если ЭтоОбменИзЖурналаОбмена ИЛИ ЭтоОбменИзФормы1СОтчетность ИЛИ ЭтоРасшифровка
		ИЛИ РассматриваетсяСотрудникомКонтролирующегоОргана Тогда
		
		ЗакрытьБезДальнейшихДействий = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьКнопкуОткрытияПротокола()
	
	Если ЗначениеЗаполнено(ПротоколНесданогоОтчета) Тогда
 		Элементы.Показать.Видимость 		= Истина;
		Элементы.Показать.КнопкаПоУмолчанию = Истина;
		Элементы.Показать.Ширина 			= 16;
		Элементы.Показать.Заголовок 		= НСтр("ru = 'Показать протокол'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаКрасным(ИсходнаяСтрока)

	Возврат Новый ФорматированнаяСтрока(ИсходнаяСтрока, ,Новый Цвет(255,0,0));

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаСерым(ИсходнаяСтрока)

	Возврат Новый ФорматированнаяСтрока(ИсходнаяСтрока, ,Новый Цвет(128,128,128));

КонецФункции

&НаКлиенте
Процедура УстановитьТекущуюОрганизацию(НовоеЗначениеТекущейОрганизаци)

	ТекущаяОрганизация = НовоеЗначениеТекущейОрганизаци;
	ДлительнаяОтправкаКлиент.ИзменитьПараметрыДлительнойОтправкиКлиентСервер("ТекущаяОрганизация", ТекущаяОрганизация);

КонецПроцедуры

&НаСервере
Функция АдресДереваНовых()
	
	Если ЭтоРасшифровка Тогда
		ДеревоНовое = РеквизитФормыВЗначение("Новое");
	Иначе
		ДеревоНовое = РеквизитФормыВЗначение("Новое");
		ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаполнитьДеревоНовое(ДеревоНовое,,);
		УдалитьИзДереваТекущуюСсылку(ДеревоНовое);
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(ДеревоНовое, Новый УникальныйИдентификатор); 
	
КонецФункции

&НаКлиенте
Процедура РазборОбщихПараметровОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ТипЗнч(Параметр) = Тип("Структура") Тогда
		
		Если Параметр.Свойство("ВыполняемоеОповещение") Тогда
			ВыполняемоеОповещение = Параметр.ВыполняемоеОповещение;
		КонецЕсли;
		
		Если Параметр.Свойство("РезультатОтправки") Тогда
			РезультатОтправки = Параметр.РезультатОтправки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДополнительныеПараметрыОперации()

	ДополнительныеПараметры = ДлительнаяОтправкаКлиентСервер.НовыеПараметрыСохранения(ЭтоАвтозапрос);
	ДополнительныеПараметры.Ошибки 					= Ошибки;
	ДополнительныеПараметры.ОтчетСсылка 			= ОтчетСсылка;
	ДополнительныеПараметры.ЕстьОшибки 				= ЕстьОшибки;
	ДополнительныеПараметры.ЭтоОбмен 				= ЭтоОбмен;
	ДополнительныеПараметры.ЭтоОтправка 			= ЭтоОтправка;
	ДополнительныеПараметры.ЭтоРасшифровка 			= ЭтоРасшифровка;
	ДополнительныеПараметры.ЭтоОбменИзОтчета 		= ЭтоОбменИзОтчета;
	ДополнительныеПараметры.ЭтоОбменИзЭтаповОтправки 	= ЭтоОбменИзЭтаповОтправки;
	ДополнительныеПараметры.ЭтоАвтозапрос 				= ЭтоАвтозапрос;
	ДополнительныеПараметры.ЭтоОбменИзФормы1СОтчетность = ЭтоОбменИзФормы1СОтчетность;
	ДополнительныеПараметры.ЭтоОбновлениеМодуля 		= ЭтоОбновлениеМодуля;
	ДополнительныеПараметры.ЭтоОтправкаЗаявления 		= ЭтоОтправкаЗаявления;
	ДополнительныеПараметры.ВидКонтролирующегоОргана 	= ВидКонтролирующегоОргана;
	ДополнительныеПараметры.НаименованиеКонтролирующегоОргана 	= НаименованиеКонтролирующегоОргана;
	ДополнительныеПараметры.Заголовок 							= Заголовок;
	ДополнительныеПараметры.ЗакрытьБезДальнейшихДействий 		= ЗакрытьБезДальнейшихДействий;
	ДополнительныеПараметры.ПутьКОбъекту 						= КонтекстЭДОКлиент.ПутьКОбъекту;
	ДополнительныеПараметры.ТекстРезультатаОбменаПоОрганизации 	= ТекстРезультатаОбменаПоОрганизации;
	ДополнительныеПараметры.КартинкаРезультатаОбменаПоОрганизации = КартинкаРезультатаОбменаПоОрганизации;
	
	Если ЭтоАвтозапрос Тогда
		ДополнительныеПараметры.ОтправкаСсылка 	 = АвтозапросПараметры.Ключ;
		ДополнительныеПараметры.ПротоколЗаполнен = АвтозапросПараметры.ПротоколЗаполнен;
	Иначе
		
		// Для отправок.
		ДополнительныеПараметры.ВыполняемоеОповещение 	= ВыполняемоеОповещение;
		ДополнительныеПараметры.РезультатОтправки 		= РезультатОтправки;
		
		// Для обменов.
		ДополнительныеПараметры.ИгнорироватьФорму1СОтчетности 	= НЕ ЭтоОбменИзФормы1СОтчетность;
		ДополнительныеПараметры.АдресДереваНовых 				= АдресДереваНовых();
		ДополнительныеПараметры.ЕстьНовые 						= ЕстьНовые;
		ДополнительныеПараметры.ПротоколНесданогоОтчета 		= ПротоколНесданогоОтчета;
		
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;

КонецФункции

&НаКлиенте
Процедура ОбработкаОповещенийОбновления(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Завершение обновления"
		ИЛИ ИмяСобытия = "Завершение расшифровки" Тогда
		
		ОбработатьЗавершениеДлительногоОбмена(Параметр);
		
	ИначеЕсли ИмяСобытия = "Смена этапа обмена" Тогда
		
		ОтработатьСменуЭтапаОбмена(Параметр);
		
	ИначеЕсли ИмяСобытия = "Смена этапа расшифровки" Тогда
		
		ОтработатьСменуЭтапаРасшифровки(Параметр);
		
	ИначеЕсли (ЭтоОбменИзОтчета ИЛИ ЭтоОбменИзЭтаповОтправки) И ИмяСобытия = "Невозможно обновить состояние отчета"
		ИЛИ (ЭтоОбменИзОтчета ИЛИ ЭтоОбменИзЭтаповОтправки) И ИмяСобытия = "Неудачная отправка" Тогда
		
		НеУдалосьОбновитьСтатусОтчета = Истина;
		
	ИначеЕсли ЭтоОбмен И ИмяСобытия = "Неудачная отправка"
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("НетДоступаВИнтернет") Тогда
		
		НетДоступаВИнтернет = Истина;
		ОбработатьЗавершениеДлительногоОбмена(Параметр);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗавершениеДлительногоОбмена(Параметр)

	Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
	// Выполняем перед закрытием, поскольку при сбросе все ошибки очистятся. 
	ПолучитьВсеОшибки();
	РазрешитьЗакрыватьФорму();
	
	ЕстьНовые = ЕстьИзмененияВДеревеНовыхПослеОбмена();
	
	Если ЭтоОбменИзОтчета
		ИЛИ ЭтоОбменИзЭтаповОтправки Тогда
		
		Заголовок = НСтр("ru = 'Обновление завершено'");
		
		// Состояние отчета изменилось на "Отправлен в ФНС".
		СформироватьТекстОРезультатеОбменаПоОтчету();
		// Этот текст будет выведен в форме, открывшейся следом за бубликом.
		ТекстРезультатаОбменаПоОрганизации 		= ТекстПроНовыеИОшибкиПриОбновлении();
		КартинкаРезультатаОбменаПоОрганизации 	= КартинкаПриЗавершенииОбновления();
		
	ИначеЕсли ЭтоРасшифровка Тогда
		
		Если ЭтоАвторасшифровка Тогда
			Заголовок = НСтр("ru = '1С-Отчетность'");
		Иначе
			Заголовок = НСтр("ru = 'Расшифровка завершена'");
		КонецЕсли;
		
		Элементы.ПоясняющийТекст.Заголовок = ТекстПроНовыеИОшибкиПриРасшифровке(Параметр);
		
		Если ЕстьНовые ИЛИ ЕстьОшибки Тогда
			Элементы.Показать.Видимость 		= Истина;
			Элементы.Показать.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
		Если ЕстьОшибки И НЕ ЕстьНовые Тогда
			Элементы.Показать.Ширина = 15;
			Элементы.Показать.Заголовок = НСтр("ru = 'Показать ошибки'");
		КонецЕсли;
		
	Иначе
		
		Заголовок = НСтр("ru = 'Обновление завершено'");
		
		Элементы.ПоясняющийТекст.Заголовок = ТекстПроНовыеИОшибкиПриОбновлении();
		Элементы.ДекорацияДлительнаяОперация.Картинка = КартинкаПриЗавершенииОбновления();
		
		Если (ЕстьНовые ИЛИ ЕстьОшибки) И НЕ НетДоступаВИнтернет
			ИЛИ ЕстьОшибки И НетДоступаВИнтернет Тогда
			Элементы.Показать.Видимость 		= Истина;
			Элементы.Показать.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// Оповещаем только в случае отсутствия новых, поскольку при их наличии
	// ошибки обновятся одновременно с новыми.
	Если ЕстьОшибки И НЕ ЕстьНовые Тогда
		РезультатОбновления 	= ДополнительныеПараметрыОперации();
		АдресСведенийПоОшибкам 	= ПоместитьВоВременноеХранилище(РезультатОбновления, Новый УникальныйИдентификатор);
		Оповестить("Обновить ошибки обмена", АдресСведенийПоОшибкам);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КартинкаПриЗавершенииОбновления() Экспорт
	
	Если НетДоступаВИнтернет Тогда
		Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
	ИначеЕсли ЕстьНовые Тогда
		Картинка = БиблиотекаКартинок.РасшифрованныеСообщения;
	ИначеЕсли НЕ ЕстьНовые И ЕстьОшибки Тогда
		Картинка = БиблиотекаКартинок.ОшибкаОтправки;
	Иначе
		// Нет новых, нет ошибок.
		Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
	КонецЕсли;
	
	Возврат Картинка;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещенийОтправки(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Пересчитать процент отправки" Тогда
		
		КоличествоПройденныхЭтапов = КоличествоПройденныхЭтапов + 1;
		ВывестиПоясняющийТекстПриВыполняющейсяОперации(ЭтотОбъект);
		
	ИначеЕсли ИмяСобытия = "Успешная отправка" Тогда
		
		ЭтоУспешнаяОтправка = Истина;
		
		// Отправка завершена успешно и нужно начинать проверять статус отчета на сервере.
		Если ТипЗнч(Параметр) = Тип("Структура") 
			И Параметр.Свойство("ПараметрыАвтозапроса") Тогда
			
			ЭтоАвтозапрос = Истина;
			Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			ЭтоПромежуточныйПротокол = Параметр.Свойство("ЭтоПромежуточныйПротокол") И Параметр.ЭтоПромежуточныйПротокол;
			ПолучитьВсеОшибки();
			
			Если ЕстьОшибки Тогда
				// Закрываем текущую форму и открываем форму с ошибками.
				Подключаемый_ЗакрытьФорму();
			Иначе
				НачалоАвтообмена(Параметр.ПараметрыАвтозапроса);
			КонецЕсли;

		Иначе
			
			ДополнительноеСообщение = ?(ТипЗнч(Параметр) = Тип("Структура")
				И Параметр.Свойство("ДополнительноеСообщение"), Параметр.ДополнительноеСообщение, "");
			ДополнительныеРеквизитыДляОтправки = ?(ТипЗнч(Параметр) = Тип("Структура")
				И Параметр.Свойство("ДополнительныеРеквизитыДляОтправки"), Параметр.ДополнительныеРеквизитыДляОтправки, Неопределено);
			ДополнительныеВложенияДляОтправки = ?(ТипЗнч(Параметр) = Тип("Структура")
				И Параметр.Свойство("ДополнительныеВложенияДляОтправки"), Параметр.ДополнительныеВложенияДляОтправки, Неопределено);
			
			ОбновитьПанельОтправкиОтчета();
			
			ЗакрытьБезДальнейшихДействий = Истина;
			Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.УспешнаяОтправка;
			// Выполняем перед закрытием, поскольку при сбросе все ошибки очистятся. 
			ПолучитьВсеОшибки();
			РазрешитьЗакрыватьФорму();
			ВывестиПоясняющийТекстИЗаголовокПриЗавершеннойОтправке(Истина, ДополнительноеСообщение);
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Неудачная отправка" Тогда
		
		Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ОшибкаОтправки;
		// Выполняем перед закрытием, поскольку при сбросе все ошибки очистятся. 
		ПолучитьВсеОшибки();
		
		Если КоличествоОшибокОтменыДействия = КоличествоОшибок Тогда
			// Пользователь отказался от ввода пароля и это единственная причина, по которой отправка не выполнилась.
			ЗавершитьБезДальнейшихДействий();
		Иначе
			РазрешитьЗакрыватьФорму();
			
			ВывестиПоясняющийТекстИЗаголовокПриЗавершеннойОтправке(Ложь);
			
			Если ЕстьОшибки Тогда
				// Закрываем текущую форму и открываем форму с ошибками.
				Подключаемый_ЗакрытьФорму();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Завершить отправку без дальнейших действий" Тогда
		
		// При отправке в ФТС может быть выведена своя форма ошибок.
		ЗавершитьБезДальнейшихДействий();
		
	КонецЕсли;

КонецПроцедуры

// Используется в следующих случаях:
//   1. При отправке в ФТС может быть выведена своя форма ошибок.
//   2. Если пользователь отказался от ввода пароля и это единственная причина, по которой отправка не выполнилась.
//
&НаКлиенте
Процедура ЗавершитьБезДальнейшихДействий()

	РазрешитьЗакрыватьФорму();
	ЗакрытьБезДальнейшихДействий = Истина;
	
	Подключаемый_ЗакрытьФорму();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Активизировать();
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	ВывестиПоясняющийТекстПриВыполняющейсяОперации(ЭтотОбъект);
	
	ДоопределитьПредыдущееСостояние();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВсеОшибки()
	
	Ошибки 				= ДлительнаяОтправкаКлиент.ОшибкиКлиентСервер();
	КоличествоОшибок	= Ошибки.Количество();
	ЕстьОшибки 			= КоличествоОшибок > 0;
	
	Если ЭтоОтправка Тогда
		КоличествоОшибокОтменыДействия = ДлительнаяОтправкаКлиентСервер.КоличествоОшибокОтменыДействия(Ошибки);
	КонецЕсли;
	
	// Для того, чтобы в формах, имеющих панель с ошибками, ошибки сохранялись до следующего обмена.
	Если ЕстьОшибки И ЭтоОбмен Тогда
		СведенияПоОшибкам = ДополнительныеПараметрыОперации();
		ДлительнаяОтправкаВызовСервера.СохранитьОшибкиПоследнегоОбмена(СведенияПоОшибкам);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	ДлительнаяОтправкаКлиент.ОчиститьПараметрыДлительнойОтправкиКлиентСервер();
	СбросПараметровВыполнен = Истина;
	
	Если ЭтоАвтозапрос Тогда
		РазрешитьЗакрыватьФорму();
	КонецЕсли;
	
	Если Открыта() Тогда
		
		ДополнительныеПараметры = ДополнительныеПараметрыОперации();
		Закрыть(ДополнительныеПараметры);

	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительныеВложенияДляОтправки) И ДополнительныеВложенияДляОтправки.Количество() > 0 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Отправитель", ТекущаяОрганизация));
		ПараметрыФормы.Вставить("ДополнительныеРеквизиты", ДополнительныеРеквизитыДляОтправки);
		ПараметрыФормы.Вставить("ДополнительныеВложения", ДополнительныеВложенияДляОтправки);
		ДополнительныеВложенияДляОтправки = Неопределено;
		
		ОткрытьФорму("Справочник.ПерепискаСКонтролирующимиОрганами.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьЗакрыватьФорму()
	
	// Форму не закрываем, но разрешаем закрыть вручную. 
	РазрешитьЗакрытиеФормы = Истина;

	// Кнопку закрытия показываем в самом конце, когда уже выполнены все действия.
	Элементы.ФормаЗакрыть.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаменитьДвойнойПробел(Знач Строка)
	
	Пока СтрНайти(Строка, "  ") <> 0 Цикл
		Строка = СокрЛП(СтрЗаменить(Строка, "  ", " "));
	КонецЦикла;
	
	Возврат Строка;
	
КонецФункции

&НаСервере
Процедура ОбработатьВходныеПараметры(Параметры)

	Параметры.Свойство("ИдентификаторПолучателя", ИдентификаторПолучателя);
	Параметры.Свойство("АдресДереваНовыхСобытий", АдресДереваНовыхСобытий);
		
	Если Параметры.Свойство("ЭтоОбмен") Тогда
		
		ОбработатьВходныеПараметрыОбновления(Параметры);
		
	ИначеЕсли Параметры.Свойство("ЭтоОбновлениеМодуля") Тогда
		
		ЭтоОбновлениеМодуля = Истина;
		
	ИначеЕсли Параметры.Свойство("ЭтоОтправкаЗаявления") Тогда
		
		ОбработатьВходныеПараметрыОтправкиЗаявления(Параметры);
		
	ИначеЕсли Параметры.Свойство("ЭтоУниверсальноеОжидание") Тогда
		
		ЭтоОтправка 	 = Истина;
		ВыводитьПроценты = Истина;
		
		ЭтоУниверсальноеОжидание = Истина;
		
		Заголовок = Параметры.Заголовок;
		Элементы.ПоясняющийТекст.Заголовок = Параметры.Надпись;
		
	Иначе
		
		ОбработатьВходныеПараметрыОтправки(Параметры);
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент()
		И НЕ ЭтоРасшифровка Тогда
		ВыводитьПроценты = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВходныеПараметрыОтправкиЗаявления(Параметры)

	ЭтоОтправкаЗаявления = Истина;
	ТекущаяОрганизация 	= Параметры.Организация;
	СоздаватьКлюч 		= Параметры.СоздаватьКлюч;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВходныеПараметрыОбновления(Параметры)

	ЭтоОбмен = Истина;
	
	ВыводитьПроценты 			= Параметры.ВыводитьПроценты;
	Организации 	 			= Параметры.Организации;
	ОбщееКоличествоЭтапов 		= Параметры.ОбщееКоличествоЭтапов;
	ЭтоРасшифровка 				= Параметры.ЭтоРасшифровка;
	ЭтоАвторасшифровка          = Параметры.ЭтоАвторасшифровка;
	ЭтоОбменИзФормы1СОтчетность = Параметры.ЭтоОбменИзФормы1СОтчетность;
	ЭтоОбменИзОтчета 			= Параметры.ЭтоОбменИзОтчета;
	ЭтоОбменИзЭтаповОтправки 	= Параметры.ЭтоОбменИзЭтаповОтправки;
	ЭтоОбменИзЖурналаОбмена 	= Параметры.ЭтоОбменИзЖурналаОбмена;
	
	ВыводитьОрганизациюВНадпись = НЕ ИспользуетсяОднаОрганизация;
	
	ОтчетСсылка = Параметры.ОтчетСсылка;
	ПолучитьСведенияОбОтчете(ОтчетСсылка);
	
	Если ЭтоРасшифровка Тогда
		ТекущаяОрганизация = Организации;
	Иначе
		Если Организации <> Неопределено Тогда
			ЗаполнитьТаблицуОповещений(Организации);
			ОбщееКоличествоЭтапов = ОповещенияОбмена.Количество();
			
			Если ОповещенияОбмена.Количество() >= 1 Тогда
				ТекущаяОрганизация = ОповещенияОбмена[0].Организация;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоОбмен И НЕ ЭтоРасшифровка Тогда
		ВывестиПоясняющийТекстПриВыполняющейсяОперации(ЭтотОбъект);
	КонецЕсли;
	
	// Очистка ошибок в красной панели.
	Если ЭтоОбмен Тогда
		ДлительнаяОтправкаВызовСервера.СохранитьОшибкиПоследнегоОбмена(Неопределено);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьВходныеПараметрыОтправки(Параметры)

	ЭтоОтправка 	 = Истина;
	ВыводитьПроценты = Истина;
	
	ОтчетСсылка = Параметры.ОтчетСсылка;
	ПолучитьСведенияОбОтчете(ОтчетСсылка);
	
	ОбщееКоличествоЭтапов = Параметры.ОбщееКоличествоЭтапов;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСведенияОбОтчете(ОтчетСсылка)
	
	Если ОтчетСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	КонтекстЭДОСервер 	= ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	СведенияОбОтчете 	= КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(ОтчетСсылка);
	
	НаименованиеКонтролирующегоОргана = XMLСтрока(СведенияОбОтчете.ВидКонтролирующегоОргана);
	ВидКонтролирующегоОргана 		  = СведенияОбОтчете.ВидКонтролирующегоОргана;
	НаименованиеОтчета 		 		  = Строка(СведенияОбОтчете.Наименование);
	ТекущаяОрганизация				  = СведенияОбОтчете.Организация;
	
	// Получение параметров прорисовки
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", Ложь);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Истина);
	
	СостояниеОтчета = ТекущийЭтапОтправки(ОтчетСсылка);
	
	Если СостояниеОтчета <> Неопределено Тогда
		ПредыдущееСостояниеОтчета = СостояниеОтчета.ТекстНадписи;
	КонецЕсли;

КонецПроцедуры
	
&НаСервере
Функция ТекущийЭтапОтправки(ОтчетСсылка)
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	// Получение параметров прорисовки
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", Ложь);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Истина);
	
	Орган = ?(ЗначениеЗаполнено(НаименованиеКонтролирующегоОргана), НаименованиеКонтролирующегоОргана, "ФНС");
	
	ТекущееСостояниеОтправки = КонтекстЭДОСервер.ТекущееСостояниеОтправки(
		ОтчетСсылка, 
		Орган, 
		ДополнительныеПараметры);
		
	Возврат ТекущееСостояниеОтправки.ТекущийЭтапОтправки;

КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуОповещений(Организации)
	
	Если НЕ ВыводитьПроценты Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	Для каждого Организация Из Организации Цикл
		
		// Учетная запись.
		УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
		
		ОбменРазрешен = ЗначениеЗаполнено(УчетнаяЗапись) 
			И КонтекстЭДОСервер.ПользователюРазрешеноВыполнятьОбменПоУчетнойЗаписи(УчетнаяЗапись);
			
		Если ОбменРазрешен Тогда
			
			ДобавитьНовоеОповещение("АвтоматическаяНастройкаУчетнойЗаписи", Организация,,,УчетнаяЗапись);
			ДобавитьНовоеОповещение("ПолучитьИРасшифроватьСообщенияПоУчетнойЗаписи",Организация,,,УчетнаяЗапись);
			ДобавитьНовоеОповещение("ПолучитьСообщенияПоУчетнойЗаписи", Организация,,,УчетнаяЗапись);
			ДобавитьНовоеОповещение("РасшифроватьСообщенияПоУчетнойЗаписи", Организация,,,УчетнаяЗапись);
			ДобавитьНовоеОповещение("ЗашифроватьСообщенияПоУчетнойЗаписи", Организация,,,УчетнаяЗапись);
			ДобавитьНовоеОповещение("ОтправитьСообщенияПоУчетнойЗаписи", Организация,,,УчетнаяЗапись);
			
		КонецЕсли;
		
		Если НЕ ЭтоОбменИзЖурналаОбмена Тогда
		
			// ФСС.
			Отправки 	= КонтекстЭДОСервер.НезавершенныеПоследниеОтправкиФССПоОрганизации(Организация);
			Орган 		= Перечисления.ТипыКонтролирующихОрганов.ФСС;
			Для каждого Отправка Из Отправки Цикл
				ДобавитьНовоеОповещение("ОсуществитьОбменПоОрганизацииФСС", Организация, Отправка, Орган);
			КонецЦикла;
			
			// ФСРАР.
			Отправки 	= КонтекстЭДОСервер.НезавершенныеПоследниеОтправкиФСРАРПоОрганизации(Организация);
			Орган 		= Перечисления.ТипыКонтролирующихОрганов.ФСРАР;
			Для каждого Отправка Из Отправки Цикл
				ДобавитьНовоеОповещение("ОсуществитьОбменПоОрганизацииФСРАР", Организация, Отправка, Орган);
			КонецЦикла;
			
			// РПН.
			Отправки 	= КонтекстЭДОСервер.НезавершенныеПоследниеОтправкиРПНПоОрганизации(Организация);
			Орган 		= Перечисления.ТипыКонтролирующихОрганов.РПН;
			Для каждого Отправка Из Отправки Цикл
				ДобавитьНовоеОповещение("ОсуществитьОбменПоОрганизацииРПН", Организация, Отправка, Орган);
			КонецЦикла;
			
			// ФТС.
			Отправки 	= КонтекстЭДОСервер.НезавершенныеПоследниеОтправкиФТСПоОрганизации(Организация);
			Орган 		= Перечисления.ТипыКонтролирующихОрганов.ФТС;
			Для каждого Отправка Из Отправки Цикл
				ДобавитьНовоеОповещение("ОсуществитьОбменПоОрганизацииФТС", Организация, Отправка, Орган);
			КонецЦикла;
			
			// Банк России
			ИмяПодсистемыБанкаРоссии = "РегламентированнаяОтчетность.ЭлектронныйДокументооборотСКонтролирующимиОрганами.СдачаОтчетностиВБанкРоссии";
			Если ОбщегоНазначения.ПодсистемаСуществует(ИмяПодсистемыБанкаРоссии) Тогда
				
				МодульДокументооборотСБанкомРоссииВызовСервера = ОбщегоНазначения.ОбщийМодуль("ДокументооборотСБанкомРоссииВызовСервера");
				
				Отправки 	= МодульДокументооборотСБанкомРоссииВызовСервера.ПолучитьНеЗавершенныеОтправки(Организация);
				Орган 		= Перечисления.ТипыКонтролирующихОрганов.БанкРоссии;
				
				Для каждого Отправка Из Отправки Цикл
					ДобавитьНовоеОповещение("ОсуществитьОбменПоОрганизацииБанкРоссии", Организация, Отправка, Орган);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНовоеОповещение(
		ИмяСобытия, 
		Организация, 
		Отправка = Неопределено, 
		Орган = Неопределено, 
		УчетнаяЗапись = Неопределено)
	
	НоваяСтрока = ОповещенияОбмена.Добавить();
	НоваяСтрока.ИмяСобытия 		= ИмяСобытия;
	НоваяСтрока.Организация 	= Организация;
	НоваяСтрока.Отправка 		= Отправка;
	НоваяСтрока.Орган 			= Орган;
	НоваяСтрока.УчетнаяЗапись 	= УчетнаяЗапись;

КонецПроцедуры

&НаКлиенте
Процедура ОтработатьСменуЭтапаОбмена(Параметр)

	ПометитьЭтапыПройденными(Параметр);
	ОпределитьКоличествоПройденныхЭтаповОбмена();
	
	ВывестиПоясняющийТекстПриВыполняющейсяОперации(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтработатьСменуЭтапаРасшифровки(Параметр)

	Если Параметр.Свойство("ТекстСообщения") Тогда
		Элементы.ПоясняющийТекст.Заголовок = Параметр.ТекстСообщения;
	Иначе
		КоличествоПройденныхЭтапов 	= Параметр.КоличествоПройденныхЭтапов;
		ОбщееКоличествоЭтапов 		= Параметр.ОбщееКоличествоЭтапов;
		
		УстановитьТекущуюОрганизацию(Параметр.ТекущаяОрганизация);
		
		ВывестиПоясняющийТекстПриВыполняющейсяОперации(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПометитьЭтапыПройденными(Параметр)
	
	Отбор = Новый Структура();
	ДобавитьУсловиеОтбора(Отбор, "ИмяСобытия", 		Параметр.ИмяСобытия);
	ДобавитьУсловиеОтбора(Отбор, "Орган", 			Параметр.Орган);
	ДобавитьУсловиеОтбора(Отбор, "Отправка", 		Параметр.Отправка);
	ДобавитьУсловиеОтбора(Отбор, "УчетнаяЗапись", 	Параметр.УчетнаяЗапись);
	
	Этапы = ОповещенияОбмена.НайтиСтроки(Отбор);
	Для каждого Этап Из Этапы Цикл
		Этап.ЭтапПройден 	= Истина;
		УстановитьТекущуюОрганизацию(Этап.Организация);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьУсловиеОтбора(Отбор, Ключ, Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		Отбор.Вставить(Ключ, Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьКоличествоПройденныхЭтаповОбмена()
	
	ОтборПройденныхЭтапов 		= Новый Структура("ЭтапПройден", Истина);
	ПройденныеЭтапы 			= ОповещенияОбмена.НайтиСтроки(ОтборПройденныхЭтапов);
	КоличествоПройденныхЭтапов 	= ПройденныеЭтапы.Количество();

КонецПроцедуры

#КонецОбласти

#Область Автозапрос

&НаКлиенте
Процедура НачалоАвтообмена(Параметр)
	
	АвтозапросПараметры = Параметр;
	АвтозапросПараметры.Вставить("СтатусОтправки", 		ПредопределенноеЗначение("Перечисление.СтатусыОтправки.Отправлен"));
	АвтозапросПараметры.Вставить("ПротоколЗаполнен", 	Ложь);
	
	// Отобразить появление новой записи в журнале обмена
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("Ссылка", ОтчетСсылка);
	ПараметрыОповещения.Вставить("Организация", ТекущаяОрганизация);
	
	Оповестить("Запись_Отправки" + НаименованиеКонтролирующегоОргана, ПараметрыОповещения, АвтозапросПараметры.Ключ);
	
	// Обновление статуса отправки на форме отправляемого отчета
	ОбновитьПанельОтправкиОтчета();
	
	// Действия по автозапросу.
	
	АвтозапросВнутриОбработчикаЗавершения = Ложь;
	АвтозапросМаксПопыток 	= 3;
	АвтозапросЧислоПопыток 	= 1;
	АвтозапросМаксСекунд 	= 50;
	АвтозапросЧислоСекунд 	= АвтозапросМаксСекунд;
	АвтозапросШагСекунд		= 0.5;
	
	Заголовок = НСтр("ru = 'Пожалуйста, подождите...'");
	УстановитьНадписьПриАвтозапросе(0);
	РазрешитьЗакрыватьФорму();
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСостояниеАвтозапроса", АвтозапросШагСекунд, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСостояниеАвтозапроса() Экспорт
	
	Если АвтозапросВнутриОбработчикаЗавершения Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСостояниеАвтозапроса", АвтозапросШагСекунд, Истина);
	
	АвтозапросВнутриОбработчикаЗавершения = Истина;
	
	Если АвтозапросЧислоСекунд < АвтозапросШагСекунд Тогда
		
		//обновим результат отправки
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"Подключаемый_ОбновитьСостояниеАвтозапросаЗавершение", 
			ЭтотОбъект);
		
		КонтекстЭДОКлиент.ОбновитьРезультатКонкретнойОтправки(
			ЭтаФорма, 
			АвтозапросПараметры.Ключ, 
			ОписаниеОповещения, 
			АвтозапросПараметры);
			
		Возврат;
		
	Иначе
		
		АвтозапросЧислоСекунд 	= АвтозапросЧислоСекунд - АвтозапросШагСекунд;
		ПрошлоСекунд 			= АвтозапросМаксСекунд - АвтозапросЧислоСекунд;
		ПроцентВыполнения 		= Цел(100 * ПрошлоСекунд / АвтозапросМаксСекунд);
		
		УстановитьНадписьПриАвтозапросе(ПроцентВыполнения);
		
	КонецЕсли;
	
	АвтозапросВнутриОбработчикаЗавершения = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьПриАвтозапросе(ПроцентВыполнения)
	
	Если АвтозапросЧислоПопыток = 1 Тогда
		Если ЭтоПромежуточныйПротокол Тогда
			ТекстНадписи = НСтр("ru = 'Отчет отправлен. Идет получение промежуточного
									  |протокола (%1%%)'");
		Иначе
			ТекстНадписи = НСтр("ru = 'Отчет отправлен. Идет получение протокола (%1%%)'");
		КонецЕсли;
		
	ИначеЕсли АвтозапросЧислоПопыток = 2 Тогда
		Если ЭтоПромежуточныйПротокол Тогда
			ТекстНадписи = НСтр("ru = 'Вторая попытка получения промежуточного протокола (%1%%)'");
		Иначе
			ТекстНадписи = НСтр("ru = 'Вторая попытка получения протокола (%1%%)'");
		КонецЕсли;
		
	Иначе
		Если ЭтоПромежуточныйПротокол Тогда
			ТекстНадписи = НСтр("ru = 'Третья попытка получения промежуточного протокола (%1%%)'");
		Иначе
			ТекстНадписи = НСтр("ru = 'Третья попытка получения протокола (%1%%)'");
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ПоясняющийТекст.Заголовок = СтрШаблон(
		ТекстНадписи,
		ПроцентВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСостояниеАвтозапросаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПолучитьВсеОшибки();
	
	ИнформацияОбОтправке = ИнформацияОбОтправкеНаСервере(АвтозапросПараметры.Ключ);
	АвтозапросПараметры.СтатусОтправки 		= ИнформацияОбОтправке.СтатусОтправки;
	АвтозапросПараметры.ПротоколЗаполнен 	= ИнформацияОбОтправке.ПротоколЗаполнен;
	РассматриваетсяСотрудникомКонтролирующегоОргана = (ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("Рассматривается") И Результат.Рассматривается);
	
	// Обновление статуса отправки в форме Управления обменом
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Ссылка", 		ОтчетСсылка);
	ПараметрыОповещения.Вставить("Организация", ТекущаяОрганизация);
	
	Оповестить("Редактирование_Отправки" + НаименованиеКонтролирующегоОргана, ПараметрыОповещения, АвтозапросПараметры.Ключ);
	
	// Обновление статуса отправки на форме отправляемого отчета
	ОбновитьПанельОтправкиОтчета();
	
	АвтозапросЧислоПопыток 	= АвтозапросЧислоПопыток + 1;
	АвтозапросЧислоСекунд 	= АвтозапросМаксСекунд;
	
	Если Ошибки.Количество() <> 0 
		ИЛИ АвтозапросЧислоПопыток > АвтозапросМаксПопыток
		ИЛИ АвтозапросПараметры.СтатусОтправки <> ПредопределенноеЗначение("Перечисление.СтатусыОтправки.Отправлен")
		ИЛИ РассматриваетсяСотрудникомКонтролирующегоОргана Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ОбновитьСостояниеАвтозапроса");
		
		Если АвтозапросПараметры.ПротоколЗаполнен ИЛИ ЕстьОшибки Тогда
			Если РассматриваетсяСотрудникомКонтролирующегоОргана Тогда
				Заголовок = НСтр("ru = 'Промежуточный протокол получен'");
				Элементы.ПоясняющийТекст.Заголовок = СтрШаблон(
					НСтр("ru = 'Окончательный протокол должен быть сформирован по результатам рассмотрения сотрудником %1"
							   + " в срок от 1 дня до 2-х недель.'"),
					НаименованиеКонтролирующегоОргана);
				Элементы.Показать.КнопкаПоУмолчанию = Истина;
				Элементы.Показать.Ширина 			= 30;
				Элементы.Показать.Заголовок 		= НСтр("ru = 'Показать промежуточный протокол'");
		 		Элементы.Показать.Видимость 		= Истина;
				Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ВыполненоНужноПодождать;
				
			Иначе
				Подключаемый_ЗакрытьФорму();
			КонецЕсли;
		Иначе
			Заголовок = НСтр("ru = 'Протокол не получен'");
			Элементы.ПоясняющийТекст.Заголовок = НСтр("ru = 'Попробуйте обновить состояние отчета позже'");
			Элементы.ДекорацияДлительнаяОперация.Картинка = БиблиотекаКартинок.ИнформацияПоДлительнойОтправке;
		КонецЕсли;
		
	КонецЕсли;
	
	АвтозапросВнутриОбработчикаЗавершения = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнформацияОбОтправкеНаСервере(ОтправкаСсылка)
	
	СвойстваОтправки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОтправкаСсылка, "СтатусОтправки, Протокол");
	
	Результат = Новый Структура();
	Результат.Вставить("СтатусОтправки",   СвойстваОтправки.СтатусОтправки);
	Результат.Вставить("ПротоколЗаполнен", Истина);

	Если СвойстваОтправки.СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен Тогда
		
		Протокол = ?(ЗначениеЗаполнено(СвойстваОтправки.Протокол), СвойстваОтправки.Протокол.Получить(), Неопределено);
		Результат.ПротоколЗаполнен = ЗначениеЗаполнено(Протокол);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область НовыеСообщения

&НаСервере
Функция ЕстьИзмененияВДеревеНовыхПослеОбмена()
	
	НовоеДеревоНовых = РеквизитФормыВЗначение("Новое");
	
	Если ЭтоАдресВременногоХранилища(АдресДереваНовыхСобытий) Тогда
		// Дерево новых уже было сформировано перед вызовом формы, извлечем его из временного хранилища.
		НовоеДеревоНовых = ПолучитьИзВременногоХранилища(АдресДереваНовыхСобытий);
	ИначеЕсли ЭтоРасшифровка Тогда
		ИнициаторСеанса = Перечисления.ИнициаторыСеансовСвязиСКонтролирующимиОрганами.Автообмен;
		ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаполнитьДеревоНовое(НовоеДеревоНовых, ИнициаторСеанса, Истина);
	Иначе
		ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаполнитьДеревоНовое(НовоеДеревоНовых, , Истина);
	КонецЕсли;
	
	// Сравнение
	УдалитьИзДереваТекущуюСсылку(НовоеДеревоНовых);
	
	ЕстьИзмененияВНовыхСообщениях = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЕстьИзмененияВДеревеНовых(НовоеДеревоНовых);
	
	ЗначениеВРеквизитФормы(НовоеДеревоНовых, "Новое");
	
	// В форме 1С-Отчетность есть 4е раздела на закладке Новые:
	// Полученные сообщения, обработанные запросы, завершенные отправки и незавершенные отправки.
	// Если после обмена есть только незавершенные отправки, то при переключении на форму 1С-Отчетность
	// не понятно, что изменилось. Для таких случаев будем пользователю специально сообщать, что 
	// изменились состояния только незавершенных отправок.
	Если ЕстьИзмененияВНовыхСообщениях Тогда
		ЕстьТолькоНезавершенныеОтправки = ЕстьТолькоНезавершенныеОтправки();
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ОбновитьКоличествоНовых(ЭтотОбъект);
	КонецЕсли;
	
	Возврат ЕстьИзмененияВНовыхСообщениях И НЕ ЕстьТолькоНезавершенныеОтправки;
	
КонецФункции

&НаСервере
Процедура УдалитьИзДереваТекущуюСсылку(НовоеДеревоНовых)

	НовоеДеревоНовых.Колонки.Добавить("НужноУдалить");
	
	// Если обмен выполнялся из отчета, то в дереве новых этот отчет не показываем.
	Если ЗначениеЗаполнено(ОтчетСсылка) Тогда
		УдаляемаяСтрока = НовоеДеревоНовых.Строки.Найти(ОтчетСсылка, "Ссылка");
		Если УдаляемаяСтрока <> Неопределено Тогда
			УдаляемаяСтрока.НужноУдалить = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("НужноУдалить", Истина);
	
	// Удаляем со всех самых нижних уровней, только в следующий проход - с верхнего.
	Для Каждого Строка1Уровня Из НовоеДеревоНовых.Строки Цикл
		
		СтрокиКоторыеНужноУдалить = Строка1Уровня.Строки.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаКоторуюНужноУдалить Из СтрокиКоторыеНужноУдалить Цикл
			Строка1Уровня.Строки.Удалить(СтрокаКоторуюНужноУдалить);
		КонецЦикла;
		
	КонецЦикла;
	
	// Удаляем с верхнего уровня.
	СтрокиКоторыеНужноУдалить = НовоеДеревоНовых.Строки.НайтиСтроки(Отбор);
	Для Каждого СтрокаКоторуюНужноУдалить Из СтрокиКоторыеНужноУдалить Цикл
		НовоеДеревоНовых.Строки.Удалить(СтрокаКоторуюНужноУдалить);
	КонецЦикла;
	
	НовоеДеревоНовых.Колонки.Удалить("НужноУдалить");

КонецПроцедуры

&НаСервере
Функция ЕстьТолькоНезавершенныеОтправки() Экспорт
	
	ЕстьПолученныеСообщения   = Ложь;
	ЕстьОбработанныеЗапросы   = Ложь;
	ЕстьЗавершенныеОтправки   = Ложь;
	ЕстьНезавершенныеОтправки = Ложь;
	
	СтрокиДерева = Новое.ПолучитьЭлементы();
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Группа = Перечисления.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.ПолученныеСообщения Тогда
			
			Если СтрокаДерева.НеПрочитано И ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
				ЕстьПолученныеСообщения = Истина;
			КонецЕсли;
				
		ИначеЕсли СтрокаДерева.Группа = Перечисления.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.ОбработанныеЗапросы Тогда 
			
			Если СтрокаДерева.НеПрочитано И ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
				 ЕстьОбработанныеЗапросы = Истина;
			КонецЕсли;
			
		ИначеЕсли СтрокаДерева.Группа = Перечисления.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.ЗавершенныеОтправки Тогда 
			
			Если СтрокаДерева.НеПрочитано И ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
				ЕстьЗавершенныеОтправки = Истина;
			КонецЕсли;
		
		ИначеЕсли СтрокаДерева.Группа = Перечисления.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.НезавершенныеОтправки Тогда 
			
			Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
				ЕстьНезавершенныеОтправки = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВДеревеТолькоНезавершенныеОтправки = НЕ ЕстьПолученныеСообщения 
		И НЕ ЕстьОбработанныеЗапросы
		И НЕ ЕстьЗавершенныеОтправки
		И ЕстьНезавершенныеОтправки;

	Возврат ВДеревеТолькоНезавершенныеОтправки;
	
КонецФункции

#КонецОбласти

#КонецОбласти