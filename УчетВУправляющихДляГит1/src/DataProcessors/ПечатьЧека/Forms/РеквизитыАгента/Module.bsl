
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПодготовитьФормуНаСервере();
	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если ЗначениеЗаполнено(ИННПоставщика) Тогда
		РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИННПоставщика, Истина);
		Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
			Если ЭтоПоставщик(ПризнакАгента) Тогда
				ПредставлениеПоля = "ИНН комитента";
			ИначеЕсли ЭтоБанковскийАгент(ПризнакАгента) Тогда 
				ПредставлениеПоля = "ИНН получателя средств";
			Иначе
				ПредставлениеПоля = "ИНН поставщика";
			КонецЕсли;
			
			ТекстСообщенияПользователю = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", ПредставлениеПоля,,,РезультатПроверки.ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияПользователю,,"ИННПоставщика", , Отказ);
		КонецЕсли; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ИННОператораПеревода) Тогда
		РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИННОператораПеревода, Истина);
		Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
			ТекстСообщенияПользователю = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", "ИНН оператора перевода",,,РезультатПроверки.ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияПользователю,,"ИННОператораПеревода", , Отказ);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПризнакАгентаПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СобственныеТоварыУслуги1ПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СобственныеТоварыУслуги0ПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура КомандаОК(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПризнакАгента",    ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПустаяСсылка"));
	Результат.Вставить("ДанныеАгента",     МенеджерОборудованияКлиентСервер.ПараметрыДанныеАгента());
	Результат.Вставить("ДанныеПоставщика", МенеджерОборудованияКлиентСервер.ПараметрыДанныеПоставщика());
	
	Если СобственныеТоварыУслуги = 0 Тогда
		Результат.ПризнакАгента = ПризнакАгента;
		Если ЭтоБанковскийАгент(ПризнакАгента) ИЛИ ЭтоПлатежныйАгент(ПризнакАгента) Тогда
			// Заполняем данные агента
			Результат.ДанныеАгента.ОператорПеревода.ИНН             = ИННОператораПеревода;
			Результат.ДанныеАгента.ОператорПеревода.Наименование    = НаименованиеОператораПеревода;
			Результат.ДанныеАгента.ОператорПеревода.Адрес           = АдресОператораПеревода;
			Результат.ДанныеАгента.ОператорПеревода.Телефон         = ТелефонОператораПеревода;
			
			Результат.ДанныеАгента.ПлатежныйАгент.Операция          = ОперацияПлатежногоАгента;
			Результат.ДанныеАгента.ПлатежныйАгент.Телефон           = ТелефонПлатежногоАгента;
			
			Результат.ДанныеАгента.ОператорПоПриемуПлатежей.Телефон = ТелефонОператораПоПриемуПлатежей ;
		КонецЕсли;
		
		// Заполняем данные поставщика
		Результат.ДанныеПоставщика.ИНН = ИННПоставщика;
		Результат.ДанныеПоставщика.Наименование = НаименованиеПоставщика;
		Результат.ДанныеПоставщика.Телефон = ТелефонПоставщика;
	КонецЕсли; 
	
	Закрыть(Результат);
КонецПроцедуры
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПодготовитьФормуНаСервере()
	Перем ДанныеАгента, ДанныеПоставщика;
	
	ПризнакАгента = Параметры.ПризнакАгента;
	
	Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.БанковскийПлатежныйАгент);
	Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.БанковскийПлатежныйСубагент);
	Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.ПлатежныйАгент);
	Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.ПлатежныйСубагент);
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов") Тогда
		Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.Агент);
		Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.Комиссионер);
	КонецЕсли;
	Элементы.ПризнакАгента.СписокВыбора.Добавить(Перечисления.ПризнакиАгента.Поверенный);
	
	Элементы.ПризнакАгента.СписокВыбора.СортироватьПоЗначению();
	
	СобственныеТоварыУслуги = ?(ЗначениеЗаполнено(ПризнакАгента), 0, 1);
	
	Если Параметры.Свойство("ДанныеАгента", ДанныеАгента) Тогда
		ИННОператораПеревода             = ДанныеАгента.ОператорПеревода.ИНН;
		НаименованиеОператораПеревода    = ДанныеАгента.ОператорПеревода.Наименование;
		АдресОператораПеревода           = ДанныеАгента.ОператорПеревода.Адрес;
		ТелефонОператораПеревода         = ДанныеАгента.ОператорПеревода.Телефон;
		
		ОперацияПлатежногоАгента         = ДанныеАгента.ПлатежныйАгент.Операция;
		ТелефонПлатежногоАгента          = ДанныеАгента.ПлатежныйАгент.Телефон;
		
		ТелефонОператораПоПриемуПлатежей = ДанныеАгента.ОператорПоПриемуПлатежей.Телефон;
	КонецЕсли;
	
	Если Параметры.Свойство("ДанныеПоставщика", ДанныеПоставщика) Тогда
		ИННПоставщика                    = ДанныеПоставщика.ИНН;
		НаименованиеПоставщика           = ДанныеПоставщика.Наименование;
		ТелефонПоставщика                = ДанныеПоставщика.Телефон;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПоставщик(ПризнакАгента)
	ПризнакиПоставщика = Новый Массив;
	ПризнакиПоставщика.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Комиссионер"));
	ПризнакиПоставщика.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Агент"));
	ПризнакиПоставщика.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Поверенный"));
	
	Возврат (ПризнакиПоставщика.Найти(ПризнакАгента) <> Неопределено);
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПлатежныйАгент(ПризнакАгента)
	ПризнакиПлатежныйАгент = Новый Массив;
	ПризнакиПлатежныйАгент.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйАгент"));
	ПризнакиПлатежныйАгент.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйСубАгент"));
	
	Возврат (ПризнакиПлатежныйАгент.Найти(ПризнакАгента) <> Неопределено);
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоБанковскийАгент(ПризнакАгента)
	ПризнакиБанковскийАгент = Новый Массив;
	ПризнакиБанковскийАгент.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент"));
	ПризнакиБанковскийАгент.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент"));
	
	Возврат (ПризнакиБанковскийАгент.Найти(ПризнакАгента) <> Неопределено);
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаРеквизитыПоставщика.Видимость        = (Форма.СобственныеТоварыУслуги = 0) И ЭтоПоставщик(Форма.ПризнакАгента);
	Элементы.ГруппаРеквизитыПлатежногоАгента.Видимость  = (Форма.СобственныеТоварыУслуги = 0) И ЭтоПлатежныйАгент(Форма.ПризнакАгента);
	Элементы.ГруппаРеквизитыБанковскогоАгента.Видимость = (Форма.СобственныеТоварыУслуги = 0) И ЭтоБанковскийАгент(Форма.ПризнакАгента);
	
	Если Форма.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Комиссионер") Тогда
		Элементы.ГруппаРеквизиты.Заголовок = НСтр("ru = 'Реквизиты комитента'");
	ИначеЕсли Форма.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Агент") Тогда
		Элементы.ГруппаРеквизиты.Заголовок = НСтр("ru = 'Реквизиты принципала'");
	ИначеЕсли Форма.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Поверенный") Тогда
		Элементы.ГруппаРеквизиты.Заголовок = НСтр("ru = 'Реквизиты доверителя'");
	КонецЕсли;
	
	Элементы.ПризнакАгента.Доступность = (Форма.СобственныеТоварыУслуги = 0);
КонецПроцедуры
#КонецОбласти 




