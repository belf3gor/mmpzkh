#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОписаниеСсылок();
	ПоказыватьПриОткрытииПрограммы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НачалоРаботыПредпринимательВСервисе", "Показывать", Истина);
	Если ПолучитьФункциональнуюОпцию("РегистрацияНеЗавершена") Тогда
		ДанныеПомощникаРегистрации = Обработки.РегистрацияОрганизации.ДанныеПомощникаРегистрации();
		Организация = ДанныеПомощникаРегистрации.Заявление.Ссылка;
	Иначе
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ПодготовитьТекстСпискаДействий();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ЗаписатьНовыеСобытияВЖурналРегистрации", 60, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНовыеСобытияВЖурналРегистрации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокДействийПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПерейтиПоСсылке(ДанныеСобытия.Href);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПриОткрытииПрограммыПриИзменении(Элемент)
	
	СохранитьНастройкиПоказыватьПриОткрытииПрограммы(ПоказыватьПриОткрытииПрограммы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПодготовитьТекстСпискаДействий();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СообщитьОНенайденном(Команда)
	
	Если ПустаяСтрока(ТекстСообщения) Тогда
		ТекстПредупреждения = НСтр("ru='Напишите, что не удалось найти.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		ТекущийЭлемент = Элементы.ТекстСообщения;
		Возврат;
	КонецЕсли;
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		СобытиеЖурналаРегистрацииСообщение(),
		,
		ТекстСообщения,
		,
		Истина); // Сообщение сразу записываем.
	ПоказатьОповещениеПользователя(НСтр("ru='Сообщение отправлено'"), , ТекстСообщения);
	
	ТекстСообщения = "";	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьТекстСпискаДействий()
	
	ВидОрганизации = Неопределено;
	Если ЗначениеЗаполнено(Организация) Тогда
		ВидОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо");
	КонецЕсли;
	ЭтоЮрЛицо = ВидОрганизации = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	Макет = Обработки.НачалоРаботы.ПолучитьМакет("НачалоРаботыВСервисе");
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	ПоказатьУСН    = УчетнаяПолитика.ПрименяетсяУСН(Организация, ТекущаяДата);
	ПоказатьЕНВД   = УчетнаяПолитика.ПлательщикЕНВД(Организация, ТекущаяДата);
	ПоказатьПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, ТекущаяДата);
	Показать1СОтчетность = НЕ ИнтерфейсыВзаимодействияБРО.ОрганизацияИмеетУчетнуюЗапись(Организация);
	ПоказатьИнструкциюПоРегистрации = ПолучитьФункциональнуюОпцию("РегистрацияНеЗавершена");
	ПоказатьИнструкциюПоИзменениямЕГРЮЛ = ПолучитьФункциональнуюОпцию("ИзмененияЕГРЗавершены") И ЭтоЮрЛицо;
	ПоказатьИнструкциюПоИзменениямЕГРИП = ПолучитьФункциональнуюОпцию("ИзмененияЕГРЗавершены") И НЕ ЭтоЮрЛицо;
	АдресДемоБазы = Константы.АдресДемобазы.Получить();
	ПоказатьСсылкуНаДемоБазу = ПоказатьИнструкциюПоРегистрации И ЗначениеЗаполнено(АдресДемоБазы);
	ПоказатьДляЮридическогоЛица = ПоказатьИнструкциюПоРегистрации И ЭтоЮрЛицо;
	
	ДокументHTML = Макет.ПолучитьДокументHTML();
	Если НЕ ПоказатьИнструкциюПоРегистрации Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "Регистрация");
	КонецЕсли;
	Если НЕ ПоказатьИнструкциюПоИзменениямЕГРЮЛ Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "ИзмененияЕГРЮЛ");
	КонецЕсли;
	Если НЕ ПоказатьИнструкциюПоИзменениямЕГРИП Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "ИзмененияЕГРИП");
	КонецЕсли;
	Если НЕ ПоказатьИнструкциюПоИзменениямЕГРЮЛ И НЕ ПоказатьИнструкциюПоИзменениямЕГРИП Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "ВводНачальныхОстатков");
	КонецЕсли;
	Если НЕ ПоказатьСсылкуНаДемоБазу Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "Демоданные");
	КонецЕсли;
	Если НЕ ПоказатьДляЮридическогоЛица Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "ЮридическоеЛицо");
	КонецЕсли;
	Если НЕ ПоказатьУСН Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "УСН");
	КонецЕсли;
	Если НЕ ПоказатьЕНВД Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "ЕНВД");
	КонецЕсли;
	Если НЕ ПоказатьПатент Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "Патент");
	КонецЕсли;
	Если НЕ Показать1СОтчетность Тогда
		УдалитьБлокHTMLПоИдентификатору(ДокументHTML, "1СОтчетность");
	КонецЕсли;
	СписокДействий = ПолучитьТекстДокументаHTML(ДокументHTML);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеСсылок()

	ВсеСсылки = Новый Соответствие;
	
	// Регистрация
	// Регистрация.ПомощникРегистрации, Регистрация.РеквизитыОрганизации,
	// Регистрация.ПриказВступленииВДолжностьРуководителя - см. ПерейтиПоСсылке.
	
	// Перед началом работы
	ВсеСсылки.Вставить("ПередНачаломРаботы.ВводНачальныхОстатков", "e1cib/app/Обработка.ВводНачальныхОстатков");
	ВсеСсылки.Вставить("ПередНачаломРаботы.НалогиОтчеты", "e1cib/command/ОбщаяКоманда.НалогиИОтчеты");
	
	// Демоданные
	ВсеСсылки.Вставить("Демоданные.ОткрытьДемоданные", Константы.АдресДемобазы.Получить());
	
	// Деньги
	ВсеСсылки.Вставить("Деньги.ДосьеКонтрагента",   "e1cib/command/ОбщаяКоманда.ДосьеНовогоКонтрагента");
	
	// Документы
	ВсеСсылки.Вставить("Документы.ШаблоныДоговоров",          "e1cib/list/Справочник.ШаблоныДоговоров");
	ВсеСсылки.Вставить("Документы.НастройкиЭлектроннойПочты", "e1cib/list/Справочник.УчетныеЗаписиЭлектроннойПочты");
	
	// Товары
	ВсеСсылки.Вставить("Товары.Номенклатура",   "e1cib/list/Справочник.Номенклатура");
	
	// Другие возможности программы
	ВсеСсылки.Вставить("ДругиеВозможности.МобильноеПриложение", 
		"e1cib/app/ПланОбмена.МобильноеПриложениеПредприниматель.Форма.ФормаСинхронизацииСМобильнымПриложением");
	ВсеСсылки.Вставить("ДругиеВозможности.Функциональность", "e1cib/app/Обработка.ФункциональностьПрограммы");
	
	// Помощь
	ВсеСсылки.Вставить("Помощь.ОбращениеВСлужбуПоддержки",
		"e1cib/app/Обработка.ИнформационныйЦентр.Форма.ОбращенияВСлужбуПоддержки");
	ВсеСсылки.Вставить("Помощь.Видеоролики",     "http://its.1c.ru/clip8/?clip=buh8");
	ВсеСсылки.Вставить("Помощь.Бухобслуживание", "https://1cbo.ru/");
	
	ОписаниеСсылок = Новый ФиксированноеСоответствие(ВсеСсылки);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПерейтиПоСсылке(ПолнаяСсылкаHTML)
	
	Если НЕ ЗначениеЗаполнено(ПолнаяСсылкаHTML) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиСсылкиHTML   = СтрРазделить(ПолнаяСсылкаHTML, "/", Ложь);
	СсылкаHTML        = ЧастиСсылкиHTML.Получить(ЧастиСсылкиHTML.Количество() - 1);
	СсылкаДляПерехода = ОписаниеСсылок.Получить(СсылкаHTML);
	Если СсылкаДляПерехода <> Неопределено Тогда
		ПерейтиПоНавигационнойСсылке(СсылкаДляПерехода);
	Иначе
		Если СсылкаHTML = "Документы.НовыйАкт" Тогда
			ЗначенияЗаполнения = Новый Структура("ВидОперации, Организация", 
				ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Услуги"),
				Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Документы.НоваяНакладная" Тогда
			ЗначенияЗаполнения = Новый Структура("ВидОперации, Организация", 
				ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары"),
				Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Расходы.НовыйАктПоставщика" Тогда
			ЗначенияЗаполнения = Новый Структура("ВидОперации, Организация", 
				ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги"),
				Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Товары.НоваяНакладнаяПоставщика"
			ИЛИ СсылкаHTML = "Расходы.НоваяНакладнаяПоставщика" Тогда
			ЗначенияЗаполнения = Новый Структура("ВидОперации, Организация", 
				ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Товары"),
				Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Отчетность.Подключение1СОтчетности" Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(Организация);
			
		ИначеЕсли СсылкаHTML = "Деньги.ПодключениеДиректБанка" Тогда
			ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(Организация, Неопределено);
			
		ИначеЕсли СсылкаHTML = "Документы.Логотип" Тогда
			Если ЗначениеЗаполнено(Организация) Тогда
				ПараметрыФормы = Новый Структура("Ключ", Организация);
				ФормаОрганизации = ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
				Если ФормаОрганизации.Элементы.Найти("ГруппаЛоготипИПечать") <> Неопределено Тогда
					ФормаОрганизации.ТекущийЭлемент = ФормаОрганизации.Элементы.ГруппаЛоготипИПечать;
				КонецЕсли;
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите организацию.'"),, "Организация");
			КонецЕсли;
		ИначеЕсли СсылкаHTML = "Регистрация.ПомощникРегистрации" Тогда
			
			РегистрацияОрганизацииКлиент.ОткрытьПомощникРегистрации();
			
		ИначеЕсли СсылкаHTML = "Регистрация.РеквизитыОрганизации" Тогда
			
			ПараметрыФормы = Новый Структура("Ключ", Организация);
			ФормаОрганизации = ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
			Если ФормаОрганизации.Элементы.Найти("ИНН") <> Неопределено Тогда
				ФормаОрганизации.ТекущийЭлемент = ФормаОрганизации.Элементы.ИНН;
			КонецЕсли;
			
		ИначеЕсли СсылкаHTML = "Регистрация.ПриказВступленииВДолжностьРуководителя" Тогда
			
			ПараметрыПечати = Новый Структура("ЗаголовокФормы", НСтр("ru = 'Приказ о вступлении в должность руководителя'"));
			ПараметрКоманды = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Справочник.Организации", "ПриказВступленииВДолжностьРуководителя", ПараметрКоманды, ЭтотОбъект, ПараметрыПечати);
			
		ИначеЕсли СсылкаHTML = "ИзмененияЕГРИП.ПомощникВнесенияИзмененийЕГР"
			Или СсылкаHTML = "ИзмененияЕГРЮЛ.ПомощникВнесенияИзмененийЕГР" Тогда
			
			ПараметрыПомощника = РегистрацияОрганизацииКлиентСервер.НовыеПараметрыПомощникаВнесенияИзменений();
			ПараметрыПомощника.Организация = Организация;
			ПараметрыПомощника.КонтекстныйВызов = Истина;
			
			РегистрацияОрганизацииКлиент.ОткрытьПомощникВнесенияИзменений(ПараметрыПомощника);
			
		ИначеЕсли СсылкаHTML = "ИзмененияЕГРИП.РеквизитыОрганизации"
			Или СсылкаHTML = "ИзмененияЕГРЮЛ.РеквизитыОрганизации" Тогда
			
			ПараметрыФормы = Новый Структура("Ключ", Организация);
			ФормаОрганизации = ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Документы.НовыйДоговор" Тогда
			
			ЗначенияЗаполнения = Новый Структура("Организация", Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Документы.НовыйСчет" Тогда
			
			ЗначенияЗаполнения = Новый Структура("Организация", Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Отчетность.ПомощникУСН" Тогда
			
			ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма", Новый Структура("Организация", Организация));
			
		ИначеЕсли СсылкаHTML = "Отчетность.ПомощникЕНВД" Тогда
			
			ОткрытьФорму("Обработка.ПомощникРасчетаЕНВД.Форма", Новый Структура("Организация", Организация));
			
		ИначеЕсли СсылкаHTML = "Отчетность.НовыйПатент" Тогда
			
			ЗначенияЗаполнения = Новый Структура("Владелец", Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Справочник.Патенты.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Отчетность.СписокЗадач" Тогда
			
			ОткрытьФорму("ОбщаяФорма.СписокЗадач", Новый Структура("Организация", Организация));
			
		ИначеЕсли СсылкаHTML = "Отчетность.КнигаДоходовПатент" Тогда
			
			ОткрытьОтчет("Отчет.КнигаУчетаДоходовПатент.ФормаОбъекта");
			
		ИначеЕсли СсылкаHTML = "Аналитика.Монитор" Тогда
			
			ОткрытьФорму("ОбщаяФорма.МониторОсновныхПоказателей", Новый Структура("Организация", Организация));
			
		ИначеЕсли СсылкаHTML = "Аналитика.ДоходыРасходы" Тогда
			
			ОткрытьОтчет("Отчет.ДоходыРасходы.ФормаОбъекта");
			
		ИначеЕсли СсылкаHTML = "Аналитика.НеоплаченныеСчета" Тогда
			
			ОткрытьОтчет("Отчет.АнализНеоплаченныхСчетовПокупателям.ФормаОбъекта");
			
		ИначеЕсли СсылкаHTML = "Аналитика.ЗадолженностьПокупателей" Тогда
			
			ОткрытьОтчет("Отчет.ЗадолженностьПокупателей.ФормаОбъекта");
			
		ИначеЕсли СсылкаHTML = "Деньги.ПлатежныйКалендарь" Тогда
			
			ОткрытьФорму("Обработка.ПлатежныйКалендарь.Форма", Новый Структура("Организация", Организация));
			
		ИначеЕсли СсылкаHTML = "Деньги.БанковскиеВыписки" Тогда
			
			Отбор = Новый Структура("Организация", Организация);
			ПараметрыФормы = Новый Структура("Отбор", Отбор);
			ОткрытьФорму("ЖурналДокументов.Деньги.Форма.БанковскиеВыписки", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Товары.ОстаткиТоваров" Тогда
			
			ОткрытьОтчет("Отчет.ОстаткиТоваров.ФормаОбъекта");
			
		ИначеЕсли СсылкаHTML = "Расходы.КассовыеДокументы" Тогда
			
			Отбор = Новый Структура("Организация", Организация);
			ПараметрыФормы = Новый Структура("Отбор", Отбор);
			ОткрытьФорму("ЖурналДокументов.Деньги.Форма.КассовыеДокументы", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Расходы.НовыйАвансовыйОтчет" Тогда
			
			ЗначенияЗаполнения = Новый Структура("Организация", Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.АвансовыйОтчет.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "Расходы.НовоеПлатежноеПоручение" Тогда
			
			ЗначенияЗаполнения = Новый Структура("Организация", Организация);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли СсылкаHTML = "" Тогда
		
		Иначе
			ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
				СобытиеЖурналаРегистрацииНеизвестнаяСсылка(),
				"Ошибка",
				СсылкаHTML,
				,
				Истина); // Сразу записываем.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		СобытиеЖурналаРегистрацииПереходПоСсылке(СсылкаHTML));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(ИмяОтчета)
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("Организация", Организация);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("РежимРасшифровки",          Истина);
	ПараметрыОтчета.Вставить("ВидРасшифровки",            2);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму(ИмяОтчета, ПараметрыОтчета, ЭтотОбъект);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиПоказыватьПриОткрытииПрограммы(ПоказыватьПриОткрытии)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НачалоРаботыПредпринимательВСервисе", "Показывать", ПоказыватьПриОткрытии);
	
	СтруктураСообщения = Новый Структура;
	СтруктураСообщения.Вставить("ИмяСобытия", СобытиеЖурналаРегистрацииПоказыватьПриОткрытии(ПоказыватьПриОткрытии));
	СтруктураСообщения.Вставить("ПредставлениеУровня", "");
	СтруктураСообщения.Вставить("Комментарий", "");
	СтруктураСообщения.Вставить("ДатаСобытия", "");
	СобытияЖурнала = Новый СписокЗначений;
	СобытияЖурнала.Добавить(СтруктураСообщения);
	ЖурналРегистрации.ЗаписатьСобытияВЖурналРегистрации(СобытияЖурнала);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СобытиеЖурналаРегистрацииПереходПоСсылке(НавигационнаяСсылка)

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Переход по ссылке.%2'", КодЯзыка);
	Возврат СтрШаблон(ШаблонТекста, ГруппаСобытийЖурналаРегистрации(), НавигационнаяСсылка);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СобытиеЖурналаРегистрацииСообщение()

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Сообщение'", КодЯзыка);
	Возврат СтрШаблон(ШаблонТекста, ГруппаСобытийЖурналаРегистрации());

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СобытиеЖурналаРегистрацииПоказыватьПриОткрытии(ПоказыватьПриОткрытии)
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Показывать при открытии.%2'", КодЯзыка);
	ТекстПоказывать = ?(ПоказыватьПриОткрытии, НСтр("ru='Да'", КодЯзыка), НСтр("ru='Нет'", КодЯзыка));
	Возврат СтрШаблон(ШаблонТекста, ГруппаСобытийЖурналаРегистрации(), ТекстПоказывать);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СобытиеЖурналаРегистрацииНеизвестнаяСсылка()

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Неизвестная ссылка'", КодЯзыка);
	Возврат СтрШаблон(ШаблонТекста, ГруппаСобытийЖурналаРегистрации());

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ГруппаСобытийЖурналаРегистрации()
	
	КодЯзыка  = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	Возврат НСтр("ru='Начало работы Предприниматель в сервисе'", КодЯзыка);

КонецФункции

&НаКлиенте
Процедура ЗаписатьНовыеСобытияВЖурналРегистрации()

	НезаписанныеСобытия = ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"];
	Если ТипЗнч(НезаписанныеСобытия) = Тип("СписокЗначений")
		И НезаписанныеСобытия.Количество() > 0 Тогда
		ЖурналРегистрацииВызовСервера.ЗаписатьСобытияВЖурналРегистрации(НезаписанныеСобытия);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьБлокHTMLПоИдентификатору(ДокументHTML, ИдентификаторБлока)

	УдаляемыйБлок = ДокументHTML.ПолучитьЭлементПоИдентификатору(ИдентификаторБлока);
	Если УдаляемыйБлок <> Неопределено Тогда
		УдаляемыйБлок.РодительскийУзел.УдалитьДочерний(УдаляемыйБлок);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстДокументаHTML(ДокументHTML)
	
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	Возврат ЗаписьHTML.Закрыть();
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОткрытШагПомощника_РегистрацияОрганизации" И Параметр = Неопределено Тогда
		
		ПодготовитьТекстСпискаДействий();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
