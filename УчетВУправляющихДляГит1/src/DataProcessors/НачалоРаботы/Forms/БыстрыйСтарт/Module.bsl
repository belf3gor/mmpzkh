
&НаКлиенте
Перем ПараметрыОбработчикаОжидания; 

&НаКлиенте
Перем ФормаВыбораВидаОрганизации;

&НаКлиенте
Перем ФормаВыбораСистемыНалогообложения;

&НаКлиенте
Перем УИДЗамера;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОрганизацияСуществует = ОрганизацияСуществует();
	
	ИнициализироватьСвойстваОрганизации(ЭтотОбъект);
	
	РазрешенУчетРегулярнойДеятельности = ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности();
	
	Если ОбщегоНазначения.РазделениеВключено()
		Или Обработки.РегистрацияОрганизации.ИспользуетсяСервисРегистрации() Тогда
		
		ПодключенаИнтернетПоддержка = Истина;
		ПодключенСервис1СКонтрагент = Истина;
		
		Если ОрганизацияСуществует Тогда
			ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаВидБизнеса");
		Иначе
			ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаИНН");
		КонецЕсли;
	Иначе
		ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаПодключениеИнтернетПоддержки");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ЛогинПарольИПП = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	Если ЛогинПарольИПП <> Неопределено Тогда
		
		ЛогинИнтернетПоддержки		 = ЛогинПарольИПП.Логин;
		ПарольИнтернетПоддержки		 = ЛогинПарольИПП.Пароль;
		
	КонецЕсли;
	
	СистемыНалогообложенияДопускающиеСовмещение.ЗагрузитьЗначения(Обработки.НачалоРаботы.СистемыНалогообложенияДопускающиеСовмещение());
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.ЗагрузитьЗначения(Обработки.НачалоРаботы.СистемыНалогообложенияДопускающиеТрудНаемныхРаботников());
	
	ПоказатьНачалоРаботыВСервисе = ОбщегоНазначения.РазделениеВключено()
		И РольДоступна("ПолныеПрава")
		И ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НачалоРаботыПредпринимательВСервисе", "Показывать", Истина);
	
	ЗаполнитьДоступныеВидыБизнеса();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "БыстрыйСтартЗакрытиеПоОповещению");
		УстановитьИнтерфейс();
		ОбновитьИнтерфейс();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИННИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьКомментарийНекорректныйИНН");
	
	Элемент.ОтметкаНезаполненного = Ложь;
	
	ТекстИНН = СокрЛП(Текст);
	
	ВидОрганизации = ?(ЭтоЮрЛицо(ТекстИНН), "ЮридическоеЛицо", "ИндивидуальныйПредприниматель");
	
	ДлинаИНН = СтрДлина(ТекстИНН);
	
	Если ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
		Реквизиты = Неопределено;
		ИННКомментарий = "";
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ТекстИНН, ЭтоЮрЛицо(ТекстИНН));
	
	Если РезультатПроверки.СоответствуетТребованиям Тогда
		
		Реквизиты = ПолучитьРеквизитыПоИНН(ТекстИНН, ЭтоЮрЛицо(ТекстИНН));
		
		Если Реквизиты <> Неопределено Тогда
			
			ИННКомментарий = Новый ФорматированнаяСтрока(СокращенноеНаименование(Реквизиты.НаименованиеСокращенное));
			
		КонецЕсли;
		
	Иначе
		
		Если ДлинаИНН = 12 Тогда
			УстановитьКомментарийНекорректныйИНН();
		Иначе
			ПодключитьОбработчикОжидания("Подключаемый_УстановитьКомментарийНекорректныйИНН", 2, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОрганизацииРасширеннаяПодсказкаНажатие(Элемент)
	
	Если ФормаВыбораВидаОрганизации <> Неопределено И ФормаВыбораВидаОрганизации.Открыта() Тогда
		ФормаВыбораВидаОрганизации.Закрыть();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимПовторногоВыбораВидаОрганизации", Истина);
	ПараметрыФормы.Вставить("ВидОрганизации",                       ВидОрганизации);

	ФормаВыбораВидаОрганизации = ПолучитьФорму("Справочник.Организации.Форма.ФормаВыбораСистемыНалогообложения", 
		ПараметрыФормы,
		ЭтотОбъект,
		"ФормаВыбораСистемыНалогообложения_ВыборВидаОрганизации_БыстрыйСтарт");
		
	ФормаВыбораВидаОрганизации.ЗакрыватьПриЗакрытииВладельца = Истина;
	ФормаВыбораВидаОрганизации.ОписаниеОповещенияОЗакрытии = 
		Новый ОписаниеОповещения("ОбновитьВидОрганизации", ЭтотОбъект);
			
	ФормаВыбораВидаОрганизации.Открыть();
			
КонецПроцедуры

&НаКлиенте
Процедура ГруппаОсновнаяСистемаНалогообложенияРасширеннаяПодсказкаНажатие(Элемент)
	
	Если ФормаВыбораСистемыНалогообложения <> Неопределено И ФормаВыбораСистемыНалогообложения.Открыта() Тогда
		ФормаВыбораСистемыНалогообложения.Закрыть();
	КонецЕсли;
	
	ПризнакПрименяетсяУСНПатент = Ложь;
	ПризнакПлательщикЕНВД = Ложь;
	
	Если Найти(СистемаНалогообложения, "Упрощенная") Тогда
		ВидСистемыНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
		Если СистемаНалогообложения = "УпрощеннаяДоходы" Тогда
			ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы");
		Иначе
			ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы");
		КонецЕсли;
	ИначеЕсли СистемаНалогообложения = "Патентная" Тогда
		ВидСистемыНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок");
		ПризнакПрименяетсяУСНПатент = Истина;
	ИначеЕсли СистемаНалогообложения = "ЕНВД" Тогда
		ВидСистемыНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок");
		ПризнакПлательщикЕНВД = Истина;
	ИначеЕсли СистемаНалогообложения = "НалогНаПрофессиональныйДоход" Тогда
		ВидСистемыНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход");
	Иначе
		ВидСистемыНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая");
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимПовторногоВыбораСистемыНалогообложения", Истина);
	ПараметрыФормы.Вставить("ВидОрганизации",                              ВидОрганизации);
	ПараметрыФормы.Вставить("СистемаНалогообложения",                      ВидСистемыНалогообложения);
	ПараметрыФормы.Вставить("ОбъектНалогообложенияУСН",                    ОбъектНалогообложенияУСН);
	ПараметрыФормы.Вставить("ПрименяетсяУСНПатент",                        ПризнакПрименяетсяУСНПатент);
	ПараметрыФормы.Вставить("ПлательщикЕНВД",                              ПризнакПлательщикЕНВД);
	
	ФормаВыбораСистемыНалогообложения = ПолучитьФорму("Справочник.Организации.Форма.ФормаВыбораСистемыНалогообложения", 
		ПараметрыФормы,
		ЭтотОбъект,
		"ФормаВыбораСистемыНалогообложения_ВыборСистемыНалогообложения_БыстрыйСтарт");
		
	ФормаВыбораСистемыНалогообложения.ЗакрыватьПриЗакрытииВладельца = Истина;
	ФормаВыбораСистемыНалогообложения.ОписаниеОповещенияОЗакрытии = 
		Новый ОписаниеОповещения("ОбновитьСистемуНалогообложения", ЭтотОбъект);
	
	ФормаВыбораСистемыНалогообложения.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	
	ИнициализироватьСистемыНалогообложения();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродолжитьСИНН(Команда)
	
	Элементы.ИНН.ОбновитьТекстРедактирования();
	
	Если НЕ ПроверитьЗаполнениеИНН() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораСистемНалогообложения();
	
	ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаСистемаНалогообложения");
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьБезИНН(Команда)
	
	ПерейтиНаСтраницуВидОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьВидОрганизации(Команда)
	
	ЗаполнитьСписокВыбораСистемНалогообложения();
	
	ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаСистемаНалогообложения");
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьСистемаНалогообложения(Команда)
	
	ПропускатьВыборВидаБизнеса = ИспользуетсяПолнаяФункциональность()
		Или СистемаНалогообложения = "НалогНаПрофессиональныйДоход"
		Или Не РазрешенУчетРегулярнойДеятельности;
	
	Если ПропускатьВыборВидаБизнеса Тогда
		СоздатьОрганизацию();
	Иначе
		ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаВидБизнеса");
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ИнициализироватьСвойстваОрганизации(ЭтотОбъект);
	
	Если Не ПодключенаИнтернетПоддержка Тогда
		
		ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаПодключениеИнтернетПоддержки");
		
	ИначеЕсли ПодключенаИнтернетПоддержка И Не ПодключенСервис1СКонтрагент Тогда
		
		ПерейтиНаСтраницуВидОрганизации();
		
	Иначе
		
		ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаИНН");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключиться(Команда)
	
	Если Не ЗначениеЗаполнено(ЛогинИнтернетПоддержки) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Введите логин'"),, "ЛогинИнтернетПоддержки");
		
		Возврат;
		
	ИначеЕсли Не ЗначениеЗаполнено(ПарольИнтернетПоддержки) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Введите пароль'"),, "ПарольИнтернетПоддержки");
		
		Возврат;
		
	КонецЕсли;
	
	ПодключитьИнтернетПоддержку();
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПодключитьсяПозжеНажатие(Элемент)
	
	ПерейтиНаСтраницуВидОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаВосстановлениеПароляНажатие(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьВебСтраницу(
		ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin("/remind_request"),
		НСтр("ru = 'Восстановление пароля'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаРегистрацияНажатие(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьВебСтраницу(
		ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin("/registration"),
		НСтр("ru = 'Регистрация'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидБизнеса(Команда)
	
	ВидБизнеса = ВыбранныйВидБизнеса(ТекущийЭлемент.Имя);
	
	СоздатьОрганизацию();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяПолнаяФункциональность()
	
	Возврат Обработки.НачалоРаботы.ИспользуетсяПолнаяФункциональность();
	
КонецФункции

&НаКлиенте
Процедура ПропуститьВыборВидаБизнеса(Команда)
	
	ВидБизнеса = ПредопределенноеЗначение("Перечисление.ВидыБизнеса.Другое");
	
	СоздатьОрганизацию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЭтоИндивидуальныйПредприниматель = (Форма.ВидОрганизации <> "ЮридическоеЛицо");
	
	Элементы.ПлательщикЕНВД.Видимость = Форма.РазрешенУчетРегулярнойДеятельности
		И Форма.СистемыНалогообложенияДопускающиеСовмещение.НайтиПоЗначению(Форма.СистемаНалогообложения) <> Неопределено;
	Элементы.ПрименяетсяУСНПатент.Видимость = Форма.РазрешенУчетРегулярнойДеятельности
		И Форма.СистемыНалогообложенияДопускающиеСовмещение.НайтиПоЗначению(Форма.СистемаНалогообложения) <> Неопределено
		И ЭтоИндивидуальныйПредприниматель;
	Элементы.ТорговыйСбор.Видимость = Форма.РазрешенУчетРегулярнойДеятельности
		И Форма.СистемыНалогообложенияДопускающиеСовмещение.НайтиПоЗначению(Форма.СистемаНалогообложения) <> Неопределено;
	
	Элементы.ИспользуетсяТрудНаемныхРаботников.Видимость = ЭтоИндивидуальныйПредприниматель
		И Форма.СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.НайтиПоЗначению(Форма.СистемаНалогообложения) <> Неопределено;
	
	Элементы.ОписаниеСистемаНалогообложения.Видимость = ПустаяСтрока(Форма.ИНН);
	Элементы.ОписаниеВидОрганизации.Видимость = ПустаяСтрока(Форма.ИНН);
	
	Элементы.ВидОрганизации.ОтображениеПодсказки = ?(ПустаяСтрока(Форма.ИНН),
		ОтображениеПодсказки.ОтображатьСнизу,
		ОтображениеПодсказки.Нет);
	Элементы.ГруппаОсновнаяСистемаНалогообложения.ОтображениеПодсказки = ?(ПустаяСтрока(Форма.ИНН),
		ОтображениеПодсказки.ОтображатьСнизу,
		ОтображениеПодсказки.Нет);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПерейтиНаСтраницу(Форма, АктивнаяСтраница)
	
	Форма.АктивнаяСтраница = АктивнаяСтраница;
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы[Форма.АктивнаяСтраница];
	
	Если Форма.АктивнаяСтраница = "СтраницаПодключениеИнтернетПоддержки" Тогда
		
		Элементы.Подключиться.КнопкаПоУмолчанию = Истина;
		Элементы.ГруппаОжиданиеПодключения.Видимость = Ложь;
		Форма.ТекущийЭлемент = Элементы.ЛогинИнтернетПоддержки;
		
	ИначеЕсли Форма.АктивнаяСтраница = "СтраницаИНН" Тогда
		
		Элементы.ПродолжитьСИНН.КнопкаПоУмолчанию = Истина;
		Форма.ТекущийЭлемент = Элементы.ИНН;
		
	ИначеЕсли Форма.АктивнаяСтраница = "СтраницаВидОрганизации" Тогда
		
		Элементы.ПродолжитьВидОрганизации.КнопкаПоУмолчанию = Истина;
		Форма.ТекущийЭлемент = Элементы.ВидОрганизации;
		// Если интернет поддержка уже подключена, но сервис 1С: Контрагент недоступен, то на странице с выбором
		// вида организации не отображается кнопка "Отмена", т.к. возврат к предыдущем страницам не требуется
		ПоддержкаПодключенаБезСервиса1СКонтрагент = Форма.ПодключенаИнтернетПоддержка И Не Форма.ПодключенСервис1СКонтрагент;
		Элементы.ОтменаВидОрганизации.Видимость = Не ПоддержкаПодключенаБезСервиса1СКонтрагент;
		
	ИначеЕсли Форма.АктивнаяСтраница = "СтраницаСистемаНалогообложения" Тогда
		
		Элементы.ПродолжитьСистемаНалогообложения.КнопкаПоУмолчанию = Истина;
		Форма.ТекущийЭлемент = Элементы.СистемаНалогообложения;
		Форма.ИспользуетсяТрудНаемныхРаботников = ОрганизацииФормыКлиентСервер.ИндивидуальныйПредпринимательЗарегистрированВФСС(Форма.Реквизиты);
		
	КонецЕсли;
	
	Элементы.ГруппаОжидание.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораСистемНалогообложения()
	
	ЭтоИндивидуальныйПредприниматель = (ВидОрганизации <> "ЮридическоеЛицо");
	
	Элементы.СистемаНалогообложения.СписокВыбора.Очистить();
	Если ЭтоИндивидуальныйПредприниматель
		И РазрешенУчетРегулярнойДеятельности
		И ТекущаяДатаСеанса() >= НалогНаПрофессиональныйДоходКлиентСервер.ДатаНачалаЭксперимента() Тогда
		Элементы.СистемаНалогообложения.СписокВыбора.Добавить("НалогНаПрофессиональныйДоход", НСтр("ru='Налог на профессиональный доход (""самозанятые"")'"));
	КонецЕсли;
	Элементы.СистемаНалогообложения.СписокВыбора.Добавить("УпрощеннаяДоходы", НСтр("ru='УСН 6%'"));
	Элементы.СистемаНалогообложения.СписокВыбора.Добавить("УпрощеннаяДоходыМинусРасходы", НСтр("ru='УСН 15%'"));
	Если ЭтоИндивидуальныйПредприниматель И РазрешенУчетРегулярнойДеятельности Тогда
		Элементы.СистемаНалогообложения.СписокВыбора.Добавить("Патентная", НСтр("ru='Только патент'"));
		Элементы.СистемаНалогообложения.СписокВыбора.Добавить("ЕНВД", НСтр("ru='Только ЕНВД'"));
	КонецЕсли;
	Если ЭтоИндивидуальныйПредприниматель Тогда
		Элементы.СистемаНалогообложения.СписокВыбора.Добавить("Общая", НСтр("ru='Общая (НДФЛ)'"));
	Иначе
		Элементы.СистемаНалогообложения.СписокВыбора.Добавить("Общая", НСтр("ru='Общая (налог на прибыль)'"));
	КонецЕсли;
	
	Если Элементы.СистемаНалогообложения.СписокВыбора.НайтиПоЗначению(СистемаНалогообложения) = Неопределено
		И ЗначениеЗаполнено(Элементы.СистемаНалогообложения.СписокВыбора) Тогда
		СистемаНалогообложения = Элементы.СистемаНалогообложения.СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидОрганизации(ВыбранныйВидОрганизации, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранныйВидОрганизации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ВидОрганизации.СписокВыбора.НайтиПоЗначению(ВыбранныйВидОрганизации) <> Неопределено Тогда
		ВидОрганизации = ВыбранныйВидОрганизации;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСистемуНалогообложения(ПараметрыУчетнойПолитики, ДополнительныеПараметры = Неопределено) Экспорт
	
	ФормаВыбораСистемыНалогообложения = Неопределено;
	
	Если ПараметрыУчетнойПолитики = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУчетнойПолитики.ПрименяетсяУСНДоходы Тогда
		СистемаНалогообложения = "УпрощеннаяДоходы"
	ИначеЕсли ПараметрыУчетнойПолитики.ПрименяетсяУСНДоходыМинусРасходы Тогда
		СистемаНалогообложения = "УпрощеннаяДоходыМинусРасходы"
	ИначеЕсли ПараметрыУчетнойПолитики.СистемаНалогообложения =
		ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок")
		И ПараметрыУчетнойПолитики.ПлательщикЕНВД Тогда
		СистемаНалогообложения = "ЕНВД"
	ИначеЕсли ПараметрыУчетнойПолитики.СистемаНалогообложения =
		ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок")
		И ПараметрыУчетнойПолитики.ПрименяетсяУСНПатент Тогда
		СистемаНалогообложения = "Патентная"
	ИначеЕсли ПараметрыУчетнойПолитики.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход") Тогда
		СистемаНалогообложения = "НалогНаПрофессиональныйДоход"
	Иначе
		СистемаНалогообложения = "Общая"
	КонецЕсли;
	
	ИнициализироватьСистемыНалогообложения();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомментарийНекорректныйИНН()
	
	ИННКомментарий = Новый ФорматированнаяСтрока(НСтр("ru='ИНН некорректный. Опечатка?'"),, Новый Цвет(255,0,0));	
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьКомментарийНекорректныйИНН()
	
	УстановитьКомментарийНекорректныйИНН();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
			ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
			ЗавершитьРаботуПомощника();
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ИзменитьТекстСообщенияОжидания()
	
	Элементы.ТекстОжидания.Заголовок = НСтр("ru='Еще чуть-чуть...'");
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеИНН()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(ИНН) Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru='ИНН'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "ИНН", Отказ);
		
	Иначе
			
		Если СтрДлина(ИНН) <> 10 И СтрДлина(ИНН) <> 12 Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", НСтр("ru='ИНН'"),,, 
				НСтр("ru='ИНН должен состоять из 10 или 12 цифр.'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "ИНН", Отказ);
			
		Иначе
		
			РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо(ИНН));
			
			Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
			
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", НСтр("ru='ИНН'"),,, 
					РезультатПроверки.ОписаниеОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "ИНН", Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ИНН.ОтметкаНезаполненного = Отказ;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_СоздатьОрганизацию()
	
	// Если организация уже существует в информационной базе,
	// то окно открывается только для выбора вида деятельности.
	// Поэтому реквизиты в данном случае не важны.
	Если ОрганизацияСуществует Тогда
		РезультатВыполнения = ЗапуститьУстановкуВидаБизнесаВФоне();
	Иначе
		РезультатВыполнения = ЗапуститьСозданиеОрганизациюВФоне();
	КонецЕсли;
	
	Если НЕ РезультатВыполнения.ЗаданиеВыполнено Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	Иначе
		ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
		ЗавершитьРаботуПомощника();
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция СоздатьОрганизациюНаСервере()
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ВидОрганизации",           ВидОрганизации);
	ПараметрыЗадания.Вставить("СистемаНалогообложения",   СистемаНалогообложения);
	ПараметрыЗадания.Вставить("Реквизиты",                Реквизиты);
	ПараметрыЗадания.Вставить("ПлательщикЕНВД",           ПлательщикЕНВД);
	ПараметрыЗадания.Вставить("ПрименяетсяУСНПатент",     ПрименяетсяУСНПатент);
	ПараметрыЗадания.Вставить("ПлательщикТорговогоСбора", ПлательщикТорговогоСбора);
	ПараметрыЗадания.Вставить("ИНН",                      ИНН);
	ПараметрыЗадания.Вставить("ИспользуетсяТрудНаемныхРаботников", ИспользуетсяТрудНаемныхРаботников);
	ПараметрыЗадания.Вставить("ВидБизнеса", ВидБизнеса);
	
	РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Справочники.Организации.СоздатьОрганизацию",
		ПараметрыЗадания,
		НСтр("ru='Создание организации'"));
		
	ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыПоИНН(ИНН, ЭтоЮрЛицо)
	
	Результат = Неопределено;
	
	Если ЭтоЮрЛицо Тогда
		Результат = РаботаСКонтрагентами.РеквизитыЮридическогоЛицаПоИНН(ИНН);
	Иначе
		Результат = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ИНН);
	КонецЕсли;
	
	Результат.Вставить("ЮридическоеФизическоеЛицо", 
		?(ЭтоЮрЛицо, 
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо, 
			Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо));
			
	Результат.Вставить("ОГРН", Результат.РегистрационныйНомер);
	
	Если Результат.Свойство("ИсторияРеквизитов") Тогда
		Результат.Удалить("ИсторияРеквизитов");
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СокращенноеНаименование(Наименование)
	
	Результат = Наименование;
	
	Результат = СтрЗаменить(Результат, "Общество с ограниченной ответственностью", "ООО");
	Результат = СтрЗаменить(Результат,"Акционерное общество", "АО");
	Результат = СтрЗаменить(Результат,"Публичное акционерное общество", "ПАО");
	Результат = СтрЗаменить(Результат, "Закрытое акционерное общество", "ЗАО");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЮрЛицо(ИНН)
	
	Возврат СтрДлина(ИНН) = 10;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементовОжиданияПодключения(Ожидание)
	
	Элементы.ГруппаПодключиться.Доступность         = НЕ Ожидание;
	Элементы.Подключиться.Видимость                 = НЕ Ожидание;
	Элементы.ГиперссылкаПодключитьсяПозже.Видимость = НЕ Ожидание;
	Элементы.ГруппаОжиданиеПодключения.Видимость    = Ожидание;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовОжиданияСозданияОрганизации(Ожидание)
	
	Элементы.СтраницаСистемаНалогообложения.Доступность = НЕ Ожидание;
	Элементы.ОтменаСистемаНалогообложения.Видимость = НЕ Ожидание;
	Элементы.ГруппаВидыБизнеса.Доступность = НЕ Ожидание;
	Элементы.ПропуститьВыборВидаБизнеса.Видимость = НЕ Ожидание;
	Элементы.ГруппаОжидание.Видимость = Ожидание;
	
КонецПроцедуры

&НаКлиенте
Функция ПодключитьИнтернетПоддержку()
	
	УстановитьДоступностьЭлементовОжиданияПодключения(Истина);
	
	Элементы.ГруппаОшибкаПодключения.Видимость = Ложь;
	
	ДлительнаяОперация = ПодключитсяКИнтернетПоддержкеНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецФункции

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		УстановитьДоступностьЭлементовОжиданияПодключения(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		УстановитьДоступностьЭлементовОжиданияПодключения(Ложь);
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	РезультатПодключения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Если РезультатПодключения = "ПодключениеУспешно" Тогда 
		
		ПодключенаИнтернетПоддержка = Истина;
		ПодключенСервис1СКонтрагент = Истина;
		
		// Переходим к вводу ИНН
		ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаИНН");
		
	ИначеЕсли РезультатПодключения = "Сервис1СКонтрагентНеПодключен" Тогда
		
		ПодключенаИнтернетПоддержка = Истина;
		ПодключенСервис1СКонтрагент = Ложь;
		ПерейтиНаСтраницуВидОрганизации();
		
	Иначе
		// Произошла ошибка
		УстановитьДоступностьЭлементовОжиданияПодключения(Ложь);
		
		Если РезультатПодключения = "ОшибкаАвторизации" Тогда
			
			ТекстОшибкиПодключения = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Неверный логин или пароль. Укажите правильные данные для авторизации.'"));
			
		Иначе
			
			МассивСтрок = Новый Массив;
			МассивСтрок.Добавить(НСтр("ru = 'При подключении к сервису возникла ошибка. Возможно, указаны неверные настройки'") + " ");
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'прокси-сервера'"),,,, "e1cib/app/ОбщаяФорма.ПараметрыПроксиСервера"));
			МассивСтрок.Добавить(". " + НСтр("ru = 'Подробности в'") + " ");
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'журнале регистрации'"),,,, "e1cib/app/Обработка.ЖурналРегистрации"));
			МассивСтрок.Добавить(".");
			ТекстОшибкиПодключения = Новый ФорматированнаяСтрока(МассивСтрок);
			
		КонецЕсли;
		
		Элементы.ГруппаОшибкаПодключения.Видимость = Истина;
		Элементы.ТекстОшибкиПодключения.Заголовок = ТекстОшибкиПодключения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодключитсяКИнтернетПоддержкеНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение регистрационных данных из веб-сервиса'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ПараметрыЗадания = Новый Структура("ЛогинИПП, ПарольИПП", ЛогинИнтернетПоддержки, ПарольИнтернетПоддержки);
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("Обработки.НачалоРаботы.ПодключитьИнтернетПоддержку",
		ПараметрыЗадания, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СнятьПризнакНачалаРаботы()
	
	Константы.НачалоРаботы.Установить(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонстанты()
	
	СнятьПризнакНачалаРаботы();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтерфейс()
	
	УстановленныйВариантИнтерфейса = УстановитьИнтерфейсТаксиПоСистемеНалогообложения();
	
	Если УстановленныйВариантИнтерфейса = "ИнтерфейсТаксиПростой" Тогда
		Если РазрешенУчетРегулярнойДеятельности Тогда
			ОткрытьФорму("Обработка.ПанелиПростойИнтерфейс.Форма.ПанельПомощь");
		Иначе
			ОткрытьФорму("Обработка.МониторНалоговИОтчетности.Форма");
		КонецЕсли;
	Иначе
		Если ПоказатьНачалоРаботыВСервисе Тогда
			ОткрытьФорму("Обработка.НачалоРаботы.Форма.НачалоРаботыВСервисе");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботуПомощника()
	
	ОповеститьОЗавершенииПомощникаНачалаРаботы();
	УстановитьИнтерфейс();
	УстановитьКонстанты();
	ОбновитьИнтерфейс();
	// Форма может открываться как форма на рабочем столе, так и обычная форма.
	// В том случае, если форма открывается как форма на рабочем столе, то после установки стандратного интефейса и вызова ОбновитьИнтерфейс()
	// форма автоматически закрывается платформой и дополнительно закрывать ее не нужно.
	Если ЭтотОбъект.Открыта() Тогда
		ЭтотОбъект.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОЗавершенииПомощникаНачалаРаботы()
	Оповестить("ЗавершенаРаботаПомощникаНачалаРаботы", ЭтотОбъект.ИмяФормы, ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИнициализироватьСвойстваОрганизации(Форма)
	
	Форма.ИНН = "";
	Форма.ИННКомментарий = "";
	
	Форма.ВидОрганизации = "ИндивидуальныйПредприниматель";
	Форма.СистемаНалогообложения = "НалогНаПрофессиональныйДоход";
	
	Форма.ПлательщикЕНВД = Ложь;
	Форма.ПрименяетсяУСНПатент = Ложь;
	Форма.ПлательщикТорговогоСбора = Ложь;
	
	Форма.ВидБизнеса = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьСистемыНалогообложения()
	
	Если СистемыНалогообложенияДопускающиеСовмещение.НайтиПоЗначению(СистемаНалогообложения) = Неопределено Тогда
		ПлательщикЕНВД = Ложь;
		ПрименяетсяУСНПатент = Ложь;
		ПлательщикТорговогоСбора = Ложь;
	КонецЕсли;
	
	Если СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.НайтиПоЗначению(СистемаНалогообложения) = Неопределено Тогда
		ИспользуетсяТрудНаемныхРаботников = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОрганизацию()
	
	УстановитьДоступностьЭлементовОжиданияСозданияОрганизации(Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_СоздатьОрганизацию", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ИзменитьТекстСообщенияОжидания", 10, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыбранныйВидБизнеса(ИмяКнопкиВыбораВидаБизнеса)
	
	Строка = Число(Сред(ИмяКнопкиВыбораВидаБизнеса, СтрДлина(ИмяКнопкиВыбораВидаБизнеса) - 1, 1));
	Колонка = Число(Прав(ИмяКнопкиВыбораВидаБизнеса, 1));
	Индекс = Строка * КоличествоКолонокВидовБизнеса() + Колонка;
	
	Если Индекс < Перечисления.ВидыБизнеса.КоличествоДляВыбора() Тогда
		ВидБизнеса = Перечисления.ВидыБизнеса[Индекс];
	КонецЕсли;
	
	Возврат ВидБизнеса;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДоступныеВидыБизнеса()
	
	ОписанияВидовБизнеса = Перечисления.ВидыБизнеса.ОписанияВидовБизнеса();
	
	КоличествоВидовБизнеса = Перечисления.ВидыБизнеса.КоличествоДляВыбора();
	Для Строка = 0 По КоличествоСтрокВидовБизнеса() - 1 Цикл
		Для Колонка = 0 По КоличествоКолонокВидовБизнеса() - 1 Цикл
			НастроитьКнопкуВыбораВидаБизнеса(Строка, Колонка, ОписанияВидовБизнеса);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКнопкуВыбораВидаБизнеса(Строка, Колонка, ОписанияВидовБизнеса)
	
	Индекс = Строка * КоличествоКолонокВидовБизнеса() + Колонка;
	КнопкаВыбораВидаБизнеса = Элементы[ИмяКнопкиВыбораВидаБизнеса(Строка, Колонка)];
	
	Если Индекс >= Перечисления.ВидыБизнеса.КоличествоДляВыбора() Тогда
		КнопкаВыбораВидаБизнеса.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	КнопкаВыбораВидаБизнеса.Видимость = Истина;
	
	ВидБизнеса = Перечисления.ВидыБизнеса.Получить(Индекс);
	
	КнопкаВыбораВидаБизнеса.Заголовок = Строка(ВидБизнеса);
	КнопкаВыбораВидаБизнеса.Картинка = Перечисления.ВидыБизнеса.Картинка(ВидБизнеса);
	Если КнопкаВыбораВидаБизнеса.Картинка.Вид <> ВидКартинки.Пустая Тогда
		КнопкаВыбораВидаБизнеса.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Иначе
		КнопкаВыбораВидаБизнеса.Отображение = ОтображениеКнопки.Текст;
	КонецЕсли;
	
	КнопкаВыбораВидаБизнеса.РасширеннаяПодсказка.Заголовок = ОписанияВидовБизнеса[ВидБизнеса];
	КнопкаВыбораВидаБизнеса.ОтображениеПодсказки = ОтображениеПодсказки.Всплывающая;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяКнопкиВыбораВидаБизнеса(Строка, Колонка)
	
	Возврат СтрШаблон("ВидБизнеса%1%2", Строка, Колонка);
	
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоСтрокВидовБизнеса()
	
	Возврат 4;
	
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоКолонокВидовБизнеса()
	
	Возврат 3;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОрганизацияСуществует()
	
	Возврат Справочники.Организации.КоличествоОрганизаций() <> 0;
	
КонецФункции

&НаСервере
Функция УстановитьИнтерфейсТаксиПоСистемеНалогообложения()
	
	// Если организация на момент открытия помощника существовала,
	// то систему налогообложения нужно определять по данным информационной базы.
	// Поэтому систему налогообложения для определения интерфейса оставляем пустой.
	СистемаНалогообложенияДляИнтерфейса = ?(ОрганизацияСуществует, "", СистемаНалогообложения);
	
	ВариантИнтерфейса = Обработки.НачалоРаботы.УстановитьПодходящийИнтерфейс(
		ВидОрганизации,
		СистемаНалогообложения,
		ИспользуетсяТрудНаемныхРаботников,
		РазрешенУчетРегулярнойДеятельности
	);
	
	// Привилегированный режим устанавливается, так как нужно изменить настройки интерфейса
	// для всех пользователей, а не только текущего пользователя.
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначенияБПВызовСервера.УстановитьСтандартныйИнтерфейсДляВсехпользователей();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВариантИнтерфейса;
	
КонецФункции

&НаКлиенте
Функция ЗапуститьСозданиеОрганизациюВФоне()
	
	Если Реквизиты = Неопределено Тогда
		Реквизиты = ПолучитьРеквизитыПоИНН(ИНН, ЭтоЮрЛицо(ИНН));
	КонецЕсли;
	
	Если Реквизиты <> Неопределено Тогда
		КлючеваяОперация = "БыстрыйСтартСозданиеОрганизацииПоИННРеквизитыПолучены";
	ИначеЕсли ЗначениеЗаполнено(ИНН) Тогда
		КлючеваяОперация = "БыстрыйСтартСозданиеОрганизацииПоИННРеквизитыНеПолучены";
	Иначе
		КлючеваяОперация = "БыстрыйСтартСозданиеОрганизацииБезИНН";
	КонецЕсли;
	
	УИДЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Ложь, КлючеваяОперация);
	
	Возврат СоздатьОрганизациюНаСервере();
	
КонецФункции

&НаКлиенте
Функция ЗапуститьУстановкуВидаБизнесаВФоне()
	
	Возврат УстановитьВидБизнесаНаСервере();
	
КонецФункции

&НаСервере
Функция УстановитьВидБизнесаНаСервере()
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ВидБизнеса", ВидБизнеса);
	
	РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Справочники.Организации.УстановитьВидБизнесаВФоне",
		ПараметрыЗадания,
		НСтр("ru='Установка видов деятельности'"));
	
	ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиНаСтраницуВидОрганизации()

	ИНН = "";
	
	ПерейтиНаСтраницу(ЭтотОбъект, "СтраницаВидОрганизации");
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти
