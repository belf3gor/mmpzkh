
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОповещениеПослеПеререгистрации = Параметры.ОповещениеПослеПеререгистрации;
	
	РеквизитыОрганизации = РеквизитыОрганизации();
	ИННОрганизацииДоАктивации = РеквизитыОрганизации.ИНН;
	
	Если ЗначениеЗаполнено(Параметры.ОшибкаПроверкиАктивации) Тогда
		ПодготовитьФормуНаСервере(Параметры.ОшибкаПроверкиАктивации);
	Иначе
		ОбновитьДанныеРегистрации();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПовторнуюАктивацию(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("Подключаемый_ВыполненоОбновлениеЛицензии", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("Обработка.НачалоРаботы.Форма.АктивацияПрограммы", ПараметрыФормы, , , , , 
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ВыполненоОбновлениеЛицензии(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДанныеРегистрации();
	
	Если ЗначениеЗаполнено(ОповещениеПослеПеререгистрации)
		И ЭтотОбъект.ВладелецФормы <> Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
		Оповещение = Новый ОписаниеОповещения(ОповещениеПослеПеререгистрации, ЭтотОбъект.ВладелецФормы, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеРегистрации()
	
	РезультатПроверки = НачалоРаботы.ПроверитьЛегальностьИспользования();
	ПодготовитьФормуНаСервере(РезультатПроверки.Ошибка);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере(ОшибкаПроверкиАктивации)
	
	РеквизитыОрганизации = РеквизитыОрганизации();
	ИННОрганизацииПослеАктивации = РеквизитыОрганизации.ИНН;
	
	// Получаем сведения о лицензии
	СведенияОРегистрации = НачалоРаботы.СведенияОРегистрации();
	
	ОписаниеРегистрации = НачалоРаботы.РегистрационныеДанныеФорматированнойСтрокой(СведенияОРегистрации);
	
	Подстроки = Новый Массив;
	
	ОсновнойШрифт = Элементы.ТекстСообщения.Шрифт;
	ЖирныйШрифт = Новый Шрифт(ОсновнойШрифт, , , Истина);
	
	Если ЗначениеЗаполнено(ОшибкаПроверкиАктивации) Тогда
		Если ОшибкаПроверкиАктивации = "НесколькоОрганизаций" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'В информационной базе ведется учет нескольких организаций.'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Программа предназначена для ведения учета только одной организации.'"), ОсновнойШрифт));
			
			Элементы.ВыполнитьПовторнуюАктивацию.Доступность = Ложь;
		ИначеЕсли ОшибкаПроверкиАктивации = "НетРегистрационныхДанных" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Программа не активирована.'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Выполните активацию программы.'"), ОсновнойШрифт));
		ИначеЕсли ОшибкаПроверкиАктивации = "НевернаяПодпись" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'В информационной базе указаны некорректные регистрационные данные.'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Выполните активацию программы.'"), ОсновнойШрифт));
		ИначеЕсли ОшибкаПроверкиАктивации = "РегистрацияДругойОрганизации" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Программа активирована для: '"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(ОписаниеРегистрации);
			Подстроки.Добавить(Символы.ПС);
			
			РеквизитыОрганизации = РеквизитыОрганизации();
			
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'В информационной базе ведется учет'"), ОсновнойШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(РеквизитыОрганизации.Наименование, ЖирныйШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '(ИНН'"), ОсновнойШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(РеквизитыОрганизации.ИНН, ЖирныйШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Необходимо выполнить активацию программы для этой организации.'"), ОсновнойШрифт));
			
		Иначе
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Неизвестная ошибка активации:'"), ОсновнойШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(ОшибкаПроверкиАктивации , ЖирныйШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Выполните активацию программы.'"), ОсновнойШрифт));
		КонецЕсли;
		
		ЭтотОбъект.Заголовок = НСтр("ru = 'Ошибка активации программы'");
		Элементы.ГруппаСообщение.ЦветФона = ЦветаСтиля.ЦветФонаНекорректногоКонтрагента;
	Иначе
		Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Программа активирована для: '"), ОсновнойШрифт));
		Подстроки.Добавить(Символы.ПС);
		Подстроки.Добавить(ОписаниеРегистрации);
		Подстроки.Добавить(Символы.ПС);
		Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Повторная активация не требуется.'"), ОсновнойШрифт));
		
		Элементы.ГруппаСообщение.ЦветФона = ЦветаСтиля.ЦветФонаКорректныхКонтрагентов;
		ЭтотОбъект.Заголовок = НСтр("ru = 'Сведения об активации программы'");
	КонецЕсли;
	
	Элементы.ТекстСообщения.Заголовок = Новый ФорматированнаяСтрока(Подстроки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыОрганизации()
	
	РеквизитыОрганизации = Новый Структура("ИНН, Наименование", "", "");
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.ИНН,
	|	Организации.НаименованиеСокращенное КАК Наименование
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыОрганизации, Выборка);
	КонецЕсли;
	
	Возврат РеквизитыОрганизации;
	
КонецФункции

#КонецОбласти

