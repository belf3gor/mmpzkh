#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачатьРаботу(Команда)
	
	Если ОрганизацияУжеСоздана() Тогда
		Оповестить("ЗавершенаРаботаПомощникаНачалаРаботы", ЭтотОбъект.ИмяФормы, ЭтотОбъект);
		ПодготовитьКонстантыДляНачалаРаботы();
		ОткрытьФорму("Обработка.НачалоРаботы.Форма.НачалоРаботыВСервисе");
		ОбновитьИнтерфейс();
	Иначе
		СохранитьРежимРаботыБыстрыйСтартНаСервере();
		ОбщегоНазначенияБПКлиент.ОткрытьБыстрыйСтарт();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	Если РегистрацияОрганизацииКлиент.ОткрытьПомощникРегистрации() Тогда
		СохранитьРежимРаботыПользователя("Регистрация");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВнестиИзменения(Команда)
	
	Если РегистрацияОрганизацииКлиент.ОткрытьПомощникВнесенияИзменений() Тогда
		СохранитьРежимРаботыПользователя("ВнесениеИзменений");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ОрганизацияУжеСоздана()
	
	Возврат (Справочники.Организации.КоличествоОрганизаций() > 0);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПодготовитьКонстантыДляНачалаРаботы()
	
	Константы.НачалоРаботы.Установить(Ложь);
	РежимыРаботыПользователя     = Обработки.НачалоРаботы.НовыйРежимРаботыПользователя();
	РежимРаботыВыборПользователя = Обработки.НачалоРаботы.РежимРаботыВыборПользователя();
	Если РежимРаботыВыборПользователя = РежимыРаботыПользователя.Регистрация Тогда
		// Оставим помощник регистрации на случай, чтобы пользователь не потерял ранее введенные данные.
		Константы.РегистрацияНеЗавершена.Установить(Истина);
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.УстановитьСтандартныйИнтерфейс();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьРежимРаботыБыстрыйСтартНаСервере()
	
	РежимыРаботыПользователя = Обработки.НачалоРаботы.НовыйРежимРаботыПользователя();
	Обработки.НачалоРаботы.СохранитьРежимРаботыПользователя(РежимыРаботыПользователя.БыстрыйСтарт);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьРежимРаботыПользователя(Знач РежимРаботы)
	
	РежимыРаботыПользователя = Обработки.НачалоРаботы.НовыйРежимРаботыПользователя();
	Если РежимыРаботыПользователя.Свойство(РежимРаботы) Тогда
		Обработки.НачалоРаботы.СохранитьРежимРаботыПользователя(РежимРаботы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти