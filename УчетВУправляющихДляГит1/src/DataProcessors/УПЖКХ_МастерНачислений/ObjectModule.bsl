
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ДОКУМЕНТОВ

#Область ПроцедурыИФункцииФормированияДокументов

// Процедура запускает формирование документов либо многопоточное, либо в одном в потоке.
Процедура СформироватьДокументы(ИдентификаторСеанса, Приоритет, ЭтоДоработкаДокументов) Экспорт
	
	Если КоличествоПотоков = 1 ИЛИ ЭтоДоработкаДокументов Тогда
		
		СформироватьДокументыВОдномПотоке(ИдентификаторСеанса, Приоритет, ЭтоДоработкаДокументов);
		
	Иначе
		
		СформироватьДокументыВНесколькихПотоках(ИдентификаторСеанса, Приоритет);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура запускает фоновое формирование документов в одном потоке.
Процедура СформироватьДокументыВОдномПотоке(ИдентификаторСеанса, Приоритет, ЭтоДоработкаДокументов)
	
	// Определим дату регистрации документов.
	ДатаРегистрацииДокументов = КонецМесяца(МесяцНачисления);
	Если ДеньРегистрацииДокументов > 0 Тогда
		ДлинаСуток = 60 * 60 * 24;
		ДатаРегистрацииДокументов = НачалоМесяца(МесяцНачисления) + ДлинаСуток * (ДеньРегистрацииДокументов - 1);
	КонецЕсли;
	
	// Запускаем формирование в фоновом задании.
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(ИдентификаторСеанса);
	МассивПараметров.Добавить(МесяцНачисления);
	МассивПараметров.Добавить(ДатаРегистрацииДокументов);
	МассивПараметров.Добавить(Приоритет);
	МассивПараметров.Добавить(1);
	МассивПараметров.Добавить(ЭтоДоработкаДокументов);
	
	ЗапускаемаяПроцедура = "УПЖКХ_РаботаСМастеромНачислений.СоздатьДокументы";
	
	ФоновыеЗадания.Выполнить(ЗапускаемаяПроцедура, МассивПараметров, , "МастерНачислений");
	
КонецПроцедуры

// Процедура запускает фоновое формирование документов в указанном числе потоков.
Процедура СформироватьДокументыВНесколькихПотоках(ИдентификаторСеанса, Приоритет)
	
	Для СчетчикПотоков = 1 По КоличествоПотоков Цикл
		
		// Определим дату регистрации документов.
		ДатаРегистрацииДокументов = КонецМесяца(МесяцНачисления);
		Если ДеньРегистрацииДокументов > 0 Тогда
			ДлинаСуток = 60 * 60 * 24;
			ДатаРегистрацииДокументов = НачалоМесяца(МесяцНачисления) + ДлинаСуток * (ДеньРегистрацииДокументов - 1);
		КонецЕсли;
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ИдентификаторСеанса);
		МассивПараметров.Добавить(МесяцНачисления);
		МассивПараметров.Добавить(ДатаРегистрацииДокументов);
		МассивПараметров.Добавить(Приоритет);
		МассивПараметров.Добавить(СчетчикПотоков);
		
		ЗапускаемаяПроцедура = "УПЖКХ_РаботаСМастеромНачислений.СоздатьДокументы";
		
		Задание = ФоновыеЗадания.Выполнить(ЗапускаемаяПроцедура, МассивПараметров, , "МастерНачислений");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти