#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВидДеятельности", ВидДеятельности);
	Параметры.Свойство("Период", Период);
	
	Если Не ЗначениеЗаполнено(ВидДеятельности) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначенияБП.ПолучитьРабочуюДату();
	КонецЕсли;
	
	Заголовок = ВидДеятельности;
	
	СвойстваВидаДеятельности = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДеятельности, "ДатаНачала, ДатаПрекращения");
	ДатаНачала      = СвойстваВидаДеятельности.ДатаНачала;
	ДатаПрекращения = СвойстваВидаДеятельности.ДатаПрекращения;
	
	Если Не ЗначениеЗаполнено(ДатаПрекращения) Тогда
		ОдинДень = 86400;
		ДатаПрекращения = КонецКвартала(Период) + ОдинДень;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если УстановитьПометкуУдаления(ЭтотОбъект) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПрекращения");
	Иначе
		Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаПрекращения) Тогда
			Если ДатаПрекращения < ДатаНачала Тогда
				ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", ВидДеятельности.Метаданные().Реквизиты.ДатаПрекращения.Синоним);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ДатаПрекращения", , Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	
	Если УстановитьПометкуУдаления(ЭтотОбъект) Тогда
		Период = ДатаПрекращения;
		ДатаПрекращения = '00010101'
	Иначе
		ДатаПрекращения = Период;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ВидДеятельности", ВидДеятельности);
	РезультатВыбора.Вставить("УстановитьПометкуУдаления", УстановитьПометкуУдаления(ЭтотОбъект));
	РезультатВыбора.Вставить("ДатаПрекращения", ДатаПрекращения);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ДатаПрекращения.Доступность = Не УстановитьПометкуУдаления(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьПометкуУдаления(Форма)
	
	Возврат (Форма.Действие = "УстановитьПометкуУдаления");
	
КонецФункции

#КонецОбласти
