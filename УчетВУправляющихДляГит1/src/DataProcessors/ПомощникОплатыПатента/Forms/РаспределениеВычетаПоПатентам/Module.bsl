
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"Организация, РегистрацияВНалоговомОргане, ТекущийПатент, НалоговыйВычетТекущийПатент, РасходыНаОнлайнКассыВсего");
	
	Если НЕ ЗначениеЗаполнено(Организация)
		ИЛИ НЕ ЗначениеЗаполнено(РегистрацияВНалоговомОргане)
		ИЛИ НЕ ЗначениеЗаполнено(ТекущийПатент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НаименованиеИФНС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РегистрацияВНалоговомОргане, "Наименование");
	
	ЗаполнитьТаблицуРаспределения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ Модифицированность ИЛИ ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения,
		НСтр("ru='Данные были изменены. Сохранить изменения?'"),
		РежимДиалогаВопрос.ДаНетОтмена,
		,
		КодВозвратаДиалога.Да);
	
	Отказ = Истина;
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеИФНСНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьЗначение(, РегистрацияВНалоговомОргане);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура НалоговыйВычетПриИзменении(Элемент)
	
	ДанныеЗаписи = Элементы.РаспределениеВычета.ТекущиеДанные;
	
	Если ДанныеЗаписи.НалоговыйВычет = ДанныеЗаписи.НалоговыйВычетДоИзменения Тогда
		// Ничего не изменилось.
		Возврат;
	КонецЕсли;
	
	Если НЕ ВычетПоПатентуВведенКорректно(ДанныеЗаписи) Тогда
		ДанныеЗаписи.НалоговыйВычет = ДанныеЗаписи.НалоговыйВычетДоИзменения;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Изменения не сохранены'"));
		Возврат;
	КонецЕсли;
	
	ДанныеЗаписи.РучнаяКорректировка = Истина;
	ДанныеЗаписи.НалоговыйВычетДоИзменения = ДанныеЗаписи.НалоговыйВычет;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РаспределениеВычетаОтменитьРучныеКорректировки(Команда)
	
	ПерезаполнитьРаспределение();
	Оповестить("Запись_РасходыНаОнлайнКассыУменьшающиеНалогПСН", РегистрацияВНалоговомОргане);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Модифицированность Тогда
		
		СохранитьИзменения();
		Оповестить("Запись_РасходыНаОнлайнКассыУменьшающиеНалогПСН", РегистрацияВНалоговомОргане);
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуРаспределения()
	
	РаспределениеВычета.Очистить();
	
	ДанныеРаспределения = ДанныеРаспределенияВычета(Организация, РегистрацияВНалоговомОргане, ТекущийПатент);
	
	Для Каждого СтрокаДанных Из ДанныеРаспределения Цикл
		
		НоваяСтрока = РаспределениеВычета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		
		НоваяСтрока.ОписаниеПлатежа = ОписаниеПлатежаПоПатенту(СтрокаДанных);
		НоваяСтрока.НалоговыйВычетДоИзменения = НоваяСтрока.НалоговыйВычет;
		
	КонецЦикла;
	
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеПлатежаПоПатенту(СведенияОПлатеже)
	
	ТекстВидПлатежа = "";
	
	ФорматДат = "ДФ=dd.MM.yyyy";
	
	Если СведенияОПлатеже.ВидПлатежа = ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоПатенту.ПервыйПлатеж") Тогда
		// Первый платеж из двух.
		НазваниеПлатежа = НСтр("ru = '1/3 стоимости'");
	ИначеЕсли СведенияОПлатеже.ВидПлатежа = ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоПатенту.ВторойПлатеж") Тогда
		// Второй платеж из двух.
		НазваниеПлатежа = НСтр("ru = '2/3 стоимости'");
	Иначе
		// Один платеж
		НазваниеПлатежа = НСтр("ru = 'полная стоимость'");
	КонецЕсли;
	
	ТекстВидПлатежа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 до %2'"),
		НазваниеПлатежа, Формат(СведенияОПлатеже.СрокПлатежа, ФорматДат));
	
	ОписаниеПлатежа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1, %2'"),
		СведенияОПлатеже.ПатентНаименование, ТекстВидПлатежа);
		
	Возврат ОписаниеПлатежа;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеРаспределенияВычета(Организация, РегистрацияВНалоговомОргане, ИсключаемыйПатент)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("ИсключаемыйПатент", ИсключаемыйПатент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаспределениеВычета.Организация КАК Организация,
	|	РаспределениеВычета.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РаспределениеВычета.Патент КАК Патент,
	|	РаспределениеВычета.ВидПлатежа КАК ВидПлатежа,
	|	ВЫБОР
	|		КОГДА РаспределениеВычета.ВидПлатежа = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейПоПатенту.ВторойПлатеж)
	|			ТОГДА Патенты.ДатаВторогоПлатежа
	|		ИНАЧЕ Патенты.ДатаПервогоПлатежа
	|	КОНЕЦ КАК СрокПлатежа,
	|	РаспределениеВычета.СуммаПлатежа КАК СуммаПлатежа,
	|	РаспределениеВычета.Оплачено КАК Оплачено,
	|	РаспределениеВычета.СуммаПлатежа - РаспределениеВычета.Оплачено КАК ОсталосьОплатить,
	|	РаспределениеВычета.НалоговыйВычет КАК НалоговыйВычет,
	|	РаспределениеВычета.РучнаяКорректировка КАК РучнаяКорректировка,
	|	Патенты.ДатаНачала КАК ПатентДатаНачала,
	|	Патенты.ДатаОкончания КАК ПатентДатаОкончания,
	|	Патенты.ДатаВыдачи КАК ПатентДатаВыдачи,
	|	Патенты.Наименование КАК ПатентНаименование
	|ИЗ
	|	РегистрСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН КАК РаспределениеВычета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патенты КАК Патенты
	|		ПО РаспределениеВычета.Патент = Патенты.Ссылка
	|ГДЕ
	|	РаспределениеВычета.Организация = &Организация
	|	И РаспределениеВычета.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И РаспределениеВычета.Патент <> &ИсключаемыйПатент
	|	И РаспределениеВычета.СуммаПлатежа - РаспределениеВычета.Оплачено > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПатентДатаОкончания,
	|	Патент,
	|	СрокПлатежа"
	;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Процедура ПерезаполнитьРаспределение()
	
	// Заполним распределение расходов по умолчанию
	РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.РаспределитьРасходыПоПатентам(Организация,
		РегистрацияВНалоговомОргане,
		Ложь);
	
	ЗаполнитьТаблицуРаспределения();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтоги()
	
	ИтогоОсталосьОплатить = РаспределениеВычета.Итог("ОсталосьОплатить");
	
КонецПроцедуры

&НаКлиенте
Функция ВычетПоПатентуВведенКорректно(ДанныеЗаписи)
	
	Отказ = Ложь;
	
	НовыйВычетПоПатенту = ДанныеЗаписи.НалоговыйВычет;
	ОбщийНалоговыйВычет = РаспределениеВычета.Итог("НалоговыйВычет");
	
	Если НовыйВычетПоПатенту > ДанныеЗаписи.ОсталосьОплатить Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Введенная сумма уменьшения превышает сумму к оплате'"), , , , Отказ);
	КонецЕсли;
	
	ДопустимыйВычет = РасходыНаОнлайнКассыВсего - НалоговыйВычетТекущийПатент;
	
	Если ОбщийНалоговыйВычет > ДопустимыйВычет Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Уменьшение налога по всем патентам превышает общую сумму расходов на онлайн-кассы'"), , , , Отказ);
	КонецЕсли;
	
	Возврат (НЕ Отказ);
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		// Если нажали кнопку "Отмена", то ничего не делаем.
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьИзменения();
		Оповестить("Запись_РасходыНаОнлайнКассыУменьшающиеНалогПСН", РегистрацияВНалоговомОргане);
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьИзменения()
	
	ИзмененныеСтроки = РаспределениеВычета.НайтиСтроки(Новый Структура("РучнаяКорректировка", Истина));
	
	ЕстьИзменения = Ложь;
	
	Для Каждого СтрокаТаблицы Из ИзмененныеСтроки Цикл
		Запись = РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Запись.Записать();
		
		ЕстьИзменения = Истина;
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		// Распределим вычет с учетом внесенных изменений
		РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.РаспределитьРасходыПоПатентам(
			Организация,
			РегистрацияВНалоговомОргане,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
