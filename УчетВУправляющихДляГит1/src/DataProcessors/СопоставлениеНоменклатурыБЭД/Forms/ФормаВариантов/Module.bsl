////////////////////////////////////////////////////////////////////////////////
// Модуль формы Обработка.СопоставлениеНоменклатурыБЭД.ФормаВариантов
//
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Обработка параметров формы.
	Если Параметры.Свойство("ЭлементСопоставления", ЭлементСопоставления) Тогда
		
		ЗаполнитьНоменклатуруКонтрагента(ЭлементСопоставления.НоменклатураКонтрагента);
		
	КонецЕсли;
	
	Если Параметры.Свойство("Варианты") Тогда
		
		ЗаполнитьВарианты(Параметры.Варианты);
		АктивизироватьТекущийВариант(ЭлементСопоставления.НоменклатураИБ);
		
	КонецЕсли;
	
	// Настройка формы.
	УстановитьСвойстваЭлементовФормы();
	УстановитьСвойстваЭлементовНоменклатурыКонтрагентов();
	УстановитьСвойстваЭлементовНоменклатурыСервиса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВарианты

&НаКлиенте
Процедура ВариантыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборВарианта(Варианты.НайтиПоИдентификатору(ВыбраннаяСтрока));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВариант(Команда)
	
	ОбработатьВыборВарианта(Элементы.Варианты.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариант(Команда)
	
	Вариант = Элементы.Варианты.ТекущиеДанные;
	
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Варианты.ТекущийЭлемент = Элементы.ВариантыХарактеристика Тогда
		ПоказатьЗначение(,Вариант.Характеристика);
	ИначеЕсли Элементы.Варианты.ТекущийЭлемент = Элементы.ВариантыУпаковка Тогда
		ПоказатьЗначение(,Вариант.Упаковка);
	Иначе
		ПоказатьЗначение(,Вариант.Номенклатура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВесьСписок(Команда)
	
	ОбработатьВыборДействия("ОткрытьСписок");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	
	ОбработатьВыборДействия("СоздатьНоменклатуру");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзСервиса(Команда)
	
	ОбработатьВыборДействия("ЗагрузитьИзСервиса");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьИзСервиса(Команда)
	
	ОбработатьВыборДействия("ПодобратьИзСервиса");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСФормой

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Выделим сработавшие критерии поиска.
	
	ШрифтВыделения = Новый Шрифт(Элементы.ВариантыНоменклатура.Шрифт,,, Истина);
	
	//  Наименование
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтВыделения);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Варианты.Наименование");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Наименование");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыНоменклатура.Имя);
	
	//  Артикул
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтВыделения);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Варианты.Артикул");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Артикул");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыАртикул.Имя);
	
	//  Штрихкод
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтВыделения);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Варианты.СовпадениеПоШтрихкоду");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыШтрихкод.Имя);
	
	// Отключим видимость характеристики, если она не найдена.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Варианты.Характеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыХарактеристика.Имя);
	
	// Отключим видимость упаковки, если она не найдена.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Варианты.Упаковка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыУпаковка.Имя);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	Элементы.КомандаСоздатьНоменклатуру.Видимость = ЕстьПравоДобавленияНоменклатуры();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовНоменклатурыКонтрагентов()
	
	ЗаголовокНоменклатурыКонтрагента = НСтр("ru = 'Данные для сопоставления'");
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		
		ЗаголовокНоменклатурыКонтрагента = СтрШаблон(НСтр("ru = 'Данные ""%1""'"), Владелец);
		
	КонецЕсли;
	
	Элементы.ГруппаНоменклатураКонтрагента.Заголовок = ЗаголовокНоменклатурыКонтрагента;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьСвойстваЭлементовНоменклатурыСервиса()
	
	ИспользоватьСервис = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		
		МодульРаботаСНоменклатурой = ОбщегоНазначения.ОбщийМодуль("РаботаСНоменклатурой");
		
		ИспользоватьСервисРаботаСНоменклатурой = МодульРаботаСНоменклатурой.ДоступнаФункциональностьПодсистемы();
		
		ИспользоватьСервис = ИспользоватьСервисРаботаСНоменклатурой
			И МодульРаботаСНоменклатурой.ПравоИзмененияДанных();
		
	КонецЕсли;
	
	ЕстьИдентификаторСервиса = ЗначениеЗаполнено(ЭлементСопоставления.НоменклатураКонтрагента.ИдентификаторНоменклатурыСервиса);
	Элементы.КомандаЗагрузитьИзСервиса.Видимость = ИспользоватьСервис И ЕстьИдентификаторСервиса;
	Элементы.КомандаПодобратьИзСервиса.Видимость = ИспользоватьСервис И Не ЕстьИдентификаторСервиса;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСВариантами

&НаСервере
Процедура АктивизироватьТекущийВариант(Знач НоменклатураИБ)
	
	Если Не ЗначениеЗаполнено(НоменклатураИБ.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ПоляПоиска = "Номенклатура" 
		+ ?(ЗначениеЗаполнено(НоменклатураИБ.Характеристика), ",Характеристика", "")
		+ ?(ЗначениеЗаполнено(НоменклатураИБ.Упаковка), ",Упаковка", "");
	ОтборТекущегоВарианта = Новый Структура(ПоляПоиска);
	ЗаполнитьЗначенияСвойств(ОтборТекущегоВарианта, НоменклатураИБ);
	
	НайденныеВарианты = Варианты.НайтиСтроки(ОтборТекущегоВарианта);
	
	Если ЗначениеЗаполнено(НайденныеВарианты) Тогда
		
		Элементы.Варианты.ТекущаяСтрока = НайденныеВарианты[0].ПолучитьИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВарианты(Знач Данные)
	
	Варианты.Очистить();
	
	НаборШтрихкодов = СтрРазделить(Штрихкод, ",");
	
	Для Каждого Вариант Из Данные Цикл
		
		НовыйВариант = Варианты.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйВариант, Вариант);
		Если ЗначениеЗаполнено(НовыйВариант.Штрихкод) 
			И НаборШтрихкодов.Найти(НовыйВариант.Штрихкод) <> Неопределено Тогда
			НовыйВариант.СовпадениеПоШтрихкоду = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДействия(Знач Действие)
	
	ОписаниеДействия = Новый Структура("Действие,ЭлементСопоставления", Действие, ЭлементСопоставления);
	
	ОповеститьОВыборе(ОписаниеДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборВарианта(Вариант)
	
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭлементСопоставления.НоменклатураИБ, Вариант);
	
	ОбработатьВыборДействия("ПрименитьВариант");
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСНоменклатуройКонтрагента

&НаСервере
Процедура ЗаполнитьНоменклатуруКонтрагента(Знач НоменклатураКонтрагента)
	
	Владелец = НоменклатураКонтрагента.Владелец;
	Идентификатор = НоменклатураКонтрагента.Идентификатор;
	Наименование = НоменклатураКонтрагента.Наименование;
	Характеристика = НоменклатураКонтрагента.Характеристика;
	ЕдиницаИзмерения = НоменклатураКонтрагента.ЕдиницаИзмерения;
	Артикул = НоменклатураКонтрагента.Артикул;
	СтавкаНДС = НоменклатураКонтрагента.СтавкаНДС;
	Штрихкод = НоменклатураКонтрагента.Штрихкод;
	ИдентификаторНоменклатурыСервиса = НоменклатураКонтрагента.ИдентификаторНоменклатурыСервиса;
	ИдентификаторХарактеристикиСервиса = НоменклатураКонтрагента.ИдентификаторХарактеристикиСервиса;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция ЕстьПравоДобавленияНоменклатуры()
	
	ЕстьПраво = Истина;
	
	Для каждого ТипНоменклатуры Из Метаданные.ОпределяемыеТипы.НоменклатураБЭД.Тип.Типы() Цикл
		
		МетаданныеТипа = Метаданные.НайтиПоТипу(ТипНоменклатуры);
		Если МетаданныеТипа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			ЕстьПравоДобавления = ПравоДоступа("Добавление", МетаданныеТипа);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ЕстьПраво = ЕстьПраво И ЕстьПравоДобавления;
		
	КонецЦикла;
	
	Возврат ЕстьПраво;
	
КонецФункции

#КонецОбласти

#КонецОбласти
