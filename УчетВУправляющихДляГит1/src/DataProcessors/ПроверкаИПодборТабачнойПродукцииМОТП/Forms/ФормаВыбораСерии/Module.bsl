&НаКлиенте
Перем Ссылка Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Требуется указание серии.'");
	
	ТипыНоменклатуры.ЗагрузитьЗначения(Метаданные.ОпределяемыеТипы.Номенклатура.Тип.Типы());
	
	Штрихкод            = Параметры.Штрихкод;
	Номенклатура        = Параметры.Номенклатура;
	Характеристика      = Параметры.Характеристика;
	Склад               = Параметры.Склад;
	СтатусУказанияСерий = 1;
	СброситьРазмерыИПоложениеОкна();
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ПараметрыУказанияСерий = Новый Структура;
	ПроверкаИПодборПродукцииМОТППереопределяемый.ЗаполнитьПараметрыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
	
	Если Не ЗначениеЗаполнено(Характеристика) Тогда
		
		Элементы.Характеристика.ПодсказкаВвода = НСтр("ru = '<характеристики не используются>'");
		Элементы.Характеристика.АвтоОтметкаНезаполненного = Ложь;
		
	КонецЕсли;
	
	Элементы.ЗавершитьРедактирование.Доступность = Ложь;
	Элементы.ДекорацияУдалосьПодобрать.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ИмяФормыУказанияСерии = "";
	ШтрихкодированиеИСКлиентПереопределяемый.ЗаполнитьПолноеИмяФормыУказанияСерии(ИмяФормыУказанияСерии);
	
	Если ИсточникВыбора.ИмяФормы = ИмяФормыУказанияСерии Тогда
		Серия = ВыбранноеЗначение.Значение;
	КонецЕсли;
	
	Если ТипыНоменклатуры.НайтиПоЗначению(ТипЗнч(ВыбранноеЗначение)) <> Неопределено Тогда
		
		Если ВыбранноеЗначение <> Номенклатура
			И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			
			Номенклатура = ВыбранноеЗначение;
			ОтключитьОтметкуНезаполненного();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДекорацияУдалосьПодобрать.Видимость = ЗначениеЗаполнено(Серия);
	Элементы.ЗавершитьРедактирование.Доступность = ЗначениеЗаполнено(Серия);
	
	ОтключитьОтметкуНезаполненного();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Новый Структура;
	ТекущиеДанные.Вставить("Номенклатура",               Номенклатура);
	ТекущиеДанные.Вставить("Характеристика",             Характеристика);
	ТекущиеДанные.Вставить("Серия",                      Неопределено);
	ТекущиеДанные.Вставить("ИдентификаторТекущейСтроки", 1);
	ТекущиеДанные.Вставить("СтатусУказанияСерий",        1);
	ТекущиеДанные.Вставить("ХарактеристикиИспользуются", ЗначениеЗаполнено(Характеристика));
	ТекущиеДанные.Вставить("Количество",                 1);
	
	ИнтеграцияИСКлиент.ОткрытьПодборСерий(ЭтотОбъект,, "", Ложь, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Результат = Серия;
	
	Если ЗапомнитьВыбор Тогда
			
		ДанныеВыбора = Новый Структура;
		ДанныеВыбора.Вставить("Номенклатура",   Номенклатура);
		ДанныеВыбора.Вставить("Характеристика", Характеристика);
		ДанныеВыбора.Вставить("Серия",          Серия);
			
		Результат = Новый Структура;
		Результат.Вставить("ЗапомнитьВыбор", Истина);
		Результат.Вставить("ДанныеВыбора", ДанныеВыбора);
			
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("Обработка.ПроверкаИПодборТабачнойПродукции.Форма.ФормаВыбораСерии", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти