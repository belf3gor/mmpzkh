
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.ТаблицаНОПометка.Видимость Тогда
		
		ВыбратьНалоговыеОрганы();
		
	Иначе
		
		Закрыть(Элементы.ТаблицаНО.ТекущиеДанные);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНОВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ТаблицаНОПометка.Видимость Тогда
		
		ВыбратьНалоговыеОрганы();
		
	Иначе
		
		Закрыть(Элементы.ТаблицаНО.ТекущиеДанные);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНалоговыеОрганы()
	
	МассивСтрок = ТаблицаНО.НайтиСтроки(Новый Структура("Пометка", Истина));
	
	ВыбранныеНалоговыеОрганы = Новый Массив;
	
	Для Каждого Строка Из МассивСтрок Цикл
		ВыбранныеНалоговыеОрганы.Добавить(Строка.Ссылка);
	КонецЦикла;
			
	Закрыть(ВыбранныеНалоговыеОрганы);
	
КонецПроцедуры
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ТаблицаНО.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		
	Организация     = Параметры.Организация;
	НалоговыеОрганы = Параметры.НалоговыеОрганы;
	
	СформироватьТаблицуНО();
	
	Если Параметры.ЗначенияДляОтбора.Количество() > 1 Тогда
		
		Если ТипЗнч(Параметры.ЗначенияДляОтбора[1]) = Тип("Структура") Тогда
			
			Если Параметры.ЗначенияДляОтбора[1].Свойство("КодыПричиныПостановкиНаУчет") Тогда
				
				КодыПричиныПостановкиНаУчет = Параметры.ЗначенияДляОтбора[1].КодыПричиныПостановкиНаУчет;
				КолСтроквТаблицеНО = ТаблицаНО.Количество();
				
				Для Инд = 1 По КолСтроквТаблицеНО Цикл
					
					ОбрИнд = КолСтроквТаблицеНО - Инд;
					ТекКПП = СокрЛП(ТаблицаНО[ОбрИнд].КПП);
					
					Если ЗначениеЗаполнено(ТекКПП)
						И КодыПричиныПостановкиНаУчет.Найти(Сред(ВРег(ТекКПП), 5, 2)) = Неопределено Тогда
						
						ТаблицаНО.Удалить(ОбрИнд);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.ЗначенияДляОтбора.Количество() > 0 Тогда
		
		Если Параметры.ЗначенияДляОтбора[0].Свойство("НалоговыеОрганы") Тогда
								
			Элементы.ТаблицаНО.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Верх;
			Элементы.ТаблицаНОПометка.Видимость = Истина;
			Элементы.ТаблицаНОВключитьОтключитьОтбор.Видимость = Ложь;
			
			Для Каждого НалоговыйОрган Из Параметры.ЗначенияДляОтбора[0].НалоговыеОрганы Цикл
		   
				НайденныеСтроки = ТаблицаНО.НайтиСтроки(Новый Структура("Ссылка", НалоговыйОрган));
				
				Если НайденныеСтроки.Количество() > 0 Тогда
					
					НайденныеСтроки[0].Пометка = Истина;
					
				КонецЕсли;
						   
	   		КонецЦикла;
			
		Иначе
						
			НайденныеНО = ТаблицаНО.НайтиСтроки(Параметры.ЗначенияДляОтбора[0]);
			
			Если НайденныеНО.Количество() > 0 Тогда
				
				Элементы.ТаблицаНО.ТекущаяСтрока = НайденныеНО[0].ПолучитьИдентификатор();
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Параметры.НалоговыеОрганы) Тогда
				
				Элементы.ТаблицаНО.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Верх;
				Элементы.Флажки.Видимость = Ложь;
				
				ВключенОтбор = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
						
КонецПроцедуры

&НаСервере
Процедура СформироватьТаблицуНО()
				
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникИФНС.Ссылка КАК Ссылка,
	|	СправочникИФНС.КПП КАК КПП,
	|	СправочникИФНС.Код КАК КодНО,
	|	СправочникИФНС.Наименование КАК Наименование,
	|	СправочникИФНС.Представитель КАК Представитель
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК СправочникИФНС
	|ГДЕ
	|	(СправочникИФНС.Владелец = &Организация
	|			ИЛИ СправочникИФНС.Владелец = &ГоловнаяОрганизация)
	|	И НЕ СправочникИФНС.ПометкаУдаления
	|"
	+ ?(ЗначениеЗаполнено(НалоговыеОрганы), "И СправочникИФНС.Ссылка В (&НалоговыеОрганы)", "") +	"
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодНО,
	|	КПП";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", РегламентированнаяОтчетность.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("НалоговыеОрганы", НалоговыеОрганы);
		
	ТаблицаНО.Загрузить(Запрос.Выполнить().Выгрузить());
		          	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	                                 	
	УстановитьИлиСнятьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИлиСнятьФлажки(Пометка)
	
	Для Каждого Элемент Из ТаблицаНО Цикл
		Элемент.Пометка = Пометка;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОтключитьОтбор(Команда)
	
	Если ВключенОтбор Тогда
		
		КопияСпискаНалоговыхОрганов = НалоговыеОрганы.ВыгрузитьЗначения();
		
		НалоговыеОрганы.Очистить();
		
		СформироватьТаблицуНО();
		
		НалоговыеОрганы.ЗагрузитьЗначения(КопияСпискаНалоговыхОрганов);
		
		Элементы.ТаблицаНОВключитьОтключитьОтбор.Картинка = БиблиотекаКартинок.ОтборКомпоновкиДанных;
		
		ВключенОтбор = Ложь;
						
	Иначе
		
		СформироватьТаблицуНО();
		
		Элементы.ТаблицаНОВключитьОтключитьОтбор.Картинка = БиблиотекаКартинок.ОтключитьОтбор;
		
		ВключенОтбор = Истина;
		
	КонецЕсли;
	    	
КонецПроцедуры

#КонецОбласти