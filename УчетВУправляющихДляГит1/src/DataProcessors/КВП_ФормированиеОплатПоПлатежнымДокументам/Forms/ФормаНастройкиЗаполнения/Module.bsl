
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
// Обновляет вид операции в настройках заполнения.
Процедура ОбновитьВидОперацииВТаблицеНастройкиЗаполнения()
	
	Для Каждого ТекСтрока Из НастройкаЗаполнения Цикл
		
		СтруктураОтбора = Новый Структура("Документ", ТекСтрока.Документ);
		
		ТаблицаВидов = НастройкаЗаполненияВидовОпераций.НайтиСтроки(СтруктураОтбора);
		КоличествоВидов = ТаблицаВидов.Количество();
		
		СтруктураОтбора.Вставить("Пометка", Истина);
		
		ТаблицаВидовУстановленных = НастройкаЗаполненияВидовОпераций.НайтиСтроки(СтруктураОтбора);
		КоличествоВидовУстановленных = ТаблицаВидовУстановленных.Количество();
		
		Если КоличествоВидов = КоличествоВидовУстановленных Тогда
			ТекСтрока.ВидыОпераций = "<Все виды>";
		Иначе
			ТекСтрока.ВидыОпераций = "<Выбранные виды>";
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
// Функция помещает настройки видов операций в хранилище.
Функция ПоместитьНастройкиВидовОперацийВХранилище(ДокументНастройки)
	
	СтруктураОтбор = Новый Структура("Документ", ДокументНастройки);
	
	ВидыОпераций = НастройкаЗаполненияВидовОпераций.НайтиСтроки(СтруктураОтбор);
	ТаблицаВидовОпераций = НастройкаЗаполненияВидовОпераций.Выгрузить(ВидыОпераций, "Пометка,ВидОперации");
	
	АдресНастроекВидовОплаты = ПоместитьВоВременноеХранилище(ТаблицаВидовОпераций, Новый УникальныйИдентификатор);
	
	Возврат АдресНастроекВидовОплаты;
	
КонецФункции

&НаСервере
// Функция получает настройки из хранилища.
Процедура ПолучитьНастройкиВидовОперацийИзХранилища(АдресНастроекВидовОплаты)
	
	ТаблицаВидовОпераций = ПолучитьИзВременногоХранилища(АдресНастроекВидовОплаты);
	
	Для Каждого ТекущаяСтрокаВида Из ТаблицаВидовОпераций Цикл
		СтрокиВида = НастройкаЗаполненияВидовОпераций.НайтиСтроки(Новый Структура("ВидОперации", ТекущаяСтрокаВида.ВидОперации));
		Если СтрокиВида.Количество() > 0 Тогда
			СтрокиВида[0].Пометка = ТекущаяСтрокаВида.Пометка;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьВидОперацииВТаблицеНастройкиЗаполнения();
	
КонецПроцедуры

&НаСервере
// Процедура помещает настройки заполнения на сервере.
Функция ПоместитьНастройкиЗаполненияНаСервере()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("НастройкаЗаполнения",              НастройкаЗаполнения.Выгрузить());
	СтруктураНастроек.Вставить("НастройкаЗаполненияВидовОпераций", НастройкаЗаполненияВидовОпераций.Выгрузить());
	АдресХранилищаНастроек = ПоместитьВоВременноеХранилище(СтруктураНастроек, Новый УникальныйИдентификатор);
	Возврат АдресХранилищаНастроек;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("АдресХранилищаНастроек") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек = ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаНастроек);
	
	НастройкаЗаполнения.Загрузить(СтруктураНастроек.НастройкаЗаполнения);
	НастройкаЗаполненияВидовОпераций.Загрузить(СтруктураНастроек.НастройкаЗаполненияВидовОпераций);
	
	ОбновитьВидОперацииВТаблицеНастройкиЗаполнения();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Выбор" поля "НастройкаЗаполнения".
Процедура НастройкаЗаполненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	АдресНастроекВидовОплаты = ПоместитьНастройкиВидовОперацийВХранилище(НастройкаЗаполнения.НайтиПоИдентификатору(ВыбраннаяСтрока).Документ);
	
	ОткрытьФорму("Обработка.КВП_ФормированиеОплатПоПлатежнымДокументам.Форма.ФормаНастройкиВидовОпераций",
				 Новый Структура("АдресНастроекВидовОплаты", АдресНастроекВидовОплаты),
				 ЭтаФорма,,,,
				 Новый ОписаниеОповещения("ОбработатьРезультатЗакрытияФормыНастройкиВидовОпераций", ЭтаФорма),
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // НастройкаЗаполненияВыбор()

&НаКлиенте
// Обработчик результата закрытия формы настроек видо операций, вызванной
// в процедуре "НастройкаЗаполненияВыбор()".
Процедура ОбработатьРезультатЗакрытияФормыНастройкиВидовОпераций(АдресНастроекВидовОплаты, ДопПараметры) Экспорт
	
	Если ЗначениеЗаполнено(АдресНастроекВидовОплаты) Тогда
		ПолучитьНастройкиВидовОперацийИзХранилища(АдресНастроекВидовОплаты);
	КонецЕсли;
	
КонецПроцедуры // ОбработатьРезультатЗакрытияФормыНастройкиВидовОпераций()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик события "КомандаОК".
Процедура КомандаОК(Команда)
	
	АдресХранилищаНастроек = ПоместитьНастройкиЗаполненияНаСервере();
	Закрыть(АдресХранилищаНастроек);
	
КонецПроцедуры

#КонецОбласти