
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Обработки.РегистрацияОрганизации.СервисУзнатьИННДоступен() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис недоступен'"), , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеФизическогоЛица = Параметры.ДанныеФизическогоЛица;
	
	ДлительнаяОперацияПриЗапуске = ЗапуститьПолучениеКартинкиВФоне();
	
	ОбновитьКартинку = ТекстОбновитьКартинку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ДлительнаяОперацияПриЗапуске <> Неопределено Тогда
		
		ЗапуститьПроверкуДлительнойОперацииПолученияКартинки(ДлительнаяОперацияПриЗапуске);
		
		ДлительнаяОперацияПриЗапуске = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(КодСКартинки) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Код с картинки'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КодСКартинки", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиКСервисуФНС" Тогда
		СтандартнаяОбработка = Ложь;
		ОрганизацииФормыКлиент.ПерейтиКСервисуУзнатьИНН();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКартинкуОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОбновитьКартинку" Тогда
		СтандартнаяОбработка = Ложь;
		ДлительнаяОперация = ЗапуститьПолучениеКартинкиВФоне();
		Если ДлительнаяОперация <> Неопределено Тогда
			ЗапуститьПроверкуДлительнойОперацииПолученияКартинки(ДлительнаяОперация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УзнатьИНН(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВводКода Тогда
		
		ДлительнаяОперация = ЗапуститьПолучениеИННВФоне();
		
		Если ДлительнаяОперация = Неопределено Тогда
			// Ошибка проверки заполнения
			Возврат;
		КонецЕсли;
		
		Если ДлительнаяОперация.Статус = "Ошибка" Тогда
			
			ПоказатьОшибкуПолученияИНН();
			
		ИначеЕсли ДлительнаяОперация.Статус = "Выполнено" Тогда
			
			ПослеЗавершенияФоновогоЗаданияПолученияИНН(ДлительнаяОперация, Неопределено);
			
		Иначе
			
			ПоказатьДлительнуюОперацию();
			
			НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
			
			Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияФоновогоЗаданияПолученияИНН", ЭтотОбъект);
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьОшибкуПолученияКартинкиСКодом()
	
	ЗаголовокОшибки = НСтр("ru = 'При определении ИНН произошла ошибка'");
	
	МассивОписанияОшибки = Новый Массив;
	МассивОписанияОшибки.Добавить(Символы.ПС);
	ДобавитьТекстПроСервисФНС(МассивОписанияОшибки);
	
	ОписаниеОшибки = Новый ФорматированнаяСтрока(МассивОписанияОшибки);
	
	Элементы.УзнатьИНН.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкуПолученияИНН(Знач ТекстОшибки = "")
	
	МассивОписанияОшибки = Новый Массив;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		// Сервис вернул ошибку
		ЗаголовокОшибки = НСтр("ru = 'При определении ИНН произошла ошибка'");
		
		МассивОписанияОшибки.Добавить(Символы.ПС);
		ДобавитьТекстПроСервисФНС(МассивОписанияОшибки);
	Иначе
		ЗаголовокОшибки = НСтр("ru = 'Не удалось определить ИНН'");
		
		МассивОписанияОшибки.Добавить(НСтр("ru = 'Возможно, паспортные данные указаны неверно.
												|Измените паспортные данные и попробуйте еще раз или воспользуйтесь'"));
		МассивОписанияОшибки.Добавить(" ");
		МассивОписанияОшибки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'сервисом ФНС'"), , , , "ПерейтиКСервисуФНС"));
	КонецЕсли;
	
	ОписаниеОшибки = Новый ФорматированнаяСтрока(МассивОписанияОшибки);
	
	Элементы.УзнатьИНН.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОшибка;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьВводКартинки(Токен, АдресДанных)
	
	ТокенКартинки        = Токен;
	АдресДанныхКартинки  = АдресДанных;
	
	Элементы.УзнатьИНН.Видимость = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВводКода;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДлительнуюОперацию()
	
	Элементы.УзнатьИНН.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОжидание;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьПолучениеКартинкиВФоне()
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение картинки с кодом'");
	
	ПараметрыПолученияКодаСКартинкой = Новый Структура;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РегистрацияОрганизации.ПолучитьКартинкуСКодомВФоне",
		ПараметрыПолученияКодаСКартинкой, НастройкиЗапуска);
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ПоказатьОшибкуПолученияКартинкиСКодом();
		
		Возврат Неопределено;
		
	ИначеЕсли ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатВыполненияФоновогоЗаданияПолученияКартинки(ДлительнаяОперация.АдресРезультата);
		
		Возврат Неопределено;
		
	Иначе
		
		ПоказатьДлительнуюОперацию();
		
		Возврат ДлительнаяОперация;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьПроверкуДлительнойОперацииПолученияКартинки(ДлительнаяОперация)
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияФоновогоЗаданияПолученияКартинки", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияФоновогоЗаданияПолученияКартинки(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатВыполненияФоновогоЗаданияПолученияКартинки(ДлительнаяОперация.АдресРезультата);
		
	Иначе
		
		ПоказатьОшибкуПолученияКартинкиСКодом();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатВыполненияФоновогоЗаданияПолученияКартинки(АдресРезультата)
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если РезультатВыполнения = Неопределено Тогда
		ПоказатьОшибкуПолученияКартинкиСКодом();
	Иначе
		АдресДанныхКартинки = ПоместитьВоВременноеХранилище(РезультатВыполнения.ДвоичныеДанныеКартинки);
		КодСКартинки = "";
		ТекущийЭлемент = Элементы.КодСКартинки;
		ПоказатьВводКартинки(РезультатВыполнения.Токен, АдресДанныхКартинки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьПолучениеИННВФоне()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОбновитьКартинку = ТекстОбновитьКартинку();
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Определение ИНН'");
	
	ПараметрыПолученияИНН = ПараметрыПолученияИНН();
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РегистрацияОрганизации.ПолучитьИННВФоне",
		ПараметрыПолученияИНН, НастройкиЗапуска);
	
КонецФункции

&НаСервере
Функция ПараметрыПолученияИНН()
	
	ПараметрыПолученияИНН = Новый Структура();
	ПараметрыПолученияИНН.Вставить("ДанныеФизическогоЛица", ДанныеФизическогоЛица);
	ПараметрыПолученияИНН.Вставить("Токен", ТокенКартинки);
	ПараметрыПолученияИНН.Вставить("КодСКартинки", КодСКартинки);
	
	Возврат ПараметрыПолученияИНН;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияФоновогоЗаданияПолученияИНН(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		Результат = ОбработатьРезультатВыполненияФоновогоЗаданияПолученияИНН(ДлительнаяОперация.АдресРезультата);
		Если Результат.Действие = "ОбновитьКартинку" Тогда
			Если Результат.ДлительнаяОперация <> Неопределено Тогда
				ЗапуститьПроверкуДлительнойОперацииПолученияКартинки(ДлительнаяОперация);
			КонецЕсли;
		ИначеЕсли Результат.Действие = "ОповеститьОВыбореИНН" Тогда
			Если ЗначениеЗаполнено(Результат.ИНН) Тогда
				ОповеститьОВыбореИННиЗакрыть(Результат.ИНН);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		ПоказатьОшибкуПолученияИНН();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатВыполненияФоновогоЗаданияПолученияИНН(АдресРезультата)
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Результат = Новый Структура();
	Результат.Вставить("Действие", "");
	Результат.Вставить("ДлительнаяОперация", Неопределено);
	Результат.Вставить("ИНН", "");
	
	Если ЗначениеЗаполнено(РезультатВыполнения.Ошибка) Тогда
		Если РезультатВыполнения.Ошибка = "НеверныйКод" Тогда
			ОбновитьКартинку = ТекстОшибкаВводаКода();
			ДлительнаяОперация = ЗапуститьПолучениеКартинкиВФоне();
		Иначе
			ПоказатьОшибкуПолученияИНН(РезультатВыполнения.ТекстОшибки);
		КонецЕсли;
		
		Возврат Результат;
	ИначеЕсли НЕ ЗначениеЗаполнено(РезультатВыполнения.ИНН) Тогда
		ПоказатьОшибкуПолученияИНН();
		Возврат Результат;
	Иначе
		Результат.Действие = "ОповеститьОВыбореИНН";
		Результат.ИНН = РезультатВыполнения.ИНН;
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОповеститьОВыбореИННиЗакрыть(ИНН)
	
	ОповеститьОВыборе(ИНН);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстОбновитьКартинку()
	
	Возврат Новый ФорматированнаяСтрока(НСтр("ru = 'Обновить картинку'"), , , , "ОбновитьКартинку");
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстОшибкаВводаКода()
	
	ЦветНадписи = Новый Цвет(178, 34, 34);
	Возврат Новый ФорматированнаяСтрока(НСтр("ru = 'Код введен неверно'"), , ЦветНадписи);
	
КонецФункции

&НаСервере
Процедура ДобавитьТекстПроСервисФНС(МассивОписанияОшибки)
	
	МассивОписанияОшибки.Добавить(НСтр("ru = 'Попробуйте узнать ИНН с помощью'"));
	МассивОписанияОшибки.Добавить(" ");
	МассивОписанияОшибки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'сервиса ФНС'"), , , , "ПерейтиКСервисуФНС"));
	
КонецПроцедуры

#КонецОбласти