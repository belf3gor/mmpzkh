
#Область ПроцедурыМодуляФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Процесс обновления конфигурации %1'"), Метаданные.Синоним);
	
	Макет = Обработки.Удалить_КВП_ОбновлениеИнформационнойБазы.ПолучитьМакет("ОписаниеРезультатаОбновления");
	
	// Сначала выводим важные сообщения.
	Если Параметры.Свойство("МассивВажныхСообщенийДляВывода") Тогда
		
		Если Параметры.МассивВажныхСообщенийДляВывода.Количество() > 0 Тогда
			
			ВывестиИнформациюОбОшибкахВТабличныйДокумент(ОписаниеОбновлений, Параметры.МассивВажныхСообщенийДляВывода, Макет, Истина);
			
			// Добавляем пустое сообщение для отделения важной информации от других сообщений.
			ОбластьРазделитель = Макет.ПолучитьОбласть("Разделитель");
			ОписаниеОбновлений.Вывести(ОбластьРазделитель);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Выводим остальные сообщения.
	Если Параметры.Свойство("МассивСообщенийДляВывода")Тогда
		
		Если Параметры.МассивСообщенийДляВывода.Количество() > 0 Тогда
			
			// Данные сообщения будут выводится под заголовком "Техническая информация".
			ОбластьТехническаяИнформация = Макет.ПолучитьОбласть("ТехническаяИнформация");
			ОписаниеОбновлений.Вывести(ОбластьТехническаяИнформация);
			
			ВывестиИнформациюОбОшибкахВТабличныйДокумент(ОписаниеОбновлений, Параметры.МассивСообщенийДляВывода, Макет);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия гиперссылки "ПоказатьСведенияОРезультатахОбновления"
//
Процедура ПоказатьСведенияОРезультатахОбновленияНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказатьОшибкиИПредупреждения", Истина);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма.ЖурналРегистрации", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
// Выводит сообщения для пользователя в табличный документ.
Процедура ВывестиИнформациюОбОшибкахВТабличныйДокумент(ОписаниеОбновлений, МассивСообщений, Макет, ВажныеСообщения = Ложь)
	
	ОбластьТекстаВажногоСообщения = Макет.ПолучитьОбласть("ТекстВажногоСообщения");
	ОбластьТекстаСообщения        = Макет.ПолучитьОбласть("ТекстСообщения");
	
	// Каждый элемент массива является структурой с ключами "ЭтоСообщениеОбОшибке" и "ТекстСообщения".
	// Структура используется потому, что при выводе сообщения, необходимо анализировать это сообщение об ошибке или нет.
	// Рядом с сообщением об ошибке, при выводе будет отображаться картинка красного восклицательного знака, а если
	// сообщение не об ошибке, рядом будет отображаться картинка зеленой галки.
	Для Каждого ТекСообщение Из МассивСообщений Цикл
		
		Если ТекСообщение.ЭтоСообщениеОбОшибке Тогда
			ОбластьКартинки = Макет.ПолучитьОбласть("КартинкаОперацияВыполненаСОшибками");
		Иначе
			ОбластьКартинки = Макет.ПолучитьОбласть("КартинкаОперацияВыполненаУспешно");
		КонецЕсли;
		
		ОписаниеОбновлений.Вывести(ОбластьКартинки);
		
		// Важные сообщения будут выделены жирным шрифтом.
		Если ВажныеСообщения Тогда
			
			ОбластьТекстаВажногоСообщения.Параметры.ТекстСообщения = ТекСообщение.ТекстСообщения;
			ОписаниеОбновлений.Присоединить(ОбластьТекстаВажногоСообщения);
			
		Иначе
			
			ОбластьТекстаСообщения.Параметры.ТекстСообщения = ТекСообщение.ТекстСообщения;
			ОписаниеОбновлений.Присоединить(ОбластьТекстаСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
