
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ЭД") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТекущийКомментарий", Комментарий);
	
	ОбъектыДляОбработки = Новый Массив;
	Если ТипЗнч(Параметры.ЭД) <> Тип("Массив") Тогда
		ОбъектыДляОбработки.Добавить(Параметры.ЭД);
	Иначе
		ОбъектыДляОбработки = Параметры.ЭД;
	КонецЕсли;
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбъектыДляОбработки, УникальныйИдентификатор);
	
	Если ОбъектыДляОбработки.Количество() > 1 Тогда
		Элементы.ОбъектыДляОбработки.Заголовок = НСтр("ru = 'Список'");
	КонецЕсли;
	
	Если Параметры.Свойство("Ответственный") Тогда
		Пользователь = Параметры.Ответственный;
	КонецЕсли;
	Если ОбъектыДляОбработки.Количество() = 1 Тогда
		ЭлектронныйДокумент = ОбъектыДляОбработки[0];
		ТекстГиперссылки = ОбменСКонтрагентамиСлужебный.ПолучитьПредставлениеЭД(ЭлектронныйДокумент);
	Иначе
		ТекстГиперссылки = НСтр("ru = 'Электронные документы (%1)'");
		ТекстГиперссылки = СтрЗаменить(ТекстГиперссылки, "%1", ОбъектыДляОбработки.Количество());
	КонецЕсли;
	
	ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
	НадписьОбъектыДляОбработки = ТекстГиперссылки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектыДляОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивЭД = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если МассивЭД.Количество() > 1 Тогда
		ФормаПросмотраЭД = ОткрытьФорму("Обработка.ОбменСКонтрагентами.Форма.СписокВыгружаемыхЭлектронныхДокументов",
			Новый Структура("МассивСсылокЭД", МассивЭД), ЭтотОбъект);
	Иначе
		ПоказатьЗначение(, МассивЭД[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан ответственный.'"),, "Пользователь");
		Возврат;
	КонецЕсли;
	ВсегоЭД = 0;
	Результат = ПеренаправитьЭД(ВсегоЭД);
	ОбменСКонтрагентамиСлужебныйКлиент.ОповеститьПользователяОСменеОтветственного(Пользователь, ВсегоЭД, Результат);
	Оповестить("ОбновитьТекущиеДелаЭДО");
	Закрыть(Результат > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПеренаправитьЭД(ВсегоЭД)
	
	МассивЭД = ПолучитьИзВременногоХранилища(АдресХранилища);
	ВсегоЭД = МассивЭД.Количество();
	КоличествоОбработанныхЭД = 0;
	ОбменСКонтрагентамиСлужебный.УстановитьОтветственногоЭД(МассивЭД,
		Пользователь, КоличествоОбработанныхЭД, Комментарий);
	
	Возврат КоличествоОбработанныхЭД;
	
КонецФункции

#КонецОбласти
