///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПустаяСтрока(Параметры.ТекстЗаголовка) Тогда
		Заголовок = Параметры.ТекстЗаголовка;
	КонецЕсли;
	
	Элементы.ПоясняющийТекст.Заголовок = Параметры.ПоясняющийТекст;
	Элементы.ПоясняющийТекст.Видимость = НЕ ПустаяСтрока(Параметры.ПоясняющийТекст);
	
	ТипДанных = Параметры.ТипДанных;
	
	Если ТипДанных = "string" Тогда
		ВводимыеДанные = "";
		
	ИначеЕсли ТипДанных = "dateTime" Тогда
		ВводимыеДанные = '00010101';
		
	ИначеЕсли ТипДанных = "date" Тогда
		ВводимыеДанные = '00010101';
		Элементы.ВводимыеДанные.ФорматРедактирования = "ДЛФ=D";
		
	ИначеЕсли ТипДанных = "time" Тогда
		ВводимыеДанные = '00010101';
		Элементы.ВводимыеДанные.ФорматРедактирования = "ДЛФ=T";
		Элементы.ВводимыеДанные.КнопкаВыбора = Ложь;
		
	ИначеЕсли ТипДанных = "number" Тогда
		ВводимыеДанные = 0;
		Элементы.ВводимыеДанные.ФорматРедактирования = "ЧДЦ="
			+ ?(ПустаяСтрока(Параметры.ТочностьЧисла), "0", Параметры.ТочностьЧисла);
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный тип вводимых данных.'");
		
	КонецЕсли;
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(XMLСтрокаЗначения(ВводимыеДанные));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
		ХранилищеСистемныхНастроек.Удалить("Обработка.Подключение1СТакском.Форма.ВводДанных",
			"",
			ИмяПользователя);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция XMLСтрокаЗначения(Знач Значение)
	Возврат XMLСтрока(Значение);
КонецФункции

#КонецОбласти
