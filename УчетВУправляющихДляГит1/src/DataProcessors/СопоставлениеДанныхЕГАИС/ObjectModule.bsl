#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ТаблицаТовары = Товары.Выгрузить();
	
	СведенияОбАлкогольнойПродукции       = СведенияОбАлкогольнойПродукции(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТовары, "Номенклатура", Истина));
	СведенияОбАлкогольнойПродукцииЕГАИС  = СведенияОбАлкогольнойПродукцииЕГАИС(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТовары,"АлкогольнаяПродукция", Истина));

	СоответствиеВидовЛицензийЕГАИС   = Обработки.СопоставлениеДанныхЕГАИС.СоответствиеВидовЛицензийЕГАИС();
	
	КонтрагентыЕГАИС = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТовары, "Производитель", Истина);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КонтрагентыЕГАИС, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТовары, "Импортер", Истина));
	
	СоотвествиеКонтрагентыЕГАИС = Обработки.СопоставлениеДанныхЕГАИС.СоответствиеКонтрагентовЕГАИС(КонтрагентыЕГАИС);
	СоотвествиеКонтрагентыЕГАИС.Вставить(Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка(), Справочники.Контрагенты.ПустаяСсылка());
	
	Для каждого СтрокаТовары Из Товары Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыСОшибкой = Новый Массив;
		
		СтрокаСведенийНоменклатура      = СведенияОбАлкогольнойПродукции[СтрокаТовары.Номенклатура];
		СтрокаСведенийНоменклатураЕГАИС = СведенияОбАлкогольнойПродукцииЕГАИС[СтрокаТовары.АлкогольнаяПродукция];
		
		Если СтрокаСведенийНоменклатура.Импортер <> СоотвествиеКонтрагентыЕГАИС[СтрокаТовары.Импортер] Тогда
			РеквизитыСОшибкой.Добавить(Нстр("ru = '""Импортер""'"));
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(СтрокаСведенийНоменклатура.Импортер) 
			И СтрокаСведенийНоменклатура.Производитель <> СоотвествиеКонтрагентыЕГАИС[СтрокаТовары.Производитель] Тогда
			РеквизитыСОшибкой.Добавить(НСтр("ru = '""Производитель""'"));
		КонецЕсли; 
		
		
		Если СтрокаСведенийНоменклатура.ЕстьСведенияОбАлкогольнойПродукции Тогда
			
			Если СтрокаСведенийНоменклатура.КодВида169 <> СтрокаСведенийНоменклатураЕГАИС.Код Тогда
				РеквизитыСОшибкой.Добавить(НСтр("ru = '""Код вида алкогольной продукции""'"));
			КонецЕсли;
			Если СтрокаСведенийНоменклатура.Объем <> СтрокаТовары.Объем Тогда
				РеквизитыСОшибкой.Добавить(НСтр("ru = '""Объем""'"));
			КонецЕсли;
			Если СтрокаСведенийНоменклатура.ВидЛицензии <> СоответствиеВидовЛицензийЕГАИС[СтрокаСведенийНоменклатураЕГАИС.ВидЛицензии] Тогда
				РеквизитыСОшибкой.Добавить(НСтр("ru = '""Вид лицензии""'"));
			КонецЕсли;
			
		КонецЕсли;
		
		Если РеквизитыСОшибкой.Количество() > 0 Тогда
			ИмяСписка = НСтр("ru = 'Товары'");
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Реквизиты %1 не соответствуют данным ЕГАИС'"), СтрСоединить(РеквизитыСОшибкой, ", "));
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность", НСтр("ru = 'Номенклатура'"), СтрокаТовары.НомерСтроки, ИмяСписка, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Товары", СтрокаТовары.НомерСтроки, "Номенклатура"), , Отказ);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СведенияОбАлкогольнойПродукции(СписокНоменклатуры)

	ЗначенияРеквизитов = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка,
	|	НЕ СведенияОбАлкогольнойПродукции.Номенклатура ЕСТЬ NULL  КАК ЕстьСведенияОбАлкогольнойПродукции,
	|	СправочникНоменклатура.Производитель,
	|	СправочникНоменклатура.Импортер,
	|	ЕСТЬNULL(СведенияОбАлкогольнойПродукции.КодВида169, НЕОПРЕДЕЛЕНО) КАК КодВида169,
	|	ЕСТЬNULL(СведенияОбАлкогольнойПродукции.ВидЛицензии, НЕОПРЕДЕЛЕНО) КАК ВидЛицензии,
	|	ЕСТЬNULL(СведенияОбАлкогольнойПродукции.КоэффПересчетаДал, 0) * 10 КАК Объем
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
	|		ПО СправочникНоменклатура.Ссылка = СведенияОбАлкогольнойПродукции.Номенклатура
	|ГДЕ
	|	СправочникНоменклатура.Ссылка В(&СписокНоменклатуры)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура("ЕстьСведенияОбАлкогольнойПродукции, КодВида169, ВидЛицензии, Производитель, Импортер, Объем");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗначенияРеквизитов.Вставить(Выборка.Ссылка, Результат);
	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;
КонецФункции

Функция СведенияОбАлкогольнойПродукцииЕГАИС(СписокНоменклатуры)
	
	ЗначенияРеквизитов = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыАлкогольнойПродукцииЕГАИС.ВидЛицензии,
	|	ВидыАлкогольнойПродукцииЕГАИС.Код,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
	|ИЗ
	|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукцииЕГАИС
	|		ПО КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции = ВидыАлкогольнойПродукцииЕГАИС.Ссылка
	|ГДЕ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка В(&СписокНоменклатуры)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура("ВидЛицензии, Код");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗначенияРеквизитов.Вставить(Выборка.Ссылка, Результат);
	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;
КонецФункции

#КонецОбласти 

#КонецЕсли