
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуРазделов();
	
	ПоказыватьПриОткрытииПрограммы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НачалоРаботыВ30", "Показывать", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьСтартовуюСтраницу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ДанныеСобытия.Свойство("href") И ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
		ИмяОткрываемойСтраницы = СокрЛП(ДанныеСобытия.href);
	Иначе
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ИмяОткрываемойСтраницы, "ShowMaket=") > 0 Тогда
		ПерейтиПоСсылкеНаМакет(ИмяОткрываемойСтраницы);
	Иначе
		ПерейтиПоНавигационнойСсылке(ИмяОткрываемойСтраницы );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПриОткрытииПрограммыПриИзменении(Элемент)
	
	СохранитьНастройкиПоказыватьПриОткрытииПрограммы(ПоказыватьПриОткрытииПрограммы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция загружает содержание макетов в таблицу на форму
// При этом формирует содержание по разделам
//
// Возвращаемое значение:
//   <ТекстСодержания>   - html макет содержащий раздел "Содержание" полученный из добавленных макетов
//
&НаСервере
Функция ЗагрузитьМакетыНаФорму()
	
	ТекущийОбъект 		= РеквизитФормыВЗначение("Объект");
	МетаданныеОбработки = ТекущийОбъект.Метаданные(); 
	ТекстСодержания 	= "";
	
	Для Каждого Макет Из МетаданныеОбработки.Макеты Цикл
		
		МакетHTML 				= ТекущийОбъект.ПолучитьМакет(Макет.Имя);
		URLМакета 				= МакетHTML.ПолучитьДокументHTML().БазовыйURI;
		
		НоваяСтрока 			= СписокМакетов.Добавить();
		НоваяСтрока.Макет		= Макет.Имя;
		НоваяСтрока.URLМакета	= URLМакета;
		НоваяСтрока.Содержание	= МакетHTML.ПолучитьТекст();
		
		ТекстСодержания = ТекстСодержания 
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<P><A id=%1 href=""ShowMaket=%2"">%3</A></P>"
						,Макет.Имя
						,URLМакета
						,Макет.Синоним);
		
	КонецЦикла;
	
	Возврат ТекстСодержания;

КонецФункции

// Добавляем раздел "Содержание" во все макеты
//
&НаСервере
Процедура УстановитьСодержаниеПоРазделам(ТекстСодержания)

	Для Каждого Макет из СписокМакетов Цикл
		
		Макет.Содержание = СтрЗаменить(Макет.Содержание, 
				"<td id=""contents"" valign=""top"" width=""150""></td>",
				"<td id=""contents"" valign=""top"" width=""150"">" + ТекстСодержания + "</td>");
		
	КонецЦикла;	
			
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьТаблицуРазделов()

	ТекстСодержания = ЗагрузитьМакетыНаФорму();
	
	УстановитьСодержаниеПоРазделам(ТекстСодержания);

КонецПроцедуры

// Возращает найденную по параметрам строку
//
// Параметры:
//  <ЗначениеРеквизита>  - <Строка> - что ищем
//  <ИмяРеквизита>  	 - <Строка> - где ищем
//
// Возвращаемое значение:
//   <СтрокаТаблицыЗначений>   - строка таблицы значений найденная по значению реквизита. Если не найдено - то Неопределено
//
&НаКлиенте
Функция НайтиСтрокуМакета(ЗначениеРеквизита, ИмяРеквизита)

	ОтборыДляПоиска = Новый Структура(ИмяРеквизита, ЗначениеРеквизита);
	
	НайденныеСтроки = СписокМакетов.НайтиСтроки(ОтборыДляПоиска);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
	
		Возврат Неопределено;
	
	Иначе
	
		Возврат НайденныеСтроки[0];
	
	КонецЕсли; 
	

КонецФункции 

&НаКлиенте
Процедура УстановитьРаздел(Раздел)

	Если Раздел <> Неопределено Тогда
		
		Попытка
			ПолеHTMLДокумента 	= Раздел.Содержание;
			ТекущийМакет 		= Раздел;
		Исключение
		КонецПопытки;
		
	КонецЕсли; 

КонецПроцедуры 

// Процедура устанавливает стартовую строку макета
//
&НаКлиенте
Процедура УстановитьСтартовуюСтраницу()
	
	РазделНачало = НайтиСтрокуМакета("Начало", "Макет");
	
	УстановитьРаздел(РазделНачало);
	
КонецПроцедуры 

// Перейти по ссылке на макет текущей обработки
//
// Параметры:
//  <ИмяОткрываемойСтраницы>  - <Строка> - внутренний URL открываемой страницы
//
&НаКлиенте
Процедура ПерейтиПоСсылкеНаМакет(ИмяОткрываемойСтраницы)
	
	Позиция 		= СтрНайти(ИмяОткрываемойСтраницы, "ShowMaket=");
	URLМакета 		= Сред(ИмяОткрываемойСтраницы,  Позиция + 10);
	РазделПоСсылке 	= НайтиСтрокуМакета(URLМакета,  "URLМакета");
	
	УстановитьРаздел(РазделПоСсылке);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура СохранитьНастройкиПоказыватьПриОткрытииПрограммы(ПоказыватьПриОткрытииПрограммы)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НачалоРаботыВ30", "Показывать", ПоказыватьПриОткрытииПрограммы);
	
КонецПроцедуры

#КонецОбласти
	
