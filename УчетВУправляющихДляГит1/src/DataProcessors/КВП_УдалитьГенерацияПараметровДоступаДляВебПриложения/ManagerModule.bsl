#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПечатьПамяткиДоступа(МассивОбъектов, ПараметрыПечати)
	
	// СБОР ИНФОРМАЦИИ.
	СтруктураПараметров = Новый Структура;
	ТабличныйДокумент   = Новый ТабличныйДокумент;
	
	Макет = УПЖКХ_ТиповыеМетодыСервер.ПолучитьМакет("Справочник.КВП_ЛицевыеСчета.ПФ_MXL_ПамяткаДляДоступаВЛичныйКабинетНаСайтеЖКХ");
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КВП_ЛицевыеСчета_ПамяткаДляДоступаВЛичныйКабинетНаСайтеЖКХ";
	
	Если МассивОбъектов.Количество() < 2 Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	ТипОбъекта = ТипЗнч(МассивОбъектов.Получить(1));
	
	// СБОР ИНФОРМАЦИИ.
	Организация = МассивОбъектов.Получить(0);
	СтруктураПараметров.Вставить("Организация", УПЖКХ_ПечатьКвитанцийСервер.ПолучитьНаименованиеОрганизации(Организация));
	
	// Первый элемент массива - ссылка на организацию.
	// Удаляем его.
	МассивОбъектов.Удалить(0);
	
	ТекущаяДата = УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьРабочуюДату();
	
	ПараметрыУчетнойПолитикиТСЖотказ = Ложь;
	ПараметрыУчетнойПолитикиТСЖ = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьПараметрыУчетнойПолитикиЖКХ(ТекущаяДата,
	Организация,
	ПараметрыУчетнойПолитикиТСЖотказ);
	
	Если ПараметрыУчетнойПолитикиТСЖотказ Тогда
		АдресСайта = "";
	Иначе
		АдресСайта = ПараметрыУчетнойПолитикиТСЖ.АдресСайтаКомпании;
	КонецЕсли;
	СтруктураПараметров.Вставить("АдресСайта", АдресСайта);
	
	ТаблицаВладельцевЛС = УПЖКХ_ПечатьКвитанцийСервер.ПолучитьТаблицуВладельцевЛицевыхСчетов(ТекущаяДата, МассивОбъектов);
	
	ПервыйЭлемент = Истина;
	Для Каждого ЛицевойСчет Из МассивОбъектов Цикл
		
		Если ПервыйЭлемент Тогда
			ПервыйЭлемент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		АдресДома = Справочники.КВП_Здания.ПолучитьПредставлениеЗдания(ЛицевойСчет.Адрес.Владелец);
		Адрес     = ПолучитьАдресПомещения(АдресДома, ЛицевойСчет.Адрес);
		
		Отбор = Новый Структура;
		Отбор.Вставить("Вид", Справочники.ВидыКонтактнойИнформации.УПЖКХ_АдресДоставкиСчетовИКвитанцийЛицевогоСчета);
		СтрокиАдреса = ЛицевойСчет.КонтактнаяИнформация.НайтиСтроки(Отбор);
		Если СтрокиАдреса.Количество() > 0 Тогда
			АдресДома = СтрокиАдреса[0].Представление;
			Адрес     = Адрес + " (адрес доставки: " + АдресДома + ")";
		КонецЕсли;
		СтруктураПараметров.Вставить("Адрес", Адрес);
		
		СтрокаВладельцаЛС = ТаблицаВладельцевЛС.Найти(ЛицевойСчет, "ЛицевойСчет");
		Если СтрокаВладельцаЛС = Неопределено Тогда
			ВладелецЛС = "";
		Иначе
			ВладелецЛС = СтрокаВладельцаЛС.Владелец;
		КонецЕсли;
		СтруктураПараметров.Вставить("ФИО", ВладелецЛС);
		
		СтруктураПараметров.Вставить("Логин",  ЛицевойСчет.Идентификатор);
		СтруктураПараметров.Вставить("Пароль", ЛицевойСчет.УдалитьПарольДоступа);
		
		// ВЫВОД ИНФОРМАЦИИ.
		Область = Макет.ПолучитьОбласть("Блок1");
		Область.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Блок2");
		Область.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Блок3");
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Блок4");
		Область.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(Область);
	КонецЦикла;
	
	ТабличныйДокумент.ПолеСнизу   = 0;
	ТабличныйДокумент.ПолеСверху  = 0;
	ТабличныйДокумент.ПолеСлева   = 0;
	ТабличныйДокумент.ПолеСправа  = 0;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, 
	КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПамяткаДоступа") Тогда
	
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
																   "ПамяткаДоступа",
																   "ПамяткаДоступа",
																   ПечатьПамяткиДоступа(МассивОбъектов, ПараметрыПечати));
	
	КонецЕсли;
	
КонецПроцедуры
	
#КонецЕсли