&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Объект.Организация = Параметры.Организация;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ОбновитьДанныеПоОрганизации();
	
	СпособСдачиОтчетности = "Интернет";
	
	Элементы.НадписьТребуютсяРеквизиты.Заголовок = ТекстНеЗаполненыРеквизитыДляОтчетности();
	
	ОтчетыЗаполнены    = Ложь;
	ОтчетыСформированы = Ложь;
	
	ОбновитьСостояниеПодготовкиОтчетности();
	
	НастроитьОтображениеТеста();
	
	РазрешенЭлектронныйДокументооброт = РазрешенЭлектронныйДокументооборот();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапуститьОбновлениеСпискаОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "Запись_Организации" И Источник = Объект.Организация)
		ИЛИ (ИмяСобытия = "ИзменениеУчетнойПолитики" И Параметр = Объект.Организация) Тогда
	
		ОбновитьДанныеПоОрганизации();
		
		Если ПомощникДоступен Тогда
			ЗапуститьОбновлениеСпискаОтчетов();
		Иначе
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "ОтчетыПрошлыхПериодов_ПроверкаЗавершена"
		И Источник = Объект.Организация Тогда
		
		ЗагрузитьРезультатТеста();
		ЗапуститьОбновлениеСпискаОтчетов();
		
	ИначеЕсли ИмяСобытия = "ОплатаСервиса_Изменение" Тогда
		
		РазрешенЭлектронныйДокументооброт = РазрешенЭлектронныйДокументооборот();
		ОтобразитьДействияСдачаОтчетности(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "НапечататьОтчет") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		АдресОтчета = АдресОтчета(НавигационнаяСсылкаФорматированнойСтроки);
		
		СформироватьБланкОтчета(АдресОтчета);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "ПоказатьОшибкиОтчета") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьОшибкиФормированияОтчета(НавигационнаяСсылкаФорматированнойСтроки);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "РеквизитыОрганизацииДляОтчетности") Тогда
		
		СообщениеТребуютсяРеквизитыОбработкаНавигационнойСсылки(Элементы.НадписьТребуютсяРеквизиты,
			НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СообщениеТребуютсяРеквизитыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ",                 Объект.Организация);
	ПараметрыФормы.Вставить("Назначение",           "ДляОтчетности");
	ПараметрыФормы.Вставить("ПроверяемыеРеквизиты", ПроверяемыеРеквизитыДляОтчетности());
	
	ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособСдачиОтчетностиПриИзменении(Элемент)
	
	ОтобразитьДействияСдачаОтчетности(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛичныйВизитКонтактыГосорганов_ФНС_АдресНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, ФНС_Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	Объект.Организация = ПолеОрганизация;
	
	ОбновитьДанныеПоОрганизации();
	
	Если ПомощникДоступен Тогда
		ЗапуститьОбновлениеСпискаОтчетов();
	Иначе
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТарифаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗаголовокСсылкиВозвратаКВладельцу", НСтр("ru='Вернуться к подготовке нулевой отчетности'"));
	ТарификацияБПКлиент.ОткрытьФормуВыбораТарифа(ЭтотОбъект, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПомощникНедоступенОбработкаНавигационнойСсылки(Элемент, ТекстСсылки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТекстСсылки = "ОткрытьМониторНалоговИОтчетности" Тогда
		ОбщегоНазначенияБПКлиент.ОткрытьМониторНалоговИОтчетности();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапуститьПроверкуПрошлыхПериодов(Команда)
	
	ПараметрыТеста = ПараметрыТестаПрошлыхПериодов();
	
	ОткрытьФорму("Обработка.МониторНалоговИОтчетности.Форма.ФормаТестПрошлыхПериодов", ПараметрыТеста, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключить1СОтчетность(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура Открыть1СОтчетность(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Раздел", ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты"));
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	
	ОткрытьФорму("ОбщаяФорма.РегламентированнаяОтчетность", ПараметрыФормы, , "1С-Отчетность");
	
КонецПроцедуры

&НаКлиенте
Процедура ОписьВложения(Команда)
	
	АдресДанныхОписи = ПодготовитьДанныеОписиВложения("ФНС");
	
	ДополнительныеПараметрыПечати = Новый Структура("АдресДанныхОписи", АдресДанныхОписи);
	
	ПараметрыПечати = Новый Структура("ЗаголовокФормы, ДополнительныеПараметры",
		"Опись вложения",
		ДополнительныеПараметрыПечати);
	
	ПараметрыКомандыПечати = Новый Массив;
	ПараметрыКомандыПечати.Добавить(Объект.Организация);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьОписиВложения",
		"ОписьВложения",
		ПараметрыКомандыПечати,
		ЭтотОбъект,
		ПараметрыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура Конверт(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектыПечати", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФНС_Контрагент));
	ПараметрыФормы.Вставить("СведенияОПолучателеКонверта", ФНС_СведенияОПолучателеКонверта);
	
	ОткрытьФорму("ОбщаяФорма.НастройкаПечатиКонвертов", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеПоОрганизации()
	
	НадписьОрганизация = Объект.Организация;
	ПолеОрганизация    = Объект.Организация;
	
	Сведения = Обработки.ПодготовкаНулевойОтчетности.СведенияОбОрганизации(Объект.Организация, Объект.Период);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Сведения);
	
	ЗагрузитьРезультатТеста();
	
	НастроитьКонтактныеДанныеГосорганов();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатТеста()
	
	ПериодыДобавленныеПроверкой.Очистить();
	
	РезультатТеста = Обработки.МониторНалоговИОтчетности.РезультатПроверкиОтчетностиПрошлыхПериодов(Объект.Организация);
	
	ПроверкаПрошлыхПериодовВыполнена = РезультатТеста.ПроверкаВыполнена;
	
	Если ПроверкаПрошлыхПериодовВыполнена
		И ЗначениеЗаполнено(РезультатТеста.ДобавленныеПериоды) Тогда
		
		Для Каждого СтрокаПериода Из РезультатТеста.ДобавленныеПериоды Цикл
		
			НоваяСтрока = ПериодыДобавленныеПроверкой.Добавить();
			
			НоваяСтрока.Периодичность = ?(ЗначениеЗаполнено(СтрокаПериода.Периодичность),
				СтрокаПериода.Периодичность,
				Перечисления.Периодичность.Год);
			
			НоваяСтрока.НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
				НоваяСтрока.Периодичность, СтрокаПериода.Период);
			НоваяСтрока.КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
				НоваяСтрока.Периодичность, СтрокаПериода.Период);
			
			НоваяСтрока.Требуется = СтрокаПериода.Требуется;
			
			НоваяСтрока.ПредставлениеПериода = ПредставлениеПериода(
				НоваяСтрока.НачалоПериода,
				КонецДня(НоваяСтрока.КонецПериода),
				"ФП = Истина");
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеТеста()
	
	РелевантныйОтчетныйПериод = РелевантныйОтчетныйПериод();
	
	НачалоРелевантногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		РелевантныйОтчетныйПериод.Периодичность, РелевантныйОтчетныйПериод.Период);
	
	// Если релевантный отчетный период не определен (например, еще не заполнен список задач),
	// то проверяем начало текущего года.
	Если НЕ ЗначениеЗаполнено(НачалоРелевантногоПериода) Тогда
		НачалоРелевантногоПериода = НачалоГода(Объект.Период);
	КонецЕсли;
	
	ТребуетсяПроверитьПрошлыеПериоды = НЕ ПомощникиПоУплатеНалоговИВзносов.ЭтоПервыйОтчетныйПериод(Объект.Организация,
		НачалоРелевантногоПериода, РелевантныйОтчетныйПериод.Правило, ДатаНачалаДеятельности);
	
	Элементы.НадписьПроверьтеПрошлыеПериоды.Заголовок = ОписаниеРезультатаТеста();
	
КонецПроцедуры

&НаСервере
Функция ОписаниеРезультатаТеста()
	
	// Сначала самые простые варианты
	
	Если НЕ ТребуетсяПроверитьПрошлыеПериоды Тогда // Тест не отображается
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроверкаПрошлыхПериодовВыполнена Тогда
		Возврат НСтр("ru = 'Проверьте: возможно, надо сдавать отчеты за предыдущие периоды?'");
	КонецЕсли;
	
	Если ПериодыДобавленныеПроверкой.Количество() = 0 Тогда
		Возврат НСтр("ru = 'Вы указали, что все отчеты за прошлые периоды сданы'");
	КонецЕсли;
	
	// Выявляем добавленные тестом периоды и отчеты за эти периоды
	
	ДобавленныеОтчеты = Новый Соответствие;
	
	Для Каждого ДобавленныйПериод Из ПериодыДобавленныеПроверкой Цикл
		
		Если НЕ ДобавленныйПериод.Требуется Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("НачалоПериода, КонецПериода, Периодичность",
			ДобавленныйПериод.НачалоПериода, ДобавленныйПериод.КонецПериода, ДобавленныйПериод.Периодичность);
		
		НайденныеСтроки = ТаблицаОтчеты.НайтиСтроки(Отбор);
		
		Для Каждого НайденныйОтчет Из НайденныеСтроки Цикл
			
			ПредставленияПериодов = ДобавленныеОтчеты[НайденныйОтчет.Правило];
			Если ПредставленияПериодов = Неопределено Тогда
				ПредставленияПериодов = Новый Массив;
				ДобавленныеОтчеты.Вставить(НайденныйОтчет.Правило, ПредставленияПериодов);
			КонецЕсли;
			
			ПредставленияПериодов.Добавить(ДобавленныйПериод.ПредставлениеПериода);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОписанияДобавленных = Новый Массив;
	КоличествоДобавленных = 0;
	
	Для Каждого ДобавленныйОтчет Из ДобавленныеОтчеты Цикл
		
		ПериодыДобавленногоОтчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДобавленныйОтчет.Значение);
		
		КоличествоДобавленных = КоличествоДобавленных + ПериодыДобавленногоОтчета.Количество();
		
		СтрокаОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 за %2'"),
			ОписаниеПравила(ДобавленныйОтчет.Ключ),
			СтрСоединить(ПериодыДобавленногоОтчета, ", "));
		
		ОписанияДобавленных.Добавить(СтрокаОписания);
		
	КонецЦикла;
	
	Если КоличествоДобавленных = 1 Тогда
		СтрокаЗаголовок = НСтр("ru = 'По вашим ответам добавлен отчет:'");
	Иначе
		СтрокаЗаголовок = НСтр("ru = 'По вашим ответам добавлены отчеты:'");
	КонецЕсли;
	
	СтрокаРазделительОтчетов = ";" + Символы.ПС;
	
	Описание = СтрокаЗаголовок
		+ Символы.ПС + Символы.ПС
		+ СтрСоединить(ОписанияДобавленных, СтрокаРазделительОтчетов);
	
	Возврат Описание;
	
КонецФункции

&НаСервере
Функция НачалоОбзораЗадач()
	
	МинимальныйПериод = НачалоГода(ДобавитьМесяц(Объект.Период, -12));
	
	Если ПериодыДобавленныеПроверкой.Количество() > 0 Тогда
		
		// Глубина обзора задач "в прошлое" зависит от результата теста.
		ТребующиесяПериоды = ПериодыДобавленныеПроверкой.Выгрузить(Новый Структура("Требуется", Истина), "НачалоПериода");
		ТребующиесяПериоды.Сортировать("НачалоПериода");
		
		Если ТребующиесяПериоды.Количество() > 0 Тогда
			МинимальныйПериод = Мин(МинимальныйПериод, ТребующиесяПериоды[0].НачалоПериода);
		КонецЕсли;
		
	КонецЕсли;
	
	// Учтем дату регистрации - до нее отчетов быть не может.
	НачалоОбзораЗадач = Макс(МинимальныйПериод, ДатаНачалаДеятельности);
	
	Возврат НачалоОбзораЗадач;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеПравила(Правило)
	
	Свойства = Новый Структура;
	Свойства.Вставить("Описание");
	Свойства.Вставить("КодЗадачи", "Владелец.Код");
	
	РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Правило, Свойства);
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"),
		РеквизитыПравила.Описание, РеквизитыПравила.КодЗадачи);
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьОбновлениеСпискаОтчетов()
	
	Если ПомощникДоступен Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСписокОтчетов", 0.1, Истина);
	КонецЕсли;
	
КонецПРоцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСписокОтчетов()
	
	ТаблицаОтчеты.Очистить();
	
	ОтчетыЗаполнены    = Ложь;
	ОтчетыСформированы = Ложь;
	
	УправлениеФормой(ЭтотОбъект);
	
	ДлительнаяОперация = ПолучитьДанныеОбОтчетах();
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ЗавершитьОбновлениеСпискаОтчетов", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеОбОтчетах()
	
	ПараметрыЗаполненияОтчетов = ПараметрыЗаполненияОтчетов();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение списка отчетов для комплекта нулевой отчетности'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПодготовкаНулевойОтчетности.ЗаполнитьОтчеты",
		ПараметрыЗаполненияОтчетов,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьОбновлениеСпискаОтчетов(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатОбновленияСпискаОтчетов(ДлительнаяОперация.АдресРезультата);
		
		ЗапуститьФормированиеОтчетов();
		
	Иначе
		
		ЗаголовокОшибки = НСтр("ru = 'Ошибка при заполнении списка отчетов:'");
		ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатОбновленияСпискаОтчетов(АдресРезультата)
	
	ЗадачиПоОтчетам = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Для Каждого ЗадачаОтчет Из ЗадачиПоОтчетам Цикл
		НоваяСтрока = ТаблицаОтчеты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗадачаОтчет);
	КонецЦикла;
	
	ОтчетыЗаполнены = Истина;
	
	НастроитьФормированиеОтчетов();
	
	ПроверитьРеквизитыОрганизацииДляОтчетности();
	
	ОбновитьПредставлениеСледующегоОтчета();
	ОбновитьСостояниеПодготовкиОтчетности();
	
	НастроитьОтображениеТеста();
	
	ОтобразитьОтчеты();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗаполненияОтчетов()
	
	// Необходимо получить не только текущие задачи, для которых уже требуются отчеты,
	// но и ближайшие задачи, срок выполнения которых еще не наступил.
	// При отсутствии текущих задач релевантный период определяется
	// как отчетный период ближайшей (в будущем) по началу выполнения задачи.
	НачалоОбзораЗадач = НачалоОбзораЗадач();
	
	// Для получения ближайших будущих задач строим расписание "с захватом" начала следующего года
	КонецОбзораЗадач = КонецГода(Объект.Период) + 1;
	
	Результат = Новый Структура;
	
	Результат.Вставить("Организация",            Объект.Организация);
	Результат.Вставить("ДатаНачалаДеятельности", ДатаНачалаДеятельности);
	Результат.Вставить("НачалоОбзора",           НачалоОбзораЗадач);
	Результат.Вставить("КонецОбзора",            КонецОбзораЗадач);
	Результат.Вставить("ТекущийДень",            Объект.Период);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки)
	
	Предупреждение = Новый Структура;
	
	Текст = ЗаголовокОшибки + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки;
	ВызватьИсключение Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкиФормированияОтчета(НавигационнаяСсылкаОтчета)
	
	АдресОтчета = АдресОтчета(НавигационнаяСсылкаОтчета);
	
	Отбор = Новый Структура("ГосорганНомерГруппы, ОтчетНомерГруппы", АдресОтчета.НомерГосоргана, АдресОтчета.НомерОтчета);
	ЗадачаОтчет = ТаблицаОтчеты.НайтиСтроки(Отбор)[0];
	
	Текст = НСтр("ru = 'При формировании отчета обнаружены ошибки:'")
		+ ?(ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования), СтрСоединить(ЗадачаОтчет.ОшибкиФормирования, Символы.ПС), "");
	
	ВызватьИсключение Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчетов()
	
	ОтчетыСформированы = Ложь;
	
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		// Рано формировать, будут ошибки.
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СформироватьОтчетыНаСервере();
	
	Если ДлительнаяОперация = Неопределено Тогда
		
		// Отчетов к формированию на сервере нет. Возможно, есть формирующиеся на клиенте?
		СформироватьОтчетыНаКлиенте();
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ЗаголовокОшибки = НСтр("ru = 'Ошибка при формировании отчетов:'");
		ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки);
		
	Иначе
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПродолжитьФормированиеОтчетов", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетыНаСервере()
	
	ПараметрыФормированияОтчетов = ПараметрыФормированияОтчетов();
	
	Если НЕ ПараметрыФормированияОтчетов.ФормироватьОтчеты Тогда
		// Нет отчетов для формирования на сервере, незачем запускать задание.
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование комплекта нулевой отчетности'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПодготовкаНулевойОтчетности.СформироватьОтчеты",
		ПараметрыФормированияОтчетов,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ПродолжитьФормированиеОтчетов(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатФормированияОтчетовНаСервере(ДлительнаяОперация.АдресРезультата);
		
		СформироватьОтчетыНаКлиенте();
		
	Иначе
		
		ЗаголовокОшибки = НСтр("ru = 'Ошибка при формировании отчетов:'");
		ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатФормированияОтчетовНаСервере(АдресРезультата)
	
	СформированныеОтчеты = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Для Каждого СформированныйОтчет Из СформированныеОтчеты Цикл
		
		Отбор = Новый Структура("ГосорганНомерГруппы, ОтчетНомерГруппы");
		ЗаполнитьЗначенияСвойств(Отбор, СформированныйОтчет);
		
		СтрокиТаблицы = ТаблицаОтчеты.НайтиСтроки(Отбор);
		
		ЗадачаОтчет = СтрокиТаблицы[0];
		
		ЗадачаОтчет.РегламентированныйОтчет = СформированныйОтчет.РегламентированныйОтчет;
		
		Если ЗначениеЗаполнено(СформированныйОтчет.ОшибкиФормирования) Тогда
			ЗадачаОтчет.ОшибкиФормирования = Новый ФиксированныйМассив(СформированныйОтчет.ОшибкиФормирования);
			ЗадачаОтчет.Обновить = Истина;
		Иначе
			ЗадачаОтчет.ОшибкиФормирования = Неопределено;
			ЗадачаОтчет.Обновить = Ложь;
		КонецЕсли;
		
		РазместитьОтчетНаФорме(ЗадачаОтчет);
		
	КонецЦикла;
	
КонецПРоцедуры

&НаКлиенте
Процедура СформироватьОтчетыНаКлиенте()
	
	// Не выполняем поиск, делаем простой обход - тогда вызов сервера может не понадобиться.
	Для Каждого ЗадачаОтчет Из ТаблицаОтчеты Цикл
		
		Если ЗадачаОтчет.ФормироватьНаКлиенте
			И (ЗадачаОтчет.Обновить ИЛИ НЕ ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет)) Тогда
			
			Результат = СформироватьОтчетНаКлиенте(ЗадачаОтчет);
			
			ЗадачаОтчет.Обновить = НЕ Результат.ОтчетСформирован;
			ЗадачаОтчет.РегламентированныйОтчет = Результат.СохраненныйОтчет;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗавершитьФормированиеОтчетов();
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьОтчетНаКлиенте(ЗадачаОтчет)
	
	Результат = Новый Структура();
	
	Результат.Вставить("ОтчетСформирован", Ложь);
	Результат.Вставить("СохраненныйОтчет", ПредопределенноеЗначение("Документ.РегламентированныйОтчет.ПустаяСсылка"));
	Результат.Вставить("Ошибки", Неопределено);
	
	ФормаОтчета = ФормаРегламентированногоОтчета(ЗадачаОтчет, Истина);
	
	ФормаОтчета.СохранитьНаКлиенте();
	Результат.ОтчетСформирован = Истина;
	Результат.СохраненныйОтчет = ФормаОтчета.СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗавершитьФормированиеОтчетов()
	
	ОтчетыСформированы = Истина;
	
	РегистрыСведений.АктуальностьКомплектаНулевойОтчетности.УстановитьАктуальность(Объект.Организация, ОтчетыСформированы);
	
	ОтобразитьОтчеты();
	
	ОбновитьСостояниеПодготовкиОтчетности();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецФункции

&НаСервере
Функция ПараметрыФормированияОтчетов()
	
	ОтборОтчетов = Новый Структура;
	ОтборОтчетов.Вставить("Предстоящая",          Ложь); // Не формируем отчеты по будущим задачам.
	ОтборОтчетов.Вставить("ФормироватьНаКлиенте", Ложь); // Старые формы отчетов не поддерживают формирование на сервере.
	
	ТекущиеОтчеты = ТаблицаОтчеты.Выгрузить(ОтборОтчетов);
	
	ТребуемыеОтчеты = ТекущиеОтчеты.СкопироватьКолонки();
	
	Для Каждого ЗадачаОтчет Из ТекущиеОтчеты Цикл
		
		Если НЕ ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) ИЛИ ЗадачаОтчет.Обновить Тогда
			НоваяСтрока = ТребуемыеОтчеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗадачаОтчет);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормированияОтчетов = Новый Структура;
	
	ПараметрыФормированияОтчетов.Вставить("ФормироватьОтчеты", ТребуемыеОтчеты.Количество() > 0);
	ПараметрыФормированияОтчетов.Вставить("ТребуемыеОтчеты",   ТребуемыеОтчеты);
	ПараметрыФормированияОтчетов.Вставить("Организация",       Объект.Организация);
	ПараметрыФормированияОтчетов.Вставить("Период",            Объект.Период);
	
	Возврат ПараметрыФормированияОтчетов;
	
КонецФункции

&НаСервере
Процедура НастроитьФормированиеОтчетов()
	
	ТребуемыеОтчеты = ТаблицаОтчеты.НайтиСтроки(Новый Структура("Предстоящая", Ложь));
	
	ОтчетностьНеТребуется = ТребуемыеОтчеты.Количество() = 0;
	
	Для Каждого ЗадачаОтчет Из ТребуемыеОтчеты Цикл
	
		Если ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) Тогда
			ЗадачаОтчет.Обновить = НЕ ОтчетыАктуальны;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = форма.Объект;
	
	// Доступность функционала
	Элементы.ГруппаОсновная.Видимость           = Форма.ПомощникДоступен;
	Элементы.ГруппаПомощникНедоступен.Видимость = НЕ Форма.ПомощникДоступен И ЗначениеЗаполнено(Объект.Организация);
	
	// Список отчетов
	Элементы.ПодготовкаОтчетностиСледующийОтчет.Видимость  = Форма.ОтчетностьНеТребуется;
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = Форма.ОтчетностьНеТребуется
		И Форма.ПериодСледующегоОтчетаРасширен;
	
	Элементы.НадписьТребуютсяРеквизиты.Видимость = Форма.ОтчетыЗаполнены И НЕ Форма.РеквизитыОрганизацииЗаполнены;
	
	Элементы.ПроверкаПредыдущихПериодов.Видимость = Форма.РеквизитыОрганизацииЗаполнены;
	
	Элементы.ФормированиеОтчетов.Видимость   = Форма.ОтчетыЗаполнены;
	Элементы.БлокиОтчетов.Видимость          = Форма.РеквизитыОрганизацииЗаполнены;
	Элементы.ОжиданиеОтчетов.Видимость       = Не Форма.ОтчетыЗаполнены;
	
	// Тест
	Элементы.ПроверкаПредыдущихПериодов.Видимость = Форма.ТребуетсяПроверитьПрошлыеПериоды;
	Элементы.ЗапуститьТест.Видимость              = НЕ Форма.ПроверкаПрошлыхПериодовВыполнена;
	Элементы.ЗапуститьТестПовторно.Видимость      = Форма.ПроверкаПрошлыхПериодовВыполнена;
	
	// Сдача отчетов
	Элементы.СдачаОтчетности.Видимость = Форма.ОтчетыЗаполнены И Форма.ОтчетыСформированы
		И Форма.РеквизитыОрганизацииЗаполнены И НЕ Форма.ОтчетностьНеТребуется;
	
	ОтобразитьДействияСдачаОтчетности(Форма);
	
КонецПРоцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьДействияСдачаОтчетности(Форма)
	
	Элементы = форма.Элементы;
	
	Элементы.СтраницыДействияСОтчетами.ТекущаяСтраница = Элементы[Форма.СпособСдачиОтчетности];
	
	Если Форма.СпособСдачиОтчетности = "Интернет" Тогда
		
		Элементы.Открыть1СОтчетность.КнопкаПоУмолчанию    = Форма.РазрешенЭлектронныйДокументооброт И Форма.ЭлектронныйДокументооборотДоступен;
		Элементы.Подключить1СОтчетность.КнопкаПоУмолчанию = Форма.РазрешенЭлектронныйДокументооброт И НЕ Форма.ЭлектронныйДокументооборотДоступен;
		
		Элементы.ЗаголовокПодключить1СОтчетность.Видимость = НЕ Форма.ЭлектронныйДокументооборотДоступен;
		Элементы.ОписаниеПодключить1СОтчетность.Видимость  = Форма.РазрешенЭлектронныйДокументооброт И НЕ Форма.ЭлектронныйДокументооборотДоступен;
		
		Элементы.Подключить1СОтчетность.Видимость = Форма.РазрешенЭлектронныйДокументооброт И НЕ Форма.ЭлектронныйДокументооборотДоступен;
		Элементы.Открыть1СОтчетность.Видимость    = Форма.РазрешенЭлектронныйДокументооброт И Форма.ЭлектронныйДокументооборотДоступен;
		
		Элементы.Описание1СОтчетностьНеПредусмотренаТарифом.Видимость = НЕ Форма.РазрешенЭлектронныйДокументооброт;
		Элементы.ВыборТарифа.Видимость                                = НЕ Форма.РазрешенЭлектронныйДокументооброт;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКонтактныеДанныеГосорганов()
	
	Если ПустаяСтрока(ФНС_Адрес) И ПустаяСтрока(ФНС_Телефоны) Тогда
		
		// Скроем незаполненные реквизиты ФНС.
		
		Элементы.ПочтаРоссииКонтактыГосорганов_ФНС_Наименование.Видимость = Ложь;
		Элементы.ПочтаРоссииКонтактыГосорганов_ФНС_Адрес.Видимость        = Ложь;
		Элементы.Конверт.Видимость = Ложь;
		
		Элементы.ОписаниеОтправкиПочтаРоссии.Заголовок
			= НСтр("ru = 'Распечатайте все отчеты в 1 экземпляре, подпишите каждый лист и отправьте в налоговую инспекцию ценным письмом с описью вложения.'");
		
		Элементы.ЛичныйВизитКонтактыГосорганов_ФНС.Видимость = Ложь;
		
		Элементы.ОписаниеОтправкиЛичныйВизит.Заголовок
			= НСтр("ru = 'Распечатайте все отчеты в 2 экземплярах, подпишите каждый лист и отнесите в налоговую инспекцию.'");
		
	Иначе
		
		Элементы.ПочтаРоссииКонтактыГосорганов_ФНС_Наименование.Видимость = Истина;
		Элементы.ПочтаРоссииКонтактыГосорганов_ФНС_Адрес.Видимость        = Истина;
		Элементы.Конверт.Видимость = Истина;
		
		Элементы.ОписаниеОтправкиПочтаРоссии.Заголовок
			= НСтр("ru = 'Распечатайте все отчеты в 1 экземпляре, подпишите каждый лист и отправьте ценным письмом с описью вложения по адресу:'");
		
		Элементы.ЛичныйВизитКонтактыГосорганов_ФНС.Видимость = Истина;
		
		Элементы.ОписаниеОтправкиЛичныйВизит.Заголовок
			= НСтр("ru = 'Распечатайте все отчеты в 2 экземплярах, подпишите каждый лист и отнесите в налоговую инспекцию:'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеОписиВложения(ГосорганСтрока)
	
	ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов[ГосорганСтрока];
	
	ОтправляемыеОтчеты = ТаблицаОтчеты.НайтиСтроки(Новый Структура("ГосударственныйОрган", ГосударственныйОрган));
	
	ВложенныеПредметы = Обработки.ПечатьОписиВложения.НоваяТаблицаВложенийОписи();
	
	Для Каждого ОтправляемыйОтчет Из ОтправляемыеОтчеты Цикл
		
		Если НЕ ЗначениеЗаполнено(ОтправляемыйОтчет.РегламентированныйОтчет) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВложенныеПредметы = ВложенныеПредметы.Добавить();
		СтрокаВложенныеПредметы.НаименованиеПредмета = ОтправляемыйОтчет.Наименование;
		СтрокаВложенныеПредметы.Количество = 1;
		СтрокаВложенныеПредметы.ОбъявленнаяЦенность = 1;
		
	КонецЦикла;
	
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Объект.Организация, Объект.Период);
	
	ДанныеОписи = Новый Структура;
	ДанныеОписи.Вставить("Отправитель",       СведенияОбОрганизации.НаименованиеДляПечатныхФорм);
	ДанныеОписи.Вставить("ВложенныеПредметы", ВложенныеПредметы);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОписи, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура СформироватьБланкОтчета(АдресОтчета)
	
	Отбор = Новый Структура("ГосорганНомерГруппы, ОтчетНомерГруппы", АдресОтчета.НомерГосоргана, АдресОтчета.НомерОтчета);
	
	ЗадачаОтчет = ТаблицаОтчеты.НайтиСтроки(Отбор)[0];
	
	РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФорму(
		ФормаРегламентированногоОтчета(ЗадачаОтчет, Ложь),
		"ПоказатьСДвухмернымШтрихкодомPDF417");
	
КонецПроцедуры

&НаСервере
Функция МожноПечататьОтчет(ЗадачаОтчет)
	
	Возврат РеквизитыОрганизацииЗаполнены
		И ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет)
		И НЕ ЗадачаОтчет.Обновить
		И НЕ ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования);
	
КонецФункции

&НаКлиенте
Функция ФормаРегламентированногоОтчета(ЗадачаОтчет, ЗаполнитьПриСоздании)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("мВыбраннаяФорма", ЗадачаОтчет.ВыбраннаяФорма);
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета", ЗадачаОтчет.КонецПериода);
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", ЗадачаОтчет.НачалоПериода);
	ПараметрыФормы.Вставить("мСкопированаФорма", Неопределено);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417", Истина);
	ПараметрыФормы.Вставить("СформироватьФормуОтчетаАвтоматически", ЗаполнитьПриСоздании);
	ПараметрыФормы.Вставить("БезОткрытияФормы", Истина);
	
	Если ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) Тогда
		ПараметрыФормы.Вставить("мСохраненныйДок", ЗадачаОтчет.РегламентированныйОтчет);
	КонецЕсли;
	
	ФормаОтчета = ПолучитьФорму(
		"Отчет." + ЗадачаОтчет.ИсточникОтчета + ".Форма." + ЗадачаОтчет.ВыбраннаяФорма,
		ПараметрыФормы,
		 ,
		Истина);
		
	Возврат ФормаОтчета;
	
КонецФункции

&НаСервереБезКонтекста
Функция РазрешенЭлектронныйДокументооборот()
	
	Возврат ТарификацияБПВызовСервераПовтИсп.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами()
		ИЛИ Обработки.ОплатаСервиса.ЕстьВыставленныйСчетНаОплатуСервиса();
	
КонецФункции

#Область ОтображениеЭлементов

&НаСервере
Процедура ОтобразитьОтчеты()
	
	КорневаяГруппа = Элементы.БлокиОтчетов;
	
	СкрытьПодчиненныеГруппы(КорневаяГруппа);
	
	Госорганы = ЗадействованныеГосорганы();
	
	НомераГосорганов = НомераГосорганов(Госорганы); // Постоянные
	НумераторОтчетов = НумераторОтчетов(Госорганы); // Инкрементируемый
	
	РазместитьГосорганыНаФорме(НомераГосорганов);
	
	Для Каждого ЗадачаОтчет Из ТаблицаОтчеты Цикл
		
		Если ЗадачаОтчет.Предстоящая Тогда
			// Предстоящие отчеты еще рано формировать.
			Продолжить;
		КонецЕсли;
		
		НомерГосоргана = НомераГосорганов[ЗадачаОтчет.ГосударственныйОрган];
		
		// Инкрементируем счетчик отчетов, представляемых в данный госорган.
		НомерОтчета    = НумераторОтчетов[ЗадачаОтчет.ГосударственныйОрган] + 1;
		НумераторОтчетов[ЗадачаОтчет.ГосударственныйОрган] = НомерОтчета;
		
		// Запишем координаты группы отчета в таблицу
		ЗадачаОтчет.ГосорганНомерГруппы = НомерГосоргана;
		ЗадачаОтчет.ОтчетНомерГруппы    = НомерОтчета;
		
		РазместитьОтчетНаФорме(ЗадачаОтчет);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РазместитьОтчетНаФорме(ЗадачаОтчет)
	
	СуффиксЭлементовОтчета = СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	
	ГруппаОтчет = ГруппаДляРазмещенияОтчета(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	
	// Заполним свойства элементов из задачи
	
	// Иконка отчета
	ЭлементКартинка = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Картинка" + СуффиксЭлементовОтчета];
	ЭлементКартинка.Картинка = ИконкаОтчета(ЗадачаОтчет);
	
	// Наименование отчета
	ЭлементНаименование = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Наименование" + СуффиксЭлементовОтчета];
	ЭлементНаименование.Заголовок = СтрокаНаименованиеОтчета(ЗадачаОтчет);
	
	Если ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) И НЕ ЗадачаОтчет.Обновить
		И НЕ ПустаяСтрока(ЗадачаОтчет.РасширенныйПериодПодсказка) Тогда
		ЭлементНаименование.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		ЭлементНаименование.Подсказка = ЗадачаОтчет.РасширенныйПериодПодсказка;
	Иначе
		ЭлементНаименование.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	// Статус отчета
	ЭлементСтатус = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Статус" + СуффиксЭлементовОтчета];
	ЭлементСтатус.Заголовок = СтрокаСтатусОтчета(ЗадачаОтчет);
	
	ГруппаОтчет.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Функция ГруппаДляРазмещенияОтчета(НомерГосоргана, НомерОтчета)
	
	ГруппаОтчетЭталон = Элементы.Отчет_1_1;
	
	ГруппаГосорган = Элементы[ИмяГосорган() + СуффиксГосорган(НомерГосоргана)];
	
	СуффиксЭлементовОтчета = СуффиксОтчет(НомерГосоргана, НомерОтчета);
	
	ИмяГруппыОтчет = ИмяОтчет() + СуффиксЭлементовОтчета;
	ГруппаОтчет    = ГруппаГосорган.ПодчиненныеЭлементы.Найти(ИмяГруппыОтчет);
	
	Если ГруппаОтчет = Неопределено Тогда
		
		ГруппаОтчет = Элементы.Добавить(ИмяГруппыОтчет, ТипЗнч(ГруппаОтчетЭталон), ГруппаГосорган);
		ЗаполнитьЗначенияСвойств(ГруппаОтчет, ГруппаОтчетЭталон, , "ПутьКДаннымЗаголовка");
		
		// Подчиненные элементы
		Для Каждого ЭлементЭталон Из ГруппаОтчетЭталон.ПодчиненныеЭлементы Цикл
			
			ИмяБезАдреса = ИмяЭлементаБезАдреса(ЭлементЭталон.Имя);
			
			ИмяНовогоЭлемента = ИмяЭлементаСАдресом(ИмяБезАдреса, НомерГосоргана, НомерОтчета);
			
			НовыйЭлемент = Элементы.Добавить(ИмяНовогоЭлемента, ТипЗнч(ЭлементЭталон), ГруппаОтчет);
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементЭталон);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ГруппаОтчет;
	
КонецФункции

&НаСервере
Процедура РазместитьГосорганыНаФорме(НомераГосорганов)
	
	КорневаяГруппа           = Элементы.БлокиОтчетов;
	ГруппаГосорганЭталон     = Элементы.ГосударственныйОрган_1;
	ДекорацияЗаголовокЭталон = Элементы.ГосударственныйОрганЗаголовок_1;
	
	Для Каждого ЭлементСоответствия Из НомераГосорганов Цикл
		
		Госорган       = ЭлементСоответствия.Ключ;
		НомерГосоргана = ЭлементСоответствия.Значение;
		
		ГруппаГосорган = Элементы.Найти(ИмяГосорган() + СуффиксГосорган(НомерГосоргана));
		
		Если ГруппаГосорган = Неопределено Тогда
			
			ГруппаГосорган = Элементы.Добавить(ИмяГосорган() + СуффиксГосорган(НомерГосоргана), ТипЗнч(ГруппаГосорганЭталон), КорневаяГруппа);
			ЗаполнитьЗначенияСвойств(ГруппаГосорган, ГруппаГосорганЭталон, , "ПутьКДаннымЗаголовка");
			
		КонецЕсли;
		
		ИмяЭлементаЗаголовокГосоргана = ИмяГосорган() + "Заголовок" + СуффиксГосорган(НомерГосоргана);
		ЭлементЗаголовокГосоргана = ГруппаГосорган.ПодчиненныеЭлементы.Найти(ИмяЭлементаЗаголовокГосоргана);
		
		Если ЭлементЗаголовокГосоргана = Неопределено Тогда
			ЭлементЗаголовокГосоргана = Элементы.Добавить(ИмяЭлементаЗаголовокГосоргана, ТипЗнч(ДекорацияЗаголовокЭталон), ГруппаГосорган);
			ЗаполнитьЗначенияСвойств(ЭлементЗаголовокГосоргана, ДекорацияЗаголовокЭталон);
		КонецЕсли;
		
		ЭлементЗаголовокГосоргана.Заголовок = ПомощникиПоУплатеНалоговИВзносов.ОписаниеГосорганаПолучателяОтчетности(Госорган);
		
		ГруппаГосорган.Видимость = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьПодчиненныеГруппы(ГруппаРодитель)
	
	Для Каждого ПодчиненныйЭлемент Из ГруппаРодитель.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Тогда
			
			ПодчиненныйЭлемент.Видимость = Ложь;
			СкрытьПодчиненныеГруппы(ПодчиненныйЭлемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НомераГосорганов(Госорганы)
	
	Номера = Новый Соответствие;
	
	Для ИндексЭлемента = 0 По Госорганы.ВГраница() Цикл
		Номера.Вставить(Госорганы[ИндексЭлемента], ИндексЭлемента + 1);
	КонецЦикла;
	
	Возврат Номера;
	
КонецФункции

&НаСервереБезКонтекста
Функция НумераторОтчетов(Госорганы)
	
	Нумератор = Новый Соответствие;
	
	Для Каждого Госорган Из Госорганы Цикл
		Нумератор.Вставить(Госорган, 0);
	КонецЦикла;
	
	Возврат Нумератор;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяЭлементаБезАдреса(ИмяЭлемента)
	
	ЧастиИмени = СтрРазделить(ИмяЭлемента, "_");
	
	Возврат ЧастиИмени[0];
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяЭлементаСАдресом(ИмяБезАдреса, НомерГосоргана, НомерОтчета)
	
	Возврат ИмяБезАдреса + СуффиксОтчет(НомерГосоргана, НомерОтчета);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция АдресОтчета(ИмяЭлемента)
	
	ЧастиИмени = СтрРазделить(ИмяЭлемента, "_");
	
	Возврат Новый Структура("НомерГосоргана, НомерОтчета", Число(ЧастиИмени[1]), Число(ЧастиИмени[2]));
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяГосорган()
	
	Возврат "ГосударственныйОрган";
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяОтчет()
	
	Возврат "Отчет";
	
КонецФункции

&НаСервереБезКонтекста
Функция СуффиксГосорган(НомерГосоргана)
	
	Возврат "_" + НомерГосоргана;
	
КонецФункции

&НаСервереБезКонтекста
Функция СуффиксОтчет(НомерГосоргана, НомерОтчета)
	
	Возврат "_" + НомерГосоргана + "_" + НомерОтчета;
	
КонецФункции

&НаСервере
Функция ИконкаОтчета(ЗадачаОтчет)
	
	Если НЕ РеквизитыОрганизацииЗаполнены
		ИЛИ ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования) Тогда
		
		Возврат БиблиотекаКартинок.ВниманиеКрасный;
		
	ИначеЕсли ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) И НЕ ЗадачаОтчет.Обновить Тогда
		
		Возврат БиблиотекаКартинок.ФорматPDF;
		
	Иначе
		
		Возврат БиблиотекаКартинок.ДлительнаяОперация16;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЗадействованныеГосорганы()
	
	ОтборТекущихОтчетов = Новый Структура("Предстоящая", Ложь);
	
	Госорганы = ТаблицаОтчеты.Выгрузить(ОтборТекущихОтчетов, "ГосударственныйОрган");
	Госорганы.Свернуть("ГосударственныйОрган");
	
	Госорганы.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(2, 0, ДопустимыйЗнак.Неотрицательный));
	
	Для Каждого СтрокаТаблицы Из Госорганы Цикл
		СтрокаТаблицы.Порядок = Перечисления.ТипыКонтролирующихОрганов.Индекс(СтрокаТаблицы.ГосударственныйОрган);
	КонецЦикла;
	
	Госорганы.Сортировать("Порядок");
	
	Возврат Госорганы.ВыгрузитьКолонку("ГосударственныйОрган");
	
КонецФункции

&НаСервере
Функция СтрокаНаименованиеОтчета(ЗадачаОтчет)
	
	ТекстНавигационнойСсылки = "";
	
	Если МожноПечататьОтчет(ЗадачаОтчет) Тогда
		ТекстНавигационнойСсылки = "НапечататьОтчет"
			+ СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	ИначеЕсли ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования) Тогда
		ТекстНавигационнойСсылки = "ПоказатьОшибкиОтчета"
			+ СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЗадачаОтчет.Наименование, , , ,ТекстНавигационнойСсылки);
	
КонецФункции

&НаСервере
Функция ТекстНеЗаполненыРеквизитыДляОтчетности() Экспорт
	
	ТекстДействия = НСтр("ru = 'подготовки отчетности'");
	
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Объект.Организация) Тогда
		ТекстОписанияСведений = НСтр("ru = 'сведения об организации'");
	Иначе
		ТекстОписанияСведений = НСтр("ru = 'сведения о себе'");
	КонецЕсли;
	
	ГиперссылкаСведенияОСебе = Новый ФорматированнаяСтрока(ТекстОписанияСведений,,,,"РеквизитыОрганизацииДляОтчетности");
	
	ЭлементыСообщенияОбОшибке = Новый Массив;
	ЭлементыСообщенияОбОшибке.Добавить(СтрШаблон(НСтр("ru = 'Для завершения %1 укажите'"), ТекстДействия));
	ЭлементыСообщенияОбОшибке.Добавить(" ");
	ЭлементыСообщенияОбОшибке.Добавить(ГиперссылкаСведенияОСебе);
	
	Возврат Новый ФорматированнаяСтрока(ЭлементыСообщенияОбОшибке);
	
КонецФункции

&НаСервере
Функция ОбновитьСостояниеПодготовкиОтчетности()
	
	Элементы.ПодготовкаОтчетностиСостояние.Заголовок = ТекстСостояниеПодготовкиОтчетности();
	
КонецФункции

&НаСервере
Процедура ОбновитьПредставлениеСледующегоОтчета()
	
	Если ОтчетностьНеТребуется Тогда
		
		Описание = ОписаниеСледующегоОтчета();
		
		ПериодСледующегоОтчетаРасширен = Описание.ОтчетныйПериодРасширен;
		
		Элементы.ПодготовкаОтчетностиСледующийОтчет.Заголовок = Описание.ПредставлениеОтчета;
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = Описание.РасширенныйПериодТекстПояснения;
		Элементы.ПояснениеРасширенныйНалоговыйПериодРасширеннаяПодсказка.Заголовок = Описание.РасширенныйПериодТекстПодсказки;
		
	Иначе
		// Если требуется формировать отчеты - элементы для следующего отчета скрыты, настраивать их не нужно.
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстСостояниеПодготовкиОтчетности()
	
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		
		Возврат НСтр("ru = 'Отчетность почти готова!'");
		
	ИначеЕсли ОтчетыЗаполнены И ОтчетыСформированы Тогда
		
		Если ОтчетностьНеТребуется Тогда
			Возврат НСтр("ru = 'Пока сдавать отчеты не требуется'");
		Иначе
			Возврат НСтр("ru = 'Отчетность готова!'");
		КонецЕсли;
		
	ИначеЕсли НЕ ОтчетыСформированы Тогда
		
		ЕстьПросроченные = ПериодыДобавленныеПроверкой.НайтиСтроки(Новый Структура("Требуется", Истина)).Количество() > 0;
		
		Если ЕстьПросроченные Тогда
			Возврат НСтр("ru = 'Готовится отчетность за текущую и прошедшие отчетные кампании'");
		Иначе
			
			ОтчетныйПериод = РелевантныйОтчетныйПериод();
			НачалоОтчетногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
				ОтчетныйПериод.Периодичность, ОтчетныйПериод.Период);
			КонецОтчетногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
				ОтчетныйПериод.Периодичность, ОтчетныйПериод.Период);
			
			ПредставлениеОтчетногоПериода = ПредставлениеПериода(НачалоОтчетногоПериода, КонецОтчетногоПериода, "ФП = Истина");
			
			Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Готовится отчетность за текущую отчетную кампанию - %1'"),
					ПредставлениеОтчетногоПериода);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОписаниеСледующегоОтчета()
	
	Отбор = Новый Структура("Предстоящая", Истина);
	БлижайшиеЗадачи = ТаблицаОтчеты.Выгрузить(Отбор, "Правило, Периодичность, ПериодСобытия, НачалоВыполнения");
	
	Если БлижайшиеЗадачи.Количество() = 0 Тогда
		Возврат ПомощникиПоУплатеНалоговИВзносов.НовыйОписаниеОтчета();
	КонецЕсли;
	
	БлижайшиеЗадачи.Сортировать("НачалоВыполнения");
	
	БлижайшаяЗадача = БлижайшиеЗадачи[0];
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ОписаниеОтчета(Объект.Организация, БлижайшаяЗадача.Правило, БлижайшаяЗадача.ПериодСобытия);
	
КонецФункции

&НаСервере
Функция СтрокаСтатусОтчета(ЗадачаОтчет)
	
	ТекстНавигационнойСсылки = "";
	
	Статус       = "";
	ШрифтСтатуса = Новый Шрифт( , , , Истина);
	ЦветТекста   = ЦветаСтиля.ЦветШрифтаСеройПодсказки;
	
	Если ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования) Тогда
		
		ТекстНавигационнойСсылки = "ПоказатьОшибкиОтчета" + СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
		
		Статус       = НСтр("ru = 'ошибки при формировании'");
		ШрифтСтатуса = Неопределено;
		ЦветТекста   = ЦветаСтиля.ЦветШрифтаОшибки;
		
	ИначеЕсли НЕ РеквизитыОрганизацииЗаполнены Тогда
		
		Статус = НСтр("ru = 'укажите сведения о себе'");
		
	ИначеЕсли ЗадачаОтчет.Обновить ИЛИ НЕ ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) Тогда
		
		Статус = НСтр("ru = 'формируется...'");
		
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(Статус, ШрифтСтатуса, ЦветТекста, ,ТекстНавигационнойСсылки);
	
КонецФункции

#КонецОбласти

#Область ТестПрошлыхПериодов

&НаСервере
Функция ПараметрыТестаПрошлыхПериодов()
	
	РелевантныйОтчетныйПериод = РелевантныйОтчетныйПериод();
	
	Результат = Новый Структура;
	
	Результат.Вставить("Организация",     Объект.Организация);
	Результат.Вставить("НачальныйВопрос", "ГруппаТест2");
	Результат.Вставить("ОтчетныйПериод",  РелевантныйОтчетныйПериод.Период);
	Результат.Вставить("Правило",         РелевантныйОтчетныйПериод.Правило);
	
	ДобавленныеОтчетныеПериоды =
		РегистрыСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов.ДобавленныеПериоды(Объект.Организация);
	
	Результат.Вставить("АдресВходныхДанных", ПоместитьВоВременноеХранилище(ДобавленныеОтчетныеПериоды, УникальныйИдентификатор));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РелевантныйОтчетныйПериод()
	
	Расписание = ТаблицаОтчеты.Выгрузить(, "ПериодСобытия, Правило, Периодичность, НачалоВыполнения, Срок");
	
	Возврат Обработки.ПодготовкаНулевойОтчетности.РелевантныйОтчетныйПериод(Объект.Организация, Объект.Период, Расписание);
	
КонецФункции

#КонецОбласти

#Область ПроверкаРеквизитовДляОтчетности

&НаСервере
Процедура ПроверитьРеквизитыОрганизацииДляОтчетности()
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов) Тогда
		УдалитьИзВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов);
		АдресХранилищаНезаполненныхРеквизитов = "";
	КонецЕсли;
	
	НезаполненныеРеквизиты = Неопределено;
	РеквизитыОрганизацииЗаполнены = РеквизитыДляОтчетностиЗаполнены(Объект.Организация, НезаполненныеРеквизиты);
	
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		
		АдресХранилищаНезаполненныхРеквизитов = ПоместитьВоВременноеХранилище(НезаполненныеРеквизиты, УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверяемыеРеквизитыДляОтчетности()
	
	ПроверяемыеРеквизиты = Новый Массив;
	
	ОтборТекущихОтчетов = Новый Структура("Предстоящая", Ложь);
	
	ТребуемыеОтчеты = ТаблицаОтчеты.Выгрузить(ОтборТекущихОтчетов, "ИсточникОтчета, ПериодСобытия");
	
	Для Каждого ТребуемыйОтчет Из ТребуемыеОтчеты Цикл
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ПроверяемыеРеквизиты,
			РегламентированнаяОтчетностьБП.РеквизитыОбязательныеДляОтчета(ТребуемыйОтчет.ИсточникОтчета,
				Объект.Организация, ТребуемыйОтчет.ПериодСобытия),
			Истина);
		
	КонецЦикла;
	
	Возврат ПроверяемыеРеквизиты;
	
КонецФункции

&НаСервере
Функция РеквизитыДляОтчетностиЗаполнены(Организация, НезаполненныеРеквизиты)
	
	Возврат ОрганизацииФормыДляОтчетности.РеквизитыЗаполнены(
				Организация, ПроверяемыеРеквизитыДляОтчетности(), НезаполненныеРеквизиты);
	
КонецФункции

#КонецОбласти

#КонецОбласти
