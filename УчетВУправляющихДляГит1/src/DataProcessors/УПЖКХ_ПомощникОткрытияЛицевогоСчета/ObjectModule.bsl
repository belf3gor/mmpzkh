
#Область СлужебныеПроцедурыИФункции

// Формирует таблицу комнат помещения.
//
// Параметры:
//  Помещение - Ссылка на справочник Помещения.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица комнат.
//
Функция ПолучитьСписокКомнат(Период, ТекПомещение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",       Период);
	Запрос.УстановитьПараметр("ТекПомещение", ТекПомещение);
	Запрос.УстановитьПараметр("ЖилаяПлощадь", Справочники.УПЖКХ_ВидыПлощадей.ЖилаяПлощадь);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_ПривязанныеКЛицевомуСчетуКомнатыСрезПоследних.ЛицевойСчет,
	|	УПЖКХ_ПривязанныеКЛицевомуСчетуКомнатыСрезПоследних.Комната
	|ПОМЕСТИТЬ втПривязанныеКомнаты
	|ИЗ
	|	РегистрСведений.УПЖКХ_ПривязанныеКЛицевомуСчетуКомнаты.СрезПоследних(&Период, Комната.Родитель = &ТекПомещение) КАК УПЖКХ_ПривязанныеКЛицевомуСчетуКомнатыСрезПоследних
	|ГДЕ
	|	УПЖКХ_ПривязанныеКЛицевомуСчетуКомнатыСрезПоследних.Привязка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_Помещения.Ссылка КАК Комната
	|ПОМЕСТИТЬ Комнаты
	|ИЗ
	|	Справочник.УПЖКХ_Помещения КАК УПЖКХ_Помещения
	|ГДЕ
	|	УПЖКХ_Помещения.Родитель = &ТекПомещение
	|	И НЕ УПЖКХ_Помещения.Ссылка В
	|				(ВЫБРАТЬ
	|					втПривязанныеКомнаты.Комната
	|				ИЗ
	|					втПривязанныеКомнаты КАК втПривязанныеКомнаты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Комнаты.Комната КАК Комната,
	|	ЕСТЬNULL(КВП_ПлощадьПомещенияСрезПоследних.Площадь, 0) КАК Площадь
	|ИЗ
	|	Комнаты КАК Комнаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КВП_ПлощадьПомещения.СрезПоследних(
	|				&Период,
	|				ВидПлощади = &ЖилаяПлощадь
	|					И Объект В
	|						(ВЫБРАТЬ
	|							СписокКомнат.Комната
	|						ИЗ
	|							Комнаты КАК СписокКомнат)) КАК КВП_ПлощадьПомещенияСрезПоследних
	|		ПО Комнаты.Комната = КВП_ПлощадьПомещенияСрезПоследних.Объект";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции //ПолучитьСписокКомнат

// Формирует ищет соответствие собственника и контрагента.
//
// Параметры:
//  Период - Дата поиска.
//  Собственник - Ссылка на справочник ФизЛица.
//
// Возвращаемое значение:
//  Контрагент - Ссылка на справочник Контрагенты.
// 
Функция НайтиСоответствиеКонтрагентаФизЛицу(Период, Собственник) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",       Период);
	Запрос.УстановитьПараметр("Собственник", Собственник);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УПЖКХ_СоответствиеФизЛицИКонтрагентов.Контрагент
	|ИЗ
	|	РегистрСведений.УПЖКХ_СоответствиеФизЛицИКонтрагентов.СрезПоследних(&Период, ФизЛицо = &Собственник) 
	|		КАК УПЖКХ_СоответствиеФизЛицИКонтрагентов";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Контрагент;
	Иначе
		Возврат Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// Возвращает текст кол-ва комнат л/с.
//
// Параметры:
//  Период - Дата поиска.
//  СписокВыбранныхКомнат - Список комнат.
//
// Возвращаемое значение:
//  Текст - Текст с комнатами и лицевыми счетами.
// 
Функция ПолучитьТекстКомнатЛС(Период, СписокВыбранныхКомнат) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",       Период);
	Запрос.УстановитьПараметр("СписокКомнат", СписокВыбранныхКомнат);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЛС.ЛицевойСчет КАК ЛС,
	|	ТаблицаЛС.Комната
	|ИЗ
	|	РегистрСведений.УПЖКХ_ПривязанныеКЛицевомуСчетуКомнаты.СрезПоследних(
	|			&Период,
	|			Привязка
	|				И Комната В (&СписокКомнат)) КАК ТаблицаЛС";
	
	Результат = Запрос.Выполнить().Выбрать();
	РезультатТекст = "";
	Пока Результат.Следующий() Цикл
		РезультатТекст = РезультатТекст + "Комната " + Результат.Комната + " указана для л/с " + Результат.ЛС + Символы.ПС;
	КонецЦикла;
		
	Возврат РезультатТекст;
	
КонецФункции

// Функция получает пустые паспортные данные.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Структура - Записи с пустыми паспортными данными.
//
Функция ПолучитьПустыеПаспортныеДанные() Экспорт
	
	ПаспортныеДанные = Новый Структура;
	ПаспортныеДанные.Вставить("ВидДокумента",                     Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка());
	ПаспортныеДанные.Вставить("СерияДокумента",                   "");
	ПаспортныеДанные.Вставить("НомерДокумента",                   "");
	ПаспортныеДанные.Вставить("ДатаВыдачиДокумента",              Дата(1,1,1));
	ПаспортныеДанные.Вставить("КемВыданДокумент",                 "");
	ПаспортныеДанные.Вставить("КодПодразделения",                 "");
	ПаспортныеДанные.Вставить("Период",                           Дата(1,1,1));
	ПаспортныеДанные.Вставить("ИзменятьПаспортныеДанные",         Ложь);
	
	Возврат ПаспортныеДанные;
	
КонецФункции // ПолучитьПустыеПаспортныеДанные()

#КонецОбласти