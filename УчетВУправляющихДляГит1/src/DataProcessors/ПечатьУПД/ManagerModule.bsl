#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ПЕЧАТНЫХ ФОРМ

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЕстьСчетаФактурыВыданные = Ложь;
	ЕстьСчетаФактурыПолученные = Ложь;
	
	Для каждого Объект Из МассивОбъектов Цикл
		Если ТипЗнч(Объект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ЕстьСчетаФактурыВыданные = Истина;
			Прервать;
		ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			ЕстьСчетаФактурыПолученные = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьСчетаФактурыВыданные И УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус1") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус1", "УПД (статус 1)",
			УчетНДС.ПечатьУниверсальныхПередаточныхДокументов(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина, Ложь),,, ПараметрыПечати),,
			"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент");
			
	КонецЕсли;

	Если ЕстьСчетаФактурыПолученные И УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус1") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус1", "УПД (статус 1)",
			УчетНДС.ПечатьУниверсальныхПередаточныхДокументов(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактураПолученный.ТекстЗапросаПечатьСчетовФактур(Истина, Ложь),,, ПараметрыПечати),,
			"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент");
			
	КонецЕсли;
		
	Если ЕстьСчетаФактурыВыданные И УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус1_981") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус1_981", "УПД (статус 1)",
			УчетНДС.ПечатьУниверсальныхПередаточныхДокументов981(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина, Истина),,, ПараметрыПечати),,
			"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент981");
			
	КонецЕсли;
		
	Если ЕстьСчетаФактурыПолученные И УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус1_981") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус1_981", "УПД (статус 1)",
			УчетНДС.ПечатьУниверсальныхПередаточныхДокументов981(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактураПолученный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина),,, ПараметрыПечати),,
			"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент981");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус2") Тогда
		ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов = "";
		Для каждого Объект Из МассивОбъектов Цикл
			Если ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураВыданный")
			   И ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов = 
					Документы[Объект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(Ложь);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ПустаяСтрока(ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов) Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус2", "УПД (статус 2)",
					УчетНДС.ПечатьУниверсальныхПередаточныхДокументов(МассивОбъектов, ОбъектыПечати, 
					ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов, Истина,, ПараметрыПечати),,
					"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент");
		КонецЕсли;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус2_981") Тогда
		ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов = "";
		Для каждого Объект Из МассивОбъектов Цикл
			Если ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураВыданный")
			   И ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов = 
					Документы[Объект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(Истина);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ПустаяСтрока(ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов) Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус2_981", "УПД (статус 2)",
					УчетНДС.ПечатьУниверсальныхПередаточныхДокументов981(МассивОбъектов, ОбъектыПечати, 
					ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов, Истина,, ПараметрыПечати),,
					"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент981");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

#КонецЕсли