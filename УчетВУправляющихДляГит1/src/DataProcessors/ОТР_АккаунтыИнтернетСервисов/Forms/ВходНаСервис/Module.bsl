
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Если нет аккаунта, то выполняет регистрацию. Открывает форму регисрации.
//
Процедура НадписьНетЛогинаИПароляАвторизацияНажатие(Элемент)
	
	Закрыть();
	ОткрытьФорму("Обработка.ОТР_АккаунтыИнтернетСервисов.Форма.РегистрацияНовогоАккаунта");
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия кнопки "Войти".
//
Процедура КомандаВойти(Команда)
	
	СведенияОбОшибке = "";
	ВерификацияПройдена = ВыполнитьВерификациюИмеющихсяПараметровДоступа(СведенияОбОшибке);
	
	Если ВерификацияПройдена Тогда
		ПоказатьПредупреждение(Неопределено, "Вход выполнен.", 0, );
		ДанныеЛогина = Новый Структура;
		ДанныеЛогина.Вставить("Логин", Логин);
		Оповестить("ПодключениеВыполнено", ДанныеЛогина);
		Закрыть();
	КонецЕсли;
	
	Если Не СведенияОбОшибке = "" Тогда
		ПоказатьПредупреждение(, СведенияОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Закрывает форму.
//
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
// Выполняет верификацию указанных параметров доступа.
//
Функция ВыполнитьВерификациюИмеющихсяПараметровДоступа(СведенияОбОшибке)
	
	Если ПроверитьПравильностьПараметровДоступаВерификация() Тогда
		// Формируе струкуру с регистрационными данными.
		СтруктураВерификации        = ОТР_АккаунтыИнтернетСервисовСервер.ПодготовитьСтруктуруРегистрации(Логин, Пароль, АдресЭлектроннойПочты);
		// Отправляет данные на верификацию.
		ВерификацияВыполненаУспешно = ОТР_АккаунтыИнтернетСервисовСервер.ВыполнитьВерификациюПараметров(СтруктураВерификации, СведенияОбОшибке);
		
		Возврат ВерификацияВыполненаУспешно;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
// Выполняет проверку корректности введенных параметров доступа для верификации.
//
Функция ПроверитьПравильностьПараметровДоступаВерификация()
	
	ПараметрыДоступаКорректны = Истина;
	
	Если Логин = "" Тогда
		Элементы.НадписьКорректностьЛогинаСуществующийАккаунт.Видимость = Истина;
		ПараметрыДоступаКорректны = Ложь;
	Иначе
		Элементы.НадписьКорректностьЛогинаСуществующийАккаунт.Видимость = Ложь;
	КонецЕсли;
	
	Если ОТР_АккаунтыИнтернетСервисовСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(АдресЭлектроннойПочты) Тогда
		Элементы.НадписьКорректностьЭлектроннойПочтыСуществующийАккаунт.Видимость = Ложь;
	Иначе
		Элементы.НадписьКорректностьЭлектроннойПочтыСуществующийАккаунт.Видимость = Истина;
		ПараметрыДоступаКорректны = Ложь;
	КонецЕсли;
	
	Если Пароль = "" Тогда
		Элементы.НадписьКорректностьПароляСуществующийАккаунт.Видимость = Истина;
		ПараметрыДоступаКорректны = Ложь;
	Иначе
		Элементы.НадписьКорректностьПароляСуществующийАккаунт.Видимость = Ложь;
	КонецЕсли;
	
	Возврат ПараметрыДоступаКорректны;
	
КонецФункции

#КонецОбласти





