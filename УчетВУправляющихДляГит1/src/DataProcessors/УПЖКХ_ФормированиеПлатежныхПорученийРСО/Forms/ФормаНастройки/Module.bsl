
#Область ПрочиеПроцедурыИФункции

&НаСервере
// Процедура удаляет строки из таблицы соответствия услуг.
Процедура УдалитьНастройкиСоответствияУслуг(Идентификатор)
	
	МассивСтрок = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из Объект.Настройка_СоответствиеУслуг Цикл
		Если НЕ СтрокаТаблицы.Идентификатор = Идентификатор Тогда 
			МассивСтрок.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Объект.Настройка_СоответствиеУслуг.Загрузить(Объект.Настройка_СоответствиеУслуг.Выгрузить(МассивСтрок));
	
КонецПроцедуры

&НаКлиенте
// Процедура устанавливает отбор для таблицы соответствия услуг.
Процедура УстановитьОтборТабличнойЧастиНастройкаСоответствияУслуг()
	
	ТекущиеДанные = Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		Элементы.Настройка_СоответствиеУслуг.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСВременнымХранилищем

&НаСервере
// Помещает настройки во временное хранилище.
Функция ПоместитьНастройкиВХранилище()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("Настройка_ГруппировочныеУслуги", Объект.Настройка_ГруппировочныеУслуги.Выгрузить());
	СтруктураНастроек.Вставить("Настройка_СоответствиеУслуг",    Объект.Настройка_СоответствиеУслуг.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураНастроек, Новый УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПризнакУдаления = Ложь;
	
	// Получаем настройки из хранилища.
	Если Параметры.Свойство("АдресНастроекВХранилище") И Не Параметры.АдресНастроекВХранилище = Неопределено Тогда
		
		СтруктураНастроек = ПолучитьИзВременногоХранилища(Параметры.АдресНастроекВХранилище);
		
		Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
			
			Если СтруктураНастроек.Свойство("Настройка_ГруппировочныеУслуги") Тогда
				Объект.Настройка_ГруппировочныеУслуги.Загрузить(СтруктураНастроек.Настройка_ГруппировочныеУслуги);
			КонецЕсли;
			
			Если СтруктураНастроек.Свойство("Настройка_СоответствиеУслуг") Тогда
				Объект.Настройка_СоответствиеУслуг.Загрузить(СтруктураНастроек.Настройка_СоответствиеУслуг);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "Сохранить".
Процедура Сохранить(Команда)
	
	Закрыть(ПоместитьНастройкиВХранилище());
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "Нажатие".
Процедура ПояснениеУслугиСодержаниеРасширеннаяПодсказкаНажатие(Элемент)
	ОткрытьСправку("Обработка.УПЖКХ_ФормированиеПлатежныхПорученийРСО");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборТабличнойЧастиНастройкаСоответствияУслуг();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Копирование группировочных услуг не предусмотрено!");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Идентификатор = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиПередНачаломИзменения(Элемент, Отказ)
	
	ПредыдущееНаименованиеУслуги = Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные.НаименованиеУслуги;
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиПередУдалением(Элемент, Отказ)
	
	Если НЕ Элемент.ТекущиеДанные = Неопределено Тогда
		Идентификатор = Элемент.ТекущиеДанные.Идентификатор;
		УдалитьНастройкиСоответствияУслуг(Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УстановитьОтборТабличнойЧастиНастройкаСоответствияУслуг();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиНаименованиеУслугиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные;
	ТекущееНаименованиеУслуги = ТекущаяСтрока.НаименованиеУслуги;
	
	// Проверяем, есть ли группировочная услуга с таким наименованием в табличной части.
	Если ЗначениеЗаполнено(ТекущееНаименованиеУслуги)
	   И Объект.Настройка_ГруппировочныеУслуги.НайтиСтроки(Новый Структура("НаименованиеУслуги", ТекущееНаименованиеУслуги)).Количество() > 1 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Услуга с наименованием """ + Строка(ТекущееНаименованиеУслуги) + """ уже присутствует в табличной части!");
		ТекущаяСтрока.НаименованиеУслуги = ПредыдущееНаименованиеУслуги;
	КонецЕсли;
	
	УстановитьОтборТабличнойЧастиНастройкаСоответствияУслуг();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_ГруппировочныеУслугиЯвляетсяУслугойСодержанияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные;
	ТекущееЗначениеЯвляетсяУслугойСодержания = ТекущаяСтрока.ЯвляетсяУслугойСодержания;
	
	// Проверяем, есть ли группировочная услуга с признаком услуги содержания.
	Если ТекущееЗначениеЯвляетсяУслугойСодержания
	   И Объект.Настройка_ГруппировочныеУслуги.НайтиСтроки(Новый Структура("ЯвляетсяУслугойСодержания", Истина)).Количество() > 1 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Одна из услуг уже выбрана услугой содержания общего имущества!");
		ТекущаяСтрока.ЯвляетсяУслугойСодержания = Ложь;
	КонецЕсли;
	
	УстановитьОтборТабличнойЧастиНастройкаСоответствияУслуг();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка_СоответствиеУслугПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Копирование видов услуг не предусмотрено!");
		Отказ = Истина;
	КонецЕсли;
	
	Если Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные = Неопределено
	 ИЛИ НЕ ЗначениеЗаполнено(Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные.Идентификатор) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не выделена группировочная услуга!");
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Настройка_СоответствиеУслугПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Идентификатор = Элементы.Настройка_ГруппировочныеУслуги.ТекущиеДанные.Идентификатор;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
