#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МожноСоздаватьЗаписиКУДиР = ПравоДоступа("Изменение", Метаданные.Документы.ЗаписьКУДиР);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Организация, НачалоПериода, КонецПериода");
	
	Заголовок = ТекстЗаголовка();
	
	ОбъектНалогообложенияДоходы = УчетнаяПолитика.ПрименяетсяУСНДоходы(Организация, КонецПериода);
	
	ЗаполнитьТабличнуюЧасть();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПлатежныйДокумент_УплатаНалогов" ИЛИ ИмяСобытия = "ИзменениеВыписки"
			ИЛИ ИмяСобытия = "ИзменениеЗаписиКУДиР" ИЛИ ИмяСобытия = "ИзменениеОперацииБух"
			ИЛИ ИмяСобытия = "ИзменениеНачисленияЗарплаты" ИЛИ ИмяСобытия = "ИзменениеОтраженияЗарплатыВБухучете" 
			ИЛИ ИмяСобытия = "ИзменениеОтраженияЗарплатыВУчете" Тогда
		ЗаполнитьТабличнуюЧасть();
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" И Источник <> ЭтотОбъект
		 И Не ЗначениеЗаполнено(Параметр) Тогда
		
		ЗаполнитьТабличнуюЧасть();
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.ДокументРасхода);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	
	ПараметрыФормы = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("НачалоПериода", НачалоПериода);
		ПараметрыФормы.Вставить("КонецПериода", КонецПериода);
		ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаписьКУДиР.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТабличнуюЧасть();
	УправлениеФормой(ЭтотОбъект);
	
	Оповестить("ОбновитьПоказателиРасчетаУСН", , ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.СоздатьДокумент.Доступность = Форма.МожноСоздаватьЗаписиКУДиР;
	
	ИтогоПФР        = 0;
	ИтогоФСС        = 0;
	ИтогоФОМС       = 0;
	ИтогоФСС_НС     = 0;
	ИтогоБольничные = 0;
	ИтогоДМС        = 0;
	ИтогоРасходы    = 0;
	
	Если Форма.ОбъектНалогообложенияДоходы Тогда
		
		Для Каждого СтрокаТаблицы Из Форма.Список Цикл
			ИтогоПФР        = ИтогоПФР        + СтрокаТаблицы.ПФР;
			ИтогоФСС        = ИтогоФСС        + СтрокаТаблицы.ФСС;
			ИтогоФОМС       = ИтогоФОМС       + СтрокаТаблицы.ФОМС;
			ИтогоФСС_НС     = ИтогоФСС_НС     + СтрокаТаблицы.ФСС_НС;
			ИтогоБольничные = ИтогоБольничные + СтрокаТаблицы.Больничные;
			ИтогоДМС        = ИтогоДМС        + СтрокаТаблицы.ДобровольноеСтрахование;
		КонецЦикла;
		
		ИтогоРасходы = ИтогоПФР + ИтогоФСС + ИтогоФОМС + ИтогоФСС_НС + ИтогоБольничные + ИтогоДМС;
		
	КонецЕсли;
	
	Элементы.Период.ТекстПодвала     = "Всего: " + ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоРасходы, , "0.00", " ");;
	Элементы.ПФР.ТекстПодвала        = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоПФР, ,        "0.00", " ");
	Элементы.ФСС.ТекстПодвала        = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоФСС, ,        "0.00", " ");
	Элементы.ФОМС.ТекстПодвала       = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоФОМС, ,       "0.00", " ");
	Элементы.ФСС_НС.ТекстПодвала     = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоФСС_НС, ,     "0.00", " ");
	Элементы.Больничные.ТекстПодвала = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоБольничные, , "0.00", " ");
	Элементы.ДМС.ТекстПодвала        = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ИтогоДМС, ,        "0.00", " ");
	
КонецПроцедуры

&НаСервере
Функция ТекстЗаголовка()
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина);
	
	ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Расходы, уменьшающие сумму налога УСН за %1'"), ПредставлениеПериода);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстЗаголовка = ТекстЗаголовка + " " + БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Организация);
	КонецЕсли;
	
	Возврат ТекстЗаголовка;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТабличнуюЧасть()
	
	Если НЕ ОбъектНалогообложенияДоходы Тогда
		Список.Очистить();
		Возврат;
	КонецЕсли;
	
	РасходыУменьшающиеНалог = Обработки.ПомощникРасчетаНалогаУСН.РасходыУменьшающиеНалог(
		Организация, НачалоПериода, КонецПериода);
	
	Если НЕ ЗначениеЗаполнено(РасходыУменьшающиеНалог) Тогда
		Список.Очистить();
		Возврат;
	КонецЕсли;
	
	РасходыУменьшающиеНалог.Колонки.Добавить("ПредставлениеДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	
	Для Каждого СтрокаТаблицы Из РасходыУменьшающиеНалог Цикл
		
		ВидДокумента = СтрокаТаблицы.ВидДокумента;
		
		НазваниеДокумента = "";
		
		Если ВидДокумента = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Выдача наличных, вх.'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Списание с расчетного счета, вх.'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.ЗаписьКУДиР") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Запись книги доходов и расходов УСН'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.ОперацияБух") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Операция'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.БольничныйЛист") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Больничный лист'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.НачислениеЗарплаты") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Начисление зарплаты'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.ОтражениеЗарплатыВБухучете") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Отражение зарплаты в бухучете'");
			
		ИначеЕсли ВидДокумента = Тип("ДокументСсылка.ОтражениеЗарплатыВУчете") Тогда
			
			НазваниеДокумента = НСтр("ru = 'Зарплата (ЗУП 2.5, ЗиК 7.7)'");
			
		Иначе
			
			НазваниеДокумента = НСтр("ru = 'Реквизиты первичного документа:'");
			
		КонецЕсли;
		
		СтрокаТаблицы.ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2'"), НазваниеДокумента, СтрокаТаблицы.РеквизитыПервичногоДокумента);
		
	КонецЦикла;
	
	Список.Загрузить(РасходыУменьшающиеНалог);
	
КонецПроцедуры

#КонецОбласти