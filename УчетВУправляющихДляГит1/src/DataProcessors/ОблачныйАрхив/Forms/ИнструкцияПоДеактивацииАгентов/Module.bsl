///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Отключать имеет смысл только автоматические расписания.
	ПараметрыОкруженияСервер = ОблачныйАрхив.ПолучитьНастройкиОблачногоАрхива("ПараметрыОкруженияСервер");
	ИдентификаторИБ = ПараметрыОкруженияСервер.ИдентификаторИБ;
	ИдентификаторИБ_Полный =
		ИдентификаторИБ
			+ ОблачныйАрхивКлиентСервер.ПолучитьОписаниеСуффиксовИдентификаторовИБ().АвтоматическаяКопия.Суффикс;

	// В зависимости от того, установлен ли Агент резервного копирования на этом компьютере, отобразить разные страницы.
	ИнформацияОКлиенте = ОблачныйАрхив.ПолучитьНастройкиОблачногоАрхива("ИнформацияОКлиенте", ИмяКомпьютера());
	Если ИнформацияОКлиенте.АгентКопированияУстановлен Тогда
		ЭтотОбъект.КоманднаяСтрокаЭтотКомпьютер   =
			""""
			+ ИнформацияОКлиенте.КаталогУстановкиАгентаКопирования
			+ ПолучитьРазделительПути()
			+ "BackupAgent.exe"
			+ """"
			+ " stop_backup -db_id="
			+ ИдентификаторИБ_Полный;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаАгентУстановленНаЛокальномКомпьютере;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаАгентНеУстановленНаЛокальномКомпьютере;
	КонецЕсли;
	ЭтотОбъект.КоманднаяСтрокаДругойКомпьютер = 
		НСтр("ru='<Путь к установленному Агенту резервного копирования>'")
		+ ПолучитьРазделительПути()
		+ "BackupAgent.exe stop_backup -db_id="
		+ ИдентификаторИБ_Полный;

	// В виде подсказки вывести список компьютеров, где установлен Агент резервного копирования.
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	Рег.ИмяКомпьютера           КАК ИмяКомпьютера,
		|	Рег.ВерсияАгентаКопирования КАК ВерсияАгентаКопирования
		|ИЗ
		|	РегистрСведений.СвойстваЛокальныхКомпьютеровДляОблачногоАрхива КАК Рег
		|ГДЕ
		|	Рег.АгентКопированияУстановлен
		|";
	РезультатЗапроса = Запрос.Выполнить(); // Обработка.ОблачныйАрхив.ИнструкцияПоДеактивацииАгентов.
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТекстПодсказки = "";
		Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока Выборка.Следующий() Цикл
			ТекстПодсказки = ТекстПодсказки
				+ "#" + СокрЛП(Выборка.ИмяКомпьютера)
				+ " ("
				+ ИнтернетПоддержкаПользователейКлиентСервер.ПользовательскоеПредставлениеНомераВерсии(
					Выборка.ВерсияАгентаКопирования,
					Ложь)
				+ ")#";
		КонецЦикла;
		ТекстПодсказки = СтрЗаменить(ТекстПодсказки, "##", Символы.ПС);
		ТекстПодсказки = СтрЗаменить(ТекстПодсказки, "#", "");
		ТекстПодсказки = НСтр("ru='Агенты резервного копирования были установлены на следующих компьютерах:'")
			+ Символы.ПС
			+ ТекстПодсказки;
		Элементы.ДекорацияШаг4_АгентУстановлен.Подсказка = ТекстПодсказки;
	Иначе
		Элементы.ДекорацияШаг4_АгентУстановлен.Подсказка = "";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Истина;

	ОблачныйАрхивКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		Неопределено);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)

	ПараметрыЗапускаПрограммы = ФайловаяСистемаКлиент.ПараметрыЗапускаПрограммы();
	ПараметрыЗапускаПрограммы.ДождатьсяЗавершения = Истина; // Необходимо дождаться завершения, чтобы несколько раз не запустили команду.
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(ЭтотОбъект.КоманднаяСтрокаЭтотКомпьютер, ПараметрыЗапускаПрограммы);

КонецПроцедуры

#КонецОбласти
