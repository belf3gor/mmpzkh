#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = "СтавкаНалогаНаПрибыльРегиональная, СтавкаУСНДоходы, СтавкаУСНДоходыРасходы, СтавкаЕНВД, К2, "
		+ "КодРегиона, РегионПредставление, КодОКВЭД, ВидДеятельностиПредставление, ВидОрганизации";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Элементы.ЕНВД.Видимость = Параметры.УплачиваетсяЕНВД;
	Элементы.НалогНаПрибыль.Видимость = ВидОрганизации <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	ПоказатьДанныеУСНВВашемРегионе();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект),
			ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеСтавокУСНСравнениеРежимовНалогообложения" Тогда
	
		ОбработатьИзменениеСтавокУСН(Параметр);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаНаРегиональныеОсобенностиУСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтавкиУСНВВашемРегионеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "УСНВВашемРегионе" Тогда
		ПараметрыОтчета = ПараметрыФормированияОтчетаУСНВВашемРегионе();
		ОткрытьФорму("Отчет.УСНВВашемРегионе.ФормаОбъекта", ПараметрыОтчета, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаРегиональныеОсобенностиЕНВДОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНарегиональныеОсобенностиОСНООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Ключи = "СтавкаНалогаНаПрибыльРегиональная, СтавкаУСНДоходы, СтавкаУСНДоходыРасходы, СтавкаЕНВД, К2";
	Результат = Новый Структура(Ключи);
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки)
	
	СсылкаНаСайтФНС = Новый Массив;
	СсылкаНаСайтФНС.Добавить("https://www.nalog.ru/rn");
	Если НавигационнаяСсылкаФорматированнойСтроки = "СсылкаОСНО" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/profitul/#title18");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СсылкаУСН" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/usn/#title11");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СсылкаЕНВД" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/envd/#title18");
	КонецЕсли;
	
	Если СсылкаНаСайтФНС.Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(СтрСоединить(СсылкаНаСайтФНС, ""));
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДанныеУСНВВашемРегионе()
	
	ПоказыватьДанные = ПолучитьФункциональнуюОпцию("ИспользоватьСервисРегиональныеСтавкиНалогов");
		
	Элементы.ДекорацияСтавкиУСНВВашемРегионе.Видимость = ПоказыватьДанные;
	Элементы.СсылкаНаРегиональныеОсобенностиУСН.Видимость = НЕ ПоказыватьДанные;
	Если НЕ ПоказыватьДанные Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	
	Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = НСтр("ru='Получение данных сервиса...'");
	
	ПараметрыПолученияИнформации = ПараметрыПолученияИнформацииУСНВВашемРегионе();
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru='Получение данных сервиса: Ставки УСН в вашем регионе'");
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегиональныеСтавкиНалогов.ПолучитьЗаголовокДекорацииУСНВВашемРегионе",
		ПараметрыПолученияИнформации, 
		ПараметрыВыполнения);
	
	АдресХранилища = ДлительнаяОперация.АдресРезультата;
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	Иначе
		ИдентификаторЗадания = Неопределено;
		Если ДлительнаяОперация.Статус = "Выполнено" Тогда
			ПоказатьПодготовленныеДанные();
		Иначе
			Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = НСтр("ru='Не удалось получить данные сервиса.'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыПолученияИнформацииУСНВВашемРегионе()

	Результат = Новый Структура;
	Результат.Вставить("КодРегиона", КодРегиона);
	Результат.Вставить("ЭтоЮрЛицо", ВидОрганизации = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	Результат.Вставить("ПрименяетсяУСНДоходы", Истина);
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", Истина);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПараметрыФормированияОтчетаУСНВВашемРегионе()

	Результат = Новый Структура;
	Результат.Вставить("Режим", "СравнениеРежимовНалогообложения");
	Результат.Вставить("КодРегиона", КодРегиона);
	Результат.Вставить("РегионПредставление", РегионПредставление);
	Результат.Вставить("КодОКВЭД", КодОКВЭД);
	Результат.Вставить("ВидДеятельностиПредставление", ВидДеятельностиПредставление);
	Результат.Вставить("ВидОрганизации", ВидОрганизации);
	Результат.Вставить("ПрименяетсяУСНДоходы", Истина);
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", Истина);
	Результат.Вставить("ПоказыватьПрименить", Истина);
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПоказатьПодготовленныеДанные()
	
	Данные = ПолучитьИзВременногоХранилища(АдресХранилища);
	УдалитьИзВременногоХранилища(АдресХранилища);
	
	Если Данные = Неопределено Тогда
		Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = НСтр("ru='Не удалось получить данные сервиса.'");
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = Данные;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииЗадания(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	
	ОбновитьОтображениеДанных(Новый Массив);
	ПоказатьПодготовленныеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСтавокУСН(НовыеСтавки)
	
	Если ТипЗнч(НовыеСтавки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НовыеСтавки.Свойство("НалоговыеКаникулы") Тогда
		Если ВидОрганизации = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") Тогда
			Возврат;
		КонецЕсли;
		СтавкаУСНДоходы = 0;
		СтавкаУСНДоходыРасходы = 0;
		ТекущийЭлемент = Элементы.СтавкаУСНДоходы;
	ИначеЕсли НовыеСтавки.УСНДоходы Тогда
		СтавкаУСНДоходы = НовыеСтавки.СтавкаНалога;
		ТекущийЭлемент = Элементы.СтавкаУСНДоходы;
	Иначе
		СтавкаУСНДоходыРасходы = НовыеСтавки.СтавкаНалога;
		ТекущийЭлемент = Элементы.СтавкаУСНДоходыРасходы;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
