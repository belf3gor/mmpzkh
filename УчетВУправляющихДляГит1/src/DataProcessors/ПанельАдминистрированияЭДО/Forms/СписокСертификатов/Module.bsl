
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьТаблицуСертификатов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СохраненПарольСертификата" Тогда
		ПриИзмененииСертификата();
		ТребуетсяПроверкаНастроек = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ТребуетсяПроверкаНастроек Тогда
		Оповестить("ПроверитьНастройкиРегламентныхЗаданий");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСертификаты

&НаКлиенте
Процедура СертификатыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СертификатыСтатус" Тогда
		
		Если Не Элемент.ТекущиеДанные.Статус
			И ЗначениеЗаполнено(Элемент.ТекущиеДанные.Сертификат) Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Сертификат", Элемент.ТекущиеДанные.Сертификат);
			ПараметрыФормы.Вставить("СохранятьДляВсех", Истина);
			
			ОткрытьФорму("ОбщаяФорма.ЗаписьПароляСертификата",
				ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		ИначеЕсли Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.Сертификат) Тогда
			
			// Нет доступных сертификатов, позволим перейти на инструкцию в интернете.
			СсылкаНаИнструкцию = ОбменСКонтрагентамиСлужебныйКлиент.СсылкаНаИнструкциюПоНастройкеЭДО();
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СсылкаНаИнструкцию);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыСертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПрофильНастроек = Элементы.Сертификаты.ТекущиеДанные.ПрофильНастроек;
	СертификатыПрофиля = СписокСертификатовПоПрофилю(ПрофильНастроек);
	Элемент.СписокВыбора.ЗагрузитьЗначения(СертификатыПрофиля);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыСертификатПриИзменении(Элемент)
	
	ПриИзмененииСертификата();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Надпись "сохраните пароль закрытого ключа", если пароль не сохранен.
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Сертификаты.ПодчиненныеЭлементы.СертификатыСтатус.Имя);
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Сертификаты.Статус");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='сохраните пароль закрытого ключа'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
	// Надпись "Действий не требуется", если пароль сохранен.
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Сертификаты.ПодчиненныеЭлементы.СертификатыСтатус.Имя);
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Сертификаты.Статус");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='действий не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	
	// Надпись "нет доступных сертификатов" для профилей без сертификатов.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Сертификаты.ПодчиненныеЭлементы.СертификатыСертификат.Имя);
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Сертификаты.Сертификат");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='нет доступных сертификатов'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
	// Надпись "установите контейнер закрытого ключа на сервере" если нет доступных сертификатов.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Сертификаты.ПодчиненныеЭлементы.СертификатыСтатус.Имя);
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Сертификаты.Сертификат");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='установите контейнер закрытого ключа на сервере'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСертификатов()
	
	РезультатЗапроса = РезультатЗапросаСертификатов();
	
	ВыборкаПрофиль = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПрофиль.Следующий() Цикл
		
		СтрокаТаблицы = Сертификаты.Добавить();
		СтрокаТаблицы.ПрофильНастроек = ВыборкаПрофиль.Профиль;
		
		ВыборкаСертификат = ВыборкаПрофиль.Выбрать();
		Пока ВыборкаСертификат.Следующий() Цикл
			
			Если ЗначениеЗаполнено(ВыборкаСертификат.Сертификат) Тогда
				
				СтрокаТаблицы.Сертификат = ВыборкаСертификат.Сертификат;
				СтрокаТаблицы.Статус     = ПарольСертификатаСохраненДляВсех(ВыборкаСертификат.Сертификат);
				
				Если СтрокаТаблицы.Статус Тогда
					Прервать;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатЗапросаСертификатов(ПрофильНастроек = Неопределено)
	
	Возврат Обработки.ПанельАдминистрированияЭДО.РезультатЗапросаСертификатов(ПрофильНастроек);
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокСертификатовПоПрофилю(Знач ПрофильНастроек)
	
	Список = Новый Массив;
	
	РезультатЗапроса = РезультатЗапросаСертификатов(ПрофильНастроек);
	ВыборкаПрофиль = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПрофиль.Следующий();
	
	ВыборкаСертификаты = ВыборкаПрофиль.Выбрать();
	Пока ВыборкаСертификаты.Следующий() Цикл
		Список.Добавить(ВыборкаСертификаты.Сертификат);
	КонецЦикла;
	
	Возврат Список;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПарольСертификатаСохраненДляВсех(Знач Сертификат)
	
	Возврат Обработки.ПанельАдминистрированияЭДО.ПарольСертификатаСохраненДляВсех(Сертификат);
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииСертификата()
	
	ТекущиеДанные = Элементы.Сертификаты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сертификат) Тогда
		ТекущиеДанные.Статус = ПарольСертификатаСохраненДляВсех(ТекущиеДанные.Сертификат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти