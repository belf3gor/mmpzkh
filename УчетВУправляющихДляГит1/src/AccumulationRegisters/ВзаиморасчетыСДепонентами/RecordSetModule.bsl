#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьПоложительностьСуммПоКлючу(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПоложительностьСуммПоКлючу(Отказ)
	
	ПоляКлюча = "ВидДвижения, Организация, Ведомость, ФизическоеЛицо";
	Депоненты = Выгрузить(, ПоляКлюча +","+ "Сумма");
	Депоненты.Свернуть(ПоляКлюча, "Сумма");
	
	Для Каждого Депонент Из Депоненты Цикл
		Если Депонент.Сумма < 0 Тогда
			СообщениеОбОшибке = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Отрицательный %1 для %2, %3, %4'"), 
					НРег(ВидДвиженияНакопления.Приход),	
					Депонент.Организация,
					Депонент.Ведомость,
					Депонент.ФизическоеЛицо);
			ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке, , , , Отказ);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли		