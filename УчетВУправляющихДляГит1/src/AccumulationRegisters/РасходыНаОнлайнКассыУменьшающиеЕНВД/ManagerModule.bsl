#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает сведения о зарегистрированных и учтенных в налоге суммах расходов на онлайн-кассы за квартал.
//
// Параметры:
//  Организация                 - СправочникСсылка.Организации - организация
//  Период                      - Дата - дата в квартале, за который определяются суммы расходов
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе,
//                                   для которой определяются суммы расходов.
//                                   Если не указана - расходы будут получены по всем регистрациям
//
// Возвращаемое значение:
//   Структура   - суммы расходов, состав см. НовыйОписаниеСуммРасходовНаОнлайнКассы()
//
Функция СуммыРасходовЗаПериод(Организация, Период, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Расходы = НовыйОписаниеСуммРасходовНаОнлайнКассы();
	
	Если Не ЗначениеЗаполнено(Период) Или Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Расходы;
	КонецЕсли;
	
	Если НЕ ПрименяетсяВычетПоОнлайнКассам(Организация, Период) Тогда
		Возврат Расходы;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.Зарегистрировано), 0) КАК РасходыНаОнлайнКассыВсего,
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.УчтеноРанее), 0) КАК РасходыНаОнлайнКассыУчтеноРанее,
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.Зарегистрировано), 0) - ЕСТЬNULL(СУММА(ВложенныйЗапрос.УчтеноРанее), 0) КАК РасходыНаОнлайнКассыНачальныйОстаток,
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.УчтеноЗаКвартал), 0) КАК НалоговыйВычетОнлайнКассы,
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.КонечныйОстаток), 0) КАК РасходыНаОнлайнКассыКонечныйОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасходыНаОнлайнКассыЗаКвартал.СуммаПриход КАК Зарегистрировано,
	|		0 КАК УчтеноРанее,
	|		РасходыНаОнлайнКассыЗаКвартал.СуммаРасход КАК УчтеноЗаКвартал,
	|		РасходыНаОнлайнКассыЗаКвартал.СуммаКонечныйОстаток КАК КонечныйОстаток
	|	ИЗ
	|		РегистрНакопления.РасходыНаОнлайнКассыУменьшающиеЕНВД.ОстаткиИОбороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				,
	|				,
	|				Организация = &Организация
	|					И &УсловиеРегистрацияВНалоговомОргане) КАК РасходыНаОнлайнКассыЗаКвартал
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасходыНаКассовуюТехникуУчтенныеРанее.СуммаПриход,
	|		РасходыНаКассовуюТехникуУчтенныеРанее.СуммаРасход,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РасходыНаОнлайнКассыУменьшающиеЕНВД.Обороты(
	|				,
	|				&КонецПредыдущегоПериода,
	|				,
	|				Организация = &Организация
	|					И &УсловиеРегистрацияВНалоговомОргане) КАК РасходыНаКассовуюТехникуУчтенныеРанее) КАК ВложенныйЗапрос"
	;
	
	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("КонецПредыдущегоПериода", НачалоКвартала(Период) - 1);
	Запрос.УстановитьПараметр("НачалоПериода",           НачалоКвартала(Период));
	Запрос.УстановитьПараметр("КонецПериода",            КонецКвартала(Период));
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане",
			"РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Расходы, Выборка);
	КонецЕсли;
	
	Возврат Расходы;
	
КонецФункции

// Получает общую сумму зарегистрированных расходов на онлайн-кассы за весь период до переданной даты,
// включая саму дату (секунду даты).
//
// Параметры:
//  Организация                 - СправочникСсылка.Организации - организация
//  ГраницаПериода              - Дата - дата, по которую определяется сумма расходов (включая ее саму)
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе,
//                                   для которой определяются расходы. Если не указана - по всем регистрациям.
//
// Возвращаемое значение:
//   Число   - сумма зарегистрированных пользователем расходов на приобретение онлайн-касс
//
Функция ЗарегистрированоВсего(Организация, ГраницаПериода, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) ИЛИ НЕ ЗначениеЗаполнено(ГраницаПериода) Тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыНаОнлайнКассыОбороты.СуммаПриход КАК ВсегоРасходов
	|ИЗ
	|	РегистрНакопления.РасходыНаОнлайнКассыУменьшающиеЕНВД.Обороты(
	|			,
	|			&ГраницаПериода,
	|			,
	|			Организация = &Организация
	|				И &УсловиеРегистрацияВНалоговомОргане) КАК РасходыНаОнлайнКассыОбороты"
	;
	
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ГраницаПериода", ГраницаПериода);
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане",
			"РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВсегоРасходов;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрименяетсяВычетПоОнлайнКассам(Организация, Период)
	
	Возврат УчетЕНВД.ПрименяетсяВычетПоОнлайнКассам(Организация, Период);
	
КонецФункции

Функция НовыйОписаниеСуммРасходовНаОнлайнКассы()
	
	Расходы = Новый Структура;
	
	Расходы.Вставить("РасходыНаОнлайнКассыВсего",            0); // Всего зарегистрированных расходов, включая текущий период.
	Расходы.Вставить("РасходыНаОнлайнКассыУчтеноРанее",      0); // Расходы, учтенные в уменьшении налога за предыдущие периоды
	Расходы.Вставить("РасходыНаОнлайнКассыНачальныйОстаток", 0); // Расходы, которые можно учесть в уменьшении налога текущего периода: остаток на начало периода + сумма зарегистрированных за текущий период.
	расходы.Вставить("НалоговыйВычетОнлайнКассы",            0); // Расходы, учтенные в уменьшении налога за текущий период.
	Расходы.Вставить("РасходыНаОнлайнКассыКонечныйОстаток",  0); // Остаток неучтенных расходов - переносится в следующий период.
	
	Возврат Расходы;
	
КонецФункции

#КонецОбласти

#КонецЕсли