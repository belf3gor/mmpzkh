#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбменДаннымиСервер.НадоВыполнитьОбработчикПослеЗагрузкиДанных(ЭтотОбъект, Ссылка) Тогда
		
		ПослеЗагрузкиДанных();
		
	КонецЕсли;
	
	Если ОбменДаннымиСервер.НадоВыполнитьОбработчикПослеВыгрузкиДанных(ЭтотОбъект, Ссылка) Тогда
		
		ПослеВыгрузкиДанных();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеЗагрузкиДанных()
	
	Если Ссылка <> ПланыОбмена.АвтономнаяРабота.ЭтотУзел() Тогда
		
		// СтандартныеПодсистемы.ЗащитаПерсональныхДанных
		ЗащитаПерсональныхДанных.ПослеЗагрузкиДанных(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ЗащитаПерсональныхДанных
		
	КонецЕсли;
	
	Справочники.КлючиАналитикиУчетаЗатрат.ЗаменитьДублиКлючейАналитики();
	Справочники.КлючиАналитикиУчетаНДС.ЗаменитьДублиКлючейАналитики();
	
КонецПроцедуры

Процедура ПослеВыгрузкиДанных()
	
	Если ТарификацияБПВызовСервераПовтИсп.ИспользуетсяНедоступнаяФункциональность() Тогда
		ВызватьИсключение НСтр("ru='В программе используется функциональность, не входящая в ваш тариф.
			|Обмен с автономным рабочим местом заблокирован.'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли