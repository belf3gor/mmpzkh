#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.ПланыСчетов.Хозрасчетный);
	Элементы.ЗаписатьИЗакрыть.Видимость = НЕ ТолькоПросмотр;
	
	ПараметрыУчета = ОбщегоНазначенияБП.ОпределитьПараметрыУчета();
	
	ВестиУчетЗатратПоПодразделениям                 = ?(Константы.ВестиУчетЗатратПоПодразделениям.Получить(), 1, 0);
	ВестиУчетЗатратПоПодразделениямИсходноеЗначение = ВестиУчетЗатратПоПодразделениям;
	
	ДоступностьНастройкиВеденияУчетаЗатратПоПодразделениям = ПланыСчетов.Хозрасчетный.ДоступнаНастройкаУчетаЗатратПоПодразделениям();
	
	Элементы.ВестиУчетЗатратПоПодразделениямПоКаждомуПодразделению.Доступность = ДоступностьНастройкиВеденияУчетаЗатратПоПодразделениям;
	Элементы.ВестиУчетЗатратПоПодразделениямСводно.Доступность = ДоступностьНастройкиВеденияУчетаЗатратПоПодразделениям;
	Элементы.Предупреждение.Видимость = Не ДоступностьНастройкиВеденияУчетаЗатратПоПодразделениям;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВестиУчетЗатратПоПодразделениямПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПередЗаписьюЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПрименитьНастройкуСубконто();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИзменения();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Если (Форма.ВестиУчетЗатратПоПодразделениям <> Форма.ВестиУчетЗатратПоПодразделениямИсходноеЗначение) 
		И Форма.ВестиУчетЗатратПоПодразделениямИсходноеЗначение = 1 Тогда
		Форма.Элементы.ГруппаУчетЗатратПоПодразделениямПредупреждениеАктивно.Видимость = Истина;
	Иначе
		Форма.Элементы.ГруппаУчетЗатратПоПодразделениямПредупреждениеАктивно.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения()
	
	СнятьПризнакУчетаПоПодразделениям = ВестиУчетЗатратПоПодразделениям <> ВестиУчетЗатратПоПодразделениямИсходноеЗначение
		И ВестиУчетЗатратПоПодразделениямИсходноеЗначение = Истина;
	ТекстВопроса = ОбщегоНазначенияБПВызовСервера.ПолучитьТекстВопросаПриУдаленииСубконто(ПараметрыУчета, СнятьПризнакУчетаПоПодразделениям);
	Если ПустаяСтрока(ТекстВопроса) Тогда
		ПрименитьНастройкуСубконто();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПередЗаписьюЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуСубконто(Отказ = Ложь)
	
	ПрименитьНастройкуСубконтоНаСервере(Отказ);
	
	Если НЕ Отказ Тогда
		Модифицированность = Ложь;
		Оповестить("ИзменениеНастройкиПланаСчетов");
		Оповестить("ИзменениеНастроекПараметровУчета");
		ОповеститьОбИзменении(Тип("ПланСчетовСсылка.Хозрасчетный"));
		ПоказатьОповещениеПользователя(НСтр("ru = 'Изменены параметры субконто'"), 
			"e1cib/app/Обработка.ЖурналРегистрации", "Журнал регистрации");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкуСубконтоНаСервере(Отказ = Ложь)
	
	ПланыСчетов.Хозрасчетный.УстановитьУчетЗатратПоПодразделениям(
		ВестиУчетЗатратПоПодразделениям,
		ОбщегоНазначенияБП.СобытиеЖурналаРегистрацииПараметрыУчета());
		
	Константы.ВестиУчетЗатратПоПодразделениям.Установить(ВестиУчетЗатратПоПодразделениям);
		
КонецПроцедуры

#КонецОбласти
