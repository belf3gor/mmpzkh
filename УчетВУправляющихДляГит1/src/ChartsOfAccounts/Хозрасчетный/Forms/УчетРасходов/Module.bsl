#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.ПланыСчетов.Хозрасчетный);
	Элементы.ЗаписатьИЗакрыть.Видимость = НЕ ТолькоПросмотр;
	
	ПараметрыУчета = ОбщегоНазначенияБП.ОпределитьПараметрыУчета();
	
	УчитыватьРасходыПоЭлементамЗатрат                 = ПараметрыУчета.УчитыватьРасходыПоЭлементамЗатрат;
	УчитыватьРасходыПоЭлементамЗатратИсходноеЗначение = УчитыватьРасходыПоЭлементамЗатрат;
	
	УчитыватьРасходыПоСтатьямЗатрат                 = ПараметрыУчета.УчитыватьРасходыПоСтатьямЗатрат;
	УчитыватьРасходыПоСтатьямЗатратИсходноеЗначение = УчитыватьРасходыПоСтатьямЗатрат;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоЭлементамЗатратФлагПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоСтатьямЗатратФлагПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	УстановитьПредупрежденияПоЭлементамЗатрат(Форма);	
	УстановитьПредупрежденияПоСтатьямЗатрат(Форма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаписьюЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПрименитьНастройкуСубконто();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИзменения();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения()
	
	ПараметрыУчета.УчитыватьРасходыПоЭлементамЗатрат = УчитыватьРасходыПоЭлементамЗатрат;
	ПараметрыУчета.УчитыватьРасходыПоСтатьямЗатрат   = УчитыватьРасходыПоСтатьямЗатрат;
	
	ТекстВопроса = ОбщегоНазначенияБПВызовСервера.ПолучитьТекстВопросаПриУдаленииСубконто(ПараметрыУчета);
	Если ПустаяСтрока(ТекстВопроса) Тогда
		ПрименитьНастройкуСубконто();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПередЗаписьюЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуСубконто(Отказ = Ложь)
	
	ПрименитьНастройкуСубконтоНаСервере(Отказ);
	
	Если НЕ Отказ Тогда
		Модифицированность = Ложь;
		Оповестить("ИзменениеНастройкиПланаСчетов");
		Оповестить("ИзменениеНастроекПараметровУчета");
		ОповеститьОбИзменении(Тип("ПланСчетовСсылка.Хозрасчетный"));
		ПоказатьОповещениеПользователя(НСтр("ru = 'Изменены параметры субконто'"), 
			"e1cib/app/Обработка.ЖурналРегистрации", "Журнал регистрации");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкуСубконтоНаСервере(Отказ = Ложь)
	
	ОбщегоНазначенияБП.ПрименитьПараметрыУчета(ПараметрыУчета, Истина, Отказ);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредупрежденияПоЭлементамЗатрат(Форма)

	Если Форма.УчитыватьРасходыПоЭлементамЗатрат <> Форма.УчитыватьРасходыПоЭлементамЗатратИсходноеЗначение
		И Форма.УчитыватьРасходыПоЭлементамЗатратИсходноеЗначение Тогда
		Форма.Элементы.ПоЭлементамЗатратПредупреждениеВключено.Видимость = Истина;
	Иначе
		Форма.Элементы.ПоЭлементамЗатратПредупреждениеВключено.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредупрежденияПоСтатьямЗатрат(Форма)

	Если Форма.УчитыватьРасходыПоСтатьямЗатрат <> Форма.УчитыватьРасходыПоСтатьямЗатратИсходноеЗначение
		И Форма.УчитыватьРасходыПоСтатьямЗатратИсходноеЗначение Тогда
		Форма.Элементы.ПоСтатьямЗатратПредупреждениеВключено.Видимость = Истина;
	Иначе
		Форма.Элементы.ПоСтатьямЗатратПредупреждениеВключено.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
