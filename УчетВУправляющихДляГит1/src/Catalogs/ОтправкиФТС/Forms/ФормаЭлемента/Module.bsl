&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		//Создается новый объект
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Справочник не предназначен для ручного заполнения!'");
		Сообщение.Сообщить();
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;

	Элементы.НадписьПомеченНаУдаление.Видимость = Объект.ПометкаУдаления;
	
	Если ТипЗнч(Объект.ОтчетСсылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		ОтчетПредставление = Строка(РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(Объект.ОтчетСсылка));
	Иначе // электронное представление
		ОтчетПредставление = Объект.ОтчетСсылка.Наименование;
	КонецЕсли;
	
	ДатаОтправки = Объект.ДатаОтправки;
	ДатаЗакрытия = НСтр("ru = '<не завершена>'");
	ДатаОбновления = НСтр("ru = '<не определено>'");
	ОбновитьФорму();
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьЭлементыФормыПриИспользованииОднойОрганизации(ЭтаФорма, "ГруппаСубъектыПереписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтчетПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Объект.ОтчетСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСтатусОтправкиНажатие(Элемент)
	
	КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФТС(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПервичноеСообщениеНажатие(Элемент)
	
	ВАрхиве = ОбъектВАрхиве(Объект.Ссылка, "Подпись");
	Если ВАрхиве Тогда 
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 4, 0, Истина);
		Возврат;
	КонецЕсли;
	
	Адрес = ПолучитьАдресФайлаПодписи(Объект.Ссылка);
	ПолучитьФайл(Адрес, НСтр("ru = 'Подпись'") + Объект.ИмяФайлаВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСтатусНаКонтролеНажатие(Элемент)
	
	ПерейтиПоНавигационнойСсылке("https://edata.customs.ru/FtsPersonalCabinetWeb/Services/Obtain/Stat");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьПротокол(Команда)
	
	ВАрхиве = ОбъектВАрхиве(Объект.Ссылка, "Протокол");
	Если ВАрхиве Тогда 
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 16, 0, Истина);
		Возврат;
	КонецЕсли;
	
	Адрес = ПолучитьАдресФайлаПротокола(Объект.Ссылка);
	ИмяФайлаВыгрузкиДоТочки = Объект.ИмяФайлаВыгрузки;
	ПозицияТочки = СтрНайти(ИмяФайлаВыгрузкиДоТочки, ".");
	Если ПозицияТочки > 0 Тогда
		ИмяФайлаВыгрузкиДоТочки = Лев(ИмяФайлаВыгрузкиДоТочки, ПозицияТочки - 1);
	КонецЕсли;
	
	ПолучитьФайл(Адрес, НСтр("ru = 'Протокол'") + ИмяФайлаВыгрузкиДоТочки + ".html");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПодписываемыйОтчет(Команда)
	
	ВАрхиве = ОбъектВАрхиве(Объект.Ссылка, "ВыгрузкаДляПодписи");
	Если ВАрхиве Тогда 
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 20, 0, Истина);
		Возврат;
	КонецЕсли;
	
	Адрес = ПолучитьАдресФайлаВыгрузкиДляПодписи(Объект.Ссылка);
	ПолучитьФайл(Адрес, Объект.ИмяФайлаВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПодпись(Команда)
	
	ВАрхиве = ОбъектВАрхиве(Объект.Ссылка, "Подпись");
	Если ВАрхиве Тогда 
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 4, 0, Истина);
		Возврат;
	КонецЕсли;
	
	Адрес = ПолучитьАдресФайлаПодписи(Объект.Ссылка);
	ПолучитьФайл(Адрес, НСтр("ru = 'Подпись'") + Объект.ИмяФайлаВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКвитанцию(Команда)
	
	ВАрхиве = ОбъектВАрхиве(Объект.Ссылка, "Квитанция");
	Если ВАрхиве Тогда 
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 1, 0, Истина);
		Возврат;
	КонецЕсли;
	
	Адрес = ПолучитьАдресФайлаКвитанции(Объект.Ссылка);
	ИмяФайлаВыгрузкиДоТочки = Объект.ИмяФайлаВыгрузки;
	ПозицияТочки = СтрНайти(ИмяФайлаВыгрузкиДоТочки, ".");
	Если ПозицияТочки > 0 Тогда
		ИмяФайлаВыгрузкиДоТочки = Лев(ИмяФайлаВыгрузкиДоТочки, ПозицияТочки - 1);
	КонецЕсли;
	
	ПолучитьФайл(Адрес, НСтр("ru = 'Квитанция'") + ИмяФайлаВыгрузкиДоТочки + ".xml");
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьЗавершение", ЭтотОбъект);
	КонтекстЭДОКлиент.ОбновитьРезультатКонкретнойОтправкиФТС(ЭтаФорма, Объект.Ссылка, ОписаниеОповещения);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормыНаСервере()
	
	Прочитать();
	ОбновитьФорму();
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьФорму()
	
	ЦветСерый = Новый Цвет(128, 128, 128);
	ЦветСиний = Новый Цвет(0, 0, 255);
	
	ПустаяДата = Дата(1,1,1);
	
	ДатаОбновления = ?(Объект.ДатаПолученияРезультата = ПустаяДата, НСтр("ru = '<не определено>'"), Объект.ДатаПолученияРезультата);
	
	Если Объект.СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен Тогда
		Элементы.НадписьСтатусОтправки.ЦветТекста = ЦветСерый;
		Элементы.НадписьСтатусОтправки.Заголовок = НСтр("ru = 'Протокол обработки отчета отсутствует'");
		Элементы.НадписьСтатусОтправки.ГиперСсылка = Ложь;
		Элементы.ГруппаНаКонтроле.Видимость = Ложь;
	Иначе
		Элементы.КартинкаПодтверждениеОтправки.Картинка = БиблиотекаКартинок.ЗеленыйШар;
		ДатаЗакрытия =  Объект.ДатаЗакрытия;
		
		Если Объект.НаКонтроле Тогда
			Элементы.КартинкаСтатусОтправки.Картинка = БиблиотекаКартинок.ПисьмоПодтверждениеПолучено;
			Элементы.НадписьСтатусОтправки.Заголовок = НСтр("ru = 'Протокол получен'");
			Элементы.НадписьСтатусОтправки.ЦветТекста = ЦветСиний;
			Элементы.НадписьСтатусОтправки.ГиперСсылка = Истина;
			Элементы.ГруппаНаКонтроле.Видимость = Истина;
		ИначеЕсли Объект.СтатусОтправки = Перечисления.СтатусыОтправки.НеПринят Тогда
			Элементы.КартинкаСтатусОтправки.Картинка = БиблиотекаКартинок.РегламентированныйОтчетНеПринят;
			Элементы.НадписьСтатусОтправки.Заголовок = НСтр("ru = 'Протокол ошибок получен'");
			Элементы.НадписьСтатусОтправки.ЦветТекста = ЦветСиний;
			Элементы.НадписьСтатусОтправки.ГиперСсылка = Истина;
			Элементы.ГруппаНаКонтроле.Видимость = Ложь;
		ИначеЕсли Объект.СтатусОтправки = Перечисления.СтатусыОтправки.ПринятЕстьОшибки Тогда
			Элементы.КартинкаСтатусОтправки.Картинка = БиблиотекаКартинок.ПисьмоПодтверждениеПолучено;
			Элементы.НадписьСтатусОтправки.Заголовок = НСтр("ru = 'Протокол ошибок получен'");
			Элементы.НадписьСтатусОтправки.ЦветТекста = ЦветСиний;
			Элементы.НадписьСтатусОтправки.ГиперСсылка = Истина;
			Элементы.ГруппаНаКонтроле.Видимость = Ложь;
		ИначеЕсли Объект.СтатусОтправки = Перечисления.СтатусыОтправки.Сдан Тогда
			Элементы.КартинкаСтатусОтправки.Картинка = БиблиотекаКартинок.ПисьмоПодтверждениеПолучено;
			Элементы.НадписьСтатусОтправки.Заголовок = НСтр("ru = 'Протокол о сдаче отчета получен'");
			Элементы.НадписьСтатусОтправки.ЦветТекста = ЦветСиний;
			Элементы.НадписьСтатусОтправки.ГиперСсылка = Истина;
			Элементы.ГруппаНаКонтроле.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьМеню();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьМеню()
	
	КнопкаВыгрузитьПротокол = Элементы.Найти("ФормаВыгрузитьПротокол");
	КнопкаВыгрузитьКвитанцию = Элементы.Найти("ФормаВыгрузитьКвитанцию");
	
	Если Объект.СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен Тогда
		КнопкаВыгрузитьПротокол.Видимость = Ложь;
		КнопкаВыгрузитьКвитанцию.Видимость = Ложь;
	ИначеЕсли Объект.СтатусОтправки = Перечисления.СтатусыОтправки.НеПринят Тогда
		КнопкаВыгрузитьПротокол.Видимость = Истина;
		КнопкаВыгрузитьКвитанцию.Видимость = Истина;
	ИначеЕсли Объект.СтатусОтправки = Перечисления.СтатусыОтправки.Сдан
		ИЛИ Объект.СтатусОтправки = Перечисления.СтатусыОтправки.ПринятЕстьОшибки Тогда
		КнопкаВыгрузитьПротокол.Видимость = Истина;
		КнопкаВыгрузитьКвитанцию.Видимость = Истина;
	КонецЕсли;
	
	//кнопка Обновить
	КнопкаОбновить = Элементы.Найти("ФормаОбновить");
	КнопкаОбновить.Видимость = (Объект.СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен) ИЛИ (Объект.СтатусОтправки = Перечисления.СтатусыОтправки.Сдан)
		ИЛИ (Объект.СтатусОтправки = Перечисления.СтатусыОтправки.НеПринят);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДанныеФормыНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдресФайлаВыгрузкиДляПодписи(ОтправкаСсылка, УникальныйИдентификатор = Неопределено)
	
	Возврат ПоместитьВоВременноеХранилище(ОтправкаСсылка.ВыгрузкаДляПодписи.Получить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьАдресФайлаПодписи(ОтправкаСсылка, УникальныйИдентификатор = Неопределено)
	
	Возврат ПоместитьВоВременноеХранилище(ОтправкаСсылка.Подпись.Получить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьАдресФайлаПротокола(ОтправкаСсылка)
	
	СтрПротокол = ОтправкаСсылка.Протокол.Получить();
	ВременныйФайл = ПолучитьИмяВременногоФайла();
	Текст = Новый ЗаписьТекста(ВременныйФайл, КодировкаТекста.UTF8);
	Текст.Записать(СтрПротокол);
	Текст.Закрыть();
	
	Результат = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ВременныйФайл));
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ВременныйФайл);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьАдресФайлаКвитанции(ОтправкаСсылка)
	
	СтрКвитанция = ОтправкаСсылка.Квитанция.Получить();
	ВременныйФайл = ПолучитьИмяВременногоФайла();
	Текст = Новый ЗаписьТекста(ВременныйФайл, КодировкаТекста.UTF8);
	Текст.Записать(СтрКвитанция);
	Текст.Закрыть();
	
	Результат = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ВременныйФайл));
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ВременныйФайл);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбъектВАрхиве(Знач ОбъектОтправки, Знач ИмяФайла)
	
	КонтекстМодуля = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	Возврат КонтекстМодуля.ОбъектВАрхиве(ОбъектОтправки, ИмяФайла);
	
КонецФункции

#КонецОбласти
