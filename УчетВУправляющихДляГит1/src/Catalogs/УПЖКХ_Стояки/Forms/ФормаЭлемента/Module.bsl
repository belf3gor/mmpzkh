
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.КоммунальныйРесурс.ПредупреждениеПриРедактировании = "На стояк уже установлены приборы учета, изменение его парамеров может спровоцировать ошибки в учете";
	
	Если НаСтоякУстановленПриборУчета(Объект.Ссылка) Тогда
		Элементы.КоммунальныйРесурс.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе
		Элементы.КоммунальныйРесурс.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля ввода "НомерСтояка".
//
Процедура НомерСтоякаПриИзменении(Элемент)
	
	Объект.Наименование = "Стояк №" + Объект.НомерСтояка;
	
КонецПроцедуры // НомерСтоякаПриИзменении()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Возвращает "Истина", если на стояк уже установлен прибор учета.
Функция НаСтоякУстановленПриборУчета(Стояк)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КВП_ЗакрепленныеСчетчики.Счетчик КАК Счетчик
	|ИЗ
	|	РегистрСведений.КВП_ЗакрепленныеСчетчики КАК КВП_ЗакрепленныеСчетчики
	|ГДЕ
	|	КВП_ЗакрепленныеСчетчики.Период <= &Период
	|	И КВП_ЗакрепленныеСчетчики.Стояк = &Стояк";
	
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	Запрос.УстановитьПараметр("Стояк",  Стояк);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // НаСтоякУстановленПриборУчета()

#КонецОбласти
