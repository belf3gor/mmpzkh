///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЭтотОбъект.ПредставлениеXML = Параметры.ПредставлениеXML;
	ЭтотОбъект.КартинкаФайлЗаписан = БиблиотекаКартинок.Записать;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	// Для веб-клиента работа с файлами недоступна.
	#Если ВебКлиент Тогда

		Элементы.ГруппаИмяФайла.Видимость = Ложь;

	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписать(Команда)

	// Для веб-клиента работа с файлами недоступна.
	#Если НЕ ВебКлиент Тогда

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = НСтр("ru='Выберите файл, куда записать содержимое XML'");
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Расширение = "xml";
	Диалог.Фильтр = НСтр("ru='Файл xml (*.xml)|*.xml'");

	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещенияПослеВыбора = Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтотОбъект, ДополнительныеПараметры);

	Диалог.Показать(ОписаниеОповещенияПослеВыбора);

	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	// Для веб-клиента работа с файлами недоступна.
	#Если НЕ ВебКлиент Тогда

	ТипМассив = Тип("Массив");

	Если (ТипЗнч(ВыбранныеФайлы) = ТипМассив)
			И (ВыбранныеФайлы.Количество() > 0) Тогда

		ИмяФайла = ВыбранныеФайлы[0];

		Если НЕ ПустаяСтрока(ИмяФайла) Тогда

			Если ПустаяСтрока(ЭтотОбъект.ПредставлениеXML) Тогда

				ТекстСообщения = НСтр("ru='Текст пустой. Нечего сохранять.'");
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = ТекстСообщения;
				Сообщение.Поле  = "ПредставлениеXML"; // Идентификатор.
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();

			Иначе

				ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, "UTF-8", Символы.ПС, Ложь, Символы.ПС);
					ЗаписьТекста.Записать(ЭтотОбъект.ПредставлениеXML);
				ЗаписьТекста.Закрыть();

				ПоказатьОповещениеПользователя(
					НСтр("ru='Файл записан.'"),
					ОбработкаНовостейКлиент.ПолучитьДействиеОткрытияФайла(ИмяФайла, "КакТекстВ1С"),
					ИмяФайла,
					ЭтотОбъект.КартинкаФайлЗаписан);

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	#КонецЕсли

КонецПроцедуры

#КонецОбласти
