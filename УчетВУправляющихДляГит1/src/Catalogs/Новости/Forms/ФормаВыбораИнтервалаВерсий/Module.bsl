///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЭтотОбъект.Продукт  = Параметры.Продукт;
	ЭтотОбъект.ВерсияОТ = Параметры.ВерсияОТ;
	ЭтотОбъект.ВерсияДО = Параметры.ВерсияДО;

	Если ПустаяСтрока(ЭтотОбъект.Продукт) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если ПустаяСтрока(ЭтотОбъект.ВерсияОТ) Тогда
		ЭтотОбъект.ВерсияОТ = "00.00.000.0000";
	КонецЕсли;
	Если ПустаяСтрока(ЭтотОбъект.ВерсияДО) Тогда
		ЭтотОбъект.ВерсияДО = "99.99.999.9999";
	КонецЕсли;

	ЭтотОбъект.Версия = ЭтотОбъект.ВерсияОТ;

	Если (ЭтотОбъект.ВерсияОТ = "00.00.000.0000") И (ЭтотОбъект.ВерсияДО = "99.99.999.9999") Тогда
		ЭтотОбъект.ВариантВерсии = 0; // Любая версия
	ИначеЕсли (ЭтотОбъект.ВерсияОТ <> "00.00.000.0000") И (ЭтотОбъект.ВерсияДО <> "99.99.999.9999") Тогда
		Если ЭтотОбъект.ВерсияОТ = ЭтотОбъект.ВерсияДО Тогда // Точная версия
			ЭтотОбъект.ВариантВерсии = 1;
		Иначе // Интервал версий
			ЭтотОбъект.ВариантВерсии = 2;
		КонецЕсли;
	ИначеЕсли (ЭтотОбъект.ВерсияОТ <> "00.00.000.0000") И (ЭтотОбъект.ВерсияДО = "99.99.999.9999") Тогда
		ЭтотОбъект.ВариантВерсии = 3; // От версии и выше
	ИначеЕсли (ЭтотОбъект.ВерсияОТ = "00.00.000.0000") И (ЭтотОбъект.ВерсияДО <> "99.99.999.9999") Тогда
		ЭтотОбъект.ВариантВерсии = 4; // До версии включительно
	Иначе
		ЭтотОбъект.ВариантВерсии = 2;
	КонецЕсли;

	УправлениеФормой(ЭтотОбъект);

	ЭтотОбъект.ЗакрыватьПриВыборе = Истина;
	ЭтотОбъект.ЗакрыватьПриЗакрытииВладельца = Истина;
	ЭтотОбъект.РежимОткрытияОкна  = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантВерсииПриИзменении(Элемент)

	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)

	ЭтотОбъект.Версия = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ЭтотОбъект.Версия);
	ЭтотОбъект.ВерсияОТ = ЭтотОбъект.Версия;
	ЭтотОбъект.ВерсияДО = ЭтотОбъект.Версия;

КонецПроцедуры

&НаКлиенте
Процедура ВерсияОТПриИзменении(Элемент)

	ЭтотОбъект.ВерсияОТ = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ЭтотОбъект.ВерсияОТ);
	ЭтотОбъект.Версия = ЭтотОбъект.ВерсияОТ;

КонецПроцедуры

&НаКлиенте
Процедура ВерсияДОПриИзменении(Элемент)

	ЭтотОбъект.ВерсияДО = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ЭтотОбъект.ВерсияДО);
	ЭтотОбъект.Версия = ЭтотОбъект.ВерсияДО;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	Если ЭтотОбъект.ВариантВерсии = 0 Тогда // Любая версия
		ЭтотОбъект.ВерсияОТ = "00.00.000.0000";
		ЭтотОбъект.ВерсияДО = "99.99.999.9999";
	ИначеЕсли ЭтотОбъект.ВариантВерсии = 1 Тогда // Точная версия
		ЭтотОбъект.ВерсияОТ = ЭтотОбъект.Версия;
		ЭтотОбъект.ВерсияДО = ЭтотОбъект.Версия;
	ИначеЕсли ЭтотОбъект.ВариантВерсии = 2 Тогда // Интервал
	ИначеЕсли ЭтотОбъект.ВариантВерсии = 3 Тогда // От версии и выше
		ЭтотОбъект.ВерсияДО = "99.99.999.9999";
	ИначеЕсли ЭтотОбъект.ВариантВерсии = 4 Тогда // До версии включительно
		ЭтотОбъект.ВерсияОТ = "00.00.000.0000";
	КонецЕсли;

	Результат = Новый Структура("ВерсияОТ, ВерсияДО",
		ЭтотОбъект.ВерсияОТ,
		ЭтотОбъект.ВерсияДО);

	ЭтотОбъект.ОповеститьОВыборе(Результат); // Эта же команда закроет форму

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)

	ЭтотОбъект.Закрыть(КодВозвратаДиалога.Отмена);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	лкЭлементы = Форма.Элементы;

	лкЭлементы.ВерсияОТ_Интервал.Доступность = Ложь;
	лкЭлементы.ВерсияДО_Интервал.Доступность = Ложь;
	лкЭлементы.ВерсияОТ.Доступность = Ложь;
	лкЭлементы.ВерсияДО.Доступность = Ложь;
	лкЭлементы.Версия.Доступность = Ложь;

	Если Форма.ВариантВерсии = 0 Тогда // Любая версия
	ИначеЕсли Форма.ВариантВерсии = 1 Тогда // Точная версия
		лкЭлементы.Версия.Доступность = Истина;
	ИначеЕсли Форма.ВариантВерсии = 2 Тогда // Интервал
		лкЭлементы.ВерсияОТ_Интервал.Доступность = Истина;
		лкЭлементы.ВерсияДО_Интервал.Доступность = Истина;
	ИначеЕсли Форма.ВариантВерсии = 3 Тогда // От версии и выше
		лкЭлементы.ВерсияОТ.Доступность = Истина;
	ИначеЕсли Форма.ВариантВерсии = 4 Тогда // До версии включительно
		лкЭлементы.ВерсияДО.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

