
#Область СлужебныеПроцедурыИФункции

// Функция получает способ расчета услуги.
Функция ПолучитьСпособРасчетаУслуги(мУслуга) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КВП_Услуги.СпособРасчета
	|ИЗ
	|	Справочник.КВП_Услуги КАК КВП_Услуги
	|ГДЕ
	|	КВП_Услуги.Ссылка = &Услуга";
	Запрос.УстановитьПараметр("Услуга", мУслуга);
	
	Выборка = Запрос.Выполнить().Выбрать();
	НайденныйСпособРасчета = Неопределено;
	Если Выборка.Следующий() Тогда
		НайденныйСпособРасчета = Выборка.СпособРасчета;
	КонецЕсли;
	
	Возврат НайденныйСпособРасчета;
	
КонецФункции

// Функция сравнивает способы расчета двух услуг.
Функция СпособыРасчетаСовпадают(Услуга1, Услуга2) Экспорт
	
	МассивУслуг = Новый Массив;
	МассивУслуг.Добавить(Услуга1);
	МассивУслуг.Добавить(Услуга2);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КВП_Услуги.СпособРасчета
	|ИЗ
	|	Справочник.КВП_Услуги КАК КВП_Услуги
	|ГДЕ
	|	КВП_Услуги.Ссылка В (&МассивУслуг)";
	Запрос.УстановитьПараметр("МассивУслуг", МассивУслуг);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 1 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Функция получает способы расчета, которые могут быть выбраны для услуг добровольного страхования.
Функция ПолучитьДоступныеСпособыРасчетаДляУслугДобровольногоСтрахования() Экспорт
	
	СпособыРасчета = Новый СписокЗначений;
	СпособыРасчета.Добавить(Перечисления.КВП_СпособыРасчета.ПоПлощади);
	СпособыРасчета.Добавить(Перечисления.КВП_СпособыРасчета.ФиксированноеНачисление);
	
	Возврат СпособыРасчета;
	
КонецФункции // ПолучитьДоступныеСпособыРасчетаДляУслугДобровольногоСтрахования()

#КонецОбласти