#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет самый поздний завершенный сеанс, в ходе которого были загружены данные по конкретной настройке.
// См. также Справочники.НастройкиЗагрузкиДанныхВнешнихСистем.АктуальностьДанных
//
// Параметры:
//  Настройка - СправочникСсылка.НастройкиЗагрузкиДанныхВнешнихСистем - настройка, в ходе которой загружаются данные
// 
// Возвращаемое значение:
//  СправочникСсылка.СеансыЗагрузкиДанныхВнешнихСистем - сеанс; пустая ссылка, если ни одного сеанса не состоялось
//
Функция КрайнийСеансЗагрузки(Настройка) Экспорт
	
	Если Не ЗначениеЗаполнено(Настройка) Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Настройка", Настройка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Сеансы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СеансыЗагрузкиДанныхВнешнихСистем КАК Сеансы
	|ГДЕ
	|	Сеансы.Владелец = &Настройка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаЗавершения УБЫВ";
		
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ПустаяСсылка();
	
КонецФункции


// Определяет локальную дату завершения сеанса.
// См. также Справочники.НастройкиЗагрузкиДанныхВнешнихСистем.АктуальностьДанных
//
// Параметры:
//  Сеанс - СправочникСсылка.СеансыЗагрузкиДанныхВнешнихСистем - сеанс
// 
// Возвращаемое значение:
//  Дата - локальная дата (в часовом поясе пользователя)
//
Функция ЛокальнаяДатаЗагрузки(Сеанс) Экспорт
	
	Если Не ЗначениеЗаполнено(Сеанс) Тогда
		Возврат '0001-01-01';
	КонецЕсли;
	
	ДатаЗагрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сеанс, "ДатаЗавершения");
	Если ЗначениеЗаполнено(ДатаЗагрузки) Тогда
		ДатаЗагрузки = МестноеВремя(ДатаЗагрузки, ЧасовойПоясСеанса());
	КонецЕсли;
	
	Возврат ДатаЗагрузки
	
КонецФункции

#КонецОбласти

#КонецЕсли