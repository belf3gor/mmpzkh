
////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ПрочиеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
// Устанавливает видимость элементов формы.
//
// Параметры
//  нет
//
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.СтавкиПениПроцентнаяСтавкаПени.Видимость       = Объект.ИспользоватьПроцентнуюСтавку;
	Элементы.СтавкиПениДоляСтавкиРефинансирования.Видимость = Не Объект.ИспользоватьПроцентнуюСтавку;
	
	// Устанавливает видимость гиперссылки "Размер ключевой ставки ЦБРФ".
	Элементы.РазмерКлючевойСтавкиЦБРФ.Видимость = Не Объект.ИспользоватьПроцентнуюСтавку;
	
	// Текст гиперссылки-подсказки изменяется в зависимости от основания расчета пени.
	Если Не Форма.ИспользуетсяРегламентноеЗаданиеОбновленияКлючевойСтавкиЦБ Тогда
		Элементы.РазмерКлючевойСтавкиЦБРФ.Заголовок = "Размер ключевой ставки ЦБРФ
													  |(возможно автоматическое обновление ключевой ставки)"
	Иначе
		Элементы.РазмерКлючевойСтавкиЦБРФ.Заголовок = "Размер ключевой ставки ЦБРФ"
	КонецЕсли;
	
КонецПроцедуры // УправлениеФормой()

&НаКлиентеНаСервереБезКонтекста
// Процедура обновляет представление доли ставки рефинансирования в строках таблицы ставок.
//
Процедура ОбновитьПредставлениеДолиСтавкиРефинансированияВСтрокахТаблицыСтавок(Форма)
	
	Для Каждого ТекСтрока Из Форма.Объект.СтавкиПени Цикл
		ТекСтрока.ДоляСтавкиРефинансирования = УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеДроби(ТекСтрока.ДоляСтавкиРефинансированияЦБЧислитель,
																											 ТекСтрока.ДоляСтавкиРефинансированияЦБЗнаменатель);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Процедура обновляет концы интервалов табличного поля "Ставки пени".
Процедура ОбновитьКонцыИнтервалов()
	
	НомерТекущейСтроки = 1;
	КоличествоСтрок = Объект.СтавкиПени.Количество();
	Пока НомерТекущейСтроки <= КоличествоСтрок Цикл
		Если Объект.СтавкиПени[НомерТекущейСтроки - 1].НомерСтроки = КоличествоСтрок Тогда
			Объект.СтавкиПени[НомерТекущейСтроки - 1].КонецИнтервала = "настоящий момент";
		Иначе
			Объект.СтавкиПени[НомерТекущейСтроки - 1].КонецИнтервала = Строка(Объект.СтавкиПени[НомерТекущейСтроки].ДеньСоСрокаОплаты - 1);
		КонецЕсли;
		НомерТекущейСтроки = НомерТекущейСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры // ОбновитьКонцыИнтервалов()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() И Параметры.ЗначениеКопирования.Пустая() Тогда
		Объект.ВариантОплатыПени = Перечисления.КВП_ВариантыОплатыПени.ВПоследнююОчередь;
		НоваяСтрока = Объект.СтавкиПени.Добавить();
		НоваяСтрока.ДеньСоСрокаОплаты = 1;
		НоваяСтрока.ДоляСтавкиРефинансированияЦБЧислитель   = 1;
		НоваяСтрока.ДоляСтавкиРефинансированияЦБЗнаменатель = 300;
	КонецЕсли;
	
	ОбновитьПредставлениеДолиСтавкиРефинансированияВСтрокахТаблицыСтавок(ЭтаФорма);
	
	// Получим признак использования регламентного задания обновления ключевой ставки ЦБ.
	ИспользуетсяРегламентноеЗаданиеОбновленияКлючевойСтавкиЦБ = УПЖКХ_ОбщегоНазначенияСервер.ПроверитьИспользованиеРегламентногоЗаданияОбновленияКлючевойСтавкиЦБ();
	
	УправлениеФормой(ЭтаФорма);
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриОткрытии" формы.
Процедура ПриОткрытии(Отказ)
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.ДатаНачалаУчетаНачислений", "МесяцНачалаУчетаНачисленийПредставление");
	
	ОбновитьКонцыИнтервалов();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗаписью" формы.
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Проверим корректность заполнения ТЧ.
	КоличествоСтрок = Объект.СтавкиПени.Количество();
	Для ТекИндекс = 1 По КоличествоСтрок - 1 Цикл
		Если Объект.СтавкиПени[ТекИндекс].ДеньСоСрокаОплаты < Объект.СтавкиПени[ТекИндекс - 1].ДеньСоСрокаОплаты Тогда
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("В строке №" + (ТекИндекс + 1) + " нарушен порядок следования интервалов.", Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПослеЗаписи" формы.
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПредставлениеДолиСтавкиРефинансированияВСтрокахТаблицыСтавок(ЭтаФорма);
	
	ОбновитьКонцыИнтервалов();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" флажка "Использовать процентную ставку".
Процедура ИспользоватьПроцентнуюСтавкуПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередНачаломИзменения" поля "СтавкиПени".
Процедура СтавкиПениПередНачаломИзменения(Элемент, Отказ)
	
	ТекИмяКолонки = Элемент.ТекущийЭлемент.Имя;
	
	Если ТекИмяКолонки = "СтавкиПениДоляСтавкиРефинансирования" Тогда
		
		Отказ = Истина;
		ТекСтрока = Элементы.СтавкиПени.ТекущиеДанные;
		
		// Вызов формы для редактирования доли ставки рефинансирования.
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Заголовок",       "Доля ставки рефинансирования");
		СтруктураПараметров.Вставить("ДоляЧислитель",   ТекСтрока.ДоляСтавкиРефинансированияЦБЧислитель);
		СтруктураПараметров.Вставить("ДоляЗнаменатель", ТекСтрока.ДоляСтавкиРефинансированияЦБЗнаменатель);
		
		ОткрытьФорму("ОбщаяФорма.УПЖКХ_УстановкаДоли",
					 СтруктураПараметров,
					 ЭтаФорма,,,,
					 Новый ОписаниеОповещения("ОбработатьВыборДолиСтавкиРефинансирования", ЭтаФорма, Новый Структура("ТекСтрока", ТекСтрока)),
					 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура-обработчик выбора доли, вызванного в процедуре-обработчике "СтавкиПениПередНачаломИзменения()".
Процедура ОбработатьВыборДолиСтавкиРефинансирования(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ТекСтрока = ДополнительныеПараметры.ТекСтрока;
	
	Если НЕ РезультатВыбора = Неопределено И ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		
		Модифицированность = Истина;
		
		ТекСтрока.ДоляСтавкиРефинансированияЦБЧислитель   = РезультатВыбора.ДоляЧислитель;
		ТекСтрока.ДоляСтавкиРефинансированияЦБЗнаменатель = РезультатВыбора.ДоляЗнаменатель;
		
		ТекСтрока.ДоляСтавкиРефинансирования = УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеДроби(ТекСтрока.ДоляСтавкиРефинансированияЦБЧислитель,
																											 ТекСтрока.ДоляСтавкиРефинансированияЦБЗнаменатель);
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборДолиСтавкиРефинансирования()

&НаКлиенте
// Обработчик события "ПриНачалеРедактирования" поля "СтавкиПени".
Процедура СтавкиПениПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.СтавкиПени.ТекущиеДанные;
	
	Если НоваяСтрока И Не ТекущиеДанные = Неопределено Тогда
		
		ТекущиеДанные.ДоляСтавкиРефинансирования = УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеДроби(ТекущиеДанные.ДоляСтавкиРефинансированияЦБЧислитель,
																												 ТекущиеДанные.ДоляСтавкиРефинансированияЦБЗнаменатель);
		
	КонецЕсли;
	
	Если НоваяСтрока И Не ЗначениеЗаполнено(ТекущиеДанные.ДеньСоСрокаОплаты) Тогда
		ТекущиеДанные.ДеньСоСрокаОплаты = Объект.СтавкиПени[ТекущиеДанные.НомерСтроки - 2].ДеньСоСрокаОплаты + 1;
	КонецЕсли;
	
	ОбновитьКонцыИнтервалов();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередУдалением" поля "СтавкиПени".
Процедура СтавкиПениПередУдалением(Элемент, Отказ)
	
	Если Объект.СтавкиПени.Количество() = 1 Или Элементы.СтавкиПени.ТекущиеДанные.НомерСтроки = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПослеУдаления" поля "СтавкиПени".
Процедура СтавкиПениПослеУдаления(Элемент)
	
	ОбновитьКонцыИнтервалов();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" поля "СтавкиПениДеньСоСрокаОплаты".
Процедура СтавкиПениДеньСоСрокаОплатыПриИзменении(Элемент)
	
	ОбновитьКонцыИнтервалов();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Нажатие" надписи "РазмерКлючевойСтавкиЦБРФ".
Процедура РазмерКлючевойСтавкиЦБРФНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.УПЖКХ_КлючеваяСтавкаЦБ.ФормаСписка");
	
КонецПроцедуры

// Обработчик события "ПриИзменении" поля "МесяцНачалаУчетаНачисленийПредставление".
&НаКлиенте
Процедура МесяцНачалаУчетаНачисленийПредставлениеПриИзменении(Элемент)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.ДатаНачалаУчетаНачислений", "МесяцНачалаУчетаНачисленийПредставление", Модифицированность);
	
КонецПроцедуры

// Обработчик события "НачалоВыбора" поля "МесяцНачалаУчетаНачисленийПредставление".
&НаКлиенте
Процедура МесяцНачалаУчетаНачисленийПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.ДатаНачалаУчетаНачислений", "МесяцНачалаУчетаНачисленийПредставление");
	
КонецПроцедуры

// Обработчик события "Регулирование" поля "МесяцНачалаУчетаНачисленийПредставление".
&НаКлиенте
Процедура МесяцНачалаУчетаНачисленийПредставлениеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.ДатаНачалаУчетаНачислений", "МесяцНачалаУчетаНачисленийПредставление", Направление, Модифицированность);
	
КонецПроцедуры

// Обработчик события "АвтоПодбор" поля "МесяцНачалаУчетаНачисленийПредставление".
&НаКлиенте
Процедура МесяцНачалаУчетаНачисленийПредставлениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

// Обработчик события "ОкончаниеВводаТекста" поля "МесяцНачалаУчетаНачисленийПредставление".
&НаКлиенте
Процедура МесяцНачалаУчетаНачисленийПредставлениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	УПЖКХ_ТиповыеМетодыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти
