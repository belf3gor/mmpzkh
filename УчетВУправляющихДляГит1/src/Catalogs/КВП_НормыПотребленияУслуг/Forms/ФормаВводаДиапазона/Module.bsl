
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("НижнееЗначение") Или Параметры.НижнееЗначение = Неопределено Тогда
		ПустоеНижнееЗначение = Истина;
	КонецЕсли;
	Если Не Параметры.Свойство("ВерхнееЗначение") Или Параметры.ВерхнееЗначение = Неопределено Тогда
		ПустоеВерхнееЗначение = Истина;
	КонецЕсли;
	
	Параметры.Свойство("НижнееЗначение",  НижнееЗначение);
	Параметры.Свойство("ВерхнееЗначение", ВерхнееЗначение);
	
	Если Параметры.Свойство("ВерхнееЗначение") Тогда
		ИспользоватьВерхнююГраницу = ?(Параметры.ВерхнееЗначение = -1, Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "НижнееЗначение".
//
Процедура НижнееЗначениеПриИзменении(Элемент)
	
	ПустоеНижнееЗначение = Ложь;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "ВерхнееЗначение".
//
Процедура ВерхнееЗначениеПриИзменении(Элемент)
	
	ПустоеВерхнееЗначение = Ложь;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "ОтключитьВерхнийПорог".
//
Процедура ИспользоватьВерхнююГраницуПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "Ввести значение".
//
Процедура ВвестиЗначениеДиапазона(Команда)
	
	Если ИспользоватьВерхнююГраницу И ПустоеВерхнееЗначение Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не введено верхнее значение диапазона.");
		Возврат;
	КонецЕсли;
	
	Если Не ИспользоватьВерхнююГраницу И НижнееЗначение = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Нижнее значение диапазона не может быть равно нулю. Повторите ввод.");
		Возврат;
	КонецЕсли;
	
	Если ВерхнееЗначение = 0 И НижнееЗначение = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Верхнее и нижнее значения диапазона равны нулю. Повторите ввод.");
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьВерхнююГраницу И НижнееЗначение > ВерхнееЗначение Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Нижнее значение диапазона больше верхнего значения. Повторите ввод.");
		Возврат;
	КонецЕсли;
	
	Если Не НижнееЗначение = 0 И НижнееЗначение = ВерхнееЗначение Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Нижнее и верхнее значения диапазона совпадают. Повторите ввод.");
		Возврат;
	КонецЕсли;
	
	СтруктураВозвращаемыхЗначений = Новый Структура;
	СтруктураВозвращаемыхЗначений.Вставить("Значение1", НижнееЗначение);
	СтруктураВозвращаемыхЗначений.Вставить("Значение2", ?(ИспользоватьВерхнююГраницу, ВерхнееЗначение, -1));
	
	Если Не ИспользоватьВерхнююГраницу Тогда
		ВидДляВывода = "[" + НижнееЗначение + " и более)";
	Иначе
		ВидДляВывода = "[" + НижнееЗначение + "; " + ВерхнееЗначение + ")";
	КонецЕсли;
	СтруктураВозвращаемыхЗначений.Вставить("ВидДляВывода", ВидДляВывода);
	
	Закрыть(СтруктураВозвращаемыхЗначений);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыУправленияВнешнимВидомФормы

&НаКлиенте
// Процедура управления доступностью элементов формы.
//
Процедура УправлениеФормой()
	
	Элементы.ВерхнееЗначение.Доступность = ИспользоватьВерхнююГраницу;
	
	Если Не ИспользоватьВерхнююГраницу Тогда
		ВерхнееЗначение = 0;
	КонецЕсли;
	
	Если ПустоеНижнееЗначение Тогда
		НижнееЗначениеДляВывода = 0;
	Иначе
		НижнееЗначениеДляВывода = НижнееЗначение;
	КонецЕсли;
		
	Если Не ИспользоватьВерхнююГраницу Тогда
		ВерхнееЗначениеДляВывода = " и более";
	Иначе
		Если ПустоеВерхнееЗначение Тогда
			ВерхнееЗначениеДляВывода = "; 0";
		Иначе
			ВерхнееЗначениеДляВывода ="; " + ВерхнееЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Диапазон = "[" + НижнееЗначениеДляВывода + ВерхнееЗначениеДляВывода + ")";
	
КонецПроцедуры // УправлениеФормой()

#КонецОбласти