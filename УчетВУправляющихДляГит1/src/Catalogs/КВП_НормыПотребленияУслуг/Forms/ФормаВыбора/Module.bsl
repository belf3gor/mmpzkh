
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере".
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Услуга) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Владелец", Параметры.Услуга,
		                             ВидСравненияКомпоновкиДанных.Равно, , ЗначениеЗаполнено(Параметры.Услуга));
	КонецЕсли;
	
	// Установим отбор по типу нормы.
	Если Параметры.Свойство("НормаНаЛицевойСчет") Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "НормаНаЛицевойСчет", Параметры.НормаНаЛицевойСчет,
									 ВидСравненияКомпоновкиДанных.Равно, , ЗначениеЗаполнено(Параметры.НормаНаЛицевойСчет));
	КонецЕсли;
	
	Если Параметры.НеИспользоватьБлизкихРодственников Тогда
		
		СписокНорм = Справочники.КВП_НормыПотребленияУслуг.ПолучитьСписокНормЗависящихОтКоличестваБлизкихРодственников(Параметры.Услуга);
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Ссылка", СписокНорм,
		                             ВидСравненияКомпоновкиДанных.НеВСписке, , СписокНорм.Количество() > 0);
		
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "СписокПередНачаломДобавления" поля "Список".
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ФормаЭлемента = ПолучитьФорму("Справочник.КВП_НормыПотребленияУслуг.Форма.ФормаЭлемента", , Элемент);
	ФормаЭлемента.НеИспользоватьБлизкихРодственников = НеИспользоватьБлизкихРодственников;
	
	МассивЭлементов = УПЖКХ_ТиповыеМетодыКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "НормаНаЛицевойСчет");
	Если МассивЭлементов.Количество() > 0 Тогда
		ЭлементОтбора = МассивЭлементов[0];
		Если ЭлементОтбора.Использование Тогда
			ФормаЭлемента.Объект.НормаНаЛицевойСчет = ЭлементОтбора.ПравоеЗначение;
			ФормаЭлемента.ТипОбъектаНазначенияНовогоЭлементаПредустановлен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	МассивЭлементов = УПЖКХ_ТиповыеМетодыКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "Владелец");
	Если МассивЭлементов.Количество() > 0 Тогда
		ЭлементОтбора = МассивЭлементов[0];
		Если ЭлементОтбора.Использование Тогда
			ФормаЭлемента.Объект.Владелец = ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	ФормаЭлемента.Открыть();
	
КонецПроцедуры

#КонецОбласти