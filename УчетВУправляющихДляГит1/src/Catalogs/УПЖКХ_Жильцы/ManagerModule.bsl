
#Область ФункцииПолученияКонтактнойИнформацииЖильцов

&НаСервере
// Функция - формирует таблицу телефонов физ. лиц жильцов.
//
// Параметры:
//  Объекты - Массив, СписокЗначений, ссылка на "Физ. лицо", "Жильца" или "Лицевой счет" - объекты,
//    для которых нужно сформировать списки телефонов.
//  ПолучатьВсеТелефоныКромеТелефоновПереданныхОбъектов - Булево - если Истина, то будут получены
//    телефоны всех объектов, кроме переданных в параметре "Объекты".
// 
// Возвращаемое значение:
//   - ТаблицаЗначений
//
Функция ПолучитьТаблицуТелефоновФизЛицЖильцов(Объекты = Неопределено, ПолучатьВсеТелефоныКромеТелефоновПереданныхОбъектов = Ложь) Экспорт
	
	УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.Ссылка В(&МассивОбъектов)";
	
	// 1. Подготавливаем массив жильцов исходя из того, что пришло на входе.
	Если Объекты = Неопределено Тогда
		
		УсловиеОтбораЖильцов = "ИСТИНА";
		
	ИначеЕсли ТипЗнч(Объекты) = Тип("Массив") И Объекты.Количество() > 0 Тогда
		
		МассивОбъектов = Объекты;
		
		Если ТипЗнч(Объекты[0]) = Тип("СправочникСсылка.КВП_ЛицевыеСчета") Тогда
			УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.Ссылка.Владелец В(&МассивОбъектов)";
		ИначеЕсли ТипЗнч(Объекты[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.ФизЛицо В(&МассивОбъектов)";
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объекты) = Тип("СписокЗначений") И Объекты.Количество() > 0 Тогда
		
		МассивОбъектов = Объекты;
		
		Если ТипЗнч(Объекты[0].Значение) = Тип("СправочникСсылка.КВП_ЛицевыеСчета") Тогда
			УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.Ссылка.Владелец В(&МассивОбъектов)";
		ИначеЕсли ТипЗнч(Объекты[0].Значение) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.ФизЛицо В(&МассивОбъектов)";
		КонецЕсли;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(Объекты);
		
		Если ТипЗнч(Объекты) = Тип("СправочникСсылка.КВП_ЛицевыеСчета") Тогда
			УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.Ссылка.Владелец В(&МассивОбъектов)";
		ИначеЕсли ТипЗнч(Объекты) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			УсловиеОтбораЖильцов = "УПЖКХ_Жильцы.ФизЛицо В(&МассивОбъектов)";
		КонецЕсли;
		
	КонецЕсли;
	
	// Если нужно исключить телефоны объектов из поиска, то меняем условие отбора.
	Если Не Объекты = Неопределено И ПолучатьВсеТелефоныКромеТелефоновПереданныхОбъектов Тогда
		УсловиеОтбораЖильцов = "НЕ " + УсловиеОтбораЖильцов;
	КонецЕсли;
	
	// 2. Получаем всех жильцов и их физ. лица.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	УПЖКХ_Жильцы.Ссылка КАК Жилец,
	|	УПЖКХ_Жильцы.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ втЖильцыИФизЛица
	|ИЗ
	|	Справочник.УПЖКХ_Жильцы КАК УПЖКХ_Жильцы
	|ГДЕ
	|	&УсловиеОтбораЖильцов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЖильцыИФизЛица.Жилец КАК Жилец,
	|	втЖильцыИФизЛица.ФизЛицо КАК ФизЛицо
	|ИЗ
	|	втЖильцыИФизЛица КАК втЖильцыИФизЛица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЖильцыИФизЛица.ФизЛицо КАК ФизЛицо
	|ИЗ
	|	втЖильцыИФизЛица КАК втЖильцыИФизЛица";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбораЖильцов", УсловиеОтбораЖильцов);
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЖильцовИФизЛиц = ПакетРезультатов[1].Выгрузить();
	ТаблицаЖильцовИФизЛиц.Колонки.Добавить("СписокТелефонов", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	ТаблицаЖильцовИФизЛиц.Индексы.Добавить("ФизЛицо");
	
	ТаблицаФизЛиц = ПакетРезультатов[2].Выгрузить();
	
	Если Не ТаблицаФизЛиц.Количество() = 0 Тогда
		
		МассивФизЛиц  = ТаблицаФизЛиц.ВыгрузитьКолонку("ФизЛицо");
		
		// 3. Получаем таблицу телефонов физ. лиц.
		ТаблицаТелефоновФизЛиц = УПЖКХ_ТиповыеМетодыСервер.КонтактнаяИнформацияОбъектов(МассивФизЛиц, Перечисления.ТипыКонтактнойИнформации.Телефон);
		ТаблицаТелефоновФизЛиц.Индексы.Добавить("Объект");
		
		ТаблицаФизЛиц = ТаблицаТелефоновФизЛиц.Скопировать(, "Объект");
		ТаблицаФизЛиц.Свернуть("Объект");
		
		// 4. Заполняем списки телефонов физ. лиц.
		СтруктураОтбораТелефонов = Новый Структура("Объект");
		СтруктураОтбораЖильцов   = Новый Структура("ФизЛицо");
		
		// Обходим все физ. лица с телефонами.
		Для каждого ТекФизЛицо из ТаблицаФизЛиц Цикл
			
			// Формируем строковое представление списка телефонов физ. лица.
			СтруктураОтбораТелефонов.Объект = ТекФизЛицо.Объект;
			СтрокиТелефонов                 = ТаблицаТелефоновФизЛиц.НайтиСтроки(СтруктураОтбораТелефонов);
			ТаблицаТелефоновФизЛица         = ТаблицаТелефоновФизЛиц.Скопировать(СтрокиТелефонов, "Представление");
			МассивТелефонов                 = ТаблицаТелефоновФизЛица.ВыгрузитьКолонку("Представление");
			
			СтрокаСпискаТелефонов = СтрСоединить(МассивТелефонов, ", ");
			
			// Дозаполняем таблицу жильцов списками телефонов.
			СтруктураОтбораЖильцов.ФизЛицо = ТекФизЛицо.Объект;
			СтрокиЖильцов                  = ТаблицаЖильцовИФизЛиц.НайтиСтроки(СтруктураОтбораЖильцов);
			
			Для каждого ТекСтрокаЖильца из СтрокиЖильцов Цикл
				ТекСтрокаЖильца.СписокТелефонов = СтрокаСпискаТелефонов;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаЖильцовИФизЛиц;
	
КонецФункции

#КонецОбласти
