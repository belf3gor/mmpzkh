
#Область СлужебныеПроцедурыИФункции

// Обработчик события "ПередЗаписью" объекта.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Если устанавливается пометка на удаление или снимается, тогда проверка не выполняется.
	Если ПометкаУдаления 
		ИЛИ (НЕ ЭтоНовый() И Ссылка.ПометкаУдаления И НЕ ПометкаУдаления) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверяем заполнение реквизитов.
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не заполнен лицевой счет жильца!", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФизЛицо) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не заполнено имя физического лица!", Отказ);
	Иначе
		// Если наименование жильца еще не заполнено или не совпадает с наименованием
		// физ. лица, то возьмем наименование физ. лица.
		Если ПустаяСтрока(Наименование)
		 Или Не Наименование = ФизЛицо.Наименование Тогда
			Наименование = ФизЛицо.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	// В случае, если изменяется наименование жильца, мы должны проверить, задействован ли этот жилец
	// в регистре ответственных собственников л/с и при необходимости изменить представление ответственного у
	// соответствующего лицевого счета. Для этого в дополнительные параметры вносим информацию о необходимости
	// обновления представления.
	Если Не ЭтоНовый() И Не Наименование = Ссылка.Наименование Тогда
		ДополнительныеСвойства.Вставить("НеобходимоОбновитьПредставлениеОтветственногоСобственника", Истина);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

#КонецОбласти