#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ВыполнитьПроверкуКлючевыхРеквизитов(Отказ)

	Если ЭтоНовый() тогда
		//В случае нового вида номенклатуры не производить проверку
		Возврат;
	КонецЕсли;

	// Получим значения реквизитов вида номенклатуры из информационной базы
	УслугаИзИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Услуга");

	Если УслугаИзИБ = Услуга тогда
		Возврат;
	КонецЕсли;
	
	// Проверим, можно ли изменять признак услуга.
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидНоменклатуры", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	|	И НЕ Номенклатура.ЭтоГруппа";
		
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'Нельзя изменять признак ""Услуга"" если есть номенклатурыне позиции данного вида.'"); 
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", 
							НСтр("ru = 'Услуга'"), , , ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Услуга", "Объект", Отказ);
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью элемента справочника.
//
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ВыполнитьПроверкуКлючевыхРеквизитов(Отказ);

КонецПроцедуры

#КонецЕсли
