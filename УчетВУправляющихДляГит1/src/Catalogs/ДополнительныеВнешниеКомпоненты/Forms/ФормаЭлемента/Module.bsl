#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИзФайла(Команда)
	
	#Если ВебКлиент Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеВыбораФайлаВКИзВеб", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, , "*.zip", , УникальныйИдентификатор);
	#Иначе
		Режим = РежимДиалогаВыбораФайла.Открытие;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		Фильтр = НСтр("ru = 'Файл внешней компоненты'") + "(*.zip)|*.zip";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл внешней компоненты'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФайлаВКИзТонкогоКлиента", ЭтотОбъект);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЧерезИнтернет(Команда)
	
	Результат = Неопределено;
	ОбновитьВнешнююКомпоненту(Объект.Идентификатор, УникальныйИдентификатор, Результат);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеОбновленияВнешнейКомпоненты", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Обновление внешней компоненты.'");
		ДополнительныеВнешниеКомпонентыВызовСервера.ОбработатьОшибку(
			ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки);
		Прочитать();
	Иначе
		Прочитать();
		ОповеститьОбИзменении(Объект.Ссылка);
	КонецЕсли;
	
	ДополнительныеВнешниеКомпонентыКлиент.УдалитьВнешнююКомпонентуИзКэш(Объект.Идентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеОбновленияВнешнейКомпоненты(Результат, ДополнительныеПараметры) Экспорт

	Прочитать();
	ОповеститьОбИзменении(Объект.Ссылка);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьВнешнююКомпоненту(Знач ИмяМодуля, Знач УникальныйИдентификатор, Результат)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление внешней компоненты.'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Справочники.ДополнительныеВнешниеКомпоненты.ОбновитьВнешниеКомпоненты", Новый Структура(ИмяМодуля), ПараметрыВыполнения);

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаВКИзВеб(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеВнешнейКомпоненты(Адрес);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеВнешнейКомпоненты(Адрес)
	
	ИнформацияОВК = ДополнительныеВнешниеКомпонентыВызовСервера.ИнформацияОВнешнейКомпоненте(Адрес);
	
	Если ИнформацияОВК = Неопределено Тогда
		Возврат;
	ИначеЕсли ИнформацияОВК.ИмяМодуля <> Объект.Идентификатор Тогда
		ТекстСообщения = НСтр("ru = 'Файл содержит данные другой внешней компоненты:  %1.
									|Необходимо указать файл внешней компоненты: %2.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ИнформацияОВК.Название, Объект.Наименование);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СохранитьВнешнююКомпонентуВИнформационнойБазе(Адрес);
	
	ДополнительныеВнешниеКомпонентыКлиент.УдалитьВнешнююКомпонентуИзКэш(Объект.Идентификатор);
	
	ОповеститьОбИзменении(Объект.Ссылка);
	
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаВКИзТонкогоКлиента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено ИЛИ НЕ ВыбранныеФайлы.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ПутьКФайлу);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);

	ОбновитьДанныеВнешнейКомпоненты(АдресФайла);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьВнешнююКомпонентуВИнформационнойБазе(Знач Адрес)
	
	Справочники.ДополнительныеВнешниеКомпоненты.СохранитьВнешнююКомпонентуВИнформационнойБазе(Адрес);
	
КонецПроцедуры

#КонецОбласти