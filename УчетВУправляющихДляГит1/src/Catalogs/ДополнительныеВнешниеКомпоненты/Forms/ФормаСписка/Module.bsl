
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Копирование Тогда
		ТекстСообщения = НСтр("ru = 'Запрещено копировать внешние компоненты.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеВыбораФайлаВКИзВеб", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, , "*.zip", , УникальныйИдентификатор);
	#Иначе
		Режим = РежимДиалогаВыбораФайла.Открытие;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		Фильтр = НСтр("ru = 'Файл внешней компоненты'") + "(*.zip)|*.zip";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл внешней компоненты'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФайлаВКИзТонкогоКлиента", ЭтотОбъект);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьЧерезИнтернет(Команда)
	
	Результат = Неопределено;
	ОбновитьВнешниеКомпоненты(УникальныйИдентификатор, Результат);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеОбновленияВнешнихКомпонент", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Обновление внешних компонент.'");
		ДополнительныеВнешниеКомпонентыВызовСервера.ОбработатьОшибку(
			ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки);
		Элементы.Список.Обновить();
	Иначе
		Элементы.Список.Обновить();
	КонецЕсли;
	
	// Удаление внешних компонент из кэша.
	ПараметрыПодсистемыВнешниеКомпоненты = ПараметрыПриложения["ВнешниеКомпоненты"];
	Если ТипЗнч(ПараметрыПодсистемыВнешниеКомпоненты) = Тип("Структура") Тогда
		ПараметрыПодсистемыВнешниеКомпоненты = Новый Структура;;
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ОбновитьВнешниеКомпоненты(Знач УникальныйИдентификатор, Результат)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление внешних компонент.'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Справочники.ДополнительныеВнешниеКомпоненты.ОбновитьВнешниеКомпоненты", Новый Структура, ПараметрыВыполнения);

КонецПроцедуры

&НаКлиенте
Процедура ПослеОбновленияВнешнихКомпонент(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаВКИзТонкогоКлиента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено ИЛИ НЕ ВыбранныеФайлы.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ПутьКФайлу);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);

	СоздатьЭлементСправочникаВК(АдресФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлементСправочникаВК(Адрес)
	
	НазваниеВИБ = Неопределено; ВерсияВИБ = Неопределено; НазваниеВФайле = Неопределено; ВерсияВФайле = Неопределено;
	ПроверитьНаличиеВКВИБ(Адрес, НазваниеВИБ, ВерсияВИБ, НазваниеВФайле, ВерсияВФайле);
	
	Если ЗначениеЗаполнено(НазваниеВИБ) Тогда
		
		ТекстВопроса = НСтр("ru = 'В информационной базе уже есть внешняя компонента %1 (Версия %2).
						|В выбранном файле находится внешняя компонента %3 (Версия %4).
						|Загрузить внешний модуль из файла и заменить существующий?'");
		ТекстВопроса = СтрШаблон(ТекстВопроса, НазваниеВИБ, ВерсияВИБ, НазваниеВФайле, ВерсияВФайле);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Заменить");
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ЗаголовокВопроса = НСтр("ru = 'Выбор внешней компоненты'");
		ДополнительныеПараметры = Новый Структура("Адрес", Адрес);
		
		Оповещение = Новый ОписаниеОповещения("ПослеВопросаПользователю", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , , ЗаголовокВопроса);
		Возврат;
	КонецЕсли;
	
	СохранитьВнешнююКомпонентуВИнформационнойБазе(Адрес);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьВнешнююКомпонентуВИнформационнойБазе(Знач Адрес)
	
	Справочники.ДополнительныеВнешниеКомпоненты.СохранитьВнешнююКомпонентуВИнформационнойБазе(Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаПользователю(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЭлементСправочникаВКНаСервере(ДополнительныеПараметры.Адрес);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьЭлементСправочникаВКНаСервере(Адрес)
	
	ИнформацияОВК = ДополнительныеВнешниеКомпонентыВызовСервера.ИнформацияОВнешнейКомпоненте(Адрес);
	
	НайденныйЭлемент = Справочники.ДополнительныеВнешниеКомпоненты.НайтиПоРеквизиту("Идентификатор", ИнформацияОВК.ИмяМодуля);
	
	Если ЗначениеЗаполнено(НайденныйЭлемент) Тогда
		ОбъектСправочника = НайденныйЭлемент.ПолучитьОбъект();
	Иначе
		ОбъектСправочника = Справочники.ДополнительныеВнешниеКомпоненты.СоздатьЭлемент();
	КонецЕсли;
	
	ОбъектСправочника.Наименование = ИнформацияОВК.Название;
	ОбъектСправочника.Версия = ИнформацияОВК.Версия;
	ДвоичныеДанныеВК = ПолучитьИзВременногоХранилища(Адрес);
	ОбъектСправочника.ДанныеВК = Новый ХранилищеЗначения(ДвоичныеДанныеВК, Новый СжатиеДанных(9));
	ОбъектСправочника.Идентификатор = ИнформацияОВК.ИмяМодуля;
	ОбъектСправочника.Адрес = ИнформацияОВК.URLИнфо;
	ОбъектСправочника.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьНаличиеВКВИБ(Знач Адрес, НазваниеВИБ, ВерсияВИБ, НазваниеВФайле, ВерсияВФайле)
	
	ИнформацияОВК = ДополнительныеВнешниеКомпонентыВызовСервера.ИнформацияОВнешнейКомпоненте(Адрес);
	Запрос = Новый Запрос;
	Запрос.Текст =   "ВЫБРАТЬ
	                 |	ВнешниеКомпоненты.Версия,
	                 |	ВнешниеКомпоненты.Наименование
	                 |ИЗ
	                 |	Справочник.ДополнительныеВнешниеКомпоненты КАК ВнешниеКомпоненты
	                 |ГДЕ
	                 |	ВнешниеКомпоненты.Идентификатор = &Идентификатор";
	Запрос.УстановитьПараметр("Идентификатор", ИнформацияОВК.ИмяМодуля);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	НазваниеВИБ = Выборка.Наименование;
	ВерсияВИБ = Выборка.Версия;
	НазваниеВФайле = ИнформацияОВК.Название;
	ВерсияВФайле = ИнформацияОВК.Версия;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаВКИзВеб(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЭлементСправочникаВК(Адрес);
	
КонецПроцедуры

#КонецОбласти

