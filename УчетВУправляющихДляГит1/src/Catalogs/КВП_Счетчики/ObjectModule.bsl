
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение коэффициента счетчика, если он не заполнен.
	ИзмененаГруппаСчетчика = Не (Ссылка.Родитель = Родитель);
	Если Не ЭтоГруппа И Не ИзмененаГруппаСчетчика Тогда
		
		Если Коэффициент = 0 Тогда
			Коэффициент = 1;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры //ПередЗаписью()

// Обработчик проверки заполнения объекта.
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Проверка заполнения реквизитов счетчика.
	ИзмененаГруппаСчетчика = Не (Ссылка.Родитель = Родитель);
	Если Не ЭтоГруппа И Не ИзмененаГруппаСчетчика Тогда
		
		// Проверка уникальности идентификатора счетчика.
		Если Не ПустаяСтрока(Идентификатор) Тогда
			Если Не Справочники.КВП_Счетчики.ПроверитьУникальностьИдентификатора(Идентификатор, Ссылка) Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Идентификатор счетчика не уникален!", ЭтотОбъект, "Идентификатор",, Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка основных параметов счетчика.
		ПроверяемыеРеквизиты.Добавить("ВидУслуги");
		ПроверяемыеРеквизиты.Добавить("Тарифность");
		ПроверяемыеРеквизиты.Добавить("СпособРегистрацииПоказаний");
		
		// Проверки параметров не нового счетчика.
		Если Не Ссылка.Пустая() Тогда
			
			// Проверим, можно ли изменять тарифность прибора учета.
			Если Не Ссылка.Тарифность = Тарифность Тогда
				
				ВыборкаОбъектов = УПЖКХ_ВводПоказанийПриборовУчетаСервер.ПолучитьПоследниеВведенныеПоказанияСчетчика(Ссылка);
				ПроверитьТарифностьПрибораУчета(ВыборкаОбъектов, Отказ);
				Если Отказ Тогда
					УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("По данному счетчику уже имеются показания. "
					                                 + "Изменение тарифности на меньшую невозможно! "
					                                 + "Элемент не может быть записан!", ЭтотОбъект, "Тарифность",, Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
			// Проверим, можно ли изменять коэффициент прибора учета.
			Если Не Ссылка.Коэффициент = Коэффициент Тогда
				
				Если ВыборкаОбъектов = Неопределено Тогда
					ВыборкаОбъектов = УПЖКХ_ВводПоказанийПриборовУчетаСервер.ПолучитьПоследниеВведенныеПоказанияСчетчика(Ссылка);
				КонецЕсли;
				
				Если ВыборкаОбъектов.Следующий() Тогда
					Если ВыборкаОбъектов.ДневноеПоказание
					   + ВыборкаОбъектов.НочноеПоказание
					   + ВыборкаОбъектов.ПиковоеПоказание > 0 Тогда
						УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("По данному счетчику уже имеются показания. "
						                                 + "Изменение коэффициента трансформации невозможно! "
						                                 + "Элемент не может быть записан!", ЭтотОбъект, "Коэффициент",, Отказ);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет наличие введенных показаний по счетчику.
//
Процедура ПроверитьТарифностьПрибораУчета(ВыборкаОбъектов, Отказ) Экспорт
	
	Если ВыборкаОбъектов.Следующий() Тогда
		
		Отказ = НЕ Справочники.КВП_Счетчики.ПроверитьКорректностьПоказаний(ВыборкаОбъектов.ДневноеПоказание,
		                                                                   ВыборкаОбъектов.НочноеПоказание,
		                                                                   ВыборкаОбъектов.ПиковоеПоказание,
		                                                                   Тарифность);
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьТарифностьПрибораУчета()

#КонецОбласти

#КонецЕсли