
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка 
		ИЛИ ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если КодСуществует() Тогда
		
		ТекстСообщения = НСтр("ru = 'Код ""%1"" с видом ""%2"" уже существует.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Код, ВидНеоблагаемойОперации);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КодСуществует()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("ВидНеоблагаемойОперации", ВидНеоблагаемойОперации);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
	|ГДЕ
	|	КодыОперацийРаздела7ДекларацииПоНДС.Код = &Код
	|	И КодыОперацийРаздела7ДекларацииПоНДС.ВидНеоблагаемойОперации = &ВидНеоблагаемойОперации
	|	И КодыОперацийРаздела7ДекларацииПоНДС.Ссылка <> &Ссылка
	|	И НЕ КодыОперацийРаздела7ДекларацииПоНДС.ПометкаУдаления"; 
	
	СтатусВозврата = НЕ Запрос.Выполнить().Пустой();
	
	Возврат СтатусВозврата;
	
КонецФункции

#КонецОбласти

#КонецЕсли
