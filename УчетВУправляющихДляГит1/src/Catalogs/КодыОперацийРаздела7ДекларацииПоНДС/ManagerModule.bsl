
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает коды операций для 7 раздела декларации НДС
//
// Возвращаемое значение:
// - ТаблицаЗначений - таблица кодов операций для 7 раздела декларации по НДС
//				* Код - Строка - код операции
//				* Наименование - Строка - наименование операции
//				* НаименованиеПолное - Строка - описание операции
//				* ОперацияНеПодлежитНалогообложению - Булево - флаг, устанавливается, если операция не подлежит налогооблажению
//
Функция ТаблицаКлассификатораКодов() Экспорт
	
	ТаблицаПоказателей = Новый ТаблицаЗначений;
	
	Макет = Справочники.КодыОперацийРаздела7ДекларацииПоНДС.ПолучитьМакет("КодыОперацийРаздела7ДекларацииПоНДС");
	
	Список = Макет.Области.Найти("Строки");
	
	Если Список.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
		// Заполнение дерева данными списка
		ВерхОбласти = Список.Верх;
		НизОбласти = Список.Низ;
		
		НомерКолонки = 1;
		Область = Макет.Область(ВерхОбласти - 1, НомерКолонки);
		ИмяКолонки = Область.Текст;
		ДлинаКодаКлассификатора = 7;
		
		Пока ЗначениеЗаполнено(ИмяКолонки) Цикл
			
			Если ИмяКолонки = "Код" Тогда
				ТаблицаПоказателей.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(ДлинаКодаКлассификатора)));
			ИначеЕсли ИмяКолонки = "Наименование" Тогда
				ТаблицаПоказателей.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(150)));
			ИначеЕсли ИмяКолонки = "Описание" Тогда
				ТаблицаПоказателей.Колонки.Добавить("НаименованиеПолное",Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
			ИначеЕсли ИмяКолонки = "Ст149НКРФ" Тогда
				ТаблицаПоказателей.Колонки.Добавить("ОперацияНеПодлежитНалогообложению",Новый ОписаниеТипов("Булево"));
			КонецЕсли;
			
			НомерКолонки = НомерКолонки + 1;
			Область = Макет.Область(ВерхОбласти - 1, НомерКолонки);
			ИмяКолонки = Область.Текст;
			
		КонецЦикла;
		
		Для НомСтр = ВерхОбласти По НизОбласти Цикл
			// Отображаем только элементы
			
			Код = СокрП(Макет.Область(НомСтр, 1).Текст);
			Если СтрДлина(Код) = 2 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаСписка = ТаблицаПоказателей.Добавить();
			
			Для Каждого Колонка Из ТаблицаПоказателей.Колонки Цикл
				
				ЗначениеКолонки = СокрП(Макет.Область(НомСтр, ТаблицаПоказателей.Колонки.Индекс(Колонка) + 1).Текст);
				СтрокаСписка[Колонка.Имя] = ЗначениеКолонки;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаПоказателей.Сортировать(ТаблицаПоказателей.Колонки[0].Имя + " Возр");
	
	Возврат ТаблицаПоказателей;
	
КонецФункции

// Возвращает коды реализаций услуг не на территории РФ
// (необходимы для принятия НДС к вычету в соответствии с 63-ФЗ от 15.04.2019)
//
// Возвращаемое значение:
//    КодыРеализацииУслугЗаРубежом - массив кодов, соответствующих реализации услуг не на территории РФ
//
Функция КодыРеализацииУслугЗаРубежом() Экспорт
	
	КодыРеализацииУслугЗаРубежом = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодРеализацияЗаРубежом", "1010812");
	Запрос.УстановитьПараметр("КодРеализацияВЕАЭС",     "1010821");
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КодыОперацийРаздела7ДекларацииПоНДС.Ссылка КАК КодРеализацииЗаРубеж
	|ИЗ
	|	Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
	|ГДЕ
	|	(КодыОперацийРаздела7ДекларацииПоНДС.Код = &КодРеализацияЗаРубежом
	|			ИЛИ КодыОперацийРаздела7ДекларацииПоНДС.Код = &КодРеализацияВЕАЭС)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КодыРеализацииУслугЗаРубежом.Добавить(Выборка.КодРеализацииЗаРубеж);
	КонецЦикла;
	
	Возврат КодыРеализацииУслугЗаРубежом;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = СокрЛП(Данные.Код)
		+ ?(ЗначениеЗаполнено(Данные.ВидНеоблагаемойОперации), " (", "")
		+ СокрЛП(Данные.ВидНеоблагаемойОперации)
		+ ?(ЗначениеЗаполнено(Данные.ВидНеоблагаемойОперации), ")", "");
		
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Код");
	Поля.Добавить("ВидНеоблагаемойОперации");
	
КонецПроцедуры

#КонецОбласти