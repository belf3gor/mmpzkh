////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) 
		И ЗначениеЗаполнено(Объект.ИсточникДанных) Тогда
		ИнициализироватьКомпоновщикНастроек();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ИнициализироватьКомпоновщикНастроек();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Попытка
		ТекущийОбъект.Настройка = ОбщегоНазначения.ЗначениеВСтрокуXML(Настройки.Настройки.Отбор);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(Объект.Код) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Объект.Наименование, " ", Истина);
	
	Идентификатор = "";
	
	Для Каждого Слово Из МассивСлов Цикл
		
		Текст = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыXML(Слово, "");
		
		Идентификатор = Идентификатор + Врег(Лев(Текст, 1)) + Сред(Текст, 2);
		
	КонецЦикла;	

	Объект.Код = Идентификатор;
		
КонецПроцедуры

&НаКлиенте
Процедура ИсточникДанныхПриИзменении(Элемент)
	
	ИнициализироватьКомпоновщикНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаВзятьОтборИзГруппы(Команда)
	
	ВзятьОтборИзГруппы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	ЗаполнениеФормСтатистики.ИнициализироватьКомпоновщикНастроек(Настройки, Объект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ВзятьОтборИзГруппы()
	
	Объект.Настройка = Объект.Родитель.Настройка;
	ИнициализироватьКомпоновщикНастроек();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ПолучитьОтборИзГруппы.Доступность = ЗначениеЗаполнено(Объект.Родитель);

КонецПроцедуры
