#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обработчик обновления БЭД 1.0.4.0
// Разбивает ТОРГ12 на ТОРГ12Продавец и ТОРГ12Покупатель, АктВыполненныхРабот на АктИсполнитель и АктЗаказчик.
//
Процедура ОбновитьВидыДокументов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	НЕ УдалитьСоглашенияОбИспользованииЭД.ПометкаУдаления
	|	И УдалитьСоглашенияОбИспользованииЭД.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезКаталог)";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		ИскомоеСоглашение = Результат.Ссылка.ПолучитьОбъект();
		ЗаписатьОбъект = Ложь;
		
		ТОРГ12Продавец = ИскомоеСоглашение.ИсходящиеДокументы.Найти(Перечисления.ВидыЭД.ТОРГ12Продавец, "ИсходящийДокумент");
		
		Если ТОРГ12Продавец = Неопределено Тогда
			НайденнаяСтрока= ИскомоеСоглашение.ИсходящиеДокументы.Найти(Перечисления.ВидыЭД.ТОРГ12, "ИсходящийДокумент");
			Если НайденнаяСтрока <> Неопределено Тогда
				НоваяСтрока = ИскомоеСоглашение.ИсходящиеДокументы.Добавить();
				НоваяСтрока.ИсходящийДокумент         = Перечисления.ВидыЭД.ТОРГ12Продавец;
				НоваяСтрока.ИспользоватьЭП            = НайденнаяСтрока.ИспользоватьЭП;
				НоваяСтрока.ОжидатьКвитанциюОДоставке = НайденнаяСтрока.ОжидатьКвитанциюОДоставке;
				НоваяСтрока.Формировать               = НайденнаяСтрока.Формировать;
				НоваяСтрока.МаршрутПодписания         = НайденнаяСтрока.МаршрутПодписания;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ТОРГ12Покупатель = ИскомоеСоглашение.ИсходящиеДокументы.Найти(Перечисления.ВидыЭД.ТОРГ12Покупатель,
			"ИсходящийДокумент");
		
		Если ТОРГ12Покупатель = Неопределено Тогда
			НайденнаяСтрока = ИскомоеСоглашение.ВходящиеДокументы.Найти(Перечисления.ВидыЭД.ТОРГ12, "ВходящийДокумент");
			Если НайденнаяСтрока <> Неопределено Тогда
				НоваяСтрока = ИскомоеСоглашение.ИсходящиеДокументы.Добавить();
				НоваяСтрока.ИсходящийДокумент         = Перечисления.ВидыЭД.ТОРГ12Покупатель;
				НоваяСтрока.ИспользоватьЭП            = НайденнаяСтрока.ИспользоватьЭП;
				НоваяСтрока.ОжидатьКвитанциюОДоставке = НайденнаяСтрока.ОжидатьКвитанциюОДоставке;
				НоваяСтрока.Формировать               = НайденнаяСтрока.Формировать;
				НоваяСтрока.МаршрутПодписания         = НайденнаяСтрока.МаршрутПодписания;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		АктИсполнитель = ИскомоеСоглашение.ИсходящиеДокументы.Найти(Перечисления.ВидыЭД.АктИсполнитель, "ИсходящийДокумент");
		
		Если АктИсполнитель = Неопределено Тогда
			НайденнаяСтрока = ИскомоеСоглашение.ИсходящиеДокументы.Найти(Перечисления.ВидыЭД.АктВыполненныхРабот,
				"ИсходящийДокумент");
			Если НайденнаяСтрока <> Неопределено Тогда
				НоваяСтрока = ИскомоеСоглашение.ИсходящиеДокументы.Добавить();
				НоваяСтрока.ИсходящийДокумент         = Перечисления.ВидыЭД.АктИсполнитель;
				НоваяСтрока.ИспользоватьЭП            = НайденнаяСтрока.ИспользоватьЭП;
				НоваяСтрока.ОжидатьКвитанциюОДоставке = НайденнаяСтрока.ОжидатьКвитанциюОДоставке;
				НоваяСтрока.Формировать               = НайденнаяСтрока.Формировать;
				НоваяСтрока.МаршрутПодписания         = НайденнаяСтрока.МаршрутПодписания;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		АктЗаказчик = ИскомоеСоглашение.ИсходящиеДокументы.Найти(Перечисления.ВидыЭД.АктЗаказчик, "ИсходящийДокумент");
		
		Если АктЗаказчик = Неопределено Тогда
			НайденнаяСтрока = ИскомоеСоглашение.ВходящиеДокументы.Найти(Перечисления.ВидыЭД.АктВыполненныхРабот,
				"ВходящийДокумент");
			Если НайденнаяСтрока <> Неопределено Тогда
				НоваяСтрока = ИскомоеСоглашение.ИсходящиеДокументы.Добавить();
				НоваяСтрока.ИсходящийДокумент         = Перечисления.ВидыЭД.АктЗаказчик;
				НоваяСтрока.ИспользоватьЭП            = НайденнаяСтрока.ИспользоватьЭП;
				НоваяСтрока.ОжидатьКвитанциюОДоставке = НайденнаяСтрока.ОжидатьКвитанциюОДоставке;
				НоваяСтрока.Формировать               = НайденнаяСтрока.Формировать;
				НоваяСтрока.МаршрутПодписания         = НайденнаяСтрока.МаршрутПодписания;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаписатьОбъект Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ИскомоеСоглашение)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.1.6.3
// Производит заполнение версии формата в табличной части ИсходящиеДокументы.
//
Процедура ЗаполнитьВерсииФорматов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	НЕ УдалитьСоглашенияОбИспользованииЭД.ПометкаУдаления
	|	И УдалитьСоглашенияОбИспользованииЭД.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезКаталог)";
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		ИскомоеСоглашение = Результат.Ссылка.ПолучитьОбъект();
		ЗаписатьОбъект = Ложь;
		Для каждого ВидДокумента Из ИскомоеСоглашение.ИсходящиеДокументы Цикл
			Если ВидДокумента.Формировать И Не ЗначениеЗаполнено(ВидДокумента.ВерсияФормата)
				И ВидДокумента.ИсходящийДокумент = Перечисления.ВидыЭД.КаталогТоваров Тогда
				
				ВидДокумента.ВерсияФормата = "CML 4.02";
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЦикла;
		Если ЗаписатьОбъект Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ИскомоеСоглашение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.1.7.1
// Переносит сертификат ЭП из реквизита "Сертификат авторизации" в таб.часть "СертификатыПодписейОрганизации".
//
Процедура ПеренестиСертификатАвторизацииВТЧ() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка,
	|	УдалитьСоглашенияОбИспользованииЭД.УдалитьСертификатАбонента
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	НЕ УдалитьСоглашенияОбИспользованииЭД.ПометкаУдаления
	|	И УдалитьСоглашенияОбИспользованииЭД.СпособОбменаЭД = &СпособОбменаЭД";
	
	Запрос.УстановитьПараметр("СпособОбменаЭД", Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СертификатЭП = Выборка.УдалитьСертификатАбонента;
		Если ЗначениеЗаполнено(СертификатЭП) Тогда
			СоглашениеЭД = Выборка.Ссылка.ПолучитьОбъект();
			НоваяСтрока = СоглашениеЭД.СертификатыПодписейОрганизации.Добавить();
			НоваяСтрока.Сертификат = СертификатЭП;
			СоглашениеЭД.УдалитьСертификатАбонента = Неопределено;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СоглашениеЭД);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.1.7.4
// Производит заполнение версии формата в табличной части ИсходящиеДокументы.
//
Процедура ЗаполнитьВерсииФорматовИсходящихЭДИПакета() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	НЕ УдалитьСоглашенияОбИспользованииЭД.ПометкаУдаления
	|	И УдалитьСоглашенияОбИспользованииЭД.СпособОбменаЭД В(&СпособыОбменаЭД)";
	
	СпособыОбменаЭД = Новый Массив;
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту);
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезКаталог);
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезFTP);
	Запрос.УстановитьПараметр("СпособыОбменаЭД", СпособыОбменаЭД);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		ИскомоеСоглашение = Результат.Ссылка.ПолучитьОбъект();
		ЗаписатьОбъект = Ложь;
		Для каждого ВидДокумента Из ИскомоеСоглашение.ИсходящиеДокументы Цикл
			Если Не ЗначениеЗаполнено(ВидДокумента.ВерсияФормата) Тогда
				ВерсияФормата = "CML 4.02";
				Если ВидДокумента.ИсходящийДокумент = Перечисления.ВидыЭД.ПроизвольныйЭД Тогда
					ВерсияФормата = "";
				ИначеЕсли ВидДокумента.ИсходящийДокумент = Перечисления.ВидыЭД.АктЗаказчик
					ИЛИ ВидДокумента.ИсходящийДокумент = Перечисления.ВидыЭД.АктИсполнитель
					ИЛИ ВидДокумента.ИсходящийДокумент = Перечисления.ВидыЭД.ТОРГ12Покупатель
					ИЛИ ВидДокумента.ИсходящийДокумент = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
					ВерсияФормата = "ФНС 5.01";
				КонецЕсли;
				ВидДокумента.ВерсияФормата = ВерсияФормата;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ИскомоеСоглашение.ВерсияФорматаПакета) Тогда
			ИскомоеСоглашение.ВерсияФорматаПакета = Перечисления.ВерсииФорматаПакетаЭД.Версия10;
		КонецЕсли;
			
		Если ЗаписатьОбъект Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ИскомоеСоглашение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.1.13.6
// Производит заполнение версии формата в табличной части ИсходящиеДокументы.
//
Процедура ОбновитьВерсииФорматовИсходящихЭДИПакета() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	УдалитьСоглашенияОбИспользованииЭД.СпособОбменаЭД В(&СпособыОбменаЭД)";
	
	СпособыОбменаЭД = Новый Массив;
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту);
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезКаталог);
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезFTP);
	Запрос.УстановитьПараметр("СпособыОбменаЭД", СпособыОбменаЭД);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		ИскомоеСоглашение = Результат.Ссылка.ПолучитьОбъект();
		ЗаписатьОбъект = Ложь;
		Для каждого ВидДокумента Из ИскомоеСоглашение.ИсходящиеДокументы Цикл
			Если ЗначениеЗаполнено(ВидДокумента.ВерсияФормата)
				И ВидДокумента.ВерсияФормата = "CML 2.06" Тогда
				
				ВидДокумента.ВерсияФормата = "CML 2.07";
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЦикла;
		Если ЗаписатьОбъект Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ИскомоеСоглашение);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления БЭД 1.1.14.2
// Производит заполнение реквизита "ИспользуетсяКриптография".
//
Процедура ЗаполнитьИспользованиеКриптографии() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	НЕ УдалитьСоглашенияОбИспользованииЭД.УдалитьИспользуетсяКриптография
	|
	|СГРУППИРОВАТЬ ПО
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(УдалитьСоглашенияОбИспользованииЭД.СертификатыПодписейОрганизации.Ссылка) > 0";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		СоглашениеЭД = Результат.Ссылка.ПолучитьОбъект();
		СоглашениеЭД.ОбменДанными.Загрузка = Истина;
		СоглашениеЭД.УдалитьИспользуетсяКриптография = Истина;
		СоглашениеЭД.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.2.2.2
// Производит заполнение версии формата в табличной части ИсходящиеДокументы.
//
Процедура ОбновитьВерсиюФорматаИсходящихЭД207_208() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдалитьСоглашенияОбИспользованииЭД.Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК УдалитьСоглашенияОбИспользованииЭД
	|ГДЕ
	|	УдалитьСоглашенияОбИспользованииЭД.СпособОбменаЭД В(&СпособыОбменаЭД)";
	
	СпособыОбменаЭД = Новый Массив;
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту);
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезКаталог);
	СпособыОбменаЭД.Добавить(Перечисления.СпособыОбменаЭД.ЧерезFTP);
	Запрос.УстановитьПараметр("СпособыОбменаЭД", СпособыОбменаЭД);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		ИскомоеСоглашение = Результат.Ссылка.ПолучитьОбъект();
		ЗаписатьОбъект = Ложь;
		Для каждого ВидДокумента Из ИскомоеСоглашение.ИсходящиеДокументы Цикл
			Если ЗначениеЗаполнено(ВидДокумента.ВерсияФормата)
				И (ВидДокумента.ВерсияФормата = "CML 2.06"
					Или ВидДокумента.ВерсияФормата = "CML 2.07") Тогда
				ВидДокумента.ВерсияФормата = "CML 2.08";
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЦикла;
		Если ЗаписатьОбъект Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ИскомоеСоглашение);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления БЭД 1.2.7.1
// Производит заполнение версии формата в табличной части ИсходящиеДокументы.
//
Процедура ОбновитьВерсиюФорматаИсходящихЭД501_502() Экспорт
	
	ИсходящиеДокументы = Новый Массив;
	ИсходящиеДокументы.Добавить(Перечисления.ВидыЭД.СчетФактура);
	ИсходящиеДокументы.Добавить(Перечисления.ВидыЭД.КорректировочныйСчетФактура);
	ВерсияФормата = "ФНС 5.01";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка КАК Профиль
	|ИЗ
	|	Справочник.УдалитьПрофилиНастроекЭДО.ИсходящиеДокументы КАК ПрофилиНастроекЭДОИсходящиеДокументы
	|ГДЕ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ИсходящийДокумент В(&ИсходящиеДокументы)
	|	И ПрофилиНастроекЭДОИсходящиеДокументы.ВерсияФормата = &ВерсияФормата";
	Запрос.УстановитьПараметр("ИсходящиеДокументы", ИсходящиеДокументы);
	Запрос.УстановитьПараметр("ВерсияФормата", ВерсияФормата);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПрофильОбъект = Выборка.Профиль.ПолучитьОбъект();
			Для Каждого ТекСтрока Из ПрофильОбъект.ИсходящиеДокументы Цикл
				Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура
					Или ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
					ТекСтрока.ВерсияФормата = НСтр("ru = 'ФНС 5.02'");
				КонецЕсли;
			КонецЦикла;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ПрофильОбъект);
		КонецЦикла;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент В(&ИсходящиеДокументы)
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.ВерсияФормата = &ВерсияФормата";
	
	Запрос.УстановитьПараметр("ИсходящиеДокументы", ИсходящиеДокументы);
	Запрос.УстановитьПараметр("ВерсияФормата", ВерсияФормата);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НастройкаОбъект = Выборка.Соглашение.ПолучитьОбъект();
		Для Каждого ТекСтрока Из НастройкаОбъект.ИсходящиеДокументы Цикл
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура
				Или ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
				ТекСтрока.ВерсияФормата = НСтр("ru = 'ФНС 5.02'");
			КонецЕсли;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НастройкаОбъект);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.3.2.4
// Из табличной части Исходящие документы настроек и профилей ЭДО удаляются ответные титулы.
//
Процедура УдалитьОтветныеТитулы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка КАК Профиль
	|ИЗ
	|	Справочник.УдалитьПрофилиНастроекЭДО.ИсходящиеДокументы КАК ПрофилиНастроекЭДОИсходящиеДокументы
	|ГДЕ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ИсходящийДокумент В(&ОтветныеТитулы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка КАК Настройка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент В(&ОтветныеТитулы)";
	
	ОтветныеТитулы = Новый Массив;
	ОтветныеТитулы.Добавить(Перечисления.ВидыЭД.АктЗаказчик);
	ОтветныеТитулы.Добавить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель);
	ОтветныеТитулы.Добавить(Перечисления.ВидыЭД.ТОРГ12Покупатель);
	
	Запрос.УстановитьПараметр("ОтветныеТитулы", ОтветныеТитулы);
	Результаты = Запрос.ВыполнитьПакет();
	
	Профили = Результаты[0].Выгрузить();
	Для Каждого ТекСтрока Из Профили Цикл
		УдалитьИсходящиеЭД(ТекСтрока.Профиль, ОтветныеТитулы);
	КонецЦикла;
	
	Настройки = Результаты[1].Выгрузить();
	Для Каждого ТекСтрока Из Настройки Цикл
		УдалитьИсходящиеЭД(ТекСтрока.Настройка, ОтветныеТитулы);
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ОбновлениеВерсииИБ

// Регистрирует данные для обработчика обновления
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МассивСсылок = Новый Массив;
	
	ПустойМаршрут    = Справочники.МаршрутыПодписания.ПустаяСсылка();
	ИспользуемыеВиды = ОбменСКонтрагентамиСлужебный.ИспользуемыеВидыЭлектронныхДокументов();
	ПрикладныеВиды   = ОбменСКонтрагентамиСлужебный.ПрикладныеВидыЭлектронныхДокументов();
	
	ИспользуемыеВидыВНастройках = Новый Массив;
	ПрикладныеВидыВНастройках   = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка КАК Ссылка,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭП КАК ИспользоватьЭП,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.МаршрутПодписания КАК МаршрутПодписания,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент КАК ИсходящийДокумент,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ПрикладнойВидЭД КАК ПрикладнойВидЭД,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ДокументУчета КАК ДокументУчета,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ВерсияФормата КАК ВерсияФормата,
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ЗаполнениеКодаТовара КАК ЗаполнениеКодаТовара
		|ИЗ
		|	Справочник.УдалитьСоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
		|ИТОГИ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка КАК Ссылка,
		|	СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент КАК ВходящийДокумент,
		|	СоглашенияОбИспользованииЭДВходящиеДокументы.ПрикладнойВидЭД КАК ПрикладнойВидЭД,
		|	СоглашенияОбИспользованииЭДВходящиеДокументы.СпособОбработки КАК СпособОбработки
		|ИЗ
		|	Справочник.УдалитьСоглашенияОбИспользованииЭД.ВходящиеДокументы КАК СоглашенияОбИспользованииЭДВходящиеДокументы
		|ИТОГИ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиЭДО.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК НастройкиЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УдалитьСоглашенияОбИспользованииЭД КАК НастройкиЭДОПометкаУдаления
		|		ПО НастройкиЭДО.Организация = НастройкиЭДОПометкаУдаления.Организация
		|			И НастройкиЭДО.Контрагент = НастройкиЭДОПометкаУдаления.Контрагент
		|			И НастройкиЭДО.ДоговорКонтрагента = НастройкиЭДОПометкаУдаления.ДоговорКонтрагента
		|			И НастройкиЭДО.Ссылка <> НастройкиЭДОПометкаУдаления.Ссылка
		|ГДЕ
		|	НастройкиЭДО.ПометкаУдаления
		|	И НастройкиЭДО.СтатусПодключения = ЗНАЧЕНИЕ(Перечисление.СтатусыПриглашений.Принято)";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаНастроек = РезультатыЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНастроек.Следующий() Цикл
		
		ИспользуемыеВидыВНастройках.Очистить();
		ПрикладныеВидыВНастройках.Очистить();
		
		ОтмеченКОбработке = Ложь;
		
		ВыборкаИсходящиеДокументы = ВыборкаНастроек.Выбрать();
		Пока ВыборкаИсходящиеДокументы.Следующий() Цикл
			
			Если ВыборкаИсходящиеДокументы.ИспользоватьЭП 
				И ВыборкаИсходящиеДокументы.МаршрутПодписания = ПустойМаршрут Тогда
				ОтмеченКОбработке = Истина;
				Прервать;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ВыборкаИсходящиеДокументы.ЗаполнениеКодаТовара)
				И ЗначениеЗаполнено(ОбменСКонтрагентамиВнутренний.ВариантыЗаполненияПолейЭлектронныхДокументов(
					ВыборкаИсходящиеДокументы.ИсходящийДокумент, ВыборкаИсходящиеДокументы.ВерсияФормата)) Тогда
				ОтмеченКОбработке = Истина;
				Прервать;
			КонецЕсли;
			
			Если ВыборкаИсходящиеДокументы.ИсходящийДокумент = Перечисления.ВидыЭД.ПрикладнойЭД Тогда
				ПредставлениеОснования = ОбменСКонтрагентамиПовтИсп.ПредставлениеОснованияДляВидаЭД(
					ВыборкаИсходящиеДокументы.ПрикладнойВидЭД);
				ПрикладныеВидыВНастройках.Добавить(ВыборкаИсходящиеДокументы.ПрикладнойВидЭД);
			Иначе
				ПредставлениеОснования = ОбменСКонтрагентамиПовтИсп.ПредставлениеОснованияДляВидаЭД(
					ВыборкаИсходящиеДокументы.ИсходящийДокумент);
				ИспользуемыеВидыВНастройках.Добавить(ВыборкаИсходящиеДокументы.ИсходящийДокумент);
			КонецЕсли;
			
			Если ПредставлениеОснования <> ВыборкаИсходящиеДокументы.ДокументУчета Тогда
				ОтмеченКОбработке = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ОтмеченКОбработке Тогда
			МассивСсылок.Добавить(ВыборкаНастроек.Ссылка);
			Продолжить;
		ИначеЕсли ОбщегоНазначенияКлиентСервер.РазностьМассивов(ИспользуемыеВиды, ИспользуемыеВидыВНастройках).Количество()
			ИЛИ   ОбщегоНазначенияКлиентСервер.РазностьМассивов(ПрикладныеВиды, ПрикладныеВидыВНастройках).Количество() Тогда
			МассивСсылок.Добавить(ВыборкаНастроек.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	ВыборкаНастроек = РезультатыЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНастроек.Следующий() Цикл
		
		Если МассивСсылок.Найти(ВыборкаНастроек.Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаВходящиеДокументы = ВыборкаНастроек.Выбрать();
		Пока ВыборкаВходящиеДокументы.Следующий() Цикл
			
			Если ВыборкаВходящиеДокументы.ВходящийДокумент = Перечисления.ВидыЭД.ПрикладнойЭД Тогда
				ВидЭлектронногоДокумента = ВыборкаВходящиеДокументы.ПрикладнойВидЭД;
			Иначе
				ВидЭлектронногоДокумента = ВыборкаВходящиеДокументы.ВходящийДокумент;
			КонецЕсли;
			
			СписокОпераций = ОбменСКонтрагентамиСлужебный.СписокОперацийВидаЭД(ВидЭлектронногоДокумента, Истина);
			
			Если СписокОпераций.НайтиПоЗначению(ВыборкаВходящиеДокументы.СпособОбработки) = Неопределено Тогда
				МассивСсылок.Добавить(ВыборкаНастроек.Ссылка);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ВыборкаНастроек = РезультатыЗапроса[2].Выбрать();
	Пока ВыборкаНастроек.Следующий() Цикл
		МассивСсылок.Добавить(ВыборкаНастроек.Ссылка);
	КонецЦикла;
	
	МассивСсылок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСсылок);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, МассивСсылок);
	
	Данные_НоваяАрхитектураНастроекЭДО = ДанныеКОбработке_НоваяАрхитектураНастроекЭДО();
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные_НоваяАрхитектураНастроекЭДО, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИнициализироватьПараметрыОбработкиДляПереходаНаНовуюВерсию(Параметры);
	
	МетаданныеОбъекта = Метаданные.Справочники.УдалитьСоглашенияОбИспользованииЭД;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПараметрыОтметкиВыполнения = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	// Обрабатывать настройки можно только после обновления предопределенных маршрутов.
	Если Не ОбновлениеИнформационнойБазы.ОбъектОбработан("Справочник.МаршрутыПодписания").Обработан Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
			Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ПараметрыВыборки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, ПараметрыВыборки);
	
	НаборСсылок = Новый Массив;
	Пока Выборка.Следующий() Цикл
		НаборСсылок.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Реквизиты = РеквизитыНастроекЭДО(НаборСсылок);
	СвойстваОператоров = СвойстваОператоровПрофилейНастроекЭДО();
	КонверторФорматов = КонверторФорматовЭД();
	НастройкиДляОтправки = НастройкиДляОтправкиЭД();
	НастройкиПолученияВручную = НастройкиПолученияВручную();
	
	Для каждого СсылкаНаОбъект Из НаборСсылок Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаНаОбъект);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Записать = Ложь;
			
			Объект = СсылкаНаОбъект.ПолучитьОбъект();
			Если Объект <> Неопределено Тогда
				ОбработатьДанные_Основной(Объект, НастройкиДляОтправки, Записать);
				ОбработатьДанные_ЗаполнитьКонтрагентаПоРеквизитам(Объект, Записать);
				ОбработатьДанные_НоваяАрхитектураНастроекЭДО(Объект, Реквизиты, СвойстваОператоров, КонверторФорматов, НастройкиПолученияВручную, Записать);
			КонецЕсли;
			
			Если Записать Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СсылкаНаОбъект, ПараметрыОтметкиВыполнения);
			КонецЕсли;
			
			ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ШаблонСообщения = НСтр("ru = 'Не удалось обработать настройку ЭДО: %1 по причине:'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, СсылкаНаОбъект) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, СсылкаНаОбъект, ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработанныхОбъектов = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать некоторые настройки ЭДО (пропущены): %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Обработана очередная порция настроек ЭДО: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбработанныхОбъектов);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,, ТекстСообщения);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов  = Параметры.ПрогрессВыполнения.ОбработаноОбъектов  + ОбработанныхОбъектов;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

// Обработчик обновления для добавления из вне библиотеки.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюВнешнийВызов(Параметры) Экспорт
	
	ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры);
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы.
Функция ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор) Экспорт
	
	Если МетаданныеИОтбор.ПолноеИмя = "Справочник.УдалитьСоглашенияОбИспользованииЭД" Тогда
		МетаданныеИОтборНастройки = МетаданныеИОтбор;
	ИначеЕсли МетаданныеИОтбор.ПолноеИмя = "Документ.ЭлектронныйДокументИсходящий" 
		ИЛИ МетаданныеИОтбор.ПолноеИмя = "Документ.ЭлектронныйДокументВходящий" Тогда
		Настройка = МетаданныеИОтбор.Отбор.УдалитьНастройкаЭДО;
		МетаданныеИОтборНастройки = ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным(Настройка);
	КонецЕсли;
	
	Возврат ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтборНастройки);
	
КонецФункции

// Конец СтандартныеПодсистемы.ОбновлениеВерсииИБ

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьИсходящиеЭД(СправочникСсылка, ВидыЭД)
	
	СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
	ИсходящиеДокументы = СправочникОбъект.ИсходящиеДокументы;
	Счетчик = 0;
	Пока Счетчик < ИсходящиеДокументы.Количество() Цикл
		СтрокаТЗ = ИсходящиеДокументы[Счетчик];
		ИсходящийЭД = СтрокаТЗ.ИсходящийДокумент;
		Если ВидыЭД.Найти(ИсходящийЭД) = Неопределено Тогда
			Счетчик = Счетчик + 1;
			Продолжить;
		Иначе
			ИсходящиеДокументы.Удалить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
	
КонецПроцедуры

Процедура ОбновитьНастройкуИсходящихДокументов(Объект, Записать)
	
	ИспользуемыеВиды = ОбменСКонтрагентамиСлужебный.ИспользуемыеВидыЭлектронныхДокументов(
		Перечисления.НаправленияЭД.Исходящий);
	ПрикладныеВиды   = ОбменСКонтрагентамиСлужебный.ПрикладныеВидыЭлектронныхДокументов();
	УдаляемыеСтроки  = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.ИсходящиеДокументы Цикл
		
		НеиспользуемыйВид = Ложь;
		
		Если СтрокаТаблицы.ИсходящийДокумент = Перечисления.ВидыЭД.ПрикладнойЭД Тогда
			ВидЭлектронногоДокумента = СтрокаТаблицы.ПрикладнойВидЭД;
			Индекс = ПрикладныеВиды.Найти(ВидЭлектронногоДокумента);
			Если Индекс = Неопределено Тогда
				НеиспользуемыйВид = Истина;
			Иначе
				ПрикладныеВиды.Удалить(Индекс);
			КонецЕсли;
		Иначе
			ВидЭлектронногоДокумента = СтрокаТаблицы.ИсходящийДокумент;
			Индекс = ИспользуемыеВиды.Найти(ВидЭлектронногоДокумента);
			Если Индекс = Неопределено Тогда
				НеиспользуемыйВид = Истина;
			Иначе
				ИспользуемыеВиды.Удалить(Индекс);
			КонецЕсли;
		КонецЕсли;
		
		Если НеиспользуемыйВид Тогда
			УдаляемыеСтроки.Добавить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
		
		ПредставлениеОснования = ОбменСКонтрагентамиПовтИсп.ПредставлениеОснованияДляВидаЭД(ВидЭлектронногоДокумента);
		Если ПредставлениеОснования <> СтрокаТаблицы.ДокументУчета Тогда
			СтрокаТаблицы.ДокументУчета = ПредставлениеОснования;
			Записать = Истина;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ЗаполнениеКодаТовара) Тогда
			ВариантЗаполнения = ОбменСКонтрагентамиВнутренний.ВариантыЗаполненияПолейЭлектронныхДокументов(
				СтрокаТаблицы.ИсходящийДокумент, СтрокаТаблицы.ВерсияФормата);
			
			ЗначениеСвойства = Неопределено;
			Если ВариантЗаполнения.Свойство("ТоварКод", ЗначениеСвойства) Тогда
				СтрокаТаблицы.ЗаполнениеКодаТовара = ЗначениеСвойства[0].Значение;
				Записать = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если УдаляемыеСтроки.Количество() Тогда
		Записать = Истина;
		Для Каждого СтрокаТаблицы Из УдаляемыеСтроки Цикл
			Объект.ИсходящиеДокументы.Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	Если ИспользуемыеВиды.Количество() Тогда
		Записать = Истина;
		Для Каждого ВидЭлектронногоДокумента Из ИспользуемыеВиды Цикл
			ДобавитьНастройкуВидаЭлектронногоДокумента(Объект, ВидЭлектронногоДокумента, Ложь);
		КонецЦикла;
	КонецЕсли;
	
	Если Записать Тогда
		Объект.ИсходящиеДокументы.Сортировать("Приоритет");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьНастройкуВходящихДокументов(Объект, Записать)
	
	ИспользуемыеВиды = ОбменСКонтрагентамиСлужебный.ИспользуемыеВидыЭлектронныхДокументов(
		Перечисления.НаправленияЭД.Входящий);
	ПрикладныеВиды   = ОбменСКонтрагентамиСлужебный.ПрикладныеВидыЭлектронныхДокументов();
	УдаляемыеСтроки  = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.ВходящиеДокументы Цикл
		
		НеиспользуемыйВид = Ложь;
		
		Если СтрокаТаблицы.ВходящийДокумент = Перечисления.ВидыЭД.ПрикладнойЭД Тогда
			ВидЭлектронногоДокумента = СтрокаТаблицы.ПрикладнойВидЭД;
			Индекс = ПрикладныеВиды.Найти(ВидЭлектронногоДокумента);
			Если Индекс = Неопределено Тогда
				НеиспользуемыйВид = Истина;
			Иначе
				ПрикладныеВиды.Удалить(Индекс);
			КонецЕсли;
		Иначе
			ВидЭлектронногоДокумента = СтрокаТаблицы.ВходящийДокумент;
			Индекс = ИспользуемыеВиды.Найти(ВидЭлектронногоДокумента);
			Если Индекс = Неопределено Тогда
				НеиспользуемыйВид = Истина;
			Иначе
				ИспользуемыеВиды.Удалить(Индекс);
			КонецЕсли;
		КонецЕсли;
		
		Если НеиспользуемыйВид Тогда
			УдаляемыеСтроки.Добавить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
		
		СписокОпераций = ОбменСКонтрагентамиСлужебный.СписокОперацийВидаЭД(ВидЭлектронногоДокумента, Истина);
		Если СписокОпераций.НайтиПоЗначению(СтрокаТаблицы.СпособОбработки) = Неопределено Тогда
			Записать = Истина;
			
			СпособОбработки = "";
			Для Каждого ЭлементСписка Из СписокОпераций Цикл
				Если ЭлементСписка.Пометка Тогда
					СпособОбработки = ЭлементСписка.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			СтрокаТаблицы.СпособОбработки = СпособОбработки;
		КонецЕсли;
		
	КонецЦикла;
	
	Если УдаляемыеСтроки.Количество() Тогда
		Записать = Истина;
		Для Каждого СтрокаТаблицы Из УдаляемыеСтроки Цикл
			Объект.ВходящиеДокументы.Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	ВходящиеДокументы = Неопределено;
	
	Если ИспользуемыеВиды.Количество() Тогда
		ВходящиеДокументы = Объект.ВходящиеДокументы.Выгрузить();
		ВходящиеДокументы.Колонки.Добавить("ВидЭДДляСортировки");
		ВходящиеДокументы.ЗагрузитьКолонку(ВходящиеДокументы.ВыгрузитьКолонку("ВходящийДокумент"), "ВидЭДДляСортировки");
		
		Для Каждого ВидЭлектронногоДокумента Из ИспользуемыеВиды Цикл
			ДобавитьСпособОбработкиВходящегоДокумента(ВходящиеДокументы, ВидЭлектронногоДокумента, Ложь)
		КонецЦикла;
	КонецЕсли;
	
	Если ПрикладныеВиды.Количество() Тогда
		Если ВходящиеДокументы = Неопределено Тогда
			ВходящиеДокументы = Объект.ВходящиеДокументы.Выгрузить();
			ВходящиеДокументы.Колонки.Добавить("ВидЭДДляСортировки");
			ВходящиеДокументы.ЗагрузитьКолонку(ВходящиеДокументы.ВыгрузитьКолонку("ВходящийДокумент"), "ВидЭДДляСортировки");
		КонецЕсли;
		
		Для Каждого ВидЭлектронногоДокумента Из ПрикладныеВиды Цикл
			ДобавитьСпособОбработкиВходящегоДокумента(ВходящиеДокументы, ВидЭлектронногоДокумента, Истина)
		КонецЦикла;
	КонецЕсли;
	
	Если ВходящиеДокументы <> Неопределено Тогда
		Записать = Истина;
		ВходящиеДокументы.Сортировать("ВидЭДДляСортировки");
		ВходящиеДокументы.Колонки.Удалить("ВидЭДДляСортировки");
		Объект.ВходящиеДокументы.Загрузить(ВходящиеДокументы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьНастройкуВидаЭлектронногоДокумента(Объект, ВидЭлектронногоДокумента, ЭтоПрикладнойВид)
	
	НоваяСтрока = Объект.ИсходящиеДокументы.Добавить();
	
	Если Объект.РасширенныйРежимНастройкиСоглашения Тогда
		НоваяСтрока.ПрофильНастроекЭДО = Объект.ИсходящиеДокументы[0].ПрофильНастроекЭДО;
		НоваяСтрока.СпособОбменаЭД = Объект.ИсходящиеДокументы[0].СпособОбменаЭД;
		НоваяСтрока.ИдентификаторОрганизации = Объект.ИсходящиеДокументы[0].ИдентификаторОрганизации;
		НоваяСтрока.ИдентификаторКонтрагента = Объект.ИсходящиеДокументы[0].ИдентификаторКонтрагента;
	Иначе
		НоваяСтрока.ПрофильНастроекЭДО = Объект.ПрофильНастроекЭДО;
		НоваяСтрока.СпособОбменаЭД = Объект.СпособОбменаЭД;
		НоваяСтрока.ИдентификаторОрганизации = Объект.ИдентификаторОрганизации;
		НоваяСтрока.ИдентификаторКонтрагента = Объект.ИдентификаторКонтрагента;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("ИспользоватьЭП", Истина);
	ИспользоватьЭП = ЭлектроннаяПодпись.ИспользоватьЭлектронныеПодписи()
		И (НЕ ОбменСКонтрагентамиСлужебный.ЭтоПрямойОбмен(НоваяСтрока.СпособОбменаЭД)
			ИЛИ ЗначениеЗаполнено(Объект.ИсходящиеДокументы.НайтиСтроки(ПараметрыОтбора)));
	
	Если ЭтоПрикладнойВид Тогда
		ОбменСКонтрагентамиСлужебный.ЗаполнитьНастройкуПрикладногоВидаЭлектронногоДокумента(
			НоваяСтрока, ВидЭлектронногоДокумента, ИспользоватьЭП);
	Иначе
		ОбменСКонтрагентамиСлужебный.ЗаполнитьНастройкуВидаЭлектронногоДокумента(
			НоваяСтрока, ВидЭлектронногоДокумента, НоваяСтрока.СпособОбменаЭД, ИспользоватьЭП);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьСпособОбработкиВходящегоДокумента(ВходящиеДокументы, ВидЭлектронногоДокумента, ЭтоПрикладнойВид)
	
	СписокОпераций = ОбменСКонтрагентамиСлужебный.СписокОперацийВидаЭД(ВидЭлектронногоДокумента, Истина);
	
	Для Каждого ЭлементСписка Из СписокОпераций Цикл
		Если ЭлементСписка.Пометка Тогда
			СпособОбработки = ЭлементСписка.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтрока = ВходящиеДокументы.Добавить();
	НоваяСтрока.СпособОбработки = СпособОбработки;
	НоваяСтрока.ВидЭДДляСортировки = ВидЭлектронногоДокумента;
	Если ЭтоПрикладнойВид Тогда
		НоваяСтрока.ВходящийДокумент = Перечисления.ВидыЭД.ПрикладнойЭД;
		НоваяСтрока.ПрикладнойВидЭД  = ВидЭлектронногоДокумента;
	Иначе
		НоваяСтрока.ВходящийДокумент = ВидЭлектронногоДокумента;
	КонецЕсли;
	
КонецПроцедуры

#Область Обновление

Процедура ИнициализироватьПараметрыОбработкиДляПереходаНаНовуюВерсию(Параметры)
	
	// Определим общее количество объектов к обработке.
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		ПараметрыВыборки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
		ПараметрыВыборки.ВыбиратьПорциями = Ложь;
		Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Справочник.УдалитьСоглашенияОбИспользованииЭД", ПараметрыВыборки);
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеКОбработке_НоваяАрхитектураНастроекЭДО()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Соглашения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК Соглашения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|		ПО Соглашения.ИдентификаторОрганизации = УчетныеЗаписиЭДО.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументов КАК НастройкиОтправки
	|		ПО Соглашения.Ссылка.Организация = НастройкиОтправки.Отправитель
	|			И Соглашения.Ссылка.Контрагент = НастройкиОтправки.Получатель
	|			И Соглашения.Ссылка.ДоговорКонтрагента = НастройкиОтправки.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиПоВидам
	|		ПО Соглашения.Ссылка.Организация = НастройкиОтправкиПоВидам.Отправитель
	|			И Соглашения.Ссылка.Контрагент = НастройкиОтправкиПоВидам.Получатель
	|			И Соглашения.Ссылка.ДоговорКонтрагента = НастройкиОтправкиПоВидам.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПолученияЭлектронныхДокументов КАК НастройкиПолучения
	|		ПО Соглашения.Ссылка.Организация = НастройкиПолучения.Отправитель
	|			И Соглашения.Ссылка.Контрагент = НастройкиПолучения.Получатель
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК Приглашения
	|		ПО Соглашения.ИдентификаторОрганизации = Приглашения.ИдентификаторОрганизации
	|			И Соглашения.ИдентификаторКонтрагента = Приглашения.ИдентификаторКонтрагента
	|ГДЕ
	|	(УчетныеЗаписиЭДО.ИдентификаторЭДО ЕСТЬ NULL
	|			ИЛИ НастройкиОтправки.Отправитель ЕСТЬ NULL
	|			ИЛИ НастройкиОтправкиПоВидам.Отправитель ЕСТЬ NULL
	|			ИЛИ НастройкиПолучения.Отправитель ЕСТЬ NULL
	|			ИЛИ Приглашения.ИдентификаторОрганизации ЕСТЬ NULL)
	|	И Соглашения.Ссылка.СтатусПодключения <> ЗНАЧЕНИЕ(Перечисление.СтатусыПриглашений.Отклонено)
	|	И (Соглашения.СпособОбменаЭД В (&СпособыОбменаНовойАрхитектуры)
	|			ИЛИ Соглашения.Ссылка.ЭтоИнтеркампани)";
	СпособыОбменаНовойАрхитектуры = Новый Массив;
	СпособыОбменаНовойАрхитектуры.Добавить(Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском);
	СпособыОбменаНовойАрхитектуры.Добавить(Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	Запрос.УстановитьПараметр("СпособыОбменаНовойАрхитектуры", СпособыОбменаНовойАрхитектуры);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработатьДанные_Основной(Объект, НастройкиДляОтправки, Записать)
	
	ПараметрыОтбора = Новый Структура("МаршрутПодписания, ИспользоватьЭП", 
		Справочники.МаршрутыПодписания.ПустаяСсылка(), Истина);
	СтрокиСПустымМаршрутом = Объект.ИсходящиеДокументы.НайтиСтроки(ПараметрыОтбора);
	
	Если СтрокиСПустымМаршрутом.Количество() Тогда
		Записать = Истина;
		
		Для Каждого СтрокаНастройки Из СтрокиСПустымМаршрутом Цикл
			СтрокаНастройки.МаршрутПодписания = Справочники.МаршрутыПодписания.ОднойДоступнойПодписью;
		КонецЦикла;
	КонецЕсли;
	
	ОбновитьНастройкуИсходящихДокументов(Объект, Записать);
	
	ОбновитьНастройкуВходящихДокументов(Объект, Записать);
	
	Если Объект.ПометкаУдаления
		И Объект.СтатусПодключения = Перечисления.СтатусыПриглашений.Принято Тогда
		Записать = Истина;
		Объект.СтатусПодключения    = Перечисления.СтатусыПриглашений.Отклонено;
		Объект.СтатусСоглашения     = Перечисления.СтатусыСоглашенийЭД.Закрыто;
		Объект.СостояниеСоглашения  = Перечисления.СостоянияСоглашенийЭД.Закрыто;
		Объект.ДатаИзмененияСтатуса = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Проверка наличия настройки для отправки.
	Если Объект.СтатусПодключения <> Перечисления.СтатусыПриглашений.Отклонено Тогда
		ОтборСтрок = Новый Структура("Организация,Контрагент,ДоговорКонтрагента");
		ЗаполнитьЗначенияСвойств(ОтборСтрок, Объект);
		НайденныеСтроки = НастройкиДляОтправки.НайтиСтроки(ОтборСтрок);
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Объект.ИспользуетсяДляОтправки = Истина;
			Записать = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанные_ЗаполнитьКонтрагентаПоРеквизитам(Объект, Записать)
	
	Если ЗначениеЗаполнено(Объект.Контрагент)
		ИЛИ Не ЗначениеЗаполнено(Объект.ИННКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	Контрагент = Неопределено;
	ОбменСКонтрагентамиПереопределяемый.СсылкаНаОбъектПоИННКПП("Контрагенты", Объект.ИННКонтрагента, Объект.КППКонтрагента, Контрагент);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Объект.Контрагент = Контрагент;
		Записать = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.НаименованиеКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыКонтрагента = Новый Структура;
	РеквизитыКонтрагента.Вставить("ИНН", Объект.ИННКонтрагента);
	РеквизитыКонтрагента.Вставить("КПП", Объект.КППКонтрагента);
	РеквизитыКонтрагента.Вставить("Наименование", Объект.НаименованиеКонтрагента);
	
	Отказ = Ложь;
	ОбменСКонтрагентамиПереопределяемый.СоздатьКонтрагентаПоРеквизитам(РеквизитыКонтрагента, Контрагент, Отказ);
	Если Отказ ИЛИ Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Контрагент = Контрагент;
	Записать = Истина;
	
КонецПроцедуры

Процедура ОбработатьДанные_НоваяАрхитектураНастроекЭДО(Объект, Реквизиты, СвойстваОператоров, КонверторФорматов, НастройкиПолученияВручную, Записать)
	
	Если Объект.СтатусПодключения = Перечисления.СтатусыПриглашений.Отклонено Тогда
		Возврат;
	КонецЕсли;
	СпособыОбменаНовойАрхитектуры = Новый Массив;
	СпособыОбменаНовойАрхитектуры.Добавить(Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском);
	СпособыОбменаНовойАрхитектуры.Добавить(Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	Если Не ((СпособыОбменаНовойАрхитектуры.Найти(Объект.СпособОбменаЭД) <> Неопределено
		ИЛИ Объект.ЭтоИнтеркампани)) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаРеквизитов = Реквизиты.Найти(Объект.Ссылка, "НастройкаЭДО");
	
	// Учетные записи ЭДО.
	Если Не Объект.ЭтоИнтеркампани Тогда
		Настройки = Объект.ИсходящиеДокументы.Выгрузить(, "ИдентификаторОрганизации, СпособОбменаЭД,ПрофильНастроекЭДО");
		Настройки.Свернуть("ИдентификаторОрганизации, СпособОбменаЭД, ПрофильНастроекЭДО");
		Для каждого СтрокаНастроек Из Настройки Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторОрганизации) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаОператора = СвойстваОператоров.Найти(СтрокаНастроек.ПрофильНастроекЭДО, "ПрофильНастроекЭДО");
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УчетныеЗаписиЭДО");
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторЭДО", СтрокаНастроек.ИдентификаторОрганизации);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Набор = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьНаборЗаписей();
			Набор.Отбор.ИдентификаторЭДО.Установить(СтрокаНастроек.ИдентификаторОрганизации);
			Набор.Прочитать();
			
			Если Не ЗначениеЗаполнено(Набор) Тогда
				
				Запись = Набор.Добавить();
				Запись.ИдентификаторЭДО = СтрокаНастроек.ИдентификаторОрганизации;
				Запись.Организация = Объект.Организация;
				Запись.СпособОбменаЭД = СтрокаНастроек.СпособОбменаЭД;
				
				Если СтрокаОператора <> Неопределено Тогда
					Запись.ОператорЭДО                    = СтрокаОператора.ИдентификаторОператора;
					Запись.НаименованиеУчетнойЗаписи      = СтрокаОператора.НаименованиеПрофиляЭДО;
					Запись.НазначениеУчетнойЗаписи        = СтрокаОператора.НазначениеУчетнойЗаписи;
					Запись.ПодробноеОписаниеУчетнойЗаписи = СтрокаОператора.ПодробноеОписаниеУчетнойЗаписи;
					Запись.ПринятыУсловияИспользования    = СтрокаОператора.ПринятыУсловияИспользования;
				КонецЕсли;
				
				Если СтрокаНастроек.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
					Запись.ОбновитьНастройкиУведомлений = Истина;
				КонецЕсли;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// Состояние организаций БЭД.
	Если Не Объект.ЭтоИнтеркампани И ЗначениеЗаполнено(Объект.Организация) Тогда
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеОрганизацийБЭД");
		ЭлементБлокировки.УстановитьЗначение("Организация", Объект.Организация);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.СостояниеОрганизацийБЭД.СоздатьНаборЗаписей();
		Набор.Отбор.Организация.Установить(Объект.Организация);
		Набор.Прочитать();
		
		Если Не ЗначениеЗаполнено(Набор) Тогда
			СостояниеОрганизацийБЭД = Набор.Добавить();
			СостояниеОрганизацийБЭД.Организация = Объект.Организация;
			СостояниеОрганизацийБЭД.Состояние = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		КонецЕсли;
	КонецЕсли;
	
	// Настройки отправки электронных документов.
	Если Объект.ИспользуетсяДляОтправки 
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументов");
		ЭлементБлокировки.УстановитьЗначение("Отправитель", Объект.Организация);
		ЭлементБлокировки.УстановитьЗначение("Получатель", Объект.Контрагент);
		ЭлементБлокировки.УстановитьЗначение("Договор", Объект.ДоговорКонтрагента);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьНаборЗаписей();
		Набор.Отбор.Отправитель.Установить(Объект.Организация);
		Набор.Отбор.Получатель.Установить(Объект.Контрагент);
		Набор.Отбор.Договор.Установить(Объект.ДоговорКонтрагента);
		Набор.Прочитать();
		
		Если Не ЗначениеЗаполнено(Набор) 
			И ЗначениеЗаполнено(Объект.ИдентификаторОрганизации)
			И ЗначениеЗаполнено(Объект.ИдентификаторКонтрагента) Тогда
			
			Запись = Набор.Добавить();
			Запись.Отправитель = Объект.Организация;
			Запись.Получатель = Объект.Контрагент;
			Запись.Договор = Объект.ДоговорКонтрагента;
			Запись.ИспользоватьУПД = Объект.ИспользоватьУПД;
			Запись.ИспользоватьУКД = Объект.ИспользоватьУКД;
			Если СтрокаРеквизитов = Неопределено Тогда
				Запись.ДатаНачалаДействия = Дата(1900, 1, 1);
			Иначе
				Запись.ДатаНачалаДействия = СтрокаРеквизитов.ДатаНачалаДействия;
			КонецЕсли;
			Запись.ЭтоИнтеркампани = Объект.ЭтоИнтеркампани;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
			
		КонецЕсли;
	КонецЕсли;
	
	// Настройки отправки электронных документов по видам.
	Если Объект.ИспользуетсяДляОтправки
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам");
		ЭлементБлокировки.УстановитьЗначение("Отправитель", Объект.Организация);
		ЭлементБлокировки.УстановитьЗначение("Получатель", Объект.Контрагент);
		ЭлементБлокировки.УстановитьЗначение("Договор", Объект.ДоговорКонтрагента);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей();
		Набор.Отбор.Отправитель.Установить(Объект.Организация);
		Набор.Отбор.Получатель.Установить(Объект.Контрагент);
		Набор.Отбор.Договор.Установить(Объект.ДоговорКонтрагента);
		Набор.Прочитать();
		
		Если Не ЗначениеЗаполнено(Набор) Тогда
			
			Для каждого СтрокаНастроек Из Объект.ИсходящиеДокументы Цикл
				
				// Новые виды ЭД (УПД, УКД) пропускаем, т.к. их не было в старой архитектуре настроек
				// и они заполнены настройками по умолчанию.
				Если СтрокаНастроек.ИсходящийДокумент = Перечисления.ВидыЭД.УПД
					ИЛИ СтрокаНастроек.ИсходящийДокумент = Перечисления.ВидыЭД.УКД Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаОператора = СвойстваОператоров.Найти(СтрокаНастроек.ПрофильНастроекЭДО, "ПрофильНастроекЭДО");
				ИдентификаторОрганизации = ?(ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторОрганизации),
					СтрокаНастроек.ИдентификаторОрганизации, Объект.ИдентификаторОрганизации);
				ИдентификаторКонтрагента = ?(ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторКонтрагента),
					СтрокаНастроек.ИдентификаторКонтрагента, Объект.ИдентификаторКонтрагента);
					
				Если Не ЗначениеЗаполнено(ИдентификаторОрганизации)
					Или Не ЗначениеЗаполнено(ИдентификаторКонтрагента) Тогда
					Продолжить;
				КонецЕсли;
				
				Запись = Набор.Добавить();
				Запись.Отправитель = Объект.Организация;
				Запись.Получатель = Объект.Контрагент;
				Запись.Договор = Объект.ДоговорКонтрагента;
				Если Объект.ИспользоватьУПД И СтрокаНастроек.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура Тогда
					Запись.ВидДокумента = Перечисления.ВидыЭД.УПД;
				ИначеЕсли Объект.ИспользоватьУКД И СтрокаНастроек.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
					Запись.ВидДокумента = Перечисления.ВидыЭД.УКД;
				Иначе
					Запись.ВидДокумента = СтрокаНастроек.ИсходящийДокумент;
				КонецЕсли;
				Запись.ПрикладнойВидЭД = СтрокаНастроек.ПрикладнойВидЭД;
				
				ВидЭД = СтрокаНастроек.ИсходящийДокумент;
				ВерсияФормата = СтрокаНастроек.ВерсияФормата;
				Если ВидЭД = Перечисления.ВидыЭД.УПД И Объект.ИспользоватьУПД Тогда
					ВидЭД = Перечисления.ВидыЭД.СчетФактура;
					СтрокаСФ = Объект.ИсходящиеДокументы.Найти(ВидЭД, "ИсходящийДокумент");
					Если СтрокаСФ <> Неопределено Тогда
						ВерсияФормата = СтрокаСФ.ВерсияФормата;
					КонецЕсли;
				ИначеЕсли ВидЭД = Перечисления.ВидыЭД.УКД И Объект.ИспользоватьУКД Тогда
					ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
					СтрокаКСФ = Объект.ИсходящиеДокументы.Найти(ВидЭД, "ИсходящийДокумент");
					Если СтрокаКСФ <> Неопределено Тогда
						ВерсияФормата = СтрокаКСФ.ВерсияФормата;
					КонецЕсли;
				КонецЕсли;
				КлючФормата = Строка(ВидЭД) + "_" + ВерсияФормата;
				НовыйФормат = КонверторФорматов[КлючФормата];
				Если Не ЗначениеЗаполнено(НовыйФормат) Тогда
					НовыйФормат = СтрокаНастроек.ВерсияФормата;
				КонецЕсли;
				Запись.ВерсияФормата = НовыйФормат;
				
				Запись.СпособОбменаЭД = СтрокаНастроек.СпособОбменаЭД;
				Если Объект.ЭтоИнтеркампани Тогда
					Запись.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.Интеркампани;
				КонецЕсли;
				Запись.ИдентификаторОтправителя = ИдентификаторОрганизации;
				Запись.ИдентификаторПолучателя = ИдентификаторКонтрагента;
				Запись.МаршрутПодписания = СтрокаНастроек.МаршрутПодписания;
				Запись.ЗаполнениеКодаТовара = СтрокаНастроек.ЗаполнениеКодаТовара;
				Запись.ТребуетсяОтветнаяПодпись = СтрокаНастроек.ТребуетсяОтветнаяПодпись;
				Запись.ТребуетсяИзвещениеОПолучении = СтрокаНастроек.ТребуетсяИзвещениеОПолучении;
				Если СтрокаОператора <> Неопределено Тогда
					Запись.ВыгружатьДополнительныеСведения = СтрокаОператора.ВыгружатьДополнительныеСведения;
				КонецЕсли;
				Запись.Формировать = СтрокаНастроек.Формировать;
				Запись.ВерсияФорматаУстановленаВручную = Ложь;
				
			КонецЦикла;
			
			ТаблицаНабора = Набор.Выгрузить();
			
			НастройкиКопирования = Новый Соответствие;
			НастройкиКопирования.Вставить(Перечисления.ВидыЭД.УПД, Перечисления.ВидыЭД.СчетФактура);
			НастройкиКопирования.Вставить(Перечисления.ВидыЭД.УКД, Перечисления.ВидыЭД.КорректировочныйСчетФактура);
			НастройкиКопирования.Вставить(Перечисления.ВидыЭД.СчетФактура, Перечисления.ВидыЭД.УПД);
			НастройкиКопирования.Вставить(Перечисления.ВидыЭД.КорректировочныйСчетФактура, Перечисления.ВидыЭД.УКД);
			Для каждого Элемент Из НастройкиКопирования Цикл
				СтрокаИсточника = ТаблицаНабора.Найти(Элемент.Ключ, "ВидДокумента");
				СтрокаПриемника = ТаблицаНабора.Найти(Элемент.Значение, "ВидДокумента");
				Если СтрокаИсточника = Неопределено ИЛИ СтрокаПриемника <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				СтрокаПриемника = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПриемника, СтрокаИсточника);
				СтрокаПриемника.ВидДокумента = Элемент.Значение;
				Если СтрокаПриемника.ВидДокумента = Перечисления.ВидыЭД.СчетФактура
					ИЛИ СтрокаПриемника.ВидДокумента = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
					СтрокаПриемника.ТребуетсяОтветнаяПодпись = Ложь;
					СтрокаПриемника.ТребуетсяИзвещениеОПолучении = Ложь;
				ИначеЕсли СтрокаПриемника.ВидДокумента = Перечисления.ВидыЭД.УПД
					ИЛИ СтрокаПриемника.ВидДокумента = Перечисления.ВидыЭД.УКД Тогда
					СтрокаПриемника.ТребуетсяОтветнаяПодпись = Истина;
					СтрокаПриемника.ТребуетсяИзвещениеОПолучении = Истина;
				КонецЕсли;
			КонецЦикла;
			Набор.Загрузить(ТаблицаНабора);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
			
		КонецЕсли;
	КонецЕсли;
	
	// Состояния контрагентов БЭД.
	Если Не Объект.ЭтоИнтеркампани И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияКонтрагентовБЭД");
		ЭлементБлокировки.УстановитьЗначение("Контрагент", Объект.Контрагент);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.СостоянияКонтрагентовБЭД.СоздатьНаборЗаписей();
		Набор.Отбор.Контрагент.Установить(Объект.Контрагент);
		Набор.Прочитать();
		
		Если Не ЗначениеЗаполнено(Набор)
			ИЛИ Набор[0].Состояние <> Перечисления.СостоянияКонтрагентаБЭД.НастроенЭДО Тогда 
			
			Набор.Очистить();
			Состояние = Набор.Добавить();
			Состояние.Контрагент = Объект.Контрагент;
			Состояние.Состояние = Перечисления.СостоянияКонтрагентаБЭД.НастроенЭДО;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		КонецЕсли;
	КонецЕсли;
	
	// Настройки получения электронных документов.
	Если Не Объект.ЭтоИнтеркампани
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиПолученияЭлектронныхДокументов");
		ЭлементБлокировки.УстановитьЗначение("Получатель", Объект.Организация);
		ЭлементБлокировки.УстановитьЗначение("Отправитель", Объект.Контрагент);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		// Общие настройки получения.
		Набор = РегистрыСведений.НастройкиПолученияЭлектронныхДокументов.СоздатьНаборЗаписей();
		Набор.Отбор.Получатель.Установить(Объект.Организация);
		Набор.Отбор.Отправитель.Установить(Объект.Контрагент);
		Набор.Отбор.ИдентификаторОтправителя.Установить("");
		Набор.Отбор.ИдентификаторПолучателя.Установить("");
		Набор.Прочитать();
		
		Если Не ЗначениеЗаполнено(Набор) Тогда
			
			ТаблицаНастроекПолучения = Набор.Выгрузить();
			
			ТекущаяТаблица = ОбменСКонтрагентамиСлужебный.ТаблицаПредопределенногоПрофиля("ПервоначальноеЗаполнение");
			ТекущаяТаблица.Колонки.Добавить("Отправитель"              , Метаданные.ОпределяемыеТипы.УчастникЭДО.Тип);
			ТекущаяТаблица.Колонки.Добавить("Получатель"               , Метаданные.ОпределяемыеТипы.Организация.Тип);
			ТекущаяТаблица.Колонки.Добавить("ИдентификаторОтправителя" , Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100)));
			ТекущаяТаблица.Колонки.Добавить("ИдентификаторПолучателя"  , Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100)));
			
			ТекущаяТаблица.ЗаполнитьЗначения(Объект.Контрагент  , "Отправитель");
			ТекущаяТаблица.ЗаполнитьЗначения(Объект.Организация , "Получатель");
			ТекущаяТаблица.ЗаполнитьЗначения(""                 , "ИдентификаторОтправителя");
			ТекущаяТаблица.ЗаполнитьЗначения(""                 , "ИдентификаторПолучателя");
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТекущаяТаблица, ТаблицаНастроекПолучения);
			
			Для каждого СтрокаНастроек Из ТаблицаНастроекПолучения Цикл
				ОтборСтрок = Новый Структура("Организация,Контрагент,ВходящийДокумент",
					СтрокаНастроек.Получатель, СтрокаНастроек.Отправитель, СтрокаНастроек.ВидДокумента);
				НайденныеСтроки = НастройкиПолученияВручную.НайтиСтроки(ОтборСтрок);
				Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
					СтрокаНастроек.СпособОбработки = "Вручную";
				КонецЕсли;
			КонецЦикла;
			
			Набор.Загрузить(ТаблицаНастроекПолучения);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		
		КонецЕсли;
		
	КонецЕсли;
	
	// Приглашения к обмену электронными документами.
	Если Не Объект.ЭтоИнтеркампани Тогда
		Настройки = Объект.ИсходящиеДокументы.Выгрузить(, "ИдентификаторОрганизации, ИдентификаторКонтрагента");
		Настройки.Свернуть("ИдентификаторОрганизации, ИдентификаторКонтрагента");
		Для каждого СтрокаНастроек Из Настройки Цикл
			
			Если Не (ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторОрганизации)
				И ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторКонтрагента)) Тогда
				Продолжить;
			КонецЕсли;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами");
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторОрганизации", СтрокаНастроек.ИдентификаторОрганизации);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторКонтрагента", СтрокаНастроек.ИдентификаторКонтрагента);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Набор = РегистрыСведений.ПриглашенияКОбменуЭлектроннымиДокументами.СоздатьНаборЗаписей();
			Набор.Отбор.ИдентификаторОрганизации.Установить(СтрокаНастроек.ИдентификаторОрганизации);
			Набор.Отбор.ИдентификаторКонтрагента.Установить(СтрокаНастроек.ИдентификаторКонтрагента);
			Набор.Прочитать();
			
			Если Не ЗначениеЗаполнено(Набор) Тогда
				
				Запись = Набор.Добавить();
				Запись.ИдентификаторОрганизации = СтрокаНастроек.ИдентификаторОрганизации;
				Запись.ИдентификаторКонтрагента = СтрокаНастроек.ИдентификаторКонтрагента;
				Запись.Контрагент = Объект.Контрагент;
				Запись.Статус = Объект.СтатусПодключения;
				Запись.ТекстПриглашения = Объект.ТекстПриглашения;
				Запись.ИНН = Объект.ИННКонтрагента;
				Запись.КПП = Объект.КППКонтрагента;
				Запись.Наименование = Объект.НаименованиеКонтрагента;
				
				Если Не ЗначениеЗаполнено(Запись.Контрагент)
					И Запись.Статус = Перечисления.СтатусыПриглашений.Принято Тогда
					Запись.Статус = Перечисления.СтатусыПриглашений.ТребуетсяСогласие;
				КонецЕсли;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыНастроекЭДО(Знач НаборНастроекЭДО)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументИсходящий.УдалитьНастройкаЭДО КАК НастройкаЭДО,
	|	МИНИМУМ(ЭлектронныйДокументИсходящий.Дата) КАК ДатаНачалаДействия
	|ИЗ
	|	Документ.ЭлектронныйДокументИсходящий КАК ЭлектронныйДокументИсходящий
	|ГДЕ
	|	ЭлектронныйДокументИсходящий.УдалитьНастройкаЭДО В(&НаборНастроекЭДО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭлектронныйДокументИсходящий.УдалитьНастройкаЭДО";
	Запрос.УстановитьПараметр("НаборНастроекЭДО", НаборНастроекЭДО);
	
	Реквизиты = Запрос.Выполнить().Выгрузить();
	Реквизиты.Индексы.Добавить("НастройкаЭДО");
	
	Возврат Реквизиты;
	
КонецФункции

Функция СвойстваОператоровПрофилейНастроекЭДО()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдалитьПрофилиНастроекЭДО.Ссылка КАК ПрофильНастроекЭДО,
	|	ЕСТЬNULL(ОператорыЭДО.ОтправлятьДополнительныеСведения, ЛОЖЬ) КАК ВыгружатьДополнительныеСведения,
	|	ЕСТЬNULL(ОператорыЭДО.ИдентификаторОператора, """") КАК ИдентификаторОператора,
	|	УдалитьПрофилиНастроекЭДО.Наименование КАК НаименованиеПрофиляЭДО,
	|	УдалитьПрофилиНастроекЭДО.НазначениеУчетнойЗаписи КАК НазначениеУчетнойЗаписи,
	|	УдалитьПрофилиНастроекЭДО.ПодробноеОписаниеУчетнойЗаписи КАК ПодробноеОписаниеУчетнойЗаписи,
	|	УдалитьПрофилиНастроекЭДО.ПринятыУсловияИспользования КАК ПринятыУсловияИспользования
	|ИЗ
	|	Справочник.УдалитьПрофилиНастроекЭДО КАК УдалитьПрофилиНастроекЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОператорыЭДО КАК ОператорыЭДО
	|		ПО ((ВЫРАЗИТЬ(УдалитьПрофилиНастроекЭДО.ОператорЭДОИд КАК СТРОКА(10))) = ОператорыЭДО.ИдентификаторОператора
	|				ИЛИ УдалитьПрофилиНастроекЭДО.СпособОбменаЭД = &ЧерезТакском
	|					И ОператорыЭДО.СпособОбменаЭД = &ЧерезТакском)";
	Запрос.УстановитьПараметр("ЧерезТакском", Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском);
	
	СвойстваОператоров = Запрос.Выполнить().Выгрузить();
	СвойстваОператоров.Индексы.Добавить("ПрофильНастроекЭДО");
	
	Возврат СвойстваОператоров;
	
КонецФункции

Функция КонверторФорматовЭД()
	
	СведенияОФорматах = ОбменСКонтрагентамиПовтИсп.СведенияОФорматахЭлектронныхДокументовИзМакета();
	
	Конвертор = Новый Соответствие;
	
	Для каждого СтрокаСведений Из СведенияОФорматах Цикл
		
		Если СтрокаСведений.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.ПрикладнойЭД Тогда
			Конвертор.Вставить(СтрокаСведений.ИдентификаторФормата, СтрокаСведений.ИдентификаторФормата);
		Иначе
			Ключ = Строка(СтрокаСведений.ВидЭлектронногоДокумента) + "_" + СтрокаСведений.УдалитьИдентификаторФормата;
			Конвертор.Вставить(Ключ, СтрокаСведений.ИдентификаторФормата);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Конвертор;
	
КонецФункции

Функция НастройкиДляОтправкиЭД()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭД.Ссылка КАК НастройкаЭДО,
	|	СоглашенияОбИспользованииЭД.Организация КАК Организация,
	|	СоглашенияОбИспользованииЭД.Контрагент КАК Контрагент,
	|	СоглашенияОбИспользованииЭД.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД КАК СоглашенияОбИспользованииЭД
	|ГДЕ
	|	СоглашенияОбИспользованииЭД.ИспользуетсяДляОтправки
	|	И СоглашенияОбИспользованииЭД.СтатусПодключения <> ЗНАЧЕНИЕ(Перечисление.СтатусыПриглашений.Отклонено)";
	
	Настройки = Запрос.Выполнить().Выгрузить();
	Настройки.Индексы.Добавить("Организация,Контрагент,ДоговорКонтрагента");
	
	Возврат Настройки;
	
КонецФункции

Функция НастройкиПолученияВручную()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтарыеНастройкиПолучения.Ссылка.Организация КАК Организация,
	|	СтарыеНастройкиПолучения.Ссылка.Контрагент КАК Контрагент,
	|	СтарыеНастройкиПолучения.ВходящийДокумент КАК ВходящийДокумент
	|ИЗ
	|	Справочник.УдалитьСоглашенияОбИспользованииЭД.ВходящиеДокументы КАК СтарыеНастройкиПолучения
	|ГДЕ
	|	СтарыеНастройкиПолучения.СпособОбработки = ""Вручную""
	|	И СтарыеНастройкиПолучения.Ссылка.СтатусПодключения <> ЗНАЧЕНИЕ(Перечисление.СтатусыПриглашений.Отклонено)";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Таблица.Индексы.Добавить("Организация,Контрагент,ВходящийДокумент");
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти


#КонецОбласти

#КонецЕсли