#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьТиповыеУсловия() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеУсловия.Ссылка
	|ИЗ
	|	Справочник.ДополнительныеУсловия КАК ДополнительныеУсловия
	|ГДЕ
	|	ДополнительныеУсловия.ИмяМакета = ""ТиповыеУсловия""";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТиповыеУсловия = Выборка.Ссылка;
	Иначе
		ТиповыеУсловия = Справочники.ДополнительныеУсловия.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ТиповыеУсловия;
	
КонецФункции

Процедура ЗаполнениеТиповыхДополнительныхУсловий() Экспорт

	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда // В подчиненных узлах РИБ не выполняется, чтобы не было дублей
		Возврат;
	КонецЕсли;
	
	ТиповыеМакеты = Новый Структура;
	МетаданныеМакеты = Метаданные.Справочники.ДополнительныеУсловия.Макеты;
	Для Каждого МетаМакета Из МетаданныеМакеты Цикл
		ТиповыеМакеты.Вставить(МетаМакета.Имя, МетаМакета);
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеУсловия.ИмяМакета
	|ИЗ
	|	Справочник.ДополнительныеУсловия КАК ДополнительныеУсловия
	|ГДЕ
	|	ДополнительныеУсловия.ИмяМакета <> """"";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Если ТиповыеМакеты.Свойство(Выборка.ИмяМакета) Тогда
			// Второй раз не добавляем
			ТиповыеМакеты.Удалить(Выборка.ИмяМакета);
		КонецЕсли;
	
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ТиповыеМакеты Цикл
	
		Попытка
		
			ДополнительныеУсловияОбъект = Справочники.ДополнительныеУсловия.СоздатьЭлемент();
			ДополнительныеУсловияОбъект.Наименование 	= КлючИЗначение.Значение.Синоним;
			ДополнительныеУсловияОбъект.ИмяМакета		= КлючИЗначение.Ключ;
			
			Макет = Справочники.ДополнительныеУсловия.ПолучитьМакет(КлючИЗначение.Ключ);
			ДополнительныеУсловияОбъект.ТекстУсловий = Макет.ПолучитьТекст();
			
			ДополнительныеУсловияОбъект.Записать();
			
		Исключение
	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось создать дополнительные условия: %1 по причине:
					|%2'"),
					КлючИЗначение.Ключ, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.ДополнительныеУсловия, ТекстСообщения);
		
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнениеТиповыхДополнительныхУсловийВОрганизации() Экспорт
	
	ТиповыеУсловия = ПолучитьТиповыеУсловия();
	Если НЕ ЗначениеЗаполнено(ТиповыеУсловия) Тогда
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
			НСтр("ru = 'Не удалось выполнить процедуру ДополнительныеУсловия.ЗаполнениеТиповыхДополнительныхУсловийВОрганизации
			|из-за того, что не созданы типовые условия'"), );
	Иначе
		// Заполним типовыми условиями Организации
		ЗапросПоОрганизациям = Новый Запрос;
		ЗапросПоОрганизациям.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ДополнительныеУсловияПоУмолчанию = ЗНАЧЕНИЕ(Справочник.ДополнительныеУсловия.ПустаяСсылка)";
		ВыборкаПоОрганизациям = ЗапросПоОрганизациям.Выполнить().Выбрать();
		
		Пока ВыборкаПоОрганизациям.Следующий() Цикл
			Попытка
				ОрганизацияОбъект = ВыборкаПоОрганизациям.Ссылка.ПолучитьОбъект();
				ОрганизацияОбъект.ДополнительныеУсловияПоУмолчанию = ТиповыеУсловия;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОрганизацияОбъект);
			Исключение
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось заполнить ДополнительныеПараметры условия организации ""%1"" по причине:
					|%2'"), 
					ВыборкаПоОрганизациям.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,, 
					ВыборкаПоОрганизациям.Ссылка, ТекстСообщения);
			КонецПопытки
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнениеТиповыхДополнительныхУсловийВСчетахНаОплатуПокупателю(Параметры) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда // В подчиненных узлах РИБ не выполняется, чтобы не было дублей
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если Параметры.Свойство("ТиповыеУсловия") Тогда
		ТиповыеУсловия = Параметры.ТиповыеУсловия;
	Иначе
		ТиповыеУсловия = ПолучитьТиповыеУсловия();
		Параметры.Вставить("ТиповыеУсловия", ТиповыеУсловия);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ТиповыеУсловия) Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
			НСтр("ru = 'Не удалось выполнить процедуру ДополнительныеУсловия.ЗаполнениеТиповыхДополнительныхУсловийВСчетахНаОплатуПокупателю
			|из-за того, что не созданы типовые условия'"), );
	Иначе
		// Заполним типовыми условиями
		ЗапросПоДокументам = Новый Запрос;
		ЗапросПоДокументам.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	СчетНаОплатуПокупателю.Ссылка
		|ИЗ
		|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
		|ГДЕ
		|	СчетНаОплатуПокупателю.ДополнительныеУсловия = ЗНАЧЕНИЕ(Справочник.ДополнительныеУсловия.ПустаяСсылка)";
		
		ПроблемныхОбъектов = 0;
		ОбъектовОбработано = 0;
		
		ВыборкаПоДокументам = ЗапросПоДокументам.Выполнить().Выбрать();
		Пока ВыборкаПоДокументам.Следующий() Цикл
			Попытка
				СчетНаОплатуОбъект = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
				СчетНаОплатуОбъект.ДополнительныеУсловия = ТиповыеУсловия;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СчетНаОплатуОбъект);
				ОбъектовОбработано = ОбъектовОбработано + 1;
			Исключение
				// Если не удалось обработать какой-либо документ, повторяем попытку снова.
				ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось заполнить ДополнительныеПараметры условия счета на оплату ""%1"" по причине:
					|%2'"), 
					ВыборкаПоДокументам.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,, 
					ВыборкаПоДокументам.Ссылка, ТекстСообщения);
			КонецПопытки
		КонецЦикла;
		
		Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
			Параметры.ОбработкаЗавершена = Истина;
		Иначе
			Параметры.ОбработкаЗавершена = Ложь;
			Если ОбъектовОбработано = 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедуре ДополнительныеУсловия.ЗаполнениеТиповыхДополнительныхУсловий
					|не удалось заполнить дополнительные условия в %1 документах Счет покупателю'"), 
					ПроблемныхОбъектов);
				ВызватьИсключение ТекстСообщения;
			Иначе
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедура ДополнительныеУсловия.ЗаполнениеТиповыхДополнительныхУсловий
					|обработала очередную порцию документов Счет покупателю: %1'"), 
					ОбъектовОбработано));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
