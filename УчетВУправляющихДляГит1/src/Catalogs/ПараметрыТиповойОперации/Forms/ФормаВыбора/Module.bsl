#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ТипПараметра) Тогда
		ТипПараметра = Параметры.ТипПараметра;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Владелец) Тогда
		Владелец = Параметры.Владелец;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"Владелец",
			Владелец,
			,
			,
			Истина);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТипПараметра) И ЗначениеЗаполнено(Владелец) Тогда
		УстановитьОтборПоВладельцуИТипуПараметра();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоВладельцуИТипуПараметра()

	// Получим все параметры с данным типом
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПараметрыТиповойОперации.Ссылка,
		|	ПараметрыТиповойОперации.ОписаниеТипаРеквизита
		|ИЗ
		|	Справочник.ПараметрыТиповойОперации КАК ПараметрыТиповойОперации
		|ГДЕ
		|	ПараметрыТиповойОперации.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивЭлементов = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		// Получим тип объекта
		ТипОбъекта = Выборка.ОписаниеТипаРеквизита.Получить();
		// Сравним с требуемым типом
		// Если не составной тип, то проверим входит ли он в требуемый тип
		Если ТипОбъекта.Типы().Количество() = 1 Тогда
			Если ТипПараметра.СодержитТип(ТипОбъекта.Типы()[0]) Тогда
				МассивЭлементов.Добавить(Выборка.Ссылка);
			КонецЕсли;
		// если составной, то проверяем на полное соответствие
		ИначеЕсли ТипОбъекта = ТипПараметра Тогда
			МассивЭлементов.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	// Установим отбор по полученным элементам
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
		"Ссылка",
		МассивЭлементов,
		ВидСравненияКомпоновкиДанных.ВСписке
		,"Отбор по типу данных"
		,
		,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);

	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтборПоТипуПараметра(Результат, Параметры) Экспорт
	
	// Заново сформируем отбор по типу параметра
	Если ЗначениеЗаполнено(Параметры.ТипПараметра) И ЗначениеЗаполнено(Параметры.Владелец) Тогда
		УстановитьОтборПоВладельцуИТипуПараметра();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ			 = Истина;
	ПараметрыФормы	 = Новый Структура;
	Если ЗначениеЗаполнено(Владелец) Тогда
		ПараметрыФормы.Вставить("Владелец", Владелец);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТипПараметра) Тогда
		ПараметрыФормы.Вставить("ТипПараметра", ТипПараметра);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьОтборПоТипуПараметра", ЭтотОбъект, ПараметрыФормы);
	ОткрытьФорму("Справочник.ПараметрыТиповойОперации.Форма.ФормаЭлемента", ПараметрыФормы, , , , ,ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти
