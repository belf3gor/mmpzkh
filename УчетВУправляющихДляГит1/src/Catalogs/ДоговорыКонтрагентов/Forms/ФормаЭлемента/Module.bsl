#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	СрокОплатыПокупателей = Константы.СрокОплатыПокупателей.Получить();
	СрокОплатыПоставщикам = Константы.СрокОплатыПоставщикам.Получить();
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ДоговорСРезидентомТаможенногоСоюза = УчетНДС.КонтрагентРезидентТаможенногоСоюза(Объект.Владелец);
	
	Параметры.Свойство("СозданИзФормыДокумента", СозданИзФормыДокумента);
	
	Если Параметры.Свойство("ОткрытИзПлатежки") Тогда
		Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком;
	КонецЕсли;
	
	Если Параметры.ПараметрыВыбора.Свойство("Организация") И ЗначениеЗаполнено(Параметры.ПараметрыВыбора.Организация) Тогда
		Параметры.ПараметрыВыбора.Организация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(
			Параметры.ПараметрыВыбора.Организация);
	КонецЕсли;

	Если НЕ Параметры.Ключ.Пустая() Тогда
		Справочники.ДоговорыКонтрагентов.УстановитьДоступныеВидыДоговора(Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	ОграничитьВыборРеквизитов(Параметры.ЗначенияЗаполнения);
	
	ОплатаВРублях = Объект.РасчетыВУсловныхЕдиницах;

	ЗаполнитьСписокСпособовРасчетаКомиссионногоВознаграждения(ЭтотОбъект);
	
	НаименованиеПоУмолчанию  = ПечатьДоговоровКлиентСервер.НаименованиеПоУмолчаниюБезРеквизитов();
	РеквизитыДоговораСтрокой = ПечатьДоговоровКлиентСервер.РеквизитыДоговораСтрокой(Объект.Номер, Объект.Дата);
	Элементы.НаименованиеДляСчетаФактурыНалоговогоАгента.Видимость = ПолучитьФункциональнуюОпцию("ИсполняютсяОбязанностиНалоговогоАгентаПоНДС")
		ИЛИ БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	Элементы.ТипЦен.Видимость = ПравоДоступа("Чтение", Метаданные.Справочники.ТипыЦенНоменклатуры);
	
	РаздельныйУчетНДС = УчетНДСРаздельный.ЕстьУчетнаяПолитикаСРаздельнымУчетомНДС();
	
	ПредусмотреноОбеспечениеОбязательств = ЗначениеЗаполнено(Объект.ВидОбеспеченияОбязательствИПлатежей);
	ОбеспечениеПредоставил = Объект.ОбеспечениеПредоставил;
	ОбеспечениеПолучил     = Объект.ОбеспечениеПолучил;
	ОбеспечениеВыданоЗа    = Объект.ОбеспечениеВыданоЗа;
	УстановитьСпискиЭлементовОбеспеченияОбязательств();
	
	УстановитьФункциональныеОпцииФормы();
	
	УправлениеФормой(ЭтотОбъект);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Справочник.ДоговорыКонтрагентов",
		"ФормаЭлемента",
		НСтр("ru='Новости: Договор'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
	// На форме размещены 4 реквизита, чьи представления склоняются по падежам.
	// Основным режимом работы со склонениями Руководитель и ДолжностьРуководителя является их изменение в форме элемента
	// соответствующего справочника. Но возможен переход к их склонениям и из текущей формы.
	// Работа с реквизитами РуководительКонтрагента и ДолжностьРуководителяКонтрагента ведётся с данной форме
	// с использованием подсистемы БСП "СклонениеПредставленийОбъектов". Данные склонений реквизитов хранятся в реквизитах
	// СклоненияРуководительКонтрагента и СклоненияДолжностьРуководителяКонтрагента. Имя реквизита формы, с которым сейчас
	// ведётся работа с использованием подсистемы БСП, хранится в переменной ТекущийРеквизитСклонения. Данные из текущего
	// реквизита синхронизируются с реквизитом формы Склонения, относящимся к подсистеме БСП.
	
	СклонениеПредставленийОбъектов.ПриСозданииНаСервере(ЭтотОбъект, Объект.РуководительКонтрагента);
	ТекущийРеквизитСклонения = "СклоненияРуководительКонтрагента";
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	
	Если ЗначениеЗаполнено(Объект.ДолжностьРуководителяКонтрагента) Тогда
		
		СтруктураСклонения = СклонениеПредставленийОбъектовКлиентСервер.СтруктураСклонения();
		СклонениеПредставленийОбъектовВызовСервера.ПросклонятьПредставлениеПоВсемПадежам(
			Объект.ДолжностьРуководителяКонтрагента, СтруктураСклонения);
		ЭтотОбъект["СклоненияДолжностьРуководителяКонтрагента"] = СтруктураСклонения;
		
	КонецЕсли;
	
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

	НастройкиУчетаМСФО.НастроитьФормуОбъектаУчета(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// Квартплата +
	ОбновитьЭлементыУПЖКХ();
	// Квартплата -
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ДоговорыКонтрагентов_СозданиеФайлаДоговора" 
		И Параметр.ДоговорКонтрагента = Объект.Ссылка Тогда 
		// Для текущего договора был добавлен присоединенный файл с текстом договора.
		// Сохраним ссылку на него в текущем объекте.
		ОбработкаОповещенияСозданиеФайлаДоговора(Параметр.ФайлДоговора);
	КонецЕсли;

	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	ПриЗаписиНаСервереСклоненийРеквизитов(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ДоговорыКонтрагентов", ПараметрыЗаписи, Объект.Ссылка);
	
	Если СозданИзФормыДокумента Тогда
		ОповеститьОВыборе(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	СформироватьНаименованиеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	СформироватьНаименованиеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		ВладелецПриИзмененииНаСервере();
		// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
		СклонениеПредставленийОбъектовКлиент.ПриИзмененииПредставления(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
		ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента = Истина;
		ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора)
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее")) Тогда
		Объект.ТипЦен = Неопределено;
		Объект.СпособРасчетаКомиссионногоВознаграждения = Неопределено;
		Объект.ПроцентКомиссионногоВознаграждения = 0;
		Объект.УстановленСрокОплаты = Ложь;
		Объект.СрокОплаты = 0;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора)
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией")) Тогда
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
		ОплатаВРублях = 0;
	КонецЕсли;
	
	ЭтоДоговорПриобретения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
	
	Если НЕ ЭтоДоговорПриобретения Тогда
		Объект.УчетАгентскогоНДС       = Ложь;
		Объект.НДСПоСтавкам4и2         = Ложь;
		Объект.ВидАгентскогоДоговора   = Неопределено;
		Если Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
			Объект.КодРаздел7ДекларацииНДС   = Неопределено;
			Раздел7ДекларацииНДСНаименование = "";
		КонецЕсли;
	Иначе
		Объект.КодРаздел7ДекларацииНДС   = Неопределено;
		Раздел7ДекларацииНДСНаименование = "";
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")
			И Не ИсполняютсяОбязанностиНалоговогоАгентаПоНДС Тогда
			
			Объект.УчетАгентскогоНДС     = Ложь;
			Объект.ВидАгентскогоДоговора = Неопределено;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.УчетАгентскогоНДС
			И (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")) Тогда
		Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Нерезидент");
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку") Тогда
		Если НЕ УсловиеОплатаВВалютеПередано Тогда
			Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией") Тогда
		Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
	КонецЕсли;
	
	ЗаполнитьСписокСпособовРасчетаКомиссионногоВознаграждения(ЭтотОбъект);
	Если ЗначениеЗаполнено(Объект.СпособРасчетаКомиссионногоВознаграждения) Тогда
		Если Элементы.СпособРасчетаКомиссионногоВознаграждения.СписокВыбора.НайтиПоЗначению(Объект.СпособРасчетаКомиссионногоВознаграждения) = Неопределено Тогда
			Объект.СпособРасчетаКомиссионногоВознаграждения = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
		
		Объект.ПлатежныйАгент = Ложь;
		
		ПлатежныйАгентПриИзменении(Элементы.ПлатежныйАгент);
	КонецЕсли;
	
	ЭтоДоговорКомиссии     = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
		
	Если ЭтоДоговорКомиссии 
		ИЛИ НЕ ЗначениеЗаполнено(Объект.ВидДоговора) Тогда
		Объект.ГосударственныйКонтракт = Неопределено;
	КонецЕсли;
	
	Если Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку") Тогда
		Объект.СчетаФактурыОтИмениОрганизации = Ложь;
	КонецЕсли;
	
	СпособЗаполненияСтавкиНДСПоУмолчанию();
	// Квартплата +
	ОбновитьЭлементыУПЖКХ();
	// Квартплата -
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаВзаиморасчетовПриИзменении(Элемент)
	
	ВалютаВзаиморасчетовПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаВПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Объект.РасчетыВУсловныхЕдиницах = ОплатаВРублях;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановленСрокОплатыПриИзменении(Элемент)

	Если Объект.УстановленСрокОплаты Тогда
		Если (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")) Тогда
			Объект.СрокОплаты = СрокОплатыПокупателей;
		ИначеЕсли (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку")) Тогда
			Объект.СрокОплаты = СрокОплатыПоставщикам;
		КонецЕсли;
	Иначе
		СрокОплаты = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УчетАгентскогоНДСПриИзменении(Элемент)
	
	Если Объект.УчетАгентскогоНДС Тогда
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом") 
			ИЛИ ЭтоДоговорИностранца Тогда
			Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Нерезидент");
		Иначе
			Объект.ВидАгентскогоДоговора = ?(
				ИсполняютсяОбязанностиНалоговогоАгентаПоНДС,
				ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Аренда"),
				ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров"));
		КонецЕсли;
	Иначе
		Объект.ВидАгентскогоДоговора = Неопределено;
	КонецЕсли;
	
	СпособЗаполненияСтавкиНДСПоУмолчанию();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектронныеУслугиПриИзменении(Элемент)
	
	// Очистим поля связанные с настройкой по налоговому агенту.
	Объект.УчетАгентскогоНДС = Ложь;
	Объект.ВидАгентскогоДоговора = Неопределено;
	Объект.НаименованиеДляСчетаФактурыНаАванс = ""; // используется для счетов-фактур налогового агента
	
	Если ЭлектронныеУслугиИностранца Тогда
		ПорядокУплатыНДСПоЭлектроннымУслугам = 1; // НДС платит иностранец
		Объект.ЭлектронныеУслуги = Истина;
	Иначе
		Объект.ЭлектронныеУслуги = Ложь;
	КонецЕсли;
	
	СпособЗаполненияСтавкиНДСПоУмолчанию();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУплатыНДСПоЭлектроннымУслугамПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Объект.ЭлектронныеУслуги = ПорядокУплатыНДСПоЭлектроннымУслугам = 1; // НДС платит иностранец
	Если ПорядокУплатыНДСПоЭлектроннымУслугам = 2 Тогда // НДС платит организация как налоговый агент
		Объект.УчетАгентскогоНДС = Истина;
		Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги");
	Иначе
		// Очистим поля связанные с настройкой по налоговому агенту.
		Объект.УчетАгентскогоНДС = Ложь;
		Объект.ВидАгентскогоДоговора = Неопределено;
		Объект.НаименованиеДляСчетаФактурыНаАванс = ""; // используется для счетов-фактур налогового агента
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УчетАгентскогоНДСПокупателемПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокРегистрацииСчетовФактурНаАвансПоДоговоруПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАгентскогоДоговораПриИзменении(Элемент)
	
	Если Объект.ВидАгентскогоДоговора = 
		ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров") Тогда 
		
		Если Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета
			И НЕ Объект.РасчетыВУсловныхЕдиницах Тогда
			Объект.РасчетыВУсловныхЕдиницах = Истина;
			ОплатаВРублях = 1; 
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаФактурыОтИмениОрганизацииПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РуководительКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Список = ПолучитьСписокКонтактныхЛиц(Объект.Владелец);
	
	ОповещениеВыбора = Новый ОписаниеОповещения("РуководительКонтрагентаНачалоВыбораЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеВыбора, Список, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура РуководительКонтрагентаПриИзменении(Элемент)
	
	ПриИзмененииПредставленияРуководителяКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяКонтрагентаПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	СклонениеПредставленийОбъектовКлиент.ПриИзмененииПредставления(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлДоговораОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ФайлДоговора) Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ФайлДоговора", Объект.ФайлДоговора);
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлДоговораСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Модифицированность Тогда
		// Выполняем запись договора перед открытием формы редактирования файла с текстом договора,
		// аналогично тому как происходит при нажатии на кнопку Печать.
		Записать();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	Если ЗначениеЗаполнено(Объект.ФайлДоговора) Тогда
		// Открываем существующий файл договора вместо создания нового.
		ПараметрыФормы.Вставить("ФайлДоговора", Объект.ФайлДоговора);
	Иначе
		ПараметрыФормы.Вставить("ОбъектПечати", Объект.Ссылка);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЭтоГосударственныйКонтрактПриИзменении(Элемент)
	
	Если НЕ ЭтоГосударственныйКонтракт Тогда
		Объект.ГосударственныйКонтракт = Неопределено;
	КонецЕсли;
	
	Элементы.ГосударственныйКонтракт.Доступность = ЭтоГосударственныйКонтракт;
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакАгентаПриИзменении(Элемент)
	ОчиститьПризнакиАгента();
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныйАгентПриИзменении(Элемент)
	
	ОчиститьПризнакиАгента();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастройкаМСФОПриИзменении(Элемент)
	
	НастройкиУчетаМСФОКлиент.ПриИзмененииПоляФормы(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредусмотреноОбеспечениеОбязательствПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Если НЕ ПредусмотреноОбеспечениеОбязательств Тогда
		Объект.ВидОбеспеченияОбязательствИПлатежей = Неопределено;
		Объект.ОбеспечениеПредоставил              = Неопределено;
		Объект.ОбеспечениеПолучил                  = Неопределено;
		Объект.ОбеспечениеВыданоЗа                 = Неопределено;
		
		ОбеспечениеПредоставил = "";
		ОбеспечениеПолучил     = "";
		ОбеспечениеВыданоЗа    = "";
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбеспечениеСторонаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		
		ДополнительныеПараметры = Новый Структура("ИмяЭлемента", Элемент.Имя);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОткрытьФормуВыбораКонтрагентаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", , ЭтотОбъект, , , , ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		Объект[Элемент.Имя] = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	ВалютаРегламентированногоУчета = Форма.ВалютаРегламентированногоУчета;

	ЭтоДоговорКомиссии     = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
		
	ЭтоДоговорПриобретения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"));
		
	ЭтоДоговорРеализации   = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
	
	ЭтоДоговорФакторинга = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией"));
	
	ЭтоПрочийДоговор = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее");
	
	ЭтоПлательщикНДС = Форма.ЭтоПлательщикНДС;
	
	ВидимостьКомиссионногоВознаграждения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
	
	//Группа "Расчеты"
	
	Элементы.ОплатаВРублях.Видимость = (ЭтоДоговорРеализации И Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")
			ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		И (Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета)
		И ЗначениеЗаполнено(Объект.ВалютаВзаиморасчетов)
		И Объект.ВидАгентскогоДоговора <> ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров");
		
	ЗначениеВыбора = Элементы.ОплатаВРублях.СписокВыбора.НайтиПоЗначению(0);
	ЗначениеВыбора.Представление = Объект.ВалютаВзаиморасчетов;
	
	Если Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
		ТекстЗаголовкаРасчеты = НСтр("ru= 'Расчеты'");
	ИначеЕсли Объект.РасчетыВУсловныхЕдиницах Тогда
		ТекстЗаголовкаРасчеты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru= 'Расчеты: %1 (оплата в руб.)'"), Объект.ВалютаВзаиморасчетов);
	Иначе
		ТекстЗаголовкаРасчеты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru= 'Расчеты: %1'"), Объект.ВалютаВзаиморасчетов);
	КонецЕсли;
	
	УстановитьЗаголовокГруппы(Форма, "ГруппаРасчеты", ТекстЗаголовкаРасчеты);
	
	Если Элементы.ОплатаВРублях.Видимость Тогда
		Элементы.ОплатаВРублях.ТолькоПросмотр = Форма.УсловиеОплатаВВалютеПередано;
	КонецЕсли;
	
	Элементы.ГруппаВалютаВзаиморасчетов.Видимость = НЕ ЭтоДоговорФакторинга;
	
	Элементы.ГруппаСрокОплаты.Видимость = Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее");
	
	Элементы.СрокОплаты.Видимость = Объект.УстановленСрокОплаты;
	
	//Группа "Комиссионное вознаграждение"
	
	Элементы.ГруппаКомиссионноеВознаграждение.Видимость = ВидимостьКомиссионногоВознаграждения;
	
	//Группа "НДС"
	
	Если ЭтоДоговорРеализации Тогда
		Элементы.ГруппаНалоговыйАгентОрганизация.Видимость = Ложь;
		Элементы.ГруппаЭлектронныеУслуги.Видимость         = Ложь;
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда 
			Элементы.ГруппаНалоговыйАгентПокупатель.Видимость = Истина;
		Иначе
			Элементы.ГруппаНалоговыйАгентПокупатель.Видимость = Ложь;
		КонецЕсли;
		Элементы.ГруппаНДССАвансов.Видимость = ЭтоПлательщикНДС;
		Элементы.ГруппаЗаполнениеСтавкиНДС.Видимость = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером");
			
		Если Объект.УчетАгентскогоНДСПокупателем Тогда 
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Покупатель выступает в качестве налогового агента по уплате НДС'");
		ИначеЕсли ЭтоПлательщикНДС Тогда
			Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")
				И Объект.СчетаФактурыОтИмениОрганизации Тогда 
				ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Счета-фактуры перевыставляются от имени организации'");
			ИначеЕсли ЗначениеЗаполнено(Объект.ПорядокРегистрацииСчетовФактурНаАвансПоДоговору) Тогда
				ТекстЗаголовкаНДС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'НДС: %1'"), Строка(Объект.ПорядокРегистрацииСчетовФактурНаАвансПоДоговору));
			Иначе 
				ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Регистрировать счета-фактуры на аванс в порядке, соответствующем учетной политике'");
			КонецЕсли;
		Иначе
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС'");
		КонецЕсли;
		
		Элементы.КодДекларацияРаздел7.Видимость = 
			ЭтоПлательщикНДС 
			И Форма.РаздельныйУчетНДС 
			И НЕ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");
		Элементы.ГруппаПеревыставлениеСФ.Видимость = 
			Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");
	ИначеЕсли ЭтоПрочийДоговор Тогда
		Элементы.ГруппаНалоговыйАгентОрганизация.Видимость = Ложь;
		Элементы.ГруппаЭлектронныеУслуги.Видимость         = Ложь;
		Элементы.ГруппаНалоговыйАгентПокупатель.Видимость  = Ложь;
		Элементы.ГруппаНДССАвансов.Видимость               = Ложь;
		Элементы.ГруппаЗаполнениеСтавкиНДС.Видимость       = Ложь;
		Элементы.КодДекларацияРаздел7.Видимость            = Ложь;
		Элементы.ГруппаПеревыставлениеСФ.Видимость         = Ложь;
	Иначе
		Элементы.ГруппаНалоговыйАгентОрганизация.Видимость = НЕ ЭтоДоговорФакторинга И Форма.ИсполняютсяОбязанностиНалоговогоАгентаПоНДС;
		Элементы.ГруппаЭлектронныеУслуги.Видимость         = Форма.ЭтоДоговорИностранца И Форма.ВедетсяУчетУслугИностранныхИнтернетКомпаний;
		Элементы.ГруппаНалоговыйАгентПокупатель.Видимость  = Ложь;
		Элементы.ГруппаНДССАвансов.Видимость               = Ложь;
		Элементы.ГруппаЗаполнениеСтавкиНДС.Видимость       = Истина;
		Элементы.КодДекларацияРаздел7.Видимость            = Ложь;
		
		// Установим параметры выбора вида агентского договора в зависимости от страны регистрации контрагента.
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.ИностранныйКонтрагент", Форма.ЭтоДоговорИностранца);
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ПараметрВыбора);
		Элементы.ВидАгентскогоДоговора.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
		Элементы.ГруппаНалоговыйАгентОрганизация.Доступность = ЭтоДоговорПриобретения 
			И Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку");
			
		// Установим "виртуальные" флажки и переключатели по электронным услугам.
		Если Объект.ЭлектронныеУслуги 
			ИЛИ Объект.УчетАгентскогоНДС 
			И Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги") Тогда 
			Форма.ЭлектронныеУслугиИностранца = Истина;
			Если Объект.ЭлектронныеУслуги Тогда 
				Форма.ПорядокУплатыНДСПоЭлектроннымУслугам = 1;
			Иначе
				Форма.ПорядокУплатыНДСПоЭлектроннымУслугам = 2;
			КонецЕсли;
		Иначе
			Форма.ЭлектронныеУслугиИностранца = Ложь;
		КонецЕсли;
		
		Элементы.УчетАгентскогоНДС.Видимость = НЕ Форма.ЭлектронныеУслугиИностранца;
		Элементы.ВидАгентскогоДоговора.Видимость = НЕ Форма.ЭлектронныеУслугиИностранца;
		Элементы.ГруппаПорядокУплатыНДСПоЭлектроннымУслугам.Видимость = 
			Форма.ЭлектронныеУслугиИностранца И Форма.ИсполняютсяОбязанностиНалоговогоАгентаПоНДС;
		
		ДоступенВыборВидаАгентскогоДоговора = ЭтоДоговорПриобретения
			И Объект.УчетАгентскогоНДС
			И НЕ ЭтоДоговорКомиссии
			И Форма.ИсполняютсяОбязанностиНалоговогоАгентаПоНДС
			И НЕ Форма.ЭтоДоговорИностранца;
		Элементы.ВидАгентскогоДоговора.Доступность = Объект.УчетАгентскогоНДС;
		Элементы.ВидАгентскогоДоговора.ТолькоПросмотр = Не ДоступенВыборВидаАгентскогоДоговора;
		Элементы.ВидАгентскогоДоговора.КнопкаВыпадающегоСписка = ДоступенВыборВидаАгентскогоДоговора;
		
		Элементы.НаименованиеДляСчетаФактурыНалоговогоАгента.Доступность = Объект.УчетАгентскогоНДС;
		
		Если Объект.ЭлектронныеУслуги Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Приобретение электронных услуг'");
		ИначеЕсли Объект.УчетАгентскогоНДС Тогда
			ТекстЗаголовкаНДС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'НДС: Налоговый агент по уплате НДС - %1'"), Строка(Объект.ВидАгентскогоДоговора));
		ИначеЕсли Форма.ДоговорСРезидентомТаможенногоСоюза И НЕ Объект.УчетАгентскогоНДС Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Организация не выступает в качестве налогового агента по уплате НДС'");
		ИначеЕсли Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.ИзКарточкиНоменклатуры") Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: ставка в документах из карточки номенклатуры'");
		ИначеЕсли Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.БезНДС") Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: ставка в документах ""без НДС""'");
		ИначеЕсли Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.Автоматически") Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: ставка в документах определяется автоматически'");
		Иначе
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС'");
		КонецЕсли;
		Элементы.ГруппаПеревыставлениеСФ.Видимость = Ложь;
		
	КонецЕсли;
	
	УстановитьЗаголовокГруппы(Форма, "ГруппаНДС", ТекстЗаголовкаНДС);
	
	//Группа "Государственный контракт"
	Если ЭтоДоговорКомиссии Тогда 
		Элементы.ГруппаГосударственныйКонтракт.Видимость = Ложь;
	Иначе
		Элементы.ГруппаГосударственныйКонтракт.Видимость = Истина;
		Форма.ЭтоГосударственныйКонтракт = ЗначениеЗаполнено(Объект.ГосударственныйКонтракт);
		Элементы.ГосударственныйКонтракт.Доступность = Форма.ЭтоГосударственныйКонтракт;
	КонецЕсли;
	
	//Группа "Платежный агент"
	Элементы.ГруппаПлатежныйАгент.Видимость               = ВидимостьКомиссионногоВознаграждения;
	
	Элементы.ПризнакАгента.Доступность                    = Объект.ПлатежныйАгент;
	
	ЭтоПлатежныйАгент  = Объект.ПлатежныйАгент 
		И (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйАгент") 
			ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйСубагент")));
	ЭтоБанковскийАгент = Объект.ПлатежныйАгент 
		И (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент") 
			ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент")));
	
	Элементы.ТелефонПоставщика.Видимость                = ЭтоПлатежныйАгент ИЛИ ЭтоБанковскийАгент;
	Элементы.НаименованиеОператораПеревода.Видимость    = ЭтоБанковскийАгент;
	Элементы.ИННОператораПеревода.Видимость             = ЭтоБанковскийАгент;
	Элементы.АдресОператораПеревода.Видимость           = ЭтоБанковскийАгент;
	Элементы.ТелефонОператораПеревода.Видимость         = ЭтоБанковскийАгент;
	Элементы.ТелефонОператораПоПриемуПлатежей.Видимость = ЭтоПлатежныйАгент;
	Элементы.ТелефонПлатежногоАгента.Видимость          = ЭтоПлатежныйАгент ИЛИ ЭтоБанковскийАгент;
	Элементы.ОперацияПлатежногоАгента.Видимость         = ЭтоБанковскийАгент;
	
	Элементы.СпособЗаполненияСтавкиНДС.Доступность = 
		НЕ (ЭтоДоговорРеализации ИЛИ Объект.УчетАгентскогоНДС ИЛИ Форма.ДоговорСРезидентомТаможенногоСоюза)
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером");
	
	Элементы.НДСПоСтавкам4и2.Доступность = ?(Объект.СпособЗаполненияСтавкиНДС =
		 ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.ИзКарточкиНоменклатуры"), Истина, Ложь);
	
	// Группа "Обеспечения обязательств"
	Элементы.ВидОбеспеченияОбязательствИПлатежей.Видимость = Форма.ПредусмотреноОбеспечениеОбязательств;
	Элементы.ОбеспечениеПредоставил.Видимость              = Форма.ПредусмотреноОбеспечениеОбязательств;
	Элементы.ОбеспечениеПолучил.Видимость                  = Форма.ПредусмотреноОбеспечениеОбязательств;
	Элементы.ОбеспечениеВыданоЗа.Видимость                 = Форма.ПредусмотреноОбеспечениеОбязательств;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппы(Форма, НазваниеГруппы, ЗаголовокТекст)
	
	Форма.Элементы[НазваниеГруппы].ЗаголовокСвернутогоОтображения = ЗаголовокТекст;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокСпособовРасчетаКомиссионногоВознаграждения(Форма)

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;

	ЭтоКомиссияПоЗакупке = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");

	СписокСпособов = ОбщегоНазначенияБПКлиентСервер.СформироватьСписокСпособовРасчетаКомиссионногоВознаграждения(ЭтоКомиссияПоЗакупке);
	СписокВыбора = Элементы.СпособРасчетаКомиссионногоВознаграждения.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокСпособов Цикл
		СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СформироватьНаименованиеДоговора()
	
	ТекстНаименования = Объект.Наименование;
	
	НовыеРеквизитыДоговораСтрокой = ПечатьДоговоровКлиентСервер.РеквизитыДоговораСтрокой(Объект.Номер, Объект.Дата);
	
	Если ПустаяСтрока(ТекстНаименования) Или ТекстНаименования = НаименованиеПоУмолчанию Тогда // См. ПечатьДоговоровКлиентСервер.НаименованиеПоУмолчаниюБезРеквизитов()
		ТекстНаименования = НовыеРеквизитыДоговораСтрокой;
	ИначеЕсли СтрНайти(ТекстНаименования, РеквизитыДоговораСтрокой) > 0 
		И СтрНайти(ТекстНаименования, НовыеРеквизитыДоговораСтрокой) = 0 Тогда
		ТекстНаименования = СтрЗаменить(ТекстНаименования, РеквизитыДоговораСтрокой, НовыеРеквизитыДоговораСтрокой);
	КонецЕсли;
	
	РеквизитыДоговораСтрокой = НовыеРеквизитыДоговораСтрокой;
	
	Объект.Наименование = ТекстНаименования;
	
КонецПроцедуры

&НаСервере
Процедура ОграничитьВыборРеквизитов(ЗначенияЗаполнения)

	Если ЗначенияЗаполнения.Свойство("Организация") Тогда
		Если ТипЗнч(ЗначенияЗаполнения.Организация) = Тип("СправочникСсылка.Организации") Тогда
			Элементы.Организация.ТолькоПросмотр = ЗначениеЗаполнено(ЗначенияЗаполнения.Организация);
		КонецЕсли;
	КонецЕсли;

	Если ЗначенияЗаполнения.Свойство("Владелец") Тогда
		Если ТипЗнч(ЗначенияЗаполнения.Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
			Элементы.Владелец.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначенияЗаполнения.Свойство("ВалютаВзаиморасчетов") Тогда
		Если ТипЗнч(ЗначенияЗаполнения.ВалютаВзаиморасчетов) = Тип("СправочникСсылка.Валюты") Тогда
			Элементы.ВалютаВзаиморасчетов.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	//Проверим, передавались ли ограничения по валюте
	Если Параметры.ЗначенияЗаполнения.Свойство("Валютный") Тогда
		
		МассивРазрешенныхВалют = 
			Справочники.ДоговорыКонтрагентов.ПодготовитьСписокРазрешенныхВалют(
				ВалютаРегламентированногоУчета, Параметры.ЗначенияЗаполнения.Валютный);
		
		Элементы.ВалютаВзаиморасчетов.ПараметрыВыбора = Новый ФиксированныйМассив(МассивРазрешенныхВалют);
		
	КонецЕсли;
	
	Если Параметры.ЗначенияЗаполнения.Свойство("ОплатаВВалюте") Тогда
		УсловиеОплатаВВалютеПередано = Истина;
	КонецЕсли;
	
	Если ЗначенияЗаполнения.Свойство("ВидДоговора") Тогда
		МассивВидовДоговоров = Новый Массив;
		Если ТипЗнч(ЗначенияЗаполнения.ВидДоговора) = Тип("Массив") Тогда
			МассивВидовДоговоров = ЗначенияЗаполнения.ВидДоговора;
		ИначеЕсли ТипЗнч(ЗначенияЗаполнения.ВидДоговора) = Тип("ФиксированныйМассив") Тогда
			МассивВидовДоговоров = Новый Массив(ЗначенияЗаполнения.ВидДоговора);
		КонецЕсли;
		ОдинВидДоговора = МассивВидовДоговоров.Количество() <= 1;
		Если ОдинВидДоговора Тогда
			Элементы.ВидДоговора.ТолькоПросмотр = Истина;
		Иначе
			Элементы.ВидДоговора.РежимВыбораИзСписка = Истина;
			Элементы.ВидДоговора.СписокВыбора.ЗагрузитьЗначения(МассивВидовДоговоров);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияСозданиеФайлаДоговора(ФайлДоговора)
	
	Если ЗначениеЗаполнено(Объект.ФайлДоговора) Тогда
		// В договоре уже указан какой-то файл договора, не меняем его.
		Возврат;
	КонецЕсли;

	Объект.ФайлДоговора = ФайлДоговора;
	
	Если НЕ Модифицированность Тогда
		// Пользователь договор не менял, записываем его самостоятельно.
		Записать();
	КонецЕсли;	
	
	// Сообщим форме редактирования текста договора, что мы запомнили ссылку на файл, 
	// и сам объект справочника ДоговорыКонтрагентов изменять не требуется,
	// чтобы при интерактивной работе пользователя не возникало сообщений об изменении
	// объекта в другом сеансе из-за записи изменений элемента справочника.
	Оповестить("ДоговорыКонтрагентов_СозданиеФайлаОбработаноОсновнойФормойДоговора", Объект.Ссылка);

КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	
	Справочники.ДоговорыКонтрагентов.ЗаполнитьРуководителяКонтрагента(Объект);
	УстановитьСпискиЭлементовОбеспеченияОбязательств();
		// Очистим признаки налогового агента и электронных услуг.
	Объект.УчетАгентскогоНДС     = Ложь;
	Объект.ВидАгентскогоДоговора = Неопределено;
	Объект.ЭлектронныеУслуги     = Ложь;
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Справочники.ДоговорыКонтрагентов.ЗаполнитьРуководителяОрганизации(Объект);
	УстановитьСпискиЭлементовОбеспеченияОбязательств();
	
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ЭтоПлательщикНДС                            = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, ТекущаяДатаСеанса());
	ИсполняютсяОбязанностиНалоговогоАгентаПоНДС = ПолучитьФункциональнуюОпцию("ИсполняютсяОбязанностиНалоговогоАгентаПоНДС");
	ОбратноеНачислениеНДС                       = ПолучитьФункциональнуюОпцию("ОбратноеНачислениеНДС");
	ВедетсяУчетУслугИностранныхИнтернетКомпаний = ПолучитьФункциональнуюОпцию("ВедетсяУчетУслугИностранныхИнтернетКомпаний");
	
	// Для отображения релевантных настроек НДС необходимо различать договоры с российскими и иностранными организациями.
	СтранаРегистрацииКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "СтранаРегистрации");
	ЭтоДоговорИностранца = ЗначениеЗаполнено(СтранаРегистрацииКонтрагента)
		И СтранаРегистрацииКонтрагента <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокКонтактныхЛиц(Знач Контрагент)
	
	Возврат Справочники.КонтактныеЛица.СписокКонтактныхЛиц(Контрагент, Ложь);

КонецФункции

&НаКлиенте
Процедура РуководительКонтрагентаНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивФИОДолжность = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РезультатВыбора.Представление, ", ");
	Если МассивФИОДолжность.Количество() > 0 Тогда
		Объект.РуководительКонтрагента = МассивФИОДолжность[0];
	КонецЕсли;
	Если МассивФИОДолжность.Количество() > 1 Тогда
		Объект.ДолжностьРуководителяКонтрагента = МассивФИОДолжность[1];
	КонецЕсли;
	ПриИзмененииПредставленияРуководителяКонтрагента();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВалютаВзаиморасчетовПриИзмененииНаСервере()
	
	Если Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
		ОплатаВРублях = 0;
	Иначе
		
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем")
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
			ИЛИ ДокументОформляетсяВУсловныхЕдиницах() Тогда
			Объект.РасчетыВУсловныхЕдиницах = Истина;
			ОплатаВРублях = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ДокументОформляетсяВУсловныхЕдиницах()
	
	// Для этих видов договоров оплата в У.Е. не поддерживается.
	МассивВидовДоговоровБезУЕ = Новый Массив;
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.Прочее);
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	Если МассивВидовДоговоровБезУЕ.Найти(Объект.ВидДоговора) <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат УсловиеОплатаВВалютеПередано И НЕ Объект.ОплатаВВалюте;
	
КонецФункции

&НаКлиенте
Процедура СпособЗаполненияСтавкиНДСПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияСтавкиНДСПоУмолчанию()
	
	ЗаполнятьСтавкуНДСБезНДС = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");
	
	Если ЗаполнятьСтавкуНДСБезНДС Тогда
		Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.БезНДС");
	Иначе
		Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.Автоматически");
	КонецЕсли;
	
	Если Объект.НДСПоСтавкам4и2 и ЗаполнятьСтавкуНДСБезНДС Тогда
		Объект.НДСПоСтавкам4и2 = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПризнакиАгента()
	Если НЕ Объект.ПлатежныйАгент Тогда
		Объект.ПризнакАгента = Неопределено;
		Объект.ТелефонПоставщика             = "";
		Объект.ТелефонПлатежногоАгента       = "";
	КонецЕсли; 
	
	ЭтоПлатежныйАгент  = (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйАгент")) ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйСубагент"));
	ЭтоБанковскийАгент = (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент")) ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент"));
	
	Если НЕ ЭтоБанковскийАгент Тогда
		Объект.НаименованиеОператораПеревода = "";
		Объект.ИННОператораПеревода          = "";
		Объект.АдресОператораПеревода        = "";
		Объект.ТелефонОператораПеревода      = "";
	КонецЕсли;
	
	Если НЕ ЭтоПлатежныйАгент Тогда
		Объект.ТелефонОператораПоПриемуПлатежей = "";
		Объект.ОперацияПлатежногоАгента         = "";
	КонецЕсли; 
КонецПроцедуры 

&НаСервере
Процедура УстановитьСпискиЭлементовОбеспеченияОбязательств()
	
	СписокВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		СписокВыбора.Добавить(Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		СписокВыбора.Добавить(Объект.Владелец);
	КонецЕсли;
	
	СписокВыбора.Добавить(НСтр("ru='Выбрать...'"));
	
	Элементы.ОбеспечениеПредоставил.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	Элементы.ОбеспечениеПолучил.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	Элементы.ОбеспечениеВыданоЗа.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораКонтрагентаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтотОбъект[ДополнительныеПараметры.ИмяЭлемента] = Результат;
		Объект[ДополнительныеПараметры.ИмяЭлемента] = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыПодсистемыСвойств

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()

	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ПредусмотреноОбеспечениеОбязательств Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ВидОбеспеченияОбязательствИПлатежей) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
				НСтр("ru='Вид обеспечения'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ВидОбеспеченияОбязательствИПлатежей", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаСервереБезКонтекста
Функция РеквизитыСклоненияРуководителя(Руководитель)
	
	РеквизитыСклонения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Руководитель, "ФИО,Пол");
	
	РеквизитыСклонения.Пол = ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(РеквизитыСклонения.Пол);
	
	РеквизитыСклонения.Вставить("ТолькоПросмотр", Не ПравоНаРедактирование(Истина));
	
	Возврат РеквизитыСклонения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПравоНаРедактирование(ЭтоФИО)
	
	ПроверяемыйСправочник = ?(ЭтоФИО, Метаданные.Справочники.ФизическиеЛица, Метаданные.Справочники.Должности);
	Возврат ПравоДоступа("Редактирование", ПроверяемыйСправочник);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьВРегистрСклонения(ОбъектСоСклоняемымиРеквизитами, Представление, СтруктураСклонения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписейСклонения = РегистрыСведений.СклоненияПредставленийОбъектов.СоздатьНаборЗаписей();
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(Представление);
	ХешПредставления = СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", "");
	
	НаборЗаписейСклонения.Отбор.ХешПредставления.Установить(ХешПредставления);
	Если Не ЗначениеЗаполнено(ОбъектСоСклоняемымиРеквизитами) Тогда
		// Может быть несколько склонений представлений, относимых к одному пустому значению.
		НаборЗаписейСклонения.Отбор.Объект.Установить(ОбъектСоСклоняемымиРеквизитами);
	КонецЕсли;
	
	НоваяСтрока = НаборЗаписейСклонения.Добавить();
	НоваяСтрока.Объект           = ОбъектСоСклоняемымиРеквизитами;
	НоваяСтрока.ХешПредставления = ХешПредставления;
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураСклонения);
	НаборЗаписейСклонения.Записать();
	
КонецПроцедуры

// Вызывается для реквизитов, чьи склонения привязаны к договору: ФИО и должности представителя контрагента.
//
&НаКлиенте
Процедура Подключаемый_ПросклонятьПредставлениеПоВсемПадежам() Экспорт
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	ТекущийРеквизитСклонения         = "СклоненияРуководительКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента;
	СклонениеПредставленийОбъектовКлиент.ПросклонятьПредставлениеПоВсемПадежам(
		ЭтотОбъект,
		Объект.РуководительКонтрагента,
		Истина,
		ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(Объект.ПолРуководителяКонтрагента));
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента = ЭтотОбъект.ИзмененоПредставление;
	
	ТекущийРеквизитСклонения         = "СклоненияДолжностьРуководителяКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента;
	СклонениеПредставленийОбъектовКлиент.ПросклонятьПредставлениеПоВсемПадежам(ЭтотОбъект, Объект.ДолжностьРуководителяКонтрагента);
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента = ЭтотОбъект.ИзмененоПредставление;
	
КонецПроцедуры

// Завершение процедуры ОбработатьКомандуСклонения.
// Вызывается для реквизитов, чьи склонения привязаны к объектам вне договора: ФИО и должности руководителя организации.
//
&НаКлиенте
Процедура Подключаемый_ОткрытьФормуСклоненияЗавершение(РезультатРедактирования, ДополнительныеПараметры) Экспорт

	Если РезультатРедактирования <> Неопределено
	   И РезультатРедактирования.ИменительныйПадеж = ДополнительныеПараметры.Представление
	   И Не ДополнительныеПараметры.Свойство("ЗапретРедактирования") Тогда
		
		СтруктураСклонения = Новый ФиксированнаяСтруктура(РезультатРедактирования);
		ЗаписатьВРегистрСклонения(
			Объект[ТекущийРеквизитСклонения],
			ДополнительныеПараметры.Представление,
			СтруктураСклонения);
		
	КонецЕсли;	
	
	// Закончили редактирование объекта склонения внешнего по отношению к данной форме.
	// Возвращаемся к редактированию склонений реквизита формы.
	ТекущийРеквизитСклонения         = "СклоненияРуководительКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПредставленияРуководителяКонтрагента()
	
	// Пытаемся определить пол.
	ПредполагаемыйПол = СотрудникиКлиентСервер.ОпределитьПолПоОтчеству(СокрП(Объект.РуководительКонтрагента));
	Если ЗначениеЗаполнено(ПредполагаемыйПол) Тогда
		Объект.ПолРуководителяКонтрагента = ПредполагаемыйПол;
	КонецЕсли;
	
	СклонениеПредставленийОбъектовКлиент.ПриИзмененииПредставления(ЭтотОбъект);
	ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервереСклоненийРеквизитов(ТекущийОбъект)
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	ОбъектСоСклоняемымиРеквизитами = Справочники.ДоговорыКонтрагентов.ПустаяСсылка(); // любой договор
	
	Если ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента Тогда
		
		СтруктураСклонений = СклонениеПредставленийОбъектов.ПросклонятьПредставлениеПоВсемПадежам(
			ТекущийОбъект.РуководительКонтрагента,
			Истина,
			ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(ТекущийОбъект.ПолРуководителяКонтрагента));
		ЭтотОбъект.СклоненияРуководительКонтрагента = Новый ФиксированнаяСтруктура(СтруктураСклонений);
		ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента = Ложь;
		
	КонецЕсли;
	ЗаписатьСклонения = ТипЗнч(ЭтотОбъект.СклоненияРуководительКонтрагента) = Тип("ФиксированнаяСтруктура");
	Если ЗаписатьСклонения Тогда
		
		ЗаписатьВРегистрСклонения(
			ОбъектСоСклоняемымиРеквизитами,
			ТекущийОбъект.РуководительКонтрагента,
			ЭтотОбъект.СклоненияРуководительКонтрагента);
		
	КонецЕсли;
	
	Если ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента Тогда
		
		СтруктураСклонений = СклонениеПредставленийОбъектов.ПросклонятьПредставлениеПоВсемПадежам(
			ТекущийОбъект.ДолжностьРуководителяКонтрагента);
		ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента = Новый ФиксированнаяСтруктура(СтруктураСклонений);
		ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента = Ложь;
		
	КонецЕсли;
	ЗаписатьСклонения = ТипЗнч(ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента) = Тип("ФиксированнаяСтруктура");
	Если ЗаписатьСклонения Тогда
		
		ЗаписатьВРегистрСклонения(
			ОбъектСоСклоняемымиРеквизитами,
			ТекущийОбъект.ДолжностьРуководителяКонтрагента,
			ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента);
		
	КонецЕсли;
		
	ЭтотОбъект.Склонения = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = Ложь;	
	
КонецПроцедуры

// Обработчик команды "Склонения" формы объекта склонения.
//
// Параметры:
//  Представление   - Строка - Строка для склонения.
//  ЭтоФИО       	- Булево - Признак склонения ФИО.
//	Пол				- Число	- Пол физического лица (в случае склонения ФИО)
//							1 - мужской 
//							2 - женский.
//
&НаКлиенте
Процедура ОбработатьКомандуСклонения(Представление, ЗапретРедактирования, ЭтоФИО = Ложь, Пол = Неопределено)
	
	СтруктураСклонения = СклонениеПредставленийОбъектовКлиентСервер.СтруктураСклонения();
	
	СклонениеПредставленийОбъектовВызовСервера.ПросклонятьПредставлениеПоВсемПадежам(Представление, СтруктураСклонения, ЭтоФИО, Пол);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Представление", Представление);
	Если ЗапретРедактирования Тогда
		ДополнительныеПараметры.Вставить("ЗапретРедактирования", Истина);
	КонецЕсли;
	Оповещение = Новый ОписаниеОповещения("Подключаемый_ОткрытьФормуСклоненияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Склонения",             СтруктураСклонения);
	СтруктураПараметров.Вставить("ЭтоФИО",                ЭтоФИО);
	СтруктураПараметров.Вставить("Пол",                   Пол);
	СтруктураПараметров.Вставить("Представление",         Представление);
	СтруктураПараметров.Вставить("ИзмененоПредставление", Ложь);
	
	Если ЗапретРедактирования Тогда
		СтруктураПараметров.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
		
	ОткрытьФорму("ОбщаяФорма.Склонения", СтруктураПараметров, ЭтотОбъект, , , , Оповещение);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

// СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаКлиенте
Процедура ПоказатьСклоненияРуководительКонтрагента(Команда)
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	ТекущийРеквизитСклонения         = "СклоненияРуководительКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента;
	СклонениеПредставленийОбъектовКлиент.ОбработатьКомандуСклонения(
		ЭтотОбъект,
		Объект.РуководительКонтрагента,
		Истина,
		ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(Объект.ПолРуководителяКонтрагента));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСклоненияДолжностьРуководителяКонтрагента(Команда)
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	ТекущийРеквизитСклонения         = "СклоненияДолжностьРуководителяКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента;
	СклонениеПредставленийОбъектовКлиент.ОбработатьКомандуСклонения(ЭтотОбъект, Объект.ДолжностьРуководителяКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСклоненияРуководитель(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Руководитель) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	ТекущийРеквизитСклонения = "Руководитель";
	
	РеквизитыСклонения = РеквизитыСклоненияРуководителя(Объект.Руководитель);
	
	ОбработатьКомандуСклонения(РеквизитыСклонения.ФИО, РеквизитыСклонения.ТолькоПросмотр, Истина, РеквизитыСклонения.Пол);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСклоненияДолжностьРуководителя(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ДолжностьРуководителя) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	ТекущийРеквизитСклонения = "ДолжностьРуководителя";
	
	ПредставлениеОбъекта = Строка(Объект.ДолжностьРуководителя);
	ЗапретРедактирования = Не ПравоНаРедактирование(Ложь);
	ОбработатьКомандуСклонения(ПредставлениеОбъекта, ЗапретРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьАвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьОкончаниеВводаТекста(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

#КонецОбласти

// Квартплата +
// Созадет на форме два элемента интерфейса - "ВариантПоставкиУслуг" и "ОплатаЧерезУК".
&НаСервере
Процедура СоздатьЭлементыУПЖКХ() Экспорт
	Если (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
	 ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))Тогда
	
		Элемент = Элементы.Добавить("УПЖКХ_ВариантПоставкиУслуг", Тип("ПолеФормы"), Элементы.ГруппаШапкаПравая);
		Элемент.Вид                            = ВидПоляФормы.ПолеВвода;
		Элемент.ОтображениеПодсказки           = ОтображениеПодсказки.Кнопка;
		Элемент.РасширеннаяПодсказка.Заголовок = "Вариант поставки меняется в соответствии с видом договора автоматически";
		Элемент.ПутьКДанным                    = "Объект.УПЖКХ_ВариантПоставкиУслуг";
		Элемент.УстановитьДействие("ПриИзменении","УПЖКХ_ВариантПоставкиУслугПриИзменении");
		
		Элемент = Элементы.Добавить("УПЖКХ_ОплатаЧерезУК", Тип("ПолеФормы"), Элементы.ГруппаШапкаПравая);
		Элемент.Вид                            = ВидПоляФормы.ПолеФлажка;
		Элемент.ПоложениеЗаголовка             = ПоложениеЗаголовкаЭлементаФормы.Право;
		Элемент.ОтображениеПодсказки           = ОтображениеПодсказки.Кнопка;
		Элемент.РасширеннаяПодсказка.Заголовок = "Определяет, каким образом жильцы будут оплачивать начисления за услуги поставщика: на расчетный счет поставщика или на расчетный счет управляющей организации";

		Элемент.ПутьКДанным        = "Объект.УПЖКХ_ОплатаЧерезУК";
		
		Если (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком")) Тогда 
			Элемент.Доступность = Ложь;
		КонецЕсли;
		
		Элементы.Переместить(Элементы.Комментарий, Элементы.ГруппаШапкаЛевая, Элементы.Родитель);
		
		Если НЕ ЗначениеЗаполнено(Объект.УПЖКХ_ВариантПоставкиУслуг) Тогда
			ОбновитьЭлементыУПЖКХ();
		Иначе
			ПроверитьСоответствиеВидаДоговораСхемеПоставки()
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команды ПриИзменении, созданного программно элемента ВариантПоставкиУслуг.
&НаКлиенте
Процедура УПЖКХ_ВариантПоставкиУслугПриИзменении(Элемент)
	
	ПроверитьСоответствиеВидаДоговораСхемеПоставки();
	
КонецПроцедуры

// Проверяет соответствие варианта поставки выбранному виду договора.
&НаСервере
Процедура ПроверитьСоответствиеВидаДоговораСхемеПоставки() Экспорт
	Если ((Объект.УПЖКХ_ВариантПоставкиУслуг = Перечисления.УПЖКХ_ВариантыРасчетовСПоставщикамиУслуг.КупляПродажаУслуг)
	   И  (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")))
	ИЛИ  ((Объект.УПЖКХ_ВариантПоставкиУслуг = Перечисления.УПЖКХ_ВариантыРасчетовСПоставщикамиУслуг.АгентскаяСхема)
	   И  (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))) Тогда
		Элементы.УПЖКХ_ВариантПоставкиУслуг.ЦветФона                       = Новый Цвет(220, 60, 60);
		Элементы.УПЖКХ_ВариантПоставкиУслуг.РасширеннаяПодсказка.Заголовок = "Вариант поставки не соответствует виду договора, рекомендуется изменить его";
		Элементы.УПЖКХ_ВариантПоставкиУслуг.ОтображениеПодсказки           = ОтображениеПодсказки.ОтображатьСправа;
	Иначе
		Элементы.УПЖКХ_ВариантПоставкиУслуг.ЦветФона                       = ЦветаСтиля.ЦветФонаПоля;
		Элементы.УПЖКХ_ВариантПоставкиУслуг.РасширеннаяПодсказка.Заголовок = "Вариант поставки меняется в соответстии с видом договора автоматически";
		Элементы.УПЖКХ_ВариантПоставкиУслуг.ОтображениеПодсказки           = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
КонецПроцедуры

// Обновляет, созданные программно элементы интерфейса "ВариантПоставкиУслуг" и "ОплатаЧерезУК".
&НаСервере
Процедура ОбновитьЭлементыУПЖКХ() Экспорт
	Если(НЕ Элементы.Найти("УПЖКХ_ОплатаЧерезУК") = Неопределено И НЕ Элементы.Найти("УПЖКХ_ВариантПоставкиУслуг") = Неопределено) Тогда
		Если (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")) Тогда
			Объект.УПЖКХ_ВариантПоставкиУслуг = Перечисления.УПЖКХ_ВариантыРасчетовСПоставщикамиУслуг.АгентскаяСхема;
			Объект.УПЖКХ_ОплатаЧерезУК        = Ложь;
			
			Элементы.УПЖКХ_ОплатаЧерезУК.Доступность = Истина;
			Элементы.УПЖКХ_ОплатаЧерезУК.Видимость   = Истина;
			
			Элементы.УПЖКХ_ВариантПоставкиУслуг.Видимость = Истина;
			
			ПроверитьСоответствиеВидаДоговораСхемеПоставки();
		ИначеЕсли (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком")) Тогда
			Объект.УПЖКХ_ВариантПоставкиУслуг = Перечисления.УПЖКХ_ВариантыРасчетовСПоставщикамиУслуг.КупляПродажаУслуг;
			Объект.УПЖКХ_ОплатаЧерезУК        = Истина;
			
			Элементы.УПЖКХ_ОплатаЧерезУК.Доступность = Ложь;
			Элементы.УПЖКХ_ОплатаЧерезУК.Видимость   = Истина;
			
			Элементы.УПЖКХ_ВариантПоставкиУслуг.Видимость = Истина;
			
			ПроверитьСоответствиеВидаДоговораСхемеПоставки();
		Иначе
			Объект.УПЖКХ_ВариантПоставкиУслуг = Неопределено;
			Объект.УПЖКХ_ОплатаЧерезУК        = Ложь;
			
			Элементы.УПЖКХ_ОплатаЧерезУК.Видимость        = Ложь;
			Элементы.УПЖКХ_ВариантПоставкиУслуг.Видимость = Ложь;
		КонецЕсли;
	Иначе
		СоздатьЭлементыУПЖКХ();
	КонецЕсли
КонецПроцедуры
// Квартплата -
