#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуКлассификатора();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокВалют

&НаКлиенте
Процедура КлассификаторВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВКлассификаторе(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВыполнить()
	
	ОбработатьВыборВКлассификаторе();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуКлассификатора()
	
	// Заполняет список из макета ОКП
	КлассификаторXML = Справочники.ОбщероссийскийКлассификаторПродукции.ПолучитьМакет("ОбщероссийскийКлассификаторПродукции").ПолучитьТекст();
	
	ЗначениеВРеквизитФормы(ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные, "Классификатор");
	
КонецПроцедуры

&НаСервере
Функция СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки)
	
	ТекущаяСсылка = Неопределено;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = Классификатор[НомерСтроки];
		
		СтрокаВБазе = Справочники.ОбщероссийскийКлассификаторПродукции.НайтиПоКоду(ТекущиеДанные.Код);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.Классификатор.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Справочники.ОбщероссийскийКлассификаторПродукции.СоздатьЭлемент();
		НоваяСтрока.Код                       = ТекущиеДанные.Код;
		НоваяСтрока.Наименование              = ТекущиеДанные.Наименование;
		НоваяСтрока.НаименованиеПолное        = ТекущиеДанные.Наименование;
		НоваяСтрока.Записать();
		
		Если НомерСтроки = Элементы.Классификатор.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТекущаяСсылка;

КонецФункции

&НаКлиенте
Процедура ОбработатьВыборВКлассификаторе(СтандартнаяОбработка = Неопределено)
	
	// Добавление элемента справочника и вывод результата пользователю
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСсылка = СохранитьВыбранныеСтроки(Элементы.Классификатор.ВыделенныеСтроки);
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.ОбщероссийскийКлассификаторПродукции"));
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Заполнено из классификатора.'"),
		,
		ЭтаФорма.Заголовок,
		БиблиотекаКартинок.Информация32);
	
	ОповеститьОВыборе(ТекущаяСсылка);
	
КонецПроцедуры

#КонецОбласти