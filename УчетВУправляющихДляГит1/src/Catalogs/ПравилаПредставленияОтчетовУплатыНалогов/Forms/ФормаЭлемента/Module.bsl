
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриЧтенииСозданииНаСервере();
	
	// Настроим форму
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	// Настроим форму
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторятьНесколькоРазПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
	Если Не ПовторятьНесколькоРаз Тогда
		Объект.Периодичность = Объект.ФинансовыйПериод;
		Объект.ОграничениеПериода = "";
		ЗаданоОграничениеПериода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйПериодПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Периодичность) Тогда
		Объект.Периодичность = Объект.ФинансовыйПериод;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданоОграничениеПериодаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничениеПериодаПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.ОграничениеПериода) Тогда
		ЗаданоОграничениеПериода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтандартныйБазовыйПериодПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
	Если СтандартныйБазовыйПериод = 1 Тогда
		Объект.ОтставаниеБазовогоПериода = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НестандартныйБазовыйПериодРанееПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
	Если СтандартныйБазовыйПериод = 0 Тогда
		Объект.ОтставаниеБазовогоПериода = 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	УплатаНалога = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога");
	Если Форма.Объект.Действие = УплатаНалога Тогда
		Форма.Элементы.ПериодДекларации.Видимость = Истина;
		
		СтандартныйБазовыйПериод = (Форма.СтандартныйБазовыйПериод = 1);
		Форма.Элементы.БазовыйПериод.Доступность             = СтандартныйБазовыйПериод;
		Форма.Элементы.ОтставаниеБазовогоПериода.Доступность = Не СтандартныйБазовыйПериод;
		Форма.Элементы.БазовыйПериодРанее.Доступность        = Не СтандартныйБазовыйПериод;
		
	Иначе
		Форма.Элементы.ПериодДекларации.Видимость = Ложь;
		
		ЭтоДействиеОтчет = (Форма.Объект.Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Отчет"));
		Форма.Элементы.ОтправкаОтчетаОграничена.Доступность = ЭтоДействиеОтчет;
		
	КонецЕсли;
	
	Форма.Элементы.ПериодичностьГруппа.Доступность = Форма.ПовторятьНесколькоРаз;
	Форма.Элементы.ОграничениеПериода.Доступность  = Форма.ЗаданоОграничениеПериода;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ПовторятьНесколькоРаз    = (Объект.Периодичность <> Объект.ФинансовыйПериод);
	ЗаданоОграничениеПериода = Не ПустаяСтрока(Объект.ОграничениеПериода);
	СтандартныйБазовыйПериод = (Объект.ОтставаниеБазовогоПериода = 0);

КонецПроцедуры

#КонецОбласти
