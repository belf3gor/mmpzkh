#Область ПрочиеПроцедурыИФункции

&НаКлиенте
// Обновляет данные списка стояков.
Процедура ОбновитьДанныеСпискаСтояков()
	
	// Удаление предыдущих отборов по проекту/зданию.
	УПЖКХ_ТиповыеМетодыКлиентСервер.УдалитьЭлементыГруппыОтбора(СписокСтояков.Отбор, "Владелец");
	УПЖКХ_ТиповыеМетодыКлиентСервер.УдалитьЭлементыГруппыОтбора(СписокСтояков.Отбор, "Владелец.Владелец");
	
	ТекущиеДанные = Элементы.ПроектыПомещений.ТекущиеДанные;
	ПолеОтбора    = "Владелец";
	Заголовок     = "Список стояков проекта";
	ОтображатьПроектВСтоякахДляЗдания = Ложь;
	
	Если ТекущиеДанные = Неопределено Тогда 
		ТекущийПроектПомещения = ПредопределенноеЗначение("Справочник.УПЖКХ_ПроектыПомещений.ПустаяСсылка");
	ИначеЕсли ТекущиеДанные.Свойство("РодительскаяГруппировкаСтроки") И ТекущиеДанные.РодительскаяГруппировкаСтроки = Неопределено Тогда 
		ТекущийПроектПомещения = ТекущиеДанные.Владелец;
		ПолеОтбора             = "Владелец.Владелец";
		Заголовок              = "Список стояков всех проектов здания";
		ОтображатьПроектВСтоякахДляЗдания = Истина;
	Иначе
		ТекущийПроектПомещения = ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	// Установка отбора списка стояков по текущему проекту/зданию.
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(СписокСтояков.Отбор, ПолеОтбора, ТекущийПроектПомещения);
	
	// Обновление заголовка списка стояков.
	Элементы.ГруппаСтоякиПоПроектам.Заголовок   = Заголовок + " """ + ?(ТекущийПроектПомещения = Неопределено, "<не определен>", ТекущийПроектПомещения) + """";
	Элементы.СтоякиПоПроектамВладелец.Видимость = ОтображатьПроектВСтоякахДляЗдания;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ЧастоЗадаваемыеВопросы
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПроектыПомещенийКоманднаяПанель);
	// Конец ЧастоЗадаваемыеВопросы
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСведенияОСтояках" Тогда
		Элементы.СтоякиПоПроектам.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" табличного поля "Проекты помещений".
Процедура ПроектыПомещенийПриИзменении(Элемент)
	
	//ПодключитьОбработчикОжидания("ОбновитьДанныеСпискаСтояков", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриАктивизацииСтроки" табличного поля "Проекты помещений".
Процедура ПроектыПомещенийПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбновитьДанныеСпискаСтояков", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Выбор" табличного поля "Проекты помещений".
Процедура ПроектыПомещенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбраннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Элементы.ПроектыПомещений.Развернут(ВыбраннаяСтрока) = Истина Тогда
			Элементы.ПроектыПомещений.Свернуть(ВыбраннаяСтрока);
		Иначе
			Элементы.ПроектыПомещений.Развернуть(ВыбраннаяСтрока, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередНачаломДобавления" таблицы "СтоякиПоПроектам".
Процедура СтоякиПоПроектамПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекущийПроектПомещения = Элементы.ПроектыПомещений.ТекущаяСтрока;
	Если ТекущийПроектПомещения = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Для создания стояка проекта необходимо определить проект помещения!'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// ЧастоЗадаваемыеВопросы
&НаКлиенте
//Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти