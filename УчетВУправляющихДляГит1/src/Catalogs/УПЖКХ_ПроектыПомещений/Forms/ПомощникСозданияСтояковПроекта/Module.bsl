
#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПроектПомещения") Тогда
		
		ПроектПомещения = Параметры.ПроектПомещения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "СоздатьСтоякиПроекта".
//
Процедура СоздатьСтоякиПроекта(Команда)
	
	СоздатьСтоякиПроектаНаСервере();
	
	Оповестить("ОбновитьСведенияОСтояках");
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
// Создает стояки проекта с учетом заданного количества стояков
// и нумерует их по порядку с учетом максимального номера существующего стояка по проекту и ресурсу.
Процедура СоздатьСтоякиПроектаНаСервере()
	
	МаксимальныйНомер = ПолучитьМаксимальныйНомерСтояка();
	
	Для НомерСтояка = 1 По КоличествоСтояков Цикл
		
		ТекущийНомер = МаксимальныйНомер + НомерСтояка;
		
		НовыйСтояк = Справочники.УПЖКХ_Стояки.СоздатьЭлемент();
		НовыйСтояк.Владелец           = ПроектПомещения;
		НовыйСтояк.КоммунальныйРесурс = КоммунальныйРесурс;
		НовыйСтояк.НомерСтояка        = ТекущийНомер;
		НовыйСтояк.Наименование       = "Стояк №" + ТекущийНомер;
		КВП_ЗаписатьОбъект(НовыйСтояк);
		
	КонецЦикла;
	
КонецПроцедуры // СоздатьСтоякиПроектаНаСервере()

&НаСервере
// Возвращает максимальный номер существующего стояка по проекту и ресурсу.
Функция ПолучитьМаксимальныйНомерСтояка()
	
	МаксимальныйНомер = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КоммунальныйРесурс", КоммунальныйРесурс);
	Запрос.УстановитьПараметр("ПроектПомещения",    ПроектПомещения);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(МАКСИМУМ(УПЖКХ_Стояки.НомерСтояка), 0) КАК МаксимальныйНомерСтояка
	|ИЗ
	|	Справочник.УПЖКХ_Стояки КАК УПЖКХ_Стояки
	|ГДЕ
	|	УПЖКХ_Стояки.Владелец = &ПроектПомещения
	|	И УПЖКХ_Стояки.КоммунальныйРесурс = &КоммунальныйРесурс";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		МаксимальныйНомер = Результат.МаксимальныйНомерСтояка;
	КонецЕсли;
	
	Возврат МаксимальныйНомер;
	
КонецФункции // ПолучитьМаксимальныйНомерСтояка()

#КонецОбласти