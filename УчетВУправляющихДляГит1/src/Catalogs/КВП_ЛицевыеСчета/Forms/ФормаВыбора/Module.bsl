
#Область СлужебныеПроцедурыИФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
// Функция определяет, открыт ли лицевой счет на сервере.
Функция ЛицевойСчетОткрытНаСервере(ЛицевойСчет)
	
	Возврат Справочники.КВП_ЛицевыеСчета.ЛицевойСчетОткрыт(ЛицевойСчет);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Если был передан лицевой счет, то активизирует строку с этим лицевым счетом.
	Параметры.Свойство("ЛицевойСчет", Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ОбработкаОповещения" формы.
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Оповещение_ОбновитьСписокЛицевыхСчетов" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура вызывается при изменении списка.
Процедура СписокПриИзменении(Элемент)
	
	ЛицевойСчет = Элемент.ТекущаяСтрока;
	Если Не ЛицевойСчет = Неопределено И УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(ЛицевойСчет, "ПометкаУдаления") Тогда
		
		Если ЛицевойСчетОткрытНаСервере(ЛицевойСчет) Тогда
		
			// если лицевой счет открыт, то запрашиваем о его закрытии
			ДопПараметры                  = Новый Структура("ЛицевойСчет", ЛицевойСчет);
			ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатВопроса", ЭтаФорма, ДопПараметры);
			ТекстВопроса                  = "При установке пометки удаления необходимо закрыть лицевой счет.
											|Закрыть лицевой счет?";
			ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СписокПриИзменении()

&НаКлиенте
// Процедура-обработчик результата вопроса, вызванной в процедуре "СписокПриИзменении()".
// Открывает форму документа закрытия лицевого счета.
//
// Параметры:
//  РезультатВопроса		 - 	КодВозвратаДиалога - код ответа.
//  ДополнительныеПараметры	 - 	Структура - дополнительные параметры.
//
Процедура ОбработатьРезультатВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Документ.КВП_ЗакрытиеЛицевогоСчета.ФормаОбъекта",
					 Новый Структура("ЛицевойСчет", ДополнительныеПараметры.ЛицевойСчет),
					 ЭтаФорма,,
					 ВариантОткрытияОкна.ОтдельноеОкно,,,
					 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры // ОбработатьРезультатВопроса()

&НаКлиенте
// Процедура вызывается перед началом добавления нового элемента.
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Группа Тогда
		
		Отказ = Истина;
		
		Если Копирование И Не Элемент.ТекущиеДанные = Неопределено Тогда
			
			СтруктураДанных = Новый Структура;
			СтруктураДанных.Вставить("ШаблонЛС", Элемент.ТекущаяСтрока);
			СтруктураДанных.Вставить("Здание",
										УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Элемент.ТекущиеДанные.Адрес, "Владелец"));
			
		Иначе
			СтруктураДанных = Неопределено;
		КонецЕсли;
		
		УПЖКХ_РаботаСЛицевымиСчетамиКлиент.ОткрытиеЛицевогоСчета(СтруктураДанных);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти