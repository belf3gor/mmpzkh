
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура-обработчик "ПроверкаЗаполнения" элемента справочника.
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидВторойСтороныДоговора = Перечисления.УПЖКХ_ВидыВторойСтороныУправленияДомами.СобственникиПомещений Тогда
		ПроверяемыеРеквизиты.Добавить("ВтораяСторонаДоговораСобственники");
	Иначе
		ПроверяемыеРеквизиты.Добавить("ВтораяСторонаДоговораКонтрагент");
	КонецЕсли;
	
	Если ПериодПодачиПоказанийПоСчетчикамНачало > ПериодПодачиПоказанийПоСчетчикамКонец Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Неверно указан период подачи показаний счетчиков.", Отказ);
	КонецЕсли;
	
	Для Каждого СтрокаКУ Из ПереченьКоммунальныхУслуг Цикл
		Если СтрокаКУ.Предоставляет Тогда
			Если СтрокаКУ.ДатаНачалаПредоставления = Дата("00010101") Или СтрокаКУ.ДатаОкончанияПредоставления = Дата("00010101")
			 Или СтрокаКУ.ДатаНачалаПредоставления > СтрокаКУ.ДатаОкончанияПредоставления Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Неверно указан период предоставления коммунальной услуги "
																 + СтрокаКУ.Услуга + ".", Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрокаКУ.ВидУслуги) Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не указан вид предоставляемой коммунальной услуги "
																 + СтрокаКУ.Услуга + ".", Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаДУ Из ПереченьДополнительныхУслуг Цикл
		Если СтрокаДУ.ДатаНачалаПредоставления = Дата("00010101") Или СтрокаДУ.ДатаОкончанияПредоставления = Дата("00010101")
		 Или СтрокаДУ.ДатаНачалаПредоставления > СтрокаДУ.ДатаОкончанияПредоставления Тогда
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Неверно указан период предоставления дополнительной услуги "
															 + СтрокаДУ.ВидУслуги + ".", Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти

#КонецЕсли