
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Наименование");
	Поля.Добавить("ТребуетсяЗагрузка");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.ТребуетсяЗагрузка = Истина
		И НЕ ЗначениеЗаполнено(Данные.Наименование) Тогда
		СтандартнаяОбработка = Ложь;
		Представление = НСтр("ru = '<не загружено>'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Загрузить заболевание
//
// Параметры:
//  Заболевание		 - Структура - см. ИнтеграцияВЕТИСКлиентСервер.СтруктураДанныеЗаболевания()
//  СправочникОбъект - СправочникОбъект.ЗаболеванияВЕТИС, Неопределено - объект в который производится загрузка
//  ТребуетсяПоиск	 - Булево - если не передан объект, искать элемент перед заполнением по идентификатору (по умолчанию Истина)
// 
// Возвращаемое значение:
//  СправочникСсылка.ЗаболеванияВЕТИС - ссылка на записанный/обновленный элемент справочника
//
Функция ЗагрузитьЗаболевание(Заболевание, СправочникОбъект = Неопределено, ТребуетсяПоиск = Истина) Экспорт
	
	ЗаписьНового = Ложь;
	
	Если СправочникОбъект = Неопределено Тогда
		
		СправочникСсылка = Справочники.ЗаболеванияВЕТИС.ПустаяСсылка();
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = Справочники.ЗаболеванияВЕТИС.НайтиПоРеквизиту("Идентификатор", Заболевание.Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			СправочникОбъект = Справочники.ЗаболеванияВЕТИС.СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			СправочникОбъект.УстановитьСсылкуНового(Справочники.ЗаболеванияВЕТИС.ПолучитьСсылку(Новый УникальныйИдентификатор(Заболевание.Идентификатор)));
			ЗаписьНового = Истина;
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Идентификатор       = Заболевание.Идентификатор;
	СправочникОбъект.ИдентификаторВерсии = Заболевание.ИдентификаторВерсии;
	СправочникОбъект.Статус              = Заболевание.Статус;
	СправочникОбъект.Наименование        = Заболевание.Наименование;
	СправочникОбъект.ТребуетсяЗагрузка   = Ложь;
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли