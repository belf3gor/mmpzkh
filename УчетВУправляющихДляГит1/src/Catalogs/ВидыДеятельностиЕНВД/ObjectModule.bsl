#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Владелец") Тогда
			Если ТипЗнч(ДанныеЗаполнения.Владелец) = Тип("СписокЗначений") И ДанныеЗаполнения.Количество() > 0 Тогда
				ДанныеЗаполнения.Владелец = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(ДанныеЗаполнения.Владелец[0].Значение);
			КонецЕсли;
		Иначе
			Если ДанныеЗаполнения.Свойство("Организация") Тогда
				ДанныеЗаполнения.Вставить("Владелец", ДанныеЗаполнения.Организация);
			Иначе
				ДанныеЗаполнения.Вставить("Владелец", БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
			КонецЕсли;
		КонецЕсли;
		
		Если Не ДанныеЗаполнения.Свойство("ДатаНачала") Тогда
			ДанныеЗаполнения.Вставить("ДатаНачала", НачалоКвартала(ОбщегоНазначения.ТекущаяДатаПользователя()));
		КонецЕсли;
		
	Иначе
		Владелец = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ДатаНачала = НачалоКвартала(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЗначениеЗаполнено(КодВидаДеятельности) Тогда
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КодВидаДеятельности, "РегистрацияПоМестуНахожденияОрганизации") Тогда
			МассивНепроверяемыхРеквизитов.Добавить("КодПоОКТМО");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаПрекращения) Тогда
		Если ДатаПрекращения <= ДатаНачала Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", Метаданные().Реквизиты.ДатаПрекращения.Синоним);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "ДатаПрекращения", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодВидаДеятельности)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КодВидаДеятельности, "РегистрацияПоМестуНахожденияОрганизации") Тогда
		
		Адрес = "";
		КодПоОКТМО = "";
		
	КонецЕсли;
	
	АдресСтрокой = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(Адрес);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаНачала      = НачалоКвартала(ОбщегоНазначения.ТекущаяДатаПользователя());
	ДатаПрекращения = '00010101';
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли