#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗначениеПоУмолчанию() Экспорт
	
	Результат = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТипыЦенНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.ТипыЦенНоменклатуры КАК ТипыЦенНоменклатуры";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьКонстантуИспользоватьТипыЦенНоменклатуры() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Константы.ИспользоватьТипыЦенНоменклатуры.Установить(КоличествоТиповЦен() > 0);
	
КонецПроцедуры

Процедура ПроверитьЗначениеОпцииИспользоватьТипыЦенНоменклатуры() Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры") И КоличествоТиповЦен() > 0 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Константы.ИспользоватьТипыЦенНоменклатуры.Установить(Истина);
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Функция КоличествоТиповЦен()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	Справочник.ТипыЦенНоменклатуры КАК ТипыЦенНоменклатуры";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	Иначе
		Количество = 0;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

#КонецОбласти

#КонецЕсли