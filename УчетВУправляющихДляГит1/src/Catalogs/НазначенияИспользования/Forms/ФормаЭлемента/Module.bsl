#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	УстановитьЗначенияРасшифровок(
		Объект.Владелец,
		ПредставлениеЕдИзмерения,
		Объект.СрокПолезногоИспользования,
		РашифровкаСрокПолезногоИспользования);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	УстановитьЗначенияРасшифровок(
		Объект.Владелец,
		ПредставлениеЕдИзмерения,
		Объект.СрокПолезногоИспользования,
		РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПогашенияСтоимостиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияПриИзменении(Элемент)
	
	УстановитьЗначенияРасшифровок(
		Объект.Владелец,
		ПредставлениеЕдИзмерения,
		Объект.СрокПолезногоИспользования,
		РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьЗначенияРасшифровок(Номенклатура, ПредставлениеЕдИзмерения, СрокИспользования, РашифровкаСрокИспользования)

	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ПредставлениеЕдИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения.Наименование");
	Иначе
		ПредставлениеЕдИзмерения = ""
	КонецЕсли; 

	РашифровкаСрокИспользования = УправлениеВнеоборотнымиАктивамиКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользования);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Если Форма.Объект.СпособПогашенияСтоимости = 
		ПредопределенноеЗначение("Перечисление.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию") Тогда
		
		Форма.Элементы.ГруппаСрокИспользованияИРасшифровка.Подсказка = 
			НСтр("ru='Для способа погашения стоимости при передаче в эксплуатацию указывается справочно'");
	Иначе
		Форма.Элементы.ГруппаСрокИспользованияИРасшифровка.Подсказка = "";
	КонецЕсли;
	
	ПропорциональноОбъемуПродукцииРабот = Форма.Объект.СпособПогашенияСтоимости
		= ПредопределенноеЗначение("Перечисление.СпособыПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот");
		
	Форма.Элементы.ОбщийОбъемПродукцииРабот.Видимость = ПропорциональноОбъемуПродукцииРабот;
	Форма.Элементы.ГруппаСрокИспользованияИРасшифровка.Видимость = Не ПропорциональноОбъемуПродукцииРабот;

КонецПроцедуры

#КонецОбласти