#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура        = Параметры.Номенклатура;
	ПериодичностьУслуги = Параметры.ПериодичностьУслуги;
	Дата                = Параметры.Дата;
	СформироватьТекстВопроса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПериодичностьУслуги(Команда)
	
	Структура = Новый Структура;
	Структура.Вставить("Содержание", НовоеСодержание(Номенклатура, ПериодичностьУслуги, Дата));
	Закрыть(Структура);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьТекстВопроса()

	ВставляемыеЗначения = Новый Структура;
	ВставляемыеЗначения.Вставить("Периодичность", НРег(ПериодичностьУслуги));
	ВставляемыеЗначения.Вставить("ПериодичностьРодительныйПадеж", ПериодичностьРодительныйПадеж());
	ВставляемыеЗначения.Вставить("Номенклатура", Номенклатура);
	
	// Для примера покажем следующий период
	Если ПериодичностьУслуги = Перечисления.Периодичность.Месяц Тогда
		ДатаПримера = ДобавитьМесяц(Дата, 1);
	Иначе
		ДатаПримера = ДобавитьМесяц(Дата, 3);
	КонецЕсли;
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Наименование, НаименованиеПолное");
	
	НаименованиеНоменклатуры = ?(ПустаяСтрока(РеквизитыНоменклатуры.НаименованиеПолное), 
		РеквизитыНоменклатуры.Наименование,
		РеквизитыНоменклатуры.НаименованиеПолное);
	
	ПримерСодержания = РаботаСНоменклатуройКлиентСервер.СодержаниеУслуги(
		НаименованиеНоменклатуры,
		ПериодичностьУслуги,
		ДатаПримера);
	ВставляемыеЗначения.Вставить("ПримерСодержания", ПримерСодержания);
	
	// Сформируем строку
	ШаблонСтроки = 
		НСтр("ru='Программа может автоматически добавлять [Периодичность] к содержанию услуги.
		|
		|В следующий раз содержание услуги будет выглядеть так:
		|""[ПримерСодержания]""
		|
		|Включить автоматическое добавление [ПериодичностьРодительныйПадеж]?'");
	
	Элементы.ТекстВопроса.Заголовок = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		ШаблонСтроки, ВставляемыеЗначения);
	
КонецПроцедуры

&НаСервере
Функция ПериодичностьРодительныйПадеж()
	
	Если ПериодичностьУслуги = Перечисления.Периодичность.Месяц Тогда
		Возврат НСтр("ru='месяца'");
	ИначеЕсли ПериодичностьУслуги = Перечисления.Периодичность.Квартал Тогда
		Возврат НСтр("ru='квартала'");
	Иначе
		Возврат НРег(ПериодичностьУслуги);
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовоеСодержание(Знач Номенклатура, Знач ПериодичностьУслуги, Знач Дата)
	
	// Заполним периодичность услуги в номенклатуре
	НоменклатураОбъект = Номенклатура.ПолучитьОбъект();
	НоменклатураОбъект.ПериодичностьУслуги = ПериодичностьУслуги;
	НоменклатураОбъект.Записать();
	
	НаименованиеНоменклатуры = ?(ПустаяСтрока(НоменклатураОбъект.НаименованиеПолное), 
		НоменклатураОбъект.Наименование,
		НоменклатураОбъект.НаименованиеПолное);
	
	// Сформируем новое содержание
	Содержание = РаботаСНоменклатуройКлиентСервер.СодержаниеУслуги(
		НаименованиеНоменклатуры,
		ПериодичностьУслуги,
		Дата);
		
	Возврат Содержание;
	
КонецФункции

#КонецОбласти

