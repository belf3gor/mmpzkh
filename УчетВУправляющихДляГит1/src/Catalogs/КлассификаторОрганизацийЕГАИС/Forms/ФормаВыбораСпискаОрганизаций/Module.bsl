
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОрганизацииЕГАИС.Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК ОрганизацииЕГАИС
	|ГДЕ
	|	ОрганизацииЕГАИС.СоответствуетОрганизации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОрганизацииЕГАИС.Наименование");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаОрганизацииЕГАИС.Добавить();
		НоваяСтрока.ОрганизацияЕГАИС = Выборка.Ссылка;
		НоваяСтрока.Выбрана = Параметры.ОрганизацииЕГАИС.Найти(Выборка.Ссылка) <> Неопределено;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Настройки.Очистить();
	
	СохраненнаяТаблицаОрганизацийЕГАИС = Настройки.Получить("ТаблицаОрганизацииЕГАИС");
	Если СохраненнаяТаблицаОрганизацийЕГАИС = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СохраненнаяТаблицаОрганизацийЕГАИС) <> Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из ТаблицаОрганизацииЕГАИС Цикл
		НайденнаяСтрока = СохраненнаяТаблицаОрганизацийЕГАИС.Найти(СтрокаТЧ.ОрганизацияЕГАИС, "ОрганизацияЕГАИС");
		Если НайденнаяСтрока = Неопределено Тогда
			СтрокаТЧ.Выбрана = Ложь;
		Иначе
			СтрокаТЧ.Выбрана = НайденнаяСтрока.Выбрана;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ТаблицаОрганизацииЕГАИС Цикл
		СтрокаТЧ.Выбрана = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ТаблицаОрганизацииЕГАИС Цикл
		СтрокаТЧ.Выбрана = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	РезультатВыбора = Новый Массив;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбрана", Истина);
	НайденныеСтроки = ТаблицаОрганизацииЕГАИС.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		РезультатВыбора.Добавить(СтрокаТЧ.ОрганизацияЕГАИС);
	КонецЦикла;
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

