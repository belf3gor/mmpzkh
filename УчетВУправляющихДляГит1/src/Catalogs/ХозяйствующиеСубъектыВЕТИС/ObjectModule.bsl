
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозяйствующиеСубъектыВЕТИС.Ссылка КАК Ссылка,
		|	ХозяйствующиеСубъектыВЕТИС.Наименование  КАК Наименование,
		|	ХозяйствующиеСубъектыВЕТИС.Идентификатор КАК Идентификатор
		|ИЗ
		|	Справочник.ХозяйствующиеСубъектыВЕТИС КАК ХозяйствующиеСубъектыВЕТИС
		|ГДЕ
		|	ХозяйствующиеСубъектыВЕТИС.Контрагент = &Контрагент
		|	И ХозяйствующиеСубъектыВЕТИС.Ссылка <> &ТекущийХозяйствующийСубъект";
		
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("ТекущийХозяйствующийСубъект", Ссылка);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			
			Если СоответствуетОрганизации Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru='Данная организация уже сопоставлена другому хозяйствующему субъекту с идентификатором %1.'"),
				                           Выборка.Идентификатор);
			Иначе
				ТекстСообщения = СтрШаблон(НСтр("ru='Данный контрагент уже сопоставлен другому хозяйствующему субъекту (%1).'"),
				                           Выборка.Наименование);
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения ,
			                                                  ЭтотОбъект,
			                                                  "Контрагент",
			                                                  ,
			                                                  Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Предприятия.Количество() > 1 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	УказанныеПредприятия.Предприятие            КАК Предприятие,
		|	УказанныеПредприятия.ТорговыйОбъект         КАК ТорговыйОбъект,
		|	УказанныеПредприятия.ПроизводственныйОбъект КАК ПроизводственныйОбъект,
		|	УказанныеПредприятия.НомерСтроки            КАК НомерСтроки
		|ПОМЕСТИТЬ УказанныеПредприятия
		|ИЗ
		|	&УказанныеПредприятия КАК УказанныеПредприятия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УказанныеПредприятия.ТорговыйОбъект                    КАК ТорговыйОбъект,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УказанныеПредприятия.Предприятие) КАК Количество
		|ПОМЕСТИТЬ ТорговыеОбъекты
		|ИЗ
		|	УказанныеПредприятия КАК УказанныеПредприятия
		|ГДЕ
		|	УказанныеПредприятия.ТорговыйОбъект НЕ В (&ПустойТорговыйОбъект)
		|СГРУППИРОВАТЬ ПО
		|	УказанныеПредприятия.ТорговыйОбъект
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УказанныеПредприятия.Предприятие) > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УказанныеПредприятия.ПроизводственныйОбъект            КАК ПроизводственныйОбъект,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УказанныеПредприятия.Предприятие) КАК Количество
		|ПОМЕСТИТЬ ПроизводственныеОбъекты
		|ИЗ
		|	УказанныеПредприятия КАК УказанныеПредприятия
		|ГДЕ
		|	УказанныеПредприятия.ПроизводственныйОбъект НЕ В (&ПустойПроизводственныйОбъект)
		|СГРУППИРОВАТЬ ПО
		|	УказанныеПредприятия.ПроизводственныйОбъект
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УказанныеПредприятия.Предприятие) > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТорговыеОбъекты.ТорговыйОбъект            КАК Объект,
		|	МИНИМУМ(УказанныеПредприятия.НомерСтроки) КАК НомерСтроки,
		|	ТорговыеОбъекты.Количество                КАК Количество,
		|	ИСТИНА                                    КАК ЭтоТорговыйОбъект
		|ИЗ
		|	ТорговыеОбъекты КАК ТорговыеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УказанныеПредприятия КАК УказанныеПредприятия
		|		ПО ТорговыеОбъекты.ТорговыйОбъект = УказанныеПредприятия.ТорговыйОбъект
		|
		|СГРУППИРОВАТЬ ПО
		|	ТорговыеОбъекты.ТорговыйОбъект,
		|	ТорговыеОбъекты.Количество
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроизводственныеОбъекты.ПроизводственныйОбъект,
		|	МИНИМУМ(УказанныеПредприятия.НомерСтроки),
		|	ПроизводственныеОбъекты.Количество,
		|	ЛОЖЬ
		|ИЗ
		|	ПроизводственныеОбъекты КАК ПроизводственныеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УказанныеПредприятия КАК УказанныеПредприятия
		|		ПО ПроизводственныеОбъекты.ПроизводственныйОбъект = УказанныеПредприятия.ПроизводственныйОбъект
		|
		|СГРУППИРОВАТЬ ПО
		|	ПроизводственныеОбъекты.ПроизводственныйОбъект,
		|	ПроизводственныеОбъекты.Количество
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	УказанныеПредприятия.Предприятие КАК Предприятие,
		|	УказанныеПредприятия.НомерСтроки,
		|	Неопределено,
		|	Неопределено
		|ИЗ
		|	УказанныеПредприятия КАК УказанныеПредприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УказанныеПредприятия КАК ДублированныеУказанныеПредприятия
		|		ПО УказанныеПредприятия.Предприятие = ДублированныеУказанныеПредприятия.Предприятие
		|		И УказанныеПредприятия.НомерСтроки <> ДублированныеУказанныеПредприятия.НомерСтроки
		|ГДЕ
		|	УказанныеПредприятия.ТорговыйОбъект В (&ПустойТорговыйОбъект)
		|	И УказанныеПредприятия.ПроизводственныйОбъект В (&ПустойПроизводственныйОбъект)";
		
		Запрос.УстановитьПараметр("УказанныеПредприятия",         Предприятия.Выгрузить());
		Запрос.УстановитьПараметр("ПустойТорговыйОбъект",         ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа(Метаданные.ОпределяемыеТипы.ТорговыйОбъектВЕТИС));
		Запрос.УстановитьПараметр("ПустойПроизводственныйОбъект", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа(Метаданные.ОпределяемыеТипы.ПроизводственныйОбъектВЕТИС));
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ЭтоТорговыйОбъект = Истина Тогда
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Торговый объект %1 сопоставлен более чем одному предприятию.'"), Выборка.Объект);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				                                                  ЭтотОбъект,
				                                                  ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Предприятия", Выборка.НомерСтроки, "ТорговыйОбъект")
				                                                  ,,Отказ);
				
			ИначеЕсли Выборка.ЭтоТорговыйОбъект = Ложь Тогда
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Производственный объект %1 сопоставлен более чем одному предприятию.'"), Выборка.Объект);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, 
				                                                  ЭтотОбъект,
				                                                  ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Предприятия", Выборка.НомерСтроки, "ПроизводственныйОбъект")
				                                                  ,,Отказ);
				
			Иначе
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Предприятию %1, указанному для множественного сопоставления, ничего не сопоставлено.'"), Выборка.Объект);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, 
				                                                  ЭтотОбъект,
				                                                  ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Предприятия", Выборка.НомерСтроки, "Предприятие")
				                                                  ,,Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияВЕТИС.УстановитьПометкуУдаленияПрисоединенныхФайловХозяйствующегоСубъекта(ЭтотОбъект, Отказ);
	
	КоличествоПредприятий = Предприятия.Количество();
	ЕстьНесопоставленные  = Ложь;
	
	Для Каждого СтрокаПредприятия Из Предприятия Цикл
		
		СтрокаСопоставлена = ЗначениеЗаполнено(СтрокаПредприятия.ТорговыйОбъект);
		
		Если СоответствуетОрганизации Тогда
			СтрокаСопоставлена = СтрокаСопоставлена Или ЗначениеЗаполнено(СтрокаПредприятия.ПроизводственныйОбъект);
		Иначе
			СтрокаПредприятия.ПроизводственныйОбъект = Неопределено;
		КонецЕсли;
		
		ЕстьНесопоставленные = ЕстьНесопоставленные Или НЕ СтрокаСопоставлена;
		
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.ЭтоНовый Тогда
		РегистрыСведений.ПредприятияЗонОтветственностиВЕТИС.УдалитьПредприятияХозяйствующегоСубъектаИзЗонОтветственности(Ссылка,, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		РегистрыСведений.ПредприятияЗонОтветственностиВЕТИС.ДобавитьПредприятияВЗоныОтветственностиПоНаследованиюАдреса(Ссылка,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#КонецЕсли