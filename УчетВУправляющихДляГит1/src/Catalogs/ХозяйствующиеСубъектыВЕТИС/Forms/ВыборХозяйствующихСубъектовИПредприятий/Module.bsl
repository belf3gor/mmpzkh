#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастроитьОформлениеФормы();
	
	ОрганизацииПредприятияВЕТИС = Новый Соответствие;
	Для Каждого СтрокаОрганизация Из Параметры.ОрганизацииПредприятияВЕТИС.ПолучитьЭлементы() Цикл 
		МассивПредприятий = Новый Массив;
		МассивПредприятий.Добавить(Ложь);
		Для Каждого СтрокаПредприятие Из СтрокаОрганизация.ПолучитьЭлементы() Цикл
			МассивПредприятий.Добавить(СтрокаПредприятие.ХозяйствующийСубъектПредприятиеВЕТИС);
		КонецЦикла;
		ОрганизацииПредприятияВЕТИС.Вставить(СтрокаОрганизация.ХозяйствующийСубъектПредприятиеВЕТИС, МассивПредприятий);
	КонецЦикла;
	
	РежимБезПредприятий = Параметры.Свойство("РежимБезПредприятий");
	
	ОбновитьДерево(ОрганизацииПредприятияВЕТИС);
	
	ТекущаяСтрокаДерева = Новый Структура("Организация, Предприятие, Уровень");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если Параметры.Свойство("ОрганизацииПредприятияВЕТИС") Тогда
		
		Если Настройки.Получить("ДеревоХозяйствующихСубъектовПредприятийВЕТИС") <> Неопределено Тогда
			
			Настройки.Удалить("ДеревоХозяйствующихСубъектовПредприятийВЕТИС");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СохраненнаяТаблицаОрганизацийВЕТИС = Настройки.Получить("ДеревоХозяйствующихСубъектовПредприятийВЕТИС");
	
	Если ТипЗнч(СохраненнаяТаблицаОрганизацийВЕТИС) <> Тип("ДеревоЗначений") Тогда
		Если Параметры.Свойство("ОрганизацииПредприятияВЕТИС")=Неопределено Тогда
			ОбновитьДеревоФормы();
		Иначе
			Выборка = ВыборкаПоОтборамФормы();
			Если ОтборФормыСоответствуетДереву(Выборка) Тогда
				Выборка.Сбросить();
				ОбновитьДеревоФормы(Выборка);
			Иначе 
				СброситьУстановленныеНастройкиФормы();
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОбновитьДеревоФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	РезультатСХэшем = ИнтеграцияВЕТИСКлиентСервер.ДеревоОтбораОрганизацииВЕТИСВМассивСтруктур(ДеревоХозяйствующихСубъектовПредприятийВЕТИС, Ложь);
	Настройки.Вставить("ХэшСтроки", РезультатСХэшем.ХэшСтроки);
	Настройки.Вставить("ПредставлениеОрганизации", РезультатСХэшем.ПредставлениеОрганизации);
	Настройки.Вставить("ПредставлениеПредприятия", РезультатСХэшем.ПредставлениеПредприятия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкиОтображенияПриИзменении(Элемент)
	ОбновитьДеревоФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТолькоДоступныеПользователюВЕТИСПриИзменении(Элемент)
	ОбновитьДеревоФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ПолучитьЭлементы() Цикл
		СтрокаТЧ.Выбрана = Истина;
		Для Каждого СтрокаПредприятие Из СтрокаТЧ.ПолучитьЭлементы() Цикл 
			СтрокаПредприятие.Выбрана = Истина;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ПолучитьЭлементы() Цикл
		СтрокаТЧ.Выбрана = Ложь;
		Для Каждого СтрокаПредприятие Из СтрокаТЧ.ПолучитьЭлементы() Цикл 
			СтрокаПредприятие.Выбрана = Ложь;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ХозяйствующиеСубъектыВЕТИС = ИнтеграцияВЕТИСКлиентСервер.ДеревоОтбораОрганизацииВЕТИСВМассивСтруктур(ДеревоХозяйствующихСубъектовПредприятийВЕТИС, Ложь);
	Закрыть(ХозяйствующиеСубъектыВЕТИС);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоХозяйствующихСубъектовПредприятийВЕТИС

&НаКлиенте
Процедура ТаблицаОрганизацииВЕТИСВыбранаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ТекущиеДанные;
	ОрганизацияГруппа = ТекущиеДанные.ПолучитьРодителя();
	Если ТекущиеДанные.Выбрана Тогда 
		Если НЕ(ОрганизацияГруппа = Неопределено)Тогда 
			ОрганизацияГруппа.Выбрана = Истина;
		Иначе 
			Для Каждого ПодстрокаПредприятия Из ТекущиеДанные.ПолучитьЭлементы() Цикл 
				ПодстрокаПредприятия.Выбрана = Истина;
			КонецЦикла;
		КонецЕсли;
	Иначе 
		Если ОрганизацияГруппа = Неопределено Тогда 
			Для Каждого ПодстрокаПредприятия Из ТекущиеДанные.ПолучитьЭлементы() Цикл 
				ПодстрокаПредприятия.Выбрана = Ложь;
			КонецЦикла;
		Иначе 
			ОрганизацияГруппа.Выбрана = Ложь;
			Для Каждого ПодстрокаПредприятия Из ОрганизацияГруппа.ПолучитьЭлементы() Цикл 
				ОрганизацияГруппа.Выбрана = ОрганизацияГруппа.Выбрана ИЛИ ПодстрокаПредприятия.Выбрана;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХозяйствующихСубъектовПредприятийВЕТИСПередРазворачиванием(Элемент, Строка, Отказ)
	
	СтрокаДерева = ДеревоХозяйствующихСубъектовПредприятийВЕТИС.НайтиПоИдентификатору(Строка);
	Если НЕ(СтрокаДерева = Неопределено) Тогда
		СтрокаДерева.Свернута = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХозяйствующихСубъектовПредприятийВЕТИСПередСворачиванием(Элемент, Строка, Отказ)
	
	СтрокаДерева = ДеревоХозяйствующихСубъектовПредприятийВЕТИС.НайтиПоИдентификатору(Строка);
	Если НЕ(СтрокаДерева = Неопределено) Тогда
		СтрокаДерева.Свернута = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХозяйствующихСубъектовПредприятийВЕТИСПриАктивизацииСтроки(Элемент)
	
	Если ДеревоОбновлено Тогда
		
		Для Каждого СтрокаДерева Из ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ПолучитьЭлементы() Цикл
			Если СтрокаДерева.Свернута Тогда
				Элементы.ДеревоХозяйствующихСубъектовПредприятийВЕТИС.Свернуть(СтрокаДерева.ПолучитьИдентификатор());
			Иначе 
				Элементы.ДеревоХозяйствующихСубъектовПредприятийВЕТИС.Развернуть(СтрокаДерева.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;
		ДеревоОбновлено = Ложь;
		
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		ТекущаяСтрокаДерева.Уровень = 1;
		ТекущаяСтрокаДерева.Организация = ТекущиеДанные.ХозяйствующийСубъектПредприятиеВЕТИС;
	Иначе
		ТекущаяСтрокаДерева.Уровень = 2;
		ТекущаяСтрокаДерева.Организация = ТекущиеДанные.ПолучитьРодителя().ХозяйствующийСубъектПредприятиеВЕТИС;
		ТекущаяСтрокаДерева.Предприятие = ТекущиеДанные.ХозяйствующийСубъектПредприятиеВЕТИС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХозяйствующихСубъектовПредприятийВЕТИСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ТекущиеДанные;
	
	ПоказатьЗначение(, ТекущиеДанные.ХозяйствующийСубъектПредприятиеВЕТИС);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьУстановленныеНастройкиФормы()
	
	НастройкиОтображения = 0;
	ОтображатьТолькоДоступныеПользователюВЕТИС = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОформлениеФормы()
	
	ТекущийПользовательВЕТИС = ПользователиВЕТИС.ТекущийПользовательВЕТИС();
	СкрыватьНастройкиОтображения = Истина;
	
	Если ИспользуетсяУполномоченноеГашение() Тогда
		СкрыватьНастройкиОтображения = Ложь;
	Иначе
		Элементы.НастройкиОтображения.СписокВыбора.Удалить(3);
	КонецЕсли;
	
	Если ИнтеграцияВЕТИС.ИспользуетсяКомиссияПриЗакупкахИлиПереработкаДавальческогоСырья() Тогда
		СкрыватьНастройкиОтображения = Ложь;
	Иначе
		Элементы.НастройкиОтображения.СписокВыбора.Удалить(2);
	КонецЕсли;
	
	Если СкрыватьНастройкиОтображения Тогда
		Элементы.НастройкиОтображения.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ОтображатьТолькоДоступныеПользователюВЕТИС.Заголовок = НСтр(СтрШаблон("ru = 'Доступные для пользователя ВЕТИС: %1'",ТекущийПользовательВЕТИС));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоФормы(Выборка = Неопределено)
	
	ОбновитьДерево(ОрганизацииПредприятияВЕТИСИзДерева(), Выборка);
	
КонецПроцедуры

&НаСервере
Функция ОрганизацииПредприятияВЕТИСИзДерева()
	
	ОрганизацииПредприятияВЕТИС = Новый Соответствие;
	Для Каждого СтрокаОрганизация Из ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ПолучитьЭлементы() Цикл
		Если СтрокаОрганизация.Выбрана Тогда
			МассивПредприятий = Новый Массив;
			МассивПредприятий.Добавить(СтрокаОрганизация.Свернута);
			Для Каждого СтрокаПредприятие Из СтрокаОрганизация.ПолучитьЭлементы() Цикл
				Если СтрокаПредприятие.Выбрана Тогда
					МассивПредприятий.Добавить(СтрокаПредприятие.ХозяйствующийСубъектПредприятиеВЕТИС);
				КонецЕсли;
			КонецЦикла;
			ОрганизацииПредприятияВЕТИС.Вставить(СтрокаОрганизация.ХозяйствующийСубъектПредприятиеВЕТИС,МассивПредприятий);
		КонецЕсли;
	КонецЦикла;
	Возврат ОрганизацииПредприятияВЕТИС;
	
КонецФункции

&НаСервере
Функция ВыборкаПоОтборамФормы()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПользовательВЕТИС",ТекущийПользовательВЕТИС);
	Запрос.УстановитьПараметр("ВсеПользователиВЕТИС",НЕ ОтображатьТолькоДоступныеПользователюВЕТИС);
	
	Запрос.УстановитьПараметр("ОтображатьСобственныеОрганизации",Ложь);
	Запрос.УстановитьПараметр("ОтображатьКомитентовДавальцев",Ложь);
	Запрос.УстановитьПараметр("ОтображатьКонтрагентовУполномоченногоГашения",Ложь);
	Если НастройкиОтображения = 0 Тогда
		Запрос.УстановитьПараметр("ОтображатьСобственныеОрганизации",Истина);
		Запрос.УстановитьПараметр("ОтображатьКомитентовДавальцев",Истина);
		Запрос.УстановитьПараметр("ОтображатьКонтрагентовУполномоченногоГашения",Истина);
	ИначеЕсли НастройкиОтображения = 1 Тогда
		Запрос.УстановитьПараметр("ОтображатьСобственныеОрганизации",Истина);
	ИначеЕсли НастройкиОтображения = 2 Тогда
		Запрос.УстановитьПараметр("ОтображатьКомитентовДавальцев",Истина);
	ИначеЕсли НастройкиОтображения = 3 Тогда
		Запрос.УстановитьПараметр("ОтображатьКонтрагентовУполномоченногоГашения",Истина);
	КонецЕсли;
	Запрос.УстановитьПараметр("ПредставлениеПустогоПредприятия", НСтр("ru = '<Без предприятия>'"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПраваДоступаПользователейВЕТИС.ХозяйствующийСубъект КАК ХозяйствующийСубъект
	|ПОМЕСТИТЬ ДоступныеХозяйствующиеСубъекты
	|ИЗ
	|	РегистрСведений.ПраваДоступаПользователейВЕТИС КАК ПраваДоступаПользователейВЕТИС
	|ГДЕ
	|	(ПраваДоступаПользователейВЕТИС.ПользовательВЕТИС = &ПользовательВЕТИС
	|			ИЛИ &ВсеПользователиВЕТИС)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПраваДоступаПользователейВЕТИС.ХозяйствующийСубъект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозяйствующийСубъект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АдресаЗонОтветственностиВЕТИС.ХозяйствующийСубъект КАК ХозяйствующийСубъект
	|ПОМЕСТИТЬ ОграничениеПользователя
	|ИЗ
	|	РегистрСведений.АдресаЗонОтветственностиВЕТИС КАК АдресаЗонОтветственностиВЕТИС
	|ГДЕ
	|	АдресаЗонОтветственностиВЕТИС.ПользовательВЕТИС = &ПользовательВЕТИС
	|	И НЕ &ВсеПользователиВЕТИС
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПредприятияЗонОтветственностиВЕТИС.ХозяйствующийСубъект
	|ИЗ
	|	РегистрСведений.ПредприятияЗонОтветственностиВЕТИС КАК ПредприятияЗонОтветственностиВЕТИС
	|ГДЕ
	|	ПредприятияЗонОтветственностиВЕТИС.ПользовательВЕТИС = &ПользовательВЕТИС
	|	И НЕ &ВсеПользователиВЕТИС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозяйствующийСубъект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПредприятияЗонОтветственностиВЕТИС.ХозяйствующийСубъект КАК ХозяйствующийСубъект,
	|	ПредприятияЗонОтветственностиВЕТИС.Предприятие          КАК Предприятие
	|ПОМЕСТИТЬ ПредприятияЗонОтветственности
	|ИЗ
	|	РегистрСведений.ПредприятияЗонОтветственностиВЕТИС КАК ПредприятияЗонОтветственностиВЕТИС
	|ГДЕ
	|	ПредприятияЗонОтветственностиВЕТИС.ПользовательВЕТИС = &ПользовательВЕТИС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозяйствующийСубъект,
	|	Предприятие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка      КАК Ссылка,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие КАК Предприятие
	|ПОМЕСТИТЬ ПредприятияУполномоченногоГашения
	|ИЗ
	|	Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ХозяйствующиеСубъектыВЕТИСПредприятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУполномоченногоГашенияВЕТИС КАК НастройкиУполномоченногоГашенияВЕТИС
	|		ПО ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка = НастройкиУполномоченногоГашенияВЕТИС.Грузополучатель
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредприятияЗонОтветственности КАК ПредприятияЗонОтветственности
	|		ПО (НастройкиУполномоченногоГашенияВЕТИС.Грузоотправитель = ПредприятияЗонОтветственности.ХозяйствующийСубъект)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОграничениеПользователя КАК ОграничениеПользователя
	|		ПО ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка = ОграничениеПользователя.ХозяйствующийСубъект
	|ГДЕ
	|	&ОтображатьКонтрагентовУполномоченногоГашения
	|	И Не ХозяйствующиеСубъектыВЕТИСПредприятия.НеИспользовать
	|	И (ОграничениеПользователя.ХозяйствующийСубъект ЕСТЬ NULL
	|			ИЛИ НЕ ПредприятияЗонОтветственности.Предприятие ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие               КАК Предприятие,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие.Идентификатор КАК ПредприятиеИдентификатор,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка                    КАК Ссылка,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка.Идентификатор      КАК Идентификатор,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка.Наименование       КАК СсылкаНаименование,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие.Наименование                                 КАК ПредприятиеНаименование,
	|	ВЫРАЗИТЬ(ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие.АдресПредставление КАК Строка(100)) КАК АдресПредставление
	|ПОМЕСТИТЬ ХозяйствующиеСубъектыВЕТИСПредприятия
	|ИЗ
	|	Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ХозяйствующиеСубъектыВЕТИСПредприятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоступныеХозяйствующиеСубъекты КАК ДоступныеХозяйствующиеСубъекты
	|		ПО (ДоступныеХозяйствующиеСубъекты.ХозяйствующийСубъект = ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредприятияЗонОтветственности КАК ПредприятияЗонОтветственности
	|		ПО ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка = ПредприятияЗонОтветственности.ХозяйствующийСубъект
	|			И ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие = ПредприятияЗонОтветственности.Предприятие
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОграничениеПользователя КАК ОграничениеПользователя
	|		ПО ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка = ОграничениеПользователя.ХозяйствующийСубъект
	|ГДЕ
	|	(ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка.СоответствуетОрганизации
	|				И &ОтображатьСобственныеОрганизации
	|			ИЛИ НЕ ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка.СоответствуетОрганизации
	|				И &ОтображатьКомитентовДавальцев)
	|	И (ОграничениеПользователя.ХозяйствующийСубъект ЕСТЬ NULL
	|			ИЛИ НЕ ПредприятияЗонОтветственности.Предприятие ЕСТЬ NULL)
	|	И Не ХозяйствующиеСубъектыВЕТИСПредприятия.НеИспользовать
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ПредприятияВЕТИС.ПустаяСсылка),
	|	&ПредставлениеПустогоПредприятия,
	|	ХозяйствующиеСубъектыВЕТИС.Ссылка,
	|	ХозяйствующиеСубъектыВЕТИС.Идентификатор,
	|	ХозяйствующиеСубъектыВЕТИС.Наименование,
	|	&ПредставлениеПустогоПредприятия,
	|	""""
	|ИЗ
	|	Справочник.ХозяйствующиеСубъектыВЕТИС КАК ХозяйствующиеСубъектыВЕТИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоступныеХозяйствующиеСубъекты КАК ДоступныеХозяйствующиеСубъекты
	|		ПО (ДоступныеХозяйствующиеСубъекты.ХозяйствующийСубъект = ХозяйствующиеСубъектыВЕТИС.Ссылка)
	|ГДЕ
	|	(ХозяйствующиеСубъектыВЕТИС.СоответствуетОрганизации
	|				И &ОтображатьСобственныеОрганизации
	|			ИЛИ НЕ ХозяйствующиеСубъектыВЕТИС.СоответствуетОрганизации
	|				И &ОтображатьКомитентовДавальцев)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПредприятияУполномоченногоГашения.Предприятие,
	|	ПредприятияУполномоченногоГашения.Предприятие.Идентификатор,
	|	ПредприятияУполномоченногоГашения.Ссылка,
	|	ПредприятияУполномоченногоГашения.Ссылка.Идентификатор,
	|	ПредприятияУполномоченногоГашения.Ссылка.Наименование,
	|	ПредприятияУполномоченногоГашения.Предприятие.Наименование,
	|	ВЫРАЗИТЬ(ПредприятияУполномоченногоГашения.Предприятие.АдресПредставление КАК Строка(100))
	|ИЗ
	|	ПредприятияУполномоченногоГашения КАК ПредприятияУполномоченногоГашения
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Справочник.ПредприятияВЕТИС.ПустаяСсылка),
	|	&ПредставлениеПустогоПредприятия,
	|	ПредприятияУполномоченногоГашения.Ссылка,
	|	ПредприятияУполномоченногоГашения.Ссылка.Идентификатор,
	|	ПредприятияУполномоченногоГашения.Ссылка.Наименование,
	|	&ПредставлениеПустогоПредприятия,
	|	""""
	|ИЗ
	|	ПредприятияУполномоченногоГашения КАК ПредприятияУполномоченногоГашения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие              КАК Предприятие,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.ПредприятиеИдентификатор КАК ПредприятиеИдентификатор,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка                   КАК Ссылка,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Идентификатор            КАК Идентификатор,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.СсылкаНаименование       КАК СсылкаНаименование,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.ПредприятиеНаименование  КАК ПредприятиеНаименование,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.АдресПредставление       КАК АдресПредставление
	|ИЗ
	|	ХозяйствующиеСубъектыВЕТИСПредприятия КАК ХозяйствующиеСубъектыВЕТИСПредприятия
	|УПОРЯДОЧИТЬ ПО
	|	СсылкаНаименование,
	|	ВЫБОР КОГДА Предприятие = ЗНАЧЕНИЕ(Справочник.ПредприятияВЕТИС.ПустаяСсылка) ТОГДА
	|		0
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ УБЫВ,
	|	ПредприятиеНаименование
	|ИТОГИ
	|	МАКСИМУМ(Идентификатор),
	|	МАКСИМУМ(СсылкаНаименование)
	|ПО
	|	Ссылка";
	
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

&НаСервере
Процедура ОбновитьДерево(ОрганизацииПредприятияВЕТИС, Выборка = Неопределено)
	
	Если Выборка = Неопределено Тогда 
		Выборка = ВыборкаПоОтборамФормы();
	КонецЕсли;
	
	ОрганизацииУдалить = МассивИдентификаторов(ДеревоХозяйствующихСубъектовПредприятийВЕТИС);
	
	ХозяйствующиеСубъектыСтроки = ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ПолучитьЭлементы();
	
	Пока Выборка.Следующий() Цикл
		
		ВыбраннаяОрганизация = ОрганизацииПредприятияВЕТИС.Получить(Выборка.Ссылка);
		
		НовыйХозяйствующийСубъект = Неопределено;
		Для Каждого СтрокаОрганизация Из ХозяйствующиеСубъектыСтроки Цикл
			Если СтрокаОрганизация.ХозяйствующийСубъектПредприятиеВЕТИС = Выборка.Ссылка Тогда
				НовыйХозяйствующийСубъект = СтрокаОрганизация;
				ОрганизацииУдалить.Удалить(ОрганизацииУдалить.Найти(СтрокаОрганизация.ПолучитьИдентификатор()));
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НовыйХозяйствующийСубъект = Неопределено Тогда
			НовыйХозяйствующийСубъект = ХозяйствующиеСубъектыСтроки.Добавить();
			НовыйХозяйствующийСубъект.ХозяйствующийСубъектПредприятиеВЕТИС = Выборка.Ссылка;
			НовыйХозяйствующийСубъект.Идентификатор                        = Выборка.Идентификатор;
			НовыйХозяйствующийСубъект.Представление                        = Выборка.СсылкаНаименование;
		КонецЕсли;
		
		НовыйХозяйствующийСубъект.Выбрана                              = ВыбраннаяОрганизация <> Неопределено;
		НовыйХозяйствующийСубъект.Свернута = (ВыбраннаяОрганизация = Неопределено ИЛИ ВыбраннаяОрганизация.Найти(Ложь) = Неопределено);
		
		ПредприятияУдалить = МассивИдентификаторов(НовыйХозяйствующийСубъект);
		
		НовыйХозяйствующийСубъект = НовыйХозяйствующийСубъект.ПолучитьЭлементы();
		ВыборкаПредприятий = Выборка.Выбрать();
		
		НовоеПредприятие = Неопределено;
		Пока ВыборкаПредприятий.Следующий() Цикл
			
			НовоеПредприятие = Неопределено;
			Для Каждого СтрокаПредприятие Из НовыйХозяйствующийСубъект Цикл
				Если СтрокаПредприятие.ХозяйствующийСубъектПредприятиеВЕТИС = ВыборкаПредприятий.Предприятие Тогда
					НовоеПредприятие = СтрокаПредприятие;
					ПредприятияУдалить.Удалить(ПредприятияУдалить.Найти(СтрокаПредприятие.ПолучитьИдентификатор()));
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НовоеПредприятие = Неопределено Тогда
				НовоеПредприятие = НовыйХозяйствующийСубъект.Добавить();
				НовоеПредприятие.ХозяйствующийСубъектПредприятиеВЕТИС = ВыборкаПредприятий.Предприятие;
				НовоеПредприятие.Идентификатор                        = ВыборкаПредприятий.ПредприятиеИдентификатор;
				
				Если ЗначениеЗаполнено(ВыборкаПредприятий.АдресПредставление) Тогда
					НовоеПредприятие.Представление = СтрШаблон(
						"%1 (%2)",
						ВыборкаПредприятий.ПредприятиеНаименование,
						ВыборкаПредприятий.АдресПредставление);
				Иначе
					НовоеПредприятие.Представление = ВыборкаПредприятий.ПредприятиеНаименование;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ВыбраннаяОрганизация <> Неопределено Тогда 
				НовоеПредприятие.Выбрана = ВыбраннаяОрганизация.Найти(ВыборкаПредприятий.Предприятие) <> Неопределено;
			КонецЕсли;
			
		КонецЦикла;
		
		УдалитьЛишниеПоИдентификаторам(НовыйХозяйствующийСубъект,ПредприятияУдалить);
		
	КонецЦикла;
	
	УдалитьЛишниеПоИдентификаторам(ХозяйствующиеСубъектыСтроки,ОрганизацииУдалить);
	
	Если РежимБезПредприятий Тогда
		Для Каждого ХозяйствующийСубъект Из ДеревоХозяйствующихСубъектовПредприятийВЕТИС.ПолучитьЭлементы() Цикл
			Предприятия = ХозяйствующийСубъект.ПолучитьЭлементы();
			Предприятия.Очистить();
		КонецЦикла;
	КонецЕсли;
	
	ДеревоОбновлено = Истина;
	Выборка = Неопределено;
	
КонецПроцедуры

&НаСервере
Функция ОтборФормыСоответствуетДереву(Выборка)
	
	ОрганизацииПредприятияВЕТИС = ОрганизацииПредприятияВЕТИСИзДерева();
	
	Пока Выборка.Следующий() Цикл
		
		ВыбраннаяОрганизация = ОрганизацииПредприятияВЕТИС.Получить(Выборка.Ссылка);
		Если ВыбраннаяОрганизация = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		ВыбраннаяОрганизация.Удалить(0);
		
		ВыборкаПредприятий = Выборка.Выбрать();
		
		Пока ВыборкаПредприятий.Следующий() Цикл
			
			Индекс = ВыбраннаяОрганизация.Найти(ВыборкаПредприятий.Предприятие);
			Если Индекс <> Неопределено Тогда 
				ВыбраннаяОрганизация.Удалить(Индекс);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ВыбраннаяОрганизация.Количество() > 0 Тогда
			Возврат Ложь;
		Иначе 
			ОрганизацииПредприятияВЕТИС.Удалить(Выборка.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат (ОрганизацииПредприятияВЕТИС.Количество() = 0);
	
КонецФункции

&НаСервере
Функция ИспользуетсяУполномоченноеГашение()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиУполномоченногоГашенияВЕТИС.Грузоотправитель КАК Грузоотправитель
	|ИЗ
	|	РегистрСведений.НастройкиУполномоченногоГашенияВЕТИС КАК НастройкиУполномоченногоГашенияВЕТИС";
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервере
Функция МассивИдентификаторов(Родитель)
	
	Результат = Новый Массив;
	Для Каждого ЭлементДерева Из Родитель.ПолучитьЭлементы() Цикл
		Результат.Добавить(ЭлементДерева.ПолучитьИдентификатор());
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УдалитьЛишниеПоИдентификаторам(Коллекция,Идентификаторы)
	
	Для Каждого Идентификатор Из Идентификаторы Цикл
		Коллекция.Удалить(ДеревоХозяйствующихСубъектовПредприятийВЕТИС.НайтиПоИдентификатору(Идентификатор));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти