#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЭтоГруппа И ДатаРождения > ТекущаяДатаСеанса() Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru='Дата рождения не может быть больше текущей'"),
			,
			"ФизическоеЛицо.ДатаРождения",
			,
			Отказ);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаРождения) Тогда
		ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаРождения, "ФизическоеЛицо.ДатаРождения", Отказ, НСтр("ru='Дата рождения'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьНаименованиеСлужебное();
	ЗаполнитьЧастиИмени();
	ЗаполнитьИнициалыИмени();

	ПерсонифицированныйУчетКлиентСервер.УстановитьФорматСтраховогоНомераПФР(СтраховойНомерПФР);
	
	ЗапомнитьРеквизитыПрежнегоСостоянияОбъекта();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьКорпоративныеКарты();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаименованиеСлужебное()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
		
	НаименованиеСлужебное = ФизическиеЛицаЗарплатаКадры.НаименованиеСлужебное(Наименование);
	
КонецПроцедуры

Процедура ЗаполнитьЧастиИмени()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ФИОУстановлены") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСреза = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(, Новый Структура("ФизическоеЛицо", Ссылка));
	
	Если ДанныеСреза.Количество() > 0 И Не ПустаяСтрока(ДанныеСреза[0].Фамилия) Тогда
		ЧастиИмени = ДанныеСреза[0];
	Иначе
		ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЧастиИмени, "Фамилия,Имя,Отчество");
	
КонецПроцедуры

Процедура ЗаполнитьИнициалыИмени()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ИнициалыИмени = Неопределено;
	
	Если ПустаяСтрока(Имя) Тогда
		Возврат;
	КонецЕсли;

	ИнициалыИмени = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыИмени(Имя);
	
КонецПроцедуры

Процедура ЗапомнитьРеквизитыПрежнегоСостоянияОбъекта()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ИменаРеквизитов = ОбщегоНазначения.ВыгрузитьКолонку(Метаданные.Справочники.ФизическиеЛица.Реквизиты, "Имя");
	ПрежниеЗначения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, СтрСоединить(ИменаРеквизитов, ","));
	
	ДополнительныеСвойства.Вставить("ПрежниеЗначения", ПрежниеЗначения);
	
КонецПроцедуры

Процедура УдалитьКорпоративныеКарты()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.КорпоративныеКарты КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ПодотчетноеЛицо = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.КорпоративныеКарты.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли

