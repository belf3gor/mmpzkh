#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ЗарплатаКадрыВызовСервера.ПодготовитьДанныеВыбораКлассификаторовСПорядкомКодов(ДанныеВыбора, Параметры, СтандартнаяОбработка, "Справочник.ТерриториальныеУсловияПФР");
	
	Если ДанныеВыбора <> Неопределено Тогда
		
		Если Параметры.Свойство("ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями") Тогда
			
			ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями = Параметры.ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями;
			Если НЕ ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями Тогда
				
				Для каждого Территория Из СписокТерриторийСОсобымиКлиматическимиУсловиями() Цикл
					ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(Территория.Значение);
					Если ЭлементПрочиеТерритории <> Неопределено Тогда
						ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
					КонецЕсли; 
				КонецЦикла;

			КонецЕсли; 
			
		Иначе
			ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.ПРОЧ"));
			Если ЭлементПрочиеТерритории <> Неопределено Тогда
				ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура НачальноеЗаполнение() Экспорт
	
	ЗаполнитьСправочникТерриториальныеУсловияПФР();
	
КонецПроцедуры

Процедура ЗаполнитьСправочникТерриториальныеУсловияПФР() Экспорт
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч31";
	ОписаниеЭлемента.Наименование = "Работа в зоне отчуждения";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч33";
	ОписаниеЭлемента.Наименование = "Постоянное проживание (работа) на территории зоны проживания с правом на отселение";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч34";
	ОписаниеЭлемента.Наименование = "Постоянное проживание (работа) на территории зоны проживания с льг. социально-экономическим статусом";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);

	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч35";
	ОписаниеЭлемента.Наименование = "Постоянное проживание (работа) в зоне отселения до переселения в другие районы";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);

   	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч36";
	ОписаниеЭлемента.Наименование = "Работа в зоне отселения (по фактической продолжительности)";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);	
КонецПроцедуры

Функция ОписаниеЭлементаТерриториальныхУсловий()
	Возврат Новый Структура("Код, Наименование");	
КонецФункции	

Процедура СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлементаТерриториальныхУсловий)
	Если Не ЗначениеЗаполнено(Справочники.ТерриториальныеУсловияПФР.НайтиПоКоду(ОписаниеЭлементаТерриториальныхУсловий.Код)) Тогда
		Элемент = Справочники.ТерриториальныеУсловияПФР.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Элемент, ОписаниеЭлементаТерриториальныхУсловий);
		Элемент.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		Элемент.Записать();
	КонецЕсли;	
КонецПроцедуры	

#КонецЕсли

Функция СписокТерриторийСОсобымиКлиматическимиУсловиями() Экспорт
	
	СписокТерриторий = Новый СписокЗначений;
	СписокТерриторий.Добавить(Справочники.ТерриториальныеУсловияПФР.МКС);
	СписокТерриторий.Добавить(Справочники.ТерриториальныеУсловияПФР.МКСР);
	СписокТерриторий.Добавить(Справочники.ТерриториальныеУсловияПФР.РКС);
	СписокТерриторий.Добавить(Справочники.ТерриториальныеУсловияПФР.РКСМ);
	СписокТерриторий.Добавить(Справочники.ТерриториальныеУсловияПФР.ПРОЧ);
			
	Возврат СписокТерриторий;
	
КонецФункции

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
// Используется для сопоставления элементов механизмом «Выгрузка/загрузка областей данных»
//
// Возвращаемое значение: Массив(Строка) - массив имен реквизитов, образующих
//  естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
