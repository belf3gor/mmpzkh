
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Документ = Параметры.Документ;
	ЕстьОшибкиПоСтрокам = Ложь;
	
	ТаблицаОшибок = ИнтеграцияВЕТИСВызовСервера.ПолучитьТаблицуОшибок(Документ);
	Если ТаблицаОшибок <> Неопределено И ТаблицаОшибок.Количество() > 0 Тогда
		СписокОшибок.Загрузить(ТаблицаОшибок);
		
		Для Каждого Строка Из СписокОшибок Цикл
			Если Строка.НомерСтрокиСОшибкой >= 0 Тогда
				Строка.Картинка = БиблиотекаКартинок.ПоказатьВСписке;
				ЕстьОшибкиПоСтрокам = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьОшибкиПоСтрокам
			И ТаблицаОшибок.Количество() = 1 Тогда
				
			ОткрытьФормуСтрокой = Истина;
			Возврат;
			
		КонецЕсли;
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОписанияСРазделением;
		Элементы.СписокОшибокПоказатьОшибку.Видимость = ЕстьОшибкиПоСтрокам;
	Иначе
		ОписаниеОшибки = ИнтеграцияВЕТИСВызовСервера.ТекстОшибкиИзПротокола(Параметры.Документ);
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОписаниеБезРазделения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОткрытьФормуСтрокой Тогда
		
		Отказ = Истина;
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Документ", Документ);
		ОткрытьФорму("Справочник.ВЕТИСПрисоединенныеФайлы.Форма.ФормаОшибкиСтрокой",
			ПараметрыОткрытияФормы, ЭтаФорма.ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СписокОшибокПриАктивизацииСтроки(Элемент)
	
	Элементы.СписокОшибокПоказатьОшибку.Доступность = ЗначениеЗаполнено(СписокОшибок[Элемент.ТекущаяСтрока].Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибку(Команда)
	
	ТекущаяСтрока = Элементы.СписокОшибок.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		Оповестить("Закрытие_ПерейтиКСтрокеОшибки", СписокОшибок.Получить(ТекущаяСтрока).НомерСтрокиСОшибкой-1, ЭтаФорма.ИмяФормы);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
