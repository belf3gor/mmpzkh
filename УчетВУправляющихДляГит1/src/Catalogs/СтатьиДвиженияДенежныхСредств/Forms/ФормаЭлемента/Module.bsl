#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ТаблицаПредопределенныхЗначений = Справочники.СтатьиДвиженияДенежныхСредств.ТаблицаПредопределенныхЗначений();
	Если ЗначениеЗаполнено(Объект.ИмяПредопределенныхДанных) Тогда
		СтруктураПоиска = Новый Структура("ИмяПредопределенныхДанных", Объект.ИмяПредопределенныхДанных);
		ТекущееНазначение = ТаблицаПредопределенныхЗначений.Найти(Объект.ИмяПредопределенныхДанных, "ИмяПредопределенныхДанных");
		Если ТекущееНазначение <> Неопределено Тогда
			ИспользованиеПоУмолчанию = ТекущееНазначение.ПредставлениеОперации;
			ИспользованиеПоУмолчаниюТекущееЗначение = ИспользованиеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ИспользованиеПоУмолчанию.СписокВыбора.Очистить();
	Для Каждого ПредопределенныйЭлемент Из ТаблицаПредопределенныхЗначений Цикл
		Если ПредопределенныйЭлемент.Доступность Тогда
			Элементы.ИспользованиеПоУмолчанию.СписокВыбора.Добавить(ПредопределенныйЭлемент.ПредставлениеОперации);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ИзмененоИспользованиеПоУмолчанию") Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		Оповестить("ИзменениеСтатьиДДСПоУмолчанию");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ИспользованиеПоУмолчаниюТекущееЗначение <> ИспользованиеПоУмолчанию Тогда
		ОбработатьИмяПредопределенныхДанных(ТекущийОбъект, Отказ);
		Если Не Отказ Тогда
			ПараметрыЗаписи.Вставить("ИзмененоИспользованиеПоУмолчанию");
			ИспользованиеПоУмолчаниюТекущееЗначение = ИспользованиеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодВидаОперацииНФОНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийКод", Объект.КодВидаОперацииНФО);
	
	ДопПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьКодИзКлассификатораЗавершение", 
		ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("Справочник.СтатьиДвиженияДенежныхСредств.Форма.ВыборВидаОперацииНФО", 
		ПараметрыФормы,,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаОперацииНФООчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьКодИзКлассификатораЗавершение(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Объект.КодВидаОперацииНФО          = РезультатЗакрытия.Код;
		Объект.НаименованиеВидаОперацииНФО = РезультатЗакрытия.Наименование;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИмяПредопределенныхДанных(ТекущийОбъект, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаПредопределнных = Справочники.СтатьиДвиженияДенежныхСредств.ТаблицаПредопределенныхЗначений();
	
	Если НЕ ЗначениеЗаполнено(ИспользованиеПоУмолчанию) Тогда
		ТекущийОбъект.ИмяПредопределенныхДанных = "";
		Возврат;
	КонецЕсли;
	
	ТекущееНазначение = ТаблицаПредопределнных.Найти(ИспользованиеПоУмолчанию, "ПредставлениеОперации");
	Если НЕ ТекущееНазначение = НЕОПРЕДЕЛЕНО Тогда
		НовыйПредопределенный = ТекущееНазначение.ИмяПредопределенныхДанных;
		СтарыйПредопределенный = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатьиДвиженияДенежныхСредств." + НовыйПредопределенный);
		
		Если ЗначениеЗаполнено(СтарыйПредопределенный) И СтарыйПредопределенный <> ТекущийОбъект.Ссылка Тогда
			СтарыйОбъект = СтарыйПредопределенный.ПолучитьОбъект();
			Попытка
				СтарыйОбъект.Заблокировать();
			Исключение
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заблокировать объект %1!'"), СтарыйОбъект)+"
				|"+ ОписаниеОшибки();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, СтарыйПредопределенный, , , Отказ);
				Возврат;
			КонецПопытки;
			СтарыйОбъект.ИмяПредопределенныхДанных = "";
			СтарыйОбъект.Записать();	
			СтарыйОбъект.Разблокировать();
		КонецЕсли;
		ТекущийОбъект.ИмяПредопределенныхДанных = НовыйПредопределенный;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеПоУмолчаниюПриИзменении(Элемент)
	Модифицированность = Истина;
	Если ЗначениеЗаполнено(ИспользованиеПоУмолчанию) Тогда
		ЗаполнитьВидДДСПриИзменениИспользованиеПоумолчаниюНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Перезаполним "ВидДвиженияДенежныхСредств", если новый вид движения не подходит текущей операции по умолчанию:
// вид движения не указан в предопределенных статьях
// или не подходит текущей статье, но подходит другой предопределеной статье
&НаСервере
Процедура ЗаполнитьВидДДСПриИзменениИспользованиеПоумолчаниюНаСервере()
	
	СтруктураПоиска = Новый Структура("ВидДвиженияДенежныхСредств", Объект.ВидДвиженияДенежныхСредств);
	ТаблицаПредопределенныхЗначений = Справочники.СтатьиДвиженияДенежныхСредств.ТаблицаПредопределенныхЗначений();
	ДоступныеВидыОпераций = ТаблицаПредопределенныхЗначений.НайтиСтроки(СтруктураПоиска);
	Если ДоступныеВидыОпераций.Количество() > 0 Тогда
		
		Для каждого ВидОперации Из ДоступныеВидыОпераций Цикл
			Если ИспользованиеПоУмолчанию = ВидОперации.ПредставлениеОперации Тогда
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		ТекущееНазначение = ТаблицаПредопределенныхЗначений.Найти(ИспользованиеПоУмолчанию, "ПредставлениеОперации");
		Если ТекущееНазначение <> Неопределено Тогда
			Объект.ВидДвиженияДенежныхСредств= ТекущееНазначение.ВидДвиженияДенежныхСредств;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
