#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	Если ТекущаяДатаПользователя < '2017-05-01' Тогда
		ПериодНачалаДействия = "2015";
	Иначе
		ПериодНачалаДействия = "2017";
	КонецЕсли;
	ПериодНачалаДействияДоИзменения = ПериодНачалаДействия;
	
	ЗагрузитьКодыВидовОперацийНФО(Параметры.ТекущийКод);
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодНачалаДействияПриИзменении(Элемент)
	
	Если ПериодНачалаДействия = ПериодНачалаДействияДоИзменения Тогда
		// Период не изменился, ничего делать не требуется.
		Возврат;
	КонецЕсли;
	
	ПериодНачалаДействияДоИзменения = ПериодНачалаДействия;
	
	ТекущийКод = Неопределено;
	ТекущиеДанные = Элементы.ВидыОпераций.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийКод = ТекущиеДанные.Код;
	КонецЕсли;
	
	ЗагрузитьКодыВидовОперацийНФО(ТекущийКод);

КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокКодов

&НаКлиенте
Процедура СписокКодовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбраннаяСтрока = Элементы.ВидыОпераций.ТекущиеДанные;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяСтрока.ЭтоГруппа Тогда
		Если Элементы.ВидыОпераций.ТекущийРодитель = Неопределено Тогда
			Элементы.ВидыОпераций.ТекущийРодитель = ВыбраннаяСтрока.ПолучитьИдентификатор();
		Иначе
			Элементы.ВидыОпераций.ТекущийРодитель = Неопределено;
		КонецЕсли;
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент", 0.1, Истина);
	Иначе
		Результат = Новый Структура("Код,Наименование");
		ЗаполнитьЗначенияСвойств(Результат, ВыбраннаяСтрока);
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиКомандФормы
//Код процедур и функций
#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьКодыВидовОперацийНФО(ТекущийКод)
	
	ДанныеВидовОпераций = Справочники.СтатьиДвиженияДенежныхСредств.ДанныеВыбораВидаОперацииНФО(ПериодНачалаДействия);
	
	ВидыОпераций.ПолучитьЭлементы().Очистить();
	
	ЭлементыДерева = ВидыОпераций.ПолучитьЭлементы();
	ТекущаяСтрока  = Неопределено;
	Для каждого СтрокаДанных Из ДанныеВидовОпераций.Строки Цикл
	
		НоваяСтрока = ЭлементыДерева.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		Если ТекущаяСтрока = Неопределено И СтрокаДанных.Код = ТекущийКод Тогда
			ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		Если СтрокаДанных.ЭтоГруппа Тогда
			ЭлементыГруппы = НоваяСтрока.ПолучитьЭлементы();
			Для каждого СтрокаДанныхГруппы Из СтрокаДанных.Строки Цикл
				НоваяСтрока = ЭлементыГруппы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхГруппы);
				Если ТекущаяСтрока = Неопределено И СтрокаДанныхГруппы.Код = ТекущийКод Тогда
					ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	КонецЦикла;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Элементы.ВидыОпераций.ТекущаяСтрока = ТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент()

	ТекущийЭлемент = Элементы.ВидыОпераций;

КонецПроцедуры

#КонецОбласти
