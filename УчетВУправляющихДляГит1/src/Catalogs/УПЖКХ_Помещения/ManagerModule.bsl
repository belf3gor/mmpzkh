
#Область СлужебныеПроцедурыИФункции

// Возвращает доступные типы помещений для передаваемого объекта.
//
Функция ПолучитьДоступныеТипыПомещенияДляОбъекта(Объект) Экспорт
	
	ТипыПомещений = Новый СписокЗначений;
	
	Если ЗначениеЗаполнено(Объект.Родитель) Тогда
		
		// Если это второй уровень иерархии.
		ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Комната);
		ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Кабинет);
		
		Если Объект.Родитель.ВидПомещения.ТипПомещения = Перечисления.КВП_ТипыПомещений.Дом
		 ИЛИ Объект.Родитель.ВидПомещения.ТипПомещения = Перечисления.КВП_ТипыПомещений.Квартира
		 ИЛИ Объект.Родитель.ВидПомещения.ТипПомещения = Перечисления.КВП_ТипыПомещений.Офис Тогда
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Кухня);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Коридор);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Санузел);
		КонецЕсли;

	ИначеЕсли ЗначениеЗаполнено(Объект.Владелец) Тогда
		
		// Если заполнен владелец - здание.
		Если Объект.Владелец.ТипСооружения = Перечисления.УПЖКХ_ТипСооружения.Паркинг Тогда
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Автоместо);
			
		ИначеЕсли Объект.Владелец.Многоквартирное Тогда
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Квартира);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.МестоОбщегоПользования);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Офис);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.ПодсобноеПомещение);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Чердак);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Подвал);
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Апартамент);
			
			// У зданий также могут быть парковочные места.
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Автоместо);
			
		ИначеЕсли Объект.Владелец.ЖилойДомБлокированнойЗастройки Тогда
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Квартира);
			// У ЖД блокированной застройки пока по аналогии с обычными ЖД оставляем парковочное место.
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Автоместо);
		Иначе
			
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Дом);
			
			// У индивидуального дома также может быть парковочное место.
			ТипыПомещений.Добавить(Перечисления.КВП_ТипыПомещений.Автоместо);
			
		КонецЕсли;
		
	Иначе
		
		// Это элемент первого уровня, владелец не указан.
		Для каждого ТекЭлемент из Перечисления.КВП_ТипыПомещений Цикл
			
			Если ТекЭлемент = Перечисления.КВП_ТипыПомещений.Комната
			 ИЛИ ТекЭлемент = Перечисления.КВП_ТипыПомещений.Кухня
			 ИЛИ ТекЭлемент = Перечисления.КВП_ТипыПомещений.Коридор
			 ИЛИ ТекЭлемент = Перечисления.КВП_ТипыПомещений.Санузел
			 ИЛИ ТекЭлемент = Перечисления.КВП_ТипыПомещений.Кабинет Тогда
				Продолжить;
			КонецЕсли;
			
			ТипыПомещений.Добавить(ТекЭлемент);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТипыПомещений;
	
КонецФункции // ПолучитьДоступныеТипыПомещенияДляОбъекта()

#КонецОбласти