
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.ПараметрВладелец.Пустая() Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "Владелец", Параметры.ПараметрВладелец);
		ОтборЗдание = Параметры.ПараметрВладелец;
		ОтборЗданиеИспользование = Истина;
	КонецЕсли;
	
	ПереданМассивПомещений = Параметры.Свойство("ПараметрМассивПомещений") И Не Параметры.ПараметрМассивПомещений.Количество() = 0;
	
	Если ПереданМассивПомещений Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Ссылка", Параметры.ПараметрМассивПомещений,
																ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	Если Не Параметры.ПараметрРодитель.Пустая() Тогда
		Элементы.Список.ТекущийРодитель = Параметры.ПараметрРодитель;
	КонецЕсли;
	
	// Если не передано помещение или массив помещений, то отбор по зданию не нужен.
	Элементы.ГруппаОтборЗдание.Видимость = Параметры.ПараметрРодитель.Пустая() И Не ПереданМассивПомещений;
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма, Элементы.КоманднаяПанель);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриЗакрытии" формы.
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Оповестить("РедактированиеПомещенийЗавершено");
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

// СхемыУчета
&НаКлиенте
// Подключаемый обработчик команды перехода к схеме учета.
Процедура Подключаемый_ОткрытьСхемуУчета(Команда)
	
	ОТР_СхемыУчетаКлиент.Подключаемый_ОткрытьСхемуУчета(ЭтаФорма.ИмяФормы);
	
КонецПроцедуры
// Конец СхемыУчета

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Обработчик события "ПриИзменении" элемента формы ОтборЗданиеИспользование.
//
&НаКлиенте
Процедура ОтборЗданиеИспользованиеПриИзменении(Элемент)
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		"Владелец",
		ОтборЗдание,
		,
		,
		ОтборЗданиеИспользование);
	
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы ОтборЗдание.
//
&НаКлиенте
Процедура ОтборЗданиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборЗдание) Тогда
		ОтборЗданиеИспользование = Истина;
	КонецЕсли;
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		"Владелец",
		ОтборЗдание,
		,
		,
		ОтборЗданиеИспользование);
	
КонецПроцедуры

#КонецОбласти

