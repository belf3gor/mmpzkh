&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РазобратьВходныеПараметры(Параметры);
	ОтрисоватьСодержимоеТекст();
	
	УстановитьТолькоПросмотр();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяФайлаДанныхОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьФайл(СтандартнаяОбработка, ИмяФайлаДанных, АдресФайлаДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПодписиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеИмяФайлаДанныхОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьФайл(СтандартнаяОбработка, ПодтверждениеИмяФайлаДанных, АдресПодтверждениеИмяФайлаДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеИмяФайлаПодписиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РазобратьВходныеПараметры(Параметры)
	
	СтруктураПараметров = Параметры.СтруктураПараметров;
	
	Опись = Параметры.Опись;
	
	ВидДокумента 	= СтруктураПараметров.ВидДокумента;
	Номер 			= СтруктураПараметров.ЗД_Номер;
	Дата 			= СтруктураПараметров.ЗД_Дата;
	Заголовок 		= Строка(ВидДокумента) + " " + Номер + " от " + Формат(Дата, "ДЛФ=DD"); 
	
	ВидДокумента 	= СтруктураПараметров.КНД;
	ИмяФайлаДанных 	= СтруктураПараметров.ИмяФайлаДанных;
	ИмяФайлаПодписи = СтруктураПараметров.ИмяФайлаПодписи;
	
	ПодтверждениеВидДокумента		= СтруктураПараметров.ПодтверждениеКНД;
	ПодтверждениеИмяФайлаДанных 	= СтруктураПараметров.ПодтверждениеИмяФайлаДанных;
	ПодтверждениеИмяФайлаПодписи 	= СтруктураПараметров.ПодтверждениеИмяФайлаПодписи;
	
	ОпределитьАдресаФайловЗагруженногоДокумента();
	
	ВидимостьДокументаОснования(СтруктураПараметров);

КонецПроцедуры

&НаСервере
Процедура ВидимостьДокументаОснования(СтруктураПараметров)
	
	НомерДокументаОснования 	= СтруктураПараметров.НомерДокОсн;
	ДатаДокументаОснования 		= СтруктураПараметров.ДатаДокОсн;
	СведенияДокументаОснования  = СтруктураПараметров.СведенияДокументаОснования;
	
	Если ЗначениеЗаполнено(НомерДокументаОснования) 
		И ЗначениеЗаполнено(ДатаДокументаОснования) Тогда
		
		ПредставлениеДокументаОснования = "Договор " + НомерДокументаОснования + " от " + Формат(ДатаДокументаОснования, "ДЛФ=DD"); 
		
	ИначеЕсли ЗначениеЗаполнено(СведенияДокументаОснования) Тогда
		
		ПредставлениеДокументаОснования = СтруктураПараметров.СведенияДокументаОснования; 
		
	Иначе
		
		Элементы.ПредставлениеДокументаОснования.Видимость = Ложь;
		
	КонецЕсли;

КонецПроцедуры
	
&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьАдресаФайловЗагруженногоДокумента()
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	СписокИменФайлов = Новый СписокЗначений;
	СписокИменФайлов.Добавить(ИмяФайлаДанных);
	СписокИменФайлов.Добавить(ИмяФайлаПодписи);
	СписокИменФайлов.Добавить(ПодтверждениеИмяФайлаДанных);
	СписокИменФайлов.Добавить(ПодтверждениеИмяФайлаПодписи);
	
	СоответствиеИмяИАдреса = КонтекстЭДОСервер.ПолучитьФайлыПринадлежащиеОписи(
		Опись, 
		УникальныйИдентификатор, 
		СписокИменФайлов);
	
	Если СоответствиеИмяИАдреса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	АдресФайлаДанных 					= СоответствиеИмяИАдреса[ИмяФайлаДанных];
	АдресФайлаПодписи 					= СоответствиеИмяИАдреса[ИмяФайлаПодписи];
	АдресПодтверждениеИмяФайлаДанных 	= СоответствиеИмяИАдреса[ПодтверждениеИмяФайлаДанных];
	АдресПодтверждениеИмяФайлаПодписи 	= СоответствиеИмяИАдреса[ПодтверждениеИмяФайлаПодписи];
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТолькоПросмотр()
	
	Если НЕ ЗначениеЗаполнено(ПодтверждениеИмяФайлаДанных) Тогда
		Элементы.СодержимоеИПодтверждение.ТекущаяСтраница = Элементы.ГруппаСодержимое;
	Иначе
		Элементы.СодержимоеИПодтверждение.ТекущаяСтраница = Элементы.ГруппаПодтверждение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(СтандартнаяОбработка, Знач ИмяФайла, Знач АдресФайла)
	
	СтандартнаяОбработка = Ложь;
	
	Расширение = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла);
	
	ЭтоXML = Врег(Расширение) = "XML";
	
	Если ЭтоXML Тогда
		КонтекстЭДОКлиент.ПоказатьHTML(ИмяФайла, АдресФайла, Ложь, Истина);
	Иначе
		ОперацииСФайламиЭДКОКлиент.ОткрытьФайл(АдресФайла, ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтрисоватьСодержимоеТекст()
	
	Если ЗначениеЗаполнено(АдресФайлаДанных) Тогда 
		СодержимоеФайла = ПолучитьИзВременногоХранилища(АдресФайлаДанных);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
		СодержимоеФайла.Записать(ИмяВременногоФайла);
		СодержимоеФайлаДанных = ИмяВременногоФайла;
	Иначе
		СодержимоеФайлаДанных = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

