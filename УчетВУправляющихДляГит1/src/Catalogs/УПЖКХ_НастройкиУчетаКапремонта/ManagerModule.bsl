#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Функция получает элемент справочника для организации.
//
Функция ПолучитьЭлементСправочникаПоОрганизации(Организация) Экспорт
	
	СсылкаНаЭлемент = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УПЖКХ_НастройкиУчетаКапремонта.Ссылка
	|ИЗ
	|	Справочник.УПЖКХ_НастройкиУчетаКапремонта КАК УПЖКХ_НастройкиУчетаКапремонта
	|ГДЕ
	|	УПЖКХ_НастройкиУчетаКапремонта.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Организация);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		СсылкаНаЭлемент = ВыборкаДанных.Ссылка;
	КонецЦикла;
	
	Возврат СсылкаНаЭлемент;
	
КонецФункции // ПолучитьЭлементСправочникаПоОрганизации()

// Процедура предназначена для снятия пометки на удаление элемента, с текущей ссылкой.
//
// Параметры:
//  СсылкаНаЭлемент - ссылка на элемент справочника "Настройки учета по услугам капитальному ремонту".
Процедура РедактироватьПометкуНаУдалениеПоСсылке(СсылкаНаЭлемент, ПометитьЭлемент = Ложь) Экспорт
	
	ОбъектСправочника                 = СсылкаНаЭлемент.ПолучитьОбъект();
	ОбъектСправочника.ПометкаУдаления = ПометитьЭлемент;
	КВП_ЗаписатьОбъект(ОбъектСправочника);
	
КонецПроцедуры // СнятьПометкуНаУдалениеПоСсылке()

// Функция предназначена для оформления надписей полей объектов,
// связанных с ведение раздельного учета по КР.
// 
// Параметры:
//  СтруктураНастроекКапРемонта - структура настроек по услугам капремонта.
// 
// Возвращаемое значение:
//  Структура - содержит параметры оформления надписей формы.
//
//&НаКлиенте
Функция СформироватьСтруктуруОформленияПолейФормы(СтруктураНастроекКапРемонта) Экспорт
	
	// Значения настроек по умолчанию.
	СтруктураОформления = Новый Структура;
	СтруктураОформления.Вставить("СтатусВеденияУчета", "Не задана настройка ведения раздельного учета по услугам капитального ремонта!");
	СтруктураОформления.Вставить("НадписьКнопки",      "Задать");
	СтруктураОформления.Вставить("ЦветаСтатуса",       ЦветаСтиля.УПЖКХ_ТекстСообщенияОПроблемах);
	
	// Возвращаем оформление по умолчанию, если настройки кап. ремонта не определены.
	Если СтруктураНастроекКапРемонта = Неопределено Тогда
		Возврат СтруктураОформления;
	КонецЕсли;
	
	Если Не СтруктураНастроекКапРемонта.НастройкиОпределены Тогда
		СтруктураОформления.СтатусВеденияУчета = "Не задана настройка ведения раздельного учета по услугам капитального ремонта!";
		СтруктураОформления.НадписьКнопки      = "Задать";
		СтруктураОформления.ЦветаСтатуса       = ЦветаСтиля.УПЖКХ_ТекстСообщенияОПроблемах;
	ИначеЕсли СтруктураНастроекКапРемонта.ВедетсяРаздельныйУчет Тогда
		СтруктураОформления.СтатусВеденияУчета = "Ведется раздельный учет по услугам капитального ремонта.";
		СтруктураОформления.НадписьКнопки      = "Изменить";
		СтруктураОформления.ЦветаСтатуса       = ЦветаСтиля.УПЖКХ_ТекстСообщенияИнформации;
	Иначе
		СтруктураОформления.СтатусВеденияУчета = "Не ведется раздельный учет по услугам капитального ремонта.";
		СтруктураОформления.НадписьКнопки      = "Изменить";
		СтруктураОформления.ЦветаСтатуса       = Новый Цвет(0, 0, 0);
	КонецЕсли;
	
	Возврат СтруктураОформления;
	
КонецФункции // СформироватьСтруктуруОформленияПолейФормы()

// Функция предназначена для формирования наименования элемента справочника.
//
// Параметры:
//  Организация - текущая организация.
//
// Возвращаемое значение:
//  Строка - наименование элемента справочника.
//
//&НаСервере
Функция СформироватьНаименованиеЭлемента(Организация) Экспорт
	
	НаименованиеЭлемента = "Настройки для " + Организация.Наименование;
	
	Возврат НаименованиеЭлемента;
	
КонецФункции // СформироватьЗаголовокФормыНаСервере()

#КонецОбласти

#КонецЕсли