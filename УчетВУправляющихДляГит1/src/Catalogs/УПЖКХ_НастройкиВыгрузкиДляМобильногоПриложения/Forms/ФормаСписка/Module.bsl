
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма, Элементы.СписокКоманднаяПанель);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаКлиенте
// Обработка события "ПриОткрытии" формы.
Процедура ПриОткрытии(Отказ)
	
	Попытка
		
		// Попытка загрузить страницу сайта.
		Соединение = Новый HTTPСоединение("vgkh.ru");
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("host", "vgkh.ru");
		Запрос = Новый HTTPЗапрос("/jsk/мобильный_монитор/?lite=Y", Заголовки);
		Ответ  = Соединение.Получить(Запрос);
		
		ОписаниеПриложения = "https://vgkh.ru/jsk/мобильный_монитор/?lite=Y";
		
	Исключение
		
		ИзвлечьОффлайнСтраницу();
		
	КонецПопытки;
	
	УправлениеВидимостьюГиперссылкиРешенияПроблемСJavaScript();
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриНажатии" поля "ПолеОписаниеПриложения".
Процедура ПолеОписаниеПриложенияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УПЖКХ_ОбщегоНазначенияКлиент.ОткрытьСтраницуВБраузере(ДанныеСобытия.Href);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Нажатие" гиперссылки на описание решения проблем со сценариями JavaScript.
//
Процедура НадписьПерейтиНаСтраницуРешенияПроблемСJavaScriptНажатие(Элемент)
	
	УПЖКХ_ОбщегоНазначенияКлиент.ОткрытьСтраницуРешенияПроблемРежимаСовместимостиСIE7();
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ВспомогательныеПроцедурыИФункции

&НаКлиенте
// Подгружает оффлайн-страницу.
Процедура ИзвлечьОффлайнСтраницу()
	
	НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("ЗавершениеПолученияИмениВременногоФайла", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПолученияИмениВременногоФайла (ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	КаталогДопИнфо = ИмяКаталогаВременныхФайлов +Новый УникальныйИдентификатор;
	
	НачатьСозданиеКаталога(Новый ОписаниеОповещения("ЗавершениеСозданияКаталога", ЭтотОбъект), КаталогДопИнфо);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеСозданияКаталога(КаталогДопИнфо, ДополнительныеПараметры) Экспорт
	
	НачатьУдалениеФайлов(Новый ОписаниеОповещения("ЗавершениеУдаленияФайловВоВременномКаталоге", ЭтотОбъект, КаталогДопИнфо), КаталогДопИнфо, "*.*");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеУдаленияФайловВоВременномКаталоге(КаталогДопИнфо) Экспорт
	
	
	АдресХранилища = ПолучитьАдресДвоичныхДанныхОффлайнСтраницыНаСервере("ОффлайнСтраница");
	
	ДвоичныеДанныеОффлайнСтраницы = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(ДвоичныеДанныеОффлайнСтраницы) = Тип("ДвоичныеДанные") Тогда
		
		ИмяАрхива       = "tmp.zip";
		ПолноеИмяАрхива = КаталогДопИнфо + "\" + ИмяАрхива;
		
		Попытка
			ДвоичныеДанныеОффлайнСтраницы.Записать(ПолноеИмяАрхива);
			
			#Если НЕ ВебКлиент Тогда
			Архив = Новый ЧтениеZipФайла(ПолноеИмяАрхива,);
			Архив.ИзвлечьВсе(КаталогДопИнфо, РежимВосстановленияПутейФайловZIP.Восстанавливать);
			#КонецЕсли
			
						
			ИмяФайлаСтраницы = КаталогДопИнфо + "\" + "Мобильный монитор руководителя ЖКХ.html";
			
			ФайлСтраницы = Новый Файл(ИмяФайлаСтраницы);
			
			ОписаниеПриложения = ИмяФайлаСтраницы;
			
		Исключение
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не удалось загрузить информационную страницу.");
		КонецПопытки;
		
	КонецЕсли;
	
	НачатьУдалениеФайлов(Новый ОписаниеОповещения("ЗавершениеУдаленияФайловВоВременномКаталогеАрхив", ЭтотОбъект), КаталогДопИнфо, ИмяАрхива);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеУдаленияФайловВоВременномКаталогеАрхив(ДополнительныеПараметры) Экспорт
	
	// Процедура - заглушка.
КонецПроцедуры

&НаСервере
// Извлекает файлы страницы.
Функция ПолучитьАдресДвоичныхДанныхОффлайнСтраницыНаСервере(ИмяМакета)
	
	ДвоичныеДанные = Справочники.УПЖКХ_НастройкиВыгрузкиДляМобильногоПриложения.ПолучитьМакет(ИмяМакета);
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
// Управляет видимостью гиперссылки с предложением решения проблем со сценариями JavaScript.
//
Процедура УправлениеВидимостьюГиперссылкиРешенияПроблемСJavaScript()
	
	Элементы.НадписьПерейтиНаСтраницуРешенияПроблемСJavaScript.Видимость = УПЖКХ_ОбщегоНазначенияКлиент.ПроверитьИспользованиеРежимаСовместимостиСIE7(Элементы.ПолеОписаниеПриложения.ИнформацияПрограммыПросмотра);
	
КонецПроцедуры

#КонецОбласти
