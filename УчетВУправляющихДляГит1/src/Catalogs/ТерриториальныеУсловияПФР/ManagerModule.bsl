#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ЗарплатаКадрыВызовСервера.ПодготовитьДанныеВыбораКлассификаторовСПорядкомКодов(ДанныеВыбора, Параметры, СтандартнаяОбработка, "Справочник.ТерриториальныеУсловияПФР");
	
	Если ДанныеВыбора <> Неопределено Тогда
		
		Если Параметры.Свойство("ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями") Тогда
			
			ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями = Параметры.ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями;
			Если НЕ ВыбиратьТерриторииСОсобымиКлиматическимиУсловиями Тогда
				
				Для каждого Территория Из СписокТерриторийСОсобымиКлиматическимиУсловиями() Цикл
					ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(Территория.Значение);
					Если ЭлементПрочиеТерритории <> Неопределено Тогда
						ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.ПРОЧ"));
			Если ЭлементПрочиеТерритории <> Неопределено Тогда
				ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
			КонецЕсли; 
		КонецЕсли; 
		
		Если Параметры.Свойство("ВыбиратьЗаграничныеТерритории") Тогда
			Если Не Параметры.ВыбиратьЗаграничныеТерритории = Истина Тогда
				ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.ЗАГР"));
				Если ЭлементПрочиеТерритории <> Неопределено Тогда
					ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.ЗАГР"));
			Если ЭлементПрочиеТерритории <> Неопределено Тогда
				ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.Свойство("ВыбиратьТерриторииВСельскойМестности") Тогда
			Если Не Параметры.ВыбиратьЗаграничныеТерритории = Истина Тогда
				ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.СЕЛО"));
				Если ЭлементПрочиеТерритории <> Неопределено Тогда
					ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЭлементПрочиеТерритории = ДанныеВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.СЕЛО"));
			Если ЭлементПрочиеТерритории <> Неопределено Тогда
				ДанныеВыбора.Удалить(ЭлементПрочиеТерритории);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура НачальноеЗаполнение() Экспорт
	
	ЗаполнитьСправочникТерриториальныеУсловияПФР();
	
КонецПроцедуры

Процедура ЗаполнитьСправочникТерриториальныеУсловияПФР() Экспорт
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.ИмяПредопределенныхДанных = "МКС";
	ОписаниеЭлемента.Код = ОписаниеЭлемента.ИмяПредопределенныхДанных;
	ОписаниеЭлемента.Наименование = "Местность, приравненная к районам Крайнего Севера";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.ИмяПредопределенныхДанных = "МКСР";
	ОписаниеЭлемента.Код = ОписаниеЭлемента.ИмяПредопределенныхДанных;
	ОписаниеЭлемента.Наименование = "Местность, приравненная к районам Крайнего Севера, до 2002 г. относившаяся к районам Крайнего Севера";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.ИмяПредопределенныхДанных = "ПРОЧ";
	ОписаниеЭлемента.Код = ОписаниеЭлемента.ИмяПредопределенныхДанных;
	ОписаниеЭлемента.Наименование = "Прочие территории с особыми климатическими условиями";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.ИмяПредопределенныхДанных = "РКС";
	ОписаниеЭлемента.Код = ОписаниеЭлемента.ИмяПредопределенныхДанных;
	ОписаниеЭлемента.Наименование = "Район Крайнего Севера";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.ИмяПредопределенныхДанных = "РКСМ";
	ОписаниеЭлемента.Код = ОписаниеЭлемента.ИмяПредопределенныхДанных;
	ОписаниеЭлемента.Наименование = "Район Крайнего Севера, до 2002 г. являвшийся местностью, приравненной к районам Крайнего Севера";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч31";
	ОписаниеЭлемента.Наименование = "Работа в зоне отчуждения";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч33";
	ОписаниеЭлемента.Наименование = "Постоянное проживание (работа) на территории зоны проживания с правом на отселение";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч34";
	ОписаниеЭлемента.Наименование = "Постоянное проживание (работа) на территории зоны проживания с льг. социально-экономическим статусом";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч35";
	ОписаниеЭлемента.Наименование = "Постоянное проживание (работа) в зоне отселения до переселения в другие районы";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.Код = "Ч36";
	ОписаниеЭлемента.Наименование = "Работа в зоне отселения (по фактической продолжительности)";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
	ОписаниеЭлемента = ОписаниеЭлементаТерриториальныхУсловий();
	ОписаниеЭлемента.ИмяПредопределенныхДанных = "ЗАГР";
	ОписаниеЭлемента.Код = ОписаниеЭлемента.ИмяПредопределенныхДанных;
	ОписаниеЭлемента.Наименование = "Территория за пределами РФ";
	СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлемента);
	
КонецПроцедуры

Функция ОписаниеЭлементаТерриториальныхУсловий()
	Возврат Новый Структура("ИмяПредопределенныхДанных, Код, Наименование");	
КонецФункции	

Процедура СоздатьЭлементТерриториальныхУсловийПФР(ОписаниеЭлементаТерриториальныхУсловий)
	
	Если ЗначениеЗаполнено(ОписаниеЭлементаТерриториальныхУсловий.ИмяПредопределенныхДанных) Тогда
		
		СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ТерриториальныеУсловияПФР." + ОписаниеЭлементаТерриториальныхУсловий.ИмяПредопределенныхДанных);
		Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
			Элемент = СсылкаПредопределенного.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ТерриториальныеУсловияПФР.СоздатьЭлемент();
			Элемент.ИмяПредопределенныхДанных = ОписаниеЭлементаТерриториальныхУсловий.ИмяПредопределенныхДанных;
		КонецЕсли;
		
	Иначе
		
		СсылкаНаОбъект =  Справочники.ТерриториальныеУсловияПФР.НайтиПоКоду(ОписаниеЭлементаТерриториальныхУсловий.Код); 
		Если ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
			Элемент = СсылкаНаОбъект.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ТерриториальныеУсловияПФР.СоздатьЭлемент();
		КонецЕсли;
		
	КонецЕсли; 
	
	ЗаполнитьЗначенияСвойств(Элемент, ОписаниеЭлементаТерриториальныхУсловий);
	Элемент.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	Элемент.Записать();
	
КонецПроцедуры	

#КонецЕсли

Функция СписокТерриторийСОсобымиКлиматическимиУсловиями() Экспорт
	
	СписокТерриторий = СписокСеверныхТерриториальныхУсловий();
	СписокТерриторий.Добавить(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.ПРОЧ"));
	
	Возврат СписокТерриторий;
	
КонецФункции

Функция СписокСеверныхТерриториальныхУсловий() Экспорт
	
	СписокТерриторий = Новый СписокЗначений;
	СписокТерриторий.Добавить(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.МКС"));
	СписокТерриторий.Добавить(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.МКСР"));
	СписокТерриторий.Добавить(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.РКС"));
	СписокТерриторий.Добавить(ПредопределенноеЗначение("Справочник.ТерриториальныеУсловияПФР.РКСМ"));
	
	Возврат СписокТерриторий;
	
КонецФункции

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
// Используется для сопоставления элементов механизмом «Выгрузка/загрузка областей данных».
//
// Возвращаемое значение: Массив(Строка) - массив имен реквизитов, образующих
//  естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
