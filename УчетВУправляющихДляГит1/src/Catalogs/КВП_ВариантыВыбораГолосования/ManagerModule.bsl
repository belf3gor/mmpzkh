
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Обработчик события команды "Вверх" или "Вниз" списка вариантов ответов.
Процедура ПереместитьВариантВверхВниз(ТекСтрока, Направление) Экспорт
	
	ТекущийКод       = ТекСтрока.Код;
	ВладелецВарианта = ТекСтрока.Владелец;
	
	ТаблицаСоседнихВариантов = НайтиСоседниеВариантыГолосования(ТекущийКод, ВладелецВарианта, Направление);
	
	Попытка
		СоседнийВариант      = ТаблицаСоседнихВариантов[1].ВариантГолосования;
		КодСоседнегоВарианта = ТаблицаСоседнихВариантов[1].Код;
	Исключение
		Возврат;
	КонецПопытки;
	
	СпрОбъект2 = СоседнийВариант.ПолучитьОбъект();
	СпрОбъект2.Код = 1000;
	СпрОбъект2.Записать();
	
	СпрОбъект1 = ТекСтрока.ПолучитьОбъект();
	СпрОбъект1.Код = КодСоседнегоВарианта;
	СпрОбъект1.Записать();
	
	СпрОбъект2.Код = ТекущийКод;
	СпрОбъект2.Записать();
	
КонецПроцедуры

// Получает соседнюю запись текущего варианта голосования.
Функция НайтиСоседниеВариантыГолосования(КодВарианта, Владелец, Направление)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	КВП_ВариантыВыбораГолосования.Код КАК Код,
	|	КВП_ВариантыВыбораГолосования.Ссылка КАК ВариантГолосования
	|ИЗ
	|	Справочник.КВП_ВариантыВыбораГолосования КАК КВП_ВариантыВыбораГолосования
	|ГДЕ
	|	НЕ КВП_ВариантыВыбораГолосования.ПометкаУдаления
	|	И КВП_ВариантыВыбораГолосования.Владелец = &Владелец
	|	И КВП_ВариантыВыбораГолосования.Код " + ?(Направление = "Вверх", "<=", ">=") + " &Код
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код " + ?(Направление = "Вверх", "УБЫВ", "ВОЗР") + "";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Код",      КодВарианта);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти 

#КонецЕсли