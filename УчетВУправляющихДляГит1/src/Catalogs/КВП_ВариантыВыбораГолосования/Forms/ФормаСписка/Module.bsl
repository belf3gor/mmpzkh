
#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Перемещает варианты выбора голосования вверх или вниз на сервере.
Процедура ПереместитьВариантВверхВнизНаСервере(ТекСтрока, Направление)
	
	Справочники.КВП_ВариантыВыбораГолосования.ПереместитьВариантВверхВниз(ТекСтрока, Направление);
	
КонецПроцедуры

&НаКлиенте
// Процедура устанавливает отбор по теме в списке вариантов ответов.
Процедура СписокВариантовУстановитьОтборПоВладельцу(Владелец)
	
	Список.Отбор.Элементы.Очистить();
	ТипПолеКомпоновкиДанных      = Новый ПолеКомпоновкиДанных("Владелец");
	ЭлементОтбора                = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = ТипПолеКомпоновкиДанных;
	ЭлементОтбора.ПравоеЗначение = Владелец;
	
КонецПроцедуры //СписокВариантовУстановитьОтборПоВладельцу()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТЧТемыГолосования

&НаКлиенте
// Обработчик события команды "Вверх" списка вариантов ответов.
Процедура КомандаСписокВариантовВверх(Команда)
	
	Если НЕ Элементы.СписокВариантов.ТекущаяСтрока = Неопределено Тогда
		ПереместитьВариантВверхВнизНаСервере(Элементы.СписокВариантов.ТекущаяСтрока, "Вверх");
		Элементы.СписокВариантов.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события команды "Вниз" списка вариантов ответов.
Процедура КомандаСписокВариантовВниз(Команда)
	
	Если НЕ Элементы.СписокВариантов.ТекущаяСтрока = Неопределено Тогда
		ПереместитьВариантВверхВнизНаСервере(Элементы.СписокВариантов.ТекущаяСтрока, "Вниз");
		Элементы.СписокВариантов.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" табл.поля темы голосования.
Процедура ТемыГолосованияПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда 
		СписокВариантовУстановитьОтборПоВладельцу(ПредопределенноеЗначение("Справочник.КВП_ТемыГолосования.ПустаяСсылка"));
	Иначе
		СписокВариантовУстановитьОтборПоВладельцу(Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриАктивизацииСтроки" табл.поля темы голосования.
Процедура ТемыГолосованияПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда 
		СписокВариантовУстановитьОтборПоВладельцу(ПредопределенноеЗначение("Справочник.КВП_ТемыГолосования.ПустаяСсылка"));
	Иначе
		СписокВариантовУстановитьОтборПоВладельцу(Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПослеУдаления" списка тем голосования.
Процедура ТемыГолосованияПослеУдаления(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		СписокВариантовУстановитьОтборПоВладельцу(ПредопределенноеЗначение("Справочник.КВП_ТемыГолосования.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		
		// Установим отбор по владельцу в табличном поле "Темы голосования".
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(ТемыГолосования.Отбор, "Ссылка", Параметры.Отбор.Владелец,
																ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЕсли;
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма, Элементы.СписокВариантовКоманднаяПанель);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти