#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Обработчик обновления БРО, заполняющий данные реквизита ВидОтчета при переходе на очередную версию БРО
Процедура ПеренестиРеквизитВидОтчета() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦиклыОбмена.Ссылка,
	|	ЦиклыОбмена.УдалитьВидОтчета
	|ИЗ
	|	Справочник.ЦиклыОбмена КАК ЦиклыОбмена
	|ГДЕ
	|	(ЦиклыОбмена.УдалитьВидОтчета <> ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ПустаяСсылка)
	|			И ЦиклыОбмена.УдалитьВидОтчета <> ЗНАЧЕНИЕ(Справочник.РегламентированныеОтчеты.ПустаяСсылка)
	|			И ЦиклыОбмена.УдалитьВидОтчета <> НЕОПРЕДЕЛЕНО)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбъектЦО = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектЦО.ВидОтчета 			= Выборка.УдалитьВидОтчета;
		ОбъектЦО.УдалитьВидОтчета 	= Неопределено;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектЦО);
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиУчетныхЗаписейДокументооборота КАК ПользователиУчетныхЗаписей
	| ПО ПользователиУчетныхЗаписей.УчетнаяЗапись = ЭтотСписок.УчетнаяЗапись
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	| ЭтоАвторизованныйПользователь(ПользователиУчетныхЗаписей.Пользователь)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	// инициализируем контекст ЭДО - модуль обработки
	ТекстСообщения = "";
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДО = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	КонтекстЭДО.ОбработкаПолученияФормы("Справочник", "ЦиклыОбмена", ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
