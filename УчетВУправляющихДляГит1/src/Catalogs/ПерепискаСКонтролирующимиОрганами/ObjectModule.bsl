#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.ПриЗаписиОбъекта(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДО.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(СообщениеОснование)
	
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДО.ОбработкаЗаполненияОбъекта(ЭтотОбъект, СообщениеОснование);
	
	ДатаСообщения = ТекущаяДатаСеанса();
	
	Если ТипЗнч(СообщениеОснование) = Тип("Структура")
		И СообщениеОснование.Свойство("Отправитель")
		И ТипЗнч(СообщениеОснование.Отправитель) = Тип("СправочникСсылка.Организации") Тогда
		Организация = СообщениеОснование.Отправитель;
	КонецЕсли;
	
	Если ТипЗнч(СообщениеОснование) = Тип("Структура")
		И СообщениеОснование.Свойство("ОснованиеСсылка")
		И ТипЗнч(СообщениеОснование.ОснованиеСсылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
		
		ДокументыРеализацииПолномочийСсылка = СообщениеОснование.ОснованиеСсылка;
		
		НомерТребования = "";
		ДатаТребования  = Неопределено;
		
		СообщениеОснование.Свойство("Номер", НомерТребования);
		СообщениеОснование.Свойство("Дата", ДатаТребования);
		
		Шаблон = НСтр("ru = 'Ответ на: %1 № %2 от %3'");
		Наименование = СтрШаблон(Шаблон, 
			ДокументыРеализацииПолномочийСсылка.ВидДокумента, 
			НомерТребования,
			Формат(ДатаТребования, "ДЛФ=D"));
			
		Тип 			= Перечисления.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСФНС;
		Статус 			= Перечисления.СтатусыПисем.Исходящее;
		Отправитель 	= ДокументыРеализацииПолномочийСсылка.Организация;
		Получатель 		= ДокументыРеализацииПолномочийСсылка.НалоговыйОрган;
		ДатаСообщения 	= ТекущаяДатаСеанса();
		Требование 		= ДокументыРеализацииПолномочийСсылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИдентификаторОснования = Неопределено;
	ДатаОтправки = Неопределено;
	ДатаСообщения = ТекущаяДатаСеанса();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтотОбъект.Тип <> Перечисления.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСФНС Тогда
		Возврат;
	КонецЕсли;
		
	Вложения = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьСписокВложений(ЭтотОбъект.Ссылка);
	
	КоличествоВложений 		= Вложения.Количество();
	МаксимальнойКоличество 	= ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.МаксимальнойКоличествоФайловВПисьме();
	
	Если КоличествоВложений > МаксимальнойКоличество Тогда
		
		ПревышеноНа = КоличествоВложений - МаксимальнойКоличество;
				
		ТекстСообщения = "Допустимое количество приложенных файлов превышено на %1."; 
		ТекстСообщения = СтрШаблон(ТекстСообщения, ПревышеноНа);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Отказ = Истина;
				
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


