
#Область СлужебныеПроцедурыИФункции

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ПустаяСтрока(ИНН) И НЕ ИННСоответствуетТребованиям(ИНН) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("ИНН организации задан неверно!",,, СтатусСообщения.Важное);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Ссылка.КПП = КПП
	   И НЕ ПустаяСтрока(КПП) И НЕ СтрДлина(КПП) = 9 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("КПП организации должен состоять из 9 символов.",,, СтатусСообщения.Важное);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Ссылка.ОГРН = ОГРН
	   И НЕ ПустаяСтрока(ОГРН) Тогда
		ОшибкаОГРН = Ложь;
		Если НЕ СтрДлина(ОГРН) = 13 Тогда
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("ОГРН организации указан неверно! ОГРН должен состоять из 13 цифр!",,, СтатусСообщения.Важное);
			Отказ = Истина;
			ОшибкаОГРН = Истина;
		КонецЕсли;
		Если НЕ ТолькоЦифрыВСтроке(ОГРН) Тогда
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("ОГРН организации указан неверно! ОГРН должен состоять только из цифр!",,, СтатусСообщения.Важное);
			Отказ = Истина;
			ОшибкаОГРН = Истина;
		КонецЕсли;
		Если НЕ Отказ Тогда
			ОГРН12 = Число(Лев(Строка(ОГРН), 12));
			Если НЕ ОшибкаОГРН И Прав(Формат(ОГРН12 % 11, "ЧН=0; ЧГ=0"), 1) <> Прав(ОГРН, 1) Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Возможно, ОГРН организации указан неверно (контрольный разряд не совпадает с вычисленным)!",,, СтатусСообщения.Внимание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ИННСоответствуетТребованиям(Знач ИНН)
	
	ИНН      = СокрЛП(ИНН);
	ДлинаИНН = СтрДлина(ИНН);
	
	Если НЕ ТолькоЦифрыВСтроке(ИНН) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДлинаИНН = 10 Тогда
		
		КонтрольнаяСумма = 0;
		
		Для Н = 1 По 9 Цикл
			
			Если 	  Н = 1 Тогда
				Множитель = 2;
			ИначеЕсли Н = 2 Тогда
				Множитель = 4;
			ИначеЕсли Н = 3 Тогда
				Множитель = 10;
			ИначеЕсли Н = 4 Тогда
				Множитель = 3;
			ИначеЕсли Н = 5 Тогда
				Множитель = 5;
			ИначеЕсли Н = 6 Тогда
				Множитель = 9;
			ИначеЕсли Н = 7 Тогда
				Множитель = 4;
			ИначеЕсли Н = 8 Тогда
				Множитель = 6;
			ИначеЕсли Н = 9 Тогда
				Множитель = 8;
			КонецЕсли; 
			
			Цифра = Число(Сред(ИНН,Н,1));
			КонтрольнаяСумма = КонтрольнаяСумма + Цифра * Множитель;
			
		КонецЦикла; 
		
		КонтрольныйРазряд = (КонтрольнаяСумма %11) %10;
		
		Если КонтрольныйРазряд <> Число(Сред(ИНН,10,1)) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ТолькоЦифрыВСтроке(Знач СтрокаПроверки, УчитыватьЛидирующиеНули = Истина, УчитыватьПробелы = Истина)
	
	Если ТипЗнч(СтрокаПроверки) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаПроверки) Тогда
		Возврат Истина;
	КонецЕсли; 
	
	Если НЕ УчитыватьПробелы Тогда
		СтрокаПроверки = СтрЗаменить(СтрокаПроверки, " ", "");
	КонецЕсли;
	
	Если НЕ УчитыватьЛидирующиеНули Тогда
		НомерПервойЦифры = 0;
		Для ТекСимвол = 1 По СтрДлина(СтрокаПроверки) Цикл
			НомерПервойЦифры = НомерПервойЦифры + 1;
			КодСимвола = КодСимвола(Сред(СтрокаПроверки, ТекСимвол, 1));
			Если КодСимвола <> 48 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СтрокаПроверки = Сред(СтрокаПроверки, НомерПервойЦифры);
	КонецЕсли;
	
	Для ТекСимвол = 1 По СтрДлина(СтрокаПроверки) Цикл
		КодСимвола = КодСимвола(Сред(СтрокаПроверки, ТекСимвол, 1));
		Если НЕ (КодСимвола >= 48 И КодСимвола <= 57) Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла; 

	Возврат Истина;
	
КонецФункции

#КонецОбласти