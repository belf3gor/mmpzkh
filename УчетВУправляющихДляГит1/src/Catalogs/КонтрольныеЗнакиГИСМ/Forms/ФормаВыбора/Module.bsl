
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.ПодключаемоеОборудование) Тогда
		ИспользуютсяСканерыШтрихкода = (МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам("СканерШтрихкода").Количество() > 0);
	Иначе
		ИспользуютсяСканерыШтрихкода = Ложь;
	КонецЕсли;
	
	Владелец = Параметры.Отбор.Владелец;
	
	Элементы.ПодсказкаСканерШтрихКода.Видимость = ИспользуютсяСканерыШтрихкода;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ИспользуютсяСканерыШтрихкода Тогда
		// Попробуем подключить сканер штрихкода
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьСканерШКЗавершение", ЭтотОбъект); 
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, "СканерШтрихкода");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если СканерШтрихкодаПодключен И ЗавершениеРаботы Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// ПодключаемоеОборудование
	Если СканерШтрихкодаПодключен Тогда
		ТипыПО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("СканерШтрихкода");
		ОповещенияПриОтключении = Новый ОписаниеОповещения("ОтключитьСканерШКЗавершение", ЭтотОбъект); 
		МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(ОповещенияПриОтключении, УникальныйИдентификатор, ТипыПО);
	КонецЕсли;
	//Конец  ПодключаемоеОборудование
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекущийКод = Параметр[0];
			Иначе
				ТекущийКод = Параметр[1][1];
			КонецЕсли;
			Если ИнтеграцияГИСМКлиентСервер.ЭтоНомерКиЗ(ТекущийКод) Тогда
				КиЗ = НайтиПоШтрихкодуНаСервере(ТекущийКод, Владелец);
				Если КиЗ = Неопределено Тогда
					ПараметрыФормы = Новый Структура;
					
					ЗначенияЗаполнения = Новый Структура("Владелец",Владелец);
					ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
					ПараметрыФормы.Вставить("ТекстЗаполнения", ТекущийКод);
					ПараметрыФормы.Вставить("РежимВыбора", Истина);
					
					ОткрытьФорму("Справочник.КонтрольныеЗнакиГИСМ.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
				Иначе
					ОповеститьОВыборе(КиЗ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "СозданКиЗ" Тогда
		
		Если Параметры.РежимВыбора И Источник = ЭтаФорма Тогда
			ОповеститьОВыборе(Параметр);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиПоШтрихкодуНаСервере(Штрихкод, Владелец)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтрольныеЗнакиГИСМ.Ссылка
	|ИЗ
	|	Справочник.КонтрольныеЗнакиГИСМ КАК КонтрольныеЗнакиГИСМ
	|ГДЕ
	|	КонтрольныеЗнакиГИСМ.Код = &Код
	|	И КонтрольныеЗнакиГИСМ.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Код", Штрихкод);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		 Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПодключитьСканерШКЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	СканерШтрихкодаПодключен = РезультатВыполнения.Результат;

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьСканерШКЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При отключении оборудования произошла ошибка: ""%1"".'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		СканерШтрихкодаПодключен = Ложь;
	КонецЕсли;

КонецПроцедуры
