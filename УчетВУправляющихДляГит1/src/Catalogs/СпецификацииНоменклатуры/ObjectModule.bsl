#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает спецификацию основной для указанной продукции (услуги),
// если для нее не установлена основной другая спецификация.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - продукция или услуга, требующая расхода материалов по указанной
//                 спецификации.
//  ОбработкаОбновления - Булево - Истина, если действие выполняется в ходе обработки обновления информационной базы.
//
Процедура УстановитьОсновной(Номенклатура, ОбработкаОбновления = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ОсновнаяСпецификация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ОсновнаяСпецификацияНоменклатуры");
	Если ЗначениеЗаполнено(ОсновнаяСпецификация) Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Номенклатура.ПолучитьОбъект();
	
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОсновнаяСпецификацияНоменклатуры = Ссылка;
	
	Если ОбработкаОбновления Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
	Иначе
		Объект.Записать();
	КонецЕсли;
	
КонецПроцедуры
	
// Делает спецификацию не основной для указанной продукции (услуги).
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - продукция или услуга,
//                 для которой спецификация могла быть установлена основной.
//  ОбработкаОбновления - Булево - Истина, если действие выполняется в ходе обработки обновления информационной базы.
//
Процедура УдалитьУстановкуОсновной(Номенклатура, ОбработкаОбновления = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ОсновнаяСпецификация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ОсновнаяСпецификацияНоменклатуры");
	Если ОсновнаяСпецификация <> Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Номенклатура.ПолучитьОбъект();
	
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОсновнаяСпецификацияНоменклатуры = Неопределено;
	
	Если ОбработкаОбновления Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
	Иначе
		Объект.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнитьНаименованиеПоВладельцу(ДанныеЗаполнения);
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОсновнойПриСменеВладельца();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьУстановкуОсновной(Владелец);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаименованиеПоВладельцу(ДанныеЗаполнения)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Наименование") Тогда
		// Уже заполнено
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("Владелец") Тогда
		// Нечем заполнять
		Возврат;
	КонецЕсли;
			
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ДанныеЗаполнения.Владелец);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпецификацииНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.СпецификацииНоменклатуры КАК СпецификацииНоменклатуры
	|ГДЕ
	|	НЕ СпецификацииНоменклатуры.ПометкаУдаления
	|	И СпецификацииНоменклатуры.Владелец = &Номенклатура";
	
	УстановитьПривилегированныйРежим(Истина);
	ЭтоПервыйЭлемент = Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если НЕ ЭтоПервыйЭлемент Тогда
		// Первому элементу для выбранной номенклатуры название предложим.
		// Но если элементов больше одного, то "угадывать" нет смысла: 
		// пользователь сам определит понятные ему наименования
		Возврат;
	КонецЕсли;
	
	Наименование = Строка(ДанныеЗаполнения.Владелец);
				
КонецПроцедуры

Процедура УстановитьОсновнойПриСменеВладельца()
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Владелец) Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущийВладелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Владелец");
	
	Если Не ЗначениеЗаполнено(ПредыдущийВладелец) Тогда
		Возврат;
	КонецЕсли;
	
	Если Владелец = ПредыдущийВладелец Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьУстановкуОсновной(ПредыдущийВладелец);
	
	УстановитьОсновной(Владелец);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
