#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст запроса получения информации о сопоставлении продукции
//   номенклатуре, производителям, маркировке
// 
// Параметры:
//   ПолучатьПроизводителейИМаркировку - Булево - требуется ли получение данных о производителях и маркировке
//
// Возвращаемое значение:
//   Строка - текст запроса
//
Функция ТекстЗапросаИнформацияОСопоставлении(ПолучатьПроизводителейИМаркировку = Ложь) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыВЕТИС.Продукция КАК Продукция,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыВЕТИС.Номенклатура) КАК Количество
	|ПОМЕСТИТЬ СопоставленоПозиций
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|ГДЕ
	|	СоответствиеНоменклатурыВЕТИС.Продукция В (&Продукция)
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыВЕТИС.Продукция
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.ЭтоГруппа КАК ЭтоГруппа,
	|	ЕСТЬNULL(СопоставленоПозиций.Количество, 0) КАК Количество,
	|	СоответствиеНоменклатурыВЕТИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыВЕТИС.Характеристика КАК Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(СоответствиеНоменклатурыВЕТИС.Номенклатура) КАК НоменклатураПредставление
	|ИЗ
	|	Справочник.ПродукцияВЕТИС КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленоПозиций КАК СопоставленоПозиций
	|		ПО (СопоставленоПозиций.Продукция = Товары.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|		ПО Товары.Ссылка = СоответствиеНоменклатурыВЕТИС.Продукция
	|		И (СопоставленоПозиций.Количество = 1
	|		ИЛИ СоответствиеНоменклатурыВЕТИС.Порядок = 1)
	|ГДЕ
	|	Товары.Ссылка В (&Продукция)";
	
	Если ПолучатьПроизводителейИМаркировку Тогда
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПродукцияВЕТИС.Ссылка КАК Продукция,
	|	ПродукцияВЕТИС.Производитель КАК Производитель,
	|	ПроизводителиНомера.Номер КАК Маркировка
	|ПОМЕСТИТЬ ВтПроизводителиОбщая
	|ИЗ
	|	Справочник.ПродукцияВЕТИС.Производители КАК ПродукцияВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредприятияВЕТИС.НомераПредприятий КАК ПроизводителиНомера
	|		ПО ПродукцияВЕТИС.Производитель = ПроизводителиНомера.Ссылка
	|ГДЕ
	|	ПродукцияВЕТИС.Ссылка В (&Продукция)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	ВтПроизводителиОбщая.Производитель КАК Производитель,
	|	ПРЕДСТАВЛЕНИЕ(ВтПроизводителиОбщая.Производитель) КАК ПроизводительПредставление
	|ИЗ
	|	Справочник.ПродукцияВЕТИС КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПроизводителиОбщая КАК ВтПроизводителиОбщая
	|		ПО ВтПроизводителиОбщая.Продукция = Товары.Ссылка
	|ГДЕ
	|	Товары.Ссылка В (&Продукция)
	|ИТОГИ
	|	КОЛИЧЕСТВО(Производитель)
	|ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	ВтПроизводителиОбщая.Маркировка КАК Маркировка
	|ИЗ
	|	Справочник.ПродукцияВЕТИС КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПроизводителиОбщая КАК ВтПроизводителиОбщая
	|		ПО ВтПроизводителиОбщая.Продукция = Товары.Ссылка
	|		И НЕ(ВтПроизводителиОбщая.Маркировка ЕСТЬ NULL)
	|ГДЕ
	|	Товары.Ссылка В (&Продукция)
	|ИТОГИ
	|	КОЛИЧЕСТВО(Маркировка)
	|ПО
	|	Ссылка";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
			ПроизвольнаяПродукция = ИнтеграцияВЕТИСВызовСервера.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "Произвольная");
			Если ПроизвольнаяПродукция Тогда
				ВыбраннаяФорма = "ПроизвольнаяПродукция";
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		Иначе
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ПомощникСоздания";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Наименование");
	
	Поля.Добавить("ЭтоГруппа");
	Поля.Добавить("ТребуетсяЗагрузка");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.ТребуетсяЗагрузка = Истина Тогда
		СтандартнаяОбработка = Ложь;
		Представление = НСтр("ru = '<не загружено>'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ХСПроизводительОтбор") И ЗначениеЗаполнено(Параметры.ХСПроизводительОтбор) Тогда
		Параметры.Отбор.Вставить("ХозяйствующийСубъектПроизводитель", Параметры.ХСПроизводительОтбор);
	КонецЕсли;
	
	Если Параметры.Свойство("ПредприятиеОтбор") И ЗначениеЗаполнено(Параметры.ПредприятиеОтбор) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ИнтеграцияВЕТИСВызовСервера.ПолучитьДанныеВыбораПродукцииСОтборомПоПредприятию(Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

