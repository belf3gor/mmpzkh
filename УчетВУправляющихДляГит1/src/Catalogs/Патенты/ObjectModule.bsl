#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = Новый Структура;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ДатаНачала") Тогда
		Если ДанныеЗаполнения.Свойство("ДатаОкончания") Тогда
			ДатаНачала = НачалоГода(ДанныеЗаполнения.ДатаОкончания);
		Иначе
			ДатаНачала = НачалоГода(ОбщегоНазначения.ТекущаяДатаПользователя());
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ДатаОкончания") Тогда
		ДатаОкончания = КонецГода(ДатаНачала);
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("Владелец") Тогда
		Владелец = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда
		
		ДатаНачалаДействияПатентнойСистемы	= УчетУСН.ДатаНачалаДействияПатентнойСистемы();
		
		Если ДатаНачала > ДатаОкончания Тогда
			ТекстОшибки = НСтр("ru = 'Неверно задан период'");
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", "Дата окончания",,, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "ДатаОкончания", "Объект", Отказ);
		ИначеЕсли ДатаНачала < ДатаНачалаДействияПатентнойСистемы И ДатаОкончания >= ДатаНачалаДействияПатентнойСистемы Тогда
			ТекстОшибки = НСтр("ru = 'Патенты со сроком действия, истекающим после 01.01.2013 г. действуют только до конца 2012 года'");
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", "Дата окончания",,, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "ДатаОкончания", "Объект", Отказ);
		ИначеЕсли ДатаНачала >= ДатаНачалаДействияПатентнойСистемы И Год(ДатаНачала) <> Год(ДатаОкончания) Тогда
			ТекстОшибки = НСтр("ru = 'Патент выдается на период от одного до двенадцати месяцев включительно в пределах календарного года'");
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", "Дата окончания",,, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "ДатаОкончания", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Очищаем ссылки на правило из регистра сведений "ЗадачиБухгалтераНалоговыеПлатежи"
Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация,
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило";
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если УчетПСН.ПрименяетсяВычетПоОнлайнКассам(Владелец, ДатаОкончания) Тогда
		
		Если ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
			РегистрацияВНалоговомОргане = НалоговыйОрган;
		Иначе
			РегистрацияВНалоговомОргане = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "РегистрацияВНалоговомОргане");
		КонецЕсли;
		
		РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.РаспределитьРасходыПоПатентам(Владелец,
			РегистрацияВНалоговомОргане);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
