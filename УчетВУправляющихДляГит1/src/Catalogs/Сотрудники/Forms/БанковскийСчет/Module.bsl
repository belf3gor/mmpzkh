
&НаКлиенте 
Перем ТекущийТекстНомераСчета; // Текст, набранный в поле ввода номера счета

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	БанковскийСчетАвтоТекстКорреспондента = Параметры.ОписаниеДанных;
	БанковскийСчетАвтоТекстНазначения     = НСтр("ru = 'Заработная плата'");
	
	ЦветВыделенияНекорректногоЗначение = ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой;
	ЗаполнитьЗначенияСвойств(Объект, Параметры.Объект);
	
	Если ЗначениеЗаполнено(Параметры.ОписаниеДанных) Тогда
		ЭтотОбъект.Заголовок = СтрШаблон(НСтр("ru = '%1: %2'"), Параметры.ОписаниеДанных, ЭтотОбъект.Заголовок);
	КонецЕсли;
	
	ЗаполнитьРеквизитыБанков();
	БанковскиеСчетаФормыКлиентСервер.ИзменитьДлинуНомераСчета(ЭтотОбъект, Истина);
	
	Если Параметры.Ключ.Пустая() И НЕ ЗначениеЗаполнено(Объект.ВидСчета) Тогда
		Объект.ВалютаДенежныхСредств = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		Объект.ВидСчета              = "Расчетный";
	Иначе
		ОбъектДоступен = ЗаблокироватьБанковскийСчетПриРедактированииНаСервере(Объект.Ссылка, Объект.ВерсияДанных, УникальныйИдентификатор);
		ЭтотОбъект.ТолькоПросмотр = НЕ ОбъектДоступен;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТекстКорреспондента) Тогда
		БанковскийСчетТекстКорреспондентаИспользование = 1;
		БанковскийСчетТекстКорреспондента = Объект.ТекстКорреспондента;
	Иначе
		БанковскийСчетТекстКорреспондентаИспользование = 0;
		БанковскийСчетТекстКорреспондента = БанковскийСчетАвтоТекстКорреспондента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТекстНазначения) Тогда
		БанковскийСчетТекстНазначенияИспользование = 1;
		БанковскийСчетТекстНазначения              = Объект.ТекстНазначения;
	Иначе
		БанковскийСчетТекстНазначенияИспользование = 0;
		БанковскийСчетТекстНазначения              = БанковскийСчетАвтоТекстНазначения;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		Если НЕ ОбъектДоступен Тогда
			ПоказатьПредупреждение(
				,
				НСтр("ru='Не удается внести изменения в реквизиты банковского счета. Возможно данные редактируются другим пользователем.'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ТекстОшибки = "";
	
	Если Не ЗначениеЗаполнено(Объект.НомерСчета) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Заполнение", НСтр("ru = 'Номер счета'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.НомерСчета", , Отказ);
	КонецЕсли;
	
	Если Не БанковскиеСчетаФормыКлиентСервер.НомерСчетаКорректен(Объект.НомерСчета, БИКБанка, Истина, ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НомерСчета",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанЭлементБанк" Тогда
		
		Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Банки")
			И ЗначениеЗаполнено(Параметр)
			И Объект.Банк <> Параметр Тогда
			
			Объект.Банк = Параметр;
			
		КонецЕсли;
		
		ЗаполнитьРеквизитыБанков();
		
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерСчетаПриИзменении(Элемент)
	
	Объект.НомерСчета = СтрЗаменить(Объект.НомерСчета, " ", "");
	
	БанковскиеСчетаФормыКлиент.УстановитьВалютуПодсказкуСчета(
		Объект, ЭтотОбъект, БИКБанка, ЦветВыделенияНекорректногоЗначение, Истина);
	
	УстановитьНаименованиеСчета(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Объект.ГосударственныйКонтракт = Неопределено;
	
	РеквизитыБанка = БанковскиеСчетаФормыКлиент.ПолучитьДанныеБанка(ВыбранноеЗначение);
	ВыбранноеЗначение = РеквизитыБанка.ссылка;
	
	ОбновитьРеквизитыБанкаНаФорме(ЭтотОбъект, РеквизитыБанка); 
	
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ТекущийТекстНомераСчета = СтрЗаменить(Текст," ","");
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПодсказкуНомераСчета", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Банк) Тогда
		БИКБанка = "";
		НаименованиеБанка = "";
		ДеятельностьБанкаПрекращена = Ложь;
	КонецЕсли;
	
	БанковскиеСчетаФормыКлиентСервер.ИзменитьДлинуНомераСчета(ЭтотОбъект, Истина);
	Объект.НомерСчета = Элементы.НомерСчета.ОграничениеТипа.ПривестиЗначение(Объект.НомерСчета);
	
	БанковскиеСчетаФормыКлиент.УстановитьВалютуПодсказкуСчета(
		Объект, ЭтотОбъект, БИКБанка, ЦветВыделенияНекорректногоЗначение, Истина);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	БанковскиеСчетаФормыКлиент.БанкАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка, ПараметрыПолученияДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстКорреспондентаИспользованиеПриИзменении(Элемент)
	
	Если БанковскийСчетТекстКорреспондентаИспользование = 0 Тогда
		БанковскийСчетТекстКорреспондента = БанковскийСчетАвтоТекстКорреспондента;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетТекстНазначенияИспользованиеПриИзменении(Элемент)
	
	Если БанковскийСчетТекстНазначенияИспользование = 0 Тогда
		БанковскийСчетТекстНазначения = БанковскийСчетАвтоТекстНазначения;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	УстановитьНаименованиеСчета(Форма);

	Форма.ПодсказкаБанк = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляБанка(Форма.ДеятельностьБанкаПрекращена);
	
	Если Не ЗначениеЗаполнено(Форма.ПодсказкаНомерСчета) Тогда
		
		Форма.ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляНомерСчета(
			Форма.Объект.НомерСчета, Форма.БИКБанка, Истина, Форма.ЦветВыделенияНекорректногоЗначение);
		
	КонецЕсли;
		
	Элементы.ПодсказкаБанк.Видимость = ЗначениеЗаполнено(Форма.ПодсказкаБанк);
	
	Элементы.БанковскийСчетТекстКорреспондента.Доступность = Форма.БанковскийСчетТекстКорреспондентаИспользование;
	Элементы.БанковскийСчетТекстНазначения.Доступность =     Форма.БанковскийСчетТекстНазначенияИспользование;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНаименованиеСчета(Форма, ИзменениеНомераСчета = Ложь)
	
	Объект = Форма.Объект;
	
	Если ПустаяСтрока(Объект.Наименование) ИЛИ Объект.Наименование = Форма.АвтоНаименование Тогда
		Форма.АвтоНаименование = СформироватьАвтоНаименование(Форма);
		Если НЕ ПустаяСтрока(Форма.АвтоНаименование) И Форма.АвтоНаименование <> Объект.Наименование Тогда
			Объект.Наименование = Форма.АвтоНаименование;
		КонецЕсли;
	Иначе
		Если ИзменениеНомераСчета И НЕ ПустаяСтрока(Форма.НомерСчетаТекущий) Тогда
			Объект.Наименование = СтрЗаменить(Объект.Наименование, Форма.НомерСчетаТекущий, СокрЛП(Объект.НомерСчета));
		КонецЕсли;
		
		Форма.АвтоНаименование = СформироватьАвтоНаименование(Форма, Объект.Наименование);
	КонецЕсли;
	
	Форма.НомерСчетаТекущий = СокрЛП(Объект.НомерСчета);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьАвтоНаименование(Форма, Знач Текст = "")
	
	Элементы     = Форма.Элементы;
	Объект       = Форма.Объект;
	
	ПредставлениеВалюты = "" + Объект.ВалютаДенежныхСредств;
	
	ПредставлениеБанка = "";
	Если ЗначениеЗаполнено(Объект.Банк) Тогда
		ПредставлениеБанка = СокрЛП(Форма.НаименованиеБанка);
	КонецЕсли;
	
	СтрокаНаименования = БанковскиеСчетаФормыКлиентСервер.НаименованиеБанковскогоСчета(Объект, ПредставлениеБанка);
	
	Возврат СтрокаНаименования;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаблокироватьБанковскийСчетПриРедактированииНаСервере(Ссылка, ВерсияДанных, УникальныйИдентификатор)
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Ссылка, ВерсияДанных, УникальныйИдентификатор);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	Объект.ТекстКорреспондента =
		?(БанковскийСчетТекстКорреспондентаИспользование, БанковскийСчетТекстКорреспондента, "");
	Объект.ТекстНазначения     =
		?(БанковскийСчетТекстНазначенияИспользование, БанковскийСчетТекстНазначения, "");
		
	Если ПроверитьЗаполнение() Тогда
		Закрыть(Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПОДКЛЮЧАЕМЫЕ ОБРАБОТЧИКИ

&НаКлиенте
Процедура Подключаемый_УстановитьПодсказкуНомераСчета()
	
	Если Не БанковскиеПравила.ПроверитьДлинуНомераСчета(ТекущийТекстНомераСчета) Тогда
		ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиент.ПодсказкаВводаПоляНомерСчета(
			ТекущийТекстНомераСчета, БИКБанка);
	Иначе
		ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляНомерСчета(
			ТекущийТекстНомераСчета, БИКБанка, Истина, ЦветВыделенияНекорректногоЗначение);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыБанков()
	
	РеквизитыБанка = БанковскиеСчетаВызовСервера.ПолучитьРеквизитыБанкаИзСправочника(Объект.Банк);
	ОбновитьРеквизитыБанкаНаФорме(ЭтотОбъект, РеквизитыБанка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьРеквизитыБанкаНаФорме(Форма, РеквизитыБанка)
	
	Если ЗначениеЗаполнено(РеквизитыБанка.Ссылка) Тогда
		
		Форма.БИКБанка = РеквизитыБанка.Код;
		Форма.НаименованиеБанка = РеквизитыБанка.Наименование;
		Форма.ДеятельностьБанкаПрекращена = РеквизитыБанка.ДеятельностьПрекращена;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
