#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
	   И Не ДополнительныеСвойства.Свойство("ОтключитьБизнесЛогикуРИБ") Тогда
		// Последовательность распространяем по всем узлам РИБ, чтобы пользователь видел одинаковое
		// состояние данных во всех узлах, аналогично тому как передаются остальные регламентные операции
		// между узлами.
		//
		// При загрузке по РИБ состояние последовательности сохраняется только в случае, 
		// если информация приходит из узла, в котором выполняется полное перепроведение.
		// В остальных случаях состояние проставляется в "Проведен с нарушением последовательности".
		//
		// Фильтрацию выполняем согласно настройкам базы получателя.
		
		Для Каждого Движение Из ЭтотОбъект Цикл
		
			УзелПерепроведения = РаботаСПоследовательностямиПовтИсп.УзелПерепроведенияДокументовПоОрганизации(
				Движение.Организация, ОбменДанными.Отправитель);
			
			Если ОбменДанными.Отправитель = УзелПерепроведения Тогда
				Продолжить;
			КонецЕсли;
			
			Если Движение.СостояниеПроведения = Перечисления.СостоянияПроведенияВПоследовательности.ПроведенВПоследовательности
			 Или Движение.СостояниеПроведения = Перечисления.СостоянияПроведенияВПоследовательности.ПерепроведениеПропущено Тогда
				Движение.СостояниеПроведения = Перечисления.СостоянияПроведенияВПоследовательности.ПроведенСНарушениемПоследовательности;
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецЕсли
