
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// Печать

#Область Печать

// Функция формирует Таблицу документов для печати.
//
Функция СформироватьИтоговуюТаблицу(Список) Экспорт
	
	Построитель = Новый ПостроительЗапроса;
	
	Построитель.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Ссылка КАК ДокСсылка,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Дата
		|ИЗ
		|	Документ.КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства КАК КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства
		|{ГДЕ
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Ссылка.*,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.ПометкаУдаления,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Номер,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Дата,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Проведен,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Организация.*,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.ЛицевойСчет.*,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Проживающий.*,
		|	КВП_ЗаявлениеОРегистрацииОСнятииСРегистрационногоУчетаПоМестуЖительства.Комментарий}
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Ссылка,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Дата
		|ИЗ
		|	Документ.КВП_СвидетельствоОРегистрацииПоМестуЖительства КАК КВП_СвидетельствоОРегистрацииПоМестуЖительства
		|{ГДЕ
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Ссылка.*,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.ПометкаУдаления,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Номер,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Дата,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Проведен,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Организация.*,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.ЛицевойСчет.*,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Проживающий.*,
		|	КВП_СвидетельствоОРегистрацииПоМестуЖительства.Комментарий}";
		
	Для Каждого ТекОтбор Из Список.Отбор.Элементы Цикл
			
		Если ТекОтбор.Использование И Не ТекОтбор.ЛевоеЗначение = Новый  ПолеКомпоновкиДанных("Тип") Тогда
			
			НовыйОтбор = Построитель.Отбор.Добавить(Строка(ТекОтбор.ЛевоеЗначение));
			НовыйОтбор.ВидСравнения  = ВидСравнения[Строка(ТекОтбор.ВидСравнения)];
			НовыйОтбор.Значение      = ТекОтбор.ПравоеЗначение;
			НовыйОтбор.Использование = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Принудительно добавляем отбор "ПометкаУдаления".
	НовыйОтбор = Построитель.Отбор.Добавить("ПометкаУдаления");
	НовыйОтбор.ВидСравнения  = ВидСравнения.Равно;
	НовыйОтбор.Значение      = Ложь;
	НовыйОтбор.Использование = Истина;
	
	Построитель.ЗаполнитьНастройки();
	
	Построитель.Выполнить();
	
	ТаблицаДок = Построитель.Результат.Выгрузить();
	
	Возврат ТаблицаДок;
	
КонецФункции

// Функция формирует табличный документ по форме №13.
//
Функция ПечатьФорма13(МассивОбъектов, ОбъектыПечати)
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЖурналУчетаЗаявленийИСвидетельствОРегистрации";
	
	Макет = УПЖКХ_ТиповыеМетодыСервер.ПолучитьМакет("Отчет.КВП_ФормыРегистрационногоУчета.ПФ_MXL_Форма_13");
	
	Страница1 = Макет.ПолучитьОбласть("Страница1");
	ОблСтрока = Макет.ПолучитьОбласть("Страница1_1");
	
	ТабДокумент.Вывести(Страница1);
	
	нпп = 1;
	
	СтруктураПараметровМакета = Новый Структура("НПП,СведенияОЗаявителе,ДатаПоступления,ДатаРегистрации,ДокУЛ,АдресМестаЖительства");
	
	Для Каждого ТекСтрока Из МассивОбъектов Цикл
		
		СведенияОЗаявителе = ТекСтрока.Проживающий.Наименование;
		
		Если ЗначениеЗаполнено(ТекСтрока.Проживающий.ФизЛицо.ДатаРождения) Тогда
			СведенияОЗаявителе = СведенияОЗаявителе 
							   + ", " 
							   + Формат(ТекСтрока.Проживающий.ФизЛицо.ДатаРождения, "ДФ=yyyy") 
							   + " г.р.";
		КонецЕсли;
		
		МестоРегистрации 	= ПолучитьАдрес(ТекСтрока.Проживающий.Владелец.Адрес.Владелец, 
											Перечисления.КВП_ВидыАдресов.Здание, 
											"Рег");
		ПредставлениеАдреса = ПолучитьАдресПомещения(МестоРегистрации.ПредставлениеРег, 
													 ТекСтрока.Проживающий.Владелец.Адрес);
		
		ПаспортныеДанные = ПолучитьПаспортныеДанныеПроживающего(ТекСтрока.Проживающий, ТекСтрока.Дата);
		
		СтруктураПараметровМакета.НПП = нпп;
		СтруктураПараметровМакета.СведенияОЗаявителе = СведенияОЗаявителе;
		
		СтруктураПараметровМакета.ДатаПоступления = Формат(ТекСтрока.Дата, "ДЛФ=Д");
		
		СтруктураПараметровМакета.ДатаРегистрации = ?(ТипЗнч(ТекСтрока) = Тип("ДокументСсылка.КВП_СвидетельствоОРегистрацииПоМестуЖительства"), Формат(ТекСтрока.Дата, "ДЛФ=Д"), "");
		
		// Если Документ - Свидетельство о рождении, тогда записываем данные в таблицу.
		Если ПаспортныеДанные.ВидДокумента = Справочники.ВидыДокументовФизическихЛиц.СвидетельствоОРождении Тогда
			
			СтруктураПараметровМакета.ДокУЛ = Строка(ПаспортныеДанные.ВидДокумента) 
									  + ?(ЗначениеЗаполнено(ПаспортныеДанные.СерияДокумента)," серии ", "") 
									  + Строка(ПаспортныеДанные.СерияДокумента) 
									  + ?(ЗначениеЗаполнено(ПаспортныеДанные.НомерДокумента), " номер ", "") 
									  + Строка(ПаспортныеДанные.НомерДокумента);
		КонецЕсли;
		
		СтруктураПараметровМакета.АдресМестаЖительства = ПредставлениеАдреса;
		
		ОблСтрока.Параметры.Заполнить(СтруктураПараметровМакета);
		
		ТабДокумент.Вывести(ОблСтрока);
		
		нпп = нпп + 1;
		
	КонецЦикла;
	
	ТабДокумент.ПовторятьПриПечатиСтроки =ТабДокумент.Область(5, , 5);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
	
КонецФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УПЖКХ_ТиповыеМетодыСервер.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_13") Тогда
		УПЖКХ_ТиповыеМетодыСервер.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_13", "Форма №13", ПечатьФорма13(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли