#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Документы по НМА""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;
	
	ПолеЗапросаИнфо =
	"	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаНМА)
	|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПередачаНМА).Контрагент
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПоступлениеНМА)
	|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПоступлениеНМА).Контрагент
	|		ИНАЧЕ Таб.НематериальныйАктив
	|	КОНЕЦ";
	
	ПолеЗапросаСумма =
	"	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаНМА)
	|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПередачаНМА).СуммаДокумента
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПоступлениеНМА)
	|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПоступлениеНМА).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ";
	
	ПолеЗапросаВалюта =
	"	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаНМА)
	|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПередачаНМА).ВалютаДокумента
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПоступлениеНМА)
	|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПоступлениеНМА).ВалютаДокумента
	|		ИНАЧЕ &ВалютаРеглУчета
	|	КОНЕЦ";
	
	Результат.Вставить("Информация",      ПолеЗапросаИнфо);
	Результат.Вставить("СуммаДокумента",  ПолеЗапросаСумма);
	Результат.Вставить("ВалютаДокумента", ПолеЗапросаВалюта);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли