
#Область ПолучениеИЗакрытиеБаннеров

// Получает баннер, в случае если есть что показать. Если баннер не найден, в хранилище помещается Неопределено.
// Параметры:
//	СтруктураПараметров - Струкутура - Структура параметров:
//		Организация - СправочникСсылка.Организации - Организация, по которой ищем баннер.
//		Размещение - Строка - Идентификатор формы по которой нужно показать баннер. см. функции ИмяРазмещения...().
//		ПоказатьПредыдущий - Булево - Признак показа предыдущего в очереди баннера. Если Ложь - показывается следующий баннер.
//	АдресХранилища - Строка -Адрес хранилища, по которому будет помещен баннер.
//
Процедура ПолучитьБаннер(СтруктураПараметров, АдресХранилища) Экспорт
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		// Не показываем баннеры, если работа с внешними ресурсами заблокирована.
		// Нет смысла собирать и показывать баннеры для копий информационных баз.
		ПоместитьВоВременноеХранилище(Неопределено, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Организация = СтруктураПараметров.Организация;
	Размещение = СтруктураПараметров.Размещение;
	ПоказатьПредыдущий = СтруктураПараметров.ПоказатьПредыдущий;
	
	// Получаем баннеры в ротации у текущего пользователя в указанном размещении.
	БаннерыВРотации = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПерсонализированныеПредложенияСервисов_НаборБаннеровВРотации",
		Размещение, Новый Массив);
	
	Если ТипЗнч(БаннерыВРотации) <> Тип("Массив") Тогда
		БаннерыВРотации = Новый Массив;
	КонецЕсли;
	
	// Ротируем пользовательский список баннеров.
	Если БаннерыВРотации.Количество() > 1 Тогда
		Если ПоказатьПредыдущий Тогда
			ПоследнийБаннер = БаннерыВРотации[БаннерыВРотации.ВГраница()];
			БаннерыВРотации.Удалить(БаннерыВРотации.ВГраница());
			БаннерыВРотации.Вставить(0, ПоследнийБаннер);
		Иначе
			ПервыйБаннер = БаннерыВРотации[0];
			БаннерыВРотации.Удалить(0);
			БаннерыВРотации.Добавить(ПервыйБаннер);
		КонецЕсли;
	КонецЕсли;
	
	ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
	
	// Получим список баннеров для пользователя по организации и размещению.
	// Баннеры, привязанные к организации, не показываем, если организация не заполнена.
	// Баннеры, непривязанные к организации, показываем всегда.
	СписокБаннеров = БаннерыПользователя(Организация, Размещение, ТекущаяДата);
	Если СписокБаннеров.Количество() = 0 Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	// Обновим данные по которым будем получать информацию.
	ИнтервалОбновления = 60 * 60 * 4; // обновляем данные с интервалом 4 часа.
	РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(
		Организация,
		ТекущаяДата,
		ИнтервалОбновления,
		РазделыПерсонализированныхДанных(СписокБаннеров));
	
	// Получим данные для баннеров.
	ДанныеБаннеров = ДанныеБаннеров(СписокБаннеров, Организация, ТекущаяДата);
	
	// Собираем идентификаторы всех доступных баннеров.
	ИдентификаторыДоступныхБаннеров = Новый Массив;
	Для Каждого СтрокаСписка Из СписокБаннеров Цикл
		Если ВыполняютсяУсловияБаннера(СтрокаСписка.Баннер, ДанныеБаннеров) Тогда
			ИдентификаторыДоступныхБаннеров.Вставить(0, СтрокаСписка.Баннер.Идентификатор);
			// Если баннер - исключительный, показывается только он.
			// В противном случае показываются все баннеры, для которых выполняются условия.
			Если СтрокаСписка.Баннер.Исключительный Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ИдентификаторыНовогоНабораБаннеровВРотации = Новый Массив;
	
	// Оставляем в ротации только доступные баннеры.
	Для каждого БаннерВРотации Из БаннерыВРотации Цикл
		Если ИдентификаторыДоступныхБаннеров.Найти(БаннерВРотации.Идентификатор) <> Неопределено Тогда
			ИдентификаторыНовогоНабораБаннеровВРотации.Добавить(БаннерВРотации.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	// Новые доступные баннеры добавляем в начало очереди.
	Для каждого ИдентификаторДоступногоБаннера Из ИдентификаторыДоступныхБаннеров Цикл
		Позиция = ?(ПоказатьПредыдущий И ИдентификаторыНовогоНабораБаннеровВРотации.Количество() > 0, 1, 0);
		Если ИдентификаторыНовогоНабораБаннеровВРотации.Найти(ИдентификаторДоступногоБаннера) = Неопределено Тогда
			ИдентификаторыНовогоНабораБаннеровВРотации.Вставить(Позиция, ИдентификаторДоступногоБаннера);
		КонецЕсли;
	КонецЦикла;
	
	// Заменяем набор баннеров в ротации на новый
	БаннерыВРотации = Новый Массив;
	Для каждого ИдентификаторНовогоБаннераВРотации Из ИдентификаторыНовогоНабораБаннеровВРотации Цикл
		НовыйБаннер = СписокБаннеров.Найти(ИдентификаторНовогоБаннераВРотации, "ИдентификаторБаннера").Баннер;
		ЗаполнитьДанныеБаннера(НовыйБаннер, ДанныеБаннеров);
		БаннерыВРотации.Добавить(НовыйБаннер);
	КонецЦикла;
	
	// Сохраняем баннеры в ротации у текущего пользователя в указанном размещении.
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПерсонализированныеПредложенияСервисов_НаборБаннеровВРотации",
		Размещение, БаннерыВРотации);
	
	Баннер = Неопределено;
	
	Если БаннерыВРотации.Количество() > 0 Тогда
		Баннер = БаннерыВРотации[0];
		Баннер.Вставить("Единственный", БаннерыВРотации.Количество() = 1);
	КонецЕсли;
	
	Если Баннер <> Неопределено Тогда
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Персонализированные предложения сервисов. Показан баннер'"),
			УровеньЖурналаРегистрации.Информация,
			,
			,
			Баннер.Идентификатор);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Баннер, АдресХранилища);
	
КонецПроцедуры

// Процедура закрывает баннер на форме.
// Параметры:
//		Форма - УправляемаяФорма - Форма на которой нужно закрыть баннер.
//		Организация - СправочникСсылка.Организации - Организация, по которой ищем баннер.
//
Процедура ЗакрытьБаннер(Форма, Организация) Экспорт
	
	// Закроем баннер на форме.
	Баннер = Форма.Баннер;
	Форма.Баннер = Неопределено;
	Форма.Элементы.Баннер.Видимость = Ложь;
	
	СохранитьОбстоятельстваЗакрытияБаннера(Баннер, Организация);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Персонализированные предложения сервисов. Закрыт баннер'"),
		УровеньЖурналаРегистрации.Информация,
		,
		,
		Баннер.Идентификатор);
	
КонецПроцедуры

Процедура СохранитьОбстоятельстваЗакрытияБаннера(Баннер, Организация)
	
	ЗакрытыеБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		НовыйЗакрытыеПользователемБаннеры());
		
	Если НЕ Баннер.ЗависитОтОрганизации Тогда
		ЗакрытыеБаннеры.БаннерыБезОрганизации.Вставить(Баннер.Идентификатор, Баннер.ОбстоятельстваЗакрытия);
	Иначе
		ЗакрытыеБаннерыПоОрганизации = ЗакрытыеБаннеры.БаннерыПоОрганизации[Организация];
		Если ЗакрытыеБаннерыПоОрганизации = Неопределено Тогда
			ЗакрытыеБаннерыПоОрганизации = Новый Структура;
		КонецЕсли;
		ЗакрытыеБаннерыПоОрганизации.Вставить(Баннер.Идентификатор, Баннер.ОбстоятельстваЗакрытия);
		ЗакрытыеБаннеры.БаннерыПоОрганизации.Вставить(Организация, ЗакрытыеБаннерыПоОрганизации);
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		ЗакрытыеБаннеры);
		
КонецПроцедуры

// Процедура отображает баннер на форме.
// Параметры:
//		Форма - Форма - Форма, на которой отображается баннер.
//		АдресРезультата - Строка - Адрес временного хранилища, в которое помещен баннер.
//
Процедура УстановитьБаннерНаФорме(Форма, АдресРезультата) Экспорт
	
	Элементы = Форма.Элементы;
	
	Баннер = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Баннер = Неопределено Тогда
		Элементы.Баннер.Видимость = Ложь;
	Иначе
		Форма.Баннер = Баннер;
		Элементы.ГруппаБаннерОжидание.Видимость = Ложь;
		Элементы.ТекстБаннера.Видимость         = Истина;
		Элементы.КартинкаБаннера.Видимость      = Истина;
		Элементы.Баннер.Видимость               = Истина;
		Элементы.БаннерСФоном.ЦветФона          = ЦветаСтиля[Баннер.ЦветФонаБаннер];
		Элементы.КартинкаБаннера.Картинка       = БиблиотекаКартинок[Баннер.ИмяКартинкаЛоготипа];
		Форма.ТекстБаннера = Форма.Баннер.ТекстБаннера;
		
		Элементы.КартинкаПредыдущийБаннер.Видимость = НЕ Баннер.Единственный;
		Элементы.КартинкаСледующийБаннер.Видимость = НЕ Баннер.Единственный;
		
		Элементы.КартинкаЗакрытьБаннер.Доступность = Истина;
		Элементы.КартинкаПредыдущийБаннер.Доступность = Истина;
		Элементы.КартинкаСледующийБаннер.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСБаннерами

Функция ДанныеБаннеров(СписокБаннеров, Организация, ТекущаяДата)
	
	// Для получения данных удобнее использовать структуру вместо таблицы.
	// Перенесем данные из списка в структуру.
	СтруктураБаннеров = Новый Структура;
	Для Каждого СтрокаСписка Из СписокБаннеров Цикл
		СтруктураБаннеров.Вставить(СтрокаСписка.Баннер.Идентификатор, СтрокаСписка.Баннер);
	КонецЦикла;
	
	НомераТаблиц = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		ТекстЗапросаОтчетнаяКампания(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаЧисленностьСотрудников(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаНДСКВычетуКонтрагент(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
		+ ТекстЗапросаНДСКВычетуСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
		+ ТекстЗапросаКрупнейшийДебитор(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаКоличествоКонтрагентов(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаКрупнейшиеДебиторыСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаОбщееКоличествоДокументов(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаКоличествоПоступлений(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаКоличествоРеализаций(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаПроверкаОплатыНалогов(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаПроблемыСНалоговымиПлатежами(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаИнтеграцияСоSmartway(Запрос, СтруктураБаннеров, НомераТаблиц)
		+ ТекстЗапросаЗаявкаНаКредит(Запрос, СтруктураБаннеров, НомераТаблиц);
	
	Запрос.УстановитьПараметр("Организация",          Организация);
	Запрос.УстановитьПараметр("ТекущаяДата",          ТекущаяДата);
	// Ищем отчетность, которая начнется в течении 2х недель
	Запрос.УстановитьПараметр("СрокНачалаОтчетности", ТекущаяДата + 60*60*24*14);
	Запрос.УстановитьПараметр("НачалоКвартала",       НачалоКвартала(ТекущаяДата));
	Запрос.УстановитьПараметр("КонецКвартала",        КонецКвартала(ТекущаяДата));
	
	ДанныеБаннеров = Новый Структура;
	ДанныеБаннеров.Вставить("ТекущаяДата", ТекущаяДата);
	
	УстановитьПривилегированныйРежим(Истина);
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ДанныеБаннеров.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	// Данные по сторонним сертификатам получаются из хранилища общих настроек, поэтому получим их отдельно.
	ПолучитьДанныеПоСтороннемуСертификату(Организация, ДанныеБаннеров, СтруктураБаннеров);
	
	ПолучитьДанныеНадежностьБанковОрганизаций(ДанныеБаннеров, СтруктураБаннеров);

	ПолучитьДанныеПоПлатформеСамозанятые(Организация, ДанныеБаннеров, СтруктураБаннеров);
	ПолучитьДанныеПоддержкаСервиса(ДанныеБаннеров, СтруктураБаннеров);
	
	ПолучитьДанныеБаннераТестовыйТариф(ДанныеБаннеров);
	
	ПолучитьДанныеБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннеров);
	
	ПолучитьДанныеБаннераПредодобренныйКредитВСбербанке(ДанныеБаннеров);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеБаннеров;
	
КонецФункции

Функция ВыполняютсяУсловияБаннера(Баннер, ДанныеБаннеров)
	
	Идентификатор = Баннер.Идентификатор;
	Если НЕ ДанныеБаннеров.Свойство(Идентификатор) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеБаннера = ДанныеБаннеров[Идентификатор];
	Если Идентификатор = ИдентификаторБаннераСтороннийСертификат() Тогда
		Возврат ВыполняютсяУсловияБаннераСтороннийСертификат(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЧисленностьСотрудников() Тогда
		Возврат ВыполняютсяУсловияБаннераЧисленностьСотрудников(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОтчетнаяКампания() Тогда
		Возврат ВыполняютсяУсловияБаннераОтчетнаяКампания(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуКонтрагент() Тогда
		Возврат ВыполняютсяУсловияБаннераНДСКВычетуКонтрагент(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
		Возврат ВыполняютсяУсловияБаннераНДСКВычетуСПАРК(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшийДебитор() Тогда
		Возврат ВыполняютсяУсловияБаннераКрупнейшийДебитор(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоКонтрагентов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК() Тогда
		Возврат ВыполняютсяУсловияБаннераКрупнейшиеДебиторыСПАРК(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОбщееКоличествоДокументов() Тогда
		Возврат ВыполняютсяУсловияБаннераОбщееКоличествоДокументов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоПоступлений() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоПоступлений(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоРеализаций() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоРеализаций(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНадежностьБанков() Тогда
		Возврат ВыполняютсяУсловияБаннераНадежностьБанков(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПлатформаСамозанятые() Тогда
		Возврат ВыполняютсяУсловияПлатформаСамозанятые(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПоддержкаСервиса() Тогда
		Возврат ВыполняютсяУсловияБаннераПоддержкаСервиса(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераТестовыйТариф() Тогда
		Возврат ВыполняютсяУсловияБаннераТестовыйТариф(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПроверкаОплатыНалогов() Тогда
		Возврат ВыполняютсяУсловияБаннераПроверкаОплатыНалогов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПроблемыСНалоговымиПлатежами() Тогда
		Возврат ВыполняютсяУсловияБаннераПроблемыСНалоговымиПлатежами(ДанныеБаннера, Баннер);
	
	ИначеЕсли Идентификатор = ИдентификаторБаннераПредодобренныйКредитВСбербанке() Тогда
		Возврат ВыполняютсяУсловияБаннераПредодобренныйКредитВСбербанке(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаВидимостиСчетовУчета() Тогда
		Возврат ВыполняютсяУсловияБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннера, Баннер);

	ИначеЕсли Идентификатор = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
		Возврат ВыполняютсяУсловияБаннераИнтеграцияСоSmartway(ДанныеБаннера, Баннер);

	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаКредит() Тогда
		Возврат ВыполняютсяУсловияБаннераЗаявкаНаКредит(ДанныеБаннера, Баннер);
	
	Иначе
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннера(Баннер, ДанныеБаннеров)
	
	Идентификатор = Баннер.Идентификатор;
	ДанныеБаннера = ДанныеБаннеров[Идентификатор];
	ТекущаяДата   = ДанныеБаннеров.ТекущаяДата;
	
	Если Идентификатор = ИдентификаторБаннераСтороннийСертификат() Тогда
		ЗаполнитьДанныеБаннераСтороннийСертификат(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЧисленностьСотрудников() Тогда
		ЗаполнитьДанныеБаннераЧисленностьСотрудников(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОтчетнаяКампания() Тогда
		ЗаполнитьДанныеБаннераОтчетнаяКампания(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуКонтрагент() Тогда
		ЗаполнитьДанныеБаннераНДСКВычетуКонтрагент(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
		ЗаполнитьДанныеБаннераНДСКВычетуСПАРК(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшийДебитор() Тогда
		ЗаполнитьДанныеБаннераКрупнейшийДебитор(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
		ЗаполнитьДанныеБаннераКоличествоКонтрагентов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК() Тогда
		ЗаполнитьДанныеБаннераКрупнейшиеДебиторыСПАРК(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОбщееКоличествоДокументов() Тогда
		ЗаполнитьДанныеБаннераОбщееКоличествоДокументов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоПоступлений() Тогда
		ЗаполнитьДанныеБаннераКоличествоПоступлений(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоРеализаций() Тогда
		ЗаполнитьДанныеБаннераКоличествоРеализаций(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНадежностьБанков() Тогда
		ЗаполнитьДанныеБаннераНадежностьБанков(Баннер, ДанныеБаннера);

	ИначеЕсли Идентификатор = ИдентификаторБаннераПлатформаСамозанятые() Тогда
		ЗаполнитьДанныеБаннераПлатформаСамозанятые(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПоддержкаСервиса() Тогда
		ЗаполнитьДанныеБаннераПоддержкаСервиса(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераТестовыйТариф() Тогда
		ЗаполнитьДанныеБаннераТестовыйТариф(Баннер, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПроверкаОплатыНалогов() Тогда
		ЗаполнитьДанныеБаннераПроверкаОплатыНалогов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПроблемыСНалоговымиПлатежами() Тогда
		ЗаполнитьДанныеБаннераПроблемыСНалоговымиПлатежами(Баннер, ДанныеБаннера, ТекущаяДата);

	ИначеЕсли Идентификатор = ИдентификаторБаннераПредодобренныйКредитВСбербанке() Тогда
		ЗаполнитьДанныеБаннераПредодобренныйКредитВСбербанке(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаВидимостиСчетовУчета() Тогда
		ЗаполнитьДанныеБаннераНастройкаВидимостиСчетовУчета(Баннер, ДанныеБаннера, ТекущаяДата);

	ИначеЕсли Идентификатор = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
		ЗаполнитьДанныеБаннераИнтеграцияСоSmartway(Баннер, ДанныеБаннера, ТекущаяДата);

	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаКредит() Тогда
		ЗаполнитьДанныеБаннераЗаявкаНаКредит(Баннер, ДанныеБаннера, ТекущаяДата);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОписанияБаннеров

// Каждый баннера имеет следующие функции:
// 1. Функция НовыйБаннер... описывает структуру добавления баннера в общий список.
// 2. Функция ТекстЗапроса... формирует текст запроса для получения данных баннера.
// 3. Функция ВыполняютсяУсловияБаннера... проверяет данные баннера на условие срабатывания.
// 4. Функция ЗаполнитьДанныеБаннера... помещает данные из общей коллекции ДанныеБаннеров в данные конкретного баннера, 
// который помещается в реквизит формы. Эта информация может быть перенесена в ОбстоятельстваЗакрытияБаннера.
// 5. Функция ТекстБаннера... возвращает форматированную строку с текстом баннера, который будет размещенна форме.
// 6. Функция ИдентификаторБаннера... ключ баннера, который используется для поиска и идентификации баннера.

#Область СтороннийСертификат

Функция НовыйБаннерСтороннийСертификат()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СОтчетность();
	Баннер.Идентификатор       = ИдентификаторБаннераСтороннийСертификат();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераСтороннийСертификат();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераСтороннийСертификат();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаОкончанияНайденногоСертификата", Дата(1,1,1));
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеПоСтороннемуСертификату(Организация, ДанныеБаннеров, СтруктураБаннеров)
	
	Идентификатор = ИдентификаторБаннераСтороннийСертификат();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОСтороннихСертификатах = ИнформацияОСтороннихСертификатах(Организация);
	
	Если ИнформацияОСтороннихСертификатах <> Неопределено И ИнформацияОСтороннихСертификатах.СертификатНайден Тогда
		ДатаСертификата = ИнформацияОСтороннихСертификатах.СрокДействияСертификата;
	Иначе
		ДатаСертификата = Неопределено;
	КонецЕсли;
	ДанныеБаннеров.Вставить(Идентификатор, ДатаСертификата);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераСтороннийСертификат(ДатаОкончанияСертификата, Баннер)
	
	Идентификатор = ИдентификаторБаннераСтороннийСертификат();
	
	Если НЕ ЗначениеЗаполнено(ДатаОкончанияСертификата) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Баннер.ОбстоятельстваЗакрытия.ДатаОкончанияНайденногоСертификата = ДатаОкончанияСертификата Тогда
		// Баннер показывали, не повторяемся.
		Возврат Ложь;
	КонецЕсли;
	
	// Проверим условие баннера.
	Возврат ((ДатаОкончанияСертификата > ТекущаяДатаСеанса()) И ДатаОкончанияСертификата <= ДобавитьМесяц(ТекущаяДатаСеанса(), 3));
	
КонецФункции

Функция ЗаполнитьДанныеБаннераСтороннийСертификат(Баннер, ДатаОкончанияНайденногоСертификата)
	
	Баннер.ТекстБаннера = ТекстБаннераСтороннийСертификат(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаОкончанияНайденногоСертификата = ДатаОкончанияНайденногоСертификата;
	
КонецФункции

Функция ТекстБаннераСтороннийСертификат(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Пользуетесь электронной отчетностью?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Делайте это просто -'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='отправляйте отчетность прямо из 1С'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераСтороннийСертификат()

	Возврат "СтороннийСертификат";

КонецФункции

Функция ДоступностьБаннераСтороннийСертификат()
	
	Возврат НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ЧисленностьСотрудников

Функция НовыйБаннерЧисленностьСотрудников()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СОтчетность();
	Баннер.Идентификатор       = ИдентификаторБаннераЧисленностьСотрудников();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераЧисленностьСотрудников();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераЧисленностьСотрудников();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаЧисленностьСотрудников(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераЧисленностьСотрудников();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаЧисленностьСотрудниковПредыдущая", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаЧисленностьСотрудников", НомераТаблиц.Количество());
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоСотрудниковПредыдущее,
	|	0 КАК КоличествоСотрудников
	|ПОМЕСТИТЬ ВременнаяТаблицаЧисленностьСотрудниковПредыдущая
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленностьПредыдущая)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	0,
	|	ПерсонализированныеДанные.ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленность)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВременнаяТаблицаЧисленностьСотрудниковПредыдущая.КоличествоСотрудниковПредыдущее) КАК КоличествоСотрудниковПредыдущее,
	|	СУММА(ВременнаяТаблицаЧисленностьСотрудниковПредыдущая.КоличествоСотрудников) КАК КоличествоСотрудников
	|ПОМЕСТИТЬ ВременнаяТаблицаЧисленностьСотрудников
	|ИЗ
	|	ВременнаяТаблицаЧисленностьСотрудниковПредыдущая КАК ВременнаяТаблицаЧисленностьСотрудниковПредыдущая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаЧисленностьСотрудников.КоличествоСотрудников КАК КоличествоСотрудников
	|ИЗ
	|	ВременнаяТаблицаЧисленностьСотрудников КАК ВременнаяТаблицаЧисленностьСотрудников
	|ГДЕ
	|	ВременнаяТаблицаЧисленностьСотрудников.КоличествоСотрудников > ВременнаяТаблицаЧисленностьСотрудников.КоличествоСотрудниковПредыдущее";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераЧисленностьСотрудников(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат (ДанныеБаннера[0].КоличествоСотрудников >= КоличествоСотрудниковДляРекламыОтчетности());
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЧисленностьСотрудников(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераЧисленностьСотрудников(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераЧисленностьСотрудников(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧисленностьСотрудников = ДанныеБаннера[0].КоличествоСотрудников;
	ТекстПроСотрудников = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ЧисленностьСотрудников, "сотрудник, сотрудника, сотрудников");
	
	ТекстЗаголовка = НовыйЗаголовок(
		СтрШаблон(НСтр("ru='Ваша компания выросла! У вас %1.'"), ТекстПроСотрудников));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Теперь отчетность в фонды нужно'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='сдавать по-новому'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераЧисленностьСотрудников()

	Возврат "ЧисленностьСотрудников";

КонецФункции

Функция ДоступностьБаннераЧисленностьСотрудников()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоПросмотраВсехКадровыхДанных()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ТестовыйТариф

Функция НовыйБаннерТестовыйТариф()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервис1СОтчетность();
	Баннер.Идентификатор        = ИдентификаторБаннераТестовыйТариф();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераТестовыйТариф();
	Баннер.Исключительный       = Истина;
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераТестовыйТариф();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.День;
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераТестовыйТариф(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами",
		ТарификацияБПВызовСервераПовтИсп.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами());
		
	ДанныеБаннеров.Вставить(ИдентификаторБаннераТестовыйТариф(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераТестовыйТариф(ДанныеБаннера, Баннер)
	
	Возврат НЕ ДанныеБаннера.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераТестовыйТариф(Баннер, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннерТестовыйТариф(Баннер.НавигационнаяСсылка);
	
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерТестовыйТариф(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Отправляйте отчетность через Интернет, экономьте Ваше время.'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Подключите платный тариф'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераТестовыйТариф()

	Возврат "ТестовыйТариф";

КонецФункции

Функция ДоступностьБаннераТестовыйТариф()
	
	Возврат (ПолучитьФункциональнуюОпцию("ОплатаПолныйИнтерфейс") ИЛИ ПолучитьФункциональнуюОпцию("ОплатаПростойИнтерфейс"))
		И ПравоДоступа("Просмотр", Метаданные.Обработки.ОплатаСервиса)
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ОтчетнаяКампания

Функция НовыйБаннерОтчетнаяКампания()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СОтчетность();
	Баннер.Идентификатор       = ИдентификаторБаннераОтчетнаяКампания();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераОтчетнаяКампания();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераОтчетнаяКампания();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатыПрошедшихОтчетныхКампаний", Новый Массив);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаОтчетнаяКампания(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераОтчетнаяКампания();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.НаименованиеСокращенное КАК ОтчетнаяКампания,
	|	ЗадачиБухгалтера.НачалоВыполнения КАК НачалоОтчетнойКампании
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И ЗадачиБухгалтера.НачалоВыполнения МЕЖДУ &ТекущаяДата И &СрокНачалаОтчетности
	|	И ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|	И ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).Периодичность <> ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)";
	
	// В закрытых ранее баннерах хранятся даты начала отчетных кампания, которые пользователь закрыл.
	// Исключим их из выборки, чтобы не показывать еще раз.
	ОбстоятельстваЗакрытия = СтруктураБаннеров[Идентификатор].ОбстоятельстваЗакрытия;
	Если ОбстоятельстваЗакрытия.ДатыПрошедшихОтчетныхКампаний.Количество() > 0 Тогда
		ТекстЗапроса = ТекстЗапроса + " И ЗадачиБухгалтера.НачалоВыполнения НЕ В (&ПоказанныеДаты)";
		Запрос.УстановитьПараметр("ПоказанныеДаты", ОбстоятельстваЗакрытия.ДатыПрошедшихОтчетныхКампаний);
	КонецЕсли;

	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераОтчетнаяКампания(ОтчетныеКампании, Баннер)
	
	Возврат (ОтчетныеКампании.Количество() > 0);
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераОтчетнаяКампания(Баннер, ОтчетныеКампании, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераОтчетнаяКампания(Баннер.НавигационнаяСсылка, ОтчетныеКампании, ТекущаяДата);
	
	СтрокаОтчетнойКампании = ОтчетныеКампании[0];
	Баннер.ОбстоятельстваЗакрытия.ДатыПрошедшихОтчетныхКампаний.Добавить(СтрокаОтчетнойКампании.НачалоОтчетнойКампании);
	
КонецПроцедуры

Функция ТекстБаннераОтчетнаяКампания(НавигационнаяСсылка, ОтчетныеКампании, ТекущаяДата)
	
	СтрокаОтчетнойКампании = ОтчетныеКампании[0];
	Если ОтчетныеКампании.Количество() = 1 Тогда
		// Если отчетная кампания по одному отчету, то укажем имя отчета.
		ИмяОтчетнойКампании = ИмяОтчетнойКампании(СтрокаОтчетнойКампании.ОтчетнаяКампания);
	Иначе
		// Если в эту отчетную кампанию нужно сдавать несколько отчетов,
		// то не будем уточнять название отчетов.
		ИмяОтчетнойКампании = "";
	КонецЕсли;
	
	СрокиПоДням        = Перечисления.ОтносительныеСроки.СрокиПоДням();
	ОсталосьДней       = (НачалоДня(СтрокаОтчетнойКампании.НачалоОтчетнойКампании) - НачалоДня(ТекущаяДата))/86400;
	ОтносительныйСрок  = СрокиПоДням.Получить(ОсталосьДней);
	ПредставлениеСрока = НРег(БлокиИнформационнойПанели.ПредставлениеСрока(ОтносительныйСрок, ОсталосьДней));
	
	ТекстОтчетнаяКампания = ?(ПустаяСтрока(ИмяОтчетнойКампании),
		СтрШаблон(НСтр("ru='начинается %1'"),
			ПредставлениеСрока),
		СтрШаблон(НСтр("ru='%1 начинается %2'"),
			ИмяОтчетнойКампании,
			ПредставлениеСрока));
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Отчетная кампания %1'"), ТекстОтчетнаяКампания));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как просто'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='отправлять отчетность из программы'"),  НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераОтчетнаяКампания()

	Возврат "ОтчетнаяКампания";

КонецФункции

Функция ДоступностьБаннераОтчетнаяКампания()
	
	ПравоДоступаКЗадачамБухгалтера = ПравоДоступа(
		"Чтение",
		Метаданные.РегистрыСведений.ЗадачиБухгалтера
	);
	
	Возврат ПравоДоступаКЗадачамБухгалтера
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область НДСКВычетуКонтрагент

Функция НовыйБаннерНДСКВычетуКонтрагент()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СКонтрагент();
	Баннер.Идентификатор       = ИдентификаторБаннераНДСКВычетуКонтрагент();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СКонтрагент();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерКонтрагент";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНДСКВычетуКонтрагент();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНДСКВычетуКонтрагент();
	Баннер.ДанныеБаннера.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаНДСКВычетуКонтрагент(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
	
	Идентификатор = ИдентификаторБаннераНДСКВычетуКонтрагент();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ЭтоПериодНДС(ТекущаяДата) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерсонализированныеДанные.ДанныеРасшифровки КАК Контрагент,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК НДСКВычету
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.НДСКВычету)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераНДСКВычетуКонтрагент(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.НДСКВычету > ПорогСуммаДля1СКонтрагент() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНДСКВычетуКонтрагент(Баннер, ДанныеБаннера, ТекущаяДата)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераНДСКВычетуКонтрагент(Баннер.НавигационнаяСсылка, СтрокаДанных);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, СтрокаДанных);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераНДСКВычетуКонтрагент(НавигационнаяСсылка, СтрокаДанных)
	
	НаименованиеКонтрагента = Строка(СтрокаДанных.Контрагент);
	// Ограничим длину наименования контрагента 20 символами, чтобы поместилось в баннер.
	НаименованиеКонтрагента = ?(СтрДлина(НаименованиеКонтрагента) > 20,
		Лев(НаименованиеКонтрагента, 18) + "...",
		НаименованиеКонтрагента);
		
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='%1 предъявил НДС на сумму %2 руб'"),
		НаименованиеКонтрагента, СтрокаДанных.НДСКВычету));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(СтрШаблон(НСтр("ru='Узнайте всё про %1 в'"), СтрокаДанных.Контрагент)),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='досье контрагента!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераНДСКВычетуКонтрагент()

	Возврат "НДСКВычетуКонтрагент";

КонецФункции

Функция ДоступностьБаннераНДСКВычетуКонтрагент()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область НДСКВычетуСПАРК

Функция НовыйБаннерНДСКВычетуСПАРК()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СПАРК();
	Баннер.Идентификатор       = ИдентификаторБаннераНДСКВычетуСПАРК();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СПАРК();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНДСКВычетуСПАРК();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНДСКВычетуСПАРК();
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаНДСКВычетуСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
	
	Идентификатор = ИдентификаторБаннераНДСКВычетуСПАРК();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ЭтоПериодНДС(ТекущаяДата) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК НДСКВычету
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.НДСКВычету)
	|	И ПерсонализированныеДанные.ДанныеРасшифровки = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераНДСКВычетуСПАРК(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.НДСКВычету > ПорогСуммаДля1СПАРК() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНДСКВычетуСПАРК(Баннер, ДанныеБаннера, ТекущаяДата)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераНДСКВычетуСПАРК(Баннер.НавигационнаяСсылка, СтрокаДанных, ТекущаяДата);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераНДСКВычетуСПАРК(НавигационнаяСсылка, СтрокаДанных, ТекущаяДата)
	
	Квартал = Формат(ТекущаяДата, "ДФ=q");
	
	Если Квартал = "2" Тогда
		ПредлогКвартал = НСтр("ru='во'");
	Иначе
		ПредлогКвартал = НСтр("ru='в'");
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='НДС к вычету %1 %2 квартале составляет %3 руб'"),
		ПредлогКвартал, Квартал, СтрокаДанных.НДСКВычету));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, кто из Ваших контрагентов имеет'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='низкие индексы надежности!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераНДСКВычетуСПАРК()

	Возврат "НДСКВычетуСПАРК";

КонецФункции

Функция ДоступностьБаннераНДСКВычетуСПАРК()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КрупнейшийДебиторКонтрагент

Функция НовыйБаннерКрупнейшийДебитор()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СКонтрагент();
	Баннер.Идентификатор       = ИдентификаторБаннераКрупнейшийДебитор();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СКонтрагент();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерКонтрагент";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКрупнейшийДебитор();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераКрупнейшийДебитор();
	Баннер.ДанныеБаннера.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКрупнейшийДебитор(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКрупнейшийДебитор();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СУММА(ДанныеМонитораРуководителя.Сумма) КАК СуммаЗадолженности,
	|	ДанныеМонитораРуководителя.ДанныеРасшифровки КАК Контрагент
	|ИЗ
	|	РегистрСведений.ДанныеМонитораРуководителя КАК ДанныеМонитораРуководителя
	|ГДЕ
	|	ДанныеМонитораРуководителя.Организация = &Организация
	|	И ДанныеМонитораРуководителя.РазделМонитора = ЗНАЧЕНИЕ(Перечисление.РазделыМонитораРуководителя.ЗадолженностьПокупателей)
	|	И ДанныеМонитораРуководителя.ДанныеРасшифровки <> НЕОПРЕДЕЛЕНО
	|	И ДанныеМонитораРуководителя.ПрошлыйПериод = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеМонитораРуководителя.ДанныеРасшифровки
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаЗадолженности УБЫВ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКрупнейшийДебитор(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.СуммаЗадолженности > ПорогСуммаДля1СКонтрагент() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКрупнейшийДебитор(Баннер, ДанныеБаннера, ТекущаяДата)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераКрупнейшийДебиторКонтрагент(Баннер.НавигационнаяСсылка, СтрокаДанных);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, СтрокаДанных);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКрупнейшийДебиторКонтрагент(НавигационнаяСсылка, СтрокаДанных)
	
	НаименованиеКонтрагента = Строка(СтрокаДанных.Контрагент);
	// Ограничим длину наименования контрагента 25 символами, чтобы поместилось в баннер.
	НаименованиеКонтрагента = ?(СтрДлина(НаименованиеКонтрагента) > 25,
		Лев(НаименованиеКонтрагента, 23) + "...",
		НаименованиеКонтрагента);
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='%1 должен нам %2 руб'"),
		НаименованиеКонтрагента, СтрокаДанных.СуммаЗадолженности));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(СтрШаблон(НСтр("ru='Узнайте всё про %1 в'"), СтрокаДанных.Контрагент)),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='досье контрагента!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераКрупнейшийДебитор()

	Возврат "КрупнейшийДебиторКонтрагент";

КонецФункции

Функция ДоступностьБаннераКрупнейшийДебитор()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КрупнейшиеДебиторыСПАРК

Функция НовыйБаннерКрупнейшийДебиторСПАРК()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СПАРК();
	Баннер.Идентификатор       = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СПАРК();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераКрупнейшиеДебиторыСПАРК();
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКрупнейшиеДебиторыСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СУММА(ДанныеМонитораРуководителя.Сумма) КАК СуммаЗадолженности
	|ИЗ
	|	РегистрСведений.ДанныеМонитораРуководителя КАК ДанныеМонитораРуководителя
	|ГДЕ
	|	ДанныеМонитораРуководителя.Организация = &Организация
	|	И ДанныеМонитораРуководителя.РазделМонитора = ЗНАЧЕНИЕ(Перечисление.РазделыМонитораРуководителя.ЗадолженностьПокупателей)
	|	И ДанныеМонитораРуководителя.ДанныеРасшифровки = НЕОПРЕДЕЛЕНО
	|	И ДанныеМонитораРуководителя.ПрошлыйПериод = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеМонитораРуководителя.НомерСтрокиРаздела
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеМонитораРуководителя.НомерСтрокиРаздела УБЫВ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКрупнейшиеДебиторыСПАРК(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СуммаЗадолженности = ДанныеБаннера[0].СуммаЗадолженности;
	
	Если СуммаЗадолженности > ПорогСуммаДля1СПАРК() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКрупнейшиеДебиторыСПАРК(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКрупнейшиеДебиторыСПАРК(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКрупнейшиеДебиторыСПАРК(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Покупатели должны нам %1 руб'"), 
		Формат(ДанныеБаннера.СуммаЗадолженности, "ЧДЦ=")));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, кто из контрагентов имеет'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='низкие индексы надежности!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераКрупнейшиеДебиторыСПАРК()

	Возврат "КрупнейшиеДебиторыСПАРК";

КонецФункции

Функция ДоступностьБаннераКрупнейшиеДебиторыСПАРК()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КоличествоДокументов

Функция НовыйБаннерОбщееКоличествоДокументов()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераОбщееКоличествоДокументов();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераОбщееКоличествоДокументов();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераОбщееКоличествоДокументов();
	Баннер.ДанныеБаннера.Вставить("КоличествоПоступлений", 0);
	Баннер.ДанныеБаннера.Вставить("КоличествоРеализаций", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаОбщееКоличествоДокументов(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераОбщееКоличествоДокументов();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаКоличествоОбщихДокументов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоРеализаций,
	|	0 КАК КоличествоПоступлений
	|ПОМЕСТИТЬ ВременнаяТаблицаКоличествоОбщихДокументов
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоРеализаций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ПерсонализированныеДанные.ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоПоступлений)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВременнаяТаблицаКоличествоОбщихДокументов.КоличествоРеализаций), 0) КАК КоличествоРеализаций,
	|	ЕСТЬNULL(СУММА(ВременнаяТаблицаКоличествоОбщихДокументов.КоличествоПоступлений), 0) КАК КоличествоПоступлений
	|ИЗ
	|	ВременнаяТаблицаКоличествоОбщихДокументов КАК ВременнаяТаблицаКоличествоОбщихДокументов";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераОбщееКоличествоДокументов(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоДокументов = ДанныеБаннера[0].КоличествоРеализаций + ДанныеБаннера[0].КоличествоПоступлений;
	
	Если КоличествоДокументов > ОбщееКоличествоДокументовДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераОбщееКоличествоДокументов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераОбщееКоличествоДокументов(Баннер.НавигационнаяСсылка, ДанныеБаннера[0], ТекущаяДата);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераОбщееКоличествоДокументов(НавигационнаяСсылка, ДанныеБаннера, ТекущаяДата)
	
	КоличествоРеализаций = ДанныеБаннера.КоличествоРеализаций;
	КоличествоПоступлений = ДанныеБаннера.КоличествоПоступлений;
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы ввели %1 реализаций и поступлений в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоРеализаций + КоличествоПоступлений, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='сэкономить на документообороте!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераОбщееКоличествоДокументов()

	Возврат "ОбщееКоличествоДокументов";

КонецФункции

Функция ДоступностьБаннераОбщееКоличествоДокументов()
	
	ПравоДоступаКРеализациям = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.РеализацияТоваровУслуг
		);
		
	ПравоДоступаКПоступлениям = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.ПоступлениеТоваровУслуг
		);
		
	Возврат ПравоДоступаКРеализациям
		И ПравоДоступаКПоступлениям
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КоличествоПоступлений

Функция НовыйБаннерКоличествоПоступлений()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераКоличествоПоступлений();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКоличествоПоступлений();
	Баннер.ДоступенПоПравам    = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.ПоступлениеТоваровУслуг);
	Баннер.ДанныеБаннера.Вставить("КоличествоПоступлений", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоПоступлений(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКоличествоПоступлений();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоПоступлений
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоПоступлений)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоПоступлений(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоПоступлений > КоличествоПоступленийДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоПоступлений(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоПоступлений(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоПоступлений(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы ввели %1 поступлений в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ДанныеБаннера.КоличествоПоступлений, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='сэкономить на документообороте!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераКоличествоПоступлений()

	Возврат "КоличествоПоступлений";

КонецФункции

#КонецОбласти

#Область КоличествоРеализаций

Функция НовыйБаннерКоличествоРеализаций()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераКоличествоРеализаций();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКоличествоРеализаций();
	Баннер.ДоступенПоПравам    = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.РеализацияТоваровУслуг);
	Баннер.ДанныеБаннера.Вставить("КоличествоРеализаций", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоРеализаций(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКоличествоРеализаций();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоРеализаций
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоРеализаций)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоРеализаций(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоРеализаций > КоличествоРеализацийДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоРеализаций(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоРеализаций(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоРеализаций(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы оформили %1 реализаций в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ДанныеБаннера.КоличествоРеализаций, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='сэкономить на документообороте!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераКоличествоРеализаций()

	Возврат "КоличествоРеализаций";

КонецФункции

#КонецОбласти

#Область КоличествоКонтрагентов

Функция НовыйБаннерКоличествоКонтрагентов()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервис1СКонтрагент();
	Баннер.Идентификатор        = ИдентификаторБаннераКоличествоКонтрагентов();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннерКонтрагент";
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготип1СКонтрагент();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераКоличествоКонтрагентов();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераКоличествоКонтрагентов();
	Баннер.ДанныеБаннера.Вставить("КоличествоКонтрагентов", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоКонтрагентов(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКоличествоКонтрагентов();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоКонтрагентов
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоКонтрагентов)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоКонтрагентов(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоКонтрагентов >= КоличествоКонтрагентовДляРекламыКонтрагент() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоКонтрагентов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоКонтрагентов(Баннер.НавигационнаяСсылка, ДанныеБаннера[0], ТекущаяДата);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоКонтрагентов(НавигационнаяСсылка, ДанныеБаннера, ТекущаяДата)
	
	МинутНаОдногоКонтрагента = 1;
	ВсегоМинут = ДанныеБаннера.КоличествоКонтрагентов * МинутНаОдногоКонтрагента;
	КоличествоЧасов = Цел(ВсегоМинут / 60);
	КоличествоМинут = ВсегоМинут % 60;
	
	Если КоличествоМинут = 0 Тогда
		ПотраченоВремени = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЧасов, "час, часа, часов");
	ИначеЕсли КоличествоЧасов = 0 Тогда
		ПотраченоВремени = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ВсегоМинут, "минуту, минуты, минут");
	Иначе
		ПотраченоВремени = СтрШаблон("%1 %2",
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЧасов, "час, часа, часов"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоМинут, "минуту, минуты, минут"));
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы потратили %1 на ввод контрагентов в этом квартале'"), 
		ПотраченоВремени));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как делать это быстрее с помощью'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='1С:Контрагент!'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераКоличествоКонтрагентов()

	Возврат "КоличествоКонтрагентов";

КонецФункции

Функция ДоступностьБаннераКоличествоКонтрагентов()
	
	ПравоДоступаККонтрагентам = ПравоДоступа(
		"Просмотр",
		Метаданные.Справочники.Контрагенты
	);
	
	Возврат ПравоДоступаККонтрагентам
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область НадежностьБанков

Функция НовыйБаннерНадежностьБанков()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисНадежностьБанков();
	Баннер.Идентификатор       = ИдентификаторБаннераНадежностьБанков();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНадежностьБанков();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНадежностьБанков();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеНадежностьБанковОрганизаций(ДанныеБаннеров, СтруктураБаннеров)
	
	Идентификатор = ИдентификаторБаннераНадежностьБанков();
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	Информация = НадежностьБанков.ИнформацияОСобытияхБанковОрганизаций();
	Если НЕ Информация.Используется Тогда
		Возврат;
	КонецЕсли;
	Если Информация.События.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Баннер = СтруктураБаннеров[Идентификатор];
	
	ИдентификаторыЗакрытыхБаннеров = Баннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий;
	Если ТипЗнч(ИдентификаторыЗакрытыхБаннеров) <> Тип("Массив") Тогда
		ИдентификаторыЗакрытыхБаннеров = Новый Массив;
	КонецЕсли;
	
	Баннер.ДанныеБаннера.Вставить("ДанныеСобытия", НадежностьБанков.НовоеСобытие());
	ТаблицаДанныеБаннера = Новый ТаблицаЗначений;
	Для каждого ЭлементСобытия Из Баннер.ДанныеБаннера.ДанныеСобытия Цикл
		ТаблицаДанныеБаннера.Колонки.Добавить(ЭлементСобытия.Ключ);
	КонецЦикла;
	
	Для каждого ОписаниеСобытия Из Информация.События Цикл
		ДанныеСобытия = ОписаниеСобытия.Значение;
		Если ИдентификаторыЗакрытыхБаннеров.Найти(ДанныеСобытия.Идентификатор) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаДанныеБаннера.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСобытия);
	КонецЦикла;
	ТаблицаДанныеБаннера.Сортировать("Критичное Убыв, Идентификатор Возр");
	
	ДанныеБаннеров.Вставить(Идентификатор, ТаблицаДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераНадежностьБанков(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНадежностьБанков(Баннер, ДанныеБаннера)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Баннер.ТекстБаннера = ТекстБаннераНадежностьБанков(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера.ДанныеСобытия, ДанныеБаннера[0]);
	Идентификатор = ДанныеБаннера[0].Идентификатор;
	КлючСобытия = "НадежностьБанков" + Формат(Идентификатор, "ЧГ=");
	Баннер.ДанныеБаннера.Вставить("КлючСобытия", КлючСобытия);
	
	Если ДанныеБаннера[0].Критичное Тогда
		Баннер.ЦветФонаБаннер = "КритичноеСобытиеНадежностьБанковЦветФона";
		Баннер.ИмяКартинкаЛоготипа = "КритичноеСобытиеБаннераНадежностьБанков";
	Иначе
		Баннер.ЦветФонаБаннер = "ЦветФонаБлоковИнформационнойПанели";
		Баннер.ИмяКартинкаЛоготипа = "НекритичноеСобытиеБаннераНадежностьБанков";
	КонецЕсли;
	
	Если Баннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий.Найти(Идентификатор) = Неопределено Тогда
		Баннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий.Добавить(Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстБаннераНадежностьБанков(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧастиЗаголовка = Новый Массив;
	ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(ДанныеБаннера.ТекстСобытия));
	Если ЗначениеЗаполнено(ДанныеБаннера.ИсточникИнформации) Тогда
		ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(" ("));
		Если ДанныеБаннера.Событие = Перечисления.СобытияНадежностьБанков.Информация Тогда
			ТекстИсточник = НСтр("ru='подробнее'");
		Иначе
			ТекстИсточник = СтрЗаменить(НСтр("ru='информация ЦБ РФ'"), " ", Символы.НПП);
		КонецЕсли;
		ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(ТекстИсточник, ДанныеБаннера.ИсточникИнформации));
		ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(")"));
	КонецЕсли;
	ТекстЗаголовка = Новый ФорматированнаяСтрока(ЧастиЗаголовка);
	
	ЧастиПодзаголовка = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеБаннера.ТекстПояснения) Тогда
		ЧастиПодзаголовка.Добавить(НовыйСтрокаНадежностьБанков(ДанныеБаннера.ТекстПояснения));
	КонецЕсли;
	Если ДанныеБаннера.Критичное Тогда
		ЧастиПодзаголовка.Добавить(НовыйСтрокаНадежностьБанков(" "));
		ЧастиПодзаголовка.Добавить(НовыйСтрокаНадежностьБанков(НСтр("ru='Что делать'"), НавигационнаяСсылка));
	КонецЕсли;
	
	Если ЧастиПодзаголовка.Количество() > 0 Тогда
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(ЧастиПодзаголовка);
		Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	Иначе
		Возврат ТекстЗаголовка;
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторБаннераНадежностьБанков()

	Возврат "НадежностьБанков";

КонецФункции

Функция НовыйЗаголовокНадежностьБанков(ТекстЗаголовка, НавигационнаяСсылка = "")
	
	Возврат НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка, ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели);
	
КонецФункции

Функция НовыйСтрокаНадежностьБанков(ТекстСтроки, НавигационнаяСсылка = "")
	
	Возврат НовыйСтрокаБаннера(ТекстСтроки, НавигационнаяСсылка, ШрифтыСтиля.ОбычныйШрифтТекста);
	
КонецФункции

Функция ДоступностьБаннераНадежностьБанков()
	
	Возврат НадежностьБанков.СервисИспользуется()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ПлатформаСамозанятые

Функция НовыйБаннерПлатформаСамозанятые()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисМойНалог();
	Баннер.Идентификатор       = ИдентификаторБаннераПлатформаСамозанятые();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПлатформаСамозанятые();
	Баннер.Исключительный      = Истина;
	Баннер.ИмяКартинкаЛоготипа = "ПлатформаСамозанятыеИнтеграция";;
	Баннер.ДоступенПоПравам    = ДоступностьБаннераПлатформаСамозанятые();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеПоПлатформеСамозанятые(Организация, ДанныеБаннеров, СтруктураБаннеров);
	
	Идентификатор = ИдентификаторБаннераПлатформаСамозанятые();
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ДоступнаИнтеграцияСПлатформойСамозанятые") Тогда
		ДанныеСобытия = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ИнформацияОСобытияхПлатформыСамозанятые(Организация);
	ИначеЕсли УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(Организация, ТекущаяДатаСеанса()) Тогда
		ДанныеСобытия = Новый Структура("Организация, ПереходВОблако", Организация, "https://1cfresh.com/register");
	КонецЕсли;
	
	Если ДанныеСобытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Баннер = СтруктураБаннеров[Идентификатор];
	
	Баннер.ДанныеБаннера.Вставить("ДанныеСобытия", ДанныеСобытия);
	ДанныеБаннеров.Вставить(Идентификатор, ДанныеСобытия);
	
КонецПроцедуры

Функция ВыполняютсяУсловияПлатформаСамозанятые(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПлатформаСамозанятые(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера        = ТекстБаннераПлатформаСамозанятые(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	Баннер.ЦветФонаБаннер      = ЦветФонаБаннерПлатформаСамозанятые(ДанныеБаннера);
	Баннер.ИмяКартинкаЛоготипа = КартинкаЛоготипаБаннерПлатформаСамозанятые(ДанныеБаннера);
	// После закрытия баннер нельзя исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
КонецПроцедуры

Функция ТекстБаннераПлатформаСамозанятые(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧастиПодзаголовка = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ДоступнаИнтеграцияСПлатформойСамозанятые") Тогда
		
		Если ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.Ошибка
			ИЛИ ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.НеПривязано
			ИЛИ ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.НеНастроено Тогда
			ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Не настроен сервис Мой налог'"));
			ТекстПодзаголовка = НСтр("ru='Выполните настройку в личном кабинете сервиса '");
		Иначе
			ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Отправляйте данные о доходах в ФНС прямо из 1С'"));
			ТекстПодзаголовка = НСтр("ru='Подключитесь к сервису '");
		КонецЕсли;
		
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(ТекстПодзаголовка),
			" ",
			НовыйСтрокаБаннера(НСтр("ru='Мой налог'"), НавигационнаяСсылка));
		
	Иначе
		
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Отправляйте данные о доходах в ФНС прямо из 1С'"));
		ТекстПодзаголовка = НСтр("ru='Для этого перейдите в облако'");
		
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(ТекстПодзаголовка),
			" ",
			НовыйСтрокаБаннера(НСтр("ru='1cfresh.com'"), НавигационнаяСсылка));
		
	КонецЕсли;
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ЦветФонаБаннерПлатформаСамозанятые(ДанныеБаннера)
	
	Если ДанныеБаннера.Свойство("Состояние")
		И (ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.Ошибка
			ИЛИ ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.НеПривязано
			ИЛИ ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.НеНастроено) Тогда
		Возврат "ЦветБаннераПлатформаСамозанятыеКрасный";
	Иначе
		Возврат "ЦветБаннераПлатформаСамозанятыеЗеленый";
	КонецЕсли;
	
КонецФункции

Функция КартинкаЛоготипаБаннерПлатформаСамозанятые(ДанныеБаннера)
	
	Если ДанныеБаннера.Свойство("Состояние")
		И (ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.Ошибка
			ИЛИ ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.НеПривязано
			ИЛИ ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.НеНастроено) Тогда
		Возврат "ПлатформаСамозанятыеОшибка";
	Иначе
		Возврат "ПлатформаСамозанятыеИнтеграция";
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторБаннераПлатформаСамозанятые()

	Возврат "ПлатформаСамозанятые";

КонецФункции

Функция ДоступностьБаннераПлатформаСамозанятые()
	
	РекламироватьПереходВОблако = Не ОбщегоНазначения.РазделениеВключено() И Не ОбщегоНазначения.ЭтоАвтономноеРабочееМесто();
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяНалогНаПрофессиональныйДоход")
		И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
		И (ПолучитьФункциональнуюОпцию("ДоступнаИнтеграцияСПлатформойСамозанятые") Или РекламироватьПереходВОблако);
	
КонецФункции

#КонецОбласти

#Область ПоддержкаСервиса

Функция НовыйБаннерПоддержкаСервиса()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисПоддержкаСервиса();
	Баннер.Идентификатор       = ИдентификаторБаннераПоддержкаСервиса();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПоддержкаСервиса();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераПоддержкаСервиса();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераПоддержкаСервиса()
	
	Возврат "ПоддержкаСервиса";
	
КонецФункции

Функция ДоступностьБаннераПоддержкаСервиса()
	
	Возврат ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

Функция НовыйЗаголовокПоддержкаСервиса(ТекстЗаголовка, НавигационнаяСсылка = "")
	
	Возврат НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка, ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели);
	
КонецФункции

Процедура ПолучитьДанныеПоддержкаСервиса(ДанныеБаннеров, СтруктураБаннеров)
	
	Идентификатор = ИдентификаторБаннераПоддержкаСервиса();
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСообщенийСервиса = Новый ТаблицаЗначений;
	ТаблицаСообщенийСервиса.Колонки.Добавить(
		"СсылкаНаДанные", Новый ОписаниеТипов("СправочникСсылка.ОбщиеДанныеИнформационногоЦентра"));
	УстановитьПривилегированныйРежим(Истина);
	ИнформационныйЦентрСервер.СформироватьСписокНовостейНаРабочийСтол(ТаблицаСообщенийСервиса, 1);
	
	Если ТаблицаСообщенийСервиса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Баннер = СтруктураБаннеров[Идентификатор];
	
	ИдентификаторыЗакрытыхБаннеров = Баннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий;
	Если ТипЗнч(ИдентификаторыЗакрытыхБаннеров) <> Тип("Массив") Тогда
		ИдентификаторыЗакрытыхБаннеров = Новый Массив;
	КонецЕсли;
	
	РеквизитыСообщения = "Наименование, Идентификатор, Критичность, ТипИнформации";
	ДанныеСообщений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ТаблицаСообщенийСервиса.ВыгрузитьКолонку("СсылкаНаДанные"), РеквизитыСообщения);
	
	Баннер.ДанныеБаннера.Вставить("ДанныеСобытия", Новый Структура(РеквизитыСообщения));
	
	ТаблицаДанныеБаннера = Новый ТаблицаЗначений;
	Для каждого ЭлементСобытия Из Баннер.ДанныеБаннера.ДанныеСобытия Цикл
		ТаблицаДанныеБаннера.Колонки.Добавить(ЭлементСобытия.Ключ);
	КонецЦикла;
	
	Для каждого ОписаниеСобытия Из ДанныеСообщений Цикл
		ДанныеСобытия = ОписаниеСобытия.Значение;
		Если ИдентификаторыЗакрытыхБаннеров.Найти(ДанныеСобытия.Идентификатор) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаДанныеБаннера.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСобытия);
	КонецЦикла;
	ТаблицаДанныеБаннера.Сортировать("Критичность Убыв, Идентификатор Возр");
	
	ДанныеБаннеров.Вставить(Идентификатор, ТаблицаДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераПоддержкаСервиса(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат КритичноеСообщениеСервиса(ДанныеБаннера[0]);
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПоддержкаСервиса(Баннер, ДанныеБаннера)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Баннер.ТекстБаннера = ТекстБаннераПоддержкаСервиса(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера.ДанныеСобытия, ДанныеБаннера[0]);
	
	Баннер.ЦветФонаБаннер = ЦветФонаБаннераКритичноеСообщениеСервиса();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиКритичноеСообщениеСервиса();
	
КонецПроцедуры

Функция ТекстБаннераПоддержкаСервиса(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧастиЗаголовка = Новый Массив;
	ЧастиЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Внимание!'"), Новый Шрифт(,, Истина)));
	ЧастиЗаголовка.Добавить(Символы.ПС);
	ЧастиЗаголовка.Добавить(НовыйЗаголовокПоддержкаСервиса(ДанныеБаннера.Наименование));
	ЧастиЗаголовка.Добавить(" ");
	ЧастиЗаголовка.Добавить(НовыйЗаголовокПоддержкаСервиса(НСтр("ru = 'Подробнее'"), НавигационнаяСсылка));
	ТекстЗаголовка = НовыйЗаголовок(ЧастиЗаголовка);
	
	Возврат ТекстЗаголовка;
	
КонецФункции

Функция КритичноеСообщениеСервиса(ТекущееСообщение) Экспорт
	
	Возврат ТекущееСообщение.Критичность > 5 Или Строка(ТекущееСообщение.ТипИнформации) = "Недоступность";
	
КонецФункции

Функция ЦветФонаБаннераКритичноеСообщениеСервиса()
	
	Возврат "ЦветФонаПредупреждения";
	
КонецФункции

Функция ИмяКартинкиКритичноеСообщениеСервиса()
	
	Возврат "КотКритичноеСообщениеСервиса";
	
КонецФункции

#КонецОбласти

#Область ПроверкаОплатыНалогов

Функция НовыйБаннерПроверкаОплатыНалогов(Размещение)
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисПроверкаОплатыНалогов();
	Баннер.Идентификатор       = ИдентификаторБаннераПроверкаОплатыНалогов();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПроверкаОплатыНалогов();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераПроверкаОплатыНалогов();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиСервисПроверкаОплатыНалогов();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ДанныеБаннера.Вставить("Размещение", Размещение);
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаПроверкаОплатыНалогов(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераПроверкаОплатыНалогов();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.СервисПроверкаОплатыНалогов)
	|	И ПерсонализированныеДанные.Организация = &Организация";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераПроверкаОплатыНалогов(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Организация        = ДанныеБаннера[0].Организация;
	ЗначениеПоказателя = ДанныеБаннера[0].ЗначениеПоказателя;
	
	Если Баннер.ДанныеБаннера.Размещение = ИмяРазмещенияОбщее()
		И ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Организация,, Истина) Тогда
		
		Возврат Ложь; // выводим баннер на начальной странице, если не подключена отчетность
		
	КонецЕсли;
	
	Баннер.ДанныеБаннера.Вставить("Организация", Организация);
	
	Возврат Обработки.СверкаНалоговСФНС.ПоказатьБаннерПроверкаОплатыНалогов(
		Организация,
		Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия,
		ЗначениеПоказателя);
		
КонецФункции

Процедура ЗаполнитьДанныеБаннераПроверкаОплатыНалогов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераПроверкаОплатыНалогов(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераПроверкаОплатыНалогов(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Дошли ли ваши платежи до ФНС?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Нажмите'"), НавигационнаяСсылка),
		", ",
		НовыйСтрокаБаннера(НСтр("ru='чтобы отправить запрос и получите ответ через 1-3 дня.'")));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераПроверкаОплатыНалогов()

	Возврат "ПроверкаОплатыНалогов";

КонецФункции

Функция ДоступностьБаннераПроверкаОплатыНалогов()
	
	Возврат (ПолучитьФункциональнуюОпцию("ИспользуетсяПростойИнтерфейсВМоделиСервиса")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяПолныйИнтерфейсВМоделиСервиса"));
	
КонецФункции

Функция ИмяКартинкиСервисПроверкаОплатыНалогов()
	
	Возврат "КотДошелЛиПлатеж";
	
КонецФункции

#КонецОбласти

#Область ПроблемыСНалоговымиПлатежами

Функция НовыйБаннерПроблемыСНалоговымиПлатежами()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяПроблемыСНалоговымиПлатежами();
	Баннер.Идентификатор        = ИдентификаторБаннераПроблемыСНалоговымиПлатежами();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПроблемыСНалоговымиПлатежами();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПроблемыСНалоговымиПлатежами();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиПроблемыСНалоговымиПлатежами();
	Баннер.ЦветФонаБаннер       = ЦветФонаБаннераПроблемыСНалоговымиПлатежами();
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаПроблемыСНалоговымиПлатежами(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераПроблемыСНалоговымиПлатежами();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ЗапросыНаПроверкуОплатНалогов.Дата) КАК Дата,
	|	ЗапросыНаПроверкуОплатНалогов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ЗапросыНаПроверкуОплатНалогов КАК ЗапросыНаПроверкуОплатНалогов
	|ГДЕ
	|	ЗапросыНаПроверкуОплатНалогов.Организация = &Организация
	|	И ЗапросыНаПроверкуОплатНалогов.ОтветОбработан
	|	И НЕ ЗапросыНаПроверкуОплатНалогов.СверкаПройдена
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапросыНаПроверкуОплатНалогов.Организация";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ВыполняютсяУсловияБаннераПроблемыСНалоговымиПлатежами(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДатаЗапроса = ДанныеБаннера[0].Дата;
	Если Не ЗначениеЗаполнено(ДатаЗапроса) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Баннер.ДанныеБаннера.Вставить("Организация", ДанныеБаннера[0].Организация);
	
	Возврат (ДатаЗапроса > Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия);
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПроблемыСНалоговымиПлатежами(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераПроблемыСНалоговымиПлатежами(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДатаСеанса();
	
КонецПроцедуры

Функция ТекстБаннераПроблемыСНалоговымиПлатежами(НавигационнаяСсылка)

	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'В выписке из ФНС не обнаружено сведений о вашем платеже'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Подробнее'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераПроблемыСНалоговымиПлатежами()

	Возврат "ПроблемыСНалоговымиПлатежами";

КонецФункции

Функция ДоступностьБаннераПроблемыСНалоговымиПлатежами()
	
	Возврат (ПолучитьФункциональнуюОпцию("ИспользуетсяПростойИнтерфейсВМоделиСервиса")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяПолныйИнтерфейсВМоделиСервиса"));
	
КонецФункции

Функция ИмяКартинкиПроблемыСНалоговымиПлатежами()
	
	Возврат "КотПлатежНеДошел";
	
КонецФункции

Функция ЦветФонаБаннераПроблемыСНалоговымиПлатежами()

	Возврат "ЦветФонаБаннераПлатежиНеДошлиДоФНС";

КонецФункции

#КонецОбласти

#Область ПредодобренныйКредитВСбербанке

Функция НовыйБаннерПредодобренныйКредитВСбербанке()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисПредодобренныйКредитВСбербанке();
	Баннер.Идентификатор        = ИдентификаторБаннераПредодобренныйКредитВСбербанке();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипСбербанк();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннерПредодобренныйКредитВСбербанке";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПредодобренныйКредитВСбербанке();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПредодобренныйКредитВСбербанке();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераПредодобренныйКредитВСбербанке(ДанныеБаннеров)
	
	МассивИНН = ПредодобренныйКредитВСбербанкеПолучитьПредодобренныеИНН();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивИНН", МассивИНН);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.ИНН КАК ИНН
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН В(&МассивИНН)";
	ДанныеБаннера = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИНН");
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПредодобренныйКредитВСбербанке(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераПредодобренныйКредитВСбербанке(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПредодобренныйКредитВСбербанке(Баннер, ДанныеБаннера, ТекущаяДата)
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка + ?(ДанныеБаннера.Количество() = 1, ДанныеБаннера[0], "");
	
	Баннер.ТекстБаннера = ТекстБаннерПредодобренныйКредитВСбербанке(ДанныеБаннера, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерПредодобренныйКредитВСбербанке(ДанныеБаннера, НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Оформите СМАРТ-кредит в Сбербанке'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнать подробности'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераПредодобренныйКредитВСбербанке()

	Возврат "ПредодобренныйКредитВСбербанке";

КонецФункции

Функция ДоступностьБаннераПредодобренныйКредитВСбербанке()
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйБухгалтер = РольДоступна("ДобавлениеИзменениеНастроекБухгалтерии");
	
	Возврат (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйБухгалтер)
		И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

#КонецОбласти

#Область НастройкаВидимостиСчетовУчета

Функция НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение)
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяНастройкаВидимостиСчетовУчета();
	Баннер.Идентификатор        = ИдентификаторБаннераНастройкаВидимостиСчетовУчета();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераНастройкаВидимостиСчетовУчета();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераНастройкаВидимостиСчетовУчета();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиНастройкаВидимостиСчетовУчета();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ДанныеБаннера.Вставить("Размещение", Размещение);
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннера", ДатаНачалаПоказаБаннераНастройкаВидимостиСчетов());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераНастройкаВидимостиСчетовУчета(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннера, Баннер)
	
	ПользовательНеИзменялНастройкуСчетовУчета =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ВРег("ПоказыватьСчетаУчетаВДокументах"), Неопределено) = Неопределено;
	
	БаннерПоказываетсяМеньшеМесяца = НачалоДня(ТекущаяДатаСеанса()) < ДобавитьМесяц(ДанныеБаннера.ДатаНачалаПоказаБаннера, 1);
	
	Возврат ПользовательНеИзменялНастройкуСчетовУчета И БаннерПоказываетсяМеньшеМесяца;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНастройкаВидимостиСчетовУчета(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераНастройкаВидимостиСчетовУчета(Баннер.НавигационнаяСсылка);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераНастройкаВидимостиСчетовУчета(НавигационнаяСсылка)

	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Счета бухгалтерского учета могут показываться или скрываться'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Управляйте этим в'")),
		" ",
		НовыйСтрокаБаннера(НСтр("ru='Персональных настройках'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераНастройкаВидимостиСчетовУчета()

	Возврат "НастройкаВидимостиСчетовУчета";

КонецФункции

Функция ДоступностьБаннераНастройкаВидимостиСчетовУчета()
	
	Возврат ОбщегоНазначенияПовтИсп.РазделениеВключено()
		И ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных()
		И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком()
		И Пользователи.ЭтоПолноправныйПользователь(,, Ложь);
	
КонецФункции

Функция ИмяКартинкиНастройкаВидимостиСчетовУчета()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ДатаНачалаПоказаБаннераНастройкаВидимостиСчетов()

	ИмяНастройки = ВРег("ДатаНачалаПоказаБаннераНастройкаВидимостиСчетов");
	ДатаНачалаПоказаБаннера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки, Неопределено);
	Если ДатаНачалаПоказаБаннера = Неопределено Тогда
		ДатаНачалаПоказаБаннера = НачалоДня(ТекущаяДатаСеанса());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки, Неопределено, ДатаНачалаПоказаБаннера);
	КонецЕсли;
	
	Возврат ДатаНачалаПоказаБаннера;

КонецФункции

#КонецОбласти

#Область ЗаявкаНаКредит

Функция НовыйБаннерЗаявкаНаКредит()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисЗаявкаНаКредит();
	Баннер.Идентификатор        = ИдентификаторБаннераЗаявкаНаКредит();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипЗаявкаНаКредит();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераЗаявкаНаКредит();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераЗаявкаНаКредит();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаЗаявкаНаКредит(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераЗаявкаНаКредит();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ПерсонализированныеДанные.Организация.ИностраннаяОрганизация КАК ИностраннаяОрганизация,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК СреднемесячноеПоступлениеНаСчет
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ЗаявкаНаКредит)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераЗаявкаНаКредит(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого Данные Из ДанныеБаннера Цикл
		
		Если Данные.ИностраннаяОрганизация Тогда
			Продолжить;
		КонецЕсли;
		
		Если Данные.СреднемесячноеПоступлениеНаСчет >= СреднемесячноеПоступлениеНаСчетДляРекламыЗаявкаНаКредит() Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЗаявкаНаКредит(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннерЗаявкаНаКредит(ДанныеБаннера, Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерЗаявкаНаКредит(ДанныеБаннера, НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Хотите получить кредит на развитие бизнеса?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Оформить заявку'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераЗаявкаНаКредит()

	Возврат "ЗаявкаНаКредит";

КонецФункции

Функция ДоступностьБаннераЗаявкаНаКредит()
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйБухгалтер = РольДоступна("ДобавлениеИзменениеНастроекБухгалтерии");
	
	Возврат (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйБухгалтер)
		И ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаКредит")
		И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РаботаСоСпискомБаннеров

Функция ОбщийСписокБаннеров(Размещение)
	
	СписокБаннеров = НовыйТаблицаСписокБаннеров();
	
	Если Размещение = ИмяРазмещенияОбщее() Тогда
		// Исключительные баннеры. Если выполняются условия показа для одного из них,
		// остальные баннеры не показываются.
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПоддержкаСервиса()); // должен быть первым
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерТестовыйТариф());
		
		Если Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроблемыСНалоговымиПлатежами());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроверкаОплатыНалогов(Размещение));
		КонецЕсли;
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНадежностьБанков());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерСтороннийСертификат());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЧисленностьСотрудников());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерОтчетнаяКампания());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНДСКВычетуКонтрагент());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНДСКВычетуСПАРК());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКрупнейшийДебитор());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКоличествоКонтрагентов());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКрупнейшийДебиторСПАРК());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерОбщееКоличествоДокументов());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияПоступление() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКоличествоПоступлений());
		
	ИначеЕсли Размещение = ИмяРазмещенияРеализация() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКоличествоРеализаций());

	ИначеЕсли Размещение = ИмяРазмещенияНалогиИОтчеты() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());

	ИначеЕсли Размещение = ИмяРазмещенияБанковскиеВыписки() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		Если ПолучитьФункциональнуюОпцию("ИнтерфейсТакси") Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроблемыСНалоговымиПлатежами());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроверкаОплатыНалогов(Размещение));
		КонецЕсли;
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияДеньги() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		Если ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой") Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроблемыСНалоговымиПлатежами());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроверкаОплатыНалогов(Размещение));
		КонецЕсли;
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияКассовыеДокументы() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		
	ИначеЕсли Размещение = ИмяРазмещенияМониторНалогов() Тогда
		
		Если Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроблемыСНалоговымиПлатежами());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПроверкаОплатыНалогов(Размещение));
		КонецЕсли;
		
	ИначеЕсли Размещение = ИмяРазмещенияТребованиеНакладная() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		
	ИначеЕсли Размещение = ИмяРазмещенияМониторОсновныхПоказателей() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
	ИначеЕсли Размещение = ИмяРазмещенияПродажиПоКонтрагентам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
	ИначеЕсли Размещение = ИмяРазмещенияАвансовыеОтчеты() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
	ИначеЕсли Размещение = ИмяРазмещенияАнализДвиженийДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияОстаткиДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияПоступленияДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияРасходыДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияДоходыРасходы() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияОборотныеСредства() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияПродажиПоМесяцам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияАнализНеоплаченныхСчетовПоставщиков() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияДинамикаЗадолженностиПоставщикам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияЗадолженностьПоставщикам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	ИначеЕсли Размещение = ИмяРазмещенияЗадолженностьПоставщикамПоСрокамДолга() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		
	КонецЕсли;
	
	Возврат СписокБаннеров;
	
КонецФункции

Функция БаннерыПользователя(Организация, Размещение, ТекущаяДата)
	
	СписокБаннеров             = ОбщийСписокБаннеров(Размещение);
	СписокБаннеровПользователя = НовыйТаблицаСписокБаннеров();
	
	// Если организация не указана, исключим из списка баннеры, зависящие от организации.
	ИсключитьБаннерыПоОрганизации(СписокБаннеров, Организация);
	
	// Исключим из списка баннеры, по которым уже подключены сервисы.
	ИсключитьБаннерыПоПодключеннымСервисам(СписокБаннеров, Организация, ТекущаяДата);
	
	// Дополним список баннеров обстоятельствами закрытия баннера.
	ДополнитьБаннерыОбстоятельствамиЗакрытия(СписокБаннеров, Организация);
	
	Для Каждого СтрокаСписка Из СписокБаннеров Цикл
		
		ОбстоятельстваЗакрытия = СтрокаСписка.Баннер.ОбстоятельстваЗакрытия;
		
		// Пропускаем баннеры, которые уже показывали в этом периоде.
		Если ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.День Тогда
			Если НачалоДня(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоДня(ТекущаяДата) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц Тогда
			Если НачалоМесяца(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоМесяца(ТекущаяДата) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал Тогда
			Если НачалоКвартала(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоКвартала(ТекущаяДата) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ОбстоятельстваЗакрытия.Свойство("ИсключитьБаннер") И ОбстоятельстваЗакрытия.ИсключитьБаннер Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаСпискаПользователя = СписокБаннеровПользователя.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСпискаПользователя, СтрокаСписка);
		
	КонецЦикла;
	
	// Отсортируем баннеры по приоритету.
	СписокБаннеровПользователя.Сортировать("Порядок", Новый СравнениеЗначений);
	
	Возврат СписокБаннеровПользователя;
	
КонецФункции

Процедура ИсключитьБаннерыПоПодключеннымСервисам(СписокБаннеров, Организация, ТекущаяДата)
	
	// Обновим данные по которым будем получать информацию.
	Разделы = РазделыПерсонализированныхДанныхПодключенныеСервисы(СписокБаннеров);
	ИнтервалОбновления = 60 * 60 * 24 * 7; // обновляем данные с интервалом 1 неделя.
	РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(
		Организация,
		ТекущаяДата,
		ИнтервалОбновления,
		Разделы);
		
	УстановитьПривилегированныйРежим(Истина);
	ПодключенныеСервисы = ПодключенныеСервисы(Организация, Разделы);
	УстановитьПривилегированныйРежим(Ложь);
	
	// Если подключенных сервисов нет, то дальше ничего не делаем.
	Если ПодключенныеСервисы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИсключенныеСтроки = Новый Массив;
	Для Каждого ИмяСервиса Из ПодключенныеСервисы Цикл
		Отбор = Новый Структура("ИмяСервиса", ИмяСервиса);
		СтрокиБаннеров = СписокБаннеров.НайтиСтроки(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключенныеСтроки, СтрокиБаннеров, Истина);
	КонецЦикла;
	
	УдалитьСтрокиИзТаблицы(СписокБаннеров, ИсключенныеСтроки);
	
КонецПроцедуры

Процедура ДополнитьБаннерыОбстоятельствамиЗакрытия(СписокБаннеров, Организация)
	
	ЗакрытыеПользователемБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		НовыйЗакрытыеПользователемБаннеры());
	
	Для Каждого КлючИЗначение Из ЗакрытыеПользователемБаннеры.БаннерыБезОрганизации Цикл
		Для Каждого СтрокаСписка Из СписокБаннеров.НайтиСтроки(Новый Структура("ИдентификаторБаннера", КлючИЗначение.Ключ)) Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
				СтрокаСписка.Баннер.ОбстоятельстваЗакрытия,
				КлючИЗначение.Значение,
				Истина);
		КонецЦикла;
	КонецЦикла;
	
	БаннерыПоОрганизации = ЗакрытыеПользователемБаннеры.БаннерыПоОрганизации[Организация];
	Если БаннерыПоОрганизации <> Неопределено Тогда
		Для Каждого КлючИЗначение Из БаннерыПоОрганизации Цикл
			Для Каждого СтрокаСписка Из СписокБаннеров.НайтиСтроки(Новый Структура("ИдентификаторБаннера", КлючИЗначение.Ключ)) Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
					СтрокаСписка.Баннер.ОбстоятельстваЗакрытия,
					КлючИЗначение.Значение,
					Истина);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИсключитьБаннерыПоОрганизации(СписокБаннеров, Организация)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ИсключенныеСтроки = Новый Массив;
	Для каждого СтрокаБаннера Из СписокБаннеров Цикл
		Если СтрокаБаннера.Баннер.ЗависитОтОрганизации Тогда
			ИсключенныеСтроки.Добавить(СтрокаБаннера);
		КонецЕсли;
	КонецЦикла;
	УдалитьСтрокиИзТаблицы(СписокБаннеров, ИсключенныеСтроки);

КонецПроцедуры

#КонецОбласти

#Область ПерсонализированныеДанные

// Вызывает обновление записей регистра сведений "ПерсонализированныеДанные" фоновым заданием.
// При обновлении учитывается актуальность данных регистра сведений
// Параметры:
//   Параметры - Структура - Структура с параметрами переданная в фоновое задание
//		*Организация - СправочникСсылка.Организации - организация по которой нужно обновить персонализированные данные.
//									Если не заполнено данные будут обновлены по всем доступным организациям
//	ВременноеХранилищеРезультата - Строка - путь к временному хранилищу (не используется в рамках данной процедуры)	
Процедура ОбновитьПерсонализированныеДанныеВФоне(Параметры, ВременноеХранилищеРезультата) Экспорт

	ОбновитьПерсонализированныеДанные(Параметры.Организация, Истина, Параметры.РазделыПерсонализированныхДанных);

КонецПроцедуры

// Вызывает перезапись данных регистра сведений "ПерсонализированныеДанные" фоновым заданием
// Данные регистра сведений перезаписываются без учета актуальности.
// Параметры:
//   Параметры - Структура - Структура с параметрами переданная в фоновое задание
//		*Организация - СправочникСсылка.Организации - организация по которой нужно обновить персонализированные данные.
//									Если не заполнено данные будут обновлены по всем доступным организациям
//	ВременноеХранилищеРезультата - Строка - путь к временному хранилищу (не используется в рамках данной процедуры).
//
Процедура ПерезаписатьПерсонализированныеДанныеВФоне(Параметры, ВременноеХранилищеРезультата) Экспорт

	ОбновитьПерсонализированныеДанные(Параметры.Организация, Ложь, Параметры.РазделыПерсонализированныхДанных);

КонецПроцедуры	

// Вызывает первоначальное заполнение регистра сведений "ПерсонализированныеДанные" по всем организациям
// Запускается при обновлении версии конфиурации и при переходе с предыдущей версии.
Процедура ЗаполнитьПерсонализированныеДанныеРуководителя() Экспорт
	
	ОбновитьПерсонализированныеДанныеПоВсемОрганизациям(ТекущаяДатаСеанса());
	
КонецПроцедуры

// Создает пустую таблицу контейнер для персонализированных данных
// Возвращаемое значение:
//	ТаблицаЗначений - Пустая таблица персонализированных данных.
//
Функция ТаблицаДанных() Экспорт
	
	ОписаниеТиповЧисло15_3 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);
	
	НоваяТаблицаДанных = Новый ТаблицаЗначений;
	
	НоваяТаблицаДанных.Колонки.Добавить("ЗначениеПоказателя", ОписаниеТиповЧисло15_3);
	НоваяТаблицаДанных.Колонки.Добавить("Порядок",            Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(1)));
	НоваяТаблицаДанных.Колонки.Добавить("ДанныеРасшифровки",  Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	
	Возврат НоваяТаблицаДанных;
	
КонецФункции

// Обновляет данные регистра сведений "ПерсонализированныеДанные".
// Параметры:
//   Параметры - Структура - Структура с параметрами
//		*Организация - СправочникСсылка.Организации - организация по которой нужно обновить персонализированные данные.
//									Если не заполнено данные будут обновлены по всем доступным организациям
//	УчитыватьАктуальностьДанных - булево - Истина - перед обновлением проверить что данные неактуальны, Ложь - не проверять.
//
Процедура ОбновитьПерсонализированныеДанные(Организация, УчитыватьАктуальностьДанных, РазделыПерсонализированныхДанных)
	
	Дата = ТекущаяДатаСеанса();
	
	ИнтервалОбновления = 0;
	Если УчитыватьАктуальностьДанных Тогда
		ИнтервалОбновления = 600;
	КонецЕсли;
	
	// Если организация не заполнена то данные нужно обновить по всем
	Если ЗначениеЗаполнено(Организация) Тогда
		СписокДоступныхОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	
		Если СписокДоступныхОрганизаций.Найти(Организация) <> Неопределено Тогда
			РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(Организация, Дата, ИнтервалОбновления, РазделыПерсонализированныхДанных);
		КонецЕсли;
	Иначе
		ОбновитьПерсонализированныеДанныеПоВсемОрганизациям(Дата, ИнтервалОбновления, РазделыПерсонализированныхДанных);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет данные регистра сведений "ПерсонализированныеДанные" по всем организациям
// Параметры:
//  Дата - Дата - Дата на которую нужно получить данные для записи в регистр
//	ИнтервалОбновления - Число - Период в секундах в течении которого данные считаются актуальными
//
Процедура ОбновитьПерсонализированныеДанныеПоВсемОрганизациям(Дата, ИнтервалОбновления = 0, РазделыПерсонализированныхДанных = Неопределено)
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	
	Для Каждого Организация Из ДоступныеОрганизации Цикл
		
		РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(Организация, Дата, ИнтервалОбновления, РазделыПерсонализированныхДанных);
		
	КонецЦикла;
	
КонецПроцедуры

Функция РазделыПерсонализированныхДанных(СписокБаннеров)
	
	МассивРазделов = Новый Массив;
	
	Для Каждого ИдентификаторБаннера Из СписокБаннеров.ВыгрузитьКолонку("ИдентификаторБаннера") Цикл
		
		Если ИдентификаторБаннера = ИдентификаторБаннераНДСКВычетуКонтрагент()
			ИЛИ ИдентификаторБаннера = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.НДСКВычету);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераОбщееКоличествоДокументов() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.КоличествоПоступлений);
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.КоличествоРеализаций);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераЧисленностьСотрудников() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленность);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.КоличествоКонтрагентов);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераПроверкаОплатыНалогов() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.СервисПроверкаОплатыНалогов);

		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.ИнтеграцияСоSmartway);

		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераЗаявкаНаКредит() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.ЗаявкаНаКредит);

		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивРазделов);
	
КонецФункции

Функция РазделыПерсонализированныхДанныхПодключенныеСервисы(СписокБаннеров)
	
	МассивРазделов = Новый Массив;
	
	МассивСервисов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокБаннеров.ВыгрузитьКолонку("ИмяСервиса"));
	Для Каждого ИмяСервиса Из МассивСервисов Цикл
		Если ИмяСервиса = ИмяСервис1СКонтрагент() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СКонтрагентПодключен);
			
		ИначеЕсли ИмяСервиса = ИмяСервис1СОтчетность() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СОтчетностьПодключен);
			
		ИначеЕсли ИмяСервиса = ИмяСервис1СПАРК() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СПАРКПодключен);
			
		ИначеЕсли ИмяСервиса = ИмяСервис1СЭДО() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СЭДОПодключен);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивРазделов;
	
КонецФункции

// Формирует таблицу данных для персонализированных данных по организации на дату
// Параметры
// 	Организация - СправочникСсылка.Организации - Организация по которой нужны данные
// 	ДатаПолученияДанных - Дата - дата на которую нужны остатки
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с данными для Прсонализированных данных.
//
Функция ПолучитьНДСКВычетуДляПерсонализированныхДанных(Организация, ДатаПолученияДанных) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	
	Если НЕ УчетнаяПолитика.ПлательщикНДС(Организация, ДатаПолученияДанных) Тогда
		Возврат ТаблицаДанных;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("НачалоКвартала", НачалоКвартала(ДатаПолученияДанных));
	Запрос.УстановитьПараметр("КонецКвартала",  Новый Граница(КонецДня(ДатаПолученияДанных), ВидГраницы.Включая));
	ВидыЦенностейАвансы = Новый Массив;
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	Запрос.УстановитьПараметр("ВидыЦенностейАвансы", ВидыЦенностейАвансы);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДС.Контрагент КАК Контрагент,
	|	СУММА(НДС.ЗначениеПоказателя) КАК ЗначениеПоказателя
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСПокупки.Поставщик КАК Контрагент,
	|		НДСПокупки.НДСОборот КАК ЗначениеПоказателя
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|				&НачалоКвартала,
	|				&КонецКвартала,
	|				,
	|				Организация = &Организация
	|					И НЕ ВидЦенности В (&ВидыЦенностейАвансы)) КАК НДСПокупки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДСПредъявленный.Поставщик,
	|		НДСПредъявленный.НДСОборот
	|	ИЗ
	|		РегистрНакопления.НДСПредъявленный.Обороты(
	|				&НачалоКвартала,
	|				&КонецКвартала,
	|				,
	|				Организация = &Организация
	|					И НЕ ВидЦенности В (&ВидыЦенностейАвансы)) КАК НДСПредъявленный) КАК НДС
	|
	|СГРУППИРОВАТЬ ПО
	|	НДС.Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеПоказателя УБЫВ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	УстановитьПривилегированныйРежим(Ложь);
	
	Для ИндексСтроки = 0 По Мин(1, Результат.Количество() - 1) Цикл
		
		СтрокаРезультата = Результат[ИндексСтроки];
		Контрагент = СтрокаРезультата.Контрагент;
		
		СтрокаДанных = ТаблицаДанных.Добавить();
		СтрокаДанных.ДанныеРасшифровки  = Контрагент;
		СтрокаДанных.Порядок            = 1;
		СтрокаДанных.ЗначениеПоказателя = СтрокаРезультата.ЗначениеПоказателя;
		
	КонецЦикла;
	
	// Добавляем итог по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = Результат.Итог("ЗначениеПоказателя");
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Формирует таблицу данных для персонализированных данных по организации на дату
// Параметры:
//		ДатаПолученияДанных - Дата - дата на которую нужны остатки
// Возвращаемое значение:
//		ТаблицаЗначений - Таблица с данными для Персонализированных данных
//
Функция ПолучитьКоличествоКонтрагентовДляПерсонализированныхДанных(ДатаПолученияДанных) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоКвартала", НачалоКвартала(ДатаПолученияДанных));
	Запрос.УстановитьПараметр("КонецКвартала",  КонецДня(ДатаПолученияДанных));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Контрагенты.Ссылка) КАК ЗначениеПоказателя
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ДатаСоздания >= &НачалоКвартала
	|	И Контрагенты.ДатаСоздания <= &КонецКвартала
	|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	УстановитьПривилегированныйРежим(Ложь);
	
	// Добавляем информацию по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = Результат.Итог("ЗначениеПоказателя");
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С-Отчетность подключен.
// Параметры:
//		Организация - Справочник.Организация - Организация, для которой проверяется сервис.
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СОтчетностьПодключен(Организация) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СервисПодключен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Организация,,Истина);
	СтрокаДанных.ЗначениеПоказателя = ?(СервисПодключен, 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С:ЭДО подключен.
// Параметры:
//		Организация - Справочник.Организация - Организация, для которой проверяется сервис.
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СЭДОПодключен(Организация) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СервисПодключен = ОбменСКонтрагентами.ОрганизацияПодключена(Организация);
	СтрокаДанных.ЗначениеПоказателя = ?(СервисПодключен, 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С:Контрагент подключен.
//
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СКонтрагентПодключен() Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СтрокаДанных.ЗначениеПоказателя = ?(РаботаСКонтрагентамиБП.Сервис1СКонтрагентПодключен(), 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С:Контрагент подключен.
//
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СПАРКПодключен() Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		СервисПодключен = ИнтернетПоддержкаПользователей.УслугаПодключена(
			СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска());
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		ВидОшибки = СПАРКРискиМониторингСобытий.ОбновитьСобытияМониторинга();
		СервисПодключен = НЕ ЗначениеЗаполнено(ВидОшибки);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	СтрокаДанных.ЗначениеПоказателя = ?(СервисПодключен, 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Формирует таблицу данных для персонализированных данных по организации на дату
// 	Организация - СправочникСсылка.Организации - Организация по которой нужны данные
// 	ДатаПолученияДанных - Дата - дата на которую нужны остатки
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с данными для персонализированных данных
//
Функция СреднесписочнаяЧисленностьОрганизацииДляПерсонализированныеДанные(Организация, ДатаПолученияДанных) Экспорт
	
	ТаблицаДанных = ПерсонализированныеПредложенияСервисов.ТаблицаДанных();
	
	СведенияОЧисленностиРаботников = КадровыйУчет.СреднесписочнаяЧисленностьРаботающих(
		Организация,
		НачалоГода(ДатаПолученияДанных),
		КонецДня(ДатаПолученияДанных));
	ТекущаяЧисленностьРаботников = СведенияОЧисленностиРаботников.ЧисленностьРаботников;
	
	// Добавляем итог по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = ТекущаяЧисленностьРаботников;
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Устанавливает значение параметра сеанса РазрешенныеПользователюРазделыПерсонализированныхДанных,
// который используется в тексте ограничения доступа к персонализированных данным.
//
// Подробнее см. СтандартныеПодсистемыСервер.УстановкаПараметровСеанса()
//
Процедура УстановитьРазрешенныеПользователюРазделыПерсонализированныхДанных(ИмяПараметра, УстановленныеПараметры) Экспорт
	
	Если ИмяПараметра = "РазрешенныеПользователюРазделыПерсонализированныхДанных" Тогда
		
		ПараметрыСеанса.РазрешенныеПользователюРазделыПерсонализированныхДанных = 
			Перечисления.РазделыПерсонализированныхДанных.РазрешенныеПользователюРазделыПерсонализированныхДанных();
		УстановленныеПараметры.Добавить(ИмяПараметра);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПодключенныеСервисы(Организация, Разделы)
	
	ПодключенныеСервисы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Разделы", Разделы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.Раздел,
	|	ПерсонализированныеДанные.ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел В(&Разделы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЗначениеПоказателя = 1 Тогда
			Если Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СКонтрагентПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СКонтрагент());
			ИначеЕсли Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СОтчетностьПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СОтчетность());
			ИначеЕсли Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СПАРКПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СПАРК());
			ИначеЕсли Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СЭДОПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СЭДО());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенныеСервисы;
	
КонецФункции

Процедура ЗаполнитьСреднесписочнаяЧисленностьПредыдущая() Экспорт
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	Раздел = Перечисления.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленностьПредыдущая;
	ДатаПолученияДанных = ТекущаяДатаСеанса();
	
	Для Каждого Организация Из ДоступныеОрганизации Цикл
		
		СведенияОЧисленностиРаботников = КадровыйУчет.СреднесписочнаяЧисленностьРаботающих(
			Организация,
			НачалоГода(ДатаПолученияДанных),
			КонецДня(ДатаПолученияДанных));
		ТекущаяЧисленностьРаботников = СведенияОЧисленностиРаботников.ЧисленностьРаботников;
		
		НаборЗаписей = РегистрыСведений.ПерсонализированныеДанные.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация        = Организация;
		Запись.Раздел             = Раздел;
		Запись.ЗначениеПоказателя = ТекущаяЧисленностьРаботников;
		Запись.НомерСтрокиРаздела = 1;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПолучениеСтороннегоСертификата

// Возвращает признак нужно ли искать сторонний сертификат отчетности.
// Параметры:
//		Организация - СправочникСсылка.Организации - Организация, по которой выполняется поиск.
//
Функция НужноИскатьСертификат(Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Организация,,Истина) Тогда
		// Сервис подключен, ничего искать не нужно
		Возврат Ложь;
	КонецЕсли;
	
	ИнформацияОСтороннихСертификатах = ИнформацияОСтороннихСертификатах(Организация);
	Если ИнформацияОСтороннихСертификатах = Неопределено Тогда
		// Никакой информации нет - нужно искать.
		Возврат Истина;
	КонецЕсли;
	
	Если НачалоМесяца(ТекущаяДатаСеанса()) <= НачалоМесяца(ИнформацияОСтороннихСертификатах.ДатаПоиска) Тогда
		// В этом месяце уже искали сертификаты.
		Возврат Ложь;
	КонецЕсли;
	
	Если ИнформацияОСтороннихСертификатах.СертификатНайден 
		И ИнформацияОСтороннихСертификатах.СрокДействияСертификата <= ТекущаяДатаСеанса() Тогда
		// Сертификат найден, снова искать не нужно.
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Записывает информацию о сторонних сертификах в хранилище общих настроек.
// Параметры:
//		Организация - СправочникСсылка.Организации - Организация, по которой записывается информация о сертификатах.
//		СторонниеСертификаты - Структура - Ключ - имя удостоверяющего центра, Значение - срок действия сертификата.
//
Процедура ЗаписатьИнформациюОСтороннихСертификатах(Организация, СторонниеСертификаты) Экспорт
	
	Информация = НовыйИнформацияОСтороннихСертификатах();
	Информация.ДатаПоиска           = ТекущаяДатаСеанса();
	Информация.СертификатНайден     = (СторонниеСертификаты.Количество() > 0);
	Информация.СторонниеСертификаты = СторонниеСертификаты;
	СрокДействияСертификата         = '00010101';
	Для Каждого СтороннийСертификат Из СторонниеСертификаты Цикл
		СрокДействия = СтороннийСертификат.Значение;
		Если СрокДействияСертификата = '00010101' Тогда
			СрокДействияСертификата = СрокДействия;
		Иначе
			СрокДействияСертификата = Мин(СрокДействия, СрокДействияСертификата);
		КонецЕсли;
	КонецЦикла;
	Информация.СрокДействияСертификата = СрокДействияСертификата;
	
	// Получим ранее сохраненные данные.
	ИнформацияОСертификатахПоОрганизациям = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастроекСторонниеСертификаты(),
		Новый Соответствие);
		
	// Добавим информация по организации.
	ИнформацияОСертификатахПоОрганизациям.Вставить(Организация, Информация);
	
	// Сохраним всю информацию.
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастроекСторонниеСертификаты(),
		ИнформацияОСертификатахПоОрганизациям);
	
КонецПроцедуры

Функция ИнформацияОСтороннихСертификатах(Организация)

	ИнформацияОСертификатахПоОрганизациям = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастроекСторонниеСертификаты(),
		Новый Соответствие);
		
	Возврат ИнформацияОСертификатахПоОрганизациям[Организация]

КонецФункции

Функция КомандировкиЗаПрошлыйМесяц(Организация, ДатаПолученияДанных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвансовыйОтчет.Ссылка КАК Ссылка,
	|	АвансовыйОтчет.ВидОперации КАК ВидОперации,
	|	АвансовыйОтчет.НазначениеАванса КАК НазначениеАванса,
	|	АвансовыйОтчет.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ВТАвансовыеОтчеты
	|ИЗ
	|	Документ.АвансовыйОтчет КАК АвансовыйОтчет
	|ГДЕ
	|	АвансовыйОтчет.Проведен
	|	И АвансовыйОтчет.Организация = &Организация
	|	И АвансовыйОтчет.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Счет,
	|	ХозрасчетныйВидыСубконто.НомерСтроки КАК НомерСубконто
	|ПОМЕСТИТЬ ВТСубконтоСтатьиЗатрат
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыйОтчетПрочее.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ВТСубконтоСтатьиЗатрат.НомерСубконто = 1
	|			ТОГДА ВЫРАЗИТЬ(АвансовыйОтчетПрочее.Субконто1 КАК Справочник.СтатьиЗатрат)
	|		КОГДА ВТСубконтоСтатьиЗатрат.НомерСубконто = 2
	|			ТОГДА ВЫРАЗИТЬ(АвансовыйОтчетПрочее.Субконто2 КАК Справочник.СтатьиЗатрат)
	|		КОГДА ВТСубконтоСтатьиЗатрат.НомерСубконто = 3
	|			ТОГДА ВЫРАЗИТЬ(АвансовыйОтчетПрочее.Субконто3 КАК Справочник.СтатьиЗатрат)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	АвансовыйОтчетПрочее.Содержание КАК Содержание
	|ПОМЕСТИТЬ ВТТаблицаПрочее
	|ИЗ
	|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСубконтоСтатьиЗатрат КАК ВТСубконтоСтатьиЗатрат
	|		ПО АвансовыйОтчетПрочее.СчетЗатрат = ВТСубконтоСтатьиЗатрат.Счет
	|ГДЕ
	|	АвансовыйОтчетПрочее.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТАвансовыеОтчеты.Ссылка КАК Ссылка
	|			ИЗ
	|				ВТАвансовыеОтчеты КАК ВТАвансовыеОтчеты)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиЗатрат.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%суточные%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%суточных%""
	|			ТОГДА ""СУТОЧНЫЕ""
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%проживани%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%гостиниц%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%отель%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%отеле%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%отеля%""
	|			ТОГДА ""ПРОЖИВАНИЕ""
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%билет%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%посадочн%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%перелет%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%перелёт%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%рейс%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%самолет%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%самолёт%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%поезд%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%а/б%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%%маршут_квитанция%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%контрольный талон%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%ж/д%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%жд%""
	|			ТОГДА ""БИЛЕТ""
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%командировочн%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%командиров%""
	|			ТОГДА ""КОМАНДИРОВКА""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидРасхода
	|ПОМЕСТИТЬ ВТСтатьиЗатрат
	|ИЗ
	|	Справочник.СтатьиЗатрат КАК СтатьиЗатрат
	|ГДЕ
	|	СтатьиЗатрат.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТТаблицаПрочее.СтатьяЗатрат КАК СтатьяЗатрат
	|			ИЗ
	|				ВТТаблицаПрочее КАК ВТТаблицаПрочее)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаПрочее.Ссылка КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""СУТОЧНЫЕ""
	|				ТОГДА 1
	|			КОГДА ВТТаблицаПрочее.Содержание ПОДОБНО ""%суточные%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%суточных%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировочные%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировочных%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировк%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировок%""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееСуточные,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""ПРОЖИВАНИЕ""
	|				ТОГДА 1
	|			КОГДА ВТТаблицаПрочее.Содержание ПОДОБНО ""%проживани%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%гостиниц%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%отель%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%отеле%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%отеля%""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееПроживание,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""БИЛЕТ""
	|				ТОГДА 1
	|			КОГДА ВТТаблицаПрочее.Содержание ПОДОБНО ""%билет%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%посадочн%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%перелет%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%перелёт%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%рейс%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%самолет%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%самолёт%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%поезд%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%а/б%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%маршут_квитанция%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%контрольный талон%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%ж/д%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%жд%""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееБилеты,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""КОМАНДИРОВКА""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееКомандировки
	|ПОМЕСТИТЬ ВТКомандировкиТаблицаПрочее
	|ИЗ
	|	ВТТаблицаПрочее КАК ВТТаблицаПрочее
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиЗатрат КАК ВТСтатьиЗатрат
	|		ПО ВТТаблицаПрочее.СтатьяЗатрат = ВТСтатьиЗатрат.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаПрочее.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТАвансовыеОтчеты.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	ВТАвансовыеОтчеты КАК ВТАвансовыеОтчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКомандировкиТаблицаПрочее КАК ВТКомандировкиТаблицаПрочее
	|		ПО ВТАвансовыеОтчеты.Ссылка = ВТКомандировкиТаблицаПрочее.Ссылка
	|ГДЕ
	|	(ВТАвансовыеОтчеты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАвансовыйОтчет.Командировка)
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командировочные%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командировочных%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командировк%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командирвок%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%суточные%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%суточных%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%проживани%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%суточные%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%суточных%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировочные%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировочных%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировк%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировок%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%проживани%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%гостиниц%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%отель%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%отеле%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%отеля%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%билет%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%а/б%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%маршрут_квитанция%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%контрольный талон%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%жд%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%посадочн%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%перелет%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%перелёт%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%рейс%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%самолет%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%самолёт%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%поезд%""
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееСуточные > 0
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееПроживание > 0
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееБилеты > 0
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееКомандировки > 0)";
	Период = НачалоМесяца(НачалоМесяца(ДатаПолученияДанных) - 1); // проверяем авансовые отчеты за прошлый месяц.
	Запрос.УстановитьПараметр("НачалоПериода", Период);
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Период));
	Запрос.УстановитьПараметр("Организация",   Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СтрокаДанных.ЗначениеПоказателя = Выборка.КоличествоДокументов;
	
	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти

#Область ПредодобренныйКредитВСбербанке

Функция ПредодобренныйКредитВСбербанкеПолучитьПредодобренныеИНН() Экспорт
	
	ПредодобренныйКредитВСбербанкеПолучитьДанныеИзСервиса(Ложь);
	
	ПредодобренныеИНН = ПредодобренныйКредитВСбербанкеПолучитьЗначениеИзХранилища(
		"ПредодобренныеИНН",
		Новый Массив
	);
	
	Результат = Новый Массив;
	
	Для каждого ПредодобренныйИНН Из ПредодобренныеИНН Цикл
		Если КонецДня(ПредодобренныйИНН.СрокДействия) < ТекущаяДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ПредодобренныйИНН.ИНН);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПредодобренныйКредитВСбербанкеПолучитьДанныеИзСервиса(ПолучатьВФоне, ДополнительныеИНН = Неопределено) Экспорт
	
	ДатаОбновления = ПредодобренныйКредитВСбербанкеПолучитьЗначениеИзХранилища(
		"ДатаОбновления",
		Дата(1, 1, 1)
	);
	
	ИнтервалОбновления = 86400 * 14; // Раз в две недели.
	
	Если ТекущаяДатаСеанса() < ДатаОбновления + ИнтервалОбновления Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ДополнительныеИНН", ДополнительныеИНН);
	
	Если ПолучатьВФоне Тогда
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru='Получение данных по предодобренным кредитам Сбербанка'");
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		ПараметрыВыполнения.ОжидатьЗавершение = 0;
		Результат = ДлительныеОперации.ВыполнитьВФоне(
			"ПерсонализированныеПредложенияСервисов.ПредодобренныйКредитВСбербанкеЗагрузитьДанныеИзСервиса",
			ПараметрыПроцедуры,
			ПараметрыВыполнения
		);
	Иначе
		ПредодобренныйКредитВСбербанкеЗагрузитьДанныеИзСервиса(ПараметрыПроцедуры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПредодобренныйКредитВСбербанкеЗагрузитьДанныеИзСервиса(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.ИНН КАК ИНН,
		|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо) КАК ЭтоФизЛицо
		|ИЗ
		|	Справочник.Организации КАК Организации";
		Выборка = Запрос.Выполнить().Выбрать();
		
		СписокИНН = Новый Массив;
		Пока Выборка.Следующий() Цикл
			Если ПустаяСтрока(Выборка.ИНН) Тогда
				Продолжить;
			КонецЕсли;
			
			РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(
				Выборка.ИНН,
				Выборка.ЭтоФизЛицо
			);
			Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
				Продолжить;
			КонецЕсли;
			
			СписокИНН.Добавить(Выборка.ИНН);
		КонецЦикла;
		
		// В параметрах может быть передан дополнительный список ИНН.
		// Его добавляем без дополнительных проверок: считаем, что они были выполнены заранее.
		Если ТипЗнч(Параметры) = Тип("Структура") И Параметры.Свойство("ДополнительныеИНН")
			И ТипЗнч(Параметры.ДополнительныеИНН) = Тип("Массив")
			И Параметры.ДополнительныеИНН.Количество() > 0 Тогда
			Для каждого ИНН Из Параметры.ДополнительныеИНН Цикл
				СписокИНН.Добавить(ИНН);
			КонецЦикла;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("innList", СписокИНН);
		СтруктураДанных.Вставить("programName", Метаданные.Имя);
		СтруктураДанных.Вставить("programVersion", Метаданные.Версия);
		
		JSON = Новый ЗаписьJSON();
		JSON.УстановитьСтроку();
		ЗаписатьJSON(JSON, СтруктураДанных);
		ТелоЗапроса = JSON.Закрыть();
		
		АдресСервиса = ПредодобренныйКредитВСбербанкеИмяСервера() + "/api/advertisement/rest/check";
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСервиса);
		ЗапросHTTP = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере);
		ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
		ЗапросHTTP.Заголовки.Вставить("Content-Type", "application/json");
		
		ЗащищенноеСоединение = ?(СтруктураURI.Схема = "https", Новый ЗащищенноеСоединениеOpenSSL, Неопределено);
		Соединение = Новый HTTPСоединение(
			СтруктураURI.Хост,
			СтруктураURI.Порт,,,,
			20,
			ЗащищенноеСоединение
		);
		
		ОтветСервиса = Соединение.ВызватьHTTPМетод("POST", ЗапросHTTP);
		
		Если ОтветСервиса.КодСостояния <> 200 Тогда
			ВызватьИсключение СтрШаблон(
				"%1: %2 %3",
				НСтр("ru='Сервис получения данных по предодобренным кредитам Сбербанка'"),
				ОтветСервиса.КодСостояния,
				ОтветСервиса.ПолучитьТелоКакСтроку()
			);
		КонецЕсли;
		
		JSON = Новый ЧтениеJSON();
		JSON.УстановитьСтроку(ОтветСервиса.ПолучитьТелоКакСтроку());
		ДанныеОтвета = ПрочитатьJSON(JSON, Ложь, "date");
		
		ПредодобренныеИНН = Новый Массив;
		Для каждого СтрокаОтвета Из ДанныеОтвета.innInfoList Цикл
			ПредодобренныйИНН = Новый Структура;
			ПредодобренныйИНН.Вставить("ИНН", СтрокаОтвета.inn);
			ПредодобренныйИНН.Вставить("СрокДействия", СтрокаОтвета.date);
			ПредодобренныеИНН.Добавить(ПредодобренныйИНН);
		КонецЦикла;
		
		ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище("ПредодобренныеИНН", ПредодобренныеИНН);
		ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище("ДатаОбновления", ТекущаяДатаСеанса());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru='Обновление данных по предодобренным кредитам Сбербанка'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПредодобренныйКредитВСбербанкеИмяСервера() Экспорт
	
	Возврат "https://reportbank.1c.ru";
	
КонецФункции

Функция ПредодобренныйКредитВСбербанкеПолучитьЗначениеИзХранилища(Ключ, ЗначениеПоУмолчанию)
	
	УстановитьПривилегированныйРежим(Истина);
	Значение = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(
		ИмяСервисПредодобренныйКредитВСбербанке(),
		Ключ
	);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Процедура ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище(Ключ, Значение)
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(
		ИмяСервисПредодобренныйКредитВСбербанке(),
		Значение,
		Ключ
	);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриЗаписиОрганизации(ОбъектЗаписи, Отказ) Экспорт
	
	Перем ЭтоНовый;
	
	Если ОбъектЗаписи.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый) Тогда
		
		// При записи новой организации необходимо досрочно
		// обновить список предодобренных Сбербанком ИНН.
		Если ЭтоНовый И НЕ Отказ Тогда
			
			// Если ИНН организации валидный, передаем его в фоновое задание
			// для обработки на сервере, поскольку в результаты запроса он еще не попадет.
			ДополнительныеИНН = Новый Массив;
			Если НЕ ПустаяСтрока(ОбъектЗаписи.ИНН) Тогда
				ЭтоФизЛицо = ОбъектЗаписи.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
				РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(
					ОбъектЗаписи.ИНН,
					ЭтоФизЛицо
				);
				Если РезультатПроверки.СоответствуетТребованиям Тогда
					ДополнительныеИНН.Добавить(ОбъектЗаписи.ИНН);
				КонецЕсли;
			КонецЕсли;
			
			ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище("ДатаОбновления", Дата(1, 1, 1));
			ПредодобренныйКредитВСбербанкеПолучитьДанныеИзСервиса(Истина, ДополнительныеИНН);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоПериодНДС(ТекущаяДата)
	
	ЭтотГод = Год(ТекущаяДата);
	Если ТекущаяДата > Дата(ЭтотГод - 1, 12, 15) И ТекущаяДата < Дата(ЭтотГод , 1, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли ТекущаяДата > Дата(ЭтотГод, 3, 15) И ТекущаяДата < Дата(ЭтотГод, 4, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли ТекущаяДата > Дата(ЭтотГод, 6, 15) И ТекущаяДата < Дата(ЭтотГод, 7, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли ТекущаяДата > Дата(ЭтотГод, 9, 15) И ТекущаяДата < Дата(ЭтотГод, 10, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли  ТекущаяДата > Дата(ЭтотГод, 12, 15) И ТекущаяДата < Дата(ЭтотГод + 1, 1, 21) Тогда
		Возврат Истина;
		
	Иначе
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция ПорогСуммаДля1СКонтрагент()
	
	// Считаем 1000 рублей ключевой суммой после которой можно показывать баннеры.
	// Используется для определения порога НДС к вычету, НДС к возмещению и дебиторской задолженности.
	Возврат 1000;
	
КонецФункции

Функция ПорогСуммаДля1СПАРК()
	
	// Считаем 10000 рублей ключевой суммой после которой можно показывать баннеры.
	// Используется для определения порога НДС к вычету, НДС к возмещению и дебиторской задолженности.
	Возврат 10000;
	
КонецФункции

Функция ОбщееКоличествоДокументовДляРекламыЭДО()
	
	Возврат 100;
	
КонецФункции

Функция КоличествоПоступленийДляРекламыЭДО()
	
	Возврат 50;
	
КонецФункции

Функция КоличествоРеализацийДляРекламыЭДО()
	
	Возврат 50;
	
КонецФункции

Функция КоличествоКонтрагентовДляРекламыКонтрагент()
	
	Возврат 30;
	
КонецФункции

Функция КоличествоСотрудниковДляРекламыОтчетности()
	
	Возврат 25;
	
КонецФункции

Процедура ДобавитьБаннерВТаблицу(СписокБаннеров, Баннер)
	
	Если НЕ Баннер.ДоступенПоПравам Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока                      = СписокБаннеров.Добавить();
	НоваяСтрока.Порядок              = СписокБаннеров.Количество();
	НоваяСтрока.Баннер               = Баннер;
	НоваяСтрока.ИдентификаторБаннера = Баннер.Идентификатор;
	НоваяСтрока.ИмяСервиса           = Баннер.ИмяСервиса;
	
КонецПроцедуры

Процедура УдалитьСтрокиИзТаблицы(Таблица, СтрокиКУдалению)
	
	КоличествоЭлементов = СтрокиКУдалению.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		Таблица.Удалить(СтрокиКУдалению[КоличествоЭлементов - Индекс]);
	КонецЦикла;
	СтрокиКУдалению = Неопределено;
	
КонецПроцедуры

#Область Конструкторы

Функция НовыйТаблицаСписокБаннеров()
	
	ТаблицаСпискаБаннеров = Новый ТаблицаЗначений;
	// Колонки
	ТаблицаСпискаБаннеров.Колонки.Добавить("Баннер"); // см. НовыйБаннерДляСписка()
	ТаблицаСпискаБаннеров.Колонки.Добавить("ИдентификаторБаннера", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаСпискаБаннеров.Колонки.Добавить("Порядок",              ОбщегоНазначения.ОписаниеТипаЧисло(5));
	ТаблицаСпискаБаннеров.Колонки.Добавить("ИмяСервиса",           ОбщегоНазначения.ОписаниеТипаСтрока(50));
	
	// Индексы
	ТаблицаСпискаБаннеров.Индексы.Добавить("ИдентификаторБаннера");
	
	Возврат ТаблицаСпискаБаннеров;
	
КонецФункции

Функция НовыйЗакрытыеПользователемБаннеры()
	
	ЗакрытыеПользователемБаннеры = Новый Структура;
	ЗакрытыеПользователемБаннеры.Вставить("БаннерыБезОрганизации", Новый Структура);
	ЗакрытыеПользователемБаннеры.Вставить("БаннерыПоОрганизации",  Новый Соответствие);
	
	Возврат ЗакрытыеПользователемБаннеры;
	
КонецФункции

Функция НовыйБаннерДляСписка()
	
	Баннер = Новый Структура;
	Баннер.Вставить("ИмяСервиса",              "");
	Баннер.Вставить("Идентификатор",           "");
	Баннер.Вставить("НавигационнаяСсылка",     "");
	Баннер.Вставить("ТекстБаннера",            Новый ФорматированнаяСтрока(""));
	Баннер.Вставить("ИмяКартинкаЛоготипа",     "");
	Баннер.Вставить("ЦветФонаБаннер",         "ЦветФонаБаннер");
	Баннер.Вставить("ЗависитОтОрганизации",   Истина);
	Баннер.Вставить("Исключительный",         Ложь);
	Баннер.Вставить("ДоступенПоПравам",       Ложь);
	Баннер.Вставить("ДанныеБаннера",          Новый Структура);
	Баннер.Вставить("ОбстоятельстваЗакрытия", Новый Структура);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия", '00010101');
	Баннер.ОбстоятельстваЗакрытия.Вставить("Периодичность", Перечисления.Периодичность.Месяц);
	
	Возврат Баннер;
	
КонецФункции

Функция НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка = "", Шрифт = Неопределено)
	
	ШрифтЗаголовка = ?(Шрифт = Неопределено, ШрифтыСтиля.ШрифтЗаголовкаБаннера, Шрифт);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтЗаголовка, , , НавигационнаяСсылка);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтЗаголовка);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтыСтиля.ШрифтЗаголовкаБаннера);
	
КонецФункции

Функция НовыйСтрокаБаннера(ТекстСтроки, НавигационнаяСсылка = "", Шрифт = Неопределено)
	
	ШрифтСтроки = ?(Шрифт = Неопределено, ШрифтыСтиля.ШрифтТекстаБаннера, Шрифт);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтСтроки, , , НавигационнаяСсылка);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтСтроки);
	КонецЕсли;
	
КонецФункции

Функция НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
	Возврат Новый ФорматированнаяСтрока(
		ТекстЗаголовка,
		Символы.ПС,
		Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)), // Отступ между заголовком и подзаголовком.
		Символы.ПС,
		ТекстПодзаголовка);
	
КонецФункции

Функция НовыйИнформацияОСтороннихСертификатах()
	
	Информация = Новый Структура;
	Информация.Вставить("ДатаПоиска",               '00010101');
	Информация.Вставить("СертификатНайден",         Ложь);
	Информация.Вставить("СрокДействияСертификата",  '00010101');
	Информация.Вставить("СторонниеСертификаты",     Новый Структура);
	
	Возврат Информация;
	
КонецФункции 

#КонецОбласти

#Область Имена

// Ключи хранилища общих настроек

Функция ИмяНастройкиПерсонализированныеПредложенияСервисов()
	
	Возврат "ПерсонализированныеПредложенияСервисов";
	
КонецФункции

Функция ИмяКлючНастройкиЗакрытыеПользователемБаннеры()

	Возврат "ЗакрытыеПользователемБаннеры";

КонецФункции

Функция ИмяКлючНастроекСторонниеСертификаты()

	Возврат "СторонниеСертификаты";

КонецФункции 

// Имена сервисов

Функция ИмяСервис1СОтчетность()

	Возврат "Сервис1СОтчетность";

КонецФункции

Функция ИмяСервис1СЭДО()

	Возврат "Сервис1СЭДО";

КонецФункции

Функция ИмяСервис1СКонтрагент()

	Возврат "Сервис1СКонтрагент";

КонецФункции

Функция ИмяСервис1СПАРК()

	Возврат "Сервис1СПАРК";

КонецФункции

Функция ИмяСервисНадежностьБанков()

	Возврат "СервисНадежностьБанков";

КонецФункции

Функция ИмяСервисМойНалог()

	Возврат "СервисМойНалог";

КонецФункции

Функция ИмяСервисПоддержкаСервиса()
	
	Возврат "ПоддержкаСервиса";
	
КонецФункции

Функция ИмяСервисПроверкаОплатыНалогов()

	Возврат "ПроверкаОплатыНалогов";

КонецФункции

Функция ИмяПроблемыСНалоговымиПлатежами()

	Возврат "ПроблемыСНалоговымиПлатежами";

КонецФункции

Функция ИмяСервисЗаявкаНаКредит()

	Возврат "ЗаявкаНаКредит";

КонецФункции

// Имена картинок

Функция ИмяКартинкиЛоготип1СОтчетность()
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат "ЛоготипОтчетностьСервис";
	Иначе
		Возврат "ЛоготипОтчетность";
	КонецЕсли;
	
КонецФункции

Функция ИмяКартинкиЛоготип1СЭДО()
	
	Возврат "ЛоготипЭДО";
	
КонецФункции

Функция ИмяКартинкиЛоготип1СКонтрагент()
	
	Возврат "ЛоготипКонтрагент";
	
КонецФункции

Функция ИмяКартинкиЛоготип1СПАРК()
	
	Возврат "ЛоготипКонтрагент";
	
КонецФункции

Функция ИмяКартинкиЛоготипЗаявкаНаКредит()
	
	Возврат "ЛоготипСервисПоКредитам";
	
КонецФункции

// Имена размещений

// Возвращает имя размещения для формы настройки налогов и отчетов.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияНалогиИОтчеты() Экспорт

	Возврат "НалогиИОтчеты"

КонецФункции

// Возвращает имя размещения для формы списка Реализации товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияРеализация() Экспорт

	Возврат "Реализация"

КонецФункции

// Возвращает имя размещения для формы списка Поступление товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПоступление() Экспорт

	Возврат "Поступление"

КонецФункции

// Возвращает имя размещения для общих форм ИнформационнаяПанель и СписокЗадач.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОбщее() Экспорт

	Возврат "Общее"

КонецФункции

// Имя отчетной кампании

Функция ИмяОтчетнойКампании(ОтчетнаяКампания)
	
	// Для текста баннера нужно имя отчетной кампании в дательной падеже,
	// например, "Отчетная кампания по страховым взносам"
	
	ТекстНалогНа = НСтр("ru='Налог на'") + " ";
	ИмяОтчетнойКампании = СтрЗаменить(ОтчетнаяКампания, ТекстНалогНа, "");
	
	Если ВРег(ИмяОтчетнойКампании) = ИмяОтчетнойКампании Тогда
		// Если все символы в строке верхнего регистра, то это аббревиатура
		// и в дательном падеже будет то же имя.
		ОтчетнаяКампанияДательныйПадеж = ИмяОтчетнойКампании;
		
	ИначеЕсли Константы.ИспользоватьСервисСклоненияMorpher.Получить() Тогда
		// Если подключен сервис склонения, то склоняем через него.
		ОтчетнаяКампанияДательныйПадеж = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(ИмяОтчетнойКампании, 3));
		
	Иначе
		// Если сервис не подключен, то используем заданные имена кампаний.
		Если НРег(ИмяОтчетнойКампании) = НСтр("ru='акцизы'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='акцизам'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='игорный бизнес'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='игорному бизнесу'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='таможенный союз'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='таможенному союзу'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='прибыль'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='прибыли'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='водный налог'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='водному налогу'");
			
		Иначе
			ОтчетнаяКампанияДательныйПадеж = НРег(ИмяОтчетнойКампании);
			
		КонецЕсли;
	КонецЕсли;
	
	// Строка вида "по игорному бизнесу" или "по НДС".
	Возврат СтрШаблон(НСтр("ru='по %1'"), ОтчетнаяКампанияДательныйПадеж);
	
КонецФункции

// Возвращает имя размещения для журнала "Банковские выписки"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияБанковскиеВыписки() Экспорт

	Возврат "БанковскиеВыписки";

КонецФункции

// Возвращает имя размещения для журнала "Деньги"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияДеньги() Экспорт

	Возврат "Деньги";

КонецФункции

// Возвращает имя размещения для журнала "Кассовые документы"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияКассовыеДокументы() Экспорт

	Возврат "КассовыеДокументы";

КонецФункции

// Возвращает имя размещения для отчета "Монитор налогов и отчетности"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияМониторНалогов() Экспорт

	Возврат "МониторНалогов";

КонецФункции

Функция ИмяСервисПредодобренныйКредитВСбербанке()

	Возврат "ПредодобренныйКредитВСбербанке";

КонецФункции

Функция ИмяКартинкиЛоготипСбербанк()
	
	Возврат "ЛоготипСбербанк";
	
КонецФункции

Функция ИмяНастройкаВидимостиСчетовУчета()

	Возврат "НастройкаВидимостиСчетовУчета";

КонецФункции

// Возвращает имя размещения для списка документов "Требования-накладные"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияТребованиеНакладная() Экспорт

	Возврат "ТребованиеНакладная";

КонецФункции

Функция ИмяСервисSmartway()

	Возврат "Smartway";

КонецФункции

// Возвращает имя размещения для документа "Авансовые отчеты"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАвансовыеОтчеты() Экспорт

	Возврат "АвансовыеОтчеты";

КонецФункции

// Возвращает имя размещения для отчета "Монитор основных показателей"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияМониторОсновныхПоказателей() Экспорт

	Возврат "МониторОсновныхПоказателей";

КонецФункции

// Возвращает имя размещения для отчета "Продажи по контрагентам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПродажиПоКонтрагентам() Экспорт

	Возврат "ПродажиПоКонтрагентам";

КонецФункции

// Возвращает имя размещения для отчета "Анализ движений денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАнализДвиженийДенежныхСредств() Экспорт

	Возврат "АнализДвиженийДенежныхСредств";

КонецФункции

// Возвращает имя размещения для отчета "Остатки денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОстаткиДенежныхСредств() Экспорт

	Возврат "ОстаткиДенежныхСредств";

КонецФункции

// Возвращает имя размещения для отчета "Поступления денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПоступленияДенежныхСредств() Экспорт

	Возврат "ПоступленияДенежныхСредств";

КонецФункции

// Возвращает имя размещения для отчета "Расходы денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияРасходыДенежныхСредств() Экспорт

	Возврат "РасходыДенежныхСредств";

КонецФункции

// Возвращает имя размещения для отчета "Доходы и расходы"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияДоходыРасходы() Экспорт

	Возврат "ДоходыРасходы";

КонецФункции

// Возвращает имя размещения для отчета "Оборотные средства"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОборотныеСредства() Экспорт

	Возврат "ОборотныеСредства";

КонецФункции

// Возвращает имя размещения для отчета "Продажи по месяцам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПродажиПоМесяцам() Экспорт

	Возврат "ПродажиПоМесяцам";

КонецФункции

// Возвращает имя размещения для отчета "Динамика задолженности поставщикам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияДинамикаЗадолженностиПоставщикам() Экспорт

	Возврат "ДинамикаЗадолженностиПоставщикам";

КонецФункции

// Возвращает имя размещения для отчета "Счета, не оплаченные поставщикам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАнализНеоплаченныхСчетовПоставщиков() Экспорт

	Возврат "АнализНеоплаченныхСчетовПоставщиков";

КонецФункции

// Возвращает имя размещения для отчета "Задолженность поставщикам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияЗадолженностьПоставщикам() Экспорт

	Возврат "ЗадолженностьПоставщикам";

КонецФункции

// Возвращает имя размещения для отчета "Задолженность поставщикам по срокам долга"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияЗадолженностьПоставщикамПоСрокамДолга() Экспорт

	Возврат "ЗадолженностьПоставщикамПоСрокамДолга";

КонецФункции

#КонецОбласти

#Область ИнтеграцияСоSmartway

Функция НовыйБаннерИнтеграцияСоSmartway()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисSmartway();
	Баннер.Идентификатор       = ИдентификаторБаннераИнтеграцияСоSmartway();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераИнтеграцияСоSmartway();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераИнтеграцияСоSmartway();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготипSmartway();
	Баннер.ДанныеБаннера.Вставить("Организация");
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	Баннер.ЗависитОтОрганизации = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаИнтеграцияСоSmartway(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераИнтеграцияСоSmartway();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ИнтеграцияСоSmartway)
	|	И ПерсонализированныеДанные.Организация = &Организация";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераИнтеграцияСоSmartway(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтрокаДанныеБаннера = ДанныеБаннера[0];
	ЗначениеПоказателя = СтрокаДанныеБаннера.ЗначениеПоказателя;
	Организация        = СтрокаДанныеБаннера.Организация;
	
	Возврат ЗначениеПоказателя >= КоличествоКомандировокДляВыводаБаннераSmartway()
		И Не Обработки.ОтправкаЗаявокВSmartway.ЗаявкаВSmartwayОтправлена(Организация);
	
КонецФункции

Функция КоличествоКомандировокДляВыводаБаннераSmartway()

	Возврат 10;

КонецФункции

Процедура ЗаполнитьДанныеБаннераИнтеграцияСоSmartway(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераИнтеграцияСоSmartway(Баннер.НавигационнаяСсылка);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераИнтеграцияСоSmartway(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Отправляете сотрудников в командировки?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Автоматизируйте заполнение авансовых отчетов по командировкам'"), НавигационнаяСсылка));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераИнтеграцияСоSmartway()

	Возврат "ИнтеграцияСоSmartway";

КонецФункции

Функция ДоступностьБаннераИнтеграцияСоSmartway()
	
	Возврат ПравоДоступа("Просмотр", Метаданные.Обработки.ОтправкаЗаявокВSmartway)
		И Не (ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
		Или ЗначениеЗаполнено(Справочники.НастройкиЗагрузкиДанныхВнешнихСистем.ИспользуемаяНастройкаSmartway())
		Или ОбщегоНазначенияБПВызовСервераПовтИсп.ДанныеSmartway() = Неопределено);
		
КонецФункции

Функция ИмяКартинкиЛоготипSmartway()
	
	Возврат "ЛоготипSmartway";
	
КонецФункции

#КонецОбласти

#Область ЗаявкаНаКредит

Функция СреднемесячноеПоступлениеНаСчетДляРекламыЗаявкаНаКредит()
	
	Возврат 50000;
	
КонецФункции

Процедура ОбновитьДанныеДляЗаявкиНаКредит(ДатаПолученияДанных) Экспорт
	
	ТаблицаСреднемесячногоПоступленияНаСчет = Документы.ЗаявкаНаКредит.СреднемесячноеПоступлениеНаСчет(ДатаПолученияДанных, 6);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Раздел = Перечисления.РазделыПерсонализированныхДанных.ЗаявкаНаКредит;
	
	НаборЗаписей = РегистрыСведений.ПерсонализированныеДанные.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Раздел.Установить(Раздел);
	
	Для каждого СтрокаТаблицы Из ТаблицаСреднемесячногоПоступленияНаСчет Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.Раздел             = Раздел;
		Запись.НомерСтрокиРаздела = 1;
		Запись.Организация        = СтрокаТаблицы.Организация;
		Запись.ЗначениеПоказателя = СтрокаТаблицы.Сумма;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
