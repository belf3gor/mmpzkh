////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции для обработки заявок на кредиты:
// - формирование электронных представлений заявок и отчетов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Универсальным образом формирует имя файла, добавляемого в отправляемый от заемщика контейнер.
//
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит - назначение отправляемой информации.
//  ТипСодержимого - ПеречислениеСсылка.ТипыСодержимогоФайловОбменаСБанками - формат (расширение файла) информации.
//  СведенияОЗаявке - Структура - см. СведенияОЗаявке() или СведенияОбАкцепте() .
//  КлючУникальности - Строка или Массив - для вставки произвольного текста в середину названия файла.
//
// Возвращаемое значение:
//   Строка      - имя файла для переданного типа содержимого.
//
Функция ИмяФайлаДляКонтейнера(ТипДокумента, ТипСодержимого, СведенияОЗаявке, КлючУникальности = "") Экспорт
	
	СоставнойКлюч = (ТипЗнч(КлючУникальности) = Тип("Массив"));
	
	ЧастиИмениФайла = Новый Массив;
	ЧастиИмениФайла.Добавить("BANK");
	
	Если ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АнкетаЗаемщика Тогда
		
		ЧастиИмениФайла.Добавить("LOANAPP");
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.СогласиеНаОбработкуДанных Тогда
		
		ЧастиИмениФайла.Добавить("CONSENT");
		Расширение = ".xml";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ПланСчетов Тогда
		
		ЧастиИмениФайла.Добавить("COA");
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ОтчетЗаемщика Тогда
		
		Если СоставнойКлюч Тогда
			// При выгрузке расшифровки бухгалтерской отчетности ключ всегда будет составным.
			Если КлючУникальности[0] = "ОборотноСальдоваяВедомость" Тогда
			    ЧастиИмениФайла.Добавить("OSV");
			ИначеЕсли КлючУникальности[0] = "АнализСчета" Тогда
				ЧастиИмениФайла.Добавить("ANS");
			ИначеЕсли СтрНачинаетсяС(КлючУникальности[0], "Операции") Тогда
				ЧастиИмениФайла.Добавить("TRN");
			КонецЕсли;
			КлючУникальности.Удалить(0);
			
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АкцептЗаемщика Тогда
		
		ЧастиИмениФайла.Добавить("ACCEPT");
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Невозможно получить имя файла для контейнера: неизвестный тип документа обмена.'");
	КонецЕсли;
	
	Если СоставнойКлюч Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЧастиИмениФайла, КлючУникальности);
	ИначеЕсли Не ПустаяСтрока(КлючУникальности) Тогда
		ЧастиИмениФайла.Добавить(КлючУникальности);
	КонецЕсли;
	
	ЧастиИмениФайла.Добавить(СведенияОЗаявке.ДанныеЗаемщика.ИдентификаторЗаемщика);
	ЧастиИмениФайла.Добавить(Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд")); // дата выгрузки
	Если ТипДокумента <> Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АкцептЗаемщика Тогда
		ЧастиИмениФайла.Добавить(Строка(Новый УникальныйИдентификатор())); // для гарантированной уникальности файла
	КонецЕсли;	
	
	ИмяФайла = СтрСоединить(ЧастиИмениФайла, "_") + "." + ОбщегоНазначения.ИмяЗначенияПеречисления(ТипСодержимого);
	
	Возврат ИмяФайла;
	
КонецФункции

// Возвращает структуру с пустыми значениями для параметра ПараметрыПредставления функции ЭлектронноеПредставлениеСообщения().
//
// Параметры:
//	Структура - Содержит ключи, которые используются при выгрузке заявки на кредит в xml-файл.
//
Функция НовыеСведенияЗаявкиНаКредит() Экспорт
	
	Сведения = Новый Структура;
	
	Сведения.Вставить("Кодировка", "windows-1251");
	
	Сведения.Вставить("ИдентификаторФайла", "");
	Сведения.Вставить("ВерсияПрограммы",    РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	
	Сведения.Вставить("ИдентификаторЗаявки", "");
	Сведения.Вставить("ДатаЗаявки",          '0001-01-01');
	Сведения.Вставить("СуммаКредита",        0);
	Сведения.Вставить("СрокКредита",         0);
	Сведения.Вставить("ЦельКредита",         ""); // 1 - Пополнение оборотных средств, 2 - Инвестиции в ОС
	
	Сведения.Вставить("ОКВЭД", "");
	Сведения.Вставить("ОКОПФ", "");
	Сведения.Вставить("ДатаРегистрации",    '0001-01-01');
	Сведения.Вставить("ДатаФактНачалаДеят", '0001-01-01');
	Сведения.Вставить("ДатаНачалаУчета",    '0001-01-01');
	Сведения.Вставить("ДатаОтчетности",     '0001-01-01');
	
	// Сведения об отправителе - юридическом лице.
	ОтправительЮЛ = Новый Структура;
	
	ОтправительЮЛ.Вставить("Наименование",    "");
	ОтправительЮЛ.Вставить("СокрНаименование","");
	ОтправительЮЛ.Вставить("ИНН",             "");
	ОтправительЮЛ.Вставить("КПП",             "");
	ОтправительЮЛ.Вставить("СведенияОбАдресе", Новый Структура); // Результат функции РаботаСАдресами.СведенияОбАдресе().
	ОтправительЮЛ.Вставить("ОГРН",            "");
	ОтправительЮЛ.Вставить("Нерезидент",      "");  // "0" - резидент, "1" - нерезидент.
	
	ОтправительЮЛ.Вставить("ПолноеНаименованиеДоРеорганизации", "");
	ОтправительЮЛ.Вставить("СокращенноеНаименованиеДоРеорганизации", "");
	
	Сведения.Вставить("ОтправительЮЛ", ОтправительЮЛ);
	
	// Сведения об отправителе - индивидуальном предпринимателе.
	ОтправительИП = Новый Структура;
	
	ОтправительИП.Вставить("ОГРН",            "");
	
	Сведения.Вставить("ОтправительИП", ОтправительИП);
	
	// Сведения о руководителе (индивидуальном предпринимателе).
	Руководитель = Новый Структура;
	
	Руководитель.Вставить("ИНН",               "");
	Руководитель.Вставить("СНИЛС",             "");
	Руководитель.Вставить("Пол",               ""); // "1" - мужской, "2" - женский.
	Руководитель.Вставить("ДатаРождения",      '0001-01-01');
	Руководитель.Вставить("СведенияОбАдресе",   Новый Структура);
	Руководитель.Вставить("Гражданство",       ""); // Цифровой 3-значный код страны по классификатору ОКСМ.
	Руководитель.Вставить("МестоРождения",     "");
	Руководитель.Вставить("СемейноеПоложение", ""); // "1" - Холост/Не замужем, "2" - В разводе, "3" - Женат/Замужем, "4" - Вдовец/Вдова, "5" - Гражданский брак.

	Руководитель.Вставить("Телефон", "");
	Руководитель.Вставить("ДопТелефон", "");
	Руководитель.Вставить("ЭлПочта", "");
	
	Руководитель.Вставить("ФИО", Новый Структура("Фамилия, Имя, Отчество", "", "", ""));
	
	УдостоверениеЛичности = Новый Структура;
	УдостоверениеЛичности.Вставить("КодВидаДокумента",    "");
	УдостоверениеЛичности.Вставить("СерияДокумента",      "");
	УдостоверениеЛичности.Вставить("НомерДокумента",      "");
	УдостоверениеЛичности.Вставить("КемВыдан",            "");
	УдостоверениеЛичности.Вставить("ДатаВыдачи",          '0001-01-01');
	УдостоверениеЛичности.Вставить("КодПодразделения",    "");
	
	Руководитель.Вставить("УдостоверениеЛичности", УдостоверениеЛичности);
	
	Сведения.Вставить("Руководитель", Руководитель);
	
	// Сведения о контактном лице.
	КонтактноеЛицо = Новый Структура;
	
	КонтактноеЛицо.Вставить("Должность", "");
	КонтактноеЛицо.Вставить("Телефон",   "");
	КонтактноеЛицо.Вставить("ЭлПочта",   "");
	КонтактноеЛицо.Вставить("ФИО", Новый Структура("Фамилия, Имя, Отчество", "", "", ""));
	
	Сведения.Вставить("КонтактноеЛицо", КонтактноеЛицо);
	
	Сведения.Вставить("СистемыНО", Новый Массив); // "ОСНО", "УСНД", "УСНДМР", "ЕНВД", "ПСНО", "НПД"
	
	Возврат Сведения;
	
КонецФункции

// Формирует xml-файл сообщения для банка по заявке на кредит
//
// Параметры:
//  ТипТранзакции - ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит - определяет, какой файл нужно сформировать
//  ПараметрыПредставления - Структура - контекст выгружаемых данных, состав свойств зависит от типа транзакции
// 
// Возвращаемое значение:
//   - ДвоичныеДанные - двоичные данные XML-файла, содержащего сообщение для банка
//
Функция ЭлектронноеПредставлениеСообщения(ТипТранзакции, ПараметрыПредставления) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	Компоновщик = НовыйКомпоновщикXML(ИмяВременногоФайла, ПараметрыПредставления.Кодировка);
	
	Компоновщик.ЗаписатьНачалоЭлемента("Файл");
	
	ВывестиАтрибут(Компоновщик, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ВывестиАтрибут(Компоновщик, "ИдФайл",    ПараметрыПредставления.ИдентификаторФайла);
	ВывестиАтрибут(Компоновщик, "ВерсПрог",  ПараметрыПредставления.ВерсияПрограммы);
	ВывестиАтрибут(Компоновщик, "ВерсФорм",  "1.01.03");
	
	Если ТипТранзакции = Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.ЗаявкаНаКредит Тогда
		ВывестиУзел_ЗаявкаНаКредит_Документ(Компоновщик, ПараметрыПредставления);
	Иначе
		ВывестиУзел_Акцепт_Документ(Компоновщик, ПараметрыПредставления);
	КонецЕсли;	
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Файл
	
	Возврат ДвоичныеДанныеКомпоновщикаXML(Компоновщик, ИмяВременногоФайла);
	
КонецФункции

// Подготавливает внутри фонового задания текст согласия на обработку персональных данных.
//
// Параметры:
//	ПараметрыЗаполнения - Структура - Содержит ключи:
//		* ПараметрыСогласия - Структура - см. результат функции ПараметрыСогласия().
//		* Банки - Массив - Содержит элементы типа СправочникСсылка.БанкиУниверсальногоОбмена.
//	АдресХранилища - Строка - Адрес временного хранилища, в которое поместить результат выполнения функции.
//
Процедура ЗаполнитьТекстСогласияВФоне(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	РеквизитыБанков = ПодготовитьИнформациюОБанкахДляСогласий(ПараметрыЗаполнения.Банки);
	
	Результат = ТекстСогласия(ПараметрыЗаполнения.ПараметрыСогласия, РеквизитыБанков);
	
	ПоместитьВоВременноеХранилище(Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9)), АдресХранилища);
	
КонецПроцедуры

// Все возможные параметры заемщика, доступные для использования в макете.
//
// Параметры:
//  ЭтоЮридическоеЛицо    - Булево - если Ложь, то нужны параметры индивидуального предпринимателя; иначе юридического лица.
//
// Возвращаемое значение:
//   Структура   - набор доступных параметров макета.
//
Функция ПараметрыСогласия(ЭтоЮридическоеЛицо) Экспорт
	
	Результат = Новый Структура;
	
	// Определяют заполнение остальных параметров.
	Результат.Вставить("Организация",        Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ДатаСогласия",       '00010101');
	Результат.Вставить("ЭтоЮридическоеЛицо", ЭтоЮридическоеЛицо);
		
	// Общие для всех вариантов.
	Результат.Вставить("ИНН",              "");
	Результат.Вставить("ЮридическийАдрес", "");
	Результат.Вставить("Город",            "");
	
	// Для инидивидуального предпринимателя - данные самого ИП, для юридического лица - данные руководителя.
	Результат.Вставить("ПредставлениеФЛ",          ""); // Полное ФИО
	Результат.Вставить("ПолФЛ",                    Перечисления.ПолФизическогоЛица.ПустаяСсылка());
	Результат.Вставить("ДатаРожденияФЛ",           '00010101');
	Результат.Вставить("СтраховойНомерПФРФЛ",      ""); // СНИЛС
	Результат.Вставить("ПредставлениеДокументаФЛ", ""); // Документ, серия, номер, когда и где выдан
	Результат.Вставить("МестоРожденияФЛ",          ""); // Место рождения в том виде, как оно должно быть выведено в тексте согласия.
	
	Если ЭтоЮридическоеЛицо Тогда
		
		// Информация о самой организации.
		Результат.Вставить("ПолноеНаименование",                     "");
		Результат.Вставить("СокращенноеНаименование",                "");
		Результат.Вставить("ПолноеНаименованиеДоРеорганизации",      "");
		Результат.Вставить("СокращенноеНаименованиеДоРеорганизации", "");
		Результат.Вставить("ДолжностьРуководителяПредставление",     "");
		Результат.Вставить("ОГРН",                                   "");

		// Информация о руководителе организации.
		Результат.Вставить("ПредставлениеФЛРодительныйПадеж", ""); // Заполняется вызывающей процедурой
		Результат.Вставить("ИННФЛ",                           "");
		Результат.Вставить("АдресПоПропискеФизическиеЛицаФЛ", ""); // Регистрация по месту жительства
		Результат.Вставить("EMailФизическиеЛицаФЛ",           "");
		Результат.Вставить("ТелефонРабочийФизическиеЛицаФЛ",  ""); // Основной телефон
		Результат.Вставить("ТелефонДомашнийФизическиеЛицаФЛ", ""); // Дополнительный телефон
		
	Иначе
		
		// Информация о самом ИП.
		Результат.Вставить("Email",                     "");
		Результат.Вставить("Телефоны",                  ""); // Основной телефон
		Результат.Вставить("ТелефонЮридическогоАдреса", ""); // Дополнительный телефон
		
		Результат.Вставить("СтранаФЛ",            "");
		Результат.Вставить("СемейноеПоложениеФЛ", "");
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Получает из специального сервиса информацию о банках-получателях согласия на обработку персональных данных.
//
// Параметры:
//  Банки         - ОпределяемыйТип.БанкиУниверсальногоОбмена, Массив - о чем собирается информация.
//
// Возвращаемое значение:
//   ТаблицаЗначений - информация о кредиторах.
//
Функция ПодготовитьИнформациюОБанкахДляСогласий(Банки) Экспорт
	
	ЭтоСписок = (ТипЗнч(Банки) <> Тип("СправочникСсылка.БанкиУниверсальногоОбмена"));

	РеквизитыБанков = УниверсальныйОбменСБанками.РеквизитыБанков(
		?(ЭтоСписок, Банки, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Банки)),
		"Наименование, НаименованиеПолное, ИНН, ОГРН, Адрес");
	РеквизитыБанков.Колонки.НаименованиеПолное.Имя = "ПолноеНаименованиеБанка";
	РеквизитыБанков.Колонки.ИНН.Имя                = "ИННБанка";
	РеквизитыБанков.Колонки.ОГРН.Имя               = "ОГРНБанка";
	РеквизитыБанков.Колонки.Адрес.Имя              = "ЮридическийАдресБанка";
	
	РеквизитыБанков.Колонки.Добавить("ТекстМакета", Новый ОписаниеТипов("Строка"));
	РеквизитыБанков.Колонки.Добавить("ПараметрыМакета", Новый ОписаниеТипов("ТаблицаЗначений"));

	Для каждого РеквизитыБанка Из РеквизитыБанков Цикл

		РеквизитыБанка.ЮридическийАдресБанка =
			УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(РеквизитыБанка.ЮридическийАдресБанка);
	
	КонецЦикла;

	Возврат РеквизитыБанков;
	
КонецФункции

// Формирует текст согласия на обработку персональных данных.
//
// Параметры:
//  ПараметрыСогласия - Структура - см. ПараметрыСогласия()
//  РеквизитыБанков - ТаблицаЗначений - см. ПодготовитьИнформациюОБанкахДляСогласий()
//  ОбъединитьТексты - Булево - если Ложь, то возвращается массив текстов согласий; если Истина - один сводный текст.
//
// Возвращаемое значение:
//   Строка, Массив - текст для веб-страницы или массив текстов по каждому банку.
//
Функция ТекстСогласия(ПараметрыСогласия, РеквизитыБанков, ОбъединитьТексты = Истина) Экспорт
	
	Если РеквизитыБанков.Количество() = 0 Тогда
		Возврат ?(ОбъединитьТексты, "", Новый Массив);
	КонецЕсли;
	
	// Заполним автозаполняемые параметры.
	ПараметрыСогласия.Вставить("ДатаДокумента", Формат(ПараметрыСогласия.ДатаСогласия, "ДЛФ=DD"));
	Если ПараметрыСогласия.Свойство("ДатаРожденияФЛ") Тогда
		ПараметрыСогласия.ДатаРожденияФЛ = Формат(ПараметрыСогласия.ДатаРожденияФЛ, "ДЛФ=DD");
	КонецЕсли;

	ПараметрыСогласия.Вставить("НаименованиеДоРеорганизации", "");
	Если ПараметрыСогласия.Свойство("ПолноеНаименованиеДоРеорганизации")
		И ПараметрыСогласия.Свойство("СокращенноеНаименованиеДоРеорганизации") Тогда
		Если ЗначениеЗаполнено(ПараметрыСогласия.ПолноеНаименованиеДоРеорганизации)
			ИЛИ ЗначениеЗаполнено(ПараметрыСогласия.СокращенноеНаименованиеДоРеорганизации) Тогда

			ПараметрыСогласия.НаименованиеДоРеорганизации = СтрШаблон(
				НСтр("ru = ',</B> сведения о реорганизации: <B>%1 (%2)'"),
				ПараметрыСогласия.ПолноеНаименованиеДоРеорганизации,
				ПараметрыСогласия.СокращенноеНаименованиеДоРеорганизации);
		КонецЕсли;
	КонецЕсли;

	// Подбираем текст макета для каждого из банков.
	ПодобратьТекстыМакетов(РеквизитыБанков, ПараметрыСогласия.ЭтоЮридическоеЛицо, ПараметрыСогласия.ДатаСогласия);
	
	// Заполняем параметры в макетах.
	ТекстыСогласий = Новый Массив;
	Для каждого ИнформацияОБанке Из РеквизитыБанков Цикл
	
		ТекстыРезультата = Новый Массив;
		ПозицияКурсора = 1;
		Для каждого ПараметрМакета Из ИнформацияОБанке.ПараметрыМакета Цикл
			
			ЗначениеПараметра = Неопределено;
			Если ПараметрыСогласия.Свойство(ПараметрМакета.ИмяПараметра, ЗначениеПараметра)
			   И ЗначениеЗаполнено(ЗначениеПараметра) Тогда
			   
				ПараметрМакета.ЗначениеПараметра = ЗначениеПараметра;

			ИначеЕсли РеквизитыБанков.Колонки.Найти(ПараметрМакета.ИмяПараметра) <> Неопределено Тогда

				ПараметрМакета.ЗначениеПараметра = ИнформацияОБанке[ПараметрМакета.ИмяПараметра];

			ИначеЕсли ПараметрМакета.ИмяПараметра = "НаименованиеДоРеорганизации" Тогда // необязательные параметры
				
				ПараметрМакета.ЗначениеПараметра = "";
				
			Иначе // обязательный параметр

				ПараметрМакета.ЗначениеПараметра = "_______";

			КонецЕсли;
			
			ТекстыРезультата.Добавить(Сред(ИнформацияОБанке.ТекстМакета, ПозицияКурсора, ПараметрМакета.ПозицияНачала - ПозицияКурсора));
			ТекстыРезультата.Добавить("<B>");
			ТекстыРезультата.Добавить(ПараметрМакета.ЗначениеПараметра);
			ТекстыРезультата.Добавить("</B>");
			ПозицияКурсора = ПараметрМакета.ПозицияКонца + 1;
		
		КонецЦикла; 
		Если ПозицияКурсора <= СтрДлина(ИнформацияОБанке.ТекстМакета) Тогда
			ТекстыРезультата.Добавить(Сред(ИнформацияОБанке.ТекстМакета, ПозицияКурсора));
		КонецЕсли;
		
		ТекстыСогласий.Добавить(СтрСоединить(ТекстыРезультата));
	
	КонецЦикла;
	
	// Объединяем согласия нескольких банков в один html-текст.
	Если ТекстыСогласий.Количество() = 1 Тогда
		
		ТекстыРезультата = ?(ОбъединитьТексты, ТекстыСогласий[0], ТекстыСогласий);
		
	Иначе
		
		Если ОбъединитьТексты Тогда
			
			АнализируемыйТекст = НРег(ТекстыСогласий[0]);
			ПозицияКурсора = СтрНайти(АнализируемыйТекст, "<body");
			ПозицияКурсора = СтрНайти(АнализируемыйТекст, ">", НаправлениеПоиска.СНачала, ПозицияКурсора);
			ТекстДоТела = Лев(ТекстыСогласий[0], ПозицияКурсора);
			ПозицияКурсора = СтрНайти(АнализируемыйТекст, "</body>", НаправлениеПоиска.СКонца);
			ТекстПослеТела = Сред(ТекстыСогласий[0], ПозицияКурсора);
			
			// Оставляем только тела html-документов.
			РазделительнаяЛиния = "<P align=center ALIGN=""center""><FONT size=14><EM><U>%1</U></EM></FONT></P>
			|<P align=center ALIGN=""center"">";
			Для ИндексСогласия = 0 По ТекстыСогласий.ВГраница() Цикл
				// Добавляем разделительные линии между согласиями для разных кредиторов.
				// Текущая позиция линии в списке фрагментов текста сдвигается пропорционально числу элементов.
				ИндексРазделительнойЛинии = ИндексСогласия * 2;
				АнализируемыйТекст = НРег(ТекстыСогласий[ИндексРазделительнойЛинии]);
				ПозицияКурсора = СтрНайти(АнализируемыйТекст, "<body");
				ПозицияКурсора = СтрНайти(АнализируемыйТекст, ">", НаправлениеПоиска.СНачала, ПозицияКурсора);
				ТекстыСогласий[ИндексРазделительнойЛинии] = Сред(ТекстыСогласий[ИндексРазделительнойЛинии],
					ПозицияКурсора + 1,
					СтрНайти(АнализируемыйТекст, "</body>", НаправлениеПоиска.СКонца) - ПозицияКурсора - 1);
					
				ТекстыСогласий.Вставить(ИндексРазделительнойЛинии,
					СтрШаблон(РазделительнаяЛиния, РеквизитыБанков[ИндексСогласия].ПолноеНаименованиеБанка));
				
			КонецЦикла;

			ТекстыСогласий.Вставить(0, ТекстДоТела);
			ТекстыСогласий.Добавить(ТекстПослеТела);
			ТекстыРезультата = СтрСоединить(ТекстыСогласий);
			
		Иначе
			
			ТекстыРезультата = ТекстыСогласий;
			
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ТекстыРезультата;

КонецФункции

#Область XDTO

// Возвращает объект XDTO УсловияКредитования из корневого элемента XML по схеме пакетаXDTO ЗаявкиНаКредит.
//
// Параметры:
//	ДанныеXML - Строка, ХранилищеЗначения - Данные XML из характеристик сервиса заявок на кредит.
//
// Возвращаемое значение:
//	ОбъектXDTO, Неопределено - Объект XDTO, прочитанный из XML, или Неопределено в случае ошибок или отсутствия данных.
//
Функция УсловияКредитованияXDTO(ДанныеXML) Экспорт

	Если ТипЗнч(ДанныеXML) = Тип("Строка") Тогда
		ТекстXML = ДанныеXML;
	ИначеЕсли ТипЗнч(ДанныеXML) = Тип("ХранилищеЗначения") Тогда
		ТекстXML = ДанныеXML.Получить();
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ТекстXML) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Попытка
		
		ЧтениеXML = Новый ЧтениеXML();
		ЧтениеXML.УстановитьСтроку(ТекстXML);
		ЧтениеXML.ПерейтиКСодержимому();
		
		Результат = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ТипXDTO("УсловияКредитования"));
		
	Исключение
	
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ИмяСобытия = НСтр("ru = 'Прочитать объект XDTO'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(
			ЗаявкиНаКредитКлиентСервер.СобытиеЖурналаРегистрации(ИмяСобытия),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
	КонецПопытки;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ОбращениеКСервису

// Структура параметров для алгоритма, передающего информацию о ходе фонового задания в форму с индикатором.
//
Функция СостояниеПрогрессаПодписанияИОтправки() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяШага", ""); // 1 = "подготовка"; 2 = "подписание"; 3 = "транзакции"; 4 = "отправка"
	
	Состояние = Новый Структура;
	Состояние.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	Состояние.Вставить("ВыполненоДействий", 0);  // часть выполненных действий шага
	Состояние.Вставить("КоличествоДействий", 0); // сколько условных действий составляет шаг
	
	Возврат Состояние;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеДанныхСервиса

// Процедура - обработчик начала получения данных о сервисе заявок на кредит.
// Предназначена для вызова из модуля форм, в которых требуются актуальные данные о сервисе.
//
// Параметры:
//  Форма - УправляемаяФорма - должна содержать реквизиты СведенияОСервисе И СведенияОДлительнойОперации
//
Процедура НачатьОбновлениеДанныхСервиса(Форма) Экспорт 

	СведенияОСервисе = Форма.СведенияОСервисе;
	СведенияОДлительнойОперации = Форма.СведенияОДлительнойОперации;
	
	Если Форма.ТолькоПросмотр 
		ИЛИ СведенияОСервисе.ДанныеАктуальны
		ИЛИ СведенияОСервисе.ТребуетсяПодключениеИнтернетПоддержки Тогда
		// Обновление не требуется или невозможно.
		Возврат;
	КонецЕсли;
	
	Если СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено Тогда
		// Не завершено предыдущее фоновое задание, новое не запускаем.
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Форма.УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение           = 0; // Не ждем завершения.
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление данных сервиса по кредитам'");
	
	СведенияОДлительнойОперации.Имя = "ОбновитьДанныеСервиса";
	СведенияОДлительнойОперации.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Документы.ЗаявкаНаКредит.ОбновитьДанныеСервиса",
		Новый Структура(),
		ПараметрыВыполнения);

КонецПроцедуры

#КонецОбласти

#Область СостояниеЗаявокНаКредит

// Возвращает признак того, что заявка была одобрена
//
// Параметры:
//  Состояние - ПеречислениеСсылка.СостоянияЗаявокНаКредит - текущее сочтояние заявки 
// 
// Возвращаемое значение:
//   - Булево - Истина, если заявка ранее уже была одобрена банком
//
Функция ЗаявкаОдобрена(Состояние) Экспорт 

	Возврат(Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.Одобрено")
		ИЛИ Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.КредитЗапрошен") 
		ИЛИ Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.Готово"));
	
КонецФункции	

// Возвращает цвет текста или фона для элемента, отображающего состояние заявки на кредит.
//
// Параметры:
//  Состояние - ПеречислениеСсылка.СостоянияЗаявокНаКредит - состояние заявки на кредит.
// 
// Возвращаемое значение:
// 	Цвет - Цвет состояния.
//
Функция ЦветСостояния(Состояние) Экспорт 

	ЦветСостояния = Новый Цвет; // Автоцвет

	Если ЗаявкаОдобрена(Состояние) Тогда
		ЦветСостояния = ЦветаСтиля.ЦветШрифтаСостояниеЗаявкиНаКредитОдобрено;
	КонецЕсли;	
	
	Возврат ЦветСостояния;
	
КонецФункции	

// Возвращает документооборот по заявке на кредит.
//
// Параметры:
//	ЗаявкаНаКредит - ДокументСсылка.ЗаявкаНаКредит - Ссылка на заявку.
//
// Возвращаемое значение:
//	СправочникСсылка.ДокументооборотыОбменаСБанками - ссылка на документооборот по заявке.
//
Функция ДокументооборотПоЗаявке(ЗаявкаНаКредит) Экспорт

	Результат = Справочники.ДокументооборотыОбменаСБанками.ПустаяСсылка();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаявкаНаКредит", ЗаявкаНаКредит);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СостояниеЗаявокНаКредит.Транзакция.Документооборот КАК Документооборот
	|ИЗ
	|	РегистрСведений.СостояниеЗаявокНаКредит КАК СостояниеЗаявокНаКредит
	|ГДЕ
	|	СостояниеЗаявокНаКредит.ЗаявкаНаКредит = &ЗаявкаНаКредит";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.Документооборот) Тогда
			Результат = Выборка.Документооборот;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Заполняет реквизиты, отражающие состояние документооборота по заявке на кредит.
//
// Параметры:
//  Документооборот - СправочникСсылка.ДокументооборотыОбменаСБанками - набор взаимодействий с банком по определенному
//                                                                      предмету обмена (например, заявке на кредит).
//  ОписаниеСтатуса - Структура - см. УниверсальныйОбменСБанкамиПереопределяемый.СтатусДокументооборота()
//
Процедура СтатусДокументооборота(Документооборот, ОписаниеСтатуса) Экспорт
	
	// По-умолчанию состояние как будто заявка во все банки просрочена.
	ОписаниеСтатуса.Завершен         = Истина;
	ОписаниеСтатуса.ОжидаетсяОтвет   = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документооборот", Документооборот);
	Запрос.УстановитьПараметр("Сегодня", НачалоДня(ТекущаяДатаСеанса()));
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостояниеЗаявокНаКредит.Состояние КАК Состояние,
	|	СостояниеЗаявокНаКредит.ЕстьОшибки КАК ЕстьОшибки
	|ИЗ
	|	РегистрСведений.СостояниеЗаявокНаКредит КАК СостояниеЗаявокНаКредит
	|ГДЕ
	|	СостояниеЗаявокНаКредит.СрокАктуальности >= &Сегодня
	|	И СостояниеЗаявокНаКредит.Транзакция.Документооборот = &Документооборот
	|
	|УПОРЯДОЧИТЬ ПО
	|	СостояниеЗаявокНаКредит.Состояние.Порядок УБЫВ";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);

	Если Выборка.Следующий() Тогда
		
		Если Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.Черновик
			ИЛИ Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.НеОтправлено Тогда
			// В обычной ситуации сюда попасть не должны, т.к. не отправляли еще и документооборот не создавали.
			ОписаниеСтатуса.Завершен       = Ложь;
			ОписаниеСтатуса.ОжидаетсяОтвет = Ложь;

		ИначеЕсли Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.Отправлено
			ИЛИ Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.ОжидаетРешения
			ИЛИ Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.КредитЗапрошен Тогда
			// Ждем ответ от банка на заявку или акцепт.
			ОписаниеСтатуса.Завершен       = Ложь;
			ОписаниеСтатуса.ОжидаетсяОтвет = Истина;

		ИначеЕсли Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.Одобрено Тогда
			// Получили одобрение, если разобрали файл ответа без ошибок,
			// то теперь пользователь может выбрать или не выбрать данный банк (ждем акцепт пользователя).
			// Если возникла ошибка, то банк может попытаться прислать исправленный вариант ответа,
			// поэтому продолжаем проверять входящие сообщения.
			ОписаниеСтатуса.Завершен       = Ложь;
			ОписаниеСтатуса.ОжидаетсяОтвет = Выборка.ЕстьОшибки;

		Иначе
			// Во всех остальных случаях от банка ничего не ждем, считаем что процесс завершился.
			ОписаниеСтатуса.Завершен       = Истина;
			ОписаниеСтатуса.ОжидаетсяОтвет = Ложь;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу этапов отправки заявки.
//
// Параметры:
//	РеквизитыДокументооборота - Структура - см. УниверсальныйОбменСБанками.РеквизитыДокументооборота().
//	ДополнительныеПараметры - Произвольный - Дополнительные параметры формы.
//	ТаблицаЭтаповОтправки - ТаблицаЗначений - см. УниверсальныйОбменСБанками.ЗаготовкаТаблицыЭтаповОтправки().
//
Процедура ЗаполнитьТаблицуЭтаповОтправки(РеквизитыДокументооборота, ДополнительныеПараметры, ТаблицаЭтаповОтправки) Экспорт

    ДатаОтправкиЗаявки  = '0001-01-01';
    МожноДобавлятьЭтапы = Истина;
    
	Транзакции = УниверсальныйОбменСБанками.ТранзакцииПоДокументообороту(РеквизитыДокументооборота.Документооборот);
	ТаблицаДанныхТранзакций = УниверсальныйОбменСБанками.РеквизитыТранзакций(Транзакции);
	
	ЗначенияПеречисления = Метаданные.Перечисления.СостоянияЗаявокНаКредит.ЗначенияПеречисления;

	// 1. Сама заявка на кредит.
	СведенияОТранзакции = НайтиТранзакцию(
		ТаблицаДанныхТранзакций,
		Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.ЗаявкаНаКредит);

	Если СведенияОТранзакции <> Неопределено Тогда
		ДатаОтправкиЗаявки = СведенияОТранзакции.ДатаТранспорта;

		СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
		СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Отправлено.Синоним;
		СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
		СтрокаЭтапа.КомментарийКСостоянию		= "";
		СтрокаЭтапа.ЭтапПройден					= Истина;
		
		// Покажем в комментарии, когда получена банком.
		СведенияОТранзакции = НайтиТранзакцию(
			ТаблицаДанныхТранзакций,
			Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.ИзвещениеОПолученииБанкомЗаявки);
		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа.КомментарийКСостоянию	= СтрШаблон(НСтр("ru = 'Получено банком %1'"), Формат(СведенияОТранзакции.ДатаТранспорта, "ДЛФ=DT"));
		КонецЕсли;
		
	Иначе
		// Заявка вообще не была отправлена.
		СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
		СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.НеОтправлено.Синоним;
		СтрокаЭтапа.Дата						= '0001-01-01';
		СтрокаЭтапа.КомментарийКСостоянию		= "";
		СтрокаЭтапа.ЭтапПройден					= Ложь;
		
		// Другие этапы не показываем.
		МожноДобавлятьЭтапы = Ложь;
	КонецЕсли;
	

	// 2. Решение банка.
	Если МожноДобавлятьЭтапы Тогда

		СведенияОТранзакции = НайтиТранзакцию(
			ТаблицаДанныхТранзакций,
			Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.ПоложительноеКредитноеРешение,
			ДатаОтправкиЗаявки);
		
		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Одобрено.Синоним;
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		Иначе
			СведенияОТранзакции = НайтиТранзакцию(
				ТаблицаДанныхТранзакций,
				Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.ОтрицательноеКредитноеРешение,
				ДатаОтправкиЗаявки);
			Если СведенияОТранзакции <> Неопределено Тогда
				СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
				СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Отказано.Синоним;
				СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
				СтрокаЭтапа.КомментарийКСостоянию		= "";
				СтрокаЭтапа.ЭтапПройден					= Истина;
			
				// Другие этапы не показываем.
				МожноДобавлятьЭтапы = Ложь;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	

	// 3. Акцепт заемщика.
	Если МожноДобавлятьЭтапы Тогда
		СведенияОТранзакции = НайтиТранзакцию(
			ТаблицаДанныхТранзакций,
			Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.АкцептЗаемщика,
			ДатаОтправкиЗаявки);

		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.КредитЗапрошен.Синоним;
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		Иначе
			// Другие этапы не показываем.
			МожноДобавлятьЭтапы = Ложь;
		КонецЕсли;
	КонецЕсли;

	
	// 4. Получение банком акцепта.
	Если МожноДобавлятьЭтапы Тогда
		СведенияОТранзакции = НайтиТранзакцию(
			ТаблицаДанныхТранзакций,
			Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.ИзвещениеОПолученииБанкомАкцептаЗаемщика,
			ДатаОтправкиЗаявки);
			
		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Готово.Синоним;
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		Иначе
			// Другие этапы не показываем.
			МожноДобавлятьЭтапы = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Если банк в процессе какого-либо шага прислал тип транзакции "Ошибка", то обмен считается завершенным.
	// Покажем статус ошибки самым последним шагом.
	Если ТаблицаЭтаповОтправки.Количество() < 4 Тогда // Отображение больше 4-х этапов форма не поддерживает.
		СведенияОТранзакции = НайтиТранзакцию(
			ТаблицаДанныхТранзакций,
			Перечисления.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит.Ошибка,
			ДатаОтправкиЗаявки);

		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= НСтр("ru = 'Ошибка'");
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область РегионыЭксплуатацииСервиса

Процедура ПриЗаписиОрганизации(Организация) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаКредит") Тогда
		Возврат;
	КонецЕсли;
	
	Если СервисРаботаетВРегионеОрганизации(Организация) Тогда
		Константы.ИспользоватьЗаявкиНаКредит.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция КодыРегионовЭксплуатации() Экспорт
	
	КодыРегионов = Новый Массив;
	КодыРегионов.Добавить(69); // Тверская область
	
	Возврат КодыРегионов;
	
КонецФункции

Процедура ПроверитьРегионОрганизации(Организация, Период = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если СервисРаботаетВРегионеОрганизации(Организация, Период) Тогда
		Возврат;
	КонецЕсли;

	// Если сервис пока не работает в регионе организации, пытаемся найти
	// и подставить организацию, в регионе которой сервис работает.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Организации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Если Организации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЮридическиеАдреса = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияОбъектовНаДату(
		Организации,,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации),
		Период);
	
	Для каждого ЮридическийАдрес Из ЮридическиеАдреса Цикл
		КодРегиона = УправлениеКонтактнойИнформациейБП.КодРегионаПоАдресу(ЮридическийАдрес.Значение);
		Если КодыРегионовЭксплуатации().Найти(КодРегиона) <> Неопределено Тогда
			Организация = ЮридическийАдрес.Объект;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СервисРаботаетВРегионеОрганизации(Организация, Период = Неопределено)
	
	ЮридическийАдрес = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияНаДату(Организация,
		Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, Период);
	КодРегиона = УправлениеКонтактнойИнформациейБП.КодРегионаПоАдресу(ЮридическийАдрес.Значение);
	Возврат КодыРегионовЭксплуатации().Найти(КодРегиона) <> Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СведенияУказаны(ПроверяемыеСведения)
	
	Результат = Ложь;
	
	Для Каждого ЭлементСведений Из ПроверяемыеСведения Цикл
		Если ТипЗнч(ЭлементСведений.Значение) = Тип("Структура") Тогда
			Результат = СведенияУказаны(ЭлементСведений.Значение);
		Иначе
			Результат = ЗначениеЗаполнено(ЭлементСведений.Значение);
		КонецЕсли;
		
		Если Результат Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область XDTO

// Возвращает URI пространства имен XDTO пакета с характеристиками сервиса по кредитам.
//
// Возвращаемое значение:
//	Строка - URI пространства имен.
//
Функция URIПространстваИмен()
	Возврат "http://www.v8.1c.ru/banks/CreditApplication";
КонецФункции

// Возвращает XDTO-тип по его имени.
//
// Возвращаемое значение:
//	ТипЗначенияXDTO, ТипОбъектаXDTO - ссылка на запрошенный XDTO-тип.
//
Функция ТипXDTO(ИмяТипа)
	Возврат ФабрикаXDTO.Тип(URIПространстваИмен(), ИмяТипа);
КонецФункции

#КонецОбласти

#Область ФормированиеЗаявкиНаКредит

Процедура ВывестиУзел_ЗаявкаНаКредит_Документ(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Документ");
	
	ВывестиАтрибут(Компоновщик, "ПредметОбмена",     	ПараметрыПредставления.ИдентификаторЗаявки, "Т,255");
	ВывестиАтрибут(Компоновщик, "ДатаПредметаОбмена",   ПараметрыПредставления.ДатаЗаявки, "Д");
	ВывестиАтрибут(Компоновщик, "СуммаКредита", 		ПараметрыПредставления.СуммаКредита, "Ч,15");
	ВывестиАтрибут(Компоновщик, "Срок",         		ПараметрыПредставления.СрокКредита, "Ч,4");
	ВывестиАтрибут(Компоновщик, "ЦельКредита",         	ПараметрыПредставления.ЦельКредита, "Т,2");
	
	ВывестиУзел_ЗаявкаНаКредит_Организация(Компоновщик, ПараметрыПредставления);
	
	ВывестиУзел_ЗаявкаНаКредит_КонтактноеЛицо(Компоновщик, ПараметрыПредставления);
	
	ВывестиУзел_ЗаявкаНаКредит_ПоказателиБизнеса(Компоновщик, ПараметрыПредставления);
		
	Компоновщик.ЗаписатьКонецЭлемента(); // Документ
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_Организация(Компоновщик, ПараметрыПредставления)
	
	ОтправительЮЛ = ПараметрыПредставления.ОтправительЮЛ;
	ОтправительИП = ПараметрыПредставления.ОтправительИП;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Организация");
	
	ВывестиАтрибут(Компоновщик, "ОКВЭД", 	ПараметрыПредставления.ОКВЭД, "Т,8");
	ВывестиАтрибут(Компоновщик, "ОКОПФ", 	ПараметрыПредставления.ОКОПФ, "Т,5");
	ВывестиАтрибут(Компоновщик, "ДатаРег", 	ПараметрыПредставления.ДатаРегистрации, "Т,10");
	
	Если СведенияУказаны(ОтправительЮЛ) Тогда
		Компоновщик.ЗаписатьНачалоЭлемента("ЮЛ");
		
		ВывестиАтрибут(Компоновщик, "ПолнНаимОрг", ОтправительЮЛ.Наименование,     "Т,1000");
		ВывестиАтрибут(Компоновщик, "СокрНаимОрг", ОтправительЮЛ.СокрНаименование, "Т,255");
		ВывестиАтрибут(Компоновщик, "ИННЮЛ",       ОтправительЮЛ.ИНН,              "Т,10");
		ВывестиАтрибут(Компоновщик, "КПП",         ОтправительЮЛ.КПП,              "Т,9");
		ВывестиАтрибут(Компоновщик, "ОГРН",        ОтправительЮЛ.ОГРН,             "Т,13");
		ВывестиАтрибут(Компоновщик, "Нерезидент",  ОтправительЮЛ.Нерезидент,       "Ч,1");
		
		ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, ПараметрыПредставления.Руководитель, "Руководитель");
		
		ВывестиУниверсальныйУзел_АдресТип(Компоновщик, ОтправительЮЛ.СведенияОбАдресе);
		
		Если ЗначениеЗаполнено(ОтправительЮЛ.СокращенноеНаименованиеДоРеорганизации) 
			ИЛИ ЗначениеЗаполнено(ОтправительЮЛ.ПолноеНаименованиеДоРеорганизации) Тогда
			Компоновщик.ЗаписатьНачалоЭлемента("СвРеорг");
			ВывестиАтрибут(Компоновщик, "ПолнНаимРеорг", ОтправительЮЛ.ПолноеНаименованиеДоРеорганизации, "Т,1000");
			ВывестиАтрибут(Компоновщик, "СокрНаимРеорг", ОтправительЮЛ.СокращенноеНаименованиеДоРеорганизации, "Т,255");
			Компоновщик.ЗаписатьКонецЭлемента(); // СвРеорг
		КонецЕсли;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // ЮЛ
		
	Иначе
		Компоновщик.ЗаписатьНачалоЭлемента("ИП");
		
		ВывестиАтрибут(Компоновщик, "ОГРНИП",  ОтправительИП.ОГРН,            "Т,15");
		
		ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, ПараметрыПредставления.Руководитель, "Предприниматель");
		
		Компоновщик.ЗаписатьКонецЭлемента(); // ИП
		
	КонецЕсли;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Организация
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_КонтактноеЛицо(Компоновщик, ПараметрыПредставления)
	
	КонтактноеЛицо = ПараметрыПредставления.КонтактноеЛицо;
	
	Если НЕ СведенияУказаны(КонтактноеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	Компоновщик.ЗаписатьНачалоЭлемента("КонтактноеЛицо");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "Должность", КонтактноеЛицо.Должность, "Т,255");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Телефон", КонтактноеЛицо.Телефон, "Т,255");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ЭлПочта", КонтактноеЛицо.ЭлПочта, "Т,255");
	
	ВывестиУниверсальныйУзел_ФИОТип(Компоновщик, КонтактноеЛицо.ФИО);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // КонтактноеЛицо
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_ПоказателиБизнеса(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("ПоказателиБизнеса");
	
	ВывестиАтрибут(Компоновщик, "ДатаФактНачалаДеят", ПараметрыПредставления.ДатаФактНачалаДеят, "Т,10");
	ВывестиАтрибут(Компоновщик, "ДатаНачалаУчета", ПараметрыПредставления.ДатаНачалаУчета, "Т,10");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ДатаОтчетности", ПараметрыПредставления.ДатаОтчетности, "Т,10");
	
	ВывестиУзел_ЗаявкаНаКредит_СистемыНО(Компоновщик, ПараметрыПредставления);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ПоказателиБизнеса
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_СистемыНО(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("СистемыНО");
	
	Для Каждого СистемаНО Из ПараметрыПредставления.СистемыНО Цикл
		Компоновщик.ЗаписатьНачалоЭлемента("СистемаНО");
		СистемаНО = Лев(СистемаНО, 20);
		Компоновщик.ЗаписатьТекст(СистемаНО);
		Компоновщик.ЗаписатьКонецЭлемента(); // СистемаНО
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // СистемыНО
	
КонецПроцедуры

Функция СписокПараметровМакета()
	
	ПараметрыМакета = Новый ТаблицаЗначений;
	ПараметрыМакета.Колонки.Добавить("ИмяПараметра", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ПараметрыМакета.Колонки.Добавить("ПозицияНачала", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0, ДопустимыйЗнак.Неотрицательный));
	ПараметрыМакета.Колонки.Добавить("ПозицияКонца", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0, ДопустимыйЗнак.Неотрицательный));
	ПараметрыМакета.Колонки.Добавить("ЗначениеПараметра");
	
	Возврат ПараметрыМакета;
	
КонецФункции

Функция ПодобратьТекстыМакетов(РеквизитыБанков, ЭтоЮридическоеЛицо, ДатаСогласия)
	
	СписокБанков = Новый Массив;
	Для Каждого ИнформацияОБанке Из РеквизитыБанков Цикл
		СписокБанков.Добавить(ИнформацияОБанке.Банк);
	КонецЦикла;
	
	ИмяХарактеристики = ?(ЭтоЮридическоеЛицо,
		Перечисления.ХарактеристикиСервисаЗаявкиНаКредит.СогласиеНаОбработкуДанныхЮЛ,
		Перечисления.ХарактеристикиСервисаЗаявкиНаКредит.СогласиеНаОбработкуДанныхФЛ);

	// Для выгрузки текстов согласий по ранее отправленным заявкам, которые сейчас уже в режиме только просмотр,
	// и банки которых могут быть сейчас уже неактивны, указываем,
	// что необходимо возвращать характеристики вне зависимости от активности банков и их услуг.
	ХарактеристикиСервиса = УниверсальныйОбменСБанками.ХарактеристикиСервиса(
		Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит,
		ИмяХарактеристики,
		СписокБанков,
		Истина);

	Для каждого ИнформацияОБанке Из РеквизитыБанков Цикл
		
		ХарактеристикаБанка = ХарактеристикиСервиса.Найти(ИнформацияОБанке.Банк, "Банк");
		Если ХарактеристикаБанка = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		УсловияКредитования = ЗаявкиНаКредит.УсловияКредитованияXDTO(ХарактеристикаБанка.Значение);
		Если УсловияКредитования = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого Согласие Из УсловияКредитования.СогласиеНаОбработкуДанных Цикл
			
			Если Согласие.ДатаНачала > ДатаСогласия
			 Или Согласие.ДатаОкончания < ДатаСогласия Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрНайти(Согласие.ТекстМакета, "<HTML") = 0 И СтрНайти(Согласие.ТекстМакета, "<html") = 0 Тогда
				// Если возникает ошибка, то далее возьмем макет по-умолчанию.
				ИмяСобытия = НСтр("ru = 'Подобрать тексты макетов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(
					ЗаявкиНаКредитКлиентСервер.СобытиеЖурналаРегистрации(ИмяСобытия),
					УровеньЖурналаРегистрации.Ошибка,
					,
					ИнформацияОБанке.Банк,
					НСтр("ru = 'Текст макета не является корректным html-текстом: не содержит тега html'"));
				Продолжить;
				
			КонецЕсли;
			
			ИнформацияОБанке.ТекстМакета = Согласие.ТекстМакета;
			ИнформацияОБанке.ПараметрыМакета = РазметкаТекста(ИнформацияОБанке.ТекстМакета);
			
		КонецЦикла;
		
	КонецЦикла;
		
	// остальные заполняем макетом по-умолчанию
	ТекстМакета = "";
	ПараметрыМакета = Неопределено;
	Для Каждого ИнформацияОБанке Из РеквизитыБанков Цикл
		Если Не ПустаяСтрока(ИнформацияОБанке.ТекстМакета) Тогда
			Продолжить;
		КонецЕсли;
			
		Если ПустаяСтрока(ТекстМакета) Тогда
			ИмяХарактеристики = ?(ЭтоЮридическоеЛицо, "СогласиеНаОбработкуДанныхЮЛ", "СогласиеНаОбработкуДанныхФЛ");
			ТекстМакета = Документы.ЗаявкаНаКредит.ПолучитьМакет(ИмяХарактеристики).ПолучитьТекст();
			ПараметрыМакета = РазметкаТекста(ТекстМакета);
		КонецЕсли;
		ИнформацияОБанке.ТекстМакета = ТекстМакета;
		ИнформацияОБанке.ПараметрыМакета = ПараметрыМакета;

	КонецЦикла;
	
	Возврат ТекстМакета;
	
КонецФункции

// Анализирует, какие параметры есть в переданном тексте.
//
// Параметры:
//  ТекстМакета  - HTML-текст - текст, содержащий параметры в фигурных скобках. Для использования "{" в тексте, а не в
//                              параметре нужно ее задвоить.
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. СписокПараметровМакета()
//
Функция РазметкаТекста(ТекстМакета)
	
	ПараметрыМакета = СписокПараметровМакета();

	ПозицияПараметра = СтрНайти(ТекстМакета, "{");
	Пока ПозицияПараметра <> 0 Цикл
		
		Если Сред(ТекстМакета, ПозицияПараметра + 1, 1) = "{" Тогда
			// это не параметр, а экранированный символ
			ПозицияПараметра = ПозицияПараметра + 1;

		Иначе
			
			ПараметрМакета = ПараметрыМакета.Добавить();
			ПараметрМакета.ПозицияНачала = ПозицияПараметра;
			ПараметрМакета.ПозицияКонца  = СтрНайти(ТекстМакета, "}", , ПозицияПараметра);
			Если ПараметрМакета.ПозицияКонца = 0 Тогда // ошибочный формат текста макета
				
				ПараметрыМакета.Удалить(ПараметрМакета);
				ПозицияПараметра = СтрДлина(ТекстМакета);

			Иначе

				ПараметрМакета.ИмяПараметра = Сред(ТекстМакета,
					ПараметрМакета.ПозицияНачала + 1,
					ПараметрМакета.ПозицияКонца - ПараметрМакета.ПозицияНачала - 1);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПозицияПараметра = ПозицияПараметра + 1;
		ПозицияПараметра = СтрНайти(ТекстМакета, "{", , ПозицияПараметра);
	
	КонецЦикла;
	
	Возврат ПараметрыМакета;
	
КонецФункции

#КонецОбласти

#Область ФормированиеАкцепта

Процедура ВывестиУзел_Акцепт_Документ(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Документ");
	
	ВывестиАтрибут(Компоновщик, "ПредметОбмена",     	ПараметрыПредставления.ИдентификаторЗаявки, "Т,255");
	ВывестиАтрибут(Компоновщик, "ДатаПредметаОбмена",   ПараметрыПредставления.ДатаЗаявки, "Д");
	ВывестиНеобязательныйАтрибут(Компоновщик, "РегистрационныйНомер", ПараметрыПредставления.РегистрационныйНомер, "Т,30");
	ВывестиАтрибут(Компоновщик, "ДатаДок",              ТекущаяДатаСеанса(), "Д");
	
	ВывестиУзел_Акцепт_Организация(Компоновщик, ПараметрыПредставления);
	ВывестиУзел_Акцепт_Банк(Компоновщик, ПараметрыПредставления);
	ВывестиУзел_Акцепт_Продукт(Компоновщик, ПараметрыПредставления);
		
	Компоновщик.ЗаписатьКонецЭлемента(); // Документ
	
КонецПроцедуры

Процедура ВывестиУзел_Акцепт_Организация(Компоновщик, ПараметрыПредставления)
		
	Организация = ПараметрыПредставления.Организация;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Организация");
		
	ВывестиАтрибут(Компоновщик, "Наим", Организация.Наименование, "Т,1000");
	ВывестиАтрибут(Компоновщик, "ИНН",  Организация.ИНН, "Т,12");
	ВывестиНеобязательныйАтрибут(Компоновщик, "КПП",  Организация.КПП, "Т,9");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Организация
	
КонецПроцедуры

Процедура ВывестиУзел_Акцепт_Банк(Компоновщик, ПараметрыПредставления)
		
	Банк = ПараметрыПредставления.Банк;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Банк");
	
	ВывестиАтрибут(Компоновщик, "ИНН",  Банк.ИНН, "Т,10");
	ВывестиАтрибут(Компоновщик, "Наим", Банк.Наименование, "Т,250");
		
	Компоновщик.ЗаписатьКонецЭлемента(); // Банк
	
КонецПроцедуры

Процедура ВывестиУзел_Акцепт_Продукт(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Продукт");
		
	ВывестиАтрибут(Компоновщик, "СуммаКредита", ПараметрыПредставления.СуммаКредита, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "Срок",  ПараметрыПредставления.СрокКредита, "Ч,4");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Продукт
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеСтруктурыПредставлений

Процедура ВывестиУниверсальныйУзел_ФИОТип(Компоновщик, ФИО, ИмяУзла = "ФИО")
	
	Компоновщик.ЗаписатьНачалоЭлемента(ИмяУзла);
	
	ВывестиАтрибут(Компоновщик, "Фамилия", ФИО.Фамилия);
	ВывестиАтрибут(Компоновщик, "Имя", ФИО.Имя);
	ВывестиНеобязательныйАтрибут(Компоновщик, "Отчество", ФИО.Отчество);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ИмяУзла
	
КонецПроцедуры

Процедура ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, СведенияФЛ, ИмяУзла)
	
	Компоновщик.ЗаписатьНачалоЭлемента(ИмяУзла);
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "ИННФЛ",        СведенияФЛ.ИНН,          "Т,12");
	ВывестиНеобязательныйАтрибут(Компоновщик, "СНИЛС",        СведенияФЛ.СНИЛС,        "Т,14");

	ВывестиАтрибут(Компоновщик, "Пол",          СведенияФЛ.Пол,          "Т,1");
	ВывестиАтрибут(Компоновщик, "ДатаРождения", СведенияФЛ.ДатаРождения, "Т,10");
	
	ВывестиАтрибут(Компоновщик, "Гражданство",   СведенияФЛ.Гражданство,   "Т,3");
	ВывестиАтрибут(Компоновщик, "МестоРождения", СведенияФЛ.МестоРождения, "Т,255");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "СемПолож", СведенияФЛ.СемейноеПоложение, "Т,1");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "Телефон", СведенияФЛ.Телефон,       "Т,20");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ДопТелефон", СведенияФЛ.ДопТелефон, "Т,20");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ЭлПочта", СведенияФЛ.ЭлПочта,       "Т,255");
	
	ВывестиУниверсальныйУзел_ФИОТип(Компоновщик, СведенияФЛ.ФИО);
	
	Компоновщик.ЗаписатьНачалоЭлемента("УдЛичнФЛ");
	
	УдостоверениеЛичности = СведенияФЛ.УдостоверениеЛичности;
	
	ВывестиАтрибут(Компоновщик, "КодВидДок", УдостоверениеЛичности.КодВидаДокумента,    "Т,2");
	ВывестиНеобязательныйАтрибут(Компоновщик, "СерДок",    УдостоверениеЛичности.СерияДокумента,      "Т,14");
	ВывестиАтрибут(Компоновщик, "НомДок",    УдостоверениеЛичности.НомерДокумента,      "Т,14");
	ВывестиАтрибут(Компоновщик, "ВыдДок",    УдостоверениеЛичности.КемВыдан,            "Т,255");
	ВывестиАтрибут(Компоновщик, "ДатаДок",   УдостоверениеЛичности.ДатаВыдачи,          "Т,10");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "КодПодр", УдостоверениеЛичности.КодПодразделения, "Т,255");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // УдЛичнФЛ
	
	ВывестиУниверсальныйУзел_АдресТип(Компоновщик, СведенияФЛ.СведенияОбАдресе); 
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ИмяУзла
	
КонецПроцедуры

Процедура ВывестиУниверсальныйУзел_АдресТип(Компоновщик, СведенияОбАдресе, ИмяУзла = "СведенияОбАдресе")
	
	Компоновщик.ЗаписатьНачалоЭлемента(ИмяУзла);
	
	ВывестиАтрибут(Компоновщик, "Представление", СведенияОбАдресе.Представление, "Т,1000");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Индекс", СведенияОбАдресе.Индекс, "Т,6");
	ВывестиАтрибут(Компоновщик, "КодРегион", ?(СведенияОбАдресе.Свойство("КодРегиона"), СведенияОбАдресе.КодРегиона, ""), "Т,2");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Район", СведенияОбАдресе.Район, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Город", СведенияОбАдресе.Город, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "НаселПункт", СведенияОбАдресе.НаселенныйПункт, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Улица", СведенияОбАдресе.Улица, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Дом", СведенияОбАдресе.Здание.Номер, "Т,20");
	
	Если СведенияОбАдресе.Корпуса.Количество() > 0 Тогда
		ВывестиНеобязательныйАтрибут(Компоновщик, "Корпус", СведенияОбАдресе.Корпуса[0].Номер, "Т,20");
	КонецЕсли;
	
	Если СведенияОбАдресе.Помещения.Количество() > 0 Тогда
		ВывестиНеобязательныйАтрибут(Компоновщик, "Кварт", СведенияОбАдресе.Помещения[0].Номер, "Т,20");
	КонецЕсли;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ИмяУзла
	
КонецПроцедуры

#КонецОбласти

#Область КомпоновщикXML

Функция НовыйКомпоновщикXML(ИмяФайла, Кодировка = "windows-1251")
	
	КомпоновщикXML = Новый ЗаписьXML();
	КомпоновщикXML.ОткрытьФайл(ИмяФайла, Кодировка);
	КомпоновщикXML.ЗаписатьОбъявлениеXML();
	
	Возврат КомпоновщикXML;
	
КонецФункции
 
Процедура ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	ОписаниеТипаАтрибута = СтрРазделить(ТипАтрибута, ",");
	
	Если ОписаниеТипаАтрибута[0] = "Д" Тогда
		ПредставлениеЗначения = Формат(ЗначениеАтрибута, "ДФ=dd.MM.yyyy");
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Т" Тогда
		ПредставлениеЗначения = СокрЛП(ЗначениеАтрибута);
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			МаксимальнаяДлина = Число(ОписаниеТипаАтрибута[1]);
			ПредставлениеЗначения = Лев(ПредставлениеЗначения, МаксимальнаяДлина);
		КонецЕсли;
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Ч" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			РазмерностьПолная = Число(ОписаниеТипаАтрибута[1]);
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(РазмерностьПолная, "ЧГ=") + ";";
		КонецЕсли;
		
		Если ОписаниеТипаАтрибута.Количество() >= 3 Тогда
			РазмерностьДробнойЧасти = Число(ОписаниеТипаАтрибута[2]);
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(РазмерностьДробнойЧасти, "ЧГ=") + ";";
		КонецЕсли;
		
		ПредставлениеЗначения = Формат(ЗначениеАтрибута, СтрокаФормата);
		
	Иначе
		ПредставлениеЗначения = СокрЛП(ЗначениеАтрибута);
		
	КонецЕсли;
	
	КомпоновщикXML.ЗаписатьАтрибут(ИмяАтрибута, ПредставлениеЗначения);
	
КонецПроцедуры

Процедура ВывестиНеобязательныйАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
		ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута);
	КонецЕсли;
	
КонецПроцедуры

Функция ДвоичныеДанныеКомпоновщикаXML(КомпоновщикXML, ИмяФайла)
	
	КомпоновщикXML.Закрыть();
	КомпоновщикXML = Неопределено;
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
	УдалитьФайлы(ИмяФайла);
	
	Возврат ДвоичныеДанныеФайла;
	
КонецФункции

#КонецОбласти

#Область СостояниеЗаявок

Процедура ЗапуститьУниверсальныйОбменСБанкамиПоСервисуВФоне(Параметры, АдресРезультата) Экспорт
	
	// Скачивание данных с сервера обмена с банками в рег.задании выполняется под полными правами,
	// поэтому если вызываем его интерактивно, то самостоятельно включаем привилегированный режим.
	УстановитьПривилегированныйРежим(Истина);

	УниверсальныйОбменСБанками.ЗапуститьУниверсальныйОбменСБанкамиПоСервису(
		Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Возвращает строку данных транзакции указанного типа с наибольшей датой, но не ранее указанной даты.
//
Функция НайтиТранзакцию(ТаблицаДанныхТранзакций, ТипТранзакции, НеРанееДаты = '0001-01-01')

	Если НЕ ЗначениеЗаполнено(ТаблицаДанныхТранзакций) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Отбор = Новый Структура("ТипТранзакции", ТипТранзакции);
	НайденныеСтроки    = ТаблицаДанныхТранзакций.НайтиСтроки(Отбор);
	МаксДатаТранспорта = '0001-01-01';
	
	Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
	
		Если НайденнаяСтрока.ДатаТранспорта < НеРанееДаты Тогда
			// Не учитываем транзакции, которые были получены ранее указанной даты.
			Продолжить;
		КонецЕсли;
	
		Если НайденнаяСтрока.ДатаТранспорта < МаксДатаТранспорта Тогда
			Продолжить;
		КонецЕсли;
		
		МаксДатаТранспорта = НайденнаяСтрока.ДатаТранспорта;
		Результат          = НайденнаяСтрока;
		
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура УстановитьКонстантуИспользоватьЗаявкиНаКредит() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Организации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Если Организации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЮридическиеАдреса = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияОбъектовНаДату(
		Организации,,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации));
	
	Для каждого ЮридическийАдрес Из ЮридическиеАдреса Цикл
		КодРегиона = УправлениеКонтактнойИнформациейБП.КодРегионаПоАдресу(ЮридическийАдрес.Значение);
		Если КодыРегионовЭксплуатации().Найти(КодРегиона) <> Неопределено Тогда
			Константы.ИспользоватьЗаявкиНаКредит.Установить(Истина);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти