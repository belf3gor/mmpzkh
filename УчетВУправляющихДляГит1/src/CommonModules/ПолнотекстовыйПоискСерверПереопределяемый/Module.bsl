///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Позволяет внести изменения в дерево с разделами полнотекстового поиска, отображаемого при выборе области поиска.
// По умолчанию дерево разделов формируется на основании состава подсистем конфигурации.
// Структура дерева описана в форме Обработка.ПолнотекстовыйПоискВДанных.Форма.ВыборОбластиПоиска.
// Все колонки, неуказанные в параметрах, будут автоматически рассчитаны.
// При необходимости построить дерево разделов самостоятельно требуется сохранить состав колонок.
//
// Параметры:
//   РазделыПоиска - ДеревоЗначений - области поиска. Содержит колонки:
//       ** Раздел   - Строка   - Представление раздела: подсистемы или объекта метаданных.
//       ** Картинка - Картинка - Картинка раздела, рекомендуется только для корневых разделов.
//       ** ОбъектМД - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Задается только для объектов метаданных,
//                     для разделов остается пустым.
// Пример:
//
//	НовыйРаздел = РазделыПоиска.Строки.Добавить();
//	НовыйРаздел.Раздел = "Главное";
//	НовыйРаздел.Картинка = БиблиотекаКартинок._ДемоРазделГлавное;
//	
//	ОбъектМетаданных = Метаданные.Документы._ДемоСчетНаОплатуПокупателю;
//	
//	Если ПравоДоступа("Просмотр", ОбъектМетаданных)
//		И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда 
//		
//		НовыйОбъектРаздела = НовыйРаздел.Строки.Добавить();
//		НовыйОбъектРаздела.Раздел = ОбъектМетаданных.ПредставлениеСписка;
//		НовыйОбъектРаздела.ОбъектМД = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
//	КонецЕсли;
//
Процедура ПриПолученииРазделовПолнотекстовогоПоиска(РазделыПоиска) Экспорт
	
// Добавление раздела "Главное".
	
	НовыйРаздел = РазделыПоиска.Строки.Добавить();
	НовыйРаздел.Раздел = НСтр("ru = 'Главное'");
	НовыйРаздел.Картинка = БиблиотекаКартинок.Главное;
	
	// Перед указанием объекта метаданных убедитесь, что его свойство ПолнотекстовыйПоиск
	// установлено в значение Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать.
	// Отдельную проверку для этого писать не нужно, т.к. режим поиска в режиме предприятия не меняется.
	ОбъектыМетаданныхППИ = Новый Массив;
	ОбъектыМетаданныхППИ.Добавить(Метаданные.ПланыСчетов.Хозрасчетный);
	ОбъектыМетаданныхППИ.Добавить(Метаданные.Справочники.Организации);
	
	Для каждого ОбъектМетаданных Из ОбъектыМетаданныхППИ Цикл
	
		Если ПравоДоступа("Просмотр", ОбъектМетаданных)
			И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда 
			
			НовыйОбъектРаздела = НовыйРаздел.Строки.Добавить();
			// Для формирования представления объекта рекомендуется использовать свойства объекта метаданных:
			// РасширенноеПредставлениеСписка, ПредставлениеСписка или метод Представление(), в зависимости от объекта.
			НовыйОбъектРаздела.Раздел = ?(ЗначениеЗаполнено(ОбъектМетаданных.ПредставлениеСписка), ОбъектМетаданных.ПредставлениеСписка, ОбъектМетаданных.Синоним);
			НовыйОбъектРаздела.ОбъектМД = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
		КонецЕсли;
		
	КонецЦикла;
	
	// Сортировка корневых разделов.
	
	ПорядокРазделов = Новый Массив;
	ПорядокРазделов.Добавить(НСтр("ru = 'Главное'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Руководителю'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Банк и касса'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Продажи'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Покупки'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Склад'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Производство'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'ОС и НМА'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Зарплата и кадры'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Операции'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Отчеты'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Справочники'"));
	ПорядокРазделов.Добавить(НСтр("ru = 'Администрирование'"));
	
	КолонкаПорядка = РазделыПоиска.Колонки.Добавить("Порядок");
	
	Для Индекс = 0 По ПорядокРазделов.ВГраница() Цикл
		ИмяРаздела = ПорядокРазделов[Индекс];
		СтрокаРаздел = РазделыПоиска.Строки.Найти(ИмяРаздела, "Раздел");
		Если СтрокаРаздел <> Неопределено Тогда
			СтрокаРаздел.Порядок = Индекс;
		КонецЕсли;
	КонецЦикла;
	
	РазделыПоиска.Строки.Сортировать("Порядок");
	РазделыПоиска.Колонки.Удалить(КолонкаПорядка);
	
КонецПроцедуры

#КонецОбласти