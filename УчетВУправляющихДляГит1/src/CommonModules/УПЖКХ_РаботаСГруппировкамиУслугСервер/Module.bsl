
#Область СлужебныеПроцедурыИФункции

// Функция для получения списка услуг группы услуг.
//
// Параметры:
//  ГруппаУслуг - Ссылка на элемент справочника "Группы услуг" 
//              - группа услуг, для которой необходимо определить список услуг.
//
// Возвращаемое значение:
//   Массив - массив услуг группы.
//
Функция ПолучитьСписокУслугГруппы(ГруппаУслуг) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ГруппаУслуг",   ГруппаУслуг);
	Запрос.УстановитьПараметр("ВидГруппУслуг", ГруппаУслуг.Родитель);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппировкаУслуг.Услуга КАК Услуга
	|ИЗ
	|	РегистрСведений.УПЖКХ_ГруппировкиУслуг КАК ГруппировкаУслуг
	|ГДЕ
	|	ГруппировкаУслуг.ВидГруппУслуг = &ВидГруппУслуг
	|	И ГруппировкаУслуг.ГруппаУслуг = &ГруппаУслуг
	|
	|УПОРЯДОЧИТЬ ПО
	|	Услуга";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Услуга");
	
КонецФункции

// Определяет записаны ли услуги из списка в другие группы, кроме текущей.
//
// Параметры:
//  СписокУслуг - Список значений - список услуг, для которых выполняется поиск.
//  ВидГруппУслуг - Ссылка на группу справочника "Группы услуг" 
//                - вид группа услуг.
//  ГруппаУслуг - Ссылка на элемент справочника "Группы услуг" 
//              - группа услуг - исключение.
//
// Возвращаемое значение:
//   Таблица значений - таблица с услугами и группами услуг, в которые они уже включены.
//
Функция ПолучитьСписокГруппУслугВКоторыеУжеВключеныУслугиИзСписка(СписокУслуг, ВидГруппУслуг, ГруппаУслуг) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ГруппаУслуг",   ГруппаУслуг);
	Запрос.УстановитьПараметр("ВидГруппУслуг", ВидГруппУслуг);
	Запрос.УстановитьПараметр("СписокУслуг",   СписокУслуг);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппировкаУслуг.Услуга КАК Услуга,
	|	ГруппировкаУслуг.ГруппаУслуг КАК ГруппаУслуг
	|ИЗ
	|	РегистрСведений.УПЖКХ_ГруппировкиУслуг КАК ГруппировкаУслуг
	|ГДЕ
	|	ГруппировкаУслуг.ВидГруппУслуг = &ВидГруппУслуг
	|	И НЕ ГруппировкаУслуг.ГруппаУслуг = &ГруппаУслуг
	|	И ГруппировкаУслуг.Услуга В (&СписокУслуг)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Услуга";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти