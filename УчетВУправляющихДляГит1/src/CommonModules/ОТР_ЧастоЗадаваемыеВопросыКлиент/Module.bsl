
#Область ПроцедурыУправленияНаКлиенте

// Процедура открывает форму со списком ссылок на часто задаваемые вопросы.
//
// Параметры:
//  Команда - имя команды, вызывающей процедуру.
//
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда) Экспорт
	
	// Имя переданной команды состоит из двух частей: стандартная часть и полное имя объекта конфигурации, из формы которого вызывается данная процедура.
	// Определим позицию в имени команды, с которой начинается полное имя объекта.
	НомерПервогоСимволаИмениОбъекта = СтрДлина(ОТР_ЧастоЗадаваемыеВопросыКлиентСервер.ПолучитьСтандартнуюЧастьИмениКоманды()) + 1;
	
	// Определяем полное имя объекта.
	ПолноеИмяОбъекта = Сред(Команда.Имя, НомерПервогоСимволаИмениОбъекта);
	
	// В параметрах передаем имя объекта конфигурации, из формы которого вызывается данная процедура.
	ОткрытьФорму("ОбщаяФорма.ОТР_ЧастоЗадаваемыеВопросы", Новый Структура("ПолноеИмяОбъекта", ПолноеИмяОбъекта), , ПолноеИмяОбъекта);
	
КонецПроцедуры

// Процедура открывает статью часто задаваемых вопросов в браузере.
//
// Параметры:
//  ИнтернетАдрес - Строка - Интернет-адрес статьи часто задаваемых вопросов.
//
Процедура ОткрытьСтатьюЧастоЗадаваемыеВопросыВБраузере(ИнтернетАдрес) Экспорт
	
	ОТР_ЧастоЗадаваемыеВопросыПереопределяемый.ДополнитьИнтернетАдресСлужебнымПараметрами(ИнтернетАдрес);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(,,, "ОткрытьСтатьюЧастоЗадаваемыеВопросыВБраузереОбработатьОшибку", ЭтотОбъект);
	
	НачатьЗапускПриложения(ОписаниеОповещения, ИнтернетАдрес);
	
КонецПроцедуры

// Обработчик завершения запуска приложения.
Процедура ОткрытьСтатьюЧастоЗадаваемыеВопросыВБраузереОбработатьОшибку(КодВозврата, ДополнительныеПараметры) Экспорт
	ОТР_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке());
КонецПроцедуры

#КонецОбласти