#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьЗаявлениеСотрудникаПоОснованию(Основание, ИдентификаторСтроки = Неопределено, Заявление = Неопределено) Экспорт
	Если ЗначениеЗаполнено(Заявление) Тогда
		Параметры = Новый Структура("Ключ", Заявление);
		Уникальность = Строка(Заявление.УникальныйИдентификатор());
	ИначеЕсли ЗначениеЗаполнено(Основание) Тогда
		Параметры = Новый Структура("Основание, ИдентификаторСтроки", Основание, ИдентификаторСтроки);
		Уникальность = Строка(Основание.УникальныйИдентификатор());
		Если ИдентификаторСтроки <> Неопределено Тогда
			Уникальность = Уникальность +"/"+ Строка(ИдентификаторСтроки);
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаявлениеСотрудникаНаВыплатуПособия.ФормаОбъекта", Параметры, , Уникальность);
КонецПроцедуры

Процедура ОткрытьУведомлениеОПрекращенииПособия(Основание) Экспорт
	Если ЗначениеЗаполнено(Основание) Тогда
		Параметры = Новый Структура("Основание", Основание);
		Уникальность = Строка(Основание.УникальныйИдентификатор());
	Иначе
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Документ.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком.ФормаОбъекта", Параметры, , Уникальность);
КонецПроцедуры

Процедура ВыгрузитьРеестр(СсылкаРеестра, УникальныйИдентификатор) Экспорт
	Если ТипЗнч(СсылкаРеестра) <> Тип("ДокументСсылка.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий") Тогда
		Возврат;
	КонецЕсли;
	ФайлРеестра = УчетПособийСоциальногоСтрахованияВызовСервера.ФайлРеестраПрямыхВыплат(СсылкаРеестра, УникальныйИдентификатор);
	Если ФайлРеестра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохранения.Диалог.Заголовок = НСтр("ru = 'Выберите файл для сохранения реестра прямых выплат'");
	ПараметрыСохранения.Диалог.Фильтр    = НСтр("ru = 'Файлы XML (*.xml)|*.xml|Все файлы (*.*)|*'");
	ПараметрыСохранения.ТекстПредложения = НСтр("ru = 'Для выгрузки файлов рекомендуется установить расширение для веб-клиента 1С:Предприятие.'");
	
	СохранитьФайл(ФайлРеестра.АдресДвоичныхДанных, ФайлРеестра.ИмяФайлаСРасширением, ПараметрыСохранения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СохранитьФайл(АдресДвоичныхДанных, ИмяФайла, ПараметрыСохранения = Неопределено)
	Обработчик = Новый ОписаниеОповещения("ПослеСохраненияФайла", ЭтотОбъект);
	ФайловаяСистемаКлиент.СохранитьФайл(Обработчик, АдресДвоичныхДанных, ИмяФайла, ПараметрыСохранения);
КонецПроцедуры

Процедура ПослеСохраненияФайла(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(ПолученныеФайлы) = Тип("Массив") И ПолученныеФайлы.Количество() > 0 Тогда
		ПолноеИмя = ПолученныеФайлы[0].Имя;
		МассивИмен = СтрРазделить(ПолноеИмя, "/\", Ложь);
		Если МассивИмен.Количество() > 1 Тогда
			Обработчик = Новый ОписаниеОповещения("ОткрытьПроводник", ФайловаяСистемаКлиент, ПолноеИмя);
			КраткоеИмя = МассивИмен[МассивИмен.Количество() - 1];
		Иначе
			Обработчик = Неопределено;
			КраткоеИмя = ПолноеИмя;
		КонецЕсли;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Файл сохранен'"), Обработчик, КраткоеИмя, БиблиотекаКартинок.Успешно32);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
