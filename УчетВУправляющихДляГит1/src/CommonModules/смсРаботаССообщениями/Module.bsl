
#Область СлужебныеПроцедурыИФункции

Функция Использовать7БитСообщение(Сообщение) Экспорт
	ДлинаСообщения = СтрДлина(Сообщение);
	Результат = Истина;
	Для ТекПозиция = 1 По ДлинаСообщения Цикл
		ТекСимвол = Сред(Сообщение, ТекПозиция, 1);
		Если ТекСимвол = "@" Тогда
			Продолжить;
		ИначеЕсли ТекСимвол = "$" Тогда
			Продолжить;
		ИначеЕсли КодСимвола(ТекСимвол) > 122 Тогда
			Результат = Ложь;
			Прервать;
		ИначеЕсли КодСимвола(ТекСимвол) > 96 Тогда
			Продолжить;
		ИначеЕсли  КодСимвола(ТекСимвол) > 91 Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	Возврат Результат;
КонецФункции // Использовать7БитСообщение()

Функция ПолучитьДлинуИКоличествоСМС(ТекстСообщения) Экспорт

	// Проверяем сообщения на возможность использования кодирования
	// т.е. в 140 байт уместить 160 единиц информации.
	Это7Бит = Использовать7БитСообщение(ТекстСообщения);
	Если Это7Бит Тогда
		МаксДлинаСообщения = 160;
		МаксДлинаСообщенияБезЗаголовка = 153;
	Иначе
		МаксДлинаСообщения = 70;
		МаксДлинаСообщенияБезЗаголовка = 67;
	КонецЕсли; 
	
	ДлинаСообщения = СтрДлина(ТекстСообщения);
	
	// Подсчет количества частей СМС сообщения
	Если ДлинаСообщения = 0 Тогда
		КоличествоСМС = 0;
	ИначеЕсли ДлинаСообщения <= МаксДлинаСообщения Тогда
		КоличествоСМС = 1;
	Иначе
		КоличествоСМС = Цел(ДлинаСообщения / МаксДлинаСообщенияБезЗаголовка + 0.999999);
	КонецЕсли;
	
	ИнформацияОСообщении = Новый Структура;
	
	ИнформацияОСообщении.Вставить("ДлинаСообщения", ДлинаСообщения);
	ИнформацияОСообщении.Вставить("КоличествоСМС", КоличествоСМС);
	
	Возврат ИнформацияОСообщении;
	
КонецФункции // ПолучитьДлинуИКоличествоСМС()

#КонецОбласти