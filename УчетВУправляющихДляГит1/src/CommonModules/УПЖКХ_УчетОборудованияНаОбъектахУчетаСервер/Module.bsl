
#Область ПроцедурыИФункцииРаботыСОборудованием

// Функция возвращает таблицу связанного с объектов учета оборудования,
// введенного в эксплуатацию и установленного на объект.
//
// Параметры:
//  ОбъектыУстановки - Ссылка либо массив ссылок элементов справочников "Здания", "Подъезды" или "Помещения".
//
// Возвращаемое значение:
//  Таблица значений связанного оборудования.
//
Функция ПолучитьОборудованиеУстановленноеНаОбъектУчета(ОбъектыУстановки, Период = Неопределено) Экспорт
	
	Если Период = Неопределено Тогда
		Период = УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьРабочуюДату();
	КонецЕсли;
	
	// В запросе получается оборудование, закрепленное за объектом.
	// При этом, если оборудование обобщающее, то его ввод в эксплуатацию не требуется,
	// если не обобщающее, то оборудование должно быть введено в эксплуатацию.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектыУстановки", ОбъектыУстановки);
	Запрос.УстановитьПараметр("Период",           Период);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.Период,
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.ОбъектУстановки,
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.Оборудование,
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.Оборудование.ЭтоОбобщающееОборудование КАК ЭтоОбобщающееОборудование,
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.Оборудование.Родитель КАК ГруппаОборудования,
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.Количество
	|ПОМЕСТИТЬ втСвязанноеОборудование
	|ИЗ
	|	РегистрСведений.УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчета.СрезПоследних(&Период, ОбъектУстановки В (&ОбъектыУстановки)) КАК УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних
	|ГДЕ
	|	УПЖКХ_ВзаимосвязьОборудованияИОбъектовУчетаСрезПоследних.Установлено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УПЖКХ_СведенияОбОборудованииСрезПоследних.Оборудование
	|ПОМЕСТИТЬ втСостоянияОборудования
	|ИЗ
	|	РегистрСведений.УПЖКХ_СведенияОбОборудовании.СрезПоследних(
	|			&Период,
	|			Оборудование В
	|				(ВЫБРАТЬ
	|					втСвязанноеОборудование.Оборудование
	|				ИЗ
	|					втСвязанноеОборудование КАК втСвязанноеОборудование
	|				ГДЕ
	|					НЕ втСвязанноеОборудование.ЭтоОбобщающееОборудование)) КАК УПЖКХ_СведенияОбОборудованииСрезПоследних
	|ГДЕ
	|	УПЖКХ_СведенияОбОборудованииСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_СостоянияОборудования.ВведеноВЭксплуатацию)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втСвязанноеОборудование.Оборудование
	|ИЗ
	|	втСвязанноеОборудование КАК втСвязанноеОборудование
	|ГДЕ
	|	втСвязанноеОборудование.ЭтоОбобщающееОборудование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСвязанноеОборудование.Период КАК Период,
	|	втСвязанноеОборудование.ОбъектУстановки КАК ОбъектУстановки,
	|	втСвязанноеОборудование.Оборудование КАК Оборудование,
	|	втСвязанноеОборудование.ГруппаОборудования КАК ГруппаОборудования,
	|	втСвязанноеОборудование.Количество КАК Количество
	|ИЗ
	|	втСвязанноеОборудование КАК втСвязанноеОборудование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСостоянияОборудования КАК втСостоянияОборудования
	|		ПО втСвязанноеОборудование.Оборудование = втСостоянияОборудования.Оборудование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппаОборудования,
	|	Оборудование";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьОборудованиеУстановленноеНаОбъектУчета()

// Функция возвращает актуальное состояние оборудования. Если оборудование в регистре сведений отсутствует,
// значит оно не введено в эксплуатацию.
//
// Параметры:
//  Оборудование - Ссылка на элемент справочника "Прочие объекты учета, оборудование".
//  Период - Дата, период получения сведений о состоянии оборудования.
//
// Возвращаемое значение:
//  Значение перечисления "Состояния оборудования".
//
Функция ПолучитьАктуальноеСостояниеОборудования(Оборудование, Период = Неопределено) Экспорт
	
	Если Период = Неопределено Тогда
		Период = УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьРабочуюДату();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Оборудование", Оборудование);
	Запрос.УстановитьПараметр("Период",       Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УПЖКХ_СведенияОбОборудованииСрезПоследних.Состояние
	|ИЗ
	|	РегистрСведений.УПЖКХ_СведенияОбОборудовании.СрезПоследних(&Период, Оборудование = &Оборудование) КАК УПЖКХ_СведенияОбОборудованииСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Состояние;
	Иначе
		Возврат Перечисления.УПЖКХ_СостоянияОборудования.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции // ПолучитьАктуальноеСостояниеОборудования()

#КонецОбласти