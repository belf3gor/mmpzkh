
////////////////////////////////////////////////////////////////////////////////
// ВАЖНО!
//
// В данном модуле находятся процедуры и функции, отличающиеся содержанием
// для каждого конкретного типового решения, в котором используется модуль СМС.
//
// Поэтому структуру текущего модуля необходимо адаптировать под конкретное
// типовое решение.
//

#Область СлужебныеПроцедурыИФункции

// Функция задает текст запроса для добавления к переданной таблице номера телефона
//
// Параметры
//  ТипЗначенияПолучателя - Строка - тип значения получателя
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>.
//
// Возвращаемое значение:
//   Строка      - Текст запроса добавляющий строку с номером телефона.
//
Функция ПолучитьТекстЗапросаНомеровТелефонов() Экспорт
	
	// При формировании запроса в параметре таблица получателей находится таблица значений
	// привязка осуществляется по полю "Получатель", для каждого
	// в итоговой части поле содержащее номер телефона должно иметь имя "НомерТелефона".
	
	// Квартплата +
	
	// При этом номера телефонов получаются с приоритетом: в первую очередь берутся номера
	// телефонов из данных лицевого счета, а уже потом из данных об ответственном собственнике л/с.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПолучателей.Получатель,
	|	ТаблицаПолучателей.КлючДополнительнойИдентификации
	|ПОМЕСТИТЬ врТаблицаПолучателей
	|ИЗ
	|	&ТаблицаПолучателей КАК ТаблицаПолучателей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПолучателей.Получатель,
	|	ТаблицаПолучателей.КлючДополнительнойИдентификации,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(КВП_ЛицевыеСчета.Телефон, """") КАК СТРОКА(50)) КАК НомерТелефонаЛС
	|ПОМЕСТИТЬ ТаблицаПолучателей
	|ИЗ
	|	врТаблицаПолучателей КАК ТаблицаПолучателей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КВП_ЛицевыеСчета КАК КВП_ЛицевыеСчета
	|		ПО ТаблицаПолучателей.КлючДополнительнойИдентификации = КВП_ЛицевыеСчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПолучателей.Получатель,
	|	ТаблицаПолучателей.КлючДополнительнойИдентификации,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаПолучателей.НомерТелефонаЛС = """"
	|				ТОГДА ТаблицаПолучателей.НомерТелефонаЛС
	|			КОГДА ТаблицаПолучателей.Получатель ССЫЛКА Справочник.Контрагенты
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(КонтактнаяИнформацияКонтрагенты.Представление, """") КАК СТРОКА(50))
	|			КОГДА ТаблицаПолучателей.Получатель ССЫЛКА Справочник.ФизическиеЛица
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(КонтактнаяИнформацияФизЛица.Представление, """") КАК СТРОКА(50))
	|			ИНАЧЕ """"
	|		КОНЕЦ) КАК НомерТелефона
	|ИЗ
	|	ТаблицаПолучателей КАК ТаблицаПолучателей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформацияКонтрагенты
	|		ПО (КонтактнаяИнформацияКонтрагенты.Ссылка = ТаблицаПолучателей.Получатель)
	|			И (КонтактнаяИнформацияКонтрагенты.Вид.Наименование = ""Телефон"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК КонтактнаяИнформацияФизЛица
	|		ПО (КонтактнаяИнформацияФизЛица.Ссылка = ТаблицаПолучателей.Получатель)
	|			И (КонтактнаяИнформацияФизЛица.Вид.Наименование = ""Телефон домашний"")
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПолучателей.Получатель,
	|	ТаблицаПолучателей.КлючДополнительнойИдентификации";
	
	// Квартплата -
	
	Возврат ТекстЗапроса;
	

КонецФункции // ПолучитьТекстЗапросаНомеровТелефонов()

// Получает номер телефона контрагента
// Параметры
//  Контрагент - Ссылка - Ссылка на элемент справочника Контрагенты.
//
// Возвращаемое значение:
//   Строка   - Номер телефона контрагента.
//
Функция ПолучитьНомерТелефонаКонтрагента(Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	// Квартплата +
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка КАК Получатель,
	|	ВЫБОР
	|		КОГДА КонтактнаяИнформация.Вид.Наименование = ""Телефон""
	|			ТОГДА ВЫРАЗИТЬ(КонтактнаяИнформация.Представление КАК СТРОКА(50))
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Телефон
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Получатель
	|	И КонтактнаяИнформация.Вид.Наименование = ""Телефон""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка,
	|	ВЫБОР
	|		КОГДА КонтактнаяИнформация.Вид.Наименование = ""Телефон домашний""
	|			ТОГДА ВЫРАЗИТЬ(КонтактнаяИнформация.Представление КАК СТРОКА(50))
	|		ИНАЧЕ """"
	|	КОНЕЦ
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Получатель
	|	И КонтактнаяИнформация.Вид.Наименование = ""Телефон домашний""";
	// Квартплата -
	Запрос.УстановитьПараметр("Получатель", Контрагент);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	НомерТелефона = "";
	
	Если Результат.Количество() > 0 Тогда
		НомерТелефона = Результат[0].Телефон;
	КонецЕсли;
	
	Возврат НомерТелефона;
	
КонецФункции

// Устанавливает дополнительные параметры запроса. Такие ДатаДокумента
//
// Параметры
//  Запрос  - Запрос - Запрос для которого необходимо установить дополнительные параметры
//                 <продолжение описания параметра>
//  ДокументСообщение  - ДокументСсылка.смсСообщение - документ из которого беруться параметры
//                 Так же можно установить необходимые для работы запросов параметры.
//
Процедура УстановитьДополнительныеПараметрыЗапроса(Запрос, ДополнительныеПараметры = Неопределено) Экспорт

	// Дополнительные параметры: дата документа
	Если ДополнительныеПараметры <> Неопределено Тогда
		Запрос.УстановитьПараметр("СформироватьНаДату", ДополнительныеПараметры.ДатаДокумента);
	КонецЕсли; 
	
	// Далее можете добавлять все необходимые параметры необходимые для работы ваших запросов.
	
КонецПроцедуры // УстановитьДополнительныеПараметрыЗапроса()

// Функция определяет доступность функционала СМС рассылки.
//
// Возвращаемое значение:
//   Булево - признак доступности функционала.
//
Функция ФункционалСМСРассылкиДоступен() Экспорт
	
	Возврат Константы.УПЖКХ_ИспользоватьФункционалРассылкиСМС.Получить();
	
КонецФункции

#КонецОбласти