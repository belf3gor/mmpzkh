
#Область СлужебныеПроцедурыИФункции

// Загружает настройку из хранилища общих настроек, как метод платформы Загрузить,
// объектов СтандартноеХранилищеНастроекМенеджер или ХранилищеНастроекМенеджер.<Имя хранилища>.
// 
/// Параметры:
//   КлючОбъекта          - Строка           - см. синтакс-помощник платформы.
// Возвращаемое значение: 
//   Произвольный - см. синтакс-помощник платформы.
//
Функция ХранилищеОбщихНастроекЗагрузить(КлючОбъекта) Экспорт
	
	Результат = Неопределено;
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		Результат = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет настройку в хранилище общих настроек, как метод платформы Сохранить,
// объектов СтандартноеХранилищеНастроекМенеджер или ХранилищеНастроекМенеджер.<Имя хранилища>.
// Если нет права СохранениеДанныхПользователя, сохранение пропускается без ошибки.
//
// Параметры:
//   КлючОбъекта       - Строка           - см. синтакс-помощник платформы.
//   Настройки         - Произвольный     - см. синтакс-помощник платформы.
//
Процедура ХранилищеОбщихНастроекСохранить(КлючОбъекта, Настройки) Экспорт
			
	Если Не ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		Возврат;
	КонецЕсли;
	
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, , Настройки);
	
КонецПроцедуры

#КонецОбласти