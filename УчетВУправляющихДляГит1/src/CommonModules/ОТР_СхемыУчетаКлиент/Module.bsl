
#Область ПроцедурыУправленияНаКлиенте

// Процедура открывает форму со схемой учета для объекта конфигурации.
//
// Параметры:
//  ИмяФормы - Строка - Имя формы, на которой размещена кнопка "Схема учета".
//
Процедура Подключаемый_ОткрытьСхемуУчета(ИмяФормы) Экспорт
	
	СписокСоСхемамиУчетаДляОбъекта = ОТР_СхемыУчетаСервер.ПолучитьСписокСоСхемамиУчетаДляОбъекта(ИмяФормы);
	
	// Если объект участвует в нескольких схемах, то предлагаем выбрать, какую схему учета открыть.
	Если СписокСоСхемамиУчетаДляОбъекта.Количество() > 1 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВыборСхемыУчетаЗавершение", ЭтотОбъект);
		СписокСоСхемамиУчетаДляОбъекта.ПоказатьВыборЭлемента(ОписаниеОповещения, "Выберите схему учета, которую нужно открыть:");
		
	ИначеЕсли СписокСоСхемамиУчетаДляОбъекта.Количество() = 1 Тогда
		
		// Откроем общую форму с выбранной схемой учета. В параметрах передадим имя общего макета,
		// в котором хранится нужная схема учета.
		ОткрытьФорму("ОбщаяФорма.ОТР_СхемыУчета", Новый Структура("ИмяМакета", СписокСоСхемамиУчетаДляОбъекта[0].Значение), , СписокСоСхемамиУчетаДляОбъекта[0].Значение);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик завершения выбора схемы учета.
//
Процедура ПоказатьВыборСхемыУчетаЗавершение(ВыбраннаяСхемаУчета, Параметры) Экспорт
	
	Если Не ВыбраннаяСхемаУчета = Неопределено Тогда
		
		// Откроем общую форму с выбранной схемой учета. В параметрах передадим имя общего макета,
		// в котором хранится нужная схема учета.
		ОткрытьФорму("ОбщаяФорма.ОТР_СхемыУчета", Новый Структура("ИмяМакета", ВыбраннаяСхемаУчета.Значение), , ВыбраннаяСхемаУчета.Значение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти