#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииКомандПодключенныхКОбъекту(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды) Экспорт 
	
	Если НастройкиФормы.ИмяФормы = "Документ.ИсходящаяТранспортнаяОперацияВЕТИС.Форма.ФормаДокумента" Тогда
		
		КомандыПодключенныеКФормеИсходящаяТранспортнаяОперацияВЕТИС(Команды);
		
	КонецЕсли;
	
	Если НастройкиФормы.ИмяФормы = "Документ.ВходящаяТранспортнаяОперацияВЕТИС.Форма.ФормаДокумента" Тогда
		
		КомандыПодключенныеКФормеВходящаяТранспортнаяОперацияВЕТИС(Команды);
		
	КонецЕсли;
	
	Если НастройкиФормы.ИмяФормы = "Документ.ПроизводственнаяОперацияВЕТИС.Форма.ФормаДокумента" Тогда
		
		КомандыПодключенныеКФормеПроизводственнаяОперацияВЕТИС(Команды);
		
	КонецЕсли;
	
	Если НастройкиФормы.ИмяФормы = "Документ.ИнвентаризацияПродукцииВЕТИС.Форма.ФормаДокумента" Тогда
		
		КомандыПодключенныеКФормеИнвентаризацияПродукцииВЕТИС(Команды);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииВидовПодключаемыхКоманд(ВидыПодключаемыхКоманд) Экспорт
	
	Для Каждого ВидКомандВЕТИС Из ПодключаемыеКомандыВЕТИСКлиентСервер.ВидыПодключаемыхКоманд() Цикл
		
		Вид = ВидыПодключаемыхКоманд.Добавить();
		ЗаполнитьЗначенияСвойств(Вид,ВидКомандВЕТИС);
		Вид.ВидГруппыФормы = ВидГруппыФормы.Подменю;
		Вид.Картинка       = Новый Картинка;
		Вид.Отображение    = ОтображениеКнопки.Текст;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриСозданииНаСервере(Форма) Экспорт
	
#Область ОпределениеКомандДляУправленияВидимостью
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма,"ВидимостьПодключаемыхКоманд") = Ложь Тогда 
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВидимостьПодключаемыхКоманд",Новый ОписаниеТипов));
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		Форма.ВидимостьПодключаемыхКоманд = Новый Структура;
	КонецЕсли;
#КонецОбласти
	
#Область ДоработкаПослеБСП
	Для Каждого ВидКомандВЕТИС Из ПодключаемыеКомандыВЕТИСКлиентСервер.ВидыПодключаемыхКоманд() Цикл
		Если Форма.Элементы[ВидКомандВЕТИС.ИмяПодменю].Вид = ВидГруппыФормы.ГруппаКнопок Тогда
			Форма.Элементы[ВидКомандВЕТИС.ИмяПодменю].Вид = ВидГруппыФормы.Подменю;
			Форма.Элементы[ВидКомандВЕТИС.ИмяПодменю].ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
		КонецЕсли;
	КонецЦикла;
#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КомандыПодключенныеКБиблиотечнымОбъектамВЕТИС

Процедура КомандыПодключенныеКФормеИсходящаяТранспортнаяОперацияВЕТИС(Команды)
	
	ДоступныеКоманды = ПодключаемыеКомандыВЕТИСКлиентСервер.КомандыОбъекта("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС");
	ШаблонКоманды = ШаблонКоманды(Новый ОписаниеТипов("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС"));
	
	ЗаполнитьДоступныеКоманды(Команды,ДоступныеКоманды,ШаблонКоманды);
	
КонецПроцедуры

Процедура КомандыПодключенныеКФормеПроизводственнаяОперацияВЕТИС(Команды)
	
	ДоступныеКоманды = ПодключаемыеКомандыВЕТИСКлиентСервер.КомандыОбъекта("ДокументСсылка.ПроизводственнаяОперацияВЕТИС");
	
	ШаблонКоманды = ШаблонКоманды(Новый ОписаниеТипов("ДокументСсылка.ПроизводственнаяОперацияВЕТИС"));

	ЗаполнитьДоступныеКоманды(Команды,ДоступныеКоманды,ШаблонКоманды);
	
КонецПроцедуры

Процедура КомандыПодключенныеКФормеВходящаяТранспортнаяОперацияВЕТИС(Команды)
	
	ДоступныеКоманды = ПодключаемыеКомандыВЕТИСКлиентСервер.КомандыОбъекта("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС");
	ШаблонКоманды = ШаблонКоманды(Новый ОписаниеТипов("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС"));
	
	ЗаполнитьДоступныеКоманды(Команды,ДоступныеКоманды,ШаблонКоманды);
	
КонецПроцедуры

Процедура КомандыПодключенныеКФормеИнвентаризацияПродукцииВЕТИС(Команды)
	
	ДоступныеКоманды = ПодключаемыеКомандыВЕТИСКлиентСервер.КомандыОбъекта("ДокументСсылка.ИнвентаризацияПродукцииВЕТИС");
	
	ШаблонКоманды = ШаблонКоманды(Новый ОписаниеТипов("ДокументСсылка.ИнвентаризацияПродукцииВЕТИС"));

	ЗаполнитьДоступныеКоманды(Команды,ДоступныеКоманды,ШаблонКоманды);
	
КонецПроцедуры


#КонецОбласти

Процедура ЗаполнитьДоступныеКоманды(Команды,ДоступныеКоманды,ШаблонКоманды)
	
	Для Каждого ВидКомандВЕТИС Из ПодключаемыеКомандыВЕТИСКлиентСервер.ВидыПодключаемыхКоманд() Цикл
		
		НомерПоПорядку = 1;
		ШаблонКоманды.Вставить("Вид",        ВидКомандВЕТИС.Имя);
		ШаблонКоманды.Вставить("Обработчик", ВидКомандВЕТИС.Обработчик);
		
		Для Каждого КомандаВЕТИС Из ДоступныеКоманды[ВидКомандВЕТИС.Имя] Цикл 
			
			Если ЕстьПравоДоступа(ВидКомандВЕТИС.Имя,ВидКомандВЕТИС.ТипМетаданных,КомандаВЕТИС.ИмяМетаданных) Тогда 
				Команда = Команды.Добавить();
				ЗаполнитьЗначенияСвойств(Команда, ШаблонКоманды);
				ЗаполнитьЗначенияСвойств(Команда, КомандаВЕТИС);
				Команда.Идентификатор = ВидКомандВЕТИС.Имя + КомандаВЕТИС.ИмяМетаданных;
				Команда.Порядок = НомерПоПорядку;
			КонецЕсли;
			НомерПоПорядку = НомерПоПорядку + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьПравоДоступа(ВидКоманды,ТипМетаданных,ИмяМетаданных) 
	
	Если ВидКоманды = "ОформитьВЕТИС" Тогда
		ИмяПраваДоступа = "Добавление";
	ИначеЕсли ВидКоманды = "ВыбратьВЕТИС" Тогда 
		ИмяПраваДоступа = "Просмотр";
	КонецЕсли;
	
	Возврат ПравоДоступа(ИмяПраваДоступа,Метаданные[ТипМетаданных][ИмяМетаданных]);
	
КонецФункции

Функция ШаблонКоманды(ОписаниеТипов)
	
	Результат = Новый Структура;
	Результат.Вставить("ТипПараметра",             ОписаниеТипов);
	Результат.Вставить("Назначение",               "ДляОбъекта");
	Результат.Вставить("ИзменяетВыбранныеОбъекты", Истина);
	Результат.Вставить("РежимЗаписи",              "Проводить");
	Возврат Результат;
	
КонецФункции

#КонецОбласти