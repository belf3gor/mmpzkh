
#Область ПрограммныйИнтерфейс

// Запросить новый ключ сессии для авторизации в ИС МОТП.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация от имени которой необходимо авторизоваться.
// 	ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения после получения результата.
Процедура ЗапроситьКлючСессии(Организация, ОповещениеПриЗавершении = Неопределено) Экспорт
	
	РезультатЗапроса = ИнтерфейсМОТПВызовСервера.ЗапроситьПараметрыАвторизации();
	
	Если РезультатЗапроса.ПараметрыАвторизации = Неопределено Тогда
		Если ОповещениеПриЗавершении <> Неопределено Тогда
			ВозвращаемоеЗначение = Новый Соответствие;
			ВозвращаемоеЗначение.Вставить(Организация, РезультатЗапроса.ТекстОшибки);
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ВозвращаемоеЗначение);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатЗапроса.ТекстОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Описание = СтрШаблон(
		НСтр("ru = 'Авторизация в ИС МОТП для %1'"),
		Организация);
	
	Сообщения = Новый Массив;
	Сообщения.Добавить(
		ИнтерфейсМОТПСлужебныйКлиент.РезультатПодписания(
			Организация,
			Описание,
			РезультатЗапроса.ПараметрыАвторизации));
	
	Контекст = Новый Структура;
	Контекст.Вставить("Организация",             Организация);
	Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	
	ИнтерфейсМОТПСлужебныйКлиент.Подписать(
		Сообщения,
		Организация,
		Новый ОписаниеОповещения("ПослеПодписания", ИнтерфейсМОТПСлужебныйКлиент, Контекст));
	
КонецПроцедуры

#КонецОбласти