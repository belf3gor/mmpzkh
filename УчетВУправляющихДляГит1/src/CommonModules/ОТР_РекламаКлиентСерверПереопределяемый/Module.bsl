
// Модуль предназначен для внесения изменений в логику обработчика события "ПриСозданииНаСервере" формы,
// в которой необходимо вывести рекламу. Обработчик описан в общем модуле "ОТР_РекламаКлиентСервер".

// Здесь приведен пример обработчиков для ЖКХ 3.0.

#Область ПрограммныйИнтерфейс

// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
// Параметры:
//  Форма	 - УправляемаяФорма - форма.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	Попытка
		
		ДанныеВыводаРекламы = ОТР_РекламаСерверПереопределяемый.ВыполнитьСценарииПроверокПередВыводомРекламы(Форма.ИмяФормы);
		
		Если ДанныеВыводаРекламы.ВыводитьРекламу Тогда
			
			СоздатьОбщуюГруппуОсновныхЭлементовФормы(Форма);
			
			ВывестиЭлементыБаннераНаФорму(Форма, ДанныеВыводаРекламы);
			
		КонецЕсли;
		
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Добавляет элементы баннера на форму.
//
// Параметры:
//  Форма	 - УправляемаяФорма.
//
Процедура СоздатьОбщуюГруппуОсновныхЭлементовФормы(Форма)
	
	Группировка = Форма.Группировка;
	
	// Если настройка группировки корневого элемента на текущей форме установлена в значение "Горизонтальная", тогда:
	Если Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная Тогда
		Возврат;
	КонецЕсли;
		
	// 1. Изменим настройку группировки корневого элемента на "Вертикальная".
	Форма.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	// 2. Добавим группу с группировкой "Горизонтальная" и переместим все основные элементы формы в данную группу.
	ГруппаОсновныхЭлементовФормы = Форма.Элементы.Вставить("ОТР_ГруппаОсновныхЭлементовФормы", Тип("ГруппаФормы"), Форма, Форма.ПодчиненныеЭлементы[0]);
	
	ГруппаОсновныхЭлементовФормы.Вид         = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОсновныхЭлементовФормы.Группировка = ?(Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно,
													ГруппировкаПодчиненныхЭлементовФормы.Вертикальная,
													ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	
	// Пока не останется один элемент (новая группа формы), перемещаем основные элементы формы в новую группу формы.
	Пока Форма.ПодчиненныеЭлементы.Количество() > 1 Цикл
		
		// Перемещаемый элемент берем по индексу 1, так как элемент с индексом 0 - это новая группа форма.
		Форма.Элементы.Переместить(Форма.ПодчиненныеЭлементы[1], ГруппаОсновныхЭлементовФормы);
		
	КонецЦикла;
	
КонецПроцедуры // СоздатьОбщуюГруппуОсновныхЭлементовФормы()

// Добавляет общую группу на форме для основных элементов формы.
//
// Параметры:
//  Форма	 - УправляемаяФорма;
//  ДанныеВыводаРекламы - Структура - см. "ОТР_РекламаСерверПереопределяемый.ВыполнитьСценарииПроверокПередВыводомРекламы()".
//
Процедура ВывестиЭлементыБаннераНаФорму(Форма, ДанныеВыводаРекламы)
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОТР_Баннер", Новый ОписаниеТипов("Строка")));
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Форма.ОТР_Баннер = ДанныеВыводаРекламы.АдресКартинки;
	
	ГруппаБаннера = Форма.Элементы.Добавить("ОТР_ГруппаРеклама", Тип("ГруппаФормы"), Форма);
	ГруппаБаннера.Вид                       = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаБаннера.Группировка               = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаБаннера.ОтображатьЗаголовок       = Ложь;
	ГруппаБаннера.РазрешитьИзменениеСостава = Ложь;
	ГруппаБаннера.ТолькоПросмотр            = Истина;
	ГруппаБаннера.Доступность               = Истина;
	
	КартинкаБаннера = Форма.Элементы.Добавить("ОТР_Баннер", Тип("ПолеФормы"), ГруппаБаннера);
	КартинкаБаннера.Вид                      = ВидПоляФормы.ПолеКартинки;
	КартинкаБаннера.ПутьКДанным              = "ОТР_Баннер";
	КартинкаБаннера.Гиперссылка              = Истина;
	КартинкаБаннера.ТолькоПросмотр           = Истина;
	КартинкаБаннера.Доступность              = Истина;
	КартинкаБаннера.ПоложениеЗаголовка       = ПоложениеЗаголовкаЭлементаФормы.Нет;
	КартинкаБаннера.РазмерКартинки           = РазмерКартинки.Пропорционально;
	КартинкаБаннера.Рамка                    = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	КартинкаБаннера.РастягиватьПоГоризонтали = Ложь;
	КартинкаБаннера.АвтоМаксимальнаяШирина   = Ложь;
	КартинкаБаннера.Ширина                   = 100;
	КартинкаБаннера.Высота                   = 4;
	КартинкаБаннера.ТекстНевыбраннойКартинки = ДанныеВыводаРекламы.Гиперссылка;
	КартинкаБаннера.Подсказка                = ДанныеВыводаРекламы.Подсказка;
	КартинкаБаннера.Заголовок                = ДанныеВыводаРекламы.Сервис;
	КартинкаБаннера.РастягиватьПоВертикали   = Ложь;
	КартинкаБаннера.УстановитьДействие("Нажатие", "Подключаемый_РекламаОткрытьСтраницуСайта");
	
	КартникаЗакрытияБаннера = Форма.Элементы.Добавить("ОТР_КартникаЗакрытияБаннера", Тип("ДекорацияФормы"), ГруппаБаннера);
	КартникаЗакрытияБаннера.Вид            = ВидДекорацииФормы.Картинка;
	КартникаЗакрытияБаннера.Картинка       = БиблиотекаКартинок.Закрыть;
	КартникаЗакрытияБаннера.Гиперссылка    = Истина;
	КартникаЗакрытияБаннера.Доступность    = Истина;
	КартникаЗакрытияБаннера.УстановитьДействие("Нажатие", "Подключаемый_РекламаКартникаЗакрытияБаннераНажатие");
	
КонецПроцедуры // ВывестиЭлементыБаннераНаФорму()

// Возвращает текст гиперссылки основной страницы сайта, посвященный программе.
// 
// Возвращаемое значение:
//  Строка.
//
Функция ГиперссылкаОсновнойСтраницыСайта() Экспорт
	
	Возврат "https://vgkh.ru/jsk/";
	
КонецФункции // ГиперссылкаОсновнойСтраницыСайта()

#КонецОбласти // ПрограммныйИнтерфейс