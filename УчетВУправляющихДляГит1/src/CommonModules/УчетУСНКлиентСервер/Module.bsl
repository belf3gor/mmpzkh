////////////////////////////////////////////////////////////////////////////////
// УчетУСНКлиентСервер: процедуры и функции для отражения документов в учете УСН,
// которые могут вызываться как на клиенте в формах документов, так и на сервере
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

///////////////////////////////////////////////////////////////////////////////
// Заполнение данных для УСН в формах документов

// Структура параметров для процедур и функций отражения документов в УСН
//
Функция ПараметрыНастройкиУСН() Экспорт

	Параметры = Новый Структура;
	
	// обязательные
	Параметры.Вставить("ПрименениеУСН",       Ложь);
	Параметры.Вставить("ПрименениеУСНДоходы", Ложь);
	
	// общие - заполняются из свойств объекта и реквизитов формы
	Параметры.Вставить("Дата");
	Параметры.Вставить("ВидОперации");
	Параметры.Вставить("Налог");
	Параметры.Вставить("НалоговыйПериод");
	Параметры.Вставить("Контрагент");
	Параметры.Вставить("ДоговорКонтрагента");
	Параметры.Вставить("СчетУчетаРасчетовСКонтрагентом");
	Параметры.Вставить("ПринятоОт", "");
	Параметры.Вставить("Выдано", "");
	Параметры.Вставить("ВалютаДокумента");
	Параметры.Вставить("СтатьяДвиженияДенежныхСредств");
	Параметры.Вставить("СубконтоДт1");
	Параметры.Вставить("СубконтоКт1");
	Параметры.Вставить("ВалютаРегламентированногоУчета");
	Параметры.Вставить("КурсДокумента", 1);
	Параметры.Вставить("КратностьДокумента", 1);
	
	// специфичные - заполняются явным образом по месту использования
	Параметры.Вставить("НТТПоПродажнымЦенам", Ложь);
	Параметры.Вставить("НТТНаЕНВД",           Ложь);
	
	Параметры.Вставить("ОбязательныеПараметры", "ПрименениеУСН, ПрименениеУСНДоходы");
	
	Возврат Параметры;

КонецФункции

// Возвращает структуру параметров для настройки отражения документа в УСН
// значениями из реквизитов переданной формы и объекта
//
Функция ПараметрыФормыДокументаДляУСН(Форма) Экспорт

	Параметры = ПараметрыНастройкиУСН();
	
	ЗаполнитьЗначенияСвойств(Параметры, Форма); // Параметры из ФО формы
	
	Если НЕ Параметры.ПрименениеУСН Тогда
		Возврат Параметры;
	КонецЕсли; 
	
	Объект = Форма.Объект;
	
	ЗаполнитьЗначенияСвойств(Параметры, Форма.Объект); // Параметры из реквизитов объекта
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.РозничнаяВыручка") Тогда
		Параметры.НТТПоПродажнымЦенам = Объект.ВыручкаСНТТ И Форма.УчетВПродажныхЦенах;
		Параметры.НТТНаЕНВД           = Форма.НТТНаЕНВД;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка") Тогда
		Параметры.НТТПоПродажнымЦенам = Форма.УчетВПродажныхЦенах;
		Параметры.НТТНаЕНВД           = Форма.НТТНаЕНВД;
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Получает строковое представление порядка отражения аванса в документах поступления денежных средств
// для отображения на форме в ТЧ "Расшифровка платежа"
//
// Параметры:
//  ЗначениеПоиска      - <ПеречислениеСсылка.ПорядокОтраженияАванса|СправочникСсылка.Патенты> -
//                      значение порядка отображения аванса, сохраняемое в данных объекта
//                 
//  СоответствиеПоиска  - <ФиксированноеСоответствие> - соответствие значений и представлений порядка отражения аванса,
//                      кэшируется в реквизитах формы
//
//  СписокПредставлений  - <СписокЗначений> - список выбора поля, в которое выводится строковое представление
//                      текущего порядка отражения аванса
//
Функция ПредставлениеПорядкаОтраженияАванса(ЗначениеПоиска, СоответствиеПоиска, СписокПредставлений) Экспорт
	
	Представление = "";
	
	Если ЗначениеЗаполнено(СоответствиеПоиска) Тогда
		Для каждого КлючЗначение Из СоответствиеПоиска Цикл
			Если КлючЗначение.Значение = ЗначениеПоиска Тогда
				ЭлементСписка = СписокПредставлений.Получить(КлючЗначение.Ключ);
				Если ЭлементСписка <> Неопределено Тогда
					Представление = ЭлементСписка.Значение;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Возвращает ставки налога УСН, указанные в НК РФ.
// Ставка УСН доходы - п. 1 ст. 346.20 НК РФ.
// Ставка УСН доходы минус расходы - п. 2 ст. 346.20 НК РФ.
// Ставка минимального налога УСН доходы минус расходы - п. 6 ст. 346.18 НК РФ.
//
// Возвращаемое значение:
//  Структура - ставки налогов.
//    * СтавкаУСНДоходы - Число.
//    * СтавкаУСНДоходыМинусРасходы - Число.
//    * СтавкаМинимальногоНалогаУСНДоходыМинусРасходы - Число.
//
Функция НалоговыеСтавкиПоУмолчанию(Период = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СтавкаУСНДоходы", 6);
	Результат.Вставить("СтавкаУСНДоходыМинусРасходы", 15);
	Результат.Вставить("СтавкаМинимальногоНалогаУСНДоходыМинусРасходы", 1);
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеПериодаУплатыНалога(Период) Экспорт
	
	ТекстПериода = ПредставлениеПериода(НачалоГода(Период), КонецКвартала(Период), "ФП=Истина");
	
	ЭтоУплатаЗаГод = (КонецКвартала(Период) = КонецГода(Период));
	
	// В русскоязычном представлении оставим год только для годового платежа.
	Если ЭтоУплатаЗаГод Тогда
		ТекстПериода = СтрЗаменить(ТекстПериода, НСтр("ru = 'г.'"), НСтр("ru = 'год'"));
	Иначе
		ТекстПериода = СтрЗаменить(ТекстПериода, НСтр("ru = 'г.'"), "");
	КонецЕсли;
	
	Возврат СокрЛП(ТекстПериода);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Патентная система налогообложения

// Возвращает налоговую ставку по патентной системе налогообложения
//
Функция НалоговаяСтавкаПоПатентнойСистеме(Знач Период = Неопределено) Экспорт 
	
	Возврат 6/100;
	
КонецФункции

// Рассчитывает потенциально возможный доход по патенту
//
// Параметры:
//  ПотенциальноВозможныйГодовойДоход - Число - потенциально возможный к получению годовой доход
//  ДатаНачала - Дата - дата начала действия патента
//  ДатаОкончания - Дата - дата окончания действия патента
//
Функция РассчитатьПотенциальноВозможныйДоход(ПотенциальноВозможныйГодовойДоход, ДатаНачала, ДатаОкончания) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Или НЕ ЗначениеЗаполнено(ДатаОкончания) Или Год(ДатаНачала) <> Год(ДатаОкончания) Тогда
		Возврат ПотенциальноВозможныйГодовойДоход;
	КонецЕсли;
	
	ДействуетСНачалаМесяца = (НачалоДня(ДатаНачала) = НачалоМесяца(ДатаНачала));
	ДействуетДоКонцаМесяца = (КонецДня(ДатаОкончания) = КонецМесяца(ДатаОкончания));
	ДействуетДоКонцаГода   = (КонецДня(ДатаОкончания) = КонецГода(ДатаОкончания));
	
	Если (ДействуетСНачалаМесяца И ДействуетДоКонцаМесяца) Или ДействуетДоКонцаГода Тогда
		
		ЧислоМесяцев = Месяц(ДатаОкончания) - Месяц(ДатаНачала) + 1;
		ПотенциальноВозможныйДоход = ПотенциальноВозможныйГодовойДоход / 12 * ЧислоМесяцев;
		
	Иначе
		
		ОдинДень = 86400;
		
		РасчетнаяДатаОкончания = ДобавитьМесяц(ДатаНачала, Месяц(ДатаОкончания) - Месяц(ДатаНачала)) - ОдинДень;
		
		Если НачалоДня(ДатаОкончания) = НачалоДня(РасчетнаяДатаОкончания) Тогда
			
			// Сроком действия патента является период в пределах одного календарного года, начинающийся с любого числа месяца,
			// указанного ИП в заявлении на получение патента, и истекающий в соответствующее число последнего месяца срока.
			
			ЧислоМесяцев = Месяц(ДатаОкончания) - Месяц(ДатаНачала);
			ПотенциальноВозможныйДоход = ПотенциальноВозможныйГодовойДоход / 12 * ЧислоМесяцев;
			
		Иначе
			
			// В случае прекращения осуществления предпринимательской деятельности до истечения срока действия патента величина налога,
			// подлежит перерасчету исходя из фактического периода времени осуществления предпринимательской деятельности
			// в календарных днях.
			
			ЧислоДней = (НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала)) / ОдинДень + 1;
			
			Февраль = Дата(Год(ДатаНачала), 2, 1);
			ДнейВФеврале = День(КонецМесяца(Февраль));
			ДнейВГоду = (365 - 28) + ДнейВФеврале;
			
			ПотенциальноВозможныйДоход = ПотенциальноВозможныйГодовойДоход / ДнейВГоду * ЧислоДней;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Окр(ПотенциальноВозможныйДоход, 2, РежимОкругления.Окр15как20);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Формирование Книги учета доходов и расходов

Функция ДатаНачалаФормирования4РазделаКУДиР() Экспорт
	
	Возврат Дата(2013, 1, 1);
	
КонецФункции

// Дата, с которой требуется формировать раздел V "Уплаченный торговый сбор" Книги учета доходов и расходов.
//
Функция ДатаНачалаФормирования5РазделаКУДиР() Экспорт
	
	// Приказ Минфина РФ от 07.12.2016 N 227н.
	Возврат Дата(2018, 1, 1);
	
КонецФункции

#КонецОбласти

