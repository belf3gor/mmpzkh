
#Область ОбращениеВТехническуюПоддержку

// Возвращает текстовый докумнет с техической информацией программы.
Функция ПолучитьТехническуюИнформацию(ПараметрыТехническойИнформации) Экспорт
	
	ТехническаяИнформация = Новый ТекстовыйДокумент;
	
	ТехническаяИнформация.ДобавитьСтроку(ТекстВложенияТехническаяИнформация(ПараметрыТехническойИнформации));
	
	Возврат ТехническаяИнформация;
	
КонецФункции

// Возвращает текстовый документ с текущими настройками программы.
Функция ПолучитьНастройкиПрограммы() Экспорт
	
	НастройкиПрограммы = Новый ТекстовыйДокумент;
	
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	ДобавитьСекциюНастройкаПрограммыЖКХ(НастройкиПрограммы);
	ДобавитьСекциюНастройкаПараметровУчетаЖКХ(НастройкиПрограммы);
	ДобавитьСекциюУчетнаяПолитикаЖКХ(НастройкиПрограммы, Организация);
	ДобавитьСекциюНастройкиФормированияПлатежногоДокумента(НастройкиПрограммы, Организация);
	ДобавитьСекциюНастройкиУчетаПоКапремонту(НастройкиПрограммы, Организация);
	ДобавитьСекциюНастройкаПараметровСистемыЛицензирования(НастройкиПрограммы);
	
	Возврат НастройкиПрограммы;
	
КонецФункции

#Область ТехническаяИнформация

// Возвращает текст с технической информацией программы.
Функция ТекстВложенияТехническаяИнформация(Параметры)

	// Общие технические параметры и информация о сеансе
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ЭтоФайловаяИБ = УПЖКХ_ТиповыеМетодыСервер.ИнформационнаяБазаФайловая();
	
	// Общая информация:
	Результат = НСтр("ru = 'Техническая информация о программе:
		|Название программы: %1
		|Имя программы: %2;
		|Версия программы: %3;
		|Поставщик: %4;
		|Версия платформы: %5;
		|%6;
		|Вид приложения: %7;
		|Режим: %8;'");
		
	ВерсииПодсистем = Новый Массив;
	Для Каждого СтрокаВерсии Из УПЖКХ_ТиповыеМетодыСервер.ВерсииПодсистем() Цикл
		ВерсииПодсистем.Добавить(
			СтрШаблон(НСтр("ru='Версия %1: %2'"), 
				СтрокаВерсии.ИмяПодсистемы,
				СтрокаВерсии.Версия));
	КонецЦикла;
	Результат =
		УПЖКХ_ТиповыеМетодыКлиентСервер.ПодставитьПараметрыВСтроку(
			Результат,
			Метаданные.Синоним,
			Метаданные.Имя,
			Метаданные.Версия,
			Метаданные.Поставщик,
			СистемнаяИнформация.ВерсияПриложения,
			СтрСоединить(ВерсииПодсистем, Символы.ПС),
			Параметры.ВидПриложения,
			?(ЭтоФайловаяИБ, НСтр("ru = 'Файловый'"), НСтр("ru = 'Серверный'")));
			
	// Права:
	Результат = Результат
		+ Символы.ПС
		+ УПЖКХ_ТиповыеМетодыКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Полные права: %1;
				|Права администратора: %2;'"),
			?(Пользователи.ЭтоПолноправныйПользователь(, Ложь, Ложь), НСтр("ru = 'есть'"), НСтр("ru = 'нет'")),
			?(Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь), НСтр("ru = 'есть'"), НСтр("ru = 'нет'")));
	
	// Настройки соединения:
	НастройкиСоединения = УПЖКХ_ТиповыеМетодыКлиентСервер.НастройкиСоединенияССерверами();
	Результат = Результат
		+ Символы.ПС
		+ УПЖКХ_ТиповыеМетодыКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройки соединения с серверами Интернет-поддержки:
				|	Таймаут: %1;
				|	Устанавливать соединение на сервере 1С:Предприятие: %2;
				|	Доменная зона: %3'"),
			НастройкиСоединения.ТаймаутПодключения,
			?(ЭтоФайловаяИБ,
				Ложь,
				(НастройкиСоединения.УстанавливатьПодключениеНаСервере = 1)),
			?(НастройкиСоединения.ДоменРасположенияСерверовИПП = 1, "1c.eu", "1c.ru"));
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область НастройкиПрограммы

// Добавляет в текстовый документ секцию настроек программы ЖКХ.
Процедура ДобавитьСекциюНастройкаПрограммыЖКХ(НастройкиПрограммы) Экспорт
	
	ИменаКонстант = МассивИменКонстантНастройкаПрограммыЖКХ();
	НаборКонстант = Константы.СоздатьНабор(ИменаКонстант);
	НаборКонстант.Прочитать();
	
	Если Не НастройкиПрограммы.ПолучитьТекст() = "" Тогда
		НастройкиПрограммы.ДобавитьСтроку("");
	КонецЕсли;
	
	НастройкиПрограммы.ДобавитьСтроку("<Cекция Настройка программы ЖКХ>");
	
	Для Каждого ИмяКонстанты Из ИменаКонстант Цикл
		НастройкиПрограммы.ДобавитьСтроку(СтрШаблон("%1=%2", ИмяКонстанты, НаборКонстант[ИмяКонстанты]));
	КонецЦикла;
	
	НастройкиПрограммы.ДобавитьСтроку("<КонецCекции>");
	
КонецПроцедуры

// Добавляет в текстовый документ секцию настроек параметров учета ЖКХ.
Процедура ДобавитьСекциюНастройкаПараметровУчетаЖКХ(НастройкиПрограммы) Экспорт
	
	ИменаКонстант = МассивИменКонстантНастройкаПараметровУчетаЖКХ();
	НаборКонстант = Константы.СоздатьНабор(ИменаКонстант);
	НаборКонстант.Прочитать();
	
	НастройкиПрограммы.ДобавитьСтроку("");
	НастройкиПрограммы.ДобавитьСтроку("<Cекция Настройка параметров учета ЖКХ>");
	
	Для Каждого ИмяКонстанты Из ИменаКонстант Цикл
		НастройкиПрограммы.ДобавитьСтроку(СтрШаблон("%1=%2", ИмяКонстанты, НаборКонстант[ИмяКонстанты]));
	КонецЦикла;
	
	НастройкиПрограммы.ДобавитьСтроку("<КонецCекции>");
	
КонецПроцедуры

// Добавляет в текстовый документ секцию настроек учетной политики ЖКХ.
Процедура ДобавитьСекциюУчетнаяПолитикаЖКХ(НастройкиПрограммы, Организация) Экспорт
	
	НастройкиПрограммы.ДобавитьСтроку("");
	НастройкиПрограммы.ДобавитьСтроку("<Cекция Учетная политика ЖКХ>");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.Период КАК Период,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.Организация КАК Организация,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ЧислоВнесенияПлатежа КАК ЧислоВнесенияПлатежа,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.АдресСайтаКомпании КАК АдресСайтаКомпании,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ВариантОплатыПени КАК ВариантОплатыПени,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ВариантРаспределенияОплат КАК ВариантРаспределенияОплат,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ВариантРеализацииТоваров КАК ВариантРеализацииТоваров,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ВидСчетаУчетаУслугиДляОтраженияВРеглУчете КАК ВидСчетаУчетаУслугиДляОтраженияВРеглУчете,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ВыполнятьПереносВРамкахОдногоМесяцаНачисления КАК ВыполнятьПереносВРамкахОдногоМесяцаНачисления,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ДоляСтавкиРефинансированияЦБЗнаменатель КАК ДоляСтавкиРефинансированияЦБЗнаменатель,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ДоляСтавкиРефинансированияЦБЧислитель КАК ДоляСтавкиРефинансированияЦБЧислитель,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ДоходыКУДиРОтНачисленияПениВсегдаСчитатьАвтоматически КАК ДоходыКУДиРОтНачисленияПениВсегдаСчитатьАвтоматически,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ИспользоватьЛьготныйТариф КАК ИспользоватьЛьготныйТариф,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ИспользоватьПроцентнуюСтавку КАК ИспользоватьПроцентнуюСтавку,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ИспользоватьРучнойФорматШтрихкода КАК ИспользоватьРучнойФорматШтрихкода,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ИспользоватьУслугиИсключенияПриРасчетеПени КАК ИспользоватьУслугиИсключенияПриРасчетеПени,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ИсточникВзносыТСЖ КАК ИсточникВзносыТСЖ,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.КодУслугиДляШтрихКода КАК КодУслугиДляШтрихКода,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.КоличествоПериодовНачисления КАК КоличествоПериодовНачисления,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.НастройкаРасчетаПени КАК НастройкаРасчетаПени,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.НеУчитыватьЛьготыВоВзаиморасчетах КАК НеУчитыватьЛьготыВоВзаиморасчетах,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОбнулятьДоходыВзносыПринимаемые КАК ОбнулятьДоходыВзносыПринимаемые,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОбнулятьДоходыВсего КАК ОбнулятьДоходыВсего,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОбнулятьДоходыПринимаемые КАК ОбнулятьДоходыПринимаемые,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОрганРегистрационногоУчета КАК ОрганРегистрационногоУчета,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОтветственноеЛицоПаспортногоСтола КАК ОтветственноеЛицоПаспортногоСтола,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОтражатьНачисленияВРеглУчете КАК ОтражатьНачисленияВРеглУчете,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ОтражениеОплатыВРазрезеУслуг КАК ОтражениеОплатыВРазрезеУслуг,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ПредседательТСЖ КАК ПредседательТСЖ,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ПроцентБанкаЗаУслуги КАК ПроцентБанкаЗаУслуги,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ПроцентнаяСтавкаПени КАК ПроцентнаяСтавкаПени,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ГодоваяПроцентнаяСтавкаРассрочка КАК ГодоваяПроцентнаяСтавкаРассрочка,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.РазрешитьПереносАвансаПоНачислениямНаПогашениеЗадолженностиПоПени КАК РазрешитьПереносАвансаПоНачислениямНаПогашениеЗадолженностиПоПени,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.РазрешитьПереносАвансовНаДолгиПоВсемУслугам КАК РазрешитьПереносАвансовНаДолгиПоВсемУслугам,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.РазрешитьУстановкуСчетчикаНаНесколькоЛС КАК РазрешитьУстановкуСчетчикаНаНесколькоЛС,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.РаспределятьАвансПропорциональноНачислениям КАК РаспределятьАвансПропорциональноНачислениям,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.РассчитыватьЛьготуПоВременноОтсутствующим КАК РассчитыватьЛьготуПоВременноОтсутствующим,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.РегистрацияОплатыПоШКНаУказанныйМесяц КАК РегистрацияОплатыПоШКНаУказанныйМесяц,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.СпособОтраженияЛьгот КАК СпособОтраженияЛьгот,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ГруппыУслугДляРаспределенияАвансовПриЗакрытииПериода КАК ГруппыУслугДляРаспределенияАвансовПриЗакрытииПериода,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.УслугиНеПодлежащиеПерерасчету КАК УслугиНеПодлежащиеПерерасчету,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.УчитыватьВступительныеВзносыТСЖ КАК УчитыватьВступительныеВзносыТСЖ,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.УчитыватьПериодыСРазнымКолвомПроживающих КАК УчитыватьПериодыСРазнымКолвомПроживающих,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ФормироватьСФУСН КАК ФормироватьСФУСН,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ФормироватьТиповыеДокументыСОтрицательнымиСуммами КАК ФормироватьТиповыеДокументыСОтрицательнымиСуммами,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.УдалитьВариантОтраженияВзаиморасчетовПоКапремонту КАК УдалитьВариантОтраженияВзаиморасчетовПоКапремонту,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.СчетУчетаВзаиморасчетовПоКапремонту КАК СчетУчетаВзаиморасчетовПоКапремонту,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.ВариантОтраженияВзаиморасчетовПоКапремонту КАК ВариантОтраженияВзаиморасчетовПоКапремонту,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.СчетаУчетаДляОтраженияПени КАК СчетаУчетаДляОтраженияПени,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.СчетаУчетаДляОтраженияПениКапремонт КАК СчетаУчетаДляОтраженияПениКапремонт,
		|	КВП_УчетнаяПолитикаТСЖСрезПоследних.СтавкаНДСДляОтраженияПени КАК СтавкаНДСДляОтраженияПени
		|ИЗ
		|	РегистрСведений.КВП_УчетнаяПолитикаТСЖ.СрезПоследних(, Организация = &Организация) КАК КВП_УчетнаяПолитикаТСЖСрезПоследних";
		
		УстановитьПривилегированныйРежим(Истина);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ТаблицаУчетнаяПолитикаЖКХ = РезультатЗапроса.Выгрузить();
			
			СтрокаТаблицы = ТаблицаУчетнаяПолитикаЖКХ[0];
			
			Для Каждого Колонка Из ТаблицаУчетнаяПолитикаЖКХ.Колонки Цикл
				ИмяКолонки = Колонка.Имя;
				НастройкиПрограммы.ДобавитьСтроку(СтрШаблон("%1=%2", ИмяКолонки, СтрокаТаблицы[ИмяКолонки]));
			КонецЦикла;
			
		КонецЕсли;
		
		НастройкиПрограммы.ДобавитьСтроку("<КонецCекции>");
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет в текстовый документ секцию настроек формирования платежного документа.
Процедура ДобавитьСекциюНастройкиФормированияПлатежногоДокумента(НастройкиПрограммы, Организация) Экспорт
	
	НастройкиПрограммы.ДобавитьСтроку("");
	НастройкиПрограммы.ДобавитьСтроку("<Cекция Настройки формирования платежного документа>");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.Организация КАК Организация,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВариантОпределенияФорматаШтрихкода КАК ВариантОпределенияФорматаШтрихкода,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВидШтрихкода КАК ВидШтрихкода,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВнешняяОбработкаШтрихкодаQRКР КАК ВнешняяОбработкаШтрихкодаQRКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВнешняяОбработкаШтрихкодаQRОсн КАК ВнешняяОбработкаШтрихкодаQRОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВнешняяОбработкаШтрихкодаКР КАК ВнешняяОбработкаШтрихкодаКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВнешняяОбработкаШтрихкодаОсн КАК ВнешняяОбработкаШтрихкодаОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВыводитьQRштрихкодДляСитикардПоОсн КАК ВыводитьQRштрихкодДляСитикардПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ДлинаИдентификатораШтрихкода КАК ДлинаИдентификатораШтрихкода,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ДлинаСуммыШтрихкода КАК ДлинаСуммыШтрихкода,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ДополнительныеКонтактыОрганизацииОсн КАК ДополнительныеКонтактыОрганизацииОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ДополнительныеКонтактыОрганизацииКР КАК ДополнительныеКонтактыОрганизацииКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ДополнительныеРеквизитыQRКодаПоКР КАК ДополнительныеРеквизитыQRКодаПоКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ДополнительныеРеквизитыQRКодаПоОсн КАК ДополнительныеРеквизитыQRКодаПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ЗаголовокКР КАК ЗаголовокКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ЗаголовокОсн КАК ЗаголовокОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ИдентификаторЛСШтрихкода КАК ИдентификаторЛСШтрихкода,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ИсключитьИзАдресаПомещенияСимволыНомерИТочка КАК ИсключитьИзАдресаПомещенияСимволыНомерИТочка,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ИспользоватьСтандартныйЗаголовок КАК ИспользоватьСтандартныйЗаголовок,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодОрганизацииВСитикардПоОсн КАК КодОрганизацииВСитикардПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодОрганизацииДляОдномерногоШКПоОсн КАК КодОрганизацииДляОдномерногоШКПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодОрганизацииДляОдномерногоШКПоУслугамКР КАК КодОрганизацииДляОдномерногоШКПоУслугамКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодОрганизацииДляQRКодаПоОсн КАК КодОрганизацииДляQRКодаПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодОрганизацииДляQRКодаПоУслугамКР КАК КодОрганизацииДляQRКодаПоУслугамКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодУслугиДляШтрихКодаПоОсн КАК КодУслугиДляШтрихКодаПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодУслугиДляШтрихКодаПоКР КАК КодУслугиДляШтрихКодаПоКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КонстантаОрганизацииДляШтрихКода КАК КонстантаОрганизацииДляШтрихКода,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ОтображатьКонтрольнуюСуммуПоКР КАК ОтображатьКонтрольнуюСуммуПоКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ОтображатьКонтрольнуюСуммуПоОсн КАК ОтображатьКонтрольнуюСуммуПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ПримечаниеКвитанцииОсн КАК ПримечаниеКвитанцииОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ПримечаниеКвитанцииКР КАК ПримечаниеКвитанцииКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ПредставлениеУслугСОИ КАК ПредставлениеУслугСОИ,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.РазделительДробнойЧастиСуммы КАК РазделительДробнойЧастиСуммы,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.СоставШтрихкодаПоКР КАК СоставШтрихкодаПоКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.СоставШтрихкодаПоОсн КАК СоставШтрихкодаПоОсн,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.СхемаДляРасчетаКонтрольнойСуммы КАК СхемаДляРасчетаКонтрольнойСуммы,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ФормаКвитанции КАК ФормаКвитанции,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ФорматКодировкиТекстаQR КАК ФорматКодировкиТекстаQR,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ФорматПериодаОплаты КАК ФорматПериодаОплаты,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.ВыводитьQRштрихкодДляСитикардПоКР КАК ВыводитьQRштрихкодДляСитикардПоКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.КодОрганизацииВСитикардПоКр КАК КодОрганизацииВСитикардПоКр,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.НаименованиеПлатежаНазначениеПоКР КАК НаименованиеПлатежаНазначениеПоКР,
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.НаименованиеПлатежаНазначениеПоОсн КАК НаименованиеПлатежаНазначениеПоОсн
		|ИЗ
		|	РегистрСведений.УПЖКХ_НастройкиФормированияПлатежногоДокумента КАК УПЖКХ_НастройкиФормированияПлатежногоДокумента
		|ГДЕ
		|	УПЖКХ_НастройкиФормированияПлатежногоДокумента.Организация = &Организация";
		
		УстановитьПривилегированныйРежим(Истина);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ТаблицаНастройкиФормированияПД = РезультатЗапроса.Выгрузить();
			
			СтрокаТаблицы = ТаблицаНастройкиФормированияПД[0];
			
			Для Каждого Колонка Из ТаблицаНастройкиФормированияПД.Колонки Цикл
				ИмяКолонки = Колонка.Имя;
				НастройкиПрограммы.ДобавитьСтроку(СтрШаблон("%1=%2", ИмяКолонки, СтрокаТаблицы[ИмяКолонки]));
			КонецЦикла;
			
		КонецЕсли;
		
		НастройкиПрограммы.ДобавитьСтроку("<КонецCекции>");
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет в текстовый документ секцию настроек учета по капремонту.
Процедура ДобавитьСекциюНастройкиУчетаПоКапремонту(НастройкиПрограммы, Организация) Экспорт
	
	НастройкиПрограммы.ДобавитьСтроку("");
	НастройкиПрограммы.ДобавитьСтроку("<Cекция Настройки учета по капремонту>");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	УПЖКХ_НастройкиУчетаКапремонта.Ссылка КАК Ссылка,
		|	УПЖКХ_НастройкиУчетаКапремонта.ВерсияДанных КАК ВерсияДанных,
		|	УПЖКХ_НастройкиУчетаКапремонта.ПометкаУдаления КАК ПометкаУдаления,
		|	УПЖКХ_НастройкиУчетаКапремонта.Владелец КАК Владелец,
		|	УПЖКХ_НастройкиУчетаКапремонта.Наименование КАК Наименование,
		|	УПЖКХ_НастройкиУчетаКапремонта.ВестиРаздельныйУчетПоУслугамКапремонта КАК ВестиРаздельныйУчетПоУслугамКапремонта,
		|	УПЖКХ_НастройкиУчетаКапремонта.ЗапретитьВзаимозачетАвансовИДолговМеждуУслугамиКРиОсновнымиУслугами КАК ЗапретитьВзаимозачетАвансовИДолговМеждуУслугамиКРиОсновнымиУслугами,
		|	УПЖКХ_НастройкиУчетаКапремонта.НастройкаРасчетаПени КАК НастройкаРасчетаПени,
		|	УПЖКХ_НастройкиУчетаКапремонта.Предопределенный КАК Предопределенный,
		|	УПЖКХ_НастройкиУчетаКапремонта.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
		|	УПЖКХ_НастройкиУчетаКапремонта.Представление КАК Представление
		|ИЗ
		|	Справочник.УПЖКХ_НастройкиУчетаКапремонта КАК УПЖКХ_НастройкиУчетаКапремонта
		|ГДЕ
		|	УПЖКХ_НастройкиУчетаКапремонта.Владелец = &Организация";
		
		УстановитьПривилегированныйРежим(Истина);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ТаблицаНастройкиУчетаПоКапремонту = РезультатЗапроса.Выгрузить();
			
			СтрокаТаблицы = ТаблицаНастройкиУчетаПоКапремонту[0];
			
			Для Каждого Колонка Из ТаблицаНастройкиУчетаПоКапремонту.Колонки Цикл
				ИмяКолонки = Колонка.Имя;
				НастройкиПрограммы.ДобавитьСтроку(СтрШаблон("%1=%2", ИмяКолонки, СтрокаТаблицы[ИмяКолонки]));
			КонецЦикла;
			
		КонецЕсли;
		
		НастройкиПрограммы.ДобавитьСтроку("<КонецCекции>");
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет в текстовый документ секцию настроек параметров сервера лицензирования.
Процедура ДобавитьСекциюНастройкаПараметровСистемыЛицензирования(НастройкиПрограммы) Экспорт
	
	ИменаКонстант = МассивИменКонстантНастройкаПараметровСистемыЛицензирования();
	НаборКонстант = Константы.СоздатьНабор(ИменаКонстант);
	НаборКонстант.Прочитать();
	
	НастройкиПрограммы.ДобавитьСтроку("");
	НастройкиПрограммы.ДобавитьСтроку("<Cекция Настройка параметров системы лицензирования>");
	
	Для Каждого ИмяКонстанты Из ИменаКонстант Цикл
		НастройкиПрограммы.ДобавитьСтроку(СтрШаблон("%1=%2", ИмяКонстанты, НаборКонстант[ИмяКонстанты]));
	КонецЦикла;
	
	НастройкиПрограммы.ДобавитьСтроку("<КонецCекции>");

КонецПроцедуры

// Формирует массив имен констант, используемых для настройки программы ЖКХ.
//
Функция МассивИменКонстантНастройкаПрограммыЖКХ()
	
	МассивИменКонстант = Новый Массив;
	МассивИменКонстант.Добавить("ИспользоватьАвтономнуюРаботуВМоделиСервиса");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьМногопоточность");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьОграничениеДоступаПоОбъектамУчета");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьРежимОтладки");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьФункционалВЕЖ");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьФункционалВыгрузкиВГЦЖС");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьФункционалТСЖ");
	МассивИменКонстант.Добавить("УПЖКХ_КоличествоПотоковПриМногопоточнойОбработкеДанных");
	МассивИменКонстант.Добавить("УПЖКХ_НастройкаНижегородскаяОбласть");
	
	Возврат МассивИменКонстант;
	
КонецФункции

// Формирует массив имен констант, используемых для настройки параметров учета ЖКХ.
//
Функция МассивИменКонстантНастройкаПараметровУчетаЖКХ()
	
	МассивИменКонстант = Новый Массив;
	МассивИменКонстант.Добавить("УПЖКХ_АвтоматическиПересчитыватьПлощадиЗданий");
	МассивИменКонстант.Добавить("УПЖКХ_ВариантИзмененияЗадолженностиВКорректировкахНачислений");
	МассивИменКонстант.Добавить("УПЖКХ_ДатаНачалаУчетаСторноИндивидуальныхНачисленийПриРасчетеОДН");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьКонтрольПоказанийПриборовУчета");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьНовыйАлгоритмРасчетаЛьгот");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьПовышающиеКоэффициентыПриОтсутствииПУ");
	МассивИменКонстант.Добавить("УПЖКХ_ИспользоватьРазличныеПлощадиМОПДляУслуг");
	МассивИменКонстант.Добавить("УПЖКХ_ПечататьЧекиПриОплатеБанковскойКартой");
	МассивИменКонстант.Добавить("УПЖКХ_ПечататьЧекиСРасшифровкойПоУслугам");
	МассивИменКонстант.Добавить("УПЖКХ_РазмерДопустимогоПревышенияСреднегоРасходаПУ");
	МассивИменКонстант.Добавить("УПЖКХ_СпособОпределенияСреднегоРасходаПоПриборамУчета");
	МассивИменКонстант.Добавить("УПЖКХ_СторнированиеПриНачисленииВРазрезеСчетчиков");
	МассивИменКонстант.Добавить("УПЖКХ_СторнироватьНачисленияПоАктуальномуТарифу");
	МассивИменКонстант.Добавить("УПЖКХ_ТочностьОбъемаНачислений");
	МассивИменКонстант.Добавить("УПЖКХ_УказыватьЗависимостьУслугВРазрезеЗданий");
	МассивИменКонстант.Добавить("УПЖКХ_УказыватьКоличествоПрожИЗарегЧислами");
	МассивИменКонстант.Добавить("УПЖКХ_УпрощенныйВариантУчетаСобственников");
	МассивИменКонстант.Добавить("УПЖКХ_УчетДоговоровСВидомПрочее");
	МассивИменКонстант.Добавить("УПЖКХ_УчитыватьКорректировкиПриРасчетеЛьгот");
	
	Возврат МассивИменКонстант;
	
КонецФункции

// Формирует массив имен констант, используемых для настройки параметров системы лицензирования.
//
Функция МассивИменКонстантНастройкаПараметровСистемыЛицензирования()
	
	МассивИменКонстант = Новый Массив;
	МассивИменКонстант.Добавить("СЗК_ПараметрыСвязи");
	
	Возврат МассивИменКонстант;
	
КонецФункции

#КонецОбласти

#КонецОбласти
