#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиКомандФормы

Процедура ОбработатьДействиеОформленияОснования(ИмяПараметраКоманды, ПараметрыВыполнения) Экспорт
	
	СобытияФормВЕТИСКлиентПереопределяемый.ОткрытьФормуСозданияОснованияДокументаВЕТИС(
		ПараметрыВыполнения.Форма,
		Новый ОписаниеОповещения("Подключаемый_ПослеВыбораОснования", ПараметрыВыполнения.Форма, Новый Структура),
		СтрЗаменить(ПараметрыВыполнения.ОписаниеКоманды.Идентификатор,"ОформитьВЕТИС",""),
		ПараметрыВыполнения.ОписаниеКоманды.ТипПараметра.Типы()[0]);
	
КонецПроцедуры

Процедура ОбработатьДействиеВыбораОснования(ИмяПараметраКоманды, ПараметрыВыполнения) Экспорт
	
	СобытияФормВЕТИСКлиентПереопределяемый.ОткрытьФормуВыбораОснованияДокументаВЕТИС(
		ПараметрыВыполнения.Форма,
		Новый ОписаниеОповещения("Подключаемый_ПослеВыбораОснования", ПараметрыВыполнения.Форма, Новый Структура),
		СтрЗаменить(ПараметрыВыполнения.ОписаниеКоманды.Идентификатор,"ВыбратьВЕТИС",""),
		ПараметрыВыполнения.ОписаниеКоманды.ТипПараметра.Типы()[0]);
	
КонецПроцедуры

#КонецОбласти

#Область ДействияПоУмолчанию

Процедура ОткрытьФормуСозданияОснованияДокументаВЕТИС(ФормаВладелец, ОповещениеПриЗавершении, ИмяДокументаОснования, ТипОбъекта) Экспорт
	
	ИмяФормыДокумента = "Документ."+ИмяДокументаОснования+".ФормаОбъекта";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Основание", ФормаВладелец.Объект.Ссылка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   ФормаВладелец);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("ТипОбъекта",              ТипОбъекта);
	
	ОткрытьФорму(
		ИмяФормыДокумента,
		ПараметрыФормы,
		ФормаВладелец,
		ФормаВладелец.УникальныйИдентификатор,,,
		Новый ОписаниеОповещения(
			"ПослеЗавершенияВводаОснованияДокументаВЕТИС", 
			ПодключаемыеКомандыВЕТИСКлиент, 
			ДополнительныеПараметры));
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораОснованияДокументаВЕТИС(Форма, ОповещениеПриЗавершении, Основание, ТипОбъекта)Экспорт
	
	Если ТипОбъекта = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС")Тогда 
		СобытияФормВЕТИСКлиент.ОткрытьФормуВыбораОснованияВходящейТранспортнойОперации(Форма, ОповещениеПриЗавершении, Основание);
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ИнвентаризацияПродукцииВЕТИС") Тогда
		СобытияФормВЕТИСКлиент.ОткрытьФормуВыбораОснованияИнвентаризацииПродукции(Форма, ОповещениеПриЗавершении, Основание);
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС")Тогда 
		СобытияФормВЕТИСКлиент.ОткрытьФормуВыбораОснованияИсходящейТранспортнойОперации(Форма, ОповещениеПриЗавершении, Основание);
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ПроизводственнаяОперацияВЕТИС") Тогда
		СобытияФормВЕТИСКлиент.ОткрытьФормуВыбораОснованияПроизводственнойОперации(Форма, ОповещениеПриЗавершении, Основание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОкончанияДействий

Процедура ПослеЗавершенияВводаОснованияДокументаВЕТИС(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Форма.Объект.ДокументОснование = Результат;
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти