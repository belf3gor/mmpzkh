#Область ПрограммныйИнтерфейс

Процедура УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма, Источник) Экспорт
	
	ОписаниеФормыИсточника = ОписаниеФормыИсточникаПоОбъекту(Источник.Ссылка);
	КомандыОбъекта = КомандыОбъекта(ОписаниеФормыИсточника);
	
	ОпределитьКомандыДляУправленияВидимостью(Форма,КомандыОбъекта);
	
	Если ОписаниеФормыИсточника = "ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС" Тогда 
		УправлениеВидимостьюКомандИсходящейТранспортнойОперации(Форма, КомандыОбъекта);
	ИначеЕсли ОписаниеФормыИсточника = "ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС" Тогда
		УправлениеВидимостьюКомандВходящейТранспортнойОперации(Форма, КомандыОбъекта);
	ИначеЕсли ОписаниеФормыИсточника = "ДокументСсылка.ПроизводственнаяОперацияВЕТИС" Тогда
		УправлениеВидимостьюКомандПроизводственнойОперации(Форма, КомандыОбъекта);
	ИначеЕсли ОписаниеФормыИсточника = "ДокументСсылка.ИнвентаризацияПродукцииВЕТИС" Тогда
		УправлениеВидимостьюКомандИнвентаризацииПродукции(Форма, КомандыОбъекта);
	КонецЕсли;
	
КонецПроцедуры

Функция КомандыОбъекта(Источник) Экспорт
	
	Если Источник = "ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС" Тогда
		Возврат КомандыИсходящейТранспортнойОперации();
	ИначеЕсли Источник = "ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС" Тогда
		Возврат КомандыВходящейТранспортнойОперации();
	ИначеЕсли Источник = "ДокументСсылка.ПроизводственнаяОперацияВЕТИС" Тогда
		Возврат КомандыПроизводственнойОперации();
	ИначеЕсли Источник = "ДокументСсылка.ИнвентаризацияПродукцииВЕТИС" Тогда
		Возврат КомандыИнвентаризацииПродукции();
	КонецЕсли;
	
КонецФункции

Функция ВидыПодключаемыхКоманд() Экспорт
	
	Результат = Новый Массив;
	
	ПодключаемаяКоманда = Новый Структура;
	ПодключаемаяКоманда.Вставить("Имя",           "ОформитьВЕТИС");
	ПодключаемаяКоманда.Вставить("ИмяПодменю",    "КомандыЗаполненияОснованияНовыеДокументы");
	ПодключаемаяКоманда.Вставить("Заголовок",     НСтр("ru = 'Оформить'"));
	ПодключаемаяКоманда.Вставить("Обработчик",    "ПодключаемыеКомандыВЕТИСКлиент.ОбработатьДействиеОформленияОснования");
	ПодключаемаяКоманда.Вставить("ТипМетаданных", "Документы");
	
	Результат.Добавить(ПодключаемаяКоманда);
	
	ПодключаемаяКоманда = Новый Структура;
	ПодключаемаяКоманда.Вставить("Имя",           "ВыбратьВЕТИС");
	ПодключаемаяКоманда.Вставить("ИмяПодменю",    "КомандыЗаполненияОснованияВыборДокументов");
	ПодключаемаяКоманда.Вставить("Заголовок",     НСтр("ru = 'Выбрать'"));
	ПодключаемаяКоманда.Вставить("Обработчик",    "ПодключаемыеКомандыВЕТИСКлиент.ОбработатьДействиеВыбораОснования");
	ПодключаемаяКоманда.Вставить("ТипМетаданных", "Документы");
	
	Результат.Добавить(ПодключаемаяКоманда);
	
	ВидыПодключаемыхКоманд = Неопределено;
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.ВидыПодключаемыхКоманд(ВидыПодключаемыхКоманд);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ВидыПодключаемыхКоманд);
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьКомандуОформить(СтруктураКоманд, ИмяМетаданныхДокумента, ПредставлениеМетаданныхДокумента, СтрокаФункциональныхОпций = "") Экспорт
	
	ПрефиксИмениКомандыФормы = "КомандыЗаполненияОснованияНовыеДокументыОбычное_ОформитьВЕТИС";
	
	ОписаниеКоманды = Новый Структура;
	ОписаниеКоманды.Вставить("ИмяМетаданных",       ИмяМетаданныхДокумента);
	ОписаниеКоманды.Вставить("Представление",       ПредставлениеМетаданныхДокумента);
	ОписаниеКоманды.Вставить("ФункциональныеОпции", СтрокаФункциональныхОпций);
	ОписаниеКоманды.Вставить("ИмяКомандыФормы",     ПрефиксИмениКомандыФормы + ИмяМетаданныхДокумента);
	
	СтруктураКоманд.ОформитьВЕТИС.Добавить(ОписаниеКоманды);
	
КонецПроцедуры

Процедура ДобавитьКомандуВыбрать(СтруктураКоманд, ИмяМетаданныхДокумента, ПредставлениеМетаданныхДокумента, СтрокаФункциональныхОпций = "") Экспорт
	
	ПрефиксИмениКомандыФормы = "КомандыЗаполненияОснованияВыборДокументовОбычное_ВыбратьВЕТИС";
	
	ОписаниеКоманды = Новый Структура;
	ОписаниеКоманды.Вставить("ИмяМетаданных",       ИмяМетаданныхДокумента);
	ОписаниеКоманды.Вставить("Представление",       ПредставлениеМетаданныхДокумента);
	ОписаниеКоманды.Вставить("ФункциональныеОпции", СтрокаФункциональныхОпций);
	ОписаниеКоманды.Вставить("ИмяКомандыФормы",     ПрефиксИмениКомандыФормы + ИмяМетаданныхДокумента);
	ОписаниеКоманды.Вставить("РежимЗаписи",         "НеЗаписывать");
	
	СтруктураКоманд.ВыбратьВЕТИС.Добавить(ОписаниеКоманды);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеФормыИсточникаПоОбъекту(Источник)
	
	Если ТипЗнч(Источник) = Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС") Тогда
		Возврат "ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС";
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументСсылка.ИнвентаризацияПродукцииВЕТИС") Тогда
		Возврат "ДокументСсылка.ИнвентаризацияПродукцииВЕТИС";
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
		Возврат "ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС";
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументСсылка.ПроизводственнаяОперацияВЕТИС") Тогда
		Возврат "ДокументСсылка.ПроизводственнаяОперацияВЕТИС";
	КонецЕсли;
	
КонецФункции

Процедура ОпределитьКомандыДляУправленияВидимостью(Форма,Команды)
	
	//Не добавлять условия видимости штатным способом!
	Если Форма.ВидимостьПодключаемыхКоманд.Количество() = 0 Тогда 
		Для Каждого ИмяГруппИКоманд Из Команды Цикл
			Для Каждого ДанныеКоманды Из ИмяГруппИКоманд.Значение Цикл
				Если Форма.Команды.Найти(ДанныеКоманды.ИмяКомандыФормы) = Неопределено Тогда
					Продолжить;
				ИначеЕсли Форма.Элементы[ДанныеКоманды.ИмяКомандыФормы].Видимость Тогда
					Форма.ВидимостьПодключаемыхКоманд.Вставить(ДанныеКоманды.ИмяКомандыФормы);
					Форма.ВидимостьПодключаемыхКоманд[ДанныеКоманды.ИмяКомандыФормы] = Новый Структура("ГруппаКоманд",ИмяГруппИКоманд.Ключ);
					ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Форма.ВидимостьПодключаемыхКоманд[ДанныеКоманды.ИмяКомандыФормы],ДанныеКоманды);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Если Форма.ВидимостьПодключаемыхКоманд.Количество() = 0 Тогда 
			Форма.ВидимостьПодключаемыхКоманд.Вставить("ЗаполнениеПроведено");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область КомандыДокументовВЕТИС

Функция КомандыИсходящейТранспортнойОперации()
	
	Результат = ПустыеГруппыКоманд();
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.КомандыИсходящейТранспортнойОперации(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция КомандыВходящейТранспортнойОперации()
	
	Результат = ПустыеГруппыКоманд();
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.КомандыВходящейТранспортнойОперации(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция КомандыПроизводственнойОперации()
	
	Результат = ПустыеГруппыКоманд();
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.КомандыПроизводственнойОперации(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция КомандыИнвентаризацииПродукции()
	
	Результат = ПустыеГруппыКоманд();
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.КомандыИнвентаризацииПродукции(Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область УправлениеВидимостьюКомандВЕТИС

Процедура УправлениеВидимостьюКомандИсходящейТранспортнойОперации(Форма, КомандыОбъекта)
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.УправлениеВидимостьюКомандИсходящейТранспортнойОперации(Форма, КомандыОбъекта);
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандВходящейТранспортнойОперации(Форма, КомандыОбъекта)
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.УправлениеВидимостьюКомандВходящейТранспортнойОперации(Форма, КомандыОбъекта);
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандПроизводственнойОперации(Форма, КомандыОбъекта)
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.УправлениеВидимостьюКомандПроизводственнойОперации(Форма, КомандыОбъекта);
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандИнвентаризацииПродукции(Форма, КомандыОбъекта)
	
	ПодключаемыеКомандыВЕТИСКлиентСерверПереопределяемый.УправлениеВидимостьюКомандИнвентаризацииПродукции(Форма, КомандыОбъекта);
	
КонецПроцедуры


#КонецОбласти

#Область ЗаполнениеКомандПоГруппам

Функция ПустыеГруппыКоманд()
	
	Результат = Новый Структура;
	Для Каждого ВидКоманды Из ВидыПодключаемыхКоманд() Цикл
		Результат.Вставить(ВидКоманды.Имя,Новый Массив);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти