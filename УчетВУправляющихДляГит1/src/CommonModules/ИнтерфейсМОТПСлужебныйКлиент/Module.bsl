
#Область СлужебныйПрограммныйИнтерфейс

#Область Авторизация

// Выполняет подпись сообщений обмена с ИС МОТП от имени организации.
//
// Параметры:
//  Сообщения               - Массив - массив сообщений, которые необходимо подписать.
//  Организация             - ОпределяемыйТип.Организация - организация, от имени которой отправляются сообщения.
//  ОповещениеПриЗавершении - ОписаниеОповещения - оповещение, которое будет выполнено после завершения процесса подписи.
//
Процедура Подписать(Сообщения, Организация, ОповещениеПриЗавершении) Экспорт
	
	КоличествоСообщений = Сообщения.Количество();
	Если КоличествоСообщений = 1 Тогда
		ЗаголовокДанных = НСтр("ru = 'Сообщение'");
	Иначе
		ЗаголовокДанных = НСтр("ru = 'Сообщения'");
	КонецЕсли;
	
	НаборДанныхДляПодписания = Новый Массив;
	Для Каждого Сообщение Из Сообщения Цикл
		
		ПредставлениеДанных = Новый Структура;
		ПредставлениеДанных.Вставить("Значение",      Новый ОписаниеОповещения("ПодписатьПоказатьТекстСообщенияXML", ЭтотОбъект, Сообщение));
		ПредставлениеДанных.Вставить("Представление", Сообщение.Описание);
		
		ДвоичныеДанныеДляПодписания = ПолучитьДвоичныеДанныеИзСтроки(Сообщение.ПараметрыАвторизации.Данные);
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Данные",        ДвоичныеДанныеДляПодписания);
		//СтруктураДанные.Вставить("Представление", ПредставлениеДанных);
		СтруктураДанные.Вставить("Сообщение",     Сообщение);
		
		НаборДанныхДляПодписания.Добавить(СтруктураДанные);
		
	КонецЦикла;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Обмен с МОТП'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных",     ЗаголовокДанных);
	ОписаниеДанных.Вставить("СообщитьОЗавершении", Ложь);
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	
	Сертификаты = ИнтеграцияГИСМВызовСервера.СертификатыПользователяДляПодписиПоОрганизации(Организация);
	Если Сертификаты.Количество() > 0 Тогда
		ОписаниеДанных.Вставить("ОтборСертификатов", Сертификаты);
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'В информационной базе для пользователя %1 не найдено зарегистрированных сертификатов
				            |электронной подписи, связанных с организацией %2.'"),
				ПользователиКлиентСервер.АвторизованныйПользователь(), Организация));
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Неопределено);
		
		Возврат;
		
	КонецЕсли;
	
	ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
	ОписаниеДанных.Вставить("ВыполнятьНаСервере",  Неопределено);
	ОписаниеДанных.Вставить("НаборДанных",         НаборДанныхДляПодписания);
	ОписаниеДанных.Вставить("ПредставлениеНабора", СтрШаблон(НСтр("ru = 'Показать (%1)'"), КоличествоСообщений));
	
	ДанныеДляОповещения = Новый Структура;
	ДанныеДляОповещения.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДанныеДляОповещения.Вставить("Сообщения", Сообщения);
	
	//ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, ЭтотОбъект, Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект, ДанныеДляОповещения));
	
	ОткрытьФорму(
		"ОбщаяФорма.ЭмуляторCAdES",
		ОписаниеДанных, ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект, ДанныеДляОповещения),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Обработчик оповещения после подписания данных для авторизации в сервисе ИС МОТП
// 
// Параметры:
// 	Результат - Массив Из (См. ИнтерфейсМОТПКлиент.РезультатПодписания) - Результат подписания.
// 	ДополнительныеПараметры - Структура:
//   * Организация - СправочникВыборка.Организации - Организация.
//   * ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения при завершении процедуры.
Процедура ПослеПодписания(ПараметрыЗапросовПоОрганизациям, ДополнительныеПараметры) Экспорт
	
	Если ПараметрыЗапросовПоОрганизациям = Неопределено Тогда
		
		Результат = Новый Соответствие();
		Результат.Вставить(ДополнительныеПараметры.Организация, Неопределено);
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Результат);
		
	Иначе
		
		Результат = ИнтерфейсМОТПВызовСервера.ЗапроситьКлючиСессий(ПараметрыЗапросовПоОрганизациям);
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Результат);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения нажатия на гиперссылку описания подписываемых данных в форме подписания БСП.
// 
// Параметры:
// 	Результат
// 	ДополнительныеПараметры
Процедура ПодписатьПоказатьТекстСообщенияXML(Результат, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Обработчик завершения подписи.
//
// Параметры:
//  Результат - Структура - содержит свойство "Успех", оно установлено в Истина, если подпись была выполнена.
//  ДополнительныеПараметры - Структура - содержит дополнительные параметры обработчика.
//
Процедура ПодписатьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено
		И Результат.Успех Тогда
		
		Сообщения = Новый Массив;
		
		Для Каждого ТекущийЭлементНабораДанных Из Результат.НаборДанных Цикл
			
			СвойстваПодписи = ТекущийЭлементНабораДанных.СвойстваПодписи;
			
			Сообщение = ТекущийЭлементНабораДанных.Сообщение;
			Сообщение.Вставить("СвойстваПодписи", СвойстваПодписи);
			
			Сообщения.Добавить(Сообщение);
			
		КонецЦикла;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Сообщения);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру с результатом подписания.
// 
// Параметры:
// 	Организация - СправочникВыборка.Организации - Организация.
// 	Описание - Строка - Описание.
// 	ПараметрыАвторизации - (См. ИнтерфейсМОТПСлужебный.ПараметрыАвторизации) - Параметры авторизации.
// Возвращаемое значение:
// 	Структура - Описание:
// * Организация  - СправочникВыборка.Организации - Организация.
// * Описание - Строка - Описание.
// * ПараметрыАвторизации - (См. ИнтерфейсМОТПСлужебный.ПараметрыАвторизации) - Параметры авторизации.
// * СвойстваПодписи - Структура, Неопределено - Свойства подписи.
Функция РезультатПодписания(Организация, Описание, ПараметрыАвторизации) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Организация",          Организация);
	ВозвращаемоеЗначение.Вставить("Описание",             Описание);
	ВозвращаемоеЗначение.Вставить("ПараметрыАвторизации", ПараметрыАвторизации);
	ВозвращаемоеЗначение.Вставить("СвойстваПодписи",      Неопределено);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции
#КонецОбласти

#КонецОбласти