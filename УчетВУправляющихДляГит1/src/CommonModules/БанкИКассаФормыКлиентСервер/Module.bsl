#Область СлужебныеПроцедурыИФункции

// Позволяет идентифицировать однострочную форму.
//
// Параметры:
//  Форма - УправляемаяФорма
//
// Возвращаемое значение:
//  Булево
//
Функция ФормаДокументаОднострочная(Форма) Экспорт
	
	Возврат СтрНайти(Форма.ИмяФормы, "ФормаДокументаОднострочная") > 0;
	
КонецФункции

// Позволяет идентифицировать форму, предназначенную для работы в режиме интеграции с банком
//
// Параметры:
//  Форма - УправляемаяФорма
//
// Возвращаемое значение:
//  Булево - Истина, если форма предназначена для работы в режиме интеграции с банком
//
Функция ФормаДокументаИнтеграцииСБанком(Форма) Экспорт
	
	Возврат СтрНайти(Форма.ИмяФормы, "ФормаДокументаОднострочнаяИнтеграцияСБанком") > 0;
	
КонецФункции

// Управляет видимостью набора полей аналитики учета, которые в однострочной форме документа движения денег могут выводиться
// - непосредственно на форме, если расшифровка суммы документа не требуется
// - в отдельной форме, если сумма расшифровывается несколькими строками.
//
// Параметры:
//  Форма      - УправляемаяФорма - настраиваемая форма документа движения денег.
//  НазваниеТабличнойЧасти 
//             - Строка - имя поля, отображающего данные табличной части РасшифровкаПлатежа.
//               См. например, ПриходныйКассовыйОрдерФормыКлиентСервер.НазваниеТаблицыРасшифровкиПлатежа
//             - Неопределено - форма не предусматривает поля для отображения аналитики
//
Процедура УстановитьВидимостьПолейГруппыБезЗакрывающихДокументов(Форма, НазваниеТабличнойЧасти) Экспорт
	
	Если ФормаДокументаИнтеграцииСБанком(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НазваниеТабличнойЧасти) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	НепосредственноеРедактирование = (Форма.Объект.РасшифровкаПлатежа.Количество() <= 1); // Непосредственно на основной форме, без формы расшифровки
	
	УстановитьВидимостьПоля(
		Форма.Элементы,
		"СтатьяДвиженияДенежныхСредствБезДокументов",
		НазваниеТабличнойЧасти,
		НепосредственноеРедактирование);
		
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПолеОтражениеДоходаВидимость") Тогда
		
		УстановитьВидимостьПоля(
			Форма.Элементы,
			"ПолеОтражениеДохода",
			НазваниеТабличнойЧасти,
			Форма.ПолеОтражениеДоходаВидимость И НепосредственноеРедактирование);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьПоля(Элементы, СобственноеИмяПоля, ПрефиксСписка, УсловиеВидимости)
	
	ИмяПоля = ПрефиксСписка + СобственноеИмяПоля;
	
	Поле = Элементы.Найти(ИмяПоля);
	
	Если Поле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Поле.Видимость = УсловиеВидимости;
	
КонецПроцедуры

#КонецОбласти
