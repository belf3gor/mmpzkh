///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.РаботаСКлассификаторами".
// ОбщийМодуль.РаботаСКлассификаторами.
//
// Серверные процедуры и функции загрузки классификаторов:
//  - получение измененных файлов классификаторов регламентным заданием в тихом режиме (без участия пользователя);
//  - получение актуальных файлов классификаторов и обработка по требования прикладной подсистемы;
//  - получение информации об актуальных версиях классификаторов;
//  - загрузка и обработка версии классификатора;
//  - загрузка файлов классификатора;
//  - изменения настроек загрузки классификаторов;
//  - обработки событий Библиотеки технологии сервиса;
//  - обработки событий Библиотеки стандартных подсистем.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет загрузку обновления классификатора и обработку данных.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - содержит результат обновления классификатора:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "ОбновлениеНеТребуется" - обновление не обнаружено;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "НеОбработан" - файл классификатора успешно загружен, но не обработан.
//                      Ошибка может возникнуть, если отсутствуют алгоритмы обработки файла
//                      см. процедуру РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "ОбновлениеНеТребуется" - загружены актуальные версии классификаторов;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОбновитьКлассификаторы(Идентификаторы) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",          "");
	РезультатОбновления.Вставить("СообщениеОбОшибке",  "");
	РезультатОбновления.Вставить("ИнформацияОбОшибке", "");
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			ЗаполнитьЗначенияСвойств(РезультатОбновления, Результат, "ИнформацияОбОшибке");
			РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			Возврат РезультатОбновления;
		КонецЕсли;
		
		ДанныеАутентификации = Результат.ДанныеАутентификации;
		
	Иначе
		
		// Получение обновление будет выполнено из кэша.
		ДанныеАутентификации = Неопределено;
		
	КонецЕсли;

	// 1. Проверка доступности обновления.
	ПроверитьДоступностьОбновления();
	
	// 2. Начальное заполнение номера версии,
	// для новых классификаторов.
	УстановитьНачальныйНомерВерсииКлассификаторов(Идентификаторы);
	
	// 3. Из сервиса загружается информация об актуальных версиях классификаторов,
	// а также ссылки на скачивание файла.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		// На момент получения поставляемых данных
		// права уже проверены.
		УстановитьПривилегированныйРежим(Истина);
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		РезультатОперации = МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы);
		
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		
		РезультатОперации = ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы,
			ДанныеАутентификации);
		
	КонецЕсли;
	
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 4. Если номер версии классификатора в базе равен номеру версии в сервисе,
	// обновление не будет загружено.
	УдалитьАктуальныеВерсии(РезультатОперации.ДанныеКлассификаторов, Идентификаторы);
	
	Если РезультатОперации.ДанныеКлассификаторов.Количество() = 0 Тогда
		РезультатОбновления.КодОшибки = "ОбновлениеНеТребуется";
		РезультатОбновления.СообщениеОбОшибке  = НСтр("ru = 'Обновление не требуется. Загружена актуальная версия классификатора.'");
		РезультатОбновления.ИнформацияОбОшибке = НСтр("ru = 'Обновление не требуется. Загружена актуальная версия классификатора.'");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 5. Получение файлов классификаторов по ссылкам определенным на этапе 1.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		// На момент получения поставляемых данных
		// права уже проверены.
		УстановитьПривилегированныйРежим(Истина);
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		Для Каждого ОписательВерсии Из РезультатОперации.ДанныеКлассификаторов Цикл
			
			РезультатЗагрузки = МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗагрузитьФайлКлассификатора(
				ОписательВерсии.Идентификатор,
				ОписательВерсии.ИдентификаторФайла);
			
			ОписательВерсии.АдресФайла = РезультатЗагрузки.АдресФайла;
			
			Если РезультатЗагрузки.Ошибка Тогда
				ЗаполнитьЗначенияСвойств(
					РезультатОбновления,
					РезультатЗагрузки,
					"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
				Возврат РезультатОбновления;
			КонецЕсли;
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		
		РезультатЗагрузки = ЗагрузитьФайлыКлассификаторов(
			РезультатОперации.ДанныеКлассификаторов,
			ДанныеАутентификации);
		
	КонецЕсли;
	
	Если РезультатЗагрузки.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатЗагрузки,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 6. Обработка файлов потребителями подсистемы.
	НеОбработанныеКлассификаторы = ОбработатьФайлыКлассификаторов(РезультатОперации.ДанныеКлассификаторов);
	
	Если НеОбработанныеКлассификаторы.Количество() > 0 Тогда
		РезультатОперации.КодОшибки = КодОшибкиНеОбработан();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не удалось обработать обновления классификаторов.'");
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При обработке загруженных обновлений классификаторов %1 возникли ошибки.'"),
			СтрСоединить(НеОбработанныеКлассификаторы, ","));
	КонецЕсли;
	
	Возврат РезультатОбновления;
	
КонецФункции

// Проверяет наличие доступных обновлений классификаторов в Сервисе классификаторов
// или в кэше поставляемых данных. При работе в модели сервиса информация о
// актуальных версиях классификаторов будет сохранена в кэше опционально, т.е.
// в зависимости от настройки классификатора в переопределяемом методе
// РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов. Если
// в настройках классификатора установлено значение СохранятьФайлВКэш равным Истина
// получение файлов актуальных версий будет доступно, в противно случае метод вернет
// пустую таблицу ДоступныеВерсии.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - информация о доступных обновлениях:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//                    - "ОбновлениеНеТребуется" - загружены актуальные версии классификаторов;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *ДоступныеВерсии - Массив - содержит информацию о доступных обновлениях
//      **Идентификатор      - Строка - идентификатор классификатора в сервисе;
//      **Версия             - Строка - номер актуальной версии;
//      **КонтрольнаяСумма   - Число - контрольная сумма файла;
//      **ОписаниеВерсии     - Строка - описание изменений в версии;
//      **ИдентификаторФайла - Строка - идентификатор файла, который будет использован для загрузки;
//      **Размер             - Строка - размер файла;
//      **Наименование       - Строка - наименование классификатора;
//
Функция ДоступныеОбновленияКлассификаторов(Идентификаторы) Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		РезультатПроверки = Новый Структура;
		РезультатПроверки.Вставить("КодОшибки",          "");
		РезультатПроверки.Вставить("СообщениеОбОшибке",  "");
		РезультатПроверки.Вставить("ИнформацияОбОшибке", "");
		РезультатПроверки.Вставить("ДоступныеВерсии ",   Новый Массив);
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатПроверки.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			ЗаполнитьЗначенияСвойств(РезультатПроверки, Результат, "ИнформацияОбОшибке");
			РезультатПроверки.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			Возврат РезультатПроверки;
		КонецЕсли;
		
		ДанныеАутентификации = Результат.ДанныеАутентификации;
		
	Иначе
		
		// Получение обновление будет выполнено из кэша.
		ДанныеАутентификации = Неопределено;
		
	КонецЕсли;
	
	Возврат СлужебнаяДоступныеОбновленияКлассификаторов(
		Идентификаторы,
		ДанныеАутентификации);
	
КонецФункции

// Выполняет загрузку файла обновления классификатора и его обработку.
// Необходимо использовать совместно с функцией РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов.
//
// Параметры:
//  Идентификатор      - Строка - идентификатор классификатора в сервисе;
//  Версия             - Строка - номер актуальной версии;
//  ИдентификаторФайла - Строка - идентификатор файла, который будет использован для загрузки.
//                       см. функцию РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов;
//
// Возвращаемое значение:
//  Структура - результат обновления классификатора:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "НеОбработан" - файл классификатора успешно загружен, но не обработан.
//                      Ошибка может возникнуть, если отсутствуют алгоритмы обработки файла
//                      см. процедуру РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС.
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//
// Пример:
//	Идентификаторы = Новый Массив;
//	Идентификаторы.Добавить(Параметры.Идентификатор);
//
//	РезультатПроверки = РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов(Идентификаторы);
//	Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
//		ПоказатьПредупреждение(, РезультатПроверки.СообщениеОбОшибке);
//	ИначеЕсли РезультатПроверки.ДоступныеВерсии.Количество() = 0 Тогда
//		Возврат;
//	Иначе
//		РаботаСКлассификаторами.ОбработатьОбновлениеКлассификатора(
//			РезультатПроверки.ДоступныеВерсии[0].Идентификатор,
//			РезультатПроверки.ДоступныеВерсии[0].Версия,
//			РезультатПроверки.ДоступныеВерсии[0].ИдентификаторФайла)
//	КонецЕсли;
//
Функция ОбработатьОбновлениеКлассификатора(
		Идентификатор,
		Версия,
		ИдентификаторФайла) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",          "");
	РезультатОбновления.Вставить("СообщениеОбОшибке",  "");
	РезультатОбновления.Вставить("ИнформацияОбОшибке", "");
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			ЗаполнитьЗначенияСвойств(РезультатОбновления, Результат, "ИнформацияОбОшибке");
			РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			Возврат РезультатОбновления;
		КонецЕсли;
		
		ДанныеАутентификации = Результат.ДанныеАутентификации;
		
	Иначе
		
		// Получение обновление будет выполнено из кэша.
		ДанныеАутентификации = Неопределено;
		
	КонецЕсли;
	
	// 1. Проверка доступности обновления.
	ПроверитьДоступностьОбновления();
	
	// 2. Получение файлов классификаторов по ссылкам определенным на этапе 1.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		/// На момент получения поставляемых данных
		// права уже проверены.
		УстановитьПривилегированныйРежим(Истина);
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		РезультатОперации = МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗагрузитьФайлКлассификатора(
			Идентификатор,
			ИдентификаторФайла.ИдентификаторФайла);
		
		УстановитьПривилегированныйРежим(Ложь);
		
		// Заполнение данных версии.
		ДанныеКлассификаторов = ОписаниеДанныхКлассификаторов();
		
		ОписательВерсии = ДанныеКлассификаторов.Добавить();
		ОписательВерсии.Идентификатор    = Идентификатор;
		ОписательВерсии.Версия           = Версия;
		ОписательВерсии.АдресФайла       = РезультатОперации.АдресФайла;
		
	Иначе
		
		// Заполнение данных запроса файла.
		ДанныеКлассификаторов = ОписаниеДанныхКлассификаторов();
		
		ОписательВерсии = ДанныеКлассификаторов.Добавить();
		ОписательВерсии.Идентификатор      = Идентификатор;
		ОписательВерсии.Версия             = Версия;
		ОписательВерсии.ИдентификаторФайла = ИдентификаторФайла.ИдентификаторФайла;
		ОписательВерсии.КонтрольнаяСумма   = ИдентификаторФайла.КонтрольнаяСумма;
		
		РезультатОперации = ЗагрузитьФайлыКлассификаторов(
			ДанныеКлассификаторов,
			ДанныеАутентификации);
		
	КонецЕсли;
	
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 3. Обработка файлов потребителями подсистемы.
	НеОбработанныеКлассификаторы = ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов);
	
	Если НеОбработанныеКлассификаторы.Количество() > 0 Тогда
		РезультатОперации.КодОшибки = КодОшибкиНеОбработан();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не удалось обработать обновления классификаторов.'");
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При обработке загруженных обновлений классификаторов %1 возникли ошибки.'"),
			СтрСоединить(НеОбработанныеКлассификаторы, ","));
	КонецЕсли;
	
	Возврат РезультатОбновления;

КонецФункции

// Получает актуальные версии файлов классификаторов из сервиса классификаторов
// или из кэша поставляемых данных. При работе в модели сервиса информация о
// актуальных версиях классификаторов будет сохранена в кэше опционально, т.е.
// в зависимости от настройки классификатора в переопределяемом методе
// РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов. Если
// в настройках классификатора установлено значение СохранятьФайлВКэш равным Истина
// получение файлов актуальных версий будет доступно, в противно случае метод вернет
// пустую таблицу ДанныеКлассификаторов.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   файлы которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - информация о доступных обновлениях:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "ОбновлениеНеТребуется" - обновление не обнаружено;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "НеОбработан" - файл классификатора успешно загружен, но не обработан.
//                      Ошибка может возникнуть, если отсутствуют алгоритмы обработки файла
//                      см. процедуру РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//    *СообщениеОбОшибке     - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке    - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *ДанныеКлассификаторов - ТаблицаЗначений, Неопределено - содержит информацию о доступных обновлениях
//      **Идентификатор   - Строка - идентификатор классификатора в сервисе;
//      **Версия          - Строка - номер актуальной версии;
//      **АдресФайла      - Строка - адрес файла во временном хранилище;
//      **ОписаниеВерсии     - Строка - описание изменений в версии;
//
Функция ПолучитьФайлыКлассификаторов(Идентификаторы) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",             "");
	РезультатОбновления.Вставить("СообщениеОбОшибке",     "");
	РезультатОбновления.Вставить("ИнформацияОбОшибке",    "");
	РезультатОбновления.Вставить("ДанныеКлассификаторов", Неопределено);
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			ЗаполнитьЗначенияСвойств(РезультатОбновления, Результат, "ИнформацияОбОшибке");
			РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			Возврат РезультатОбновления;
		КонецЕсли;
		
		ДанныеАутентификации = Результат.ДанныеАутентификации;
		
	Иначе
		
		// Получение обновление будет выполнено из кэша.
		ДанныеАутентификации = Неопределено;
		
	КонецЕсли;
	
	// 1. Из сервиса или кэша поставляемых данных загружается информация об актуальных версиях классификаторов,
	// а также ссылки на файлы.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		РезультатОперации = МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы);
		
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		
		РезультатОперации = ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы,
			ДанныеАутентификации);
		
	КонецЕсли;
	
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 2. Получение файлов классификаторов по ссылкам определенным на первом этапе.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		Для каждого ОписательВерсии Из РезультатОперации.ДанныеКлассификаторов Цикл
			
			РезультатЗагрузки = МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗагрузитьФайлКлассификатора(
				ОписательВерсии.Идентификатор,
				ОписательВерсии.ИдентификаторФайла);
			
			ОписательВерсии.АдресФайла = РезультатЗагрузки.АдресФайла;
			
			Если РезультатЗагрузки.Ошибка Тогда
				ЗаполнитьЗначенияСвойств(
					РезультатЗагрузки,
					РезультатОперации,
					"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
				Возврат РезультатОбновления;
			КонецЕсли;
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	Иначе
		
		РезультатЗагрузки = ЗагрузитьФайлыКлассификаторов(
			РезультатОперации.ДанныеКлассификаторов,
			ДанныеАутентификации);
		
		Если РезультатЗагрузки.Ошибка Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОбновления,
				РезультатЗагрузки,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			Возврат РезультатОбновления;
		КонецЕсли;
		
	КонецЕсли;
	
	// 3. Подготовка таблицы классификаторов.
	РезультатОперации.ДанныеКлассификаторов.Колонки.Удалить("КонтрольнаяСумма");
	РезультатОперации.ДанныеКлассификаторов.Колонки.Удалить("ИдентификаторФайла");
	РезультатОбновления.ДанныеКлассификаторов = РезультатОперации.ДанныеКлассификаторов;
	
	Возврат РезультатОбновления;
	
КонецФункции

// Создает описание классификатора, который используется в программе.
//
// Возвращаемое значение:
//   Структура - содержит перечень значений необходимых для  подключения тестового периода:
//     *Наименование               - Строка - пользовательское представление классификатора.
//                                   Длина не более 150 символов;
//     *Идентификатор              - Строка - идентификатор классификатора в сервисе классификаторов.
//                                   Поле обязательно для заполнения, если передана пустая строка,
//                                   при переходе на новую версию будет вызвано исключение. Длина не более
//                                   25 символов;
//     *ОбновлятьАвтоматически     - Булево - настройка которая включает/отключает автоматическое
//                                   обновления данных из сервиса;
//     *ОбщиеДанные                - Булево - регулирует способ обработки поставляем данных.
//                                   Если Ложь, загрузка данных классификатора будет произведена
//                                   в каждую область базы данных. Параметр используется только при работе
//                                   в модели сервиса, в обычном режиме игнорируется;
//     *ОбработкаРазделенныхДанных - Булево - указывает на необходимость дополнительной обработки данных
//                                   областей после загрузки классификатора. Настройка используется только
//                                   при работе в модели сервиса для общих классификаторов, т.е. если свойство
//                                   ОбщиеДанные = Ложь, настройка игнорируется. Если значение настройки
//                                   равно Истина, после обработки обновления классификатора для каждой
//                                   области данных будет вызван переопределяемых метод
//                                   РаботаСКлассификаторамиВМоделиСервисаПереопределяемый.ПриОбработкеОбластиДанных.
//                                   Обработка областей выполняется в асинхронно регламентным заданием
//                                   (см. подсистемы ОчередьЗаданий), которое создается после обработки
//                                   неразделенных данных;
//     *СохранятьФайлВКэш          - Булево - указывает на необходимость сохранения файла в кэше поставляемых
//                                   данных. Настройка используется только при работе в модели сервиса для общих
//                                   классификаторов, т.е. если свойство ОбщиеДанные = Ложь, настройка игнорируется.
//                                   Для получения файла из кэша необходимо использовать метод программного интерфейса
//                                   РаботаСКлассификаторами.ПолучитьФайлыКлассификаторов.
//
Функция ОписаниеКлассификатора() Экспорт
	
	Описатель = Новый Структура;
	Описатель.Вставить("Наименование",               "");
	Описатель.Вставить("Идентификатор",              "");
	Описатель.Вставить("ОбновлятьАвтоматически",     Истина);
	Описатель.Вставить("ОбщиеДанные",                Истина);
	Описатель.Вставить("ОбработкаРазделенныхДанных", Ложь);
	Описатель.Вставить("СохранятьФайлВКэш",          Ложь);
	
	Возврат Описатель;
	
КонецФункции

// Изменяет номер загруженной версии классификатора. Процедуру следует использовать,
// если выполняется обновления данных не из сервиса классификаторов. При работе в модели
// сервиса будет автоматический определена доступность общих данных. Если общие данные
// не доступны, изменение версии регистрируется для области данных.
//
// Параметры:
//  Идентификатор - Строка - идентификатор классификатора в сервисе классификаторов;
//  Версия        - Число - новый номер версии, который необходимо установить.
//
Процедура УстановитьВерсиюКлассификатора(Идентификатор, Версия) Экспорт
	
	Если ИспользоватьДанныеОбласти() Тогда
		Запись = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьМенеджерЗаписи();
		Запись.Идентификатор = Идентификатор;
	Иначе
		Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
		Запись.Идентификатор = Идентификатор;
		Запись.Прочитать();
		Если Не Запись.Выбран() Тогда
			ВызватьИсключение НСтр("ru = 'Классификатор по идентификатору не обнаружен.'");
		КонецЕсли;
	КонецЕсли;
	
	Запись.Версия = Версия;
	Запись.Записать();
	
КонецПроцедуры

// Получает номер версии загруженного из сервиса классификатора. Если номер версии
// по идентификатору не найден, выполняет обновление данных регистра сведений
// ВерсииКлассификаторов.
//
// Параметры:
//  Идентификатор      - Строка - идентификатор классификатора в сервисе классификаторов;
//  ВызыватьИсключение - Булево - если Истина и идентификатор не найден, будет вызвано исключение.
//
// Возвращаемое значение:
//   Число, Неопределено - номер версии классификатора.
//
Функция ВерсияКлассификатора(Идентификатор, ВызыватьИсключение = Ложь) Экспорт
	
	// Версии классификаторов не являются секретной информацией
	// и могут быть получены любым пользователем ИБ. 
	УстановитьПривилегированныйРежим(Истина);

	ОбщиеДанные = Не (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
	ИмяРегистра = ?(ОбщиеДанные,
		"ВерсииКлассификаторов",
		"ВерсииКлассификаторовОбластейДанных");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяРегистра);
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Версия;
	КонецЕсли;
	
	// Возможно, не выполнено обновление настроек классификатора РС ВерсииКлассификаторов
	// из-за нарушения порядка подключения подсистем к конечной конфигурации. Если классификатор
	// используется в конфигурации, будет выполнено частичное обновление настроек подсистемы.
	Если ОбщиеДанные Тогда
		
		Классификаторы = Новый Массив;
		РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов(Классификаторы);
		
		Для Каждого Описатель Из Классификаторы Цикл
			Если Описатель.Идентификатор = Идентификатор Тогда
				Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Описатель);
				Запись.Записать();
				Возврат 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВызыватьИсключение Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Классификатор %1 не зарегистрирован.'"),
			Идентификатор);
		ВызватьИсключение ТекстИсключения;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Определяет доступность использования обработки интерактивной загрузки
// классификаторов.
//
// Возвращаемое значение:
//  Булево - определяет возможность использования обработки ОбновлениеКлассификаторов
//           Если Истина, обработку можно использовать.
// 
Функция ИнтерактивнаяЗагрузкаКлассификаторовДоступна() Экспорт
	
	Возврат Не ОбщегоНазначения.РазделениеВключено()
		И Не ЗагрузкаКлассификаторовДоступна();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ИнтеграцияСБиблиотекойСтандартныхПодсистем

#Область БСПБазоваяФункциональность

// Интеграция с подсистемой СтандартныеПодсистемы.БазоваяФункциональность.
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	НовыеРазрешения = Новый Массив;
	
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		ХостСервисаКлассификаторов(0),
		443,
		НСтр("ru = 'Сервис классификаторов (ru)'"));
	НовыеРазрешения.Добавить(Разрешение);
	
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		ХостСервисаКлассификаторов(1),
		443,
		НСтр("ru = 'Сервис классификаторов (eu)'"));
	НовыеРазрешения.Добавить(Разрешение);
	
	ЗапросыРазрешений.Добавить(РаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(НовыеРазрешения));
	
КонецПроцедуры

// См. описание процедуры в общем модуле
// ОбщегоНазначенияПереопределяемый.ПриДобавленииПереименованийОбъектовМетаданных.
//
Процедура ПриДобавленииПереименованийОбъектовМетаданных(Итог) Экспорт
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.2.5.5",
		"Роль.ОбновлениеКлассификаторов",
		"Роль.ПолучениеОбновленийКлассификаторов",
		"ИнтернетПоддержкаПользователей");
	
КонецПроцедуры

#КонецОбласти

#Область БСПОбновлениеИнформационнойБазы

// Заполняет список обработчиков обновления информационной базы.
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия              = "*";
	Обработчик.Процедура           = "РаботаСКлассификаторами.ОбновитьНастройкиРаботыСКлассификаторами";
	Обработчик.ОбщиеДанные         = Истина;
	Обработчик.НачальноеЗаполнение = Ложь;
	Обработчик.РежимВыполнения     = "Оперативно";
	Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1. Обновление списка классификаторов.'"),
		ИмяСобытияЖурналаРегистрации());
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "*";
		Обработчик.Процедура           = "РаботаСКлассификаторами.ОбновитьНастройкиРаботыСКлассификаторамиОбластейДанных";
		Обработчик.ОбщиеДанные         = Ложь;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.РежимВыполнения     = "Отложенно";
		Обработчик.Идентификатор       = Новый УникальныйИдентификатор("0589c734-f2a8-4af1-97b1-6e8deb4830d6");
		Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Обновление списка классификаторов областей данных.'"),
			ИмяСобытияЖурналаРегистрации());
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "2.2.5.3";
		Обработчик.Процедура           = "РаботаСКлассификаторами.ЗаполнитьДанныеРегистраВерсииКлассификаторовОбластейДанных";
		Обработчик.ОбщиеДанные         = Ложь;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.РежимВыполнения     = "Отложенно";
		Обработчик.Идентификатор       = Новый УникальныйИдентификатор("21d75419-ac2a-4d5f-8b61-45509c3c340e");
		Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Перенос настроек в регистр сведений ВерсииКлассификаторовОбластейДанных.'"),
			ИмяСобытияЖурналаРегистрации());
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление данных регистра сведений ВерсииКлассификаторов и
// добавляет регламентное задание проверки обновлений классификаторов.
//
Процедура ОбновитьНастройкиРаботыСКлассификаторами() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек подсистемы ""Работа с классификаторами"". Начало обновления.'"),
		Ложь);
	
	Классификаторы = Новый Массив;
	РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов(Классификаторы);
	
	// Обновление списка классификаторов и настроек обновления.
	УдаленныеКлассификаторы = ОбновитьВерсииКлассификаторов(Классификаторы);
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		// Для автоматического обновления при необходимости создается регламентное
		// задание, которое по расписанию обновляет данные.
		СоздатьЗадание = Ложь;
		Для каждого ОписательКлассификатора Из Классификаторы Цикл
			Если ОписательКлассификатора.ОбновлятьАвтоматически Тогда
				СоздатьЗадание = Истина;
			КонецЕсли;
		КонецЦикла;
		
		// Если есть классификаторы, которые необходимо загружать из сервиса
		// по расписанию, будет создано регламентное задание.
		Если СоздатьЗадание Тогда
			ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов();
		Иначе
			УдалитьРегламентноеЗаданиеОбновленияКлассификаторов();
		КонецЕсли;
		
	Иначе
		
		// При работе в модели сервиса файлы классификаторов кэшируются в поставляемых
		// данных, поэтому для удаленных классификаторов необходимо запланировать удаление
		// кэша.
		Если УдаленныеКлассификаторы.Количество() > 0 Тогда
			ПараметрыМетода = Новый Массив;
			ПараметрыМетода.Добавить(УдаленныеКлассификаторы);
			
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("ИмяМетода", "УдалитьКэшПоставляемыхКлассификаторов");
			ПараметрыЗадания.Вставить("Параметры", ПараметрыМетода);
			ПараметрыЗадания.Вставить("ОбластьДанных", -1);
			ПараметрыЗадания.Вставить("ЗапланированныйМоментЗапуска", ТекущаяУниверсальнаяДата());
			ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 3);
			
			МодульОчередьЗаданий = ОбщегоНазначения.ОбщийМодуль("ОчередьЗаданий");
			МодульОчередьЗаданий.ДобавитьЗадание(ПараметрыЗадания);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек подсистемы ""Работа с классификаторами"". Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

// Выполняет обновление данных регистра сведений ВерсииКлассификаторовОбластейДанных.
//
Процедура ОбновитьНастройкиРаботыСКлассификаторамиОбластейДанных(Параметры) Экспорт
	
	// Дозаписываем информацию о классификаторах в регистр
	// отложенном обновлении из областей данных.
	Если Не ОбщегоНазначения.РазделениеВключено()
		Или Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек областей подсистемы ""Работа с классификаторами"". Начало обновления.'"),
		Ложь);
	
	Классификаторы = Новый Массив;
	РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов(Классификаторы);
	
	ИспользуемыеКлассификаторы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииКлассификаторовОбластейДанных.Идентификатор КАК Идентификатор,
		|	ВерсииКлассификаторовОбластейДанных.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторовОбластейДанных КАК ВерсииКлассификаторовОбластейДанных";
	
	РезультатЗапроса       = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого Описатель Из Классификаторы Цикл
		
		Если Описатель.ОбщиеДанные Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", Описатель.Идентификатор);
		
		Если Не ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
			Запись = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Описатель, "Идентификатор");
			Запись.Записать();
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ИспользуемыеКлассификаторы.Добавить(Описатель.Идентификатор);
		
	КонецЦикла;
	
	// Идентификаторы классификаторов, которые перестали использоваться необходимо удалить
	// из регистра "Версии классификаторов".
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииКлассификаторовОбластейДанных.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторовОбластейДанных КАК ВерсииКлассификаторовОбластейДанных
		|ГДЕ
		|	НЕ ВерсииКлассификаторовОбластейДанных.Идентификатор В (&ИспользуемыеКлассификаторы)";
	
	Запрос.УстановитьПараметр("ИспользуемыеКлассификаторы", ИспользуемыеКлассификаторы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Набор = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьНаборЗаписей();
		Набор.Отбор.Идентификатор.Установить(ВыборкаДетальныеЗаписи.Идентификатор);
		Набор.Записать();
	КонецЦикла;
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек областей подсистемы ""Работа с классификаторами"". Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

// Выполняет перенос данных из регистра сведений УдалитьВерсииКлассификаторовОбластейДанных
// в регистр сведений УдалитьВерсииКлассификаторовОбластейДанных.
//
Процедура ЗаполнитьДанныеРегистраВерсииКлассификаторовОбластейДанных(Параметры) Экспорт
	
	// Дозаписываем информацию о классификаторах в регистр
	// отложенном обновлении из областей данных.
	Если Не ОбщегоНазначения.РазделениеВключено()
		Или Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Перенос настроек в регистр сведений ВерсииКлассификаторовОбластейДанных. Начало обновления.'"),
		Ложь);
	
	// Регистр не содержит большое количество записей.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УдалитьВерсииКлассификаторовОбластейДанных.Идентификатор КАК Идентификатор,
		|	УдалитьВерсииКлассификаторовОбластейДанных.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.УдалитьВерсииКлассификаторовОбластейДанных КАК УдалитьВерсииКлассификаторовОбластейДанных";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВерсииКлассификаторов = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьНаборЗаписей();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ВерсииКлассификаторов.Добавить(), ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	УдалитьВерсииКлассификаторов = РегистрыСведений.УдалитьВерсииКлассификаторовОбластейДанных.СоздатьНаборЗаписей();
	
	НачатьТранзакцию();
	ВерсииКлассификаторов.Записать();
	УдалитьВерсииКлассификаторов.Записать();
	ЗафиксироватьТранзакцию();
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Перенос настроек в регистр сведений ВерсииКлассификаторовОбластейДанных. Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область БСПОчередьЗаданий

// См. описание этой же процедуры в общем модуле
// ОчередьЗаданийПереопределяемый.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(
		"УдалитьКэшПоставляемыхКлассификаторов", "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.УдалитьКэшПоставляемыхКлассификаторов");
	СоответствиеИменПсевдонимам.Вставить(
		"ЗапланироватьОбновлениеДанныхОбластей", "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗапланироватьОбновлениеДанныхОбластей");
	
КонецПроцедуры

#КонецОбласти

#Область БСПРаботаВМоделиСервиса

// См. описание этой же процедуры в общем модуле
// ПоставляемыеДанныеПереопределяемый.
//
Процедура ПолучитьОбработчикиПоставляемыхДанных(Обработчики) Экспорт
	
	СтрОбработчик = Обработчики.Добавить();
	СтрОбработчик.ВидДанных      = ВидПоставляемыхДанныхКлассификаторы();
	СтрОбработчик.КодОбработчика = ВидПоставляемыхДанныхКлассификаторы();
	СтрОбработчик.Обработчик     = ОбщегоНазначения.ОбщийМодуль("РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ИнтеграцияПодсистемИнтернетПоддержкиПользователей

// Вызывается при сохранении логина и пароля пользователя ИПП в
// информационной базе из всех контекстов использования библиотеки.
//
Процедура ПриСохраненииЛогинаИПароляВИБ(Логин, Пароль) Экспорт
	
	ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов();
	
КонецПроцедуры

// Вызывается при удалении логина и пароля пользователя ИПП из
// информационной базы из всех контекстов использования библиотеки.
//
Процедура ПриУдаленииЛогинаИПароляИзИБ() Экспорт
	
	Если Не ИнтернетПоддержкаПользователей.ДоступноПодключениеИнтернетПоддержки() Тогда
		ВызватьИсключение НСтр("ru = 'Отключение интернет поддержки пользователей не доступно.'");
	КонецЕсли;
	
	УдалитьРегламентноеЗаданиеОбновленияКлассификаторов();
	
КонецПроцедуры

// Определяет настройки обновления классификаторов.
//
// Параметры:
//  Идентификаторы  - Массив - содержит список идентификаторов классификаторов,
//                    для которых необходимо получить настройки;
//
// Возвращаемое значение:
//  Структура - настройки классификатора.
//
Функция НастройкиКлассификатора(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Версия КАК Версия,
		|	ВерсииКлассификаторов.ОбновлятьАвтоматически КАК ОбновлятьАвтоматически,
		|	ВерсииКлассификаторов.ОбщиеДанные КАК ОбщиеДанные,
		|	ВерсииКлассификаторов.ОбработкаРазделенныхДанных КАК ОбработкаРазделенныхДанных,
		|	ВерсииКлассификаторов.СохранятьФайлВКэш КАК СохранятьФайлВКэш
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
		|ГДЕ
		|	ВерсииКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Настройки = Новый Структура;
		Настройки.Вставить("Версия",                     ВыборкаДетальныеЗаписи.Версия);
		Настройки.Вставить("ОбновлятьАвтоматически",     ВыборкаДетальныеЗаписи.ОбновлятьАвтоматически);
		Настройки.Вставить("ОбщиеДанные",                ВыборкаДетальныеЗаписи.ОбщиеДанные);
		Настройки.Вставить("ОбработкаРазделенныхДанных", ВыборкаДетальныеЗаписи.ОбработкаРазделенныхДанных);
		Настройки.Вставить("СохранятьФайлВКэш",          ВыборкаДетальныеЗаписи.СохранятьФайлВКэш);
		
		Возврат Настройки;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Создает таблицу с описанием данных актуальных версий.
//
// Параметры:
//  ИнтерактивнаяЗагрузка - Булево - определяет набор возвращаемых атрибутов.
//
// Возвращаемое значение:
//  ТаблицаЗначений      - содержит информацию, которая используется
//                         для обновления данных классификаторов
//   *Идентификатор      - Строка - идентификатор классификатора в сервисе;
//   *Версия             - Число - номер актуальной версии;
//   *КонтрольнаяСумма   - Число - контрольная сумма файла;
//   *ИдентификаторФайла - Строка - ссылка на скачивание файла актуальной версии;
//   *АдресФайла         - Строка - адрес файла классификатора во временном хранилище;
//   *Размер             - Строка - размер файла;
//   *Наименование       - Строка - наименование классификатора;
//   *ОписаниеВерсии     - Строка - описание версии классификатора.
//
Функция ОписаниеДанныхКлассификаторов(ИнтерактивнаяЗагрузка = Ложь) Экспорт
	
	ДанныеКлассификаторов = Новый ТаблицаЗначений;
	ДанныеКлассификаторов.Колонки.Добавить("Идентификатор",      ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ДанныеКлассификаторов.Колонки.Добавить("Версия",             ОбщегоНазначения.ОписаниеТипаЧисло(11));
	ДанныеКлассификаторов.Колонки.Добавить("КонтрольнаяСумма",   ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ДанныеКлассификаторов.Колонки.Добавить("ИдентификаторФайла", ОбщегоНазначения.ОписаниеТипаСтрока(800));
	ДанныеКлассификаторов.Колонки.Добавить("АдресФайла",         ОбщегоНазначения.ОписаниеТипаСтрока(250));
	
	Если ИнтерактивнаяЗагрузка Тогда
		ДанныеКлассификаторов.Колонки.Добавить("Размер",         ОбщегоНазначения.ОписаниеТипаЧисло(32));
		ДанныеКлассификаторов.Колонки.Добавить("Наименование",   ОбщегоНазначения.ОписаниеТипаСтрока(100));
		ДанныеКлассификаторов.Колонки.Добавить("ОписаниеВерсии", ОбщегоНазначения.ОписаниеТипаСтрока(800));
	КонецЕсли;
	
	Возврат ДанныеКлассификаторов;
	
КонецФункции

// Вызывается из обработчика ПриСозданииНаСервере() панели администрирования
// БСП, выполняется настройку отображения элементов управления для подсистем
// библиотеки ИПП.
//
// Параметры:
//  Форма - УправляемаяФорма - форма панели управления.
//
Процедура ИнтернетПоддержкаИСервисы_ПриСозданииНаСервере(Форма) Экспорт
	
	Если ИнтерактивнаяЗагрузкаКлассификаторовДоступна() Тогда
		Форма.Элементы.БИПГруппаОбновлениеКлассификаторов.Видимость = Истина;
	Иначе
		Форма.Элементы.БИПГруппаОбновлениеКлассификаторов.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Определяет вид данных и код обработчика для поставляемых данных.
//
// Возвращаемое значение:
//  Строка - наименование вида данных.
//
Функция ВидПоставляемыхДанныхКлассификаторы() Экспорт
	
	Возврат "Classifiers";
	
КонецФункции

// Добавляет запись в журнал регистрации.
//
// Параметры:
//  СообщениеОбОшибке - Строка - комментарий к записи журнала регистрации;
//  Ошибка - Булево - если истина будет установлен уровень журнала регистрации "Ошибка";
//  ОбъектМетаданных - ОбъектМетаданных - объект метаданных для которого регистрируется ошибка.
//
Процедура ЗаписатьИнформациюВЖурналРегистрации(
		СообщениеОбОшибке,
		Ошибка = Истина,
		ОбъектМетаданных = Неопределено) Экспорт
	
	УровеньЖР = ?(Ошибка, УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖР,
		ОбъектМетаданных,
		,
		Лев(СообщениеОбОшибке, 5120));
	
КонецПроцедуры

// Выполняет определение и регистрацию номера версии загруженного классификатора
//
// Параметры:
//  Идентификатор - Строка - идентификатор классификатора в сервисе.
//
// Возвращаемое значение:
//  Число - определенный номер версии.
//
Функция ОбработатьНачальнуюВерсиюКлассификатора(Идентификатор) Экспорт
	
	НомерВерсии = 0;
	РаботаСКлассификаторамиПереопределяемый.ПриОпределенииНачальногоНомераВерсииКлассификатора(
		Идентификатор,
		НомерВерсии);
	
	Если ТипЗнч(НомерВерсии) = Тип("Число") И НомерВерсии <> 0 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		УстановитьВерсиюКлассификатора(Идентификатор, НомерВерсии);
		УстановитьПривилегированныйРежим(Ложь);
		
		Возврат НомерВерсии;
		
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиРегламентныхЗаданий

// Обработчик регламентного задания "ОбновлениеКлассификаторов"
//
Процедура ОбновлениеКлассификаторов() Экспорт
	
	// Регламентные задания блокируются на время служебных
	// операций с информационной базой.
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
		|ГДЕ
		|	ВерсииКлассификаторов.ОбновлятьАвтоматически";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Идентификаторы         = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Идентификаторы.Добавить(ВыборкаДетальныеЗаписи.Идентификатор);
	КонецЦикла;
	
	Если Идентификаторы.Количество() = 0 Тогда
		ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Отсутствуют классификаторы для автоматического обновления.'"),
			Ложь);
		Возврат;
	КонецЕсли;
	
	ОбновитьКлассификаторы(Идентификаторы);
	
КонецПроцедуры

// Создает регламентное задание "ОбновлениеКлассификаторов" 
// при обновлении ИБ или при подключении Интернет-поддержки пользователей.
//
Процедура ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов()
	
	// При работе в режиме коробки обновление классификаторов производится
	// регламентным заданием.
	Если Не ОбщегоНазначения.РазделениеВключено()
		И ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
		ЗаданияОбновления = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		
		Если ЗаданияОбновления.Количество() = 0 Тогда
			
			// Чтобы не создавать пиковых нагрузок на сервис,
			// время обновления будет выбрано случайным образом
			// между 00:00 и 06:00.
			Генератор = Новый ГенераторСлучайныхЧисел;
			Расписание = Новый РасписаниеРегламентногоЗадания;
			Расписание.ВремяНачала       = Дата("00010101") + Генератор.СлучайноеЧисло(0, 21600);
			Расписание.ПериодПовтораДней = 1;
			
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("Использование", Истина);
			ПараметрыЗадания.Вставить("Метаданные",    Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			ПараметрыЗадания.Вставить("Расписание",    Расписание);
			ПараметрыЗадания.Вставить("Наименование",  НСтр("ru = 'Обновление классификаторов'"));
			
			РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
			
			ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'Создано регламентное задание обновления классификаторов.'"),
				Ложь,
				Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Создает регламентное задание "ОбновлениеКлассификаторов" 
// при обновлении ИБ или при отключении от Интернет-поддержки пользователей.
//
Процедура УдалитьРегламентноеЗаданиеОбновленияКлассификаторов()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
	ЗаданияОбновления = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	ТипЗадания = ТипЗнч(ЗаданияОбновления);
	Если ТипЗадания = Тип("Массив") Тогда
		Если ЗаданияОбновления.Количество() > 0 Тогда
			РегламентныеЗаданияСервер.УдалитьЗадание(ЗаданияОбновления[0].УникальныйИдентификатор);
		КонецЕсли;
	ИначеЕсли ТипЗадания = Тип("ТаблицаЗначений") Тогда
		Если ЗаданияОбновления.Количество() > 0 Тогда
			Если ЗаданияОбновления.Колонки.Найти("УникальныйИдентификатор") <> Неопределено Тогда
				РегламентныеЗаданияСервер.УдалитьЗадание(ЗаданияОбновления[0].УникальныйИдентификатор);
			ИначеЕсли ЗаданияОбновления.Колонки.Найти("Идентификатор") <> Неопределено Тогда
				РегламентныеЗаданияСервер.УдалитьЗадание(ЗаданияОбновления[0].Идентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Удалено регламентное задание обновления классификаторов'"),
		Ложь,
		Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИБ

// Определяет список классификаторов используемых в конфигурации
// основные сведения: идентификатор и настройку обновлять автоматический
// и обновляет данные регистра сведений справочника "ВерсииКлассификаторов".
//
// Параметры:
//  Классификаторы - Массив - содержит описатели классификаторов,
//                   см. функцию ОписаниеКлассификатора().
//
// Возвращаемое значение:
//  Массив - содержит идентификаторы классификаторов, которые были удалены из регистра
//           сведений "ВерсииКлассификаторов".
//
Функция ОбновитьВерсииКлассификаторов(Классификаторы)
	
	УдаленныеКлассификаторы    = Новый Массив;
	ИспользуемыеКлассификаторы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	ВерсииКлассификаторов.Версия КАК Версия,
		|	ВерсииКлассификаторов.ОбновлятьАвтоматически КАК ОбновлятьАвтоматически,
		|	ВерсииКлассификаторов.ОбщиеДанные КАК ОбщиеДанные,
		|	ВерсииКлассификаторов.Наименование КАК Наименование,
		|	ВерсииКлассификаторов.ОбработкаРазделенныхДанных КАК ОбработкаРазделенныхДанных,
		|	ВерсииКлассификаторов.СохранятьФайлВКэш КАК СохранятьФайлВКэш
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов";
	
	РезультатЗапроса       = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого Описатель Из Классификаторы Цикл
		
		Обновлен = Ложь;
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", Описатель.Идентификатор);
		
		// Обновление настроек классификаторов.
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
			
			Если ВыборкаДетальныеЗаписи.ОбновлятьАвтоматически <> Описатель.ОбновлятьАвтоматически
				Или ВыборкаДетальныеЗаписи.ОбщиеДанные <> Описатель.ОбщиеДанные
				Или ВыборкаДетальныеЗаписи.Наименование <> Описатель.Наименование
				Или ВыборкаДетальныеЗаписи.ОбработкаРазделенныхДанных <> Описатель.ОбработкаРазделенныхДанных
				Или ВыборкаДетальныеЗаписи.СохранятьФайлВКэш <> Описатель.СохранятьФайлВКэш Тогда
				
				Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальныеЗаписи, "Идентификатор, Версия");
				Запись.ОбновлятьАвтоматически     = Описатель.ОбновлятьАвтоматически;
				Запись.ОбщиеДанные                = Описатель.ОбщиеДанные;
				Запись.Наименование               = Описатель.Наименование;
				Запись.ОбработкаРазделенныхДанных = Описатель.ОбработкаРазделенныхДанных;
				Запись.СохранятьФайлВКэш          = Описатель.СохранятьФайлВКэш;
				Запись.Записать();
			КонецЕсли;
			
			Обновлен = Истина;
			
		КонецЕсли;
		
		// Добавление новых классификаторов.
		Если Не Обновлен Тогда
			Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
			СвойстваОписателя = "
				|Идентификатор,
				|ОбновлятьАвтоматически,
				|ОбщиеДанные,
				|Наименование,
				|ОбработкаРазделенныхДанных,
				|СохранятьФайлВКэш";
			ЗаполнитьЗначенияСвойств(Запись, Описатель, СвойстваОписателя);
			Запись.Записать();
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ИспользуемыеКлассификаторы.Добавить(Описатель.Идентификатор);
		
	КонецЦикла;
	
	// Идентификаторы классификаторов, которые перестали использоваться необходимо удалить
	// из регистра "Версии классификаторов".
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
		|ГДЕ
		|	НЕ ВерсииКлассификаторов.Идентификатор В (&ИспользуемыеКлассификаторы)";
	
	Запрос.УстановитьПараметр("ИспользуемыеКлассификаторы", ИспользуемыеКлассификаторы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Набор = РегистрыСведений.ВерсииКлассификаторов.СоздатьНаборЗаписей();
		Набор.Отбор.Идентификатор.Установить(ВыборкаДетальныеЗаписи.Идентификатор);
		Набор.Записать();
		УдаленныеКлассификаторы.Добавить(ВыборкаДетальныеЗаписи.Идентификатор);
	КонецЦикла;
	
	Возврат УдаленныеКлассификаторы;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийСервиса

////////////////////////////////////////////////////////////////////////////////
// Вызов операции /version/latest

// Возвращает список описаний актуальных версий классификаторов, которые доступны пользователю
// на текущий момент.
//
// Параметры:
//  Идентификаторы         - Массив - содержит список идентификаторов классификаторов,
//                          для которых необходимо проверить наличие обновлений;
//  ДанныеАутентификации  - Структура - параметры аутентификации пользователя Интернет-поддержки;
//  ИнтерактивнаяЗагрузка - Булево - определяет набор возвращаемых атрибутов.
//
// Возвращаемое значение:
//   Структура - содержит результат операции:
//    *Ошибка - Булево - Истина, если в не удалось получить информацию из сервиса;
//    *СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//    *ИнформацияОбОшибке - Строка - описание ошибки для администратора;
//    *ДанныеКлассификаторов - ТаблицаЗначений - см. функцию ОписаниеДанныхКлассификаторов().
//
Функция ИнформацияОбАктуальныхВерсияхКлассификаторов(
		Идентификаторы,
		ДанныеАутентификации,
		ИнтерактивнаяЗагрузка = Ложь)
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начало получения информации об актуальных версиях классификаторов: %1'"),
		СтрСоединить(Идентификаторы, ","));
	
	ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("КодОшибки",             "");
	РезультатОперации.Вставить("Ошибка",                Ложь);
	РезультатОперации.Вставить("СообщениеОбОшибке",     "");
	РезультатОперации.Вставить("ИнформацияОбОшибке",    "");
	РезультатОперации.Вставить("ДанныеКлассификаторов", ОписаниеДанныхКлассификаторов(ИнтерактивнаяЗагрузка));
	
	ПараметрыПодключения = ИнициализироватьПараметрыОбновленияКлассификаторов();
	
	URLОперации = URLОперацииСервисаКлассификаторов(
		"/version/latest",
		ПараметрыПодключения.НастройкиСоединения.ДоменРасположенияСерверовИПП);
	
	ПараметрыЗапросаJSON = versionlatest(
		Идентификаторы,
		ДанныеАутентификации,
		ИнтернетПоддержкаПользователей.ДополнительныеПараметрыВызоваОперацииСервиса());
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки         = ПереопределитьКодОшибкиСервиса(РезультатОтправки.КодСостояния);
		РезультатОперации.Ошибка            = Истина;
		РезультатОперации.СообщениеОбОшибке = ПереопределитьСообщениеПользователю(РезультатОперации.КодОшибки);
		
		ЧастиСтрок = Новый Массив;
		ЧастиСтрок.Добавить(НСтр("ru = 'Не удалось получить актуальные версии классификаторов.'"));
		ЧастиСтрок.Добавить(Символы.ПС);
		ЧастиСтрок.Добавить(РезультатОперации.СообщениеОбОшибке);
		ЧастиСтрок.Добавить(Символы.ПС);
		ЧастиСтрок.Добавить(Символы.ПС);
		ЧастиСтрок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Техническая информация об ошибке:
				|При получении информации об актуальных версиях классификаторов сервис вернул ошибку.
				|URL: %1
				|Код ошибки: %2
				|Подробная информация:
				|%3'"),
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке));
		
		РезультатОперации.ИнформацияОбОшибке = Новый ФорматированнаяСтрока(ЧастиСтрок);
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ПрочитатьДанные_versionlatest(
		РезультатОтправки.Содержимое,
		РезультатОперации.ДанныеКлассификаторов,
		ИнтерактивнаяЗагрузка,
		ПараметрыПодключения.НастройкиСоединения.ДоменРасположенияСерверовИПП);
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Завершено получение актуальных версий классификаторов: %1'"),
		СтрСоединить(Идентификаторы, ","));
	
	ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// /classifiers/version/latest.
//
Функция versionlatest(Идентификаторы, ДанныеАутентификации, ДополнительныеПараметры)
	
	// {
	//    "programNick":"nick",
	//    "classifierNickList":[nick1,nick2],
	//    "authenticationInfo": {
	//            "login": "User",
	//            "password":"Pass",
	//    },
	//    "additionalParams" : {
	//        "key":"value"
	//    }
	// }
	
	ИмяПрограммы = ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы();
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("programNick");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИмяПрограммы);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("classifierNickList");
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	Для каждого Идентификатор Из Идентификаторы Цикл
		ЗаписьДанныхСообщения.ЗаписатьЗначение(Идентификатор);
	КонецЦикла;
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ИнтернетПоддержкаПользователей.ЗаписатьДополнительныеПараметрыЗапроса(
		ДополнительныеПараметры,
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /version/latest.
//
Процедура ПрочитатьДанные_versionlatest(
		ТелоJSON,
		ДанныеКлассификаторов,
		ИнтерактивнаяЗагрузка = Ложь,
		ДоменРасположенияСерверовИПП = 1)
	
	// Тело ответа:
	// classifierNick - идентификатор классификатора в сервисе;
	// version - номер актуальной версии;
	// fileUrl - ссылка на скачивание файла актуальной версии;
	// hashSum - контрольная сумма файла;
	// versionDescription - описание версии классификатора;
	// fileSize - размер файла;
	// classifierName - наименование классификатора.
	//
	// {
	//  [
	//     {
	//      "classifierNick": "Идентификатор",
	//      "version":1,
	//      "fileUrl": "https://fileUrl",
	//      "hashSum": "Контрольная сумма",
	//      "versionDescription": "Описание",
	//      "fileSize": "Размер в байтах",
	//      "classifierName": "Имя классификатора"
	//     }
	//  ]
	//}
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ Сервиса классификаторов:
			|%1'"),
		ТелоJSON);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	ТекущийУровень = 0;
	Пока ЧтениеОтвета.Прочитать() Цикл
		
		Если ЧтениеОтвета.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоМассива Тогда
			ТекущийУровень = ТекущийУровень + 1;
		ИначеЕсли ЧтениеОтвета.ТипТекущегоЗначения = ТипЗначенияJSON.КонецМассива Тогда
			ТекущийУровень = ТекущийУровень - 1;
		ИначеЕсли ЧтениеОтвета.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства
			И ТекущийУровень = 1 Тогда
			
			Если ЧтениеОтвета.ТекущееЗначение = "classifierNick" Тогда
				ОписательВерсии = ДанныеКлассификаторов.Добавить();
				ОписательВерсии.Идентификатор = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "version" Тогда
				ОписательВерсии.Версия = ЗначениеСвойстваJSON(ЧтениеОтвета, 0);
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "fileUrl" Тогда
				ОписательВерсии.ИдентификаторФайла = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "hashSum" Тогда
				ОписательВерсии.КонтрольнаяСумма = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ИнтерактивнаяЗагрузка И ЧтениеОтвета.ТекущееЗначение = "versionDescription" Тогда
				ОписательВерсии.ОписаниеВерсии = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ИнтерактивнаяЗагрузка И ЧтениеОтвета.ТекущееЗначение = "fileSize" Тогда
				ОписательВерсии.Размер = ЗначениеСвойстваJSON(ЧтениеОтвета, 0);
			ИначеЕсли ИнтерактивнаяЗагрузка И ЧтениеОтвета.ТекущееЗначение = "classifierName" Тогда
				ОписательВерсии.Наименование = ЗначениеСвойстваJSON(ЧтениеОтвета, 0);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверка формата ответа.
	ХостСервиса = ХостСервисаКлассификаторов(ДоменРасположенияСерверовИПП);
	Для Каждого ОписательВерсии Из ДанныеКлассификаторов Цикл
		Если Не ЗначениеЗаполнено(ОписательВерсии.Идентификатор)
			Или Не ЗначениеЗаполнено(ОписательВерсии.Версия)
			Или Не ЗначениеЗаполнено(ОписательВерсии.ИдентификаторФайла) Тогда
			
			СообщениеОбОшибке = НСтр("ru = 'Неверный формат ответа Сервиса классификаторов.'");
			ЗаписатьИнформациюВЖурналРегистрации(СообщениеОбОшибке);
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецЕсли;
		
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ОписательВерсии.ИдентификаторФайла);
		Если Прав(НРег(СокрЛП(СтруктураURI.Хост)), 6) <> Прав(НРег(СокрЛП(ХостСервиса)), 6) Тогда
			
			СообщениеОбОшибке = НСтр("ru = 'Неверный адрес файла обновления классификатора.'");
			ЗаписатьИнформациюВЖурналРегистрации(СообщениеОбОшибке);
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вызов операции загрузки файлов /version/download/

// Выполняет загрузку файлов по переданным ранее URL.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию ОписаниеДанныхКлассификаторов();
//  ДанныеАутентификации  - Структура - параметры аутентификации пользователя Интернет-поддержки;
//
// Возвращаемое значение:
//   Структура - содержит результат операции:
//    *Ошибка - Булево - Истина, если в не удалось получить информацию из сервиса;
//    *СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//    *ИнформацияОбОшибке - Строка - описание ошибки для администратора.
//
Функция ЗагрузитьФайлыКлассификаторов(ДанныеКлассификаторов, ДанныеАутентификации)
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("КодОшибки",          "");
	РезультатОперации.Вставить("Ошибка",             Ложь);
	РезультатОперации.Вставить("СообщениеОбОшибке",  "");
	РезультатОперации.Вставить("ИнформацияОбОшибке", "");
	
	ПараметрыЗапросаJSON = versiondownload(ДанныеАутентификации);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод",                    "POST");
	ПараметрыОтправки.Вставить("Таймаут",                  1280);
	ПараметрыОтправки.Вставить("ФорматОтвета",             2);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки",       ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("Заголовки",                Заголовки);
	
	Для Каждого ОписательКлассификатора Из ДанныеКлассификаторов Цикл
		
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Получение файла классификатора: %1'"),
				ОписательКлассификатора.ИдентификаторФайла),
			Ложь);
		
		ИнтернетПоддержкаПользователей.ПроверитьURL(ОписательКлассификатора.ИдентификаторФайла);
		
		РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			ОписательКлассификатора.ИдентификаторФайла,
			,
			,
			ПараметрыОтправки);
		
		Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
			
			РезультатОперации.КодОшибки          = КодОшибкиФайлНеЗагружен();
			РезультатОперации.Ошибка             = Истина;
			РезультатОперации.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при получении файла классификатора %1: 
					|%2'"),
				ОписательКлассификатора.Идентификатор,
				РезультатОтправки.СообщениеОбОшибке);
				
			РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить файл классификатора %1.
					|%2
					|
					|Техническая информация об ошибке:
					|При загрузке файла сервис вернул ошибку.
					|Код ошибки: %3.
					|URL Файла: %4
					|Подробная информация:
					|%5'"),
				ОписательКлассификатора.Идентификатор,
				РезультатОперации.СообщениеОбОшибке,
				РезультатОперации.КодОшибки,
				ОписательКлассификатора.ИдентификаторФайла,
				РезультатОтправки.ИнформацияОбОшибке);
			ЗаписатьИнформациюВЖурналРегистрации(
				РезультатОперации.ИнформацияОбОшибке,
				Истина);
			
			Возврат РезультатОперации;
			
		КонецЕсли;
		
		КонтрольнаяСуммаФайл = ИнтернетПоддержкаПользователей.КонтрольнаяСуммаФайла(РезультатОтправки.Содержимое);
		Если ОписательКлассификатора.КонтрольнаяСумма <> КонтрольнаяСуммаФайл Тогда
			РезультатОперации.КодОшибки          = КодОшибкиФайлНеЗагружен();
			РезультатОперации.Ошибка             = Истина;
			РезультатОперации.СообщениеОбОшибке  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при получении файла классификатора %1: 
					|%2'"),
				ОписательКлассификатора.Идентификатор,
				НСтр("ru = 'Получен некорректный файл.'"));
				
			РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить файл классификатора %1.
					|Контрольная сумма полученного файла отличается от ожидаемой.'"),
				ОписательКлассификатора.Идентификатор);
			ЗаписатьИнформациюВЖурналРегистрации(РезультатОперации.ИнформацияОбОшибке);
			
			Возврат РезультатОперации;
		КонецЕсли;
		
		ОписательКлассификатора.АдресФайла = ПоместитьВоВременноеХранилище(РезультатОтправки.Содержимое);
		
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// /classifiers/version/download/.
//
Функция versiondownload(ДанныеАутентификации)
	
	// {
	//  "programNick":"nick",
	//  "login": "User",
	//  "password":"Pass"
	// }
	
	ИмяПрограммы = ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы();
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("programNick");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИмяПрограммы);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Логин);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

#КонецОбласти

#Область ИнтерактивноеОбновлениеКлассификаторов

// Проверяет наличие доступных обновлений классификаторов в Сервисе классификаторов.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - информация о доступных обновлениях:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//                    - "ОбновлениеНеТребуется" - загружены актуальные версии классификаторов;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *ДоступныеВерсии - Массив - содержит информацию о доступных обновлениях
//      **Идентификатор      - Строка - идентификатор классификатора в сервисе;
//      **Версия             - Строка - номер актуальной версии;
//      **КонтрольнаяСумма   - Число - контрольная сумма файла;
//      **ОписаниеВерсии     - Строка - описание изменений в версии;
//      **ИдентификаторФайла - Строка - идентификатор файла, который будет использован для загрузки;
//      **Размер             - Строка - размер файла;
//      **Наименование       - Строка - наименование классификатора;
//
Функция СлужебнаяДоступныеОбновленияКлассификаторов(Идентификаторы, ДанныеАутентификации) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("КодОшибки",          "");
	РезультатПроверки.Вставить("СообщениеОбОшибке",  "");
	РезультатПроверки.Вставить("ИнформацияОбОшибке", "");
	РезультатПроверки.Вставить("ДоступныеВерсии ",   Новый Массив);
	
	// 1. Проверка доступности обновления.
	ПроверитьДоступностьОбновления();
	
	// 2. Начальное заполнение номера версии,
	// для новых классификаторов.
	УстановитьНачальныйНомерВерсииКлассификаторов(Идентификаторы);
	
	// 3. Из сервиса загружается информация об актуальных версиях классификаторов,
	// а также ссылки на файл.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		// На момент получения поставляемых данных
		// права уже проверены.
		УстановитьПривилегированныйРежим(Истина);
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		РезультатОперации = МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы);
		
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		
		РезультатОперации = ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы,
			ДанныеАутентификации,
			Истина);
		
	КонецЕсли;
		
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатПроверки,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатПроверки;
	КонецЕсли;
	
	// 4. Если номер версии классификатора в базе равен номеру версии в сервисе,
	// обновление не будет загружено.
	УдалитьАктуальныеВерсии(РезультатОперации.ДанныеКлассификаторов, Идентификаторы);
	
	Для Каждого ОписательВерсии Из РезультатОперации.ДанныеКлассификаторов Цикл
		
		ИдентификаторФайла = Новый Структура;
		ИдентификаторФайла.Вставить("ИдентификаторФайла", ОписательВерсии.ИдентификаторФайла);
		ИдентификаторФайла.Вставить("КонтрольнаяСумма",   ОписательВерсии.КонтрольнаяСумма);
		
		ДанныеВерсии = Новый Структура;
		ДанныеВерсии.Вставить("Идентификатор",      ОписательВерсии.Идентификатор);
		ДанныеВерсии.Вставить("Наименование",       ОписательВерсии.Наименование);
		ДанныеВерсии.Вставить("Версия",             ОписательВерсии.Версия);
		ДанныеВерсии.Вставить("ОписаниеВерсии",     ОписательВерсии.ОписаниеВерсии);
		ДанныеВерсии.Вставить("Размер",             ОписательВерсии.Размер);
		ДанныеВерсии.Вставить("ИдентификаторФайла", ИдентификаторФайла);
		РезультатПроверки.ДоступныеВерсии.Добавить(ДанныеВерсии);
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Выполняет обновление данных классификаторов в фоновом задании.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для обновления;
//  АдресХранилища - Строка - адрес хранилища результат обновления.
//
Процедура ИнтерактивноеОбновлениеКлассификаторов(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",         "");
	РезультатОбновления.Вставить("СообщениеОбОшибке", "");
	
	ДанныеКлассификаторов = ПараметрыПроцедуры.ДанныеКлассификаторов;
	ДанныеКлассификаторов.Колонки.Добавить("АдресФайла", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	
	Если ПараметрыПроцедуры.РежимОбновления = 0 Тогда
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
			Возврат;
		КонецЕсли;
		
		РезультатЗагрузки = ЗагрузитьФайлыКлассификаторов(
			ДанныеКлассификаторов,
			Результат.ДанныеАутентификации);
		
		Если РезультатЗагрузки.Ошибка Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОбновления,
				РезультатЗагрузки,
				"КодОшибки, СообщениеОбОшибке");
			ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
			Возврат;
		КонецЕсли;
	Иначе
		Для Каждого ОписаниеКлассификатора Из ДанныеКлассификаторов Цикл
			Если ОписаниеКлассификатора.ДанныеФайла.Размер() = 0 Тогда
				ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Файл классификатора %1 (%2) имеет размер равный 0.
						|Загрузка данных остановлена.'"),
					ОписаниеКлассификатора.Наименование,
					ОписаниеКлассификатора.Идентификатор);
				ЗаписатьИнформациюВЖурналРегистрации(
					ТекстИсключения,
					Истина,
					Метаданные.Обработки.ОбновлениеКлассификаторов);
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			ОписаниеКлассификатора.АдресФайла = ПоместитьВоВременноеХранилище(ОписаниеКлассификатора.ДанныеФайла);
		КонецЦикла;
	КонецЕсли;
	
	НеОбработанныеКлассификаторы = ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов, Истина);
	
	Для Каждого ОписаниеКлассификатора Из ДанныеКлассификаторов Цикл
		УдалитьИзВременногоХранилища(ОписаниеКлассификатора.АдресФайла);
	КонецЦикла;
	
	Если НеОбработанныеКлассификаторы.Количество() > 0 Тогда
		Сч = 1;
		СообщениеОбОшибке = "";
		Для Каждого ОписаниеКлассификатора Из ДанныеКлассификаторов Цикл
			Если НеОбработанныеКлассификаторы.Найти(ОписаниеКлассификатора.Идентификатор) <> Неопределено Тогда
				СообщениеОбОшибке = СообщениеОбОшибке
					+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1. Версия %2 классификатора %3 не загружена;'"),
							Сч,
							ОписаниеКлассификатора.Версия,
							ОписаниеКлассификатора.Наименование)
						+ Символы.ПС;
				Сч = Сч + 1;
			КонецЕсли;
		КонецЦикла;
		РезультатОбновления.КодОшибки         = КодОшибкиНеОбработан();
			РезультатОбновления.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать обновления классификаторов:
					|
					|%1'"),
				СообщениеОбОшибке);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
	
КонецПроцедуры

// Обработка файла с обновлениями классификаторов в в фоновом задании.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для обновления;
//  АдресХранилища - Строка - адрес хранилища результат обновления.
//
Процедура ОбработатьФайлОбновленияКлассификаторов(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	ДанныеФайла = ПараметрыПроцедуры.ДанныеФайла;
	
	КаталогОбновлений = ФайловаяСистема.СоздатьВременныйКаталог(
		Строка(Новый УникальныйИдентификатор));
	
	ФайлыКлассификаторов = ПолучитьИмяВременногоФайла(".zip");
	ДанныеФайла.Записать(ФайлыКлассификаторов);
	ДанныеФайла = Неопределено;
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ФайлыКлассификаторов);
	ЧтениеZipФайла.ИзвлечьВсе(КаталогОбновлений);
	
	УдалитьФайлы(ФайлыКлассификаторов);
	
	ВерсииКлассификаторов = ДанныеКлассификаторовДляИнтерактивногоОбновления();
	
	// Начальное заполнение номера версии,
	// для новых классификаторов.
	Для каждого ОписаниеКлассификатора Из ВерсииКлассификаторов Цикл
		Если ОписаниеКлассификатора.Версия = 0 Тогда
			ОписаниеКлассификатора.Версия = ОбработатьНачальнуюВерсиюКлассификатора(
				ОписаниеКлассификатора.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеКлассификаторов = Новый Массив;
	ФайлыОбновлений = НайтиФайлы(КаталогОбновлений, "*");
	Для Каждого ФайлОбновления Из ФайлыОбновлений Цикл
		
		ТребуетсяЗагрузка         = Истина;
		КлассификаторИспользуется = Ложь;
		Наименование              = "";
		
		ПозицияРазделителя = СтрНайти(ФайлОбновления.ИмяБезРасширения, "_", НаправлениеПоиска.СКонца);
		
		// Если имя файла не соответствует шаблону [Идентификатор]_[Версия], подсистема не должна
		// выполнять его обработку.
		Если ПозицияРазделителя = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			Версия        = Число(СтрЗаменить(Сред(ФайлОбновления.ИмяБезРасширения, ПозицияРазделителя + 1), Символы.НПП, ""));
			Идентификатор = Лев(ФайлОбновления.ИмяБезРасширения, ПозицияРазделителя - 1);
		Исключение
			Версия        = Неопределено;
			Идентификатор = Неопределено;
		КонецПопытки;
		
		Для каждого ВерсияКлассификатора Из ВерсииКлассификаторов Цикл
			Если ВерсияКлассификатора.Идентификатор = Идентификатор Тогда
				КлассификаторИспользуется = Истина;
				Наименование              = ВерсияКлассификатора.Наименование;
				Если ВерсияКлассификатора.Версия >= Версия Тогда
					ТребуетсяЗагрузка = Ложь;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если КлассификаторИспользуется Тогда
			ОписаниеКлассификатора = Новый Структура;
			ОписаниеКлассификатора.Вставить("Отметка",             ТребуетсяЗагрузка);
			ОписаниеКлассификатора.Вставить("Наименование",        Наименование);
			ОписаниеКлассификатора.Вставить("Версия",              Версия);
			ОписаниеКлассификатора.Вставить("ТребуетсяОбновление", ТребуетсяЗагрузка);
			ОписаниеКлассификатора.Вставить("Идентификатор",       Идентификатор);
			Если ТребуетсяЗагрузка Тогда
				ОписаниеКлассификатора.Вставить("ДанныеФайла", Новый ДвоичныеДанные(ФайлОбновления.ПолноеИмя));
			Иначе
				ОписаниеКлассификатора.Вставить("ДанныеФайла", Неопределено);
			КонецЕсли;
			ОписаниеКлассификаторов.Добавить(ОписаниеКлассификатора);
		КонецЕсли;
		
	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйКаталог(КаталогОбновлений);
	ПоместитьВоВременноеХранилище(ОписаниеКлассификаторов, АдресХранилища);
	
КонецПроцедуры

// Получение всех идентификаторов классификаторов, наименований
// и версий, которые используются в конфигурации. Функция используется для
// интерактивной загрузки данных из обработки "Обновление классификаторов".
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе.
//
// Возвращаемое значение:
//  Массив - содержит настройки классификаторов.
//
Функция ДанныеКлассификаторовДляИнтерактивногоОбновления() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ВызватьИсключение НСтр("ru = 'Использование функции при работе в модели сервиса запрещено.'");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	РегВерсииКлассификаторов.Версия КАК Версия,
		|	РегВерсииКлассификаторов.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.ОбновлятьАвтоматически";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВерсииКлассификаторов = Новый Массив;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ОписаниеКлассификатора = Новый Структура;
		ОписаниеКлассификатора.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		ОписаниеКлассификатора.Вставить("Версия",        ВыборкаДетальныеЗаписи.Версия);
		ОписаниеКлассификатора.Вставить("Наименование",  ВыборкаДетальныеЗаписи.Наименование);
		
		ВерсииКлассификаторов.Добавить(ОписаниеКлассификатора);
	КонецЦикла;
	
	Возврат ВерсииКлассификаторов;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

// Выполняет обновления данных классификаторов и при необходимости
// создает задание, которые выполняет обработку областей данных.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию
//                          РаботаСКлассификаторами.ОписаниеДанныхКлассификаторов;
//  СообщитьПрогресс - Булево - если классификаторы загружаются в длительной операции,
//                     необходимо сообщать прогресс обновления.
//
// Возвращаемое значение:
//  Массив - классификаторы, данные которых не удалось обновить.
//
Функция ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов, СообщитьПрогресс = Ложь)
	
	// Формирование списка классификаторов, для которых необходимо запланировать
	// обновление областей данных.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Идентификаторы = ДанныеКлассификаторов.ВыгрузитьКолонку("Идентификатор");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор
			|ИЗ
			|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
			|ГДЕ
			|	ВерсииКлассификаторов.Идентификатор В(&Идентификаторы)
			|	И ВерсииКлассификаторов.ОбработкаРазделенныхДанных = ИСТИНА
			|	И ВерсииКлассификаторов.ОбщиеДанные = ИСТИНА";
		
		Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
		
		Идентификаторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Идентификатор");
	Иначе
		Идентификаторы = Новый Массив;
	КонецЕсли;
	
	НеОбработанныеКлассификаторы = Новый Массив;
	ОбновитьДанныеОбластей = Новый Массив;
	Сч = 1;
	Для Каждого ОписательФайла Из ДанныеКлассификаторов Цикл
		
		Обработан               = Ложь;
		ДополнительныеПараметры = Новый Структура;
		
		РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора(
			ОписательФайла.Идентификатор,
			ОписательФайла.Версия,
			ОписательФайла.АдресФайла,
			Обработан,
			ДополнительныеПараметры);
		
		Если Обработан Тогда
			
			Если Идентификаторы.Найти(ОписательФайла.Идентификатор) <> Неопределено Тогда
				НастройкиКлассификатора = Новый Структура;
				НастройкиКлассификатора.Вставить("Идентификатор",           ОписательФайла.Идентификатор);
				НастройкиКлассификатора.Вставить("Версия",                  ОписательФайла.Версия);
				НастройкиКлассификатора.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
				ОбновитьДанныеОбластей.Добавить(НастройкиКлассификатора);
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			УстановитьВерсиюКлассификатора(ОписательФайла.Идентификатор, ОписательФайла.Версия);
			УстановитьПривилегированныйРежим(Ложь);
			
		Иначе
			НеОбработанныеКлассификаторы.Добавить(ОписательФайла.Идентификатор);
		КонецЕсли;
		
		Если СообщитьПрогресс Тогда
			ДлительныеОперации.СообщитьПрогресс(100*Сч/ДанныеКлассификаторов.Количество());
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбновитьДанныеОбластей.Количество() <> 0 Тогда
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗапланироватьОбновлениеДанныхОбластей(
			ОбновитьДанныеОбластей);
		
	КонецЕсли;
	
	Возврат НеОбработанныеКлассификаторы;
	
КонецФункции

// Выполняет определение номера версии загруженного классификатора.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе.
//
Процедура УстановитьНачальныйНомерВерсииКлассификаторов(Идентификаторы)
	
	ВерсииКлассификаторов = ВерсииКлассификаторов(Идентификаторы);
	Для Каждого ОписаниеКлассификатора Из ВерсииКлассификаторов Цикл
		
		Если ОписаниеКлассификатора.Версия <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОбработатьНачальнуюВерсиюКлассификатора(ОписаниеКлассификатора.Идентификатор);
		
	КонецЦикла;
	
КонецПроцедуры

// Получение всех идентификаторов классификаторов
// и версий, которые используются в конфигурации.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе.
//
// Возвращаемое значение:
//  Массив - содержит настройки классификаторов.
//
Функция ВерсииКлассификаторов(Идентификаторы = Неопределено)
	
	ВерсииКлассификаторов = Новый Массив;
	
	ОбщиеДанные = Не (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
	ИмяРегистра = ?(
		ОбщиеДанные,
		"ВерсииКлассификаторов",
		"ВерсииКлассификаторовОбластейДанных");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	РегВерсииКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|	%2";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяРегистра,
		?(Идентификаторы = Неопределено,
			"",
			"ГДЕ
			|	РегВерсииКлассификаторов.Идентификатор В (&Идентификаторы)"));
	
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ОписаниеКлассификатора = Новый Структура;
		ОписаниеКлассификатора.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		ОписаниеКлассификатора.Вставить("Версия",        ВыборкаДетальныеЗаписи.Версия);
		
		ВерсииКлассификаторов.Добавить(ОписаниеКлассификатора);
	КонецЦикла;
	
	Возврат ВерсииКлассификаторов;
	
КонецФункции

// Определяет по коду состояния тип ошибку сервиса.
//
// Параметры:
//  КодСостояния - Число - код состояния ответа сервиса.
//
// Возвращаемое значение:
//  Строка - код ошибки сервиса.
//
Функция ПереопределитьКодОшибкиСервиса(КодСостояния)
	
	Если КодСостояния = 200 Тогда
		Возврат "";
	ИначеЕсли КодСостояния = 400 Тогда
		Возврат "НеизвестныйКлассификаторИлиПрограмма";
	ИначеЕсли КодСостояния = 401 Тогда
		Возврат "НетДоступаКПрограмме";
	ИначеЕсли КодСостояния = 403 Тогда
		Возврат КодОшибкиНеверныйЛогинИлиПароль();
	ИначеЕсли КодСостояния = 429 Тогда
		Возврат "ПревышеноКоличествоПопыток";
	ИначеЕсли КодСостояния = 503 Тогда
		Возврат "СервисВременноНеДоступен";
	ИначеЕсли КодСостояния = 500
		Или КодСостояния = 501
		Или КодСостояния = 502
		Или КодСостояния > 503 Тогда
		Возврат "ОшибкаСервиса";
	ИначеЕсли КодСостояния = 0 Тогда
		Возврат "ОшибкаПодключения";
	Иначе
		Возврат "НеизвестнаяОшибка";
	КонецЕсли;
	
КонецФункции

// Определяет по коду ошибки сообщение пользователю.
//
// Параметры:
//  КодОшибки - Строка - ошибка сервиса см. процедуру
//              ПереопределитьКодОшибкиСервиса.
//
// Возвращаемое значение:
//  Строка - сообщение пользователю.
//
Функция ПереопределитьСообщениеПользователю(КодОшибки)
	
	Если КодОшибки = "НеизвестныйКлассификаторИлиПрограмма" Тогда
		Возврат НСтр("ru = 'Классификатор или программа по идентификатору не обнаружены.'");
	ИначеЕсли КодОшибки = "НетДоступаКПрограмме" Тогда
		ЧастиСтрок = Новый Массив;
		ЧастиСтрок.Добавить(НСтр("ru = 'Доступ к обновлению классификатора невозможен, так как ваша программа не находится на официальной поддержке'"));
		ЧастиСтрок.Добавить(" ");
		ЧастиСтрок.Добавить(Новый ФорматированнаяСтрока("https://portal.1c.ru/support/",,,,"https://portal.1c.ru/support/"));
		Возврат Новый ФорматированнаяСтрока(ЧастиСтрок);
	ИначеЕсли КодОшибки = КодОшибкиНеверныйЛогинИлиПароль() Тогда
		Возврат НСтр("ru = 'Ошибка авторизации на Портале 1С:ИТС.
			|Подробнее см. в журнале регистрации.'");
	ИначеЕсли КодОшибки = "ПревышеноКоличествоПопыток" Тогда
		Возврат НСтр("ru = 'Превышено количество попыток ввода логина и пароля.
			|Проверьте правильность данных авторизации и повторите
			|попытку через 30 минут.'");
	ИначеЕсли КодОшибки = "СервисВременноНеДоступен" Тогда
		Возврат НСтр("ru = 'Не удалось подключиться к сервису классификаторов. Сервис временно недоступен.
			|Повторите попытку подключения позже.'");
	ИначеЕсли КодОшибки = "ОшибкаСервиса" Тогда
		Возврат НСтр("ru = 'Ошибка работы с сервисом классификаторов.'");
	ИначеЕсли КодОшибки = "ОшибкаПодключения" Тогда
		Возврат НСтр("ru = 'Не удалось подключиться к сервису классификаторов.'");
	ИначеЕсли КодОшибки = "НеизвестнаяОшибка" Тогда
		Возврат НСтр("ru = 'Неизвестная ошибка при подключении к сервису.'");;
	КонецЕсли;
	
КонецФункции

// Производит удаление актуальных версий классификаторов,
// которые определяются на основании данных ИБ.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию
//                          ОписаниеДанныхКлассификаторов()
//  Идентификаторы - Массив - список загруженных список идентификаторов
//                   классификаторов, которые необходимо обновить.
//
Процедура УдалитьАктуальныеВерсии(ДанныеКлассификаторов, Идентификаторы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	РегВерсииКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.Идентификатор В(&Идентификаторы)";
	
		
	ИмяТаблицы = ?(ИспользоватьДанныеОбласти(),
		"ВерсииКлассификаторовОбластейДанных",
		"ВерсииКлассификаторов");
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяТаблицы);
	
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	АктуальныеВерсии = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		
		НайденныеСтроки = ДанныеКлассификаторов.НайтиСтроки(Отбор);
		Для каждого ОписательВерсии Из НайденныеСтроки Цикл
			Если ОписательВерсии.Версия <= ВыборкаДетальныеЗаписи.Версия Тогда
				АктуальныеВерсии.Добавить(ОписательВерсии);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого ОписательВерсии Из АктуальныеВерсии Цикл
		ДанныеКлассификаторов.Удалить(ОписательВерсии);
	КонецЦикла;
	
КонецПроцедуры

// Проверяет права доступа на обновление данных классификаторов.
// Обновление может быть недоступно если:
//  - у пользователя нет прав на получение обновлений,
//  - при работе в модели сервиса обновления загружаются из поставляемых данных.
//
Процедура ПроверитьДоступностьОбновления()
	
	Если ЗагрузкаКлассификаторовДоступна() Тогда
		ВызватьИсключение НСтр("ru = 'Нарушение прав доступа.'");
	КонецЕсли;
	
КонецПроцедуры

// Проверяет права доступа на обновление данных классификаторов.
// Обновление может быть недоступно если:
//  - у пользователя нет прав на получение обновлений,
//  - при работе в модели сервиса обновления загружаются из поставляемых данных.
//
// Возвращаемое значение:
//  Булево - Истина, загрузка классификаторов доступна, если ложь
//           прав на загрузку не достаточно.
//
Функция ЗагрузкаКлассификаторовДоступна()
	
	ОбъектМетаданных = ?(ИспользоватьДанныеОбласти(),
		Метаданные.РегистрыСведений.ВерсииКлассификаторовОбластейДанных,
		Метаданные.РегистрыСведений.ВерсииКлассификаторов);
	
	Если Не ПравоДоступа("Чтение", ОбъектМетаданных) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Создает структуру настроек подключения к сервису классификаторов
//
Функция ИнициализироватьПараметрыОбновленияКлассификаторов()
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("НастройкиСоединения"   , ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами());
	ПараметрыЗагрузки.Вставить("НастройкиПроксиСервера", ПолучениеФайловИзИнтернета.НастройкиПроксиНаСервере());
	
	Возврат ПараметрыЗагрузки;
	
КонецФункции

// Определяет значение свойства из чтения JSON.
//
// Параметры:
//  ЧтениеОбъектаJSON    - ЧтениеJSON - чтение JSON для определения значения;
//  ЗначениеПоУмолчанию  - Неопределено, Строка, Число, Булево - определяет
//                         значение по умолчанию.
//
// Возвращаемое значение:
//  Неопределено, Строка, Число, Булево - значение.
//
Функция ЗначениеСвойстваJSON(ЧтениеОбъектаJSON, ЗначениеПоУмолчанию = Неопределено)
	
	ИмяСвойства = ЧтениеОбъектаJSON.ТекущееЗначение;
	
	ЧтениеОбъектаJSON.Прочитать();
	Если ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Строка Тогда
		Возврат СокрЛП(ЧтениеОбъектаJSON.ТекущееЗначение);
	ИначеЕсли ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Число
		Или ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Булево Тогда
		Возврат ЧтениеОбъектаJSON.ТекущееЗначение;
	ИначеЕсли ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Null
		Или ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Ничего Тогда
		Возврат ЗначениеПоУмолчанию;
	Иначе
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось прочитать значение свойства %1.
				|Некорректный тип значения свойства (%2).'"),
			ИмяСвойства,
			Строка(Строка(ЧтениеОбъектаJSON.ТипТекущегоЗначения)));
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецФункции

// Возвращает логин и пароль Интернет-поддержки.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//              аутентификации пользователя Интернет-поддержки:
//    *ДанныеАутентификации - Структура - параметры аутентификации пользователя Интернет-поддержки;
//    *ИнформацияОбОшибке   - Строка    - информация об ошибке для пользователя.
//    *Ошибка               - Строка    - признак наличия ошибки.
//
Функция ДанныеАутентификации()
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеАутентификации", Новый Структура);
	Результат.Вставить("ИнформацияОбОшибке",   "");
	Результат.Вставить("Ошибка",               Ложь);
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		ВызватьИсключение НСтр("ru = 'При работе в модели сервиса информация о классификаторах
			|загружается из поставляемых данных.'");
		
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		Результат.ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
		Если Результат.ДанныеАутентификации = Неопределено Тогда
			Результат.Ошибка             = Истина;
			Результат.ИнформацияОбОшибке =
				НСтр("ru = 'Для получения обновлений классификаторов необходимо подключить Интернет-поддержку пользователей.'");
			ЗаписатьИнформациюВЖурналРегистрации(Результат.ИнформацияОбОшибке);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавлен в запись JSON данные аутентификации.
//
// Параметры:
//  ЗаписьДанныхСообщения  - ЗаписьJSON - запись, в которую необходимо
//                           добавить данные аутентификации;
//  ДанныеАутентификации   - Структура - параметры аутентификации пользователя
//                         Интернет-поддержки. См. ДанныеАутентификации().
//
Процедура ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации)
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("authenticationInfo");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Логин);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

// Определяет URL для вызова сервиса классификаторов.
//
// Параметры:
//  Операция  - Строка - путь к ресурсу;
//  Домен     - Число  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - URL операции.
//
Функция URLОперацииСервисаКлассификаторов(Операция, Домен)
	
	Возврат "https://"
		+ ХостСервисаКлассификаторов(Домен)
		+ "/external-api"
		+ Операция;
	
КонецФункции

// Определяет хост для вызова сервиса классификаторов.
//
// Параметры:
//  Домен - Число  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - хост подключения.
//
Функция ХостСервисаКлассификаторов(Домен)
	
	
	Если Домен = 0 Тогда
		Возврат "classifier-repository.1c.ru";
	Иначе
		Возврат "classifier-repository.1c.eu";
	КонецЕсли;
	
КонецФункции

// Определяет доступность использования версий классификаторов
// загруженных в область данных.
//
// Возвращаемое значение:
//  Булево - если Истина, для определения версий необходимо использовать
//           регистр сведений ВерсииКлассификаторовОбластейДанных.
//
Функция ИспользоватьДанныеОбласти()
	
	Возврат (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
КонецФункции

// Возвращает имя события для журнала регистрации
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Работа с классификаторами'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Возвращает код ошибки "НеОбработан".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеОбработан()
	
	Возврат "НеОбработан";
	
КонецФункции

// Возвращает код ошибки "ФайлНеЗагружен".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиФайлНеЗагружен()
	
	Возврат "ФайлНеЗагружен";
	
КонецФункции

// Возвращает код ошибки "НеверныйЛогинИлиПароль".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеверныйЛогинИлиПароль()
	
	Возврат "НеверныйЛогинИлиПароль";
	
КонецФункции

#КонецОбласти

#КонецОбласти
