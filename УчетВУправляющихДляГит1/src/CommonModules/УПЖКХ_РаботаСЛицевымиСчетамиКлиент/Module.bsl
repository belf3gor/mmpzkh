#Область РаботаСЛицевымиСчетами

// Процедура открывает обработку помощник для открытия лицевого счета
//
// Параметры:
//  СтруктураНачальныхДанных - <Структура>
//                           - Содержит данные для заполнения начальных значений реквизитов обработки.
//
Процедура ОткрытиеЛицевогоСчета(СтруктураНачальныхДанных = Неопределено, Владелец = Неопределено, Отказ = Ложь) Экспорт
	
	// Проверяем корректность заполнения структуры с начальными данными.
	// Запрещаем открывать л/с на комнату.
	Если Не СтруктураНачальныхДанных = Неопределено И СтруктураНачальныхДанных.Свойство("Помещение")
	 И ЗначениеЗаполнено(УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(СтруктураНачальныхДанных.Помещение, "Родитель")) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Открывать лицевые счета на комнаты нельзя!", Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ФормаОбработки = ПолучитьФорму("Обработка.УПЖКХ_ПомощникОткрытияЛицевогоСчета.Форма",, Владелец);
	
	ФормаОбработки.РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый;
	
	Если СтруктураНачальныхДанных <> Неопределено Тогда
		Если СтруктураНачальныхДанных.Свойство("Помещение") Тогда
			ЗаполнитьЗначенияСвойств(ФормаОбработки.Объект, СтруктураНачальныхДанных);
		Иначе
			ФормаОбработки.Объект.Помещение = ПредопределенноеЗначение("Справочник.УПЖКХ_Помещения.ПустаяСсылка");
		КонецЕсли;
		Если СтруктураНачальныхДанных.Свойство("Здание")
		   И НЕ УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьЗначениеРеквизита(СтруктураНачальныхДанных.Здание, "ЭтоГруппа") Тогда
			ЗаполнитьЗначенияСвойств(ФормаОбработки.Объект, СтруктураНачальныхДанных);
		Иначе
			ФормаОбработки.Объект.Здание = ПредопределенноеЗначение("Справочник.КВП_Здания.ПустаяСсылка");
		КонецЕсли;
		// Если группа создаваемого л/с передана, то передаем ее в форму.
		Если СтруктураНачальныхДанных.Свойство("ГруппаЛС") Тогда
			ФормаОбработки.Объект.ГруппаЛС = СтруктураНачальныхДанных.ГруппаЛС;
		КонецЕсли;
	Иначе
		ФормаОбработки.Объект.Помещение = ПредопределенноеЗначение("Справочник.УПЖКХ_Помещения.ПустаяСсылка");
		ФормаОбработки.Объект.Здание    = ПредопределенноеЗначение("Справочник.КВП_Здания.ПустаяСсылка");
	КонецЕсли;
	
	ФормаОбработки.ОбработатьИзменениеРеквизитовФормы();
	
	ФормаОбработки.Открыть();
	
КонецПроцедуры // ОткрытиеЛицевогоСчета()

#КонецОбласти