
#Область ПрочиеПроцедурыИФункции

// Возвращает синоним для схемы учета.
//
Функция ПолучитьСинонимРаздела(ИмяМакета) Экспорт
	
	Возврат ОТР_СхемыУчетаПереопределяемый.ПолучитьСинонимРаздела(ИмяМакета);
	
КонецФункции

// Возвращает массив имен общих макетов со схемами учета.
//
Функция ПолучитьМассивСИменамиМакетовСхемУчета()
	
	Возврат ОТР_СхемыУчетаПереопределяемый.ПолучитьМассивСИменамиМакетовСхемУчета();
	
КонецФункции

// Возвращает список со схемами учета для объекта.
//
// Параметры:
// ИмяФормы - Строка - Имя управляемой формы.
// ВызовПриСозданииНаСервере - Булево - Устанавливается, когда массив со схемами учета для объекта получается для того,
//    чтобы определить необходимо ли разместить кнопку "Схема учета" на форме объекта.
//
Функция ПолучитьСписокСоСхемамиУчетаДляОбъекта(ИмяФормы, ВызовПриСозданииНаСервере = Ложь) Экспорт
	
	// Определяем наименование объекта по наименованию формы.
	ИмяОбъекта = ИмяФормы;
	
	// Во второй подстроке из имени формы, разделенной точкой, содержится имя объекта конфигурации.
	МассивПодстрокНаименованияФормы = СтрРазделить(ИмяОбъекта, ".", Ложь);
	Если МассивПодстрокНаименованияФормы.Количество() > 1 Тогда
		ИмяОбъекта = МассивПодстрокНаименованияФормы[1];
	КонецЕсли;
	
	СписокСхемУчетаДляОбъекта = Новый СписокЗначений;
	
	// Определим имена общих макетов со схемами учета.
	МассивСИменамиМакетовСхемУчета = ПолучитьМассивСИменамиМакетовСхемУчета();;
	
	Для Каждого ИмяМакета Из МассивСИменамиМакетовСхемУчета Цикл
		
		СхемаУчета = ПолучитьОбщийМакет(ИмяМакета);
		
		Если Не СхемаУчета.ЭлементыГрафическойСхемы.Найти(ИмяОбъекта) = Неопределено Тогда
			
			СписокСхемУчетаДляОбъекта.Добавить(ИмяМакета, ПолучитьСинонимРаздела(ИмяМакета));
			
			// Для определения нужно ли добавлять кнопку "Схема учета" на форму объекта
			// достаточно, чтобы данный объект присутствовал хотя бы на одной схеме учета.
			Если ВызовПриСозданииНаСервере Тогда
				Возврат СписокСхемУчетаДляОбъекта;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокСхемУчетаДляОбъекта;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыУправленияНаСервере

// Размещает кнопку "Схема учета" на форме.
//
// Параметры:
//  Форма          - УправляемаяФорма - форма, на которой необходимо разместить кнопку "Схема учета".
//  ГруппаКнопки   - ЭлементФормы - группа, в которую необходимо поместить кнопку "Схема учета",
//                                  по умолчанию размещается в командную панель формы.
//
Процедура ПриСозданииНаСервере(Форма, ГруппаКнопки = Неопределено) Экспорт
	
	// Определяем группу кнопки "Схема учета".
	Если ГруппаКнопки = Неопределено Тогда
		ГруппаКнопки = Форма.КоманднаяПанель;
	КонецЕсли;
	
	СписокСоСхемамиУчетаДляОбъекта = ПолучитьСписокСоСхемамиУчетаДляОбъекта(Форма.ИмяФормы, Истина);
	
	Если Не СписокСоСхемамиУчетаДляОбъекта.Количество() = 0 Тогда
		
		// Добавляем новую команду.
		КомандаПоказатьСхемуУчета = Форма.Команды.Добавить("Подключаемый_ОткрытьСхемуУчета");
		КомандаПоказатьСхемуУчета.Действие  = "Подключаемый_ОткрытьСхемуУчета";
		КомандаПоказатьСхемуУчета.Заголовок = "Схема учета";
		КомандаПоказатьСхемуУчета.Подсказка = "Переход к графической схеме ведения учета, в которой участвует данный объект";
		
		// Добавляем новую кнопку.
		КнопкаСхемаУчета = Форма.Элементы.Добавить("КнопкаСхемаУчета", Тип("КнопкаФормы"), ГруппаКнопки);
		КнопкаСхемаУчета.Вид         = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаСхемаУчета.ИмяКоманды  = "Подключаемый_ОткрытьСхемуУчета";
		КнопкаСхемаУчета.Картинка    = БиблиотекаКартинок.ОТР_СхемаУчетаКартинка;
		КнопкаСхемаУчета.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КнопкаСхемаУчета.ЦветФона    = ЦветаСтиля["ОТР_ЦветКнопкиСхемаУчета"];
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти