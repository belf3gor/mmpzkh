#Область ПрограммныйИнтерфейс

// Открывает форму для формирования чека.
//
// Параметры:
//  Форма						 - УправляемаяФорма - Владелец, который открывает форму чека.
//  ДокументОснование			 - ДокументСсылка - Ссылка на владельца чека.
//  ПараметрыФормированияЧека	 - Структура - Параметры для формирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыФормированияЧека().
//  ПараметрыОтправкиЧека		 - Структура - Параметры для отправки чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыОтправкиЧека().
//
Процедура ОткрытьФормуФормированияЧекаНПД(Форма, ДокументОснование, ПараметрыФормированияЧека, ПараметрыОтправкиЧека) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОснованиеЧека", ДокументОснование);
	ПараметрыФормы.Вставить("ПараметрыФормированияЧека", ПараметрыФормированияЧека);
	ПараметрыФормы.Вставить("ПараметрыОтправкиЧека", ПараметрыОтправкиЧека);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ОткрытьФорму("РегистрСведений.ЧекиНПД.Форма.Форма", ПараметрыФормы, Форма);
	
КонецПроцедуры

// Открывает форму для просмотра уже сформированного чека.
//
// Параметры:
//  Форма					 - УправляемаяФорма - Владелец, который открывает форму чека.
//  ДокументОснование		 - ДокументСсылка - Ссылка на владельца чека.
//  ПараметрыОтправкиЧека	 - Структура - Параметры для отправки чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыОтправкиЧека().
//
Процедура ОткрытьФормуЧекаНПД(Форма, ДокументОснование, ПараметрыОтправкиЧека) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОснованиеЧека", ДокументОснование);
	ПараметрыФормы.Вставить("ПараметрыОтправкиЧека", ПараметрыОтправкиЧека);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ОткрытьФорму("РегистрСведений.ЧекиНПД.Форма.Форма", ПараметрыФормы, Форма);
	
КонецПроцедуры

// Вызывает ожидание аннулирования чека в фоне, назначает оповещение при завершении.
//
// Параметры:
//  ДлительнаяОперация 	 - Структура - параметры длительной операции.
//  Форма					 - УправляемаяФорма - форма документа.
//
Процедура ОжидатьАннулированиеЧекаВФоне(ДлительнаяОперация, Форма) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьАннулированиеЧекаВФонеЗавершение", Форма);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

// Заполняет реквизит "Комментарий", в документе-основании чека НПД, причиной, по которой был аннулирован чек
//
// Параметры:
//  СведенияОЧеке - Структура - подробнее см. РегистрыСведений.ЧекиНПД.СведенияОЧеке
//  Форма - УправляемаяФорма - форма документа-основания для чека НПД, имеющая реквизит с именем "Объект", в состав
//                             которого входит реквизит "Комментарий"
//
Процедура ДобавитьПричинуАннулированияЧекаВКомментарий(СведенияОЧеке, Форма) Экспорт
	
	Если ЗначениеЗаполнено(СведенияОЧеке.ДатаАннулированияЧека)
		И СведенияОЧеке.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЧековНПД.Аннулирован") Тогда
		Если Не ЗначениеЗаполнено(Форма.Объект.Комментарий) Тогда
			Форма.Объект.Комментарий = СведенияОЧеке.ПричинаАннулирования;
			Если Не Форма.Модифицированность Тогда
				Форма.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
