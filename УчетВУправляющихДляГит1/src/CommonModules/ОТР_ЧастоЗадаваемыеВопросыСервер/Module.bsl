
#Область ПрочиеПроцедурыИФункции
// Возвращает закодированную в кодировке URL строку.
Функция ЗакодироватьСтрокуВURL(Строка) Экспорт
	
	Возврат КодироватьСтроку(Строка, СпособКодированияСтроки.КодировкаURL);
	
КонецФункции

#КонецОбласти

#Область ПроцедурыУправленияНаСервере

// Размещает кнопку "Часто задаваемые вопросы" на форме.
//
// Параметры:
//  Форма          - УправляемаяФорма - форма, в которой необходимо разместить кнопку "Часто задаваемые вопросы."
//  ГруппаКнопки   - ЭлементФормы - группа, в которую необходимо разместить кнопку "Часто задаваемые вопросы",
//                                  по умолчанию размещается в командную панель формы.
//
Процедура ПриСозданииНаСервере(Форма, ГруппаКнопки = Неопределено) Экспорт
	
	// Определяем группу кнопки "Часто задаваемые вопросы".
	Если ГруппаКнопки = Неопределено Тогда
		ГруппаКнопки = Форма.КоманднаяПанель;
	КонецЕсли;
	
	// Определяем наименование объекта по наименованию формы.
	ИмяОбъекта = Форма.ИмяФормы;
	
	ПолноеИмяОбъектаБезТочки = "";
	
	// В первой подстроке из имени формы, разделенной точкой, содержится тип объекта конфигурации,
	// а во второй подстроке - имя объекта.
	МассивПодстрокНаименованияФормы = СтрРазделить(ИмяОбъекта, ".", Ложь);
	Если МассивПодстрокНаименованияФормы.Количество() > 1 Тогда
		
		ТипОбъекта = МассивПодстрокНаименованияФормы[0];
		ИмяОбъекта = МассивПодстрокНаименованияФормы[1];
		
		// В имя команды будем вставлять полное имя объекта конфигурации, но так как в полном имени объекта есть символ ".",
		// а имя команды не может содержать данный символ, заменим его на символ "_".
		ПолноеИмяОбъектаБезТочки = ТипОбъекта + "_" + ИмяОбъекта;
		
	КонецЕсли;
	
	СтандартнаяЧастьИмениКоманды = ОТР_ЧастоЗадаваемыеВопросыКлиентСервер.ПолучитьСтандартнуюЧастьИмениКоманды();
	
	// Добавляем новую команду. В имени команды отражаем полное имя объекта конфигурации, форма которого передана в параметрах.
	// Символ "." в имени объекта заменен на "_".
	КомандаПоказатьЧастоЗадаваемыеВопросы = Форма.Команды.Добавить(СтандартнаяЧастьИмениКоманды + ПолноеИмяОбъектаБезТочки);
	КомандаПоказатьЧастоЗадаваемыеВопросы.Действие  = СтандартнаяЧастьИмениКоманды;
	КомандаПоказатьЧастоЗадаваемыеВопросы.Заголовок = "Часто задаваемые вопросы";
	КомандаПоказатьЧастоЗадаваемыеВопросы.Подсказка = "Часто задаваемые вопросы";
	
	// Добавляем новую кнопку.
	КнопкаЧастоЗадаваемыеВопросы = Форма.Элементы.Добавить("КнопкаЧастоЗадаваемыеВопросы", Тип("КнопкаФормы"), ГруппаКнопки);
	КнопкаЧастоЗадаваемыеВопросы.Вид         = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаЧастоЗадаваемыеВопросы.ИмяКоманды  = СтандартнаяЧастьИмениКоманды + ПолноеИмяОбъектаБезТочки;
	КнопкаЧастоЗадаваемыеВопросы.Картинка    = БиблиотекаКартинок.ОТР_ПрозрачнаяПодсказка;
	КнопкаЧастоЗадаваемыеВопросы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаЧастоЗадаваемыеВопросы.ЦветФона    = Новый Цвет(255, 225, 0);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеИнформацииОСпискеСтатейЧастоЗадаваемыхВопросов

////////////////////////////////////////////////////////////////////////////////////////
// Внимание! Список статей на часто задаваемые вопросы ведется в гугл-документе:
// https://docs.google.com/spreadsheets/d/1ITtruWjp5635V-kMNPy7I2sFNsQ0ZGH8WOO-ClJ7dXA

// Считывание данных из Регистра Сведений "УПЖКХ_СтатьиЧастоЗадаваемыхВопросов".
//
Функция ПрочитатьСписокСтатейЧастоЗадаваемыхВопросовИзРегистраСведений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОТР_СтатьиЧастоЗадаваемыхВопросов.НаименованиеСтатьи КАК НаименованиеСтатьи,
	|	ОТР_СтатьиЧастоЗадаваемыхВопросов.СсылкаНаСтатью КАК СсылкаНаСтатью,
	|	ОТР_СтатьиЧастоЗадаваемыхВопросов.ОписаниеНазначенияСтатьи КАК ОписаниеНазначенияСтатьи,
	|	ОТР_СтатьиЧастоЗадаваемыхВопросов.ОбъектыНазначения КАК ОбъектыНазначения,
	|	ОТР_СтатьиЧастоЗадаваемыхВопросов.ИмяГруппыСтатьи КАК ИмяГруппыСтатьи,
	|	ОТР_СтатьиЧастоЗадаваемыхВопросов.ГруппаОбъектовНазначения КАК ГруппаОбъектовНазначения
	|ИЗ
	|	РегистрСведений.ОТР_СтатьиЧастоЗадаваемыхВопросов КАК ОТР_СтатьиЧастоЗадаваемыхВопросов";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Получение таблицы ссылок на часто задаваемые вопросы.
//
// Возвращаемое значение:
// ТаблицаСсылокНаЧастоЗадаваемыеВопросы - таблица значений с колонками "ТемаВопроса", "Ссылка", "ОбъектМетаданных".
//
Функция ПолучитьСписокСтатейЧастоЗадаваемыхВопросов(ОбновитьСписокСтатей = Ложь)
	
	// Если пользователь пытается обновить список статей часто задаваемых вопросов вручную,
	// т.е. нажал на кнопку "Обновить список статей" на форме, то пытаемся обновить список статей,
	// затем считываем обновленные данные из регистра сведений "УПЖКХ_СтатьиЧастоЗадаваемыхВопросов".
	// В других случаях просто считываем данные из регистра сведений "УПЖКХ_СтатьиЧастоЗадаваемыхВопросов",
	// и если данный регистр пуст, то пытаемся обновить данные. Если обновление прошло успешно,
	// то считываем обновленные данные из регистра сведений заново.
	
	Если ОбновитьСписокСтатей Тогда
		ОбновитьСписокСтатейЧастоЗадаваемыхВопросов();
		СписокСтатей = ПрочитатьСписокСтатейЧастоЗадаваемыхВопросовИзРегистраСведений();
	Иначе
		СписокСтатей = ПрочитатьСписокСтатейЧастоЗадаваемыхВопросовИзРегистраСведений();
		Если СписокСтатей.Количество() = 0 Тогда
			Если ОбновитьСписокСтатейЧастоЗадаваемыхВопросов() Тогда
				СписокСтатей = ПрочитатьСписокСтатейЧастоЗадаваемыхВопросовИзРегистраСведений();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Таблица ссылок, заполняемая по данным регистра.
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы = Новый ТаблицаЗначений;
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Добавить("ТемаВопроса");
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Добавить("Ссылка");
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Добавить("ОбъектМетаданных");
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Добавить("ИмяГруппыСтатьи");
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Добавить("ЭтоВидео", Новый ОписаниеТипов("Булево"));
	
	// Техническая колонка. Нужна для того, чтобы отсортировать строки по имени группы так,
	// чтобы строки с незаполненной колонкой "ИмяГруппыСтатьи" оказались внизу.
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Добавить("Порядок");
	
	// Вспомогательные параметры для разбора макета.
	МассивИменОбъектов = Новый Массив;
	РазделительОбъектовВСтроке = ";";
	
	Для Каждого ТекСтрока Из СписокСтатей Цикл
		
		ИмяГруппыСтатьи                      = ТекСтрока.ИмяГруппыСтатьи;
		ОбъектМетаданныхГруппа               = ТекСтрока.ГруппаОбъектовНазначения;
		ТемаВопроса                          = ТекСтрока.НаименованиеСтатьи;
		Ссылка                               = ТекСтрока.СсылкаНаСтатью;
		ОписаниеНазначенияСтатьи             = ТекСтрока.ОписаниеНазначенияСтатьи;
		ОбъектМетаданных                     = ТекСтрока.ОбъектыНазначения;
		
		// Если в ячейке колонки макета "Имена объектов конфигурации" содержится несколько имен объектов,
		// то для каждого имени в новой таблице значений будем создавать отдельную строку.
		Если Найти(ОбъектМетаданных, РазделительОбъектовВСтроке) > 0 Тогда
			
			МассивИменОбъектов = СтрРазделить(ОбъектМетаданных, РазделительОбъектовВСтроке, Ложь);
			
		Иначе
			
			МассивИменОбъектов.Добавить(ОбъектМетаданных);
			
		КонецЕсли;
		
		Для Каждого ИмяОбъекта Из МассивИменОбъектов Цикл
			
			НоваяСтрока = ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Добавить();
			НоваяСтрока.ТемаВопроса      = ТемаВопроса;
			НоваяСтрока.Ссылка           = Ссылка;
			НоваяСтрока.ОбъектМетаданных = ИмяОбъекта;
			
			Если Найти(ОбъектМетаданныхГруппа, ИмяОбъекта) > 0 Тогда
				НоваяСтрока.ИмяГруппыСтатьи = ИмяГруппыСтатьи;
				НоваяСтрока.Порядок = 0;
			Иначе
				НоваяСтрока.ИмяГруппыСтатьи = "";
				НоваяСтрока.Порядок = 1;
			КонецЕсли;
			
			// Это видеоматериал, если содержит подстроку "видео".
			Если Найти(ВРег(ТемаВопроса), ВРЕГ("ВИДЕО")) > 0 Тогда
				НоваяСтрока.ЭтоВидео = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		// Обновление вспомогательных параметров.
		МассивИменОбъектов.Очистить();
		
	КонецЦикла;
	
	// Ссылки в таблице сортируются в алфавитном порядке по имени группы статьи, контенту и теме вопроса.
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Сортировать("Порядок, ИмяГруппыСтатьи, ЭтоВидео Убыв, ТемаВопроса");
	
	// Удалим техническую колонку.
	ТаблицаСсылокНаЧастоЗадаваемыеВопросы.Колонки.Удалить("Порядок");
	
	Возврат ТаблицаСсылокНаЧастоЗадаваемыеВопросы;
	
КонецФункции // ПолучитьСписокСтатейЧастоЗадаваемыхВопросов()

// Получение массива со строками из таблицы ссылок.
// Из таблицы выбираются строки со ссылками для объекта конфигурации и строки с общими ссылками.
//
// Параметры:
//  Объект - Имя объекта конфигурации.
//
// Возвращаемое значение: 
//  МассивСоСтрокамиТаблицыСсылкиДляОбъекта - массив, элементами которого являются строки таблицы значений.
//
Функция ПолучитьМассивСсылокНаЧастоЗадаваемыеВопросыДляОбъекта(ИмяОбъекта, ОбновитьСписокСтатей = Ложь) Экспорт
	
	ТаблицаСсылок = ПолучитьСписокСтатейЧастоЗадаваемыхВопросов(ОбновитьСписокСтатей);
	
	// Выбираем строки таблицы ссылок для объекта конфигурации.
	МассивСсылокНаЧастоЗадаваемыеВопросыДляОбъекта = ТаблицаСсылок.НайтиСтроки(Новый Структура("ОбъектМетаданных", ИмяОбъекта));
	
	// Выбираем строку таблицы ссылок, в которой содержится ссылка на все часто задаваемые вопросы.
	СсылкаВсеВопросы = ТаблицаСсылок.НайтиСтроки(Новый Структура("ОбъектМетаданных", "Все статьи"));
	
	// Добавляем ссылку на все часто задаваемые вопросы к ссылкам для объекта конфигурации.
	Для Каждого СтрокаСсылки Из СсылкаВсеВопросы Цикл
		
		МассивСсылокНаЧастоЗадаваемыеВопросыДляОбъекта.Добавить(СтрокаСсылки);
		
	КонецЦикла;
	
	Возврат МассивСсылокНаЧастоЗадаваемыеВопросыДляОбъекта;
	
КонецФункции // ПолучитьМассивСтрокТаблицыСсылокДляОбъекта(Объект)

#КонецОбласти

#Область ПолучениеИнформацииИзИсточникаВИнтернете

// Проверка наличия интернета у пользователя.
//
// Возвращаемое значение:
// КоличествоСтрокВГуглоФайле - количество строк в источнике гугл-файле.
//
Функция УстановитьКаналСвязиСИсточникомСтатейВИнтернете(КоличествоСтрокВГуглоФайле = 0) Экспорт
	
	HTTPСоединение = Новый HTTPСоединение("sheets.googleapis.com/", , , , , , Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено));
	ЗапросHTTP     = Новый HTTPЗапрос(ОТР_ЧастоЗадаваемыеВопросыПереопределяемый.ПолучитьАдресСтраницыСпискаЧастоЗадаваемыхВопросовКоличествоСтрокВДокументе());
	
	Попытка
		
		ОтветHTTPЧислоСтрок = HTTPСоединение.Получить(ЗапросHTTP);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	ТекстОтветаЧислоСтрок = СокрЛ(ОтветHTTPЧислоСтрок.ПолучитьТелоКакСтроку());
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстОтветаЧислоСтрок);
	СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	КоличествоСтрокВГуглоФайле = СтруктураОтвета.values.Получить(0).Получить(0);
	
	Возврат Истина;
	
КонецФункции

// Получение списка статей из гугл-файла с учетом количества строк, с помощью HTTP - запроса.
//
Функция ОбновитьСписокСтатейЧастоЗадаваемыхВопросов() Экспорт
	
	КоличествоСтрокВГуглоФайле = 0;
	Если УстановитьКаналСвязиСИсточникомСтатейВИнтернете(КоличествоСтрокВГуглоФайле) = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Получаем список статей из гугл-файла.
	HTTPСоединение       = Новый HTTPСоединение("sheets.googleapis.com/", , , , , , Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено));
	Запрос2HTTP          = Новый HTTPЗапрос(ОТР_ЧастоЗадаваемыеВопросыПереопределяемый.ПолучитьПолныйАдресСтраницыСпискаЧастоЗадаваемыхВопросовСУчетомКоличестваСтрокВДокументе(КоличествоСтрокВГуглоФайле));
	
	Попытка
		ОтветHTTPСтатьи = HTTPСоединение.Получить(Запрос2HTTP);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	ТекстОтветаСтатьи   = СокрЛ(ОтветHTTPСтатьи.ПолучитьТелоКакСтроку());
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстОтветаСтатьи);
	СтруктураОтветаСписокСтатей = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	НаборЗаписей = РегистрыСведений.ОТР_СтатьиЧастоЗадаваемыхВопросов.СоздатьНаборЗаписей();
	
	Для Каждого Элемент Из СтруктураОтветаСписокСтатей.values Цикл
		
		// Если информация о статье заполнена не полностью (должно быть 7 полей),
		// то такую статью не загружаем.
		Если Элемент.Количество() < 7 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.ИмяГруппыСтатьи                       = Элемент.Получить(1);
		НоваяЗапись.ГруппаОбъектовНазначения              = Элемент.Получить(2);
		НоваяЗапись.НаименованиеСтатьи                    = Элемент.Получить(3);
		НоваяЗапись.СсылкаНаСтатью                        = Элемент.Получить(4);
		НоваяЗапись.ОписаниеНазначенияСтатьи              = Элемент.Получить(5);
		НоваяЗапись.ОбъектыНазначения                     = Элемент.Получить(6);
		
	КонецЦикла;
	
	Попытка
		
		НачатьТранзакцию();
		
		НаборЗаписей.Записать();
		Константы.ОТР_ДатаОбновленияСпискаСтатейЧастоЗадаваемыхВопросов.Установить(ТекущаяДатаСеанса());
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ПолучениеИнформацииОСостоянииПодсистемыЧастоЗадаваемыхВопросов

// Получение информации о состоянии подсистемы часто задаваемых вопросов.
//
// Возвращаемое значение:
// Структура СостояниеПараметровЧастоЗадаваемыхВопросов. 
// Содержит параметры:
// ДатаПоследнегоОбновленияСпискаСтатей - возвращает дату последнего обновления списка статей часто задаваемых вопросов;
// КаналСвязиУспешноСоздан - возвращает успешность/не успешность создания канала связи между источником и пользователем;
// РегистрСведенийЗаполнен - возвращает сведения о заполненности данными регистра сведений УПЖКХ_СтатьиЧастоЗадаваемыхВопросов.
//
Функция ПолучитьИнформациюОСостоянииПодсистемыЧастоЗадаваемыхВопросов() Экспорт
	
	ДатаПоследнегоОбновленияСпискаСтатей          = Константы.ОТР_ДатаОбновленияСпискаСтатейЧастоЗадаваемыхВопросов.Получить();
	КаналСвязиУспешноСоздан                       = УстановитьКаналСвязиСИсточникомСтатейВИнтернете();
	РегистрСведенийЗаполнен                       = (ПолучитьСписокСтатейЧастоЗадаваемыхВопросов().Количество() = 0);
	
	СостояниеПараметровЧастоЗадаваемыхВопросов = Новый Структура;
	СостояниеПараметровЧастоЗадаваемыхВопросов.Вставить("ДатаПоследнегоОбновленияСпискаСтатей",          ДатаПоследнегоОбновленияСпискаСтатей);
	СостояниеПараметровЧастоЗадаваемыхВопросов.Вставить("КаналСвязиУспешноСоздан",                       КаналСвязиУспешноСоздан);
	СостояниеПараметровЧастоЗадаваемыхВопросов.Вставить("РегистрСведенийЗаполнен",                       РегистрСведенийЗаполнен);
	
	Возврат СостояниеПараметровЧастоЗадаваемыхВопросов;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеСпискаСтатейЧастоЗадаваемыхВопросовПоРегламентномуЗаданию

// Выполняет обновление списка статей Часто Задаваемых Вопросов по регламентному заданию.
//
Процедура ОТР_ОбновитьСписокСтатейЧастоЗадаваемыхВопросовЧерезИнтернет() Экспорт
	
	ОбновитьСписокСтатейЧастоЗадаваемыхВопросов();
	
КонецПроцедуры

#КонецОбласти


