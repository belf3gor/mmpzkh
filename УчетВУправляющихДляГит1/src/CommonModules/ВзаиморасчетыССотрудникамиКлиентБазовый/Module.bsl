
#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыИФункцииВедомостейНаВыплатуЗарплаты

// Обработчики событий формы

Процедура ВедомостьОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = ВзаиморасчетыССотрудникамиКлиент.ИмяСобытияИзмененияОплатыВедомости() Тогда
		Форма.УстановитьДоступностьЭлементов();
		Форма.УстановитьПредставлениеОплаты();
	КонецЕсли;
	
КонецПроцедуры

// Редактирование и представление параметров расчета.

Процедура ВедомостьИзменитьПараметрыРасчета(Форма, ОповещениеЗавершения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("СтароеОкругление", Форма.Объект.Округление);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Оповещение = Новый ОписаниеОповещения("ВедомостьИзменитьПараметрыРасчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	Форма.ПоказатьВыборИзМеню(Оповещение, Форма.СпособыОкругления, Форма.Элементы.ПараметрыРасчета);
	
КонецПроцедуры

Процедура ВедомостьИзменитьПараметрыРасчетаЗавершение(Выбор, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	СтароеОкругление = ДополнительныеПараметры.СтароеОкругление;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если Выбор <> Неопределено И Выбор.Значение <> СтароеОкругление И НЕ Форма.ТолькоПросмотр Тогда
		Форма.Объект.Округление = Выбор.Значение;
		Форма.РассчитатьНаСервере()
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Выбор = Неопределено);
	КонецЕсли;
	
КонецПроцедуры	
	
Функция ВедомостьПредставлениеПараметровРасчета(Форма) Экспорт

	ПредставлениеПараметровРасчета = Строка(Форма.Объект.Округление);
	
	Возврат ПредставлениеПараметровРасчета;
	
КонецФункции

Процедура ВедомостьПодобратьСотрудников(Форма) Экспорт
	
	НачалоПериодаПримененияОтбора = Форма.Объект.ПериодРегистрации;
	ОкончаниеПериодаПримененияОтбора = КонецМесяца(Форма.Объект.ПериодРегистрации);
	
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериоде(
		Форма.Элементы.Зарплата,
		Форма.Объект.Организация, Форма.Объект.Подразделение,
		НачалоПериодаПримененияОтбора, ОкончаниеПериодаПримененияОтбора,
		, Форма.АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры

Процедура ВедомостьОплатаПоказать(Форма, Элемент, НавигационнаяСсылка, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Форма.ОплатыСписок.Количество() = 0 Тогда
		Возврат
	ИначеЕсли Форма.ОплатыСписок.Количество() = 1 Тогда
		ПоказатьЗначение(, Форма.ОплатыСписок[0].Значение)
	Иначе
		Оповещение = Новый ОписаниеОповещения("ВедомостьОплатаПоказатьОповещение", ЭтотОбъект, Форма);
		//Форма.ОплатыСписок.ПоказатьВыборЭлемента(Оповещение, НСтр("ru = 'Документы выплаты по ведомости'"), Элемент)
		Форма.ПоказатьВыборИзМеню(Оповещение, Форма.ОплатыСписок, Элемент);
	КонецЕсли	
		
КонецПроцедуры

Процедура ВедомостьОплатаПоказатьОповещение(ВыбранныйЭлемент, Форма) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПоказатьЗначение(, ВыбранныйЭлемент.Значение)
	КонецЕсли	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
