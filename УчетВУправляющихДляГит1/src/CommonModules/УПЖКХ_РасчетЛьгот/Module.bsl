#Область РасчетЛьгот

// Функция определяет и возвращает индивидуальное ограничение потребления услуги
// для лицевого счета.
//
// Параметры:
//  ЛицевойСчет       - лицевой счет, на который установлена норма потребления;
//  ЛьготнаяКатегория - СправочникСсылка.КВП_ЛьготныеКатегории, ссылка на льготную категорию;
//  Услуга            - услуга, для которой определяется ограничение потребления услуги.
//
// Возвращаемое значение:
//  ограничение по объему услуги или Неопределено, если его нет.
// 
Функция ПолучитьИндивидуальноеОграничениеУслугиПоОбъемуНаЛицевойСчет(ЛицевойСчет, Услуга, ЛьготнаяКатегория) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_НастройкаИндивидуальныхОграниченийЛьгот.ОграничениеПоОбъемуУслуги
	|ИЗ
	|	РегистрСведений.УПЖКХ_НастройкаИндивидуальныхОграниченийЛьгот КАК УПЖКХ_НастройкаИндивидуальныхОграниченийЛьгот
	|ГДЕ
	|	УПЖКХ_НастройкаИндивидуальныхОграниченийЛьгот.ЛицевойСчет = &ЛицевойСчет
	|	И УПЖКХ_НастройкаИндивидуальныхОграниченийЛьгот.ЛьготнаяКатегория = &ЛьготнаяКатегория
	|	И УПЖКХ_НастройкаИндивидуальныхОграниченийЛьгот.Услуга = &Услуга";
	
	Запрос.УстановитьПараметр("ЛицевойСчет",       ЛицевойСчет);
	Запрос.УстановитьПараметр("ЛьготнаяКатегория", ЛьготнаяКатегория);
	Запрос.УстановитьПараметр("Услуга",            Услуга);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОграничениеПоОбъемуУслуги;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Функция возвращает дату окончания действия льготы или дату окончания приостановки льготы
// для записи в регистр сведений "Назначенные льготы".
// Например, если в документе "Установка льгот" дата прекращения льготы равна 31.07.2015,
// то в регистре сведений строка прекращения льготы должна быть с периодом 01.08.2015.
//
// Параметры:
//  ДатаОкончанияПериодаДействияИлиПриостановки - Дата окончания действия(приостановки) льготы из документа 
//                                                "Установка льгот" или "Закрытие лицевого счета".
//
// Возвращаемое значение:
//  Дата для записи в регистр сведений "Назначенные льготы".
// 
Функция ПолучитьДатуОкончанияДействияИлиПриостановкиЛьгот(ДатаОкончанияПериодаДействияИлиПриостановки) Экспорт
	
	Возврат КонецДня(ДатаОкончанияПериодаДействияИлиПриостановки) + 1;
	
КонецФункции // ПолучитьДатуОкончанияДействияИлиПриостановкиЛьгот()

// Дополняет запрос временной таблицей настроек предоставления и расчета льгот в разрезе услуг.
// Используется как для старого, так и для нового алгоритмов расчета льгот.
//
// Параметры:
//  Запрос       - запрос для дополнения;
//  Дата         - дата, на которую предоставляются сведения;
//  Организация  - организация ,по которой предоставляются сведения;
//  СписокУслуг  - массив услуг.
//
Процедура ДополнитьЗапросНастройкамиПредоставленияИРасчетаЛьготДляСпискаУслуг(Запрос, Период, Организация, СписокУслуг) Экспорт
	
	Если Запрос.МенеджерВременныхТаблиц = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	
	// Получим настройки расчета льгот по умолчанию.
	// Используются в редких случаях, когда настройки по группам услуг в учетной политике не заданы.
	НастройкиРасчетаЛьготПоУмолчанию = 
		РегистрыСведений.УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.ПолучитьНастройкиПредоставленияИРасчетаЛьготПоУмолчанию();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КВП_Услуги.Ссылка КАК Услуга
	|ПОМЕСТИТЬ ВрСписокУслуг
	|ИЗ
	|	Справочник.КВП_Услуги КАК КВП_Услуги
	|ГДЕ
	|	КВП_Услуги.Ссылка В(&СписокУслуг)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиРасчетаЛьготПоУмолчанию.ВидЖильцовКоторымПредоставляютсяЛьготы,
	|	НастройкиРасчетаЛьготПоУмолчанию.ТипЖильцовДляДеленияОбъемаНачисленнойУслуги,
	|	НастройкиРасчетаЛьготПоУмолчанию.ТипЖильцовДляДеленияОбъемаОграниченияЛьготы
	|ПОМЕСТИТЬ ВрНастройкиРасчетаЛьготПоУмолчанию
	|ИЗ
	|	&НастройкиРасчетаЛьготПоУмолчанию КАК НастройкиРасчетаЛьготПоУмолчанию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВрСписокУслуг.Услуга,
	|	УПЖКХ_ГруппировкиУслуг.ГруппаУслуг КАК ГруппаУслуг
	|ПОМЕСТИТЬ ВрСписокУслугИГрупп
	|ИЗ
	|	ВрСписокУслуг КАК ВрСписокУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_ГруппировкиУслуг КАК УПЖКХ_ГруппировкиУслуг
	|		ПО ВрСписокУслуг.Услуга = УПЖКХ_ГруппировкиУслуг.Услуга
	|			И (УПЖКХ_ГруппировкиУслуг.ВидГруппУслуг = ЗНАЧЕНИЕ(Справочник.УПЖКХ_ГруппыУслуг.НастройкиРасчётаЛьгот))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КВП_УчетнаяПолитикаТСЖСрезПоследних.Период,
	|	КВП_УчетнаяПолитикаТСЖСрезПоследних.Организация
	|ПОМЕСТИТЬ ВрУчетнаяПолитикаТСЖ
	|ИЗ
	|	РегистрСведений.КВП_УчетнаяПолитикаТСЖ.СрезПоследних(&Период, Организация = &Организация) КАК КВП_УчетнаяПолитикаТСЖСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.ГруппаУслуг,
	|	УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.ВидЖильцовКоторымПредоставляютсяЛьготы,
	|	УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаНачисленнойУслуги,
	|	УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаОграниченияЛьготы
	|ПОМЕСТИТЬ ВрНастройкиРасчетаЛьгот
	|ИЗ
	|	РегистрСведений.УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот КАК УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВрУчетнаяПолитикаТСЖ КАК ВрУчетнаяПолитикаТСЖ
	|		ПО УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.Период = ВрУчетнаяПолитикаТСЖ.Период
	|			И УПЖКХ_НастройкиПредоставленияИРасчетаЛьгот.Организация = ВрУчетнаяПолитикаТСЖ.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВрСписокУслугИГрупп.Услуга,
	|	ВрНастройкиРасчетаЛьгот.ВидЖильцовКоторымПредоставляютсяЛьготы,
	|	ВрНастройкиРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаНачисленнойУслуги,
	|	ВрНастройкиРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаОграниченияЛьготы,
	|	1 КАК Приоритет
	|ПОМЕСТИТЬ ВрПриоритетНастройкиРасчетаЛьгот
	|ИЗ
	|	ВрСписокУслугИГрупп КАК ВрСписокУслугИГрупп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВрНастройкиРасчетаЛьгот КАК ВрНастройкиРасчетаЛьгот
	|		ПО ВрСписокУслугИГрупп.ГруппаУслуг = ВрНастройкиРасчетаЛьгот.ГруппаУслуг
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВрСписокУслугИГрупп.Услуга,
	|	ВрНастройкиРасчетаЛьгот.ВидЖильцовКоторымПредоставляютсяЛьготы,
	|	ВрНастройкиРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаНачисленнойУслуги,
	|	ВрНастройкиРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаОграниченияЛьготы,
	|	2
	|ИЗ
	|	ВрСписокУслугИГрупп КАК ВрСписокУслугИГрупп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВрНастройкиРасчетаЛьгот КАК ВрНастройкиРасчетаЛьгот
	|		ПО (ВрНастройкиРасчетаЛьгот.ГруппаУслуг = ЗНАЧЕНИЕ(Справочник.УПЖКХ_ГруппыУслуг.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВрСписокУслугИГрупп.Услуга,
	|	ВрНастройкиРасчетаЛьготПоУмолчанию.ВидЖильцовКоторымПредоставляютсяЛьготы,
	|	ВрНастройкиРасчетаЛьготПоУмолчанию.ТипЖильцовДляДеленияОбъемаНачисленнойУслуги,
	|	ВрНастройкиРасчетаЛьготПоУмолчанию.ТипЖильцовДляДеленияОбъемаОграниченияЛьготы,
	|	3
	|ИЗ
	|	ВрСписокУслугИГрупп КАК ВрСписокУслугИГрупп
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВрНастройкиРасчетаЛьготПоУмолчанию КАК ВрНастройкиРасчетаЛьготПоУмолчанию
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВрПриоритетНастройкиРасчетаЛьгот.Услуга,
	|	МИНИМУМ(ВрПриоритетНастройкиРасчетаЛьгот.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ВрПриоритетНастройкиРасчетаЛьготИтог
	|ИЗ
	|	ВрПриоритетНастройкиРасчетаЛьгот КАК ВрПриоритетНастройкиРасчетаЛьгот
	|
	|СГРУППИРОВАТЬ ПО
	|	ВрПриоритетНастройкиРасчетаЛьгот.Услуга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВрПриоритетНастройкиРасчетаЛьгот.Услуга,
	|	ВрПриоритетНастройкиРасчетаЛьгот.ВидЖильцовКоторымПредоставляютсяЛьготы,
	|	ВрПриоритетНастройкиРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаНачисленнойУслуги,
	|	ВрПриоритетНастройкиРасчетаЛьгот.ТипЖильцовДляДеленияОбъемаОграниченияЛьготы
	|ПОМЕСТИТЬ НастройкиРасчетаЛьготПоУслугам
	|ИЗ
	|	ВрПриоритетНастройкиРасчетаЛьгот КАК ВрПриоритетНастройкиРасчетаЛьгот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВрПриоритетНастройкиРасчетаЛьготИтог КАК ВрПриоритетНастройкиРасчетаЛьготИтог
	|		ПО ВрПриоритетНастройкиРасчетаЛьгот.Услуга = ВрПриоритетНастройкиРасчетаЛьготИтог.Услуга
	|			И ВрПриоритетНастройкиРасчетаЛьгот.Приоритет = ВрПриоритетНастройкиРасчетаЛьготИтог.Приоритет";
	
	Запрос.УстановитьПараметр("Организация",                      Организация);
	Запрос.УстановитьПараметр("Период",                           Период);
	Запрос.УстановитьПараметр("СписокУслуг",                      СписокУслуг);
	Запрос.УстановитьПараметр("НастройкиРасчетаЛьготПоУмолчанию", НастройкиРасчетаЛьготПоУмолчанию);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти