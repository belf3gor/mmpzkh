
#Область СлужебныеПроцедурыИФункции

// Процедура проверяет правильность и заполненность полей доли собственности.
//
// Параметры:
//   ТаблицаЖильцов - таблица значений - Проживающий, Собственник, ДоляСобственностиЧислитель, ДоляСобственностиЗнаменатель.
//   Отказ - Булево - признак ошибки проверки заполнения.
//
// Процедура используется в следующих объектах:
// - документ "Установка собственников помещений";
// - документ "Изменение лицевого счета";
// - обработка "Помощник создания проживающего".
//
Процедура ПроверитьЗаполнениеДолейСобственностиНаПомещение(ТаблицаЖильцов, Отказ = Ложь) Экспорт
	
	// Из общей таблицы жильцов получим таблицу собственников.
	СтрокиСобственников = ТаблицаЖильцов.НайтиСтроки(Новый Структура("Собственник", Истина));
	врТаблицаСобственников = ТаблицаЖильцов.Скопировать(СтрокиСобственников);
	
	// Определяем количество собственников.
	КоличествоСобственников = врТаблицаСобственников.Количество();
	
	ИтоговыйЗнаменательДолиСобственности = врТаблицаСобственников.Итог("ДоляСобственностиЗнаменатель");
	
	// Если собственников несколько и доли введены - ДОЛЕВАЯ собственность.
	Если КоличествоСобственников > 1 И Не ИтоговыйЗнаменательДолиСобственности = 0 Тогда
		
		// Доли собственников не должны быть нулевыми.
		Для Каждого ТекСтрока Из врТаблицаСобственников Цикл
			
			ТекущаяДоля = ?(ТекСтрока.ДоляСобственностиЗнаменатель = 0, 0, ТекСтрока.ДоляСобственностиЧислитель / ТекСтрока.ДоляСобственностиЗнаменатель);
			Если ТекущаяДоля = 0 Тогда
				
				// Определим представление собственника, так как если проверка вызвана
				// из помощника создания жильца, то собственник на момент проверки может бы еще не определен.
				ПредставлениеСобственника = Строка(ТекСтрока.Проживающий);
				Если ПустаяСтрока(ПредставлениеСобственника) Тогда
					ПредставлениеСобственника = "<Новый>";
				КонецЕсли;
				
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Доля собственника """ + ПредставлениеСобственника + """ не может быть равна 0!", Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Сумма долей должна быть равна 1.
		Если НЕ Отказ Тогда
			
			// Приводим доли к общему знаменателю.
			ОбщийЗнаменатель = 1;
			Для Каждого ТекСтрока Из врТаблицаСобственников Цикл
				ОбщийЗнаменатель = ОбщийЗнаменатель * ТекСтрока.ДоляСобственностиЗнаменатель;
			КонецЦикла;
			
			// Определяем сумму числителей при общем знаменателе.
			СуммаЧислитель = 0;
			Для Каждого ТекСтрока Из врТаблицаСобственников Цикл
				
				СуммаЧислитель = СуммаЧислитель + ?(ТекСтрока.ДоляСобственностиЗнаменатель = 0, 0,
													ТекСтрока.ДоляСобственностиЧислитель * ОбщийЗнаменатель / ТекСтрока.ДоляСобственностиЗнаменатель);
				
			КонецЦикла;
			
			// Если общий числитель и общий знаменатель не равны, то сумма долей не равна 1.
			Если НЕ СуммаЧислитель = ОбщийЗнаменатель Тогда
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Сумма долей собственников не равна 1!", Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	// ОСОБЕННОСТЬ:
	// Если всего один собственник, то его доля собственности не должна заполняться - ИНДИВИДУАЛЬНАЯ собственность.
	ИначеЕсли КоличествоСобственников = 1 И Не ИтоговыйЗнаменательДолиСобственности = 0 Тогда
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Частная собственность считается индивидуальной, т.к. указан единственный собственник.
														 |При этом долю собственности указывать не нужно, необходимо очистить долю собственности!", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
