
#Область СлужебныеПроцедурыИФункции

// Проверяет установлены ли настройки для автоматического запуска сценария.
// В случае, если они установлены, запускается клиент тестирования и выполняется сценарий.
// Параметры:
//  ПараметрЗапуска - Строка - параметр запуска, который был передан через ключ командной строки /C.
//  Отказ - Булево - параметр, предназначенный для завершения сеанса менеджера тестирования, 
//                    в случае, если запуск сценария выполнен.
//
Процедура ЗапуститьСценарий(ПараметрЗапуска, Отказ) Экспорт
	
	НастройкиЗапускаСценария = ОТР_ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("НастройкиЗапускаСценария");

	Если НастройкиЗапускаСценария <> Неопределено Тогда
		Если НастройкиЗапускаСценария.ЗапускКлиента И ЗначениеЗаполнено(НастройкиЗапускаСценария.ПреобразованныйТекст) Тогда
			Если Не ПараметрЗапуска = "КлиентТестирования" Тогда
				
				ЗапуститьСистемуИВыполнитьСценарий(НастройкиЗапускаСценария.ПреобразованныйТекст, Отказ);
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Запускает клиент тестирования и выполняет в нем программный код сценария.
// Параметры:
//  ПреобразованныйТекст - Строка - Строка, содержащая текст исполняемого кода.
//  Отказ - Булево - Истина, если запуск сценария будет полностью выполнен.
//
Процедура ЗапуститьСистемуИВыполнитьСценарий(ПреобразованныйТекст, Отказ = Неопределено) Экспорт
	
	#Если НЕ ВебКлиент Тогда
	ЗапуститьСистему("/Debug /TESTCLIENT /CКлиентТестирования");
	
	Попытка
		Выполнить(ПреобразованныйТекст);
		Отказ = Истина;
	Исключение
		
		ПроверитьОшибку = Найти(ОписаниеОшибки(),"Тип не определен (ТестируемоеПриложение)");
		Если Не ПроверитьОшибку = 0 Тогда
			
			ТекстОшибки = "Выполняется запуск сценария действия пользователя, при этом пользовательский сеанс запущен не в режиме менеджера тестирования.
			|Выполнения сценария невозможно. Выполните запуск пользовательского сеанса в режиме менеджера тестирования (в конфигураторе Сервис - Параметры - Запуск 1С:Предприятия - Дополнительные)
			|либо в обработке ""Запуск сценариев действий пользователя"" отключите настройку автоматического выполнения сценария при запуске, если этого не требуется.";
			Сообщить(ТекстОшибки);
		Иначе
			Сообщить(ОписаниеОшибки());
		КонецЕсли;
		Отказ = Ложь;
		
	КонецПопытки;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти
