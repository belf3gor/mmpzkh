
#Область СлужебныеПроцедурыИФункции

// Процедура формирует запрос и получает данные с сайта.
//
Процедура ПолучитьДанныеССайта(ВидЗапроса, PINкод, ТекстОшибки) Экспорт
	
	имяВходящегоФайла = ПолучитьимяВременногоФайла("txt");
	
	// Новый адрес сайта:  otr-soft.ru.
	// Старый адрес сайта: www.vdgb-soft.ru.
	Соединение = Новый HTTPСоединение("otr-soft.ru",,,,,, Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено));
	
	СтрокаЗапроса = "smsuserverifi/exchange.php?request=" + Строка(ВидЗапроса) + "&PIN=" + Строка(PINкод);
	
	Попытка
		Соединение.Получить(СтрокаЗапроса, имяВходящегоФайла);
	Исключение
		ТекстОшибки = "Неудачная попытка интернет-соединения!";
		Возврат;
	КонецПопытки;
	
	Ответ = Новый ТекстовыйДокумент;
	Ответ.Прочитать(имяВходящегоФайла);
	
	// В зависимости от вида запроса, вызываем соответствующую процедуру обработки ответа.
	Если ВидЗапроса = "authorization" Тогда
		ОбработатьДанныеАвторизации(Ответ, ТекстОшибки);
	Иначе
		ОбработатьДанныеБаланса(Ответ, ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается для обработки ответа сайта в случае, если запрашивались данные авторизации.
//
Процедура ОбработатьДанныеАвторизации(Ответ, ТекстОшибки)
	
	// Обработка ответа сайта
	Кол_воСтрокОтвета = Ответ.КоличествоСтрок();
	
	Если (Кол_воСтрокОтвета = 0) ИЛИ (Кол_воСтрокОтвета > 5) Тогда
		ТекстОшибки = "Ошибка! Ответ сайта не распознан!";
		Возврат;
	ИначеЕсли Кол_воСтрокОтвета = 1 ИЛИ Кол_воСтрокОтвета = 2 Тогда
		// В процессе обработки возникла ошибка: PIN-код не найден или был использован ранее.
		Строка1_Result   = Ответ.ПолучитьСтроку(1);
	ИначеЕсли Кол_воСтрокОтвета = 5 Тогда
		// Разобъем ответ на отдельные строки и выделим нужные части.
		Строка1_Result   = Ответ.ПолучитьСтроку(1);
		Строка2_INN      = Сред(Ответ.ПолучитьСтроку(2), 6); // INN: 0123456789
		Строка3_Login    = Сред(Ответ.ПолучитьСтроку(3), 8); // Login: abc
		Строка4_Password = Сред(Ответ.ПолучитьСтроку(4), 11); // Password: 12345
		Строка5_Name     = Сред(Ответ.ПолучитьСтроку(5), 7); // Name: StreamSMS
	КонецЕсли;
	
	Если Строка1_Result="Result: success"  Тогда
	    СоздатьНовыйАккаунт(Строка2_INN, Строка3_Login, Строка4_Password, Строка5_Name);
	ИначеЕсли Строка1_Result="Result: error_invalid_PIN" Тогда
	    ТекстОшибки = "Ошибка получения данных с сайта! Введен неверный PIN-код!";
	ИначеЕсли Строка1_Result="Result: error_PIN_was_used" Тогда
	    ТекстОшибки = "Ошибка получения данных с сайта! Данные по указанному PIN-коду были переданы ранее!";
	КонецЕсли;
	
КонецПроцедуры

// Вызывается для обработки ответа сайта в случае, если запрашивались данные баланса.
//
Процедура ОбработатьДанныеБаланса(Ответ, ТекстОшибки)
	
	// Обработка ответа сайта
	Кол_воСтрокОтвета = Ответ.КоличествоСтрок();
	
	Если (Кол_воСтрокОтвета = 0) ИЛИ (Кол_воСтрокОтвета > 3) Тогда
		ТекстОшибки = "Ошибка! Ответ сайта не распознан!";
		Возврат;
	ИначеЕсли Кол_воСтрокОтвета = 1 ИЛИ Кол_воСтрокОтвета = 2 Тогда
		// В процессе обработки возникла ошибка: PIN-код не найден или был использован ранее.
		Строка1_Result   = Ответ.ПолучитьСтроку(1);
	ИначеЕсли Кол_воСтрокОтвета = 3 Тогда
		// Разобъем ответ на отдельные строки и выделим нужные части
		Строка1_Result   = Ответ.ПолучитьСтроку(1);
		Строка2_Login    = Сред(Ответ.ПолучитьСтроку(2), 8); // Login: abc
		Строка3_Balance  = Сред(Ответ.ПолучитьСтроку(3), 10); // Balance: 12345
	КонецЕсли;
	
	Если Строка1_Result="Result: success"  Тогда
		
		// Раньше здесь выполнялось создание документа поступление баланса.
		// Теперь данные баланса не хранятся в программе.
		
	ИначеЕсли Строка1_Result="Result: error_invalid_PIN" Тогда
		
		ТекстОшибки = "Ошибка получения данных с сайта! Введен неверный PIN-код!";
		
	ИначеЕсли Строка1_Result="Result: error_PIN_was_used" Тогда
		
		ТекстОшибки = "Ошибка получения данных с сайта! Данные по указанному PIN-коду были переданы ранее!";
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура создает новый элемент справочника "Настройки аккаунтов" по данным полученным с сайта.
//
Процедура СоздатьНовыйАккаунт(ИНН, Логин, Пароль, ИмяПользователя)
	
	НовыйАккаунт = Справочники.смсНастройкиАккаунтов.СоздатьЭлемент();

	НовыйАккаунт.Наименование = Логин;
	НовыйАккаунт.Организация = Справочники.Организации.НайтиПоРеквизиту("ИНН", ИНН);
	НовыйАккаунт.Пользователь = Логин;
	НовыйАккаунт.Пароль = Пароль;
	НовыйАккаунт.ИмяПользователяПоУмолчанию = ИмяПользователя;
	
	НовыйАккаунт.Записать();
	
КонецПроцедуры

#КонецОбласти