////////////////////////////////////////////////////////////////////////////////
// КадровыйУчетПереопределяемый: переопределяемые методы кадрового учета, работающие на стороне сервера.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура СоздатьВТОтветственныхЛиц(МенеджерВременныхТаблиц, МассивОбъектов, ПорядокЗаполнения) Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("ДокументСсылка.Отпуск"));
	Массив.Добавить(Тип("ДокументСсылка.БольничныйЛист"));
	Массив.Добавить(Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк"));
	Массив.Добавить(Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу"));
	Массив.Добавить(Тип("ДокументСсылка.НачислениеЗарплаты"));
	Массив.Добавить(Тип("ДокументСсылка.ДепонированиеЗарплаты"));
	Массив.Добавить(Тип("ДокументСсылка.СписаниеДепонированнойЗарплаты"));
	
	ТаблицаОтветственных = Новый ТаблицаЗначений;
	ТаблицаОтветственных.Колонки.Добавить("Документ",              Новый ОписаниеТипов(Массив));
	ТаблицаОтветственных.Колонки.Добавить("Руководитель",          Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаОтветственных.Колонки.Добавить("ДолжностьРуководителя", Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ТаблицаОтветственных.Колонки.Добавить("ГлавныйБухгалтер",      Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаОтветственных.Колонки.Добавить("Кассир",                Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаОтветственных.Колонки.Добавить("ДолжностьКассира",      Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ТаблицаОтветственных.Колонки.Добавить("Бухгалтер",             Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаОтветственных.Колонки.Добавить("Исполнитель",           Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаОтветственных.Колонки.Добавить("ДолжностьИсполнителя",  Новый ОписаниеТипов("СправочникСсылка.Должности"));
	
	Для Каждого СведенияОДокументе ИЗ МассивОбъектов Цикл
		
		ПорядокДозаполнения = Новый Структура();
		
		НоваяСтрока = ТаблицаОтветственных.Добавить();
		НоваяСтрока.Документ = СведенияОДокументе.Ссылка;
		
		Если ПорядокЗаполнения.Свойство("ЗаполнятьРуководителя") Тогда
			
			НоваяСтрока.Руководитель          = СведенияОДокументе.Руководитель;
			НоваяСтрока.ДолжностьРуководителя = СведенияОДокументе.ДолжностьРуководителя;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Руководитель) ИЛИ НЕ ЗначениеЗаполнено(НоваяСтрока.ДолжностьРуководителя) Тогда
				ПорядокДозаполнения.Вставить("ЗаполнятьРуководителя");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПорядокЗаполнения.Свойство("ЗаполнятьГлавногоБухгалтера") Тогда
			
			НоваяСтрока.ГлавныйБухгалтер = СведенияОДокументе.ГлавныйБухгалтер;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.ГлавныйБухгалтер) Тогда
				ПорядокДозаполнения.Вставить("ЗаполнятьГлавногоБухгалтера");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПорядокЗаполнения.Свойство("ЗаполнятьКассира") Тогда
			
			НоваяСтрока.Кассир = СведенияОДокументе.Кассир;
			
			Если СведенияОДокументе.ПолучитьОбъект().Метаданные().Реквизиты.Найти("ДолжностьКассира") <> Неопределено Тогда
				НоваяСтрока.ДолжностьКассира = СведенияОДокументе.ДолжностьКассира;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Кассир) ИЛИ НЕ ЗначениеЗаполнено(НоваяСтрока.ДолжностьКассира) Тогда
				ПорядокДозаполнения.Вставить("ЗаполнятьКассира");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПорядокЗаполнения.Свойство("ЗаполнятьБухгалтера") Тогда
			
			НоваяСтрока.Бухгалтер = СведенияОДокументе.Бухгалтер;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Бухгалтер) Тогда
				ПорядокДозаполнения.Вставить("ЗаполнятьБухгалтера");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПорядокЗаполнения.Свойство("ЗаполнятьИсполнителя") Тогда
			
			НоваяСтрока.Исполнитель           = СведенияОДокументе.Исполнитель;
			НоваяСтрока.ДолжностьИсполнителя  = СведенияОДокументе.ДолжностьИсполнителя;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Исполнитель) ИЛИ НЕ ЗначениеЗаполнено(НоваяСтрока.ДолжностьИсполнителя) Тогда
				ПорядокДозаполнения.Вставить("ЗаполнятьИсполнителя");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПорядокДозаполнения.Количество() = 0 Тогда
			Продолжить;
		Иначе
			ОтветственныеЛицаОрганизации = ОтветственныеЛицаБП.ОтветственныеЛица(СведенияОДокументе.Организация, СведенияОДокументе.Дата);
		КонецЕсли;
		
		Если ПорядокДозаполнения.Свойство("ЗаполнятьРуководителя") Тогда
			
			НоваяСтрока.Руководитель          = ?(ЗначениеЗаполнено(НоваяСтрока.Руководитель),
													НоваяСтрока.Руководитель,
													ОтветственныеЛицаОрганизации.Руководитель);
			НоваяСтрока.ДолжностьРуководителя = ?(ЗначениеЗаполнено(НоваяСтрока.ДолжностьРуководителя),
													НоваяСтрока.ДолжностьРуководителя,
													ОтветственныеЛицаОрганизации.РуководительДолжность);
		КонецЕсли;
		
		Если ПорядокДозаполнения.Свойство("ЗаполнятьГлавногоБухгалтера") Тогда
			НоваяСтрока.ГлавныйБухгалтер = ОтветственныеЛицаОрганизации.ГлавныйБухгалтер;
		КонецЕсли;
		
		Если ПорядокДозаполнения.Свойство("ЗаполнятьКассира") Тогда
			
			НоваяСтрока.Кассир                = ?(ЗначениеЗаполнено(НоваяСтрока.Кассир),
													НоваяСтрока.Кассир,
													ОтветственныеЛицаОрганизации.Кассир);
													
			НоваяСтрока.ДолжностьКассира      = ?(ЗначениеЗаполнено(НоваяСтрока.ДолжностьКассира),
													НоваяСтрока.ДолжностьКассира,
													ОтветственныеЛицаОрганизации.КассирДолжность);
		КонецЕсли;
		
		Если ПорядокДозаполнения.Свойство("ЗаполнятьБухгалтера") Тогда
			НоваяСтрока.Бухгалтер = ОтветственныеЛицаОрганизации.ГлавныйБухгалтер;
		КонецЕсли;
		
		Если ПорядокДозаполнения.Свойство("ЗаполнятьИсполнителя") Тогда
			
			НоваяСтрока.Исполнитель           = ?(ЗначениеЗаполнено(НоваяСтрока.Исполнитель),
													НоваяСтрока.Исполнитель,
													ОтветственныеЛицаОрганизации.ГлавныйБухгалтер);
			НоваяСтрока.ДолжностьИсполнителя  = ?(ЗначениеЗаполнено(НоваяСтрока.ДолжностьИсполнителя),
													НоваяСтрока.ДолжностьИсполнителя,
													ОтветственныеЛицаОрганизации.ГлавныйБухгалтерДолжность);
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОтветственных", ТаблицаОтветственных);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОтветственных.Документ,
	|	ТаблицаОтветственных.Руководитель,
	|	ТаблицаОтветственных.ДолжностьРуководителя,
	|	ТаблицаОтветственных.ГлавныйБухгалтер,
	|	ТаблицаОтветственных.Кассир,
	|	ТаблицаОтветственных.ДолжностьКассира,
	|	ТаблицаОтветственных.Бухгалтер,
	|	ТаблицаОтветственных.Исполнитель,
	|	ТаблицаОтветственных.ДолжностьИсполнителя
	|ПОМЕСТИТЬ ВТОтветственныеЛица
	|ИЗ
	|	&ТаблицаОтветственных КАК ТаблицаОтветственных";

	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти
