#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Валюта") Тогда
		Валюта = Параметры.Валюта;	
	Иначе
		Валюта = Параметры.ДоговорКонтрагента.ВалютаВзаиморасчетов;
	КонецЕсли;
	КратностьВалюты = Параметры.КратностьВзаиморасчетов;
	КурсВалюты      = Параметры.КурсВзаиморасчетов;
	ДатаУстановкиКурсаИКратности = Параметры.Дата;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьНаДату(Команда)
	
	// Выберем дату курса
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьНаДатуЗавершение", ЭтотОбъект);
	ПоказатьВводДаты(Оповещение, ДатаУстановкиКурсаИКратности, "Выберите дату курса", ЧастиДаты.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	// Заполним структуру возвращаемых параметров
	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("КратностьВалюты", КратностьВалюты);
	СтруктураВозвращаемыхЗначений.Вставить("КурсВалюты",      КурсВалюты);

	// Все сделали, можно закрывать форму
	Закрыть(СтруктураВозвращаемыхЗначений);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьНаДатуЗавершение(ВыбраннаяДата, ДополнительныеПараметры) Экспорт

	Если ВыбраннаяДата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаУстановкиКурсаИКратности = ВыбраннаяДата;
	
	// Если дата введена, заполним значение курса и кратности
	СтруктураВалюты = ПолучитьКурсВалюты(Валюта, ВыбраннаяДата);
	КурсВалюты      = СтруктураВалюты.Курс;
	КратностьВалюты = СтруктураВалюты.Кратность;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКурсВалюты(Знач Валюта, Знач ДатаУстановкиКурсаИКратности)
	
	Возврат	РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, ДатаУстановкиКурсаИКратности);

КонецФункции

#КонецОбласти


