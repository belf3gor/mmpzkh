#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МинимальнаяЭкономияНаДокумент  = 30;
	МаксимальнаяЭкономияНаДокумент = 70;
	
	ДанныеБаннера = Новый Структура("КоличествоПоступлений, КоличествоРеализаций", 0, 0);
	Параметры.Свойство("Баннер", Баннер);
	ЗаполнитьЗначенияСвойств(ДанныеБаннера, Баннер.ДанныеБаннера);
	
	СуммаЭкономии = (ДанныеБаннера.КоличествоПоступлений + ДанныеБаннера.КоличествоРеализаций) 
		* МаксимальнаяЭкономияНаДокумент;
	
	Если СуммаЭкономии > 10000 Тогда
		// Округляем сумму экономии до тысяч рублей
		СуммаЭкономии = Цел(СуммаЭкономии/1000)*1000;
	Иначе
		// Округляем сумму экономии до сотен рублей
		СуммаЭкономии = Цел(СуммаЭкономии/100)*100;
	КонецЕсли;
	
	СуммаЭкономииСтрокой = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(СуммаЭкономии,
		"рубль,рубля,рублей");
	
	ИтогДеталиЭкономии = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Можно сэкономить до %1 за год, если использовать 1С-ЭДО!'"), СуммаЭкономииСтрокой);
	
	ЗаголовокДеталиЭкономии = НСтр("ru = 'Простой расчет. За год в учете вы оформили:'");
	
	МассивПодстрок  = Новый Массив;
	
	Если ДанныеБаннера.КоличествоРеализаций > 0 Тогда
		КоличествоСтрокой = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			ДанныеБаннера.КоличествоРеализаций, "комплект,комплекта,комплектов");
		МассивПодстрок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '  • %1 документов реализации'"), КоличествоСтрокой));
	КонецЕсли;
	
	Если ДанныеБаннера.КоличествоПоступлений > 0 Тогда
		КоличествоСтрокой = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			ДанныеБаннера.КоличествоПоступлений, "комплект,комплекта,комплектов");
		МассивПодстрок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '  • %1 документов поступления'"), КоличествоСтрокой));
	КонецЕсли;
	
	Элементы.ДеталиЭкономииПоДокументам.Высота = МассивПодстрок.Количество();
	
	ДеталиЭкономииПоДокументам = Новый ФорматированнаяСтрока(СтрСоединить(МассивПодстрок, Символы.ПС));
	
	Если Баннер <> Неопределено Тогда
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Персонализированные предложения сервисов. Открыта рекламная форма'"),
			УровеньЖурналаРегистрации.Информация,
			,
			,
			Баннер.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключиться(Команда)
	
	ОбменСКонтрагентамиКлиент.ПомощникПодключенияКСервису1СЭДО();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти