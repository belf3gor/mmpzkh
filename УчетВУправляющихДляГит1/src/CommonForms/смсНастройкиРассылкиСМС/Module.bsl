/////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обновление состояния элементов
	УправлениеФормой(ЭтаФорма);
	
	// Получение данных регламентного задания.
	Задание                                   = смсОбщегоНазначенияСервер.ПолучитьРегламентноеЗаданиеСМСАвторассылкаСообщений();
	ВремяВыполненияАвторассылкиОт             = Задание.Расписание.ВремяНачала;
	ВремяВыполненияАвторассылкиДо             = Задание.Расписание.ВремяКонца;
	
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПриЗаписиНаСервере" формы.
//
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Задание = смсОбщегоНазначенияСервер.ПолучитьРегламентноеЗаданиеСМСАвторассылкаСообщений();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НаборКонстант.смсИспользоватьАвторассылку Тогда
		Если ЗначениеЗаполнено(ВремяВыполненияАвторассылкиОт) И ЗначениеЗаполнено(ВремяВыполненияАвторассылкиДо) Тогда
			Задание.Использование = Истина;
			Задание.Расписание.ДатаНачала  = ТекущаяДата();
			Задание.Расписание.ВремяНачала = ВремяВыполненияАвторассылкиОт;
			Задание.Расписание.ВремяКонца  = ВремяВыполненияАвторассылкиДо;
			Задание.Расписание.ПериодПовтораВТечениеДня = 1800;
		Иначе
			смсОбщегоНазначения.СообщитьПользователю("Заполните время выполнения авторассылки",,,, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НаборКонстант.смсУчетнаяЗаписьДляАвторассылки) Тогда
			ТекстСообщенияОбОшибке = "Необходимо указать Учетную запись!";
			смсОбщегоНазначения.СообщитьПользователю(ТекстСообщенияОбОшибке, ,"смсУчетнаяЗаписьДляАвторассылки", "НаборКонстант.смсУчетнаяЗаписьДляАвторассылки", Отказ);
		КонецЕсли;
		
	Иначе
		Задание.Использование = Ложь;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Задание.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

/////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" флага "ИспользоватьАвторассылку".
//
Процедура смсИспользоватьАвторассылкуПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

/////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработка нажатия гиперссылки "НастройкиШаблонов".
//
Процедура ПерейтиВНастройкиШаблонов(Команда)
	ОткрытьФорму("Справочник.смсШаблоны.ФормаСписка");
КонецПроцедуры

#КонецОбласти

/////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
// Процедура управляет свойствами элементов формы.
//
Процедура УправлениеФормой(Форма)
	
	Элементы      = Форма.Элементы;
	НаборКонстант = Форма.НаборКонстант;
	
	Элементы.ВремяВыполненияАвторассылкиОт.ТолькоПросмотр = НЕ НаборКонстант.смсИспользоватьАвторассылку;
	Элементы.ВремяВыполненияАвторассылкиДо.ТолькоПросмотр = НЕ НаборКонстант.смсИспользоватьАвторассылку;
	Элементы.смсУчетнаяЗаписьДляАвторассылки.Доступность                 = НаборКонстант.смсИспользоватьАвторассылку;
	
КонецПроцедуры

#КонецОбласти
