
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дерево = РеквизитФормыВЗначение("Реквизиты");
	
	СписокКолонок = ПолучитьИзВременногоХранилища(Параметры.ОписаниеКолонок);
	Для Каждого Колонка Из СписокКолонок Цикл
		
		Если Не ПустаяСтрока(Колонка.Родитель) Тогда
			УзелДерева = ДобавитьСтрокуРодителя(Дерево, СписокКолонок, Колонка.Родитель);
		Иначе
			УзелДерева = Дерево;
		КонецЕсли;
		
		СтрокаДерева = УзелДерева.Строки.Добавить();
		СтрокаДерева.Представление = Колонка.ПредставлениеКолонки;
		СтрокаДерева.Идентификатор = Колонка.Идентификатор;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "Реквизиты");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыРеквизиты

&НаКлиенте
Процедура РеквизитыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработкаВыбора(Элементы.Реквизиты.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОбработкаВыбора(Элементы.Реквизиты.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаВыбора(ДанныеТекущейСтроки)
	
	Если ДанныеТекущейСтроки.ПолучитьЭлементы().Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", ДанныеТекущейСтроки.Идентификатор);
	
	СтрокаРодитель = ДанныеТекущейСтроки.ПолучитьРодителя();
	Если СтрокаРодитель <> Неопределено Тогда
		Представление = СтрокаРодитель.Представление + ", " + ДанныеТекущейСтроки.Представление;
	Иначе
		Представление = ДанныеТекущейСтроки.Представление;
	КонецЕсли;
	Результат.Вставить("Представление", Представление);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьСтрокуРодителя(Дерево, СписокКолонок, ИдентификаторРодителя)
	
	ОписаниеРодителя = СписокКолонок.Найти(ИдентификаторРодителя, "Родитель");
	Если ОписаниеРодителя = Неопределено Тогда
		Возврат Дерево;
	КонецЕсли;
	
	УзелДерева = Дерево.Строки.Найти(ИдентификаторРодителя, "Идентификатор", Истина);
	Если УзелДерева <> Неопределено Тогда
		Возврат УзелДерева;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОписаниеРодителя.Родитель) Тогда
		УзелДерева = ДобавитьСтрокуРодителя(Дерево, СписокКолонок, ОписаниеРодителя.Родитель);
	Иначе
		УзелДерева = Дерево.Строки;
	КонецЕсли;
	
	СтрокаДерева = УзелДерева.Добавить();
	СтрокаДерева.Представление = ОписаниеРодителя.ПредставлениеКолонки;
	СтрокаДерева.Идентификатор = ОписаниеРодителя.Идентификатор;
	
	Возврат СтрокаДерева;
	
КонецФункции

#КонецОбласти
