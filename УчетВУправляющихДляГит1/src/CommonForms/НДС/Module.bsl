&НаКлиенте
Перем ОбновитьИнтерфейс;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ПорядокНумерацииСчетовФактур.ТолькоПросмотр = 
		НЕ ПравоДоступа("Изменение", Метаданные.Константы.ОтдельнаяНумерацияСчетовФактурНаАванс);
	
	ПорядокНумерацииСчетовФактур = Константы.ОтдельнаяНумерацияСчетовФактурНаАванс.Получить();
	
	Параметры.Свойство("ПараметрыОткрытия", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТипЗнч(ПараметрыОткрытия) = Тип("Структура") Тогда
		АктивныйЭлемент = Неопределено;
		ПараметрыОткрытия.Свойство("АктивныйЭлемент", АктивныйЭлемент);
		
		Если ТипЗнч(АктивныйЭлемент) = Тип("Строка") Тогда
			
			Если АктивныйЭлемент = "ПорядокНумерацииСчетовФактур" Тогда
				
				ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Найти("ПорядокНумерацииСчетовФактур");
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПорядокНумерацииСчетовФактурПриИзменении(Элемент)
	
	ОтдельнаяНумерацияСчетовФактурНаАванс = ?(ПорядокНумерацииСчетовФактур = 1, Истина, Ложь);
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер("ОтдельнаяНумерацияСчетовФактурНаАванс");
	
	Оповестить("ИзменениеНастроекПараметровУчета");
	
	ОбновитьИнтерфейс = Истина;
	ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СохранитьЗначениеРеквизита(КонстантаИмя)
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = ЭтотОбъект[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
	КонецЕсли;
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяКонстанты)
	
	КонстантаИмя = СохранитьЗначениеРеквизита(ИмяКонстанты);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти