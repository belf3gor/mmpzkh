#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("СтрокаВозвращаемыхРеквизитов", СтрокаВозвращаемыхРеквизитов); 
	Если ЗначениеЗаполнено(СтрокаВозвращаемыхРеквизитов) Тогда
		ДанныеОбъекта = Новый Структура(СтрокаВозвращаемыхРеквизитов);
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Параметры);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеОбъекта);
		
		Если ДанныеОбъекта.ОсновноеМестоРаботы Тогда
			ОсновноеМестоРаботы =  0
		Иначе
			ОсновноеМестоРаботы =  1
		КонецЕсли;
	КонецЕсли;
	
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодПричиныНетрудоспособности(Элементы.КодПричиныНетрудоспособности);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодПричиныНетрудоспособности(Элементы.ВторойКодПричиныНетрудоспособности);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораДополнительныйКодПричиныНетрудоспособности(Элементы.ДополнительныйКодПричиныНетрудоспособности);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодНарушенияРежима(Элементы.КодНарушенияРежима);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораГруппаИнвалидности(Элементы.ГруппаИнвалидности);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораИное(Элементы.НовыйСтатусНетрудоспособного);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодУсловийИсчисления(Элементы.УсловияИсчисленияКод1);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодУсловийИсчисления(Элементы.УсловияИсчисленияКод2);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодУсловийИсчисления(Элементы.УсловияИсчисленияКод3);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораТипРодственнойСвязи(Элементы.ПоУходуРодственнаяСвязь1);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораТипРодственнойСвязи(Элементы.ПоУходуРодственнаяСвязь2);
	
	Элементы.НомерЛисткаПоОсновномуМестуРаботы.Видимость = (ОсновноеМестоРаботы <> 0);
	ОбновитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ВведенныеДанные = Новый Структура(СтрокаВозвращаемыхРеквизитов);
	ЗаполнитьЗначенияСвойств(ВведенныеДанные, ЭтаФорма);
	Если ОсновноеМестоРаботы = 0 Тогда
		ВведенныеДанные.Вставить("ОсновноеМестоРаботы", Истина);
	Иначе
		ВведенныеДанные.Вставить("ОсновноеМестоРаботы", Ложь);
	КонецЕсли;
	Оповестить("ВведеныДанныеЛисткаНетрудоспособности", ВведенныеДанные, ЭтаФорма.ВладелецФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МедицинскаяОрганизацияПриИзменении(Элемент)
	МедицинскаяОрганизацияПриИзмененииНаСервере()
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура МедицинскаяОрганизацияПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(МедицинскаяОрганизация) Тогда
		РеквизитыЛПУ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МедицинскаяОрганизация, "Наименование, ОГРН, Адрес");
		НаименованиеМедицинскойОрганизации = РеквизитыЛПУ.Наименование;
		ОГРНМедицинскойОрганизации = РеквизитыЛПУ.ОГРН;
		АдресМедицинскойОрганизации = РеквизитыЛПУ.Адрес;
	Иначе
		НаименованиеМедицинскойОрганизации = "";
		ОГРНМедицинскойОрганизации = "";
		АдресМедицинскойОрганизации = "";
	КонецЕсли;
	
	ОбновитьФорму();
КонецПроцедуры

&НаСервере
Процедура ОбновитьФорму()
	Если ЗначениеЗаполнено(МедицинскаяОрганизация) Тогда
		МедицинскаяОрганизацияДопИнформация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ОГРН: %1, Адрес: %2.'"),
			ОГРНМедицинскойОрганизации,
			АдресМедицинскойОрганизации);
	Иначе
		МедицинскаяОрганизацияДопИнформация = "";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
