#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// В Параметры.Сертификат находится структура с данными сертификата.
	Сертификат = Параметры.Сертификат;
	Отпечаток = УниверсальныйОбменСБанкамиКлиентСервер.ДвоичныеДанныеВСтроку(Сертификат.Отпечаток);
	ЭтоЭлектроннаяПодписьВМоделиСервиса = УниверсальныйОбменСБанкамиВызовСервера.ЭтоОблачныйСертификатПользователя(Отпечаток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Просмотр сертификата: %1'"), Сертификат.Наименование);
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Сертификат,, "Отпечаток");
	
	МодульУниверсальныйОбменСБанкамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УниверсальныйОбменСБанкамиКлиент");
	
	СерийныйНомер = Строка(Сертификат.СерийныйНомер);
	
	СвойстваВладельца = Сертификат.Субъект;
	СвойстваВладельца.Свойство("CN", ВладелецОбщееИмя);
	СвойстваВладельца.Свойство("O", ВладелецОрганизация);
	СвойстваВладельца.Свойство("OU", ВладелецПодразделение);
	
	СвойстваПоставщика = Сертификат.Издатель;
	СвойстваПоставщика.Свойство("CN", ПоставщикОбщееИмя);
	СвойстваПоставщика.Свойство("O", ПоставщикОрганизация);
	СвойстваПоставщика.Свойство("OU", ПоставщикПодразделение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервереБезКонтекста
Функция ПроверитьВСервисе(Отпечаток)
	
	ОблачныйСертификат = УниверсальныйОбменСБанками.НайтиОблачныйСертификатВХранилище(Отпечаток);
	Если ОблачныйСертификат <> Неопределено Тогда
		РезультатПроверки = СервисКриптографии.ПроверитьСертификат(ОблачныйСертификат.Сертификат);
		Возврат РезультатПроверки;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура Проверить(Команда)
	
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		Оповещение = Новый ОписаниеОповещения("ПроверитьПослеИнициализацииОблачнойКриптографии", ЭтотОбъект);
		УниверсальныйОбменСБанкамиКлиент.ИнициализироватьСервисКриптографии(Отпечаток, Оповещение);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ПроверитьЗавершение", ЭтотОбъект);
		УниверсальныйОбменСБанкамиКлиент.ПроверитьСертификат(Оповещение, Отпечаток);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПослеИнициализацииОблачнойКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Выполнено Тогда
		РезультатПроверки = ПроверитьВСервисе(Отпечаток);
		Если РезультатПроверки <> Неопределено Тогда
			Если РезультатПроверки Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Сертификат действителен'"));
			Иначе
				ПоказатьПредупреждение(, НСтр("ru = 'Сертификат не действителен.'"));
			КонецЕсли;
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, Результат.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат.Выполнено И Результат.Валиден Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Сертификат действителен'"));
	ИначеЕсли Результат.Выполнено И Не Результат.Валиден Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Сертификат не действителен.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		АдресФайла = ПолучитьСертификатНаСервере(Отпечаток, УникальныйИдентификатор);
		Если ЗначениеЗаполнено(АдресФайла) Тогда
			ПолучитьФайл(АдресФайла, Отпечаток + ".cer", Истина);
		КонецЕсли;
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ОткрытьСертификатПослеЭкспортаСертификата", ЭтотОбъект);
		ОтпечатокСтрока = УниверсальныйОбменСБанкамиКлиент.ДвоичныеДанныеВСтроку(Сертификат.Отпечаток);
		УниверсальныйОбменСБанкамиКлиент.ЭкспортироватьСертификатВФайл(
			Оповещение, ОтпечатокСтрока, ОтпечатокСтрока + ".cer");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификатПослеЭкспортаСертификата(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат.Выполнено Тогда
		УниверсальныйОбменСБанкамиКлиент.ОткрытьФайл(Результат.ИмяФайлаСертификата);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось открыть сертификат.'"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСертификатНаСервере(Отпечаток, УникальныйИдентификатор)
	
	СертификатИзХранилища = УниверсальныйОбменСБанками.НайтиОблачныйСертификатВХранилище(Отпечаток);
	
	Если СертификатИзХранилища = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	АдресФайла = ПоместитьВоВременноеХранилище(СертификатИзХранилища.Сертификат, УникальныйИдентификатор);
	Возврат АдресФайла;
	
КонецФункции

#КонецОбласти