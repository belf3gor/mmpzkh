
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Область ПроцедурыОбщегоНазначения

&НаСервере
// Процедура заполняет форму элементами в соответствии с найденными
// внешними формами квитанциями.
//
Процедура ЗаполнитьФорму()
	
	МассивФорм = Параметры.МассивФорм;
	
	// Счетчик, нужен для именования добавляемые элементов формы.
	Сч = 1;
	
	Для Каждого Структура Из МассивФорм Цикл
		
		НоваяСтрока = ТаблицаКвитанцийСВозможнымиОшибками.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Структура);
		
		НаименованиеЭлементаГиперссылки = НазваниеНадписи + Формат(Сч, "ЧГ=");
		НоваяСтрока.НаименованиеЭлементаГиперссылки = НаименованиеЭлементаГиперссылки;
		
		Группа = Элементы.Добавить("Группа" + Формат(Сч, "ЧГ="), Тип("ГруппаФормы"));
		Группа.Вид                      = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.РастягиватьПоГоризонтали = Истина;
		
		Надпись = Элементы.Добавить(НаименованиеЭлементаГиперссылки, Тип("ДекорацияФормы"), Группа);
		Надпись.Вид = ВидДекорацииФормы.Надпись;
		Надпись.Гиперссылка = Истина;
		Надпись.Заголовок   = Структура.ФормаКвитанции;
		Надпись.УстановитьДействие("Нажатие", "НажатиеСсылки");
		
		Пояснение = Элементы.Добавить("Пояснение" + Формат(Сч, "ЧГ="), Тип("ДекорацияФормы"));
		Пояснение.Вид       = ВидДекорацииФормы.Надпись;
		Пояснение.Заголовок = Структура.Пояснение;
		
		Сч = Сч + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
// Процедура открывает форму квитанции, соответствующую надписи,
// на которое было произведено нажатие.
//  
// Параметры:
//  КодКвитанции - Строка - код элемента справочника "Внешние обработки".
//
Функция ПолучитьСсылкуКвитанцииПоКоду(КодКвитанции)
	
	ФормаКвитанции = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию(КодКвитанции);
	
	Возврат ФормаКвитанции;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НазваниеНадписи = "Квитанция";
	
	ЗаполнитьФорму();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события "Нажатие" надписей формы.
//
Процедура НажатиеСсылки(Элемент)
	
	КодКвитанции     = Элемент.Заголовок;
	ВнешняяКвитанция = ПолучитьСсылкуКвитанцииПоКоду(КодКвитанции);
	
	Если ВнешняяКвитанция.Пустая() Тогда
		
		СтрокиФормыКвитанции = ТаблицаКвитанцийСВозможнымиОшибками.НайтиСтроки(Новый Структура("НаименованиеЭлементаГиперссылки", Элемент.Имя));
		Если НЕ СтрокиФормыКвитанции.Количество() = 0 Тогда
			
			ПараметрыДляОткрытияПД = Новый Структура("ВидМакета", СтрокиФормыКвитанции[0].ФормаКвитанции);
			ОткрытьФорму("Отчет.УПЖКХ_ПлатежныйДокумент.Форма", ПараметрыДляОткрытияПД);
			
		КонецЕсли;
	Иначе
		Попытка
			ИмяОбработки = ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(ВнешняяКвитанция);
			ОткрытьФорму("ВнешнийОтчет." + ИмяОбработки + ".Форма",, ЭтаФорма);
		Исключение
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти