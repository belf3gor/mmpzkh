#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получаем настройки
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, 
		"СписокРазделов, 
		|ВариантОкругления, 
		|СравниватьСПрошлымПериодом,
		|ПоказыватьОборотыЗаТекущийМесяц,
		|ПоказыватьОборотыЗаПрошлыйМесяц,
		|ПоказыватьОборотыСНачалаГодаПоТекущийМесяц,
		|ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц");
	
	РазделПродажиНГ = Перечисления.РазделыМонитораРуководителя.ПродажиПоНоменклатурнымГруппам;
	Если БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу() Тогда
		НайденныйЭлемент = СписокРазделов.НайтиПоЗначению(РазделПродажиНГ);
		Если НайденныйЭлемент <> Неопределено Тогда
			СписокРазделов.Удалить(НайденныйЭлемент);
		КонецЕсли;
	Иначе
		НайденныйЭлемент = СписокРазделов.НайтиПоЗначению(РазделПродажиНГ);
		Если НайденныйЭлемент = Неопределено Тогда
			СписокРазделов.Добавить(РазделПродажиНГ,,Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Раздел Из СписокРазделов Цикл
		Раздел.Представление = Раздел.Значение;
	КонецЦикла;
	
	ДополнитьПредставлениеОборотныхРазделов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Если НЕ ПоказыватьОборотыЗаТекущийМесяц
	   И НЕ ПоказыватьОборотыЗаПрошлыйМесяц
	   И НЕ ПоказыватьОборотыСНачалаГодаПоТекущийМесяц
	   И НЕ ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц Тогда
		
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru='Показывать оброты за период'"),,, 
			НСтр("ru='Должен быть отмечен хотя бы один показатель.'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "ПоказыватьОборотыЗаТекущийМесяц");
		
		Возврат;
	КонецЕсли;
	
	// Сохраняем настройки
	СохранитьНастройкиНаСервере(СписокРазделов.Скопировать());
	
	Закрыть(Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	// Просто закрываем форму
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	ЗаполнитьСписокРазделовПоУмолчанию();
	ВариантОкругления 			               = 1000;
	СравниватьСПрошлымПериодом 	               = Ложь;
	Модифицированность 			               = Истина;
	ПоказыватьОборотыЗаТекущийМесяц            = Истина;
	ПоказыватьОборотыЗаПрошлыйМесяц            = Ложь;
	ПоказыватьОборотыСНачалаГодаПоТекущийМесяц = Истина;
	ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьСписокРазделов(Список)
	
	СписокРазделов.Очистить();
	
	Для каждого Раздел Из Список Цикл
		
		СписокРазделов.Добавить(Раздел.Значение, Раздел.Представление, Раздел.Пометка);
		
	КонецЦикла;
	
	ДополнитьПредставлениеОборотныхРазделов();
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу() Тогда
		НайденныйЭлемент = СписокРазделов.НайтиПоЗначению(Перечисления.РазделыМонитораРуководителя.ПродажиПоНоменклатурнымГруппам);
		Если НайденныйЭлемент <> Неопределено Тогда
			СписокРазделов.Удалить(НайденныйЭлемент);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРазделовПоУмолчанию()
	
	СписокРазделовПоУмолчанию = МониторРуководителя.СписокРазделовМонитораРуководителяПоУмолчанию();
	Для Каждого Раздел Из СписокРазделовПоУмолчанию Цикл
		Раздел.Представление = Раздел.Значение;
	КонецЦикла;
	ЗагрузитьСписокРазделов(СписокРазделовПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере(СписокРазделовМонитора)
	
	РазделПродажиНГ = Перечисления.РазделыМонитораРуководителя.ПродажиПоНоменклатурнымГруппам;
	НайденныйЭлемент = СписокРазделовМонитора.НайтиПоЗначению(РазделПродажиНГ);
	Если НайденныйЭлемент = Неопределено Тогда
		СписокРазделовМонитора.Добавить(РазделПродажиНГ, , Ложь);
	КонецЕсли;
	
	// Записываем настройки в хранилище
	Для Каждого Раздел из СписокРазделовМонитора Цикл
		Раздел.Представление = ОбщегоНазначения.ИмяЗначенияПеречисления(Раздел.Значение);
	КонецЦикла;
	
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"СписокРазделовМонитораРуководителя", 
		СписокРазделовМонитора);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"ВариантОкругленияМонитораРуководителя", 
		ВариантОкругления);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"СравниватьСПрошлымПериодом", 
		СравниватьСПрошлымПериодом);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"ПоказыватьОборотыЗаТекущийМесяц", 
		ПоказыватьОборотыЗаТекущийМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"ПоказыватьОборотыЗаПрошлыйМесяц",
		ПоказыватьОборотыЗаПрошлыйМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"ПоказыватьОборотыСНачалаГодаПоТекущийМесяц", 
		ПоказыватьОборотыСНачалаГодаПоТекущийМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц", 
		ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторРуководителя", 
		"ПоказыватьУведомлениеОНастройке", 
		Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьПредставлениеОборотныхРазделов()
	
	ОборотныеРазделы = Перечисления.РазделыМонитораРуководителя.ОборотныеРазделы();
	
	Для каждого Раздел Из СписокРазделов Цикл
		
		Если ОборотныеРазделы.Найти(Раздел.Значение) <> Неопределено Тогда
			Раздел.Представление = Раздел.Представление + НСтр("ru=' (обороты)'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
