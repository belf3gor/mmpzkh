#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Интерфейс77 = Константы.ИнтерфейсВерсии77.Получить();
	
	Если НЕ Интерфейс77 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	ВключитьСтандартныйИнтерфейс = Параметры.ВариантПредложения = "ВключитьСтандартныйИнтерфейс";
	ИзменитьКомандныйИнтерфейс = Параметры.ВариантПредложения = "ИзменитьКомандныйИнтерфейс";
	
	Если НЕ ВключитьСтандартныйИнтерфейс И НЕ ИзменитьКомандныйИнтерфейс Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияВключитьСтандартныйИнтерфейс77.Видимость = ВключитьСтандартныйИнтерфейс И Интерфейс77;
	Элементы.ДекорацияИзменитьКомандныйИнтерфейс77.Видимость = ИзменитьКомандныйИнтерфейс И Интерфейс77;
	
	ПолныеПрава = РольДоступна("ПолныеПрава");
	
	Элементы.ДекорацияИзменениеРежима.Видимость = ПолныеПрава; 
	Элементы.ДекорацияОбратитесьКАдминистратору.Видимость = НЕ ПолныеПрава;
	
	Если ИзменитьКомандныйИнтерфейс Тогда
		Элементы.ВключитьСтандартныйИнтерфейс.Заголовок = НСтр("ru='Включить стандартный интерфейс'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьСтандартныйИнтерфейс(Команда)
	
	ЗаписатьНастройки();
	ПропуститьПредупреждениеПередЗавершениемРаботыСистемы = Истина;
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозже(Команда)
	
	ОбновитьНастройки();	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНастройки()
	
	ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса("ИнтерфейсТакси");
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,ТекущаяДатаСеанса(),,);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройки()
	
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,ТекущаяДатаСеанса(),,);
	
КонецПроцедуры

#КонецОбласти
