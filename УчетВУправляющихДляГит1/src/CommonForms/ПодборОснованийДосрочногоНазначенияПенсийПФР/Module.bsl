
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтчетныйПериод = Параметры.ОтчетныйПериод;
	
	ЗаполнитьОснованияДляДосрочнойПенсии();
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОснованияДляДосрочнойПенсии

&НаКлиенте
Процедура ОснованияДляДосрочнойПенсииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыборЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОснованияДляДосрочнойПенсии()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОснованияДосрочногоНазначенияПенсии.Ссылка КАК Ссылка,
	               |	ОснованияДосрочногоНазначенияПенсии.Код КАК Код,
	               |	ОснованияДосрочногоНазначенияПенсии.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.ОснованияДосрочногоНазначенияПенсии КАК ОснованияДосрочногоНазначенияПенсии
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОсобыеУсловияТрудаПФР.Ссылка,
	               |	ОсобыеУсловияТрудаПФР.Код,
	               |	ОсобыеУсловияТрудаПФР.Наименование
	               |ИЗ
	               |	Справочник.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТрудаПФР";
	
	Если ОтчетныйПериод < '20020101' Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ОснованияДосрочногоНазначенияПенсии", "Справочник.ОснованияДосрочногоНазначенияПенсииДляСЗВК");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ОсобыеУсловияТрудаПФР", "Справочник.ОсобыеУсловияТрудаДляСЗВКПФР");
	КонецЕсли;
	
	ОснованияДляДосрочнойПенсии.Загрузить(Запрос.Выполнить().Выгрузить());
	ОснованияДляДосрочнойПенсии.Сортировать("Код");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОснования", "Видимость", ОтчетныйПериод >= '20020101');
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОснованияСЗВК", "Видимость", ОтчетныйПериод < '20020101');
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборЗначения()
	
	ТекущиеДанные = Элементы.ОснованияДляДосрочнойПенсии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		ОповеститьОВыборе(Новый Структура("ОснованиеВыслугиЛет", ТекущиеДанные.Ссылка));
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
