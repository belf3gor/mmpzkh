
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизицияПриИзменении(Элемент)
	 ЗаполнитьЗавершение(КодВозвратаДиалога.Да);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект, Новый Структура());
	ТекстВопроса = НСтр("ru = 'Табличная часть не пустая. 
						|Очистить табличную часть?'");
	
	Если Сотрудники.Количество() Тогда
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 10,  КодВозвратаДиалога.Да , НСтр("ru = 'Заполнение списка сотрудников'"));
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаДиск(Команда)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("Организация", Организация);
	Если Сотрудники.Количество() Тогда
		ПараметрыПроверки.Вставить("СведенияОРаботниках", Сотрудники);
	КонецЕсли;
	
	ИнтерфейсыВзаимодействияБРОКлиент.СформироватьИСохранитьНаДискФайлЗапросаВФНСНаПроверкуСведенийРаботников(ПараметрыПроверки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьЗавершение(Результат, Параметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокСотрудников();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСотрудников()
	ДатаПодписи = ТекущаяДатаСеанса();
	Сотрудники.Загрузить(УчетСтраховыхВзносов.СписокЗастрахованныхЛицОрганизации(Организация, НачалоКвартала(ДатаПодписи), КонецКвартала(ДатаПодписи)));
КонецПроцедуры 

#КонецОбласти

