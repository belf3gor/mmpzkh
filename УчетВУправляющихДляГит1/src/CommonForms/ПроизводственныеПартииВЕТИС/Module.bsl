

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПроизводственныеПартии") Тогда
		Для каждого Строка Из Параметры.ПроизводственныеПартии Цикл
			ЗаполнитьЗначенияСвойств(ПроизводственныеПартии.Добавить(), Строка);
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("МаксимальноеКоличествоПартий") Тогда
		МаксимальноеКоличествоПартий = Параметры.МаксимальноеКоличествоПартий;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтеграцияВЕТИСКлиент.ПронумероватьТаблицу(ЭтаФорма, "ПроизводственныеПартии");
	
	СформироватьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПроизводственныеПартии

&НаКлиенте
Процедура ПроизводственныеПартииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если МаксимальноеКоличествоПартий > 0
	   И ПроизводственныеПартии.Количество() = МаксимальноеКоличествоПартий Тогда
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Количество партий не может быть более %1'"), МаксимальноеКоличествоПартий);
		ПоказатьПредупреждение(, ТекстПредупреждения, 30);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныеПартииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	СформироватьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныеПартииПослеУдаления(Элемент)
	СформироватьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныеПартииПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ИнтеграцияВЕТИСКлиент.ПронумероватьТаблицу(ЭтаФорма, "ПроизводственныеПартии");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	СохранитьИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьИзменения();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения()
	
	ОчиститьСообщения();
	
	СтруктураПроверяемыхПолей = Новый Структура;
	СтруктураПроверяемыхПолей.Вставить("ИдентификаторПартии", НСтр("ru='Идентификатор партии'"));
	
	Если ИнтеграцияВЕТИСКлиент.ПроверитьЗаполнениеТаблицы(ЭтаФорма, "ПроизводственныеПартии", СтруктураПроверяемыхПолей) Тогда
		Модифицированность = Ложь;
		ОповеститьОВыборе(ПроизводственныеПартии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаголовокФормы()
	
	Заголовок = НСтр("ru = 'Производственные партии'")
		+ ?(ПроизводственныеПартии.Количество()," ("+ПроизводственныеПартии.Количество()+")","");
	
КонецПроцедуры

#КонецОбласти