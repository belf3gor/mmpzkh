//////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СОБЫТИЙ ФОРМЫ

#Область ПроцедурыОбработкиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
// Параметры:
//  Отказ                - <Булево>
//                       - Отказ от дальнейшего проведения
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                       - стандартной (системной) обработки события.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Если определен объект, для которого устанавливается доля,
	// то укажем его в качестве заголовка формы.
	Если Параметры.Свойство("Объект") И ЗначениеЗаполнено(Параметры.Объект) Тогда
		Заголовок = Параметры.Объект;
	КонецЕсли;
	
	// Определим заголовок группы полей ввода доли.
	Если Параметры.Свойство("Заголовок") Тогда
		
		ЗаголовокГруппыДоли = Параметры.Заголовок;
		
		// Дополним заголовок символом ":".
		Если Не Прав(ЗаголовокГруппыДоли, 1) = ":" Тогда
			ЗаголовокГруппыДоли = ЗаголовокГруппыДоли + ":";
		КонецЕсли;
		
		Элементы.ГруппаДоля.Заголовок = ЗаголовокГруппыДоли;
		
	КонецЕсли;
	
	Параметры.Свойство("ДоляЗнаменатель",  ДоляЗнаменатель);
	Параметры.Свойство("ДоляЧислитель",    ДоляЧислитель);
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ПроцедурыОбработкиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события нажатия на кнопку "Выбрать".
//
Процедура КомандаВыбрать(Команда)
	
	Если НЕ ДоляЧислитель = 0 И ДоляЗнаменатель = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Знаменатель доли не может быть равен нулю!", , 
			"ДоляЗнаменатель");
		Возврат;
	ИначеЕсли ДоляЧислитель > ДоляЗнаменатель Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Числитель доли не может быть больше знаменателя!", , 
			"ДоляЧислитель");
		Возврат;
	КонецЕсли;
	
	СтруктураДоли = Новый Структура("ДоляЧислитель, ДоляЗнаменатель",
									 ДоляЧислитель, ДоляЗнаменатель);
	
	Закрыть(СтруктураДоли);
	
КонецПроцедуры // КомандаВыбрать()

#КонецОбласти

