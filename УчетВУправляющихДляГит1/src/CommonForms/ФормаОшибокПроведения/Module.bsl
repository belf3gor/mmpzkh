////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	АдресХранилища = Параметры.АдресХранилища;
	
	// В ПроведениеСервер.ПоместитьСообщенияПользователюВоВременноеХранилищеДляФормыОшибок()
	// во временное хранилище помещается структура с ключами:
	//	* ТаблицаСообщенийПользователю - результат ПроведениеСервер.НовыеПараметрыОтчетаССообщениямиПользователю()
	//	* ОтчетПоОшибкам - Табличный документ
	//
	ОшибкиПерепроведения = ПолучитьИзВременногоХранилища(АдресХранилища);

	Если ТипЗнч(ОшибкиПерепроведения) = Тип("Структура")
		И ОшибкиПерепроведения.Свойство("ОтчетПоОшибкам") 
		И ОшибкиПерепроведения.Свойство("ТаблицаСообщенийПользователю") Тогда
		
		ТабДокумент.Вывести(ОшибкиПерепроведения.ОтчетПоОшибкам);
		
	Иначе
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(АдресХранилища);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ТабДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ИмяФормыОбъекта = "";

	Если ТипЗнч(Расшифровка) = Тип("СообщениеПользователю") Тогда
		СсылкаНаОбъект = Расшифровка.КлючДанных;
	Иначе
		СсылкаНаОбъект = Расшифровка;
	КонецЕсли;
	
	ИмяФормыОбъекта = ПолныйПутьКФормеОбъекта(СсылкаНаОбъект);

	Если НЕ ПустаяСтрока(ИмяФормыОбъекта) Тогда

		СтандартнаяОбработка = Ложь;

		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Ключ", СсылкаНаОбъект);
		ФормаОбъекта = ОткрытьФорму(ИмяФормыОбъекта, ПараметрыФормы, ЭтотОбъект, Ложь);
		
		ПоказатьСообщенияПоОбъекту(СсылкаНаОбъект, ФормаОбъекта.УникальныйИдентификатор);
		
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ПолныйПутьКФормеОбъекта(Знач СсылкаНаОбъект)

	ИмяФормыОбъекта = "";

	Если СсылкаНаОбъект <> Неопределено Тогда
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(СсылкаНаОбъект));
		Если МетаданныеОбъекта <> Неопределено Тогда
			ИмяФормыОбъекта = МетаданныеОбъекта.ПолноеИмя() + ".ФормаОбъекта";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИмяФормыОбъекта;

КонецФункции

&НаСервере
Процедура ПоказатьСообщенияПоОбъекту(Знач СсылкаНаОбъект, Знач ИдентификаторФормыОбъекта)

	ОшибкиПерепроведения = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(ОшибкиПерепроведения) = Тип("Структура")
		И ОшибкиПерепроведения.Свойство("ТаблицаСообщенийПользователю") Тогда 
		ТаблицаСообщенийПользователю = ОшибкиПерепроведения.ТаблицаСообщенийПользователю;
	Иначе
		Возврат;
	КонецЕсли;

	Отбор = Новый Структура();
	Отбор.Вставить("КлючДанных", СсылкаНаОбъект);
	
	НайденныеСтроки = ТаблицаСообщенийПользователю.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
	
		Если СтрокаТаблицы.Сообщение.ИдентификаторНазначения <> ИдентификаторФормыОбъекта Тогда
			СтрокаТаблицы.Сообщение.ИдентификаторНазначения = ИдентификаторФормыОбъекта;
			СтрокаТаблицы.Сообщение.Сообщить();
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

