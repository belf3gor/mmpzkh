
#Область УправлениеВнешнимВидомФормы

&НаКлиентеНаСервереБезКонтекста
// Управляет видимостью элементов формы.
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЭтоПользовательскийКаталог = (Форма.ВариантКаталогаСохраненияКвитанций = ПредопределенноеЗначение("Перечисление.УПЖКХ_ВариантыСохраненияФайлов.КаталогУказанныйПользователем"));
	
	Элементы.КаталогСохраненияКвитанций.Видимость = ЭтоПользовательскийКаталог;
	
	Элементы.КаталогВременныхФайлов.Видимость = Не ЭтоПользовательскийКаталог;
	
	КаталогСохраненияКвитанцийЗаполнен               = Не ПустаяСтрока(Форма.КаталогСохраненияКвитанций);
	ПользовательскийКаталогНедоступенИлиНеСуществует = ПользовательскийКаталогНедоступенИлиНеСуществует(Форма.КаталогСохраненияКвитанций, ЭтоПользовательскийКаталог);
	
	// Управление расширенной подсказкой поля "КаталогСохраненияКвитанций".
	
	ЭтоФайловыйРежимРаботыПрограммы = Ложь;
	
	Если Лев(СтрокаСоединенияИнформационнойБазы(), 4) = "File" Тогда
		ЭтоФайловыйРежимРаботыПрограммы = Истина;
	КонецЕсли;
	
	Если Не КаталогСохраненияКвитанцийЗаполнен Тогда
		
		// Подсказка по умолчанию.
		
		Элементы.КаталогСохраненияКвитанцийРасширеннаяПодсказка.ЦветТекста = Новый Цвет(28, 58, 174); // Цвет текста подсказки.
		
		Если ЭтоФайловыйРежимРаботыПрограммы Тогда
			ТекстПодсказки = "Укажите доступный сетевой каталог.";
		Иначе
			ТекстПодсказки = "Укажите каталог на сервере 1С:Предпритие или доступный с сервера сетевой каталог.";
		КонецЕсли;
		
	ИначеЕсли КаталогСохраненияКвитанцийЗаполнен И ПользовательскийКаталогНедоступенИлиНеСуществует Тогда
		
		// Сообщение об ошибке.
		
		Элементы.КаталогСохраненияКвитанцийРасширеннаяПодсказка.ЦветТекста = Новый Цвет(255, 0, 0); // Цвет текста сообщения об ошибке.
		
		Если ЭтоФайловыйРежимРаботыПрограммы Тогда
			ТекстПодсказки = "Указанный сетевой каталог не существует или недоступен.";
		Иначе
			ТекстПодсказки = "Указанный каталог на сервере 1С:Предприятие или доступный с сервера сетевой каталог не существует или недоступен.";
		КонецЕсли;
		
	Иначе
		ТекстПодсказки = "";
	КонецЕсли;
	
	Элементы.КаталогСохраненияКвитанцийРасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Проверяет доступность и существнование пользовательского каталога.
Функция ПользовательскийКаталогНедоступенИлиНеСуществует(КаталогСохраненияКвитанций, ЭтоПользовательскийКаталог)
	
	КаталогСохранения = Новый Файл(КаталогСохраненияКвитанций);
	
	КаталогСуществует        = КаталогСохранения.Существует();
	КаталогДоступенДляЗаписи = УПЖКХ_ТиповыеМетодыСервер.КаталогДоступенДляЗаписи(КаталогСохраненияКвитанций);
	
	Если ЭтоПользовательскийКаталог И (Не КаталогСуществует Или Не КаталогДоступенДляЗаписи) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыРассылкиКвитанций = УПЖКХ_РассылкаКвитанцийНаЭлектроннуюПочтуЛицевыхСчетовСервер.ПолучитьПараметрыРассылкиКвитанций();
	
	Если ТипЗнч(ПараметрыРассылкиКвитанций) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРассылкиКвитанций);
		
		Если ВариантКаталогаСохраненияКвитанций = Перечисления.УПЖКХ_ВариантыСохраненияФайлов.КаталогУказанныйПользователем Тогда
			КаталогСохраненияКвитанций = ПараметрыРассылкиКвитанций.КаталогСохраненияКвитанцийНаСервере;
		КонецЕсли;
		
	КонецЕсли;
	
	КаталогВременныхФайлов = КаталогВременныхФайлов();
	
	ТекстПодсказки = "Рекомендуется для улучшения качества рассылки, во всех рассылках, отправляемых по подписке, в тексте каждого сообщения указывать валидную не электронную контактную информацию об организации.";
	Элементы.ОтправлятьВПисьмеСведенияОбОрганизации.Подсказка = ТекстПодсказки;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗаписью" формы.
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ПустаяСтрока(ЭлектронныйАдресПолучателяТестовойРассылки)
		 И Не УПЖКХ_ТиповыеМетодыКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ЭлектронныйАдресПолучателяТестовойРассылки) Тогда
		ТекстОшибки = НСтр("ru='Указан некорректный адрес электронной почты.'");
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
	ЭтоПользовательскийКаталог = (ВариантКаталогаСохраненияКвитанций = ПредопределенноеЗначение("Перечисление.УПЖКХ_ВариантыСохраненияФайлов.КаталогУказанныйПользователем"));
	Если ПустаяСтрока(КаталогСохраненияКвитанций) И ЭтоПользовательскийКаталог Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Выберите каталог для сохранения квитанций.",,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Обработчик события "ПриЗаписиНаСервере" формы.
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыРассылкиКвитанций = УПЖКХ_РассылкаКвитанцийНаЭлектроннуюПочтуЛицевыхСчетовСервер.ПолучитьПараметрыРассылкиКвитанцийПоУмолчанию();
	
	ЗаполнитьЗначенияСвойств(ПараметрыРассылкиКвитанций, ЭтотОбъект);
	
	// Если выбран пользовательский каталог, то записываем его в параметры рассылки. 
	Если ВариантКаталогаСохраненияКвитанций = Перечисления.УПЖКХ_ВариантыСохраненияФайлов.КаталогУказанныйПользователем Тогда
		ПараметрыРассылкиКвитанций.КаталогСохраненияКвитанцийНаСервере = КаталогСохраненияКвитанций;
	КонецЕсли;
	
	УПЖКХ_РассылкаКвитанцийНаЭлектроннуюПочтуЛицевыхСчетовСервер.УстановитьПараметрыРассылкиКвитанций(ПараметрыРассылкиКвитанций);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" поля "ПереключательНастройекКаталога".
Процедура ВариантСохраненияКаталогаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" поля "КаталогСохраненияКвитанций".
Процедура КаталогСохраненияКвитанцийПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "НачалоВыбора" поля "КаталогСохраненияКвитанций".
Процедура КаталогСохраненияКвитанцийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Если Диалог.Выбрать() Тогда
		
		КаталогСохраненияКвитанций = Диалог.Каталог;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
