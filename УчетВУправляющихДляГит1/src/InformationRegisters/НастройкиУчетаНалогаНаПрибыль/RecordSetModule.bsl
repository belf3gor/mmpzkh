#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено Или ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.УстановкаНастроекПоУмолчанию(
				Запись,
				?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
		КонецЦикла;
			
		ДополнительныеСвойства.Вставить("ЗаполненЗначениямиПоУмолчанию", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
	Для каждого Строка Из ЭтотОбъект Цикл
		
		Если ЗначениеЗаполнено(Строка.Организация) Тогда
		
			РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Строка.Организация, "ЮридическоеФизическоеЛицо,ОбособленноеПодразделение");
				
			Если РеквизитыОрганизации.ОбособленноеПодразделение Тогда
				Продолжить; // Записи по ОП создаются автоматически, их не проверяем
			ИначеЕсли ДополнительныеСвойства.ГоловныеОрганизации.Найти(Строка.Организация) = Неопределено Тогда
				ДополнительныеСвойства.ГоловныеОрганизации.Добавить(Строка.Организация);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Строка.Период < '20150101' Тогда
		
			Строка.СпособПогашенияСтоимостиСпецодежды = ПредопределенноеЗначение(
				"Перечисление.СпособыПогашенияСтоимостиНУ.ПриПередачеВЭксплуатацию");

		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЗаполненЗначениямиПоУмолчанию")
		И ДополнительныеСвойства.ЗаполненЗначениямиПоУмолчанию = Истина Тогда
		
		// Заполним значениями по умолчанию связанные настройки
		
		Для Каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Настроить(
				Запись.Организация,
				Запись.Период,
				Неопределено,
				Ложь);
		КонецЦикла;
		
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	
	ДополнительныеСвойства.Удалить("ЗаполненЗначениямиПоУмолчанию");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли