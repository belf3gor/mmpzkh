
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ВспомогательныеПроцедурыИФункции

&НаКлиенте
// Процедура выводит оповещение пользователю о перерасчете площадей здания
//
Процедура ОповеститьОПересчетеПлощадейЗдания()
	
	ВладелецОбъекта = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.Объект, "Владелец");
	
	ПоказатьОповещениеПользователя("Выполнен пересчет площадей здания:",
		                                ПолучитьНавигационнуюСсылку(ВладелецОбъекта),
		                                Строка(ВладелецОбъекта),
		                                БиблиотекаКартинок.Информация32);
	
	ОповеститьОПерерасчетеПлощадиЗдания = Ложь;
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события формы "При создании на сервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОтборОбъект") Тогда
		
		Запись.Объект = Параметры.ОтборОбъект;
		
		Элементы.Объект.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВидПлощади") Тогда
		
		Запись.ВидПлощади = Параметры.ВидПлощади;
		
		Элементы.ВидПлощади.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	// При изменение полей старых записей выводим предупреждения о том, что изменения
	// могут привести к ошибкам в расчетах.
	Если ЗначениеЗаполнено(Запись.Период) И Не Запись.Объект.Пустая()
		 И Не Запись.ВидПлощади.Пустая() И ЗначениеЗаполнено(Запись.Площадь) Тогда
		
		Элементы.Период.ОтображениеПредупрежденияПриРедактировании
									= ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.Объект.ОтображениеПредупрежденияПриРедактировании
									= ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.ВидПлощади.ОтображениеПредупрежденияПриРедактировании
									= ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.Площадь.ОтображениеПредупрежденияПриРедактировании
									= ОтображениеПредупрежденияПриРедактировании.Отображать;
		
		ТекстПредупреждения = "Изменение записи может привести к изменению начислений прошлых периодов при их пересчете задним числом";
		
		Элементы.Период.ПредупреждениеПриРедактировании     = ТекстПредупреждения;
		Элементы.Объект.ПредупреждениеПриРедактировании     = ТекстПредупреждения;
		Элементы.ВидПлощади.ПредупреждениеПриРедактировании = ТекстПредупреждения;
		Элементы.Площадь.ПредупреждениеПриРедактировании    = ТекстПредупреждения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Обработчик события формы "После записи на сервере".
//
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НеобходимоПересчитыватьОбщиеПлощадиЗдания = Справочники.КВП_Здания.ПолучитьНастройкуАвтоматическогоПересчетаПлощадейЗданий();
	Если НеобходимоПересчитыватьОбщиеПлощадиЗдания И ТекущийОбъект.ВидПлощади = Справочники.УПЖКХ_ВидыПлощадей.ОбщаяПлощадь Тогда
		ОповеститьОПерерасчетеПлощадиЗдания = Справочники.КВП_Здания.ОбновитьОбщиеПлощадиЗданияПриНеобходимости(ТекущийОбъект.Объект.Владелец);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "После записи" формы.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ОповеститьОПерерасчетеПлощадиЗдания Тогда
		ПодключитьОбработчикОжидания("ОповеститьОПересчетеПлощадейЗдания", 0.5, Истина);
	КонецЕсли;
	
	Оповестить("ОбновитьПредставлениеПлощадей");
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Перед закрытием" формы.
//
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОповеститьОПерерасчетеПлощадиЗдания Тогда
		ОповеститьОПересчетеПлощадейЗдания();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти