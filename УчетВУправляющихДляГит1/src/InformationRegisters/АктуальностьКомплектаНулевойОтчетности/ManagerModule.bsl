#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает признак актуальности комплекта нулевых отчетов для переданной организации.
//
Процедура УстановитьАктуальность(Организация, Актуальность) Экспорт
	
	ТекущийПериод = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если НЕ НулеваяОтчетность.ДоступноУпрощенноеФормирование(Организация, ТекущийПериод) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.АктуальностьКомплектаНулевойОтчетности.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.ОтчетыАктуальны = Актуальность;
	
	МенеджерЗаписи.Записать();
	
КонецПРоцедуры

// Возвращает признак актуальности комплекта нулевых отчетов для переданной организации
//
Функция ОтчетыАктуальны(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АктуальностьОтчетности.ОтчетыАктуальны КАК ОтчетыАктуальны
	|ИЗ
	|	РегистрСведений.АктуальностьКомплектаНулевойОтчетности КАК АктуальностьОтчетности
	|ГДЕ
	|	АктуальностьОтчетности.Организация = &Организация"
	;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОтчетыАктуальны;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли