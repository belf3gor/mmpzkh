#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ОрганизацииПоКоторымТребуетсяВводОстатков() Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.НастройкаУчетаОстатковЕГАИС КАК НастройкаУчетаОстатковЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО НастройкаУчетаОстатковЕГАИС.ИдентификаторФСРАР = КлассификаторОрганизацийЕГАИС.Код
	|ГДЕ
	|	НастройкаУчетаОстатковЕГАИС.ДатаВводаОстатков = &ПустаяДата
	|	И НЕ НастройкаУчетаОстатковЕГАИС.НеВводитьОстатки";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

Процедура УстановитьНастройкиУчетаОстатков(ИдентификаторФСРАР, ДатаВводаОстатков = Неопределено, НеВводитьОстатки = Ложь) Экспорт
	НаборЗаписей = РегистрыСведений.НастройкаУчетаОстатковЕГАИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторФСРАР.Установить(ИдентификаторФСРАР);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли; 
	
	Запись.ИдентификаторФСРАР  = ИдентификаторФСРАР;
	Если ДатаВводаОстатков <> Неопределено 
		И НЕ ЗначениеЗаполнено(Запись.ДатаВводаОстатков) Тогда
		Запись.ДатаВводаОстатков   = ДатаВводаОстатков;
	КонецЕсли;
	
	Если НЕ Запись.НеВводитьОстатки Тогда
		Запись.НеВводитьОстатки = НеВводитьОстатки;
	КонецЕсли; 
	
	НаборЗаписей.Записать();
КонецПроцедуры

Функция ВедетсяУчетОстатков(ОрганизацияЕГАИС, Дата) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	Запрос.УстановитьПараметр("ДатаЗапроса", Дата);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.НастройкаУчетаОстатковЕГАИС КАК НастройкаУчетаОстатковЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО НастройкаУчетаОстатковЕГАИС.ИдентификаторФСРАР = КлассификаторОрганизацийЕГАИС.Код
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.Ссылка = &ОрганизацияЕГАИС
	|	И НастройкаУчетаОстатковЕГАИС.ДатаВводаОстатков < &ДатаЗапроса
	|	И НЕ НастройкаУчетаОстатковЕГАИС.НеВводитьОстатки";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

Процедура ЗаполнитьНастройкиУчета() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиОбменаЕГАИС.ИдентификаторФСРАР КАК ИдентификаторФСРАР,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВводаОстатков,
	|	ЛОЖЬ КАК НеВводитьОстатки
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС";
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	НаборЗаписей = РегистрыСведений.НастройкаУчетаОстатковЕГАИС.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(ТаблицаРезультата);
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
КонецПроцедуры

#КонецЕсли
