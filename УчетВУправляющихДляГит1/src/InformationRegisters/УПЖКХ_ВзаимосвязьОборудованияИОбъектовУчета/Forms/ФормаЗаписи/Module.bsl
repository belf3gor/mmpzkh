
///////////////////////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ВспомогательныеПроцедурыИФункции

&НаСервереБезКонтекста
// Функция возвращает актуальное состояние оборудования.
//
Функция ПолучитьАктуальноеСостояниеОборудованияНаСервере(Оборудование)
	
	Возврат УПЖКХ_УчетОборудованияНаОбъектахУчетаСервер.ПолучитьАктуальноеСостояниеОборудования(Оборудование);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Процедура управления элементами формы.
//
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Запись   = Форма.Запись;
	
	Элементы.Количество.ТолькоПросмотр = Не УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьЗначениеРеквизита(Запись.Оборудование, "ЭтоОбобщающееОборудование");
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА СО СВЕДЕНИЯМИ ОБ ОБОРУДОВАНИИ

&НаКлиентеНаСервереБезКонтекста
// Процедура производит чтение состояния оборудования.
//
Процедура ПрочитатьСостояниеОборудования(Форма)
	
	ТекущееСостояниеОборудования = ПолучитьАктуальноеСостояниеОборудованияНаСервере(Форма.Запись.Оборудование);
	Если Не ТекущееСостояниеОборудования.Пустая() Тогда
		Форма.СостояниеОборудования = ТекущееСостояниеОборудования;
	Иначе
		Форма.СостояниеОборудования = "Не определено";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Реквизиты типа "Булево" из значений заполнения нужно получить вручную.
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		Параметры.ЗначенияЗаполнения.Свойство("Установлено", Запись.Установлено);
		
		// Заполним количество из параметров заполнения, а если их нет, установим значение по умолчанию.
		Если Не Параметры.ЗначенияЗаполнения.Свойство("Количество",  Запись.Количество) Тогда
			Запись.Количество = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	ПрочитатьСостояниеОборудования(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСведенияОбОборудовании" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура")
		   И Параметр.Свойство("ТипСведений")
		   И Параметр.ТипСведений = "СостояниеОборудования"
		   И Источник = Запись.Оборудование Тогда
			
			ПрочитатьСостояниеОборудования(ЭтаФорма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗаписью" формы.
//
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.Установлено И Запись.Количество = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Не указано количество единиц установленного оборудования!",, "Запись.Количество",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Оповестим форму выбранного оборудования.
	Оповестить("ОбновитьСведенияОбУстановленномОборудовании", , Запись.ОбъектУстановки);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" реквизита "Обрудование".
//
Процедура ОборудованиеПриИзменении(Элемент)
	
	Если Не УПЖКХ_ОбщегоНазначенияКлиентСервер.ПолучитьЗначениеРеквизита(Запись.Оборудование, "ЭтоОбобщающееОборудование") Тогда
		Запись.Количество = 1;
	КонецЕсли;
	
	ПрочитатьСостояниеОборудования(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "ИсторияСведенийОбОборудовании".
//
Процедура ИсторияСведенийОбОборудовании(Команда)
	
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("Оборудование", Запись.Оборудование));
	ОткрытьФорму("РегистрСведений.УПЖКХ_СведенияОбОборудовании.ФормаСписка", ПараметрыФормы, ЭтаФорма, Истина);
	
КонецПроцедуры

#КонецОбласти