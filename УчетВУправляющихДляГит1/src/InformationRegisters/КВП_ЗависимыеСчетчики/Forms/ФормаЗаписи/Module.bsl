
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередЗаписью" формы.
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.ВыходнойПриборУчета = Запись.ВходнойПриборУчета Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Приборы учета должны быть различны!", , , ,Отказ);
	КонецЕсли;
	
	Если УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВходнойПриборУчета, "ПризнакВыходногоСчетчика") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В качестве уменьшаемого не может быть выбран прибор учета с установленным признаком ""выходной""!", , , ,Отказ);
	КонецЕсли;
	
	ВидУслугиВходногоСчетчика  = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВходнойПриборУчета, "ВидУслуги");
	ВидУслугиВыходногоСчетчика = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВыходнойПриборУчета, "ВидУслуги");
	
	Если Модифицированность И НЕ Запись.ОтключитьПроверку И НЕ ВидУслугиВходногоСчетчика = ВидУслугиВыходногоСчетчика Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для приборов учета должна быть указана одинаковая услуга!", , , ,Отказ);
	КонецЕсли;
	
	ТарифностьВходногоСчетчика =  УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВходнойПриборУчета, "Тарифность");
	ТарифностьВыходногоСчетчика = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВыходнойПриборУчета, "Тарифность");
	
	Если НЕ ТарифностьВходногоСчетчика = ТарифностьВыходногоСчетчика Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Тарифность ""входного"" и ""выходного"" приборов учета должна совпадать!", , , ,Отказ);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "НачалоВыбора" поля ввода "выходного" ПУ.
Процедура ВыходнойПриборУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ВходнойПриборУчета) Тогда
		СтандартнаяОбработка = Ложь;
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Тарифность", УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВходнойПриборУчета, "Тарифность"));
		Если НЕ Запись.ОтключитьПроверку Тогда
			СтруктураОтбора.Вставить("ВидУслуги", УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВходнойПриборУчета, "ВидУслуги"));
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ТекущаяСтрока, Отбор", ЭтаФорма.Запись.ВыходнойПриборУчета, СтруктураОтбора);
		ОткрытьФорму("Справочник.КВП_Счетчики.ФормаВыбора", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры // ВыходнойПриборУчетаНачалоВыбора()

&НаКлиенте
// Обработчик события "НачалоВыбора" поля ввода уменьшаемого ПУ.
Процедура ВходнойПриборУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ПризнакВыходногоСчетчика", Ложь);
	
	Если ЗначениеЗаполнено(Запись.ВыходнойПриборУчета) Тогда
		СтруктураОтбора.Вставить("Тарифность", УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВыходнойПриборУчета, "Тарифность"));
		Если НЕ Запись.ОтключитьПроверку Тогда
			СтруктураОтбора.Вставить("ВидУслуги", УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Запись.ВыходнойПриборУчета, "ВидУслуги"));
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ТекущаяСтрока, Отбор", ЭтаФорма.Запись.ВходнойПриборУчета, СтруктураОтбора);
	ОткрытьФорму("Справочник.КВП_Счетчики.ФормаВыбора", ПараметрыФормы, Элемент);

КонецПроцедуры // ВходнойПриборУчетаНачалоВыбора()

&НаКлиенте
// Обработчик события нажатия кнопки "Поменять местами".
Процедура ПоменятьМестами(Команда)
	
	УменьшаемыйПриборУчета = Запись.ВходнойПриборУчета;
	
	Запись.ВходнойПриборУчета  = Запись.ВыходнойПриборУчета;
	Запись.ВыходнойПриборУчета = УменьшаемыйПриборУчета;
	
КонецПроцедуры // ПоменятьМестами()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

// ЧастоЗадаваемыеВопросы
&НаКлиенте
// Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти
