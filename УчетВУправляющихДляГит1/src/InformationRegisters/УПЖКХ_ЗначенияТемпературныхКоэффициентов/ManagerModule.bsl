#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ВспомогательныеПроцедурыИФункции

// Функция возвращает значение температурного коэффициента по виду услуги и участку.
//
// Параметры:
//  ВидУслуги  - по которому выполняется поиск.
//  Участок    - по которому выполняется поиск.
//
// Возвращаемое значение:
//  Результат - Число, содержащее значение температурного коэффициента.
// 
Функция ПолучитьТемпературныйКоэффициентУслугиНаУчастке(Период, Знач ВидУслуги, Знач Участок) Экспорт

	Результат = 1;
	
	СписокГруппУчастков = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьСписокВышеСтоящихГрупп(Участок);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Период",        Период);
	Запрос.УстановитьПараметр("ВидУслуги",     ВидУслуги);
	Запрос.УстановитьПараметр("Участок",       Участок);
	Запрос.УстановитьПараметр("ПустойУчасток", Справочники.КВП_Участки.ПустаяСсылка());
	
	Индекс = 1;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	" + Индекс + " КАК Приоритет,
	|	Коэффициент
	|
	|ИЗ
	|	РегистрСведений.УПЖКХ_ЗначенияТемпературныхКоэффициентов.СрезПоследних(&Период)
	|
	|ГДЕ
	|	
	|	ВидУслуги = &ВидУслуги И
	|	Участок = &Участок
	|";
	
	Индекс = Индекс + 1;
	
	Для Каждого ГруппаУчастков Из СписокГруппУчастков Цикл
		
		Запрос.УстановитьПараметр("ГруппаУчастков" + Индекс, ГруппаУчастков);
		
		ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	" + Индекс + ",
		|	Коэффициент
		|
		|ИЗ
		|	РегистрСведений.УПЖКХ_ЗначенияТемпературныхКоэффициентов.СрезПоследних(&Период)
		|
		|ГДЕ
		|	ВидУслуги = &ВидУслуги И
		|	Участок = &ГруппаУчастков" + Индекс + "
		|";
		
		Индекс = Индекс + 1;
	
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	" + Индекс + ",
	|	Коэффициент
	|
	|ИЗ
	|	РегистрСведений.УПЖКХ_ЗначенияТемпературныхКоэффициентов.СрезПоследних(&Период)
	|
	|ГДЕ
	|	ВидУслуги = &ВидУслуги И
	|	Участок = &ПустойУчасток
	|";
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Результат = Выборка.Коэффициент;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ПолучитьТемпературныйКоэффициентУслугиНаУчастке()

#КонецОбласти

#КонецЕсли