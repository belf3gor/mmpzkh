#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Обработчик обновления БРО 1.1.13.1
// переносит данные из разделенного регистра УдалитьРесурсыМеханизмаОнлайнСервисовРО 
// в неразделенный регистр РесурсыМеханизмаОнлайнСервисовРО
Процедура ПеренестиДанныеИзРазделенногоРегистра(Параметры = Неопределено) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		Если НЕ ОбщегоНазначения.РазделениеВключено() Тогда	
			
			//переносим только для коробок
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	УдалитьРесурсыМеханизмаОнлайнСервисовРО.Тип КАК Тип,
			|	УдалитьРесурсыМеханизмаОнлайнСервисовРО.Данные КАК Данные,
			|	УдалитьРесурсыМеханизмаОнлайнСервисовРО.Имя КАК Имя,
			|	УдалитьРесурсыМеханизмаОнлайнСервисовРО.ДатаПолучения КАК ДатаПолучения
			|ИЗ
			|	РегистрСведений.УдалитьРесурсыМеханизмаОнлайнСервисовРО КАК УдалитьРесурсыМеханизмаОнлайнСервисовРО";
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Количество() > 0 Тогда
				
				// копируем данные в новый регистр
				НаборЗаписей = РегистрыСведений.РесурсыМеханизмаОнлайнСервисовРО.СоздатьНаборЗаписей();
				
				Пока Выборка.Следующий() Цикл
					Запись = НаборЗаписей.Добавить();		
					ЗаполнитьЗначенияСвойств(Запись, Выборка);
				КонецЦикла;
				
				НаборЗаписей.ОбменДанными.Загрузка = Истина;
				НаборЗаписей.Записать();			
							
			КонецЕсли; 
			
		КонецЕсли; 
		
		// удаляем записи из старого регистра в коробках и в области для сервиса
		НаборЗаписей = РегистрыСведений.УдалитьРесурсыМеханизмаОнлайнСервисовРО.СоздатьНаборЗаписей();				
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли
