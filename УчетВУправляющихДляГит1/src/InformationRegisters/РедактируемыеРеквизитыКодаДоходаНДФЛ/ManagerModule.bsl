#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение сведений.
Процедура НачальноеЗаполнение(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РедактируемыеРеквизитыКодаДоходаНДФЛ.КодДохода
	|ИЗ
	|	РегистрСведений.РедактируемыеРеквизитыКодаДоходаНДФЛ КАК РедактируемыеРеквизитыКодаДоходаНДФЛ";
	
	Если Запрос.Выполнить().Пустой() Тогда

		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыДоходовНДФЛ.Ссылка КАК КодДохода,
		|	ВидыДоходовНДФЛ.СоответствуетОплатеТруда
		|ИЗ
		|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
		|ГДЕ
		|	ВидыДоходовНДФЛ.СоответствуетОплатеТруда";
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.РедактируемыеРеквизитыКодаДоходаНДФЛ", "КодДохода", Справочники.ВидыДоходовНДФЛ.КодДоходаПоУмолчанию) Тогда 
			Возврат;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.РедактируемыеРеквизитыКодаДоходаНДФЛ.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.КодДохода = Справочники.ВидыДоходовНДФЛ.КодДоходаПоУмолчанию;
			НоваяЗапись.СоответствуетОплатеТруда = Истина;
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.КодДохода = Справочники.ВидыДоходовНДФЛ.Код2530;
			НоваяЗапись.СоответствуетОплатеТруда = Истина;
		КонецЕсли;
		НаборЗаписей.Записать();
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли