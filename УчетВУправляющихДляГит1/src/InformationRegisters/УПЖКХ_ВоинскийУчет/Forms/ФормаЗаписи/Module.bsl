
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
// Обновляет ключ текущей записи регистра.
Процедура ОбновитьКлючЗаписи()
	
	Менеджер = ДанныеФормыВЗначение(Запись, Тип("РегистрСведенийМенеджерЗаписи.УПЖКХ_ВоинскийУчет"));
	НовыйМенеджер = РегистрыСведений.УПЖКХ_ВоинскийУчет.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(НовыйМенеджер, Менеджер);
	ЗначениеВДанныеФормы(НовыйМенеджер, Запись);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ФизическоеЛицо") И ЗначениеЗаполнено(Параметры.ФизическоеЛицо) Тогда
		Запись.ФизическоеЛицо = Параметры.ФизическоеЛицо;
	КонецЕсли;
	
	Параметры.Свойство("ОткрытаИзФормыЖильца", ОткрытаИзФормыЖильца);
	ИсходныйПериод = Запись.Период;
	
	Элементы.ФизЛицо.Видимость = Не ОткрытаИзФормыЖильца;
	Элементы.КнопкаИстория.Видимость = ОткрытаИзФормыЖильца;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии(Отказ)
	
	УПЖКХ_РаботаСДиалогамиКлиентСервер.УстановитьЗаголовокФормыЗаписиФизлица("Воинский учет физического лица", Запись.ФизическоеЛицо, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПередЗаписью" формы.
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ОткрытаИзФормыЖильца
	   И ЗначениеЗаполнено(ИсходныйПериод) И Не ИсходныйПериод = Запись.Период Тогда
		ОбновитьКлючЗаписи();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","ВоинскийУчет"), Запись.ФизическоеЛицо);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" элемента формы "ФизЛицо".
//
Процедура ФизЛицоПриИзменении(Элемент)
	
	УПЖКХ_РаботаСДиалогамиКлиентСервер.УстановитьЗаголовокФормыЗаписиФизлица("Воинский учет физического лица", Запись.ФизическоеЛицо, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик нажатия на кнопку "История".
//
Процедура ПоказатьИсторию(Команда)
	
	ОткрытьФорму("РегистрСведений.УПЖКХ_ВоинскийУчет.ФормаСписка",
				 Новый Структура("Отбор", Новый Структура("ФизическоеЛицо", Запись.ФизическоеЛицо)), , Истина);
	
КонецПроцедуры

#КонецОбласти