 
#Область КомандыФормы

&НаКлиенте
Процедура ВыгрузитьФайлы(Команда)

	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = ''"));
		Возврат;
	КонецЕсли;

	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьФайлыЗавершение", ЭтотОбъект);

	ПолучаемыеФайлы = ПодготовитьФайлыНаСервере(Элементы.Список.ВыделенныеСтроки);
	
	НачатьПолучениеФайлов(ОписаниеОповещения, ПолучаемыеФайлы, , Истина);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлыЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт

	Если ПолученныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(, НСтр("ru = 'Выгрузка файлов завершена'"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьФайлыНаСервере(Знач ВыделенныеСтроки)

	МенеджерЗаписи = РегистрыСведений.ЖурналОперацийСФайламиОбменаСБанками.СоздатьМенеджерЗаписи();
	
	Результат = Новый Массив;
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		МенеджерЗаписи.Идентификатор    = ВыделеннаяСтрока.Идентификатор;
		МенеджерЗаписи.ИсходноеИмяФайла = ВыделеннаяСтрока.ИсходноеИмяФайла;
		МенеджерЗаписи.Предмет          = ВыделеннаяСтрока.Предмет;
		МенеджерЗаписи.Организация      = ВыделеннаяСтрока.Организация;
		
		МенеджерЗаписи.Прочитать();
	
		ИсходноеИмяФайла = МенеджерЗаписи.ИсходноеИмяФайла;
		
		// Сам файл
		Данные = МенеджерЗаписи.ИсходныеДанные.Получить();
		Если Данные <> Неопределено Тогда
			ОписаниеПередаваемогоФайла = ПодготовитьОписаниеФайла(Данные, ИсходноеИмяФайла, "", "");
			Результат.Добавить(ОписаниеПередаваемогоФайла);
		КонецЕсли;
		
		// Сжатые исходные данные
		Данные = МенеджерЗаписи.СжатыеИсходныеДанные.Получить();
		Если Данные <> Неопределено Тогда
			ОписаниеПередаваемогоФайла = ПодготовитьОписаниеФайла(Данные, ИсходноеИмяФайла, "_compressed", ".zip");
			Результат.Добавить(ОписаниеПередаваемогоФайла);
		КонецЕсли;

		// Подпись
		Данные = МенеджерЗаписи.Подпись.Получить();
		Если Данные <> Неопределено Тогда
			ОписаниеПередаваемогоФайла = ПодготовитьОписаниеФайла(Данные, ИсходноеИмяФайла, "_signed", ".bin");
			Результат.Добавить(ОписаниеПередаваемогоФайла);
		КонецЕсли;

		// Зашифрованные 
		Данные = МенеджерЗаписи.Результат.Получить();
		Если Данные <> Неопределено Тогда
			ОписаниеПередаваемогоФайла = ПодготовитьОписаниеФайла(Данные, ИсходноеИмяФайла, "_encrypted", ".bin");
			Результат.Добавить(ОписаниеПередаваемогоФайла);
		КонецЕсли;

	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаСервере
Функция ПодготовитьОписаниеФайла(Данные, ИсходноеИмяФайла, СуффиксИмениФайла, Знач РасширениеВыгруженногоФайла)

	РасширениеИмениФайла = "." + ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИсходноеИмяФайла);

	Если НЕ ЗначениеЗаполнено(РасширениеВыгруженногоФайла) Тогда
		РасширениеВыгруженногоФайла = РасширениеИмениФайла;
	КонецЕсли;

	ИмяСохраняемогоФайла = СтрЗаменить(ИсходноеИмяФайла, РасширениеИмениФайла, СуффиксИмениФайла)
		+ РасширениеВыгруженногоФайла;
	
	Результат = Новый ОписаниеПередаваемогоФайла;
	Результат.Имя      = ИмяСохраняемогоФайла;
	Результат.Хранение = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);

	Возврат Результат;

КонецФункции

#КонецОбласти
