#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает амортизационную группу по ОКОФ.
// Если переданному коду соответствует несколько амортизационных 
// групп, то возвращается минимальная.
// 
// Параметры:
//  ОКОФ - СправочникСсылка.ОбщероссийскийКлассификаторОсновныхФондов.
//
// Возвращаемое значение:
//  АмортизационнаяГруппа - ПеречислениеСсылка.АмортизационныеГруппы; Неопределено.
//
Функция ПолучитьАмортизационнуюГруппу(ОКОФ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОКОФ", ОКОФ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АмортизационныеГруппыОКОФ.АмортизационнаяГруппа,
	|	АмортизационныеГруппыОКОФ.АмортизационнаяГруппа.Порядок КАК Порядок
	|ИЗ
	|	РегистрСведений.АмортизационныеГруппыОКОФ КАК АмортизационныеГруппыОКОФ
	|ГДЕ
	|	АмортизационныеГруппыОКОФ.ОКОФ = &ОКОФ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		АмортизационнаяГруппа = Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			АмортизационнаяГруппа = Выборка.АмортизационнаяГруппа;
		КонецЦикла;
	КонецЕсли;
	
	Возврат АмортизационнаяГруппа;
	
КонецФункции

#КонецОбласти

#КонецЕсли