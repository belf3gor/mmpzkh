
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ИдентификаторФайла = Запись.ИдентификаторФайла;
    
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОповещение(Команда)
    ОтправитьОповещениеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИдентификаторФайлаОткрытие(Элемент, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    Если ЗначениеЗаполнено(Запись.ИдентификаторФайла) Тогда
        ОписаниеПередаваемогоФайла = ОписаниеПередаваемогоФайла(Запись.ИдентификаторФайла);
        Оповещение = Новый ОписаниеОповещения("НачатьПолучениеФайловЗавершение", ЭтотОбъект);
        ПолучаемыеФайлы = Новый Массив;
        ПолучаемыеФайлы.Добавить(ОписаниеПередаваемогоФайла);
    	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
    	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
        ДиалогВыбораФайла.Фильтр = НСтр("ru='Все файлы (*.*)|*.*'"); 
        НачатьПолучениеФайлов(Оповещение, ПолучаемыеФайлы, ДиалогВыбораФайла);
    КонецЕсли; 
    
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
    
&НаСервере
Процедура ОтправитьОповещениеНаСервере()
    
    ИнтеграцияОбъектовОбластейДанных.ОповеститьОбИзмененииОбъекта(
        Запись.УчетнаяСистема, Запись.ИдентификаторОбъекта, Истина);
    
КонецПроцедуры

&НаКлиенте 
Процедура НачатьПолучениеФайловЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
    
    Если ЗначениеЗаполнено(ПолученныеФайлы) Тогда
        Оповещение = Новый ОписаниеОповещения("ПриНажатииОповещенияПолученияФайла", ЭтотОбъект, ПолученныеФайлы[0].Имя);
        ПоказатьОповещениеПользователя(НСтр("ru='Файл сохранен'"), Оповещение, ПолученныеФайлы[0].Имя, БиблиотекаКартинок.Информация32);     
    КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ОписаниеПередаваемогоФайла(ИдентификаторФайла)
    
    Возврат ФайлыОбластейДанных.ОписаниеПередаваемогоФайла(ИдентификаторФайла, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Функция ПриНажатииОповещенияПолученияФайла(ПолноеИмяФайла) Экспорт
    
    ПустоеОповещение = Новый ОписаниеОповещения("ПустоеОповещение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ОткрытьФайлВПрограммеПросмотра(ПолноеИмяФайла, ПустоеОповещение);
	
КонецФункции

&НаКлиенте
Процедура ПустоеОповещение(КодВозврата, ДополнительныеПараметры) Экспорт
    
    Возврат;	
    
КонецПроцедуры

#КонецОбласти 
