
#Область СлужебныеПроцедурыИФункции

// Предопределенная процедура проверяет логическую
// связанность введенных данных.
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Не Отбор.Период.Использование 
		Или Отбор.Период.ВидСравнения <> ВидСравнения.Равно Тогда
		Возврат;
	КонецЕсли;
	
	// проверяем набор записей
	ДатаИзмененияГоспошлины = Отбор.Период.Значение;
	ВременнаяТЗ = ЭтотОбъект.Выгрузить();
	ВременнаяТЗ.Сортировать("НомерСтрокиСтавок");
	
	Если ВременнаяТЗ.Количество() = 0 
	 Или ВременнаяТЗ.Количество() <> ВременнаяТЗ[Количество()-1].НомерСтрокиСтавок 
	 Или ВременнаяТЗ[0].НомерСтрокиСтавок <> 1 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("В сведениях о размерах госпошлины обнаружены ошибки. Сведения не записаны!",
										  Отказ);
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(Символы.Таб + "Неверно пронумерованы строки.",Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		Ошибки = Новый Массив;
		ПредыдущийПредел = 0;
		ПредыдущаяСтрока = Неопределено;	
		Для Каждого СтрокаТЗ Из ВременнаяТЗ Цикл
			
			Индекс = ВременнаяТЗ.Индекс(СтрокаТЗ);
			
			СтрокаТЗ.Период = ДатаИзмененияГоспошлины;
			
			Если Индекс = 0 И СтрокаТЗ.СуммаДолгаС <> 0 Тогда
				Ошибки.Добавить("Верхний предел в первой строке должен быть нулевым");
			КонецЕсли;
			Если Индекс = ВременнаяТЗ.Количество()-1 И СтрокаТЗ.СуммаДолгаПо <> 0 Тогда
				Ошибки.Добавить("Нижний предел в последней строке должен быть нулевым");
			КонецЕсли;
			Если ПредыдущийПредел <> 0 И ПредыдущийПредел <> (СтрокаТЗ.СуммаДолгаС - 1) Тогда
				Ошибки.Добавить("В строке " + СтрокаТЗ.НомерСтрокиСтавок 
				+ " верхний передел не совпадает с нижним переделом предыдущей строки");
			КонецЕсли;
			Если СтрокаТЗ.СуммаДолгаС >= СтрокаТЗ.СуммаДолгаПо И НЕ Индекс = ВременнаяТЗ.Количество()-1 Тогда
				Ошибки.Добавить("В строке "+СтрокаТЗ.НомерСтрокиСтавок+" нижний предел должен быть больше верхнего");
			КонецЕсли;
			
			// запомним для следующей итерации
			ПредыдущийПредел = СтрокаТЗ.СуммаДолгаПо;
			ПредыдущаяСтрока = СтрокаТЗ;
			
		КонецЦикла;
		
		Если Ошибки.Количество() > 0 Тогда
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("В сведениях о размерах госпошлины обнаружены ошибки. Сведения не записаны!",
			Отказ);
			Для Каждого Строка Из Ошибки Цикл
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(Символы.Таб + Строка,Отказ);
			КонецЦикла;
		Иначе
			ЭтотОбъект.Загрузить(ВременнаяТЗ)
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти