#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ГоловнаяОрганизация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОсновноеРабочееМестоВОрганизации(НаборЗаписей) Экспорт
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ТаблицаТекущегоНабора = НаборЗаписей.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущийНабор", ТаблицаТекущегоНабора);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущийНабор.Сотрудник КАК Сотрудник,
	|	ТекущийНабор.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТекущийНабор.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ТекущийНабор.ТекущаяОрганизация КАК ТекущаяОрганизация,
	|	ТекущийНабор.ДатаПриема КАК ДатаПриема,
	|	ТекущийНабор.ДатаУвольнения КАК ДатаУвольнения,
	|	ТекущийНабор.ОсновноеРабочееМестоВОрганизации КАК ОсновноеРабочееМестоВОрганизации,
	|	ТекущийНабор.ТекущийВидЗанятости КАК ТекущийВидЗанятости
	|ПОМЕСТИТЬ ВТТекущийНабор
	|ИЗ
	|	&ТекущийНабор КАК ТекущийНабор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация,
	|	ТекущиеКадровыеДанныеСотрудников.ДатаПриема,
	|	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения,
	|	ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости КАК ТекущийВидЗанятости
	|ПОМЕСТИТЬ ВТДругиеЗаписи
	|ИЗ
	|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТекущийНабор КАК ТекущийНабор
	|		ПО ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ТекущийНабор.ФизическоеЛицо
	|			И ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = ТекущийНабор.ГоловнаяОрганизация
	|			И ТекущиеКадровыеДанныеСотрудников.Сотрудник <> ТекущийНабор.Сотрудник
	|			И (ТекущиеКадровыеДанныеСотрудников.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник.ГоловнойСотрудник)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущийНабор.Сотрудник,
	|	ТекущийНабор.ФизическоеЛицо,
	|	ТекущийНабор.ГоловнаяОрганизация,
	|	ТекущийНабор.ТекущаяОрганизация,
	|	ТекущийНабор.ДатаПриема,
	|	ТекущийНабор.ДатаУвольнения,
	|	ТекущийНабор.ОсновноеРабочееМестоВОрганизации,
	|	ТекущийНабор.ТекущийВидЗанятости
	|ПОМЕСТИТЬ ВТВсеЗаписиНабораПредварительно
	|ИЗ
	|	ВТТекущийНабор КАК ТекущийНабор
	|ГДЕ
	|	ТекущийНабор.Сотрудник = ВЫРАЗИТЬ(ТекущийНабор.Сотрудник КАК Справочник.Сотрудники).ГоловнойСотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДругиеЗаписи.Сотрудник,
	|	ДругиеЗаписи.ФизическоеЛицо,
	|	ДругиеЗаписи.ГоловнаяОрганизация,
	|	ДругиеЗаписи.ТекущаяОрганизация,
	|	ДругиеЗаписи.ДатаПриема,
	|	ДругиеЗаписи.ДатаУвольнения,
	|	ДругиеЗаписи.ОсновноеРабочееМестоВОрганизации,
	|	ДругиеЗаписи.ТекущийВидЗанятости
	|ИЗ
	|	ВТДругиеЗаписи КАК ДругиеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадроваяИсторияСотрудников.Сотрудник,
	|	КадроваяИсторияСотрудников.ФизическоеЛицо,
	|	КадроваяИсторияСотрудников.ГоловнаяОрганизация,
	|	МИНИМУМ(КадроваяИсторияСотрудников.Период) КАК Период
	|ПОМЕСТИТЬ ВТМинимальныеПериодыВводаНачальныхДанных
	|ИЗ
	|	ВТВсеЗаписиНабораПредварительно КАК ВсеЗаписиНабораПредварительно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	|		ПО ВсеЗаписиНабораПредварительно.Сотрудник = КадроваяИсторияСотрудников.Сотрудник
	|			И ВсеЗаписиНабораПредварительно.ФизическоеЛицо = КадроваяИсторияСотрудников.ФизическоеЛицо
	|			И ВсеЗаписиНабораПредварительно.ГоловнаяОрганизация = КадроваяИсторияСотрудников.ГоловнаяОрганизация
	|			И (ВсеЗаписиНабораПредварительно.ДатаПриема = ДАТАВРЕМЯ(1, 1, 1))
	|			И (КадроваяИсторияСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.НачальныеДанные))
	|
	|СГРУППИРОВАТЬ ПО
	|	КадроваяИсторияСотрудников.Сотрудник,
	|	КадроваяИсторияСотрудников.ФизическоеЛицо,
	|	КадроваяИсторияСотрудников.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗаписиНабораПредварительно.Сотрудник,
	|	ВсеЗаписиНабораПредварительно.ФизическоеЛицо,
	|	ВсеЗаписиНабораПредварительно.ГоловнаяОрганизация,
	|	ВсеЗаписиНабораПредварительно.ТекущаяОрганизация,
	|	ВсеЗаписиНабораПредварительно.ДатаУвольнения,
	|	ВсеЗаписиНабораПредварительно.ОсновноеРабочееМестоВОрганизации,
	|	ВсеЗаписиНабораПредварительно.ТекущийВидЗанятости,
	|	ВЫБОР
	|		КОГДА НЕ МинимальныеПериодыВводаНачальныхДанных.Период ЕСТЬ NULL 
	|			ТОГДА МинимальныеПериодыВводаНачальныхДанных.Период
	|		ИНАЧЕ ВсеЗаписиНабораПредварительно.ДатаПриема
	|	КОНЕЦ КАК ДатаНачалаУчета
	|ПОМЕСТИТЬ ВТВсеЗаписиНабора
	|ИЗ
	|	ВТВсеЗаписиНабораПредварительно КАК ВсеЗаписиНабораПредварительно
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМинимальныеПериодыВводаНачальныхДанных КАК МинимальныеПериодыВводаНачальныхДанных
	|		ПО ВсеЗаписиНабораПредварительно.Сотрудник = МинимальныеПериодыВводаНачальныхДанных.Сотрудник
	|			И ВсеЗаписиНабораПредварительно.ФизическоеЛицо = МинимальныеПериодыВводаНачальныхДанных.ФизическоеЛицо
	|			И ВсеЗаписиНабораПредварительно.ГоловнаяОрганизация = МинимальныеПериодыВводаНачальныхДанных.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗаписиНабора.Сотрудник,
	|	ВсеЗаписиНабора.ФизическоеЛицо,
	|	ВсеЗаписиНабора.ГоловнаяОрганизация,
	|	ВсеЗаписиНабора.ДатаУвольнения,
	|	ВсеЗаписиНабора.ТекущийВидЗанятости,
	|	ВсеЗаписиНабора.ДатаНачалаУчета
	|ПОМЕСТИТЬ ВТВсеЗаписиРаботающих
	|ИЗ
	|	ВТВсеЗаписиНабора КАК ВсеЗаписиНабора
	|ГДЕ
	|	ВсеЗаписиНабора.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
	|	И ВсеЗаписиНабора.ДатаНачалаУчета <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ВсеЗаписиНабора.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗаписиНабора.Сотрудник,
	|	ВсеЗаписиНабора.ФизическоеЛицо,
	|	ВсеЗаписиНабора.ГоловнаяОрганизация,
	|	ВсеЗаписиНабора.ДатаУвольнения КАК ДатаУвольнения,
	|	ВсеЗаписиНабора.ТекущийВидЗанятости,
	|	ВсеЗаписиНабора.ДатаНачалаУчета
	|ПОМЕСТИТЬ ВТЗаписиСовсемУволенных
	|ИЗ
	|	ВТВсеЗаписиНабора КАК ВсеЗаписиНабора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВсеЗаписиРаботающих КАК ВсеЗаписиРаботающих
	|		ПО ВсеЗаписиНабора.ГоловнаяОрганизация = ВсеЗаписиРаботающих.ГоловнаяОрганизация
	|			И ВсеЗаписиНабора.ФизическоеЛицо = ВсеЗаписиРаботающих.ФизическоеЛицо
	|ГДЕ
	|	ВсеЗаписиРаботающих.Сотрудник ЕСТЬ NULL 
	|	И ВсеЗаписиНабора.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ВсеЗаписиНабора.ДатаНачалаУчета <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ВсеЗаписиНабора.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиСовсемУволенных.ФизическоеЛицо,
	|	ЗаписиСовсемУволенных.ГоловнаяОрганизация,
	|	МАКСИМУМ(ЗаписиСовсемУволенных.ДатаУвольнения) КАК ДатаУвольнения
	|ПОМЕСТИТЬ ВТЗаписиСовсемУволенныеСМаксимальнойДатойУвольнения
	|ИЗ
	|	ВТЗаписиСовсемУволенных КАК ЗаписиСовсемУволенных
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиСовсемУволенных.ФизическоеЛицо,
	|	ЗаписиСовсемУволенных.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиСовсемУволенных.ФизическоеЛицо,
	|	ЗаписиСовсемУволенных.ГоловнаяОрганизация,
	|	ЗаписиСовсемУволенных.ДатаУвольнения,
	|	МИНИМУМ(ЗаписиСовсемУволенных.ТекущийВидЗанятости.Порядок) КАК ТекущийВидЗанятостиПорядок
	|ПОМЕСТИТЬ ВТСовсемУволенныеСПриоритетом
	|ИЗ
	|	ВТЗаписиСовсемУволенных КАК ЗаписиСовсемУволенных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаписиСовсемУволенныеСМаксимальнойДатойУвольнения КАК ЗаписиСовсемУволенныеСМаксимальнойДатойУвольнения
	|		ПО ЗаписиСовсемУволенных.ФизическоеЛицо = ЗаписиСовсемУволенныеСМаксимальнойДатойУвольнения.ФизическоеЛицо
	|			И ЗаписиСовсемУволенных.ГоловнаяОрганизация = ЗаписиСовсемУволенныеСМаксимальнойДатойУвольнения.ГоловнаяОрганизация
	|			И ЗаписиСовсемУволенных.ДатаУвольнения = ЗаписиСовсемУволенныеСМаксимальнойДатойУвольнения.ДатаУвольнения
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиСовсемУволенных.ФизическоеЛицо,
	|	ЗаписиСовсемУволенных.ГоловнаяОрганизация,
	|	ЗаписиСовсемУволенных.ДатаУвольнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗаписиРаботающих.Сотрудник,
	|	ВсеЗаписиРаботающих.ФизическоеЛицо,
	|	ВсеЗаписиРаботающих.ГоловнаяОрганизация,
	|	ВсеЗаписиРаботающих.ТекущийВидЗанятости,
	|	ВсеЗаписиРаботающих.ДатаНачалаУчета
	|ПОМЕСТИТЬ ВТЗаписиКОбработке
	|ИЗ
	|	ВТВсеЗаписиРаботающих КАК ВсеЗаписиРаботающих
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаписиСовсемУволенных.Сотрудник,
	|	ЗаписиСовсемУволенных.ФизическоеЛицо,
	|	ЗаписиСовсемУволенных.ГоловнаяОрганизация,
	|	ЗаписиСовсемУволенных.ТекущийВидЗанятости,
	|	ЗаписиСовсемУволенных.ДатаНачалаУчета
	|ИЗ
	|	ВТЗаписиСовсемУволенных КАК ЗаписиСовсемУволенных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСовсемУволенныеСПриоритетом КАК СовсемУволенныеСПриоритетом
	|		ПО ЗаписиСовсемУволенных.ФизическоеЛицо = СовсемУволенныеСПриоритетом.ФизическоеЛицо
	|			И ЗаписиСовсемУволенных.ГоловнаяОрганизация = СовсемУволенныеСПриоритетом.ГоловнаяОрганизация
	|			И ЗаписиСовсемУволенных.ТекущийВидЗанятости.Порядок = СовсемУволенныеСПриоритетом.ТекущийВидЗанятостиПорядок
	|			И ЗаписиСовсемУволенных.ДатаУвольнения = СовсемУволенныеСПриоритетом.ДатаУвольнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
	|	ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка
	|ПОМЕСТИТЬ ВТТекущиеТарифныеСтавкиСотрудников
	|ИЗ
	|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеЗаписиНабора КАК ВсеЗаписиНабора
	|		ПО ТекущаяТарифнаяСтавкаСотрудников.Сотрудник = ВсеЗаписиНабора.Сотрудник
	|			И ТекущаяТарифнаяСтавкаСотрудников.ФизическоеЛицо = ВсеЗаписиНабора.ФизическоеЛицо
	|			И ТекущаяТарифнаяСтавкаСотрудников.ГоловнаяОрганизация = ВсеЗаписиНабора.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВсеЗаписиНабора.Сотрудник КАК Справочник.Сотрудники) КАК Сотрудник,
	|	ВсеЗаписиНабора.ФизическоеЛицо,
	|	ВсеЗаписиНабора.ГоловнаяОрганизация,
	|	ВсеЗаписиНабора.ДатаНачалаУчета,
	|	ВЫРАЗИТЬ(ВсеЗаписиНабора.ТекущийВидЗанятости КАК Перечисление.ВидыЗанятости) КАК ТекущийВидЗанятости,
	|	ЕСТЬNULL(ТекущиеТарифныеСтавкиСотрудников.ТекущаяТарифнаяСтавка, 0) КАК ТекущаяТарифнаяСтавка
	|ПОМЕСТИТЬ ВТВсеЗаписиНабораСТарифнойСтавкой
	|ИЗ
	|	ВТЗаписиКОбработке КАК ВсеЗаписиНабора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущиеТарифныеСтавкиСотрудников КАК ТекущиеТарифныеСтавкиСотрудников
	|		ПО ВсеЗаписиНабора.Сотрудник = ТекущиеТарифныеСтавкиСотрудников.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо,
	|	ВсеЗаписиНабораСТарифнойСтавкой.ГоловнаяОрганизация,
	|	МИНИМУМ(ВсеЗаписиНабораСТарифнойСтавкой.ТекущийВидЗанятости.Порядок) КАК СотрудникВидЗанятостиПорядок
	|ПОМЕСТИТЬ ВТЗаписиСПриоритетомВидаЗанятости
	|ИЗ
	|	ВТВсеЗаписиНабораСТарифнойСтавкой КАК ВсеЗаписиНабораСТарифнойСтавкой
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо,
	|	ВсеЗаписиНабораСТарифнойСтавкой.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиСПриоритетомВидаЗанятости.ФизическоеЛицо,
	|	ЗаписиСПриоритетомВидаЗанятости.ГоловнаяОрганизация,
	|	ЗаписиСПриоритетомВидаЗанятости.СотрудникВидЗанятостиПорядок,
	|	МАКСИМУМ(ВсеЗаписиНабораСТарифнойСтавкой.ТекущаяТарифнаяСтавка) КАК ТекущаяТарифнаяСтавка
	|ПОМЕСТИТЬ ВТЗаписиСПриоритетомТарифнойСтавки
	|ИЗ
	|	ВТЗаписиСПриоритетомВидаЗанятости КАК ЗаписиСПриоритетомВидаЗанятости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеЗаписиНабораСТарифнойСтавкой КАК ВсеЗаписиНабораСТарифнойСтавкой
	|		ПО ЗаписиСПриоритетомВидаЗанятости.ФизическоеЛицо = ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо
	|			И ЗаписиСПриоритетомВидаЗанятости.ГоловнаяОрганизация = ВсеЗаписиНабораСТарифнойСтавкой.ГоловнаяОрганизация
	|			И ЗаписиСПриоритетомВидаЗанятости.СотрудникВидЗанятостиПорядок = ВсеЗаписиНабораСТарифнойСтавкой.ТекущийВидЗанятости.Порядок
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиСПриоритетомВидаЗанятости.ФизическоеЛицо,
	|	ЗаписиСПриоритетомВидаЗанятости.ГоловнаяОрганизация,
	|	ЗаписиСПриоритетомВидаЗанятости.СотрудникВидЗанятостиПорядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиСПриоритетомТарифнойСтавки.ФизическоеЛицо,
	|	ЗаписиСПриоритетомТарифнойСтавки.ГоловнаяОрганизация,
	|	ЗаписиСПриоритетомТарифнойСтавки.СотрудникВидЗанятостиПорядок,
	|	ЗаписиСПриоритетомТарифнойСтавки.ТекущаяТарифнаяСтавка,
	|	МИНИМУМ(ВсеЗаписиНабораСТарифнойСтавкой.ДатаНачалаУчета) КАК ДатаНачалаУчета
	|ПОМЕСТИТЬ ВТЗаписиСПриоритетомДатыПриема
	|ИЗ
	|	ВТЗаписиСПриоритетомТарифнойСтавки КАК ЗаписиСПриоритетомТарифнойСтавки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеЗаписиНабораСТарифнойСтавкой КАК ВсеЗаписиНабораСТарифнойСтавкой
	|		ПО ЗаписиСПриоритетомТарифнойСтавки.ФизическоеЛицо = ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо
	|			И ЗаписиСПриоритетомТарифнойСтавки.ГоловнаяОрганизация = ВсеЗаписиНабораСТарифнойСтавкой.ГоловнаяОрганизация
	|			И ЗаписиСПриоритетомТарифнойСтавки.СотрудникВидЗанятостиПорядок = ВсеЗаписиНабораСТарифнойСтавкой.ТекущийВидЗанятости.Порядок
	|			И ЗаписиСПриоритетомТарифнойСтавки.ТекущаяТарифнаяСтавка = ВсеЗаписиНабораСТарифнойСтавкой.ТекущаяТарифнаяСтавка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиСПриоритетомТарифнойСтавки.ФизическоеЛицо,
	|	ЗаписиСПриоритетомТарифнойСтавки.ГоловнаяОрганизация,
	|	ЗаписиСПриоритетомТарифнойСтавки.СотрудникВидЗанятостиПорядок,
	|	ЗаписиСПриоритетомТарифнойСтавки.ТекущаяТарифнаяСтавка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВсеЗаписиНабораСТарифнойСтавкой.Сотрудник) КАК Сотрудник,
	|	ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТОсновноеМестоРаботы
	|ИЗ
	|	ВТВсеЗаписиНабораСТарифнойСтавкой КАК ВсеЗаписиНабораСТарифнойСтавкой
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаписиСПриоритетомДатыПриема КАК ЗаписиСПриоритетомДатыПриема
	|		ПО ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо = ЗаписиСПриоритетомДатыПриема.ФизическоеЛицо
	|			И ВсеЗаписиНабораСТарифнойСтавкой.ГоловнаяОрганизация = ЗаписиСПриоритетомДатыПриема.ГоловнаяОрганизация
	|			И ВсеЗаписиНабораСТарифнойСтавкой.ТекущийВидЗанятости.Порядок = ЗаписиСПриоритетомДатыПриема.СотрудникВидЗанятостиПорядок
	|			И ВсеЗаписиНабораСТарифнойСтавкой.ДатаНачалаУчета = ЗаписиСПриоритетомДатыПриема.ДатаНачалаУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеЗаписиНабораСТарифнойСтавкой.ГоловнаяОрганизация,
	|	ВсеЗаписиНабораСТарифнойСтавкой.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗаписиНабора.Сотрудник,
	|	ВЫБОР
	|		КОГДА ОсновноеМестоРаботы.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОсновноеРабочееМестоВОрганизации
	|ИЗ
	|	ВТВсеЗаписиНабора КАК ВсеЗаписиНабора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновноеМестоРаботы КАК ОсновноеМестоРаботы
	|		ПО ВсеЗаписиНабора.Сотрудник = ОсновноеМестоРаботы.Сотрудник
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ОсновноеМестоРаботы.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ <> ВсеЗаписиНабора.ОсновноеРабочееМестоВОрганизации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаСотрудника = ТаблицаТекущегоНабора.Найти(Выборка.Сотрудник, "Сотрудник");
		
		Если СтрокаСотрудника = Неопределено Тогда
			
			Набор = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
			Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			Набор.Прочитать();
			
			Если Набор.Количество() > 0 Тогда
				
				Набор[0].ОсновноеРабочееМестоВОрганизации = Выборка.ОсновноеРабочееМестоВОрганизации;
				Набор.ОбменДанными.Загрузка = Истина;
				Набор.Записать();
				
			КонецЕсли; 
			
		Иначе
			
			СтрокаСотрудника.ОсновноеРабочееМестоВОрганизации = Выборка.ОсновноеРабочееМестоВОрганизации;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Загрузить(ТаблицаТекущегоНабора);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
