#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ГоловнаяОрганизация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет интервальный регистр сведений ВидыЗанятостиСотрудниковИнтервальный.
//
Процедура ЗаполнитьИнтервальныйРегистр(ПараметрыОбновления = Неопределено) Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияИнтервальногоРегистра();
	ЗарплатаКадрыПериодическиеРегистры.ПеренестиВозвратныйРегистрВИнтервальныйРегистрСведений("ВидыЗанятостиСотрудников", ПараметрыПостроения, ПараметрыОбновления);
	
КонецПроцедуры

// Заполняет виды занятости пустых интервалов регистра сведений ВидыЗанятостиСотрудниковИнтервальный.
//
Процедура ЗаполнитьВидЗанятостиПустыхИнтерваловИнтервальногоРегистра(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыЗанятостиСотрудниковИнтервальныйПредыдущий.ВидЗанятости КАК ВидЗанятости,
		|	ВидыЗанятостиСотрудниковИнтервальный.Сотрудник КАК Сотрудник,
		|	ВидыЗанятостиСотрудниковИнтервальный.ДатаОкончания КАК ДатаОкончания,
		|	ВидыЗанятостиСотрудниковИнтервальный.*
		|ИЗ
		|	РегистрСведений.ВидыЗанятостиСотрудниковИнтервальный КАК ВидыЗанятостиСотрудниковИнтервальный
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВидыЗанятостиСотрудниковИнтервальный КАК ВидыЗанятостиСотрудниковИнтервальныйПредыдущий
		|		ПО ВидыЗанятостиСотрудниковИнтервальный.Сотрудник = ВидыЗанятостиСотрудниковИнтервальныйПредыдущий.Сотрудник
		|			И ВидыЗанятостиСотрудниковИнтервальный.ГоловнаяОрганизация = ВидыЗанятостиСотрудниковИнтервальныйПредыдущий.ГоловнаяОрганизация
		|			И ВидыЗанятостиСотрудниковИнтервальный.ФизическоеЛицо = ВидыЗанятостиСотрудниковИнтервальныйПредыдущий.ФизическоеЛицо
		|			И (ВидыЗанятостиСотрудниковИнтервальный.ДатаНачала = ДОБАВИТЬКДАТЕ(ВидыЗанятостиСотрудниковИнтервальныйПредыдущий.ДатаОкончания, СЕКУНДА, 1))
		|			И ВидыЗанятостиСотрудниковИнтервальный.ВидЗанятости <> ВидыЗанятостиСотрудниковИнтервальныйПредыдущий.ВидЗанятости
		|ГДЕ
		|	ВидыЗанятостиСотрудниковИнтервальный.ПустойИнтервал
		|	И ВидыЗанятостиСотрудниковИнтервальный.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ВидыЗанятостиСотрудниковИнтервальный", "Сотрудник", Выборка.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыСведений.ВидыЗанятостиСотрудниковИнтервальный.СоздатьНаборЗаписей();
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
		НаборЗаписей.Отбор.ДатаОкончания.Установить(Выборка.ДатаОкончания);
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Вызывается для формирования интервального регистра из обработчиков обновления основного.
// В передаваемом МенеджерВременныхТаблиц должна быть создана временная таблица ВТОтборДляПереформирования
// с колонкой Сотрудник.
//
Процедура СформироватьДвиженияИнтервальногоРегистра(МенеджерВременныхТаблиц, ПараметрыОбновления) Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияИнтервальногоРегистра();
	
	ПараметрыПостроения.Вставить("ПолноеПереформирование", Истина);
	ПараметрыПостроения.Вставить("РежимЗагрузки", Истина);
	
	ЗарплатаКадрыПериодическиеРегистры.СформироватьДвиженияИнтервальногоРегистра(
		"ВидыЗанятостиСотрудников", МенеджерВременныхТаблиц, ПараметрыПостроения, ПараметрыОбновления);
	
КонецПроцедуры

Функция ПараметрыПостроенияИнтервальногоРегистра()
	
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияИнтервальногоРегистра();
	ПараметрыПостроения.ПараметрыРесурсов = ПараметрыНаследованияРесурсов();
	
	ПараметрыПостроения.ОсновноеИзмерение = "Сотрудник";
	
	Возврат ПараметрыПостроения;
	
КонецФункции

Функция ПараметрыНаследованияРесурсов() Экспорт
	
	ПараметрыРесурсов = ЗарплатаКадрыПериодическиеРегистры.ПараметрыНаследованияРесурсов("ВидыЗанятостиСотрудников");
	ПараметрыРесурсов["ВидЗанятости"].ПравилоНаследования = "Наследование";
	
	Возврат ПараметрыРесурсов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
