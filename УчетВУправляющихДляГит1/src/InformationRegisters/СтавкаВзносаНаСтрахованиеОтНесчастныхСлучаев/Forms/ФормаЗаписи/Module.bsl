
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура("Организация, Период", СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Организация, СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период);
	Оповестить("Запись_СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев", ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период));
	ПараметрыВыбора.Вставить("КонецПериода",  КонецМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период));
	ПараметрыВыбора.Вставить("ВидПериода",    ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	НачалоПериода = НачалоМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период);
	КонецПериода  = КонецМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период);
	
	ВыборПериодаКлиент.ПериодАвтоПодбор(
		Элемент,
		Текст,
		ДанныеВыбора,
		Ожидание,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		Период,
		НачалоПериода,
		КонецПериода);
	
КонецПроцедуры


&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			РезультатВыбора.НачалоПериода,
			РезультатВыбора.КонецПериода,
			Истина);
		
		Модифицированность = Истина;
		
		СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период = РезультатВыбора.НачалоПериода;
		
	КонецЕсли;
	
КонецПроцедуры


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период),
		КонецМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период),
		Истина);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НачалоПериода = НачалоМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период);
	КонецПериода  = КонецМесяца(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период);
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора(
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		Период,
		НачалоПериода,
		КонецПериода);
	
	Модифицированность = Истина;
	
	СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период = НачалоПериода;
	
КонецПроцедуры

#КонецОбласти