
#Область ОсновныеПроцедурыИФункции

// Процедура-обработчик события "ПриСозданииНаСервере" формы.
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установим отбор по организации.
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Организация", Параметры.Организация,
			ВидСравненияКомпоновкиДанных.Равно, , ЗначениеЗаполнено(Параметры.Организация));
		
		Организация = Параметры.Организация;
		
	КонецЕсли;
	
	// Установим отбор по признаку "Это счета учета для капитального ремонта".
	Если Параметры.Свойство("ЭтоСчетаДляКапРемонта") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ЭтоСчетаДляКапРемонта", Параметры.ЭтоСчетаДляКапРемонта,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		
		ЭтоСчетаДляКапРемонта = Параметры.ЭтоСчетаДляКапРемонта;
		
		Элементы.ЭтоСчетаДляКапРемонта.Видимость = Ложь;
		
	КонецЕсли;
	
	Автозаголовок = Ложь;
	Заголовок     = "Счета учета расчетов по пеням" + ?(ЭтоСчетаДляКапРемонта, " за капитальный ремонт", "") +
		?(Не Организация.Пустая(), " для " + Организация, "");
	
КонецПроцедуры

// Процедура-обработчик события "ПередНачаломДобавления" элемента формы "Список".
&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ОткрытьФорму("РегистрСведений.УПЖКХ_СчетаУчетаРасчетовПоПени.ФормаЗаписи", Новый Структура("Организация, ЭтоСчетаДляКапРемонта", Организация, ЭтоСчетаДляКапРемонта));
	
КонецПроцедуры

// Процедура-обработчик события "ПослеУдаления" элемента формы "Список".
&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	// В УП меняется заголовок гиперссылки перехода к счетам учета по пени.
	Оповестить("ИзмененыСчетаУчетаПоПени");
	
КонецПроцедуры

#КонецОбласти 
