#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция АбсолютныйКаталогОбменаИнформацией(Знач Корреспондент) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НастройкиТранспортаОбластиДанных.КаталогОбменаИнформацией КАК ОтносительныйКаталогОбменаИнформацией,
	|	ЕСТЬNULL(НастройкиТранспортаОбластейДанных.FILEКаталогОбменаИнформацией, """") КАК ОбщийКаталогОбменаИнформацией
	|ИЗ
	|	РегистрСведений.НастройкиТранспортаОбменаОбластиДанных КАК НастройкиТранспортаОбластиДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиТранспортаОбменаОбластейДанных КАК НастройкиТранспортаОбластейДанных
	|		ПО (НастройкиТранспортаОбластейДанных.КонечнаяТочкаКорреспондента = НастройкиТранспортаОбластиДанных.КонечнаяТочкаКорреспондента)
	|ГДЕ
	|	НастройкиТранспортаОбластиДанных.Корреспондент = &Корреспондент";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Корреспондент", Корреспондент);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заданы настройки подключения для корреспондента ""%1"".'"), Строка(Корреспондент));
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ОбщийКаталогОбменаИнформацией = Выборка.ОбщийКаталогОбменаИнформацией;
	ОтносительныйКаталогОбменаИнформацией = Выборка.ОтносительныйКаталогОбменаИнформацией;
	
	Если ПустаяСтрока(ОбщийКаталогОбменаИнформацией)
		ИЛИ ПустаяСтрока(ОтносительныйКаталогОбменаИнформацией) Тогда
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заданы настройки подключения для корреспондента ""%1"".'"), Строка(Корреспондент));
	КонецЕсли;
	
	Возврат ОбщегоНазначенияКлиентСервер.ПолучитьПолноеИмяФайла(ОбщийКаталогОбменаИнформацией, ОтносительныйКаталогОбменаИнформацией);
КонецФункции

Функция ВидТранспорта(Знач Корреспондент) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(НастройкиТранспортаОбменаОбластейДанных.ВидТранспортаСообщенийОбменаПоУмолчанию, НЕОПРЕДЕЛЕНО) КАК ВидТранспорта
	|ИЗ
	|	РегистрСведений.НастройкиТранспортаОбменаОбластиДанных КАК НастройкиТранспортаОбменаОбластиДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиТранспортаОбменаОбластейДанных КАК НастройкиТранспортаОбменаОбластейДанных
	|		ПО (НастройкиТранспортаОбменаОбластейДанных.КонечнаяТочкаКорреспондента = НастройкиТранспортаОбменаОбластиДанных.КонечнаяТочкаКорреспондента)
	|ГДЕ
	|	НастройкиТранспортаОбменаОбластиДанных.Корреспондент = &Корреспондент";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Корреспондент", Корреспондент);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ВидТранспорта;
КонецФункции

Функция НастройкиТранспорта(Знач Корреспондент) Экспорт
	
	ВерсияБСП301 = ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СтандартныеПодсистемыСервер.ВерсияБиблиотеки(), "3.0.1.1") >= 0;
	Если ВерсияБСП301 Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	"""" КАК FILEКаталогОбменаИнформацией,
		|	"""" КАК FTPСоединениеПуть,
		|	НастройкиТранспортаОбластиДанных.КаталогОбменаИнформацией КАК ОтносительныйКаталогОбменаИнформацией,
		|	НастройкиТранспортаОбластейДанных.FILEКаталогОбменаИнформацией КАК FILEОбщийКаталогОбменаИнформацией,
		|	НастройкиТранспортаОбластейДанных.FILEСжиматьФайлИсходящегоСообщения,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПуть КАК FTPОбщийКаталогОбменаИнформацией,
		|	НастройкиТранспортаОбластейДанных.FTPСжиматьФайлИсходящегоСообщения,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеМаксимальныйДопустимыйРазмерСообщения,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПассивноеСоединение,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПользователь,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПорт,
		|	НастройкиТранспортаОбластейДанных.ВидТранспортаСообщенийОбменаПоУмолчанию,
		|	НастройкиТранспорта.АдресВебСервиса КАК WSURLВебСервиса,
		|	НастройкиТранспорта.ИмяПользователя КАК WSИмяПользователя
		|ИЗ
		|	РегистрСведений.НастройкиТранспортаОбменаОбластиДанных КАК НастройкиТранспортаОбластиДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиТранспортаОбменаОбластейДанных КАК НастройкиТранспортаОбластейДанных
		|		ПО (НастройкиТранспортаОбластейДанных.КонечнаяТочкаКорреспондента = НастройкиТранспортаОбластиДанных.КонечнаяТочкаКорреспондента)
		|		ЛЕВОЕ СОЕДИНЕНИЕ #ТаблицаНастройкиТранспорта КАК НастройкиТранспорта
		|		ПО (НастройкиТранспорта.КонечнаяТочка = НастройкиТранспортаОбластиДанных.КонечнаяТочкаКорреспондента)
		|ГДЕ
		|	НастройкиТранспортаОбластиДанных.Корреспондент = &Корреспондент";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаНастройкиТранспорта", "РегистрСведений.НастройкиТранспортаОбменаСообщениями");
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	"""" КАК FILEКаталогОбменаИнформацией,
		|	"""" КАК FTPСоединениеПуть,
		|	НастройкиТранспортаОбластиДанных.КаталогОбменаИнформацией КАК ОтносительныйКаталогОбменаИнформацией,
		|	НастройкиТранспортаОбластейДанных.FILEКаталогОбменаИнформацией КАК FILEОбщийКаталогОбменаИнформацией,
		|	НастройкиТранспортаОбластейДанных.FILEСжиматьФайлИсходящегоСообщения,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПуть КАК FTPОбщийКаталогОбменаИнформацией,
		|	НастройкиТранспортаОбластейДанных.FTPСжиматьФайлИсходящегоСообщения,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеМаксимальныйДопустимыйРазмерСообщения,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПассивноеСоединение,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПользователь,
		|	НастройкиТранспортаОбластейДанных.FTPСоединениеПорт,
		|	НастройкиТранспортаОбластейДанных.ВидТранспортаСообщенийОбменаПоУмолчанию,
		|	НастройкиТранспорта.WSURLВебСервиса,
		|	НастройкиТранспорта.WSИмяПользователя
		|ИЗ
		|	РегистрСведений.НастройкиТранспортаОбменаОбластиДанных КАК НастройкиТранспортаОбластиДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиТранспортаОбменаОбластейДанных КАК НастройкиТранспортаОбластейДанных
		|		ПО (НастройкиТранспортаОбластейДанных.КонечнаяТочкаКорреспондента = НастройкиТранспортаОбластиДанных.КонечнаяТочкаКорреспондента)
		|		ЛЕВОЕ СОЕДИНЕНИЕ #ТаблицаНастройкиТранспорта КАК НастройкиТранспорта
		|		ПО (НастройкиТранспорта.Узел = НастройкиТранспортаОбластиДанных.КонечнаяТочкаКорреспондента)
		|ГДЕ
		|	НастройкиТранспортаОбластиДанных.Корреспондент = &Корреспондент";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаНастройкиТранспорта", "РегистрСведений.НастройкиТранспортаОбмена");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Корреспондент", Корреспондент);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ОбменДаннымиСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
	УстановитьПривилегированныйРежим(Истина);
	Пароли = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Корреспондент, "FTPСоединениеПарольОбластейДанных,WSПароль, ПарольАрхиваСообщенияОбменаОбластейДанных");
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат.Вставить("WSПароль", Пароли.WSПароль);
	Результат.Вставить("FTPСоединениеПароль", Пароли.FTPСоединениеПарольОбластейДанных);
	Результат.Вставить("ПарольАрхиваСообщенияОбмена", Пароли.ПарольАрхиваСообщенияОбменаОбластейДанных);
	
	Результат.FILEКаталогОбменаИнформацией = ОбщегоНазначенияКлиентСервер.ПолучитьПолноеИмяФайла(
		Результат.FILEОбщийКаталогОбменаИнформацией,
		Результат.ОтносительныйКаталогОбменаИнформацией);
	
	Результат.FTPСоединениеПуть = ОбщегоНазначенияКлиентСервер.ПолучитьПолноеИмяФайла(
		Результат.FTPОбщийКаталогОбменаИнформацией,
		Результат.ОтносительныйКаталогОбменаИнформацией);
	
	Результат.Вставить("ИспользоватьВременныйКаталогДляОтправкиИПриемаСообщений", Истина);
	
	Если Результат.ВидТранспортаСообщенийОбменаПоУмолчанию = Перечисления.ВидыТранспортаСообщенийОбмена.FTP Тогда
		
		ПараметрыFTP = ОбменДаннымиСервер.FTPИмяСервераИПуть(Результат.FTPСоединениеПуть);
		
		Результат.Вставить("FTPСервер", ПараметрыFTP.Сервер);
		Результат.Вставить("FTPПуть",   ПараметрыFTP.Путь);
		
	Иначе
		Результат.Вставить("FTPСервер", "");
		Результат.Вставить("FTPПуть",   "");
	КонецЕсли;
	
	ОбменДаннымиСервер.ДополнитьНастройкиТранспортаКоличествомЭлементовВТранзакции(Результат);
	
	Возврат Результат;
КонецФункции

Функция НастройкиТранспортаWS(Знач Корреспондент) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиТранспортаОбластиДанных.КонечнаяТочкаКорреспондента КАК КонечнаяТочкаКорреспондента
	|ИЗ
	|	РегистрСведений.НастройкиТранспортаОбменаОбластиДанных КАК НастройкиТранспортаОбластиДанных
	|ГДЕ
	|	НастройкиТранспортаОбластиДанных.Корреспондент = &Корреспондент");
	Запрос.УстановитьПараметр("Корреспондент", Корреспондент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	КонечнаяТочка = Выборка.КонечнаяТочкаКорреспондента;
	
	ВерсияБСП301 = ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СтандартныеПодсистемыСервер.ВерсияБиблиотеки(), "3.0.1.1") >= 0;
	Если ВерсияБСП301 Тогда
		МодульНастройкиТранспорта = РегистрыСведений["НастройкиТранспортаОбменаСообщениями"];
	Иначе
		МодульНастройкиТранспорта = РегистрыСведений["НастройкиТранспортаОбмена"];
	КонецЕсли;
	
	СтруктураНастроек = МодульНастройкиТранспорта.НастройкиТранспортаWS(КонечнаяТочка);
	
	Если Не ЗначениеЗаполнено(СтруктураНастроек.WSURLВебСервиса) Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заданы настройки подключения веб-сервиса для корреспондента %1.'"),
			Строка(Корреспондент));
	КонецЕсли;
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Процедура обновляет запись в регистре по переданным значениям структуры
//
Процедура ОбновитьЗапись(СтруктураЗаписи) Экспорт
	
	ОбменДаннымиСервер.ОбновитьЗаписьВРегистрСведений(СтруктураЗаписи, "НастройкиТранспортаОбменаОбластиДанных");
	
КонецПроцедуры

// Процедура добавляет запись в регистр по переданным значениям структуры
//
Процедура ДобавитьЗапись(СтруктураЗаписи) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "НастройкиТранспортаОбменаОбластиДанных");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
