
////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ПрочиеПроцедурыИФункции

&НаСервере
// Процедура изменяет порядок у текущей услуги в регистре сведений "Порядок услуг в квитанции".
Процедура ИзменитьПорядокУТекущегоЭлемента(НастройкаСбораДанных, ВидУслуги, ПоднятьВверх)
	
	ТекДанные = РегистрыСведений.КВП_ПорядокУслугВКвитанции.Получить(Новый Структура("НастройкаСбораДанных, ВидУслуги", НастройкаСбораДанных, ВидУслуги));
	
	ТЗ =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КВП_ПорядокУслугВКвитанции.НастройкаСбораДанных,
	|	КВП_ПорядокУслугВКвитанции.ВидУслуги,
	|	КВП_ПорядокУслугВКвитанции.Порядок
	|ИЗ
	|	РегистрСведений.КВП_ПорядокУслугВКвитанции КАК КВП_ПорядокУслугВКвитанции
	|ГДЕ
	|	КВП_ПорядокУслугВКвитанции.Порядок " + ?(ПоднятьВверх, "<", ">") + " &Порядок
	|
	|УПОРЯДОЧИТЬ ПО
	|	КВП_ПорядокУслугВКвитанции.Порядок " + ?(ПоднятьВверх, "Убыв", "Возр");
	
	Запрос = Новый Запрос(ТЗ);
	Запрос.УстановитьПараметр("Порядок",         ТекДанные.Порядок);
	Запрос.УстановитьПараметр("НастройкаСбораДанных", НастройкаСбораДанных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей1 = РегистрыСведений.КВП_ПорядокУслугВКвитанции.СоздатьНаборЗаписей();
	НаборЗаписей1.Отбор.НастройкаСбораДанных.Установить(НастройкаСбораДанных);
	НаборЗаписей1.Отбор.ВидУслуги.Установить(ВидУслуги);
	НаборЗаписей1.Прочитать();
	Объект1 = НаборЗаписей1.Получить(0);
	НаборЗаписей2 = РегистрыСведений.КВП_ПорядокУслугВКвитанции.СоздатьНаборЗаписей();
	НаборЗаписей2.Отбор.НастройкаСбораДанных.Установить(Выборка.НастройкаСбораДанных);
	НаборЗаписей2.Отбор.ВидУслуги.Установить(Выборка.ВидУслуги);
	НаборЗаписей2.Прочитать();
	Объект2 = НаборЗаписей2.Получить(0);
	
	НачатьТранзакцию();
	
	Порядок1 = Объект1.Порядок;
	Объект1.Порядок = Объект2.Порядок;
	Объект2.Порядок = Порядок1;
	НаборЗаписей1.Записать();
	НаборЗаписей2.Записать();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

#Область ОбработчикиКоманд

&НаКлиенте
// Обработчик команды "КомандаПереместитьВВерх".
Процедура КомандаПереместитьВВерх(Команда)
	
	// Ключ текущей записи.
	ТекущиеДанные = Элементы.ПорядокУслугВКвитанции.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьПорядокУТекущегоЭлемента(ТекущиеДанные.НастройкаСбораДанных, ТекущиеДанные.ВидУслуги, Истина);
	
	Элементы.ПорядокУслугВКвитанции.Обновить();
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "КомандаПереместитьВниз".
Процедура КомандаПереместитьВниз(Команда)
	
	// Ключ текущей записи.
	ТекущиеДанные = Элементы.ПорядокУслугВКвитанции.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьПорядокУТекущегоЭлемента(ТекущиеДанные.НастройкаСбораДанных, ТекущиеДанные.ВидУслуги, Ложь);
	
	Элементы.ПорядокУслугВКвитанции.Обновить();
	
КонецПроцедуры

#КонецОбласти