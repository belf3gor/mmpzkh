#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти
	
Процедура СформироватьДвиженияИмпортТоваровТС(ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетИмпортныхТоваров") Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не Реквизиты.ЕстьТовары
	 Или Реквизиты.УчетАгентскогоНДС
	 Или Не УчетНДС.КонтрагентРезидентТаможенногоСоюза(Реквизиты.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыИмпортТоваровТС(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Движение = Движения.ИмпортТоваровТаможенныйСоюз.Добавить();
		
	Движение.Организация         = Реквизиты.Организация;
	Движение.ДатаПериода         = НачалоМесяца(Реквизиты.Период);
	Движение.ДокументПоступления = Реквизиты.Регистратор;
	
	Движения.ИмпортТоваровТаможенныйСоюз.Записывать = Истина;
	
	Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.НДС_ВвозимыеТовары); 
	
КонецПроцедуры

Функция ПодготовитьПараметрыИмпортТоваровТС(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Регистратор,"              // <ДокументСсылка>
	+ "Организация,"              // <СправочникСсылка.Организации>
	+ "ЕстьТовары,"              // <Булево>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "Контрагент"              // <СправочникСсылка.Контрагенты>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

#КонецЕсли