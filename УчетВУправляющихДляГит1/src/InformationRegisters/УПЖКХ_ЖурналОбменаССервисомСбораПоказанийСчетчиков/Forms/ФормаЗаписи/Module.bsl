
#Область ОбработчикиСобытийФормы

&НаКлиенте
// Обработчик события "ПриОткрытии" формы.
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКоманд

&НаКлиенте
// Обработчик команды "ОткрытьФайлОбмена".
Процедура КомандаОткрытьФайлОбмена(Команда)
	
	ФайлОбмена = ПолучитьФайлОбменаНаСервере();
	
	Если ФайлОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("РасширениеФайла, СохраненныйФайл", ".txt", ФайлОбмена);
	
	НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("НачатьПолучениеКаталогаВременныхФайловЗавершение", ЭтаФорма, ДополнительныеПараметры));
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеКаталогаВременныхФайловЗавершение(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	УникальнаяСтрока = Новый УникальныйИдентификатор;
	
	ИмяФайла = ИмяКаталогаВременныхФайлов + УникальнаяСтрока + ДополнительныеПараметры.РасширениеФайла;
	
	Попытка
		ОписаниеОповещения = Новый ОписаниеОповещения("НачатьЗаписьЗавершение", ЭтаФорма, ИмяФайла);
		ДополнительныеПараметры.СохраненныйФайл.НачатьЗапись(ОписаниеОповещения, ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не удалось открыть файл обмена: " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапись".
Процедура НачатьЗаписьЗавершение(ИмяФайла) Экспорт
	
	Попытка
		НачатьЗапускПриложения(Новый ОписаниеОповещения("НачатьЗапускПриложенияЗавершение", ЭтаФорма), ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(СтрШаблон("Не удалось открыть файл обмена: %1", ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапускПриложения".
Процедура НачатьЗапускПриложенияЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиенте
// Управление видимостью элементов формы.
Процедура УправлениеФормой()
	
	Элементы.ОткрытьФайлОбмена.Видимость = Запись.ЕстьФайлОбмена;
	
КонецПроцедуры

&НаСервере
// Получает файл обмена данными с сервисом.
Функция ПолучитьФайлОбменаНаСервере()
	
	НайденнаяЗапись = РегистрыСведений.УПЖКХ_ЖурналОбменаССервисомСбораПоказанийСчетчиков.Получить(Новый Структура("ДатаОбмена, Организация, ПроизводителиСервиса",
																													Запись.ДатаОбмена, Запись.Организация, Запись.ПроизводителиСервиса));
	
	Если Не НайденнаяЗапись = Неопределено Тогда
		ФайлОбмена = НайденнаяЗапись.ФайлОбмена.Получить();
	Иначе
		ФайлОбмена = Неопределено;
	КонецЕсли;
	
	Возврат ФайлОбмена;
	
КонецФункции

#КонецОбласти