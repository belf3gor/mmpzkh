#Область ОбработчикиСобытийФормыИЭлементовУправления

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НазваниеТОГС = "";
	
	ИмяРегистра = СтрРазделить(ЭтотОбъект.ИмяФормы, ".")[1];
	АдресСервераСбораОтчетностиРосстата = РегистрыСведений[ИмяРегистра].АдресСервераССОРосстата(НазваниеТОГС);
	
	Если ЗначениеЗаполнено(НазваниеТОГС) Тогда
		АдресСервераСбораОтчетностиРосстата = АдресСервераСбораОтчетностиРосстата + "{" + НазваниеТОГС + "}";
		Элементы.АдресСервераСбораОтчетностиРосстата.Заголовок =
			Элементы.АдресСервераСбораОтчетностиРосстата.Заголовок + " (" + НазваниеТОГС + ")";
	КонецЕсли;
	
	АдресаВебСервисовРосстата(Элементы.АдресСервераСбораОтчетностиРосстата.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервераСбораОтчетностиРосстатаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранныйЭлементСписка = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ВыбранныйЭлементСписка = Неопределено И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Для Каждого ЭлементСпискаВыбора Из Элемент.СписокВыбора Цикл
			Если СтрНайти(ЭлементСпискаВыбора.Значение, ВыбранноеЗначение) > 0 Тогда
				ВыбранныйЭлементСписка = ЭлементСпискаВыбора;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВыбранныйЭлементСписка <> Неопределено Тогда
		
		АдресСервераСбораОтчетностиРосстата = ВыбранныйЭлементСписка.Значение;
		
		ПослеВводаАдреса(ВыбранныйЭлементСписка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервераСбораОтчетностиРосстатаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ВыбранныйЭлементСписка = Элемент.СписокВыбора.НайтиПоЗначению(Текст);
	Если ВыбранныйЭлементСписка = Неопределено И ЗначениеЗаполнено(Текст) Тогда
		Для Каждого ЭлементСпискаВыбора Из Элемент.СписокВыбора Цикл
			Если СтрНайти(ЭлементСпискаВыбора.Значение, Текст) > 0 Тогда
				ВыбранныйЭлементСписка = ЭлементСпискаВыбора;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВыбранныйЭлементСписка <> Неопределено Тогда
		
		АдресСервераСбораОтчетностиРосстата = ВыбранныйЭлементСписка.Значение;
		
		ПослеВводаАдреса(ВыбранныйЭлементСписка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Закрыть(АдресСервераСбораОтчетностиРосстата);
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВводаАдреса(ЭлементСписка)
	
	ТекстЗаголовка = "Адрес сервера сбора отчетности ТОГС";
	
	Если ПустаяСтрока(ЭлементСписка.Представление) Тогда
		Элементы.АдресСервераСбораОтчетностиРосстата.Заголовок = ТекстЗаголовка;
	Иначе
		Элементы.АдресСервераСбораОтчетностиРосстата.Заголовок =
			ТекстЗаголовка + " (" + ЭлементСписка.Представление + ")";
	КонецЕсли;
	
	Если ЭлементСписка.Пометка Тогда
		
		ПоказатьПредупреждение(,НСтр("ru='Этот адрес веб-сервиса может быть недоступен.
			|
			|Уточните действующий адрес в службе подержки ТОГС
			|или используйте адрес централизованного сервиса
			|(очистите строку адреса сервера сбора отчетности).'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	АдресСервераСбораОтчетностиРосстата = СокрЛП(АдресСервераСбораОтчетностиРосстата);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Константы.АдресСервераСбораОтчетностиРосстата.Установить(АдресСервераСбораОтчетностиРосстата);
	
КонецПроцедуры

&НаСервере
Процедура АдресаВебСервисовРосстата(СписокАдресовВебСервисов)
	
	Если ТипЗнч(СписокАдресовВебСервисов) = Тип("СписокЗначений") Тогда
		СписокАдресовВебСервисов.Очистить();
	Иначе
		СписокАдресовВебСервисов = Новый СписокЗначений;
	КонецЕсли;
	
	ИмяРегистра = СтрРазделить(ЭтотОбъект.ИмяФормы, ".")[1];
	МакетАдресаВебСервисов = РегистрыСведений[ИмяРегистра].ПолучитьМакет("АдресаВебСервисов");
	
	Для Каждого Область Из МакетАдресаВебСервисов.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			Для НомСтр = Область.Верх По Область.Низ Цикл
				
				Наименование = СокрП(МакетАдресаВебСервисов.Область(НомСтр, 1).Текст);
				Если ЗначениеЗаполнено(Наименование) И Наименование <> "###" Тогда
					АдресСервиса = СокрП(МакетАдресаВебСервисов.Область(НомСтр, 2).Текст);
					СервисНедоступен = ЗначениеЗаполнено(МакетАдресаВебСервисов.Область(НомСтр, 3).Текст);
					Если СтрНайти(АдресСервиса, "Централизованный сервис") > 0 Тогда
						АдресСервиса = "http://websbor.gks.ru/online";
					КонецЕсли;
					АдресСервиса = АдресСервиса + "{" + Наименование + "}";
					СписокАдресовВебСервисов.Добавить(АдресСервиса, Наименование, СервисНедоступен);
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти