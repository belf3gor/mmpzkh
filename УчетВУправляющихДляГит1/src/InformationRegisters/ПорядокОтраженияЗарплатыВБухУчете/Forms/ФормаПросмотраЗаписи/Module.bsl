#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	
	Если Параметры.Ключ.Пустой() Тогда
	
		ПодготовитьФормуНаСервере();
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
		ЭтотОбъект, Запись, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
		
	ВидимостьСчетаНУ = НЕ Запись.ЕНВД
		И Запись.Счет = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасходыНаОплатуТрудаБудущихПериодов");
	Элементы.ГруппаСчетИСубконтоНУ.Видимость = ВидимостьСчетаНУ;
	Если ВидимостьСчетаНУ Тогда
		БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
			ЭтотОбъект, Запись, ПараметрыУстановкиСвойствСубконтоНУ(ЭтотОбъект));
	КонецЕсли;
	
	УстановитьТекстНадписиСчетУчетаРасходовПоСтраховымВзносам(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстНадписиСчетУчетаРасходовПоСтраховымВзносам(Форма)
	
	ТекущаяЗапись = Форма.Запись;
	
	Если ТекущаяЗапись.ЕНВД Тогда
		
		Текст = "";
		
	ИначеЕсли ЗначениеЗаполнено(ТекущаяЗапись.Счет) Тогда
	
		Текст = "Счет учета расходов по страховым взносам ";
		
		Если ТекущаяЗапись.Счет = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасходыНаОплатуТрудаБудущихПериодов") Тогда
			
			Текст = Текст + "по БУ: " + ТекущаяЗапись.Счет + " по НУ: "
				+ ?(ЗначениеЗаполнено(ТекущаяЗапись.СчетНУ),
					ТекущаяЗапись.СчетНУ,
					"требуется указать!");
					
		Иначе
					
			Текст = Текст + "по БУ и НУ: " 
				+ ?(ЗначениеЗаполнено(ТекущаяЗапись.Счет),
					ТекущаяЗапись.Счет,
					"требуется указать!");
			
		КонецЕсли;
		
	Иначе
		
		Текст = "";
		
	КонецЕсли;
	
	Форма.НадписьСчетУчетаРасходовПоСтраховымВзносам = Текст;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто(Форма)

	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"Субконто", "", "Субконто", "", "Счет");
	
	Возврат Результат;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконтоНУ(Форма)

	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"СубконтоНУ", "", "СубконтоНУ", "", "СчетНУ");
	
	Возврат Результат;

КонецФункции

#КонецОбласти
