#Область ОписаниеПеременных

&НаКлиенте
Перем НастройкаРегламентаЭДО;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоНастройкиОбмена = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоНастройкиОбмена();
	ЕстьПравоОбработкиЭД     = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоОбработкиЭД();
	ЕстьПравоЧтенияЭД        = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоЧтенияЭД();
	
	ИспользуютсяДоговорыКонтрагентов = ОбменСКонтрагентамиПовтИсп.ИспользуютсяДоговорыКонтрагентов();
	
	ТолькоПросмотр = Не ЕстьПравоНастройкиОбмена;
	
	Элементы.ФормаОтправитьКаталогПоСоглашению.Видимость    = ЕстьПравоОбработкиЭД;
	Элементы.ФормаСформироватьСоглашениеПоШаблону.Видимость = ЕстьПравоОбработкиЭД;
	Элементы.ФормаОткрытьЭлектронныеДокументы.Видимость     = ЕстьПравоЧтенияЭД;
	Элементы.ДоговорКонтрагента.Видимость                   = ИспользуютсяДоговорыКонтрагентов;
	Элементы.ДекорацияСтатусПриглашения.Видимость           = ИспользуютсяДоговорыКонтрагентов;
	
	УстановитьУсловноеОформление();
	
	Элементы.ГруппаОжидание.Видимость = Ложь;
	Элементы.ГруппаОжиданиеУдаления.Видимость = Ложь;
	
	ПодготовитьФормуНаСервере(Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения = НСтр("ru = 'Настройки отправки не сохранены. Завершить работу с программой?'")
		Иначе
			Описание = Новый ОписаниеОповещения("ПередЗакрытиемОкнаФормы", ЭтотОбъект);
			ПоказатьВопрос(Описание, НСтр("ru = 'Настройки отправки изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ОбновитьТекущиеДелаЭДО"
		Или ИмяСобытия = "ОбновитьСостояниеПриглашений")
		И НастройкаРегламентаЭДО <> Истина Тогда
		
		ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	РеквизитыДляУдаления = Новый Массив;
	
	Если РасширенныйРежим Тогда
		РеквизитыДляУдаления.Добавить("ИдентификаторОтправителя");
		РеквизитыДляУдаления.Добавить("ИдентификаторПолучателя");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, РеквизитыДляУдаления);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораУчетныхЗаписей();
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораУчетныхЗаписей();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУПДПриИзменении(Элемент)
	УстановитьОтборПоУПД_УКД(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУКДПриИзменении(Элемент)
	УстановитьОтборПоУПД_УКД(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Модифицированность = Истина;
	ЗаполнитьИдентификаторыЭДО();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	Модифицированность = Истина;
	ЗаполнитьИдентификаторыЭДО();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СтатусПодключенияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ИдентификаторОтправителя)
		Или ПустаяСтрока(ИдентификаторПолучателя) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Необходимо записать изменения в настройке отправки.
                             |Продолжить?'");
		
		Описание = Новый ОписаниеОповещения("ПослеВопросаНажатиеСтатусПодключения", ЭтотОбъект);
		ПоказатьВопрос(Описание, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОткрытьПриглашение(ИдентификаторОтправителя, ИдентификаторПолучателя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсходящиеДокументы

&НаКлиенте
Процедура ИсходящиеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено 
		Или Не ЕстьПравоНастройкиОбмена Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле = Элементы.ИсходящиеДокументыДополнительныеНастройки Тогда
		ОткрытьФормуРедактированияРегламентаЭДО(ТекущиеДанные);
	ИначеЕсли Поле = Элементы.ИсходящиеДокументыСтатусПриглашения Тогда
		ОткрытьПриглашение(ТекущиеДанные.ИдентификаторОтправителя, ТекущиеДанные.ИдентификаторПолучателя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения("НажатиеЗаписать", ЭтотОбъект, Новый Структура);
	ВыполняемоеОповещение.ДополнительныеПараметры.Вставить("ОповещениеКоманды", ВыполняемоеОповещение);
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения("НажатиеЗаписатьИЗакрыть", ЭтотОбъект, Новый Структура);
	ВыполняемоеОповещение.ДополнительныеПараметры.Вставить("ОповещениеКоманды", ВыполняемоеОповещение);
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКаталогПоСоглашению(Команда)
	
	НастройкиЭДОСсылка = СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента);
	ДопПараметры = Новый Структура("НастройкаЭДО", НастройкиЭДОСсылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СформироватьПодписатьОтправитьКаталогЗавершить",
		ОбменСКонтрагентамиСлужебныйКлиент, ДопПараметры);
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ОткрытьФормуПодбораТоваров(УникальныйИдентификатор, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлектронныеДокументы(Команда)
	НастройкиЭДОСсылка = СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента);
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьДеревоЭД(НастройкиЭДОСсылка, Неопределено, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСоглашениеПоШаблону(Команда)
	
	НастройкиЭДОСсылка = СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента);
	ЗаполнитьСоглашениеОбОбменеЭД(НастройкиЭДОСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасширенныйРежим(Команда)
	
	Если РасширенныйРежим Тогда
		
		Описание = Новый ОписаниеОповещения("ИзменитьРасширенныйРежимПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Описание, НСтр("ru = 'Изменения расширенного режима будут очищены.
                                       |Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		
	Иначе
		РасширенныйРежим = Не РасширенныйРежим;
		ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	Если Модифицированность Тогда
		
		Описание = Новый ОписаниеОповещения("ПослеВопросаЗаписатьДокумент", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru = 'Для удаления настройки необходимо записать внесенные изменения.
                             |Записать данные?'");
		ПоказатьВопрос(Описание, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьВопросУдаленияНастройки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОформлениеФормы

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументы.ЦветГиперссылки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ИсходящиеДокументыСтатусПриглашения");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументы.ЦветГиперссылки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ИсходящиеДокументыСтатусПриглашения");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.УчетнаяЗаписьКонтрагента.Видимость            = Не Форма.РасширенныйРежим;
	Элементы.УчетнаяЗаписьОрганизации.Видимость            = Не Форма.РасширенныйРежим;
	Элементы.СтатусПриглашения.Видимость                   = Не Форма.РасширенныйРежим;
	Элементы.ФормаРасширенныйРежим.Пометка                 = Форма.РасширенныйРежим;
	Элементы.ИсходящиеДокументыСтатусПриглашения.Видимость = Форма.РасширенныйРежим;
	
	ДоступенВыборУчетныхЗаписей = ЗначениеЗаполнено(Форма.Организация) И ЗначениеЗаполнено(Форма.Контрагент);
	Элементы.УчетнаяЗаписьОрганизации.Доступность = ДоступенВыборУчетныхЗаписей;
	Элементы.УчетнаяЗаписьКонтрагента.Доступность = ДоступенВыборУчетныхЗаписей;
	
	Если ДоступенВыборУчетныхЗаписей Тогда
		ПодсказкаВводаУчетныхЗаписей = НСтр("ru = 'Выберите учетную запись'");
	Иначе 
		ПодсказкаВводаУчетныхЗаписей = "";
	КонецЕсли;
	Элементы.УчетнаяЗаписьОрганизации.ПодсказкаВвода = ПодсказкаВводаУчетныхЗаписей;
	Элементы.УчетнаяЗаписьКонтрагента.ПодсказкаВвода = ПодсказкаВводаУчетныхЗаписей;
	
	УстановитьОтборПоУПД_УКД(Форма);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоУПД_УКД(Форма)

	Элементы = Форма.Элементы;
	
	Для Каждого СтрокаТЧ Из Форма.ИсходящиеДокументы Цикл
		
		СтрокаТЧ.Отображать = Истина;
		
		Если Форма.ИспользоватьУПД
			И (СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.ТОРГ12Продавец")
				Или СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.СчетФактура")
				Или СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.АктИсполнитель")
				Или СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.АктНаПередачуПрав")) Тогда
				
			СтрокаТЧ.Отображать = Ложь;
		ИначеЕсли Не Форма.ИспользоватьУПД
			И СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.УПД") Тогда
			СтрокаТЧ.Отображать = Ложь;
		КонецЕсли;
		
		Если Форма.ИспользоватьУКД
			И (СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.КорректировочныйСчетФактура")
				Или СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель")) Тогда
				
			СтрокаТЧ.Отображать = Ложь;
		ИначеЕсли Не Форма.ИспользоватьУКД
			И СтрокаТЧ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.УКД") Тогда
			СтрокаТЧ.Отображать = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ИсходящиеДокументы.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("Отображать", Истина));
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьПредставлениеСтатусаПриглашения(СтатусПриглашений)
	
	
	Результат = Новый Структура("ТекстЗаголовка, ЦветСсылки");
	Результат.ТекстЗаголовка = Строка(СтатусПриглашений);
	Результат.ЦветСсылки = 0;
	
	Если СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ПустаяСсылка") Тогда
		Результат.ТекстЗаголовка = НСтр("ru = 'Отправить приглашение'");
	ИначеЕсли СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Отклонено")
			Или СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Ошибка") Тогда
			
			Результат.ЦветСсылки = 1;
			
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ИнициализацияФормы

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(Знач ПараметрыФормы)
	ПараметрыФормы.Свойство("Организация" , Организация);
	ПараметрыФормы.Свойство("Контрагент"  , Контрагент);
	ПараметрыФормы.Свойство("Договор"     , ДоговорКонтрагента);
	
	Если ПараметрыФормы.Свойство("Ключ") Тогда
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Если ТипЗнч(ПараметрыФормы.Ключ) = Тип("СправочникСсылка.НастройкиЭДО") Тогда
				Организация = ПараметрыФормы.Ключ.Организация;
			Иначе
				Организация = ПараметрыФормы.Ключ.Отправитель;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Контрагент) Тогда
			Если ТипЗнч(ПараметрыФормы.Ключ) = Тип("СправочникСсылка.НастройкиЭДО") Тогда
				Контрагент = ПараметрыФормы.Ключ.Контрагент;
			Иначе
				Контрагент = ПараметрыФормы.Ключ.Получатель;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			Если ТипЗнч(ПараметрыФормы.Ключ) = Тип("СправочникСсылка.НастройкиЭДО") Тогда
				ДоговорКонтрагента = ПараметрыФормы.Ключ.ДоговорКонтрагента;
			Иначе
				ДоговорКонтрагента = ПараметрыФормы.Ключ.Договор;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СоздатьПоДоговору = Неопределено;
	Создание          = Неопределено;
	Если ПараметрыФормы.Свойство("СоздатьПоДоговору"  , СоздатьПоДоговору) Тогда
		
		Элементы.ДоговорКонтрагента.ТолькоПросмотр = СоздатьПоДоговору = Ложь;
		Элементы.Организация.ТолькоПросмотр = ЗначениеЗаполнено(Организация);
		
	ИначеЕсли ПараметрыФормы.Свойство("Создание"  , Создание) И Создание Тогда
		
		Элементы.ДоговорКонтрагента.ТолькоПросмотр = Ложь;
		Элементы.Организация.ТолькоПросмотр        = Ложь;
		Элементы.Контрагент.ТолькоПросмотр         = Ложь;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере(ПараметрыФормы)
	
	ФорматыЭД.Загрузить(ОбменСКонтрагентамиПовтИсп.СведенияОФорматахЭлектронныхДокументов());
	
	ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам);
	
	ЗаполнитьРеквизитыФормы(ПараметрыФормы);
	
	ИспользуетсяНесколькоОрганизацийЭД = ЭлектронноеВзаимодействиеСлужебный.ИспользуетсяНесколькоОрганизаций();
	
	Если Не ИспользуетсяНесколькоОрганизацийЭД
		И НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЭлектронноеВзаимодействиеСлужебный.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ТаблицаНастроек = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНастройкиОтправкиДокументов();
	
	НастройкиОтправки = ОбменСКонтрагентамиСлужебный.ТекущиеНастройкиОтправкиЭлектронныхДокументовПоВидам(
		Организация, Контрагент, ДоговорКонтрагента, ТаблицаНастроек);
	
	ИдентификаторОтправителяПоУмолчанию = "";
	ИдентификаторПолучателяПоУмолчанию = "";
	СпособОбменаПоУмолчанию = Перечисления.СпособыОбменаЭД.ПустаяСсылка();
	Для каждого СтрокаНастроек Из НастройкиОтправки Цикл
		Если ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторОтправителя)
			И ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторПолучателя) Тогда
			ИдентификаторОтправителяПоУмолчанию = СтрокаНастроек.ИдентификаторОтправителя;
			ИдентификаторПолучателяПоУмолчанию = СтрокаНастроек.ИдентификаторПолучателя;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаНастроек.СпособОбменаЭД) Тогда
			СпособОбменаПоУмолчанию = СтрокаНастроек.СпособОбменаЭД;
		КонецЕсли;
		Если ЗначениеЗаполнено(ИдентификаторОтправителяПоУмолчанию)
			И ЗначениеЗаполнено(ИдентификаторПолучателяПоУмолчанию)
			И ЗначениеЗаполнено(СпособОбменаПоУмолчанию) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаНастроек Из НастройкиОтправки Цикл
		Если Не ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторОтправителя) Тогда
			СтрокаНастроек.ИдентификаторОтправителя = ИдентификаторОтправителяПоУмолчанию;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаНастроек.ИдентификаторПолучателя) Тогда
			СтрокаНастроек.ИдентификаторПолучателя = ИдентификаторПолучателяПоУмолчанию;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаНастроек.СпособОбменаЭД) Тогда
			СтрокаНастроек.СпособОбменаЭД = СпособОбменаПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
	
	ПолучитьУПД_УКД();
	ЗаполнитьПредставлениеНастроек(ЭтотОбъект, ТаблицаНастроек, НастройкиОтправки);
	ИсходящиеДокументы.Загрузить(ТаблицаНастроек);
	
	РасширенныйРежим = ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеНастроек(Форма, НастройкиПоУмолчанию, Знач ТекущиеНастройки)
	
	СтрокаСЗаполненнымиДанными = Неопределено;
	Для Каждого ТекСтрока Из НастройкиПоУмолчанию Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ВидДокумента"    , ТекСтрока.ВидДокумента);
		Отбор.Вставить("ПрикладнойВидЭД" , ТекСтрока.ПрикладнойВидЭД);
		
		СтрокиТаблицы = ТекущиеНастройки.НайтиСтроки(Отбор);
		
		Если СтрокиТаблицы.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(ТекСтрока, СтрокиТаблицы[0]);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрока.ИдентификаторОтправителя) И ЗначениеЗаполнено(ТекСтрока.ИдентификаторПолучателя) Тогда
			СтрокаСЗаполненнымиДанными = ТекСтрока;
		КонецЕсли;
		ЗаполнитьПредставлениеРегламентаЭДОВСтроке(Форма, ТекСтрока);
		
	КонецЦикла;
	
	Если СтрокаСЗаполненнымиДанными <> Неопределено Тогда
		Отбор = Новый Структура("ИдентификаторОтправителя, ИдентификаторПолучателя", "", "");
		СтрокиСНезаполненнымиИдентификаторами = НастройкиПоУмолчанию.НайтиСтроки(Отбор);
		Для каждого СтрокаТЗ Из СтрокиСНезаполненнымиИдентификаторами Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТЗ, СтрокаСЗаполненнымиДанными, "ИдентификаторОтправителя, ИдентификаторПолучателя, СпособОбменаЭД");
		КонецЦикла;
	КонецЕсли;
	
	НастройкиПоУмолчанию.Сортировать("Приоритет");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтатусПриглашения(Знач ИдентификаторОтправителя, Знач ИдентификаторПолучателя)
	
	СтатусПодключения = Перечисления.СтатусыПриглашений.ТребуетсяОтправить;
	
	ДанныеПриглашения = РегистрыСведений.ПриглашенияКОбменуЭлектроннымиДокументами.ДанныеПриглашения(
							ИдентификаторОтправителя, ИдентификаторПолучателя);
							
	Если ЗначениеЗаполнено(ДанныеПриглашения) Тогда
		СтатусПодключения = ДанныеПриглашения.Статус;
	КонецЕсли;
	
	Возврат СформироватьПредставлениеСтатусаПриглашения(СтатусПодключения);
	
КонецФункции

&НаСервере
Процедура ПолучитьУПД_УКД()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиОтправкиЭлектронныхДокументов.ИспользоватьУПД КАК ИспользоватьУПД,
		|	НастройкиОтправкиЭлектронныхДокументов.ИспользоватьУКД КАК ИспользоватьУКД
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументов КАК НастройкиОтправкиЭлектронныхДокументов
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументов.Отправитель = &Отправитель
		|	И НастройкиОтправкиЭлектронныхДокументов.Получатель = &Получатель
		|	И НастройкиОтправкиЭлектронныхДокументов.Договор = &Договор";
	
	Запрос.УстановитьПараметр("Договор", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Отправитель", Организация);
	Запрос.УстановитьПараметр("Получатель", Контрагент);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеУчетныхЗаписей

&НаКлиенте
Процедура ОткрытьФормуВыбораУчетныхЗаписей()
	
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ПриЗавершенииВыбораУченыхЗаписей", ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отправитель"             , Организация);
	ПараметрыОткрытия.Вставить("Получатель"              , Контрагент);
	ПараметрыОткрытия.Вставить("ДоговорКонтрагента"      , ДоговорКонтрагента);
	ПараметрыОткрытия.Вставить("ИдентификаторПолучателя" , ИдентификаторПолучателя);
	ПараметрыОткрытия.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	ПараметрыОткрытия.Вставить("ВыборУчетныхЗаписей"     , Истина);
	ПараметрыОткрытия.Вставить("НеСоздаватьНастройкиАвтоматически" , Истина);
	
	ОткрытьФорму("Обработка.ОбменСКонтрагентами.Форма.НастройкаОбменаСКонтрагентом", ПараметрыОткрытия,
		УникальныйИдентификатор,,,, Описание, режимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступныеИдентификаторы()
	
	Элементы.УчетнаяЗаписьОрганизации.СписокВыбора.Очистить();
	Элементы.УчетнаяЗаписьКонтрагента.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Контрагент) Тогда
		ДоступныеИдентификаторы = ОбменСКонтрагентамиСлужебный.ДоступныеИдентификаторыЭДО(Организация, Контрагент);
		
		Для Каждого Идентификатор Из ДоступныеИдентификаторы.ИдентификаторыОтправителя Цикл
			
			Элементы.УчетнаяЗаписьОрганизации.СписокВыбора.Добавить(Идентификатор.Значение, Идентификатор.Представление);
			
		КонецЦикла;
		
		Для Каждого Идентификатор Из ДоступныеИдентификаторы.ИдентификаторыПолучателя Цикл
			
			Элементы.УчетнаяЗаписьКонтрагента.СписокВыбора.Добавить(Идентификатор.Значение, Идентификатор.Представление);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииВыбораУченыхЗаписей(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеПриИзмененииИдентификаторов(Результат);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПриИзмененииИдентификаторов(Результат)
	
	ИдентификаторОтправителя = ВРег(Результат.ИдентификаторОрганизации);
	ИдентификаторПолучателя  = ВРег(Результат.ИдентификаторКонтрагента);
	СпособОбмена             = ПолучитьСпособОбменаУчетнойЗаписи(ИдентификаторОтправителя);
	
	Если Не РасширенныйРежим Тогда
		Таблица = ИсходящиеДокументы.Выгрузить();
		Таблица.ЗаполнитьЗначения(ИдентификаторОтправителя, "ИдентификаторОтправителя");
		Таблица.ЗаполнитьЗначения(ИдентификаторПолучателя , "ИдентификаторПолучателя");
		Таблица.ЗаполнитьЗначения(СпособОбмена,             "СпособОбменаЭД");
		ИсходящиеДокументы.Загрузить(Таблица);
	КонецЕсли;
	
	СтруктураСтатусПодключения = ПолучитьСтатусПриглашения(ИдентификаторОтправителя, ИдентификаторПолучателя);
	СтатусПриглашения = СтруктураСтатусПодключения.ТекстЗаголовка;
	
	Если СтруктураСтатусПодключения.ЦветСсылки = 0 Тогда
		Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиБЭД;
	ИначеЕсли СтруктураСтатусПодключения.ЦветСсылки = 1 Тогда
		Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеРегламентаЭДО

&НаКлиенте
Процедура ОткрытьФормуРедактированияРегламентаЭДО(ТекущиеДанные)
	
	НастройкаРегламентаЭДО = Истина;
	
	Описание = Новый ОписаниеОповещения("ПриЗавершенииРедактированияРегламентаЭДО", ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отправитель"             , Организация);
	ПараметрыОткрытия.Вставить("Получатель"              , Контрагент);
	ПараметрыОткрытия.Вставить("ДоговорКонтрагента"      , ДоговорКонтрагента);
	ПараметрыОткрытия.Вставить("ИдентификаторПолучателя" , ТекущиеДанные.ИдентификаторПолучателя);
	ПараметрыОткрытия.Вставить("ИдентификаторОтправителя", ТекущиеДанные.ИдентификаторОтправителя);
	ПараметрыОткрытия.Вставить("МаршрутПодписания"       , ТекущиеДанные.МаршрутПодписания);
	ПараметрыОткрытия.Вставить("ВидЭД"                   , ТекущиеДанные.ВидДокумента);
	ПараметрыОткрытия.Вставить("Формат"                  , ТекущиеДанные.ВерсияФормата);
	ПараметрыОткрытия.Вставить("ТребуетсяИзвещение"      , ТекущиеДанные.ТребуетсяИзвещениеОПолучении);
	ПараметрыОткрытия.Вставить("ТребуетсяПодтверждение"  , ТекущиеДанные.ТребуетсяОтветнаяПодпись);
	ПараметрыОткрытия.Вставить("ВыгружатьДопСведения"    , ТекущиеДанные.ВыгружатьДополнительныеСведения);
	ПараметрыОткрытия.Вставить("ЗаполнениеКодаТовара"    , ТекущиеДанные.ЗаполнениеКодаТовара);
	ПараметрыОткрытия.Вставить("ПрикладнойВидЭД"         , ТекущиеДанные.ПрикладнойВидЭД);
	ПараметрыОткрытия.Вставить("НастройкаРегламентаЭДО"  , Истина);
	ПараметрыОткрытия.Вставить("НеСоздаватьНастройкиАвтоматически" , Истина);
	ПараметрыОткрытия.Вставить("СкрыватьНастройкиТранспорта", Не РасширенныйРежим);
	ПараметрыОткрытия.Вставить("ВерсияФорматаУстановленаВручную"    , ТекущиеДанные.ВерсияФорматаУстановленаВручную);
	
	ОткрытьФорму("Обработка.ОбменСКонтрагентами.Форма.НастройкаОбменаСКонтрагентом", ПараметрыОткрытия,
		УникальныйИдентификатор,,,, Описание, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииРедактированияРегламентаЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	НастройкаРегламентаЭДО = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ВерсияФормата = Результат.НастройкиРегламента.ВерсияФормата;
	ТекущиеДанные.ИдентификаторОтправителя = Результат.НастройкиРегламента.ИдентификаторОрганизации;
	ТекущиеДанные.ИдентификаторПолучателя = Результат.НастройкиРегламента.ИдентификаторКонтрагента;
	ТекущиеДанные.МаршрутПодписания = Результат.НастройкиРегламента.МаршрутПодписания;
	ТекущиеДанные.ТребуетсяОтветнаяПодпись = Результат.НастройкиРегламента.ТребуетсяПодтверждение;
	ТекущиеДанные.ТребуетсяИзвещениеОПолучении = Результат.НастройкиРегламента.ТребуетсяИзвещение;
	ТекущиеДанные.ВыгружатьДополнительныеСведения = Результат.НастройкиРегламента.ВыгружатьДополнительныеСведения;
	ТекущиеДанные.ПрикладнойВидЭД = Результат.НастройкиРегламента.ПрикладнойВидЭД;
	ТекущиеДанные.ВидДокумента = Результат.НастройкиРегламента.ВидЭД;
	ТекущиеДанные.ЗаполнениеКодаТовара = Результат.НастройкиРегламента.ЗаполнениеКодаТовара;
	ТекущиеДанные.СпособОбменаЭД = ПолучитьСпособОбменаУчетнойЗаписи(ТекущиеДанные.ИдентификаторОтправителя);
	ТекущиеДанные.ВерсияФорматаУстановленаВручную = ТекущиеДанные.ВерсияФорматаУстановленаВручную;
	
	ЗаполнитьПредставлениеРегламентаЭДОВСтроке(ЭтотОбъект, ТекущиеДанные);
	
	СтруктураСтатусПодключения = ПолучитьСтатусПриглашения(ТекущиеДанные.ИдентификаторОтправителя, ТекущиеДанные.ИдентификаторПолучателя);
	ТекущиеДанные.СтатусПриглашенияПредставление = СтруктураСтатусПодключения.ТекстЗаголовка;
	ТекущиеДанные.ЦветГиперссылки = СтруктураСтатусПодключения.ЦветСсылки;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СформироватьСоглашениеПоШаблону

&НаКлиенте
Процедура ЗаполнитьСоглашениеОбОбменеЭД(ПараметрКоманды)
	
	Если НапечататьСогласиеНаОбработкуПерсональныхДанныхСубъекта(ПараметрКоманды) Тогда
		ОткрытьФорму("Документ.ЭлектронныйДокументИсходящий.Форма.ФормаПросмотраЭД", Новый Структура("ДокументОснование", ПараметрКоманды));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НапечататьСогласиеНаОбработкуПерсональныхДанныхСубъекта(НастройкаЭДО);

	АдресХранилища = Неопределено;
	
	ИмяМакета = "ПФ_DOC_СоглашениеОбОбменеЭлектроннымиДокументами";
	МакетИДанныеОбъекта = ПодготовитьДанныеПечатиСоглашенияПолучитьМакет(НастройкаЭДО, ИмяМакета);
	
	Области					= МакетИДанныеОбъекта.Макеты.ОписаниеОбластей;
	ДвоичныеДанныеМакетов	= МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	ДанныеОбъекта = МакетИДанныеОбъекта.Данные[НастройкаЭДО][ИмяМакета];
	
	Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(ДвоичныеДанныеМакетов[ИмяМакета], "");
	Если Макет <> Неопределено Тогда
		Попытка
			ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму("",, Макет);
			Если ПечатнаяФорма <> Неопределено Тогда
				// Вывод обычных областей с параметрами.
				Область = УправлениеПечатью.ОбластьМакета(Макет, Области[ИмяМакета]["Шапка"]);
				УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				АдресХранилища = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
			КонецЕсли;
		Исключение
			ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;
		
	УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
	
	Если АдресХранилища <> Неопределено Тогда
		ПоместитьДанныеСоглашенияВоВременноеХранилище(НастройкаЭДО, АдресХранилища);
		Результат = Истина;
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодготовитьДанныеПечатиСоглашенияПолучитьМакет(НастройкаЭДО, ИмяМакета)
	
	РеквизитыНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЭДО, "Организация, Контрагент");
	Сторона1 = ПредставлениеЮрФизЛица(РеквизитыНастройки.Организация);
	Сторона2 = ПредставлениеЮрФизЛица(РеквизитыНастройки.Контрагент);
	
	ДанныеОрганизации = Новый Структура;
	ЭлектронноеВзаимодействиеПереопределяемый.ЗаполнитьРегистрационныеДанныеОрганизации(
		РеквизитыНастройки.Организация, ДанныеОрганизации);
	
	Город = "";
	Если ДанныеОрганизации.Свойство("Город", Город) И НЕ ЗначениеЗаполнено(Город)
		И ДанныеОрганизации.Свойство("КодРегиона") И ЗначениеЗаполнено(ДанныеОрганизации.КодРегиона) Тогда
		
		Если ДанныеОрганизации.КодРегиона = "77" // Москва
			ИЛИ ДанныеОрганизации.КодРегиона = "78" // Санкт-Петербург
			ИЛИ ДанныеОрганизации.КодРегиона = "92" // Севастополь
			ИЛИ ДанныеОрганизации.КодРегиона = "99" Тогда // Байконур и иные территории
			
			ОбменСКонтрагентамиПереопределяемый.НазваниеРегиона(ДанныеОрганизации.КодРегиона, Город);
		КонецЕсли;
	КонецЕсли;
	
	Субъект = Новый Структура;
	Субъект.Вставить("НастройкаЭДО", НастройкаЭДО);
	Субъект.Вставить("Город",        Город);
	Субъект.Вставить("Дата",         Формат(ТекущаяДатаСеанса(), "ДЛФ=DD"));
	Субъект.Вставить("Сторона1",     Сторона1);
	Субъект.Вставить("Сторона2",     Сторона2);
	
	Субъекты = Новый Массив;
	Субъекты.Добавить(Субъект);
	
	ИмяМенеджераПечати = "Справочник.НастройкиЭДО";
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати(ИмяМенеджераПечати, ИмяМакета, Субъекты);
	
	Возврат МакетИДанныеОбъекта;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПоместитьДанныеСоглашенияВоВременноеХранилище(НастройкаЭДО, АдресХранилища)
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	СтруктураФайла  = Новый Структура;
	СтруктураФайла.Вставить("ИмяФайла",                           НСтр("ru = 'Соглашение об обмене электронными документами.docx'"));
	СтруктураФайла.Вставить("Наименование",                       НСтр("ru = 'Соглашение об обмене электронными документами'"));
	СтруктураФайла.Вставить("Расширение",                         "docx");
	СтруктураФайла.Вставить("Размер",                             ДвоичныеДанныеФайла.Размер());
	СтруктураФайла.Вставить("Редактирует",                        Неопределено);
	СтруктураФайла.Вставить("ПодписанЭП",                         Ложь);
	СтруктураФайла.Вставить("Зашифрован",                         Ложь);
	СтруктураФайла.Вставить("ФайлРедактируется",                  Ложь);
	СтруктураФайла.Вставить("СсылкаНаДвоичныеДанныеФайла",        АдресХранилища);
	СтруктураФайла.Вставить("ДатаМодификацииУниверсальная",       ТекущаяУниверсальнаяДата());
	СтруктураФайла.Вставить("ФайлРедактируетТекущийПользователь", Ложь);
	АдресХранилища = ПоместитьВоВременноеХранилище(СтруктураФайла, Новый УникальныйИдентификатор);
	ОбменСКонтрагентамиСлужебный.ПоместитьПараметрВПараметрыКлиентаНаСервере(НастройкаЭДО, АдресХранилища);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеЮрФизЛица(ЮрФизЛицо)
	
	Результат = "";
	
	Сведения = Новый Структура;
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, Сведения);
	
	Если Сведения.Свойство("ПолноеНаименование") И ЗначениеЗаполнено(Сведения.ПолноеНаименование) Тогда
		Результат = Сведения.ПолноеНаименование;
	Иначе
		Результат = Строка(ЮрФизЛицо);
	КонецЕсли;
	
	Если Сведения.Свойство("ИНН") И ЗначениеЗаполнено(Сведения.ИНН) Тогда
		Результат = Результат + " " + СтрШаблон(НСтр("ru = 'ИНН %1'"), Сведения.ИНН);
		Если Сведения.Свойство("КПП") И ЗначениеЗаполнено(Сведения.КПП) Тогда
			Результат = Результат + " " + СтрШаблон(НСтр("ru = 'КПП %1'"), Сведения.КПП);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПодборПодходящихИдентификаторовЭДО

&НаКлиенте
Процедура ИзменитьРасширенныйРежимПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РасширенныйРежим Тогда
		Возврат
	КонецЕсли;
	
	ЗаполнитьИдентификаторыЭДО();

	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИдентификаторыЭДО()
	
	ОбновитьДоступныеИдентификаторы();
	
	ИдентификаторыОтправителя = Элементы.УчетнаяЗаписьОрганизации.СписокВыбора.ВыгрузитьЗначения();
	ИдентификаторыПолучателя  = Элементы.УчетнаяЗаписьКонтрагента.СписокВыбора.ВыгрузитьЗначения();
	
	Если ЗначениеЗаполнено(ИдентификаторыОтправителя) И ЗначениеЗаполнено(ИдентификаторыПолучателя) Тогда
		Элементы.ДекорацияНадпись.Заголовок = НСтр("ru = 'Ожидается получение данных по совместимости идентификаторов ЭДО'");
		Элементы.ГруппаОжидание.Видимость = Истина;
		
		Элементы.ФормаРасширенныйРежим.Доступность = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ПодобратьПодходящиеИдентификаторыЭДОЗавершение", ЭтотОбъект);
		
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("ИдентификаторыОтправителя", ИдентификаторыОтправителя);
		ПараметрыПроцедуры.Вставить("ИдентификаторыПолучателя",  ИдентификаторыПолучателя);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительнаяОперация = ПодобратьПодходящиеИдентификаторыЭДО(ПараметрыПроцедуры, УникальныйИдентификатор);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодобратьПодходящиеИдентификаторыЭДО(Знач ПараметрыПроцедуры,Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подбор подходящих идентификаторов ЭДО'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ПолучитьЛучшуюПоРейтингуСвязкуИдентификаторовЭДО", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПодобратьПодходящиеИдентификаторыЭДОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		УстановитьИдентификаторыЭДО(Результат.АдресРезультата);
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Во время выбора подходящих учетных записей произошла ошибка.
                                                                |Подробнее см. в журнале регистрации.'"));
		
	КонецЕсли;
	
	Элементы.ГруппаОжидание.Видимость = Ложь;
	Элементы.ФормаРасширенныйРежим.Доступность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИдентификаторыЭДО(АдресРезультата)
	
	Идентификаторы = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если Не ЗначениеЗаполнено(Идентификаторы) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не найден идентификатор Абонента ЭДО'"));
		Возврат;
	КонецЕсли;
	
	РасширенныйРежим = Ложь;
	ИдентификаторОтправителя = Врег(Идентификаторы.ИдентификаторОтправителя);
	ИдентификаторПолучателя = ВРег(Идентификаторы.ИдентификаторПолучателя);
	СпособОбменаЭД = ПолучитьСпособОбменаУчетнойЗаписи(ИдентификаторОтправителя);
	
	Таблица = ИсходящиеДокументы.Выгрузить();
	Таблица.ЗаполнитьЗначения(ИдентификаторОтправителя, "ИдентификаторОтправителя");
	Таблица.ЗаполнитьЗначения(ИдентификаторПолучателя , "ИдентификаторПолучателя");
	Таблица.ЗаполнитьЗначения(СпособОбменаЭД          , "СпособОбменаЭД");
	ИсходящиеДокументы.Загрузить(Таблица);
	
	ОбновитьДоступныеИдентификаторы();
	
	ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура НажатиеЗаписать(ПропуститьПроверки, ДополнительныеПараметры) Экспорт

	Модифицированность = НЕ НачатьСохраненияНастроек(ПропуститьПроверки, ДополнительныеПараметры.ОповещениеКоманды);

КонецПроцедуры

&НаКлиенте
Процедура НажатиеЗаписатьИЗакрыть(ПропуститьПроверки, ДополнительныеПараметры) Экспорт

	Если НачатьСохраненияНастроек(ПропуститьПроверки, ДополнительныеПараметры.ОповещениеКоманды) Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаНажатиеСтатусПодключения(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Записать(Неопределено);
	Иначе
		Возврат;
	КонецЕсли;
	
	ОткрытьПриглашение(ИдентификаторОтправителя, ИдентификаторПолучателя);
	
КонецПроцедуры

&НаКлиенте
Функция НачатьСохраненияНастроек(ПропуститьПроверки = Ложь, ОповещениеКоманды = Неопределено)
	
	Если Не ПропуститьПроверки Тогда
		// Выполняем серверный вызов, так как нужно проверить валидность маршрута и в случае ошибок спросить у пользователя,
		// записывать ли его.
		ЕстьОшибкиЗаполнения = Ложь;
		Если Не НастройкаВалидирована(ЕстьОшибкиЗаполнения) Тогда
			Если Не ЕстьОшибкиЗаполнения Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОЗаписиПолученОтвет", ЭтотОбъект,
					Новый Структура("ОповещениеКоманды", ОповещениеКоманды));
				ТекстВопроса = НСтр("ru = 'Обнаружены возможные ошибки в настройках маршрутов. Продолжить запись?'");
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, 
					НСтр("ru = 'Настройка некорректна'"));
			КонецЕсли;
				
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Результат = ЗаписатьНастройки(Организация, Контрагент, ДоговорКонтрагента, ИсходящиеДокументы, ИспользоватьУКД, ИспользоватьУПД);
	
	Если Результат Тогда
		Модифицированность = Ложь;
		Оповестить("ОбновитьТекущиеДелаЭДО", Контрагент, Организация);
		ТекстЗаголовка = НСтр("ru = 'Успех'");
		ТекстСообщения = НСтр("ru = 'Настройки отправки сохранены успешно'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ВопросОЗаписиПолученОтвет(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеКоманды, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция НастройкаВалидирована(ЕстьОшибкиЗаполнения = Ложь)
	Если НЕ ПроверитьЗаполнение() Тогда
		ЕстьОшибкиЗаполнения = Истина;	
	КонецЕсли;
	
	ЕстьОшибкиВЗависимыхНастройках = Ложь;
	Если Не ЕстьОшибкиЗаполнения Тогда
		// Проверим, что все указанные маршруты возможно выполнить
		ПроверитьСоответствиеСертификатовМаршрутам(ЕстьОшибкиВЗависимыхНастройках);
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибкиЗаполнения И Не ЕстьОшибкиВЗависимыхНастройках;

КонецФункции

&НаСервере
Процедура ПроверитьСоответствиеСертификатовМаршрутам(Отказ)
	
	// Подготовим соответствие наборов сертификатов учетным записям
	ИспользованныеУчетныеЗаписи = ИсходящиеДокументы.Выгрузить(, "ИдентификаторОтправителя").ВыгрузитьКолонку("ИдентификаторОтправителя");
	
	НаборыСертификатовУчетныхЗаписей = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетныеЗаписи", ИспользованныеУчетныеЗаписи);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК УчетнаяЗапись,
	|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|ГДЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО В(&УчетныеЗаписи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяЗапись,
	|	Сертификат
	|ИТОГИ ПО
	|	УчетнаяЗапись";
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		СертификатыУчетныхЗаписей = Новый Массив;
		ИдентификаторНабора = "ИД_";
		Выборка = ВыборкаУчетныхЗаписей.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если СертификатыУчетныхЗаписей.Найти(Выборка.Сертификат) = Неопределено Тогда
				СертификатыУчетныхЗаписей.Добавить(Выборка.Сертификат);
				ИдентификаторНабора = ИдентификаторНабора + Строка(Выборка.Сертификат.УникальныйИдентификатор());
			КонецЕсли; 
		КонецЦикла;
		
		ИдентификаторНабора = СтрЗаменить(ИдентификаторНабора, "-", "_");
		СтруктураОписанияНабораСертификатов = Новый Структура("ИдентификаторНабора, Сертификаты", 
			ИдентификаторНабора, СертификатыУчетныхЗаписей);
		НаборыСертификатовУчетныхЗаписей.Вставить(ВыборкаУчетныхЗаписей.УчетнаяЗапись, СтруктураОписанияНабораСертификатов);
	КонецЦикла;
	
	// Подготовим уникальные комбинации значений наборов сертификатов, маршрутов и видов ЭД.
	УникальныеПараметрыПроверки = Новый Структура;
	Для Каждого СтрокаИсходящегоДокумента Из ИсходящиеДокументы Цикл
		Если СтрокаИсходящегоДокумента.Формировать Тогда
			МаршрутПодписания 	     = СтрокаИсходящегоДокумента.МаршрутПодписания;
			ВидЭД				     = СтрокаИсходящегоДокумента.ВидДокумента;
			УчетнаяЗаписьНастроекЭДО = СтрокаИсходящегоДокумента.ИдентификаторОтправителя;
			
			ПараметрыНабораСертификатов = НаборыСертификатовУчетныхЗаписей[ИдентификаторОтправителя];
			Если ПараметрыНабораСертификатов <> Неопределено Тогда
				ИдентификаторМаршрута   = СтрЗаменить(Строка(МаршрутПодписания.УникальныйИдентификатор()), "-", "_");
				ИдентификаторПараметров = ПараметрыНабораСертификатов.ИдентификаторНабора + Строка(ИдентификаторМаршрута);
				
				СтруктураОписанияПараметров = Неопределено;
				Если Не УникальныеПараметрыПроверки.Свойство(ИдентификаторПараметров, СтруктураОписанияПараметров) Тогда
					СтруктураОписанияПараметров = Новый Структура;
					СтруктураОписанияПараметров.Вставить("Сертификаты",       ПараметрыНабораСертификатов.Сертификаты);
					СтруктураОписанияПараметров.Вставить("МаршрутПодписания", МаршрутПодписания);
					СтруктураОписанияПараметров.Вставить("ВидыЭД",            Новый Массив);
					УникальныеПараметрыПроверки.Вставить(ИдентификаторПараметров, СтруктураОписанияПараметров);
				КонецЕсли;
				СтруктураОписанияПараметров.ВидыЭД.Добавить(ВидЭД);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеНастройки = Новый Структура("Отправитель, Получатель, Договор");
	ОписаниеНастройки.Отправитель = Организация;
	ОписаниеНастройки.Получатель  = Контрагент;
	ОписаниеНастройки.Договор     = ДоговорКонтрагента;
	НастройкаОбмена = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьКлючЗаписи(ОписаниеНастройки);
	ПредставлениеНастройки = ОбменСКонтрагентамиСлужебный.ПредставлениеНастройкиОтправкиЭлектронныхДокументов(ОписаниеНастройки);
	
	// Проверим валидность каждого набора параметров проверки
	Для Каждого НаборПараметровПроверки Из УникальныеПараметрыПроверки Цикл
		МаршрутПодписания = НаборПараметровПроверки.Значение.МаршрутПодписания;
		Сертификаты       = НаборПараметровПроверки.Значение.Сертификаты;
		ВидыЭД            = НаборПараметровПроверки.Значение.ВидыЭД;
		
		РезультатыПроверки = ЭлектронноеВзаимодействиеСлужебный.РезультатыПроверкиМаршрутаПоПараметрамНастройки(
			МаршрутПодписания, Сертификаты, ВидыЭД);
			
		ЭлектронноеВзаимодействиеСлужебный.ВывестиРезультатыПроверкиМаршрута(РезультатыПроверки, 
			НастройкаОбмена, МаршрутПодписания, Отказ,, ПредставлениеНастройки);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьНастройки(Знач Организация, Знач Контрагент, Знач ДоговорКонтрагента, Знач ИсходящиеДокументы, Знач ИспользоватьУКД, Знач ИспользоватьУПД)
	
	ИсходящиеДокументы = ИсходящиеДокументы.Выгрузить();
	
	ИсходящиеДокументы.Колонки.Добавить("Отправитель");
	ИсходящиеДокументы.ЗаполнитьЗначения(Организация, "Отправитель");
	
	ИсходящиеДокументы.Колонки.Добавить("Получатель");
	ИсходящиеДокументы.ЗаполнитьЗначения(Контрагент, "Получатель");
	
	ИсходящиеДокументы.Колонки.Добавить("Договор");
	ИсходящиеДокументы.ЗаполнитьЗначения(ДоговорКонтрагента, "Договор");
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.УстановитьЗначение("Договор"    , ДоговорКонтрагента);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументов");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.УстановитьЗначение("Договор"    , ДоговорКонтрагента);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отправитель.Установить(Организация);
		НаборЗаписей.Отбор.Получатель.Установить(Контрагент);
		НаборЗаписей.Отбор.Договор.Установить(ДоговорКонтрагента);
		НаборЗаписей.Загрузить(ИсходящиеДокументы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьМенеджерЗаписи();
		НаборЗаписей.Отправитель = Организация;
		НаборЗаписей.Получатель  = Контрагент;
		НаборЗаписей.Договор = ДоговорКонтрагента;
		НаборЗаписей.Прочитать();
		НаборЗаписей.ИспользоватьУПД = ИспользоватьУПД;
		НаборЗаписей.ИспользоватьУКД = ИспользоватьУКД;
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ВидОперации = НСтр("ru = 'Сохранение настроек отправки ЭДО'");
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
			НСтр("ru = 'Не удалось сохранить Настройки отправки
                  |Подробнее см. в журнале регистрации.'"));
			
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция СсылкаНаОбъектНастройкиЭДО(Знач Организация, Знач Контрагент, Знач ДоговорКонтрагента)
	Возврат ОбменСКонтрагентамиСлужебный.СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента);
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемОкнаФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть(Элементы.ФормаЗаписатьИЗакрыть);
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСпособОбменаУчетнойЗаписи(Знач ИдентификаторОтправителя)
	
	Данные = РегистрыСведений.УчетныеЗаписиЭДО.ДанныеУчетнойЗаписиЭДОПоИдентификатору(ИдентификаторОтправителя);
	
	Если ЗначениеЗаполнено(Данные) Тогда
		Возврат Данные.СпособОбменаЭД;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений()

	Таблица = ИсходящиеДокументы.Выгрузить(, "ИдентификаторОтправителя, ИдентификаторПолучателя");
	Таблица.Свернуть("ИдентификаторОтправителя, ИдентификаторПолучателя");
	
	ИдентификаторОтправителя = ВРег(Таблица[0].ИдентификаторОтправителя);
	ИдентификаторПолучателя  = ВРег(Таблица[0].ИдентификаторПолучателя);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаИдентификаторы.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	ТаблицаИдентификаторы.ИдентификаторПолучателя КАК ИдентификаторПолучателя
		|ПОМЕСТИТЬ ТаблицаИдентификаторы
		|ИЗ
		|	&Идентификаторы КАК ТаблицаИдентификаторы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИдентификаторы.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	ТаблицаИдентификаторы.ИдентификаторПолучателя КАК ИдентификаторПолучателя,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус КАК СтатусПриглашения
		|ИЗ
		|	ТаблицаИдентификаторы КАК ТаблицаИдентификаторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
		|		ПО ТаблицаИдентификаторы.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
		|			И ТаблицаИдентификаторы.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента";
	
	Запрос.УстановитьПараметр("Идентификаторы", Таблица);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТаблицаИсходящиеДокументы = ИсходящиеДокументы.Выгрузить();
	ТаблицаИсходящиеДокументы.ЗаполнитьЗначения(Перечисления.СтатусыПриглашений.ПустаяСсылка(), "СтатусПриглашения");
	ТаблицаИсходящиеДокументы.Индексы.Добавить("ИдентификаторОтправителя, ИдентификаторПолучателя");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("ИдентификаторОтправителя", ВыборкаДетальныеЗаписи.ИдентификаторОтправителя);
		Отбор.Вставить("ИдентификаторПолучателя" , ВыборкаДетальныеЗаписи.ИдентификаторПолучателя);
		
		НайденныеСтроки = ТаблицаИсходящиеДокументы.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
			
			СтрокаТЧ.СтатусПриглашения = ВыборкаДетальныеЗаписи.СтатусПриглашения;
			
			СтруктураСтатусПодключения = СформироватьПредставлениеСтатусаПриглашения(СтрокаТЧ.СтатусПриглашения);
			
			СтрокаТЧ.СтатусПриглашенияПредставление = СтруктураСтатусПодключения.ТекстЗаголовка;
			СтрокаТЧ.ЦветГиперссылки = СтруктураСтатусПодключения.ЦветСсылки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИсходящиеДокументы.Загрузить(ТаблицаИсходящиеДокументы);
	
	Если Таблица.Количество() = 1 Тогда
		
		ОбновитьДоступныеИдентификаторы();
		
		СтатусПриглашения = ТаблицаИсходящиеДокументы[0].СтатусПриглашенияПредставление;
		
		Если ИсходящиеДокументы[0].ЦветГиперссылки = 0 Тогда
			Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиБЭД;
		ИначеЕсли ИсходящиеДокументы[0].ЦветГиперссылки = 1 Тогда
			Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
		КонецЕсли;
		
	КонецЕсли;

	Возврат Таблица.Количество() > 1;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеРегламентаЭДОВСтроке(Форма, СтрокаТаблицы)
	
	Строки = Форма.ФорматыЭД.НайтиСтроки(Новый Структура("ИдентификаторФормата", СтрокаТаблицы.ВерсияФормата));
	
	МассивСтрок = Новый Массив;
	Если Строки.Количество() > 0 Тогда
		МассивСтрок.Добавить(Строки[0].ПредставлениеФормата);
	Иначе
		МассивСтрок.Добавить(НСтр("ru = 'Произвольный документ'"));
	КонецЕсли;
	
	МассивСтрок.Добавить(?(СтрокаТаблицы.ТребуетсяОтветнаяПодпись, НСтр("ru = 'с ответной подписью'"), НСтр("ru = 'без ответной подписи'")));
	
	СтрокаТаблицы.ДополнительныеНастройки = СтрСоединить(МассивСтрок,", ");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПриглашение(ВыбранныйИдентификаторОрганизации, ВыбранныйИдентификаторКонтрагента)
	
	Если Не ЗначениеЗаполнено(Организация)
		Или Не ЗначениеЗаполнено(Контрагент) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация"              , Организация);
	ПараметрыОткрытия.Вставить("Получатель"               , ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контрагент));
	ПараметрыОткрытия.Вставить("ИдентификаторОрганизации" , ВыбранныйИдентификаторОрганизации);
	ПараметрыОткрытия.Вставить("ИдентификаторКонтрагента" , ВыбранныйИдентификаторКонтрагента);
	
	ОткрытьФорму("РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами.Форма.ФормаОтправкиПриглашений",
		ПараметрыОткрытия, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеНастройкиОбмена

&НаКлиенте
Процедура ПослеВопросаЗаписатьДокумент(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения("ЗаписатьСУдалением", ЭтотОбъект, Новый Структура);
	ВыполняемоеОповещение.ДополнительныеПараметры.Вставить("ОповещениеКоманды", ВыполняемоеОповещение);
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСУдалением(ПропуститьПроверки, ДополнительныеПараметры) Экспорт

	Если НачатьСохраненияНастроек(ПропуститьПроверки, ДополнительныеПараметры.ОповещениеКоманды) Тогда
		Модифицированность = Ложь;
		ПоказатьВопросУдаленияНастройки();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросУдаленияНастройки()
	
	Отказ = Ложь;
	ТекстСообщения = НСтр("ru = 'Для удаления настройки необходимо вначале разорвать приглашение.'");
	ОчиститьСообщения();
	
	Для Каждого СтрокаКоллекции Из ИсходящиеДокументы Цикл
		
		Если СтрокаКоллекции.СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Принято") Тогда
			
			РеквизитФормы = ?(РасширенныйРежим,
				"ИсходящиеДокументы[" + ИсходящиеДокументы.Индекс(СтрокаКоллекции) + "].СтатусПриглашенияПредставление", "СтатусПриглашения");
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , РеквизитФормы, , Отказ);
			
			Если Не РасширенныйРежим Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ПослеВопросаУдаленияНастройки", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru = 'Сейчас будут удалена настройка обмена с контрагентом? Отправка электронных документов контрагенту будет не возможна.
                         |Продолжить?'");
	
	ПоказатьВопрос(Описание, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаУдаленияНастройки(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Организация"       , Организация);
	ПараметрыПроцедуры.Вставить("Контрагент"        , Контрагент);
	ПараметрыПроцедуры.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
	
	ДлительнаяОперация = НачатьУдалениеНастроекОтправкиНаСервере(УникальныйИдентификатор, ПараметрыПроцедуры);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ТолькоПросмотр = Истина;
		Элементы.ГруппаОжиданиеУдаления.Видимость = Истина;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		Описание = Новый ОписаниеОповещения("ПриЗавершенииУдаленияНастроекОтправки", ЭтаФорма);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Описание, ПараметрыОжидания);
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьУдалениеНастроекОтправкиНаСервере(Знач УникальныйИдентификатор, Знач ПараметрыПроцедуры)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Удаление настройки отправки электронных документов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.УдалитьНастройкиОтправкиЭДО",
		ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииУдаленияНастроекОтправки(Результат, ДополнительныеПараметры) Экспорт
	
	ТолькоПросмотр = Ложь;
	Элементы.ГруппаОжиданиеУдаления.Видимость = Ложь;

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		
		ТекстСообщения = НСтр("ru = 'Во время удаления настройки отправки произошла ошибка.'");
		
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(НСтр("ru = 'Удаление настройки отправки ЭДО'"),
			Результат.ПодробноеПредставлениеОшибки, ТекстСообщения);
			
		Возврат;
	КонецЕсли;
	
	Если ПолучитьИзВременногоХранилища(Результат.АдресРезультата) = Истина Тогда
		
		Оповестить("ОбновитьТекущиеДелаЭДО");
		Закрыть();
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Во время удаления настройки отправки произошла ошибка.
                               |Подробнее см. в журнале регистрации.'");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
