
#Область СлужебныеПроцедурыИФункции

/////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
// Открывает форму настроек доступа.
//
Процедура ОткрытьФормуНастроекДоступа(Пользователь = Неопределено)
	
	ОткрытьФорму("РегистрСведений.УПЖКХ_НастройкиДоступаПользователейКОбъектамУчета.Форма.ФормаНастроекПользователя", Новый Структура("Пользователь", Пользователь));
	
КонецПроцедуры

&НаСервере
// Удаляет настройки доступа пользователя на сервере.
//
Процедура УдалитьНастройкиДоступаНаСервере(Пользователь)
	
	НаборЗаписей = РегистрыСведений.УПЖКХ_НастройкиДоступаПользователейКОбъектамУчета.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
// Обработчик завершения удаления настроек.
//
Процедура ОбработатьЗавершенияУдаленияНастроек(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да И ДопПараметры.Свойство("Пользователь") Тогда
		УдалитьНастройкиДоступаНаСервере(ДопПараметры.Пользователь);
		Оповестить("ОбновитьСписокНастроекДоступаКОбъектамУчета");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСписокНастроекДоступаКОбъектамУчета" Тогда
		Элементы.СписокПользователей.Обновить();
		Элементы.СписокОбъектов.Обновить();
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Обработчик события "Выбор" поля "СписокПользователей".
//
Процедура СписокПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		ОткрытьФормуНастроекДоступа(ТекущиеДанные.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриАктивизацииСтроки" поля "СписокПользователей".
//
Процедура СписокПользователейПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	
	Если Не ТекущиеДанные = Неопределено Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(СписокОбъектов.Отбор, "Пользователь", ТекущиеДанные.Пользователь, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе
		УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(СписокОбъектов.Отбор, "Пользователь", Неопределено, ВидСравненияКомпоновкиДанных.Равно, , Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПередНачаломДобавления" поля "СписокПользователей".
//
Процедура СписокПользователейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ОткрытьФормуНастроекДоступа();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Выбор" поля "СписокОбъектов".
//
Процедура СписокОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		ОткрытьФормуНастроекДоступа(ТекущиеДанные.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
// Обработчик команды "КомандаСоздать".
//
Процедура КомандаСоздать(Команда)
	
	ОткрытьФормуНастроекДоступа();
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "КомандаИзменить".
//
Процедура КомандаИзменить(Команда)
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		ОткрытьФормуНастроекДоступа(ТекущиеДанные.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "КомандаУдалить".
//
Процедура КомандаУдалить(Команда)
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьЗавершенияУдаленияНастроек", ЭтотОбъект, Новый Структура("Пользователь", ТекущиеДанные.Пользователь));
		ПоказатьВопрос(Оповещение, "Удалить настройки доступа пользователя?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
