#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляем в набор записей по правилу новое платежное поручение, если добавление не нужно, возвращаем ЛОЖЬ
Функция ДобавитьЗаписьПравила(Организация, ПлатежноеПоручение, ПравилоНалоговогоПлатежа, ПериодСобытия, ОплатаЗадолженности = Ложь) Экспорт
	
	ДанныеЗаполнения = НовыйСтруктураРегистра();
	СтруктураЗаписи = НовыйСтруктураРегистра();
	
	НаборЗаписей = РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПлатежноеПоручение.Установить(ПлатежноеПоручение);
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
	
		ЗаписьПравила = НаборЗаписей[0];
		
	Иначе
		
		ЗаписьПравила = НаборЗаписей.Добавить();
	
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		СтруктураЗаписи,
		ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(ЗаписьПравила, Метаданные.РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи));
	
	ДанныеЗаполнения.Вставить("Организация",         Организация);
	ДанныеЗаполнения.Вставить("ПлатежноеПоручение",  ПлатежноеПоручение);
	ДанныеЗаполнения.Вставить("Правило",             ПравилоНалоговогоПлатежа);
	ДанныеЗаполнения.Вставить("ПериодСобытия",       ПериодСобытия);
	ДанныеЗаполнения.Вставить("ОплатаЗадолженности", ОплатаЗадолженности);
	
	ЗаписьИзменена = Ложь;
	
	Если НЕ ОбщегоНазначения.ДанныеСовпадают(СтруктураЗаписи, ДанныеЗаполнения) Тогда
		
		ЗаполнитьЗначенияСвойств(ЗаписьПравила, ДанныеЗаполнения);
		
		НаборЗаписей.Записать();
		
		ЗаписьИзменена = Истина;
	
	КонецЕсли;
	
	Возврат ЗаписьИзменена;
	
КонецФункции

// Удаляет из регистра запись по об исполнении задачи указанным документом.
//
// Параметры:
//  Организация         - СправочникСсылка.Организации - организация
//  ПлатежноеПоручение  - ДокументСсылка - документ, по которому удаляется запись
//                       (допустимые типы - см. тип значения измерения ПлатежноеПоручения)
//
Процедура УдалитьЗапись(Организация, ПлатежноеПоручение) Экспорт
	
	ВнешняяТранзакция = ТранзакцияАктивна();
	
	Если НЕ ВнешняяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	// Установим управляемые блокировки
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи");
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	ЭлементБлокировки.УстановитьЗначение("ПлатежноеПоручение", ПлатежноеПоручение);
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.ПлатежноеПоручение.Установить(ПлатежноеПоручение);
	
	НаборЗаписей.Записать();
	
	Если НЕ ВнешняяТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция НовыйСтруктураРегистра()

	Возврат Новый Структура("Организация, ПлатежноеПоручение, Правило, ПериодСобытия, ОплатаЗадолженности");

КонецФункции 

#КонецОбласти

#КонецЕсли