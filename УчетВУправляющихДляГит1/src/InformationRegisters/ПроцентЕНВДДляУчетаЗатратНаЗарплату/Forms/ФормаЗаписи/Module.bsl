
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияКлючаЗаписи = Неопределено;
	Если Параметры.Свойство("ЗначенияКлючаЗаписи", ЗначенияКлючаЗаписи) Тогда
		
		КлючСохраненияПоложенияОкна = "НачислениеЗарплаты_ТекущееЗначение";
		
		Параметры.Ключ = РегистрыСведений.ПроцентЕНВДДляУчетаЗатратНаЗарплату.СоздатьКлючЗаписи(ЗначенияКлючаЗаписи);
		
		МенеджерЗаписи = РегистрыСведений.ПроцентЕНВДДляУчетаЗатратНаЗарплату.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Параметры.Ключ);
		МенеджерЗаписи.Прочитать();
		
		ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
		
		Элементы.ГруппаОсновнаяКоманднаяПанель.Видимость = Ложь;
		Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию      = Истина;
		Элементы.Организация.Видимость = Ложь;
		Элементы.Месяц.Вид             = ВидПоляФормы.ПолеНадписи;
		
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Запись.Месяц", "МесяцСтрокой");
		
	Иначе
		Элементы.ГруппаВспомогательнаяКоманднаяПанель.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Ключ = РегистрыСведений.ПроцентЕНВДДляУчетаЗатратНаЗарплату.ПустойКлюч() Тогда
		
		// создается новая запись
		// параметры структуры
		// Месяц - заполнить значением начала месяца текущей даты сеанса
		// Организация - значение по умолчанию
		ЗначенияДляЗаполнения = Новый Структура("Месяц, Организация", 
		"Запись.Месяц",
		"Запись.Организация");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Запись.Месяц", "МесяцСтрокой");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Запись.Месяц", "МесяцСтрокой");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеречитатьДанныеУчета(Команда)
	
	ПеречитатьДанныеУчетаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура МесяцНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Запись.Месяц", "МесяцСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Запись.Месяц", "МесяцСтрокой", Направление, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Запись.Месяц", "МесяцСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПеречитатьДанныеУчетаНаСервере()
	
	ДоляЕНВД = ОтражениеЗарплатыВБухучетеПереопределяемый.ПроцентЕНВД(Запись.Организация, Запись.Месяц);
	Если НЕ ЗначениеЗаполнено(ДоляЕНВД) Тогда
		ДоляЕНВД = 0;
	КонецЕсли;
	
	Запись.Процент = ДоляЕНВД;
	
КонецПроцедуры

#КонецОбласти