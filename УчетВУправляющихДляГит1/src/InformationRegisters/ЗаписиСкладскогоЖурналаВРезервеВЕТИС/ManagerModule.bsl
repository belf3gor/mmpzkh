#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	
	ПолноеИмяРегистра = "РегистрСведений.ЗаписиСкладскогоЖурналаВРезервеВЕТИС";
	
	// Исходящая транспортная операция ВЕТИС
	ЗапросРегистраторов = Новый Запрос;
	ЗапросРегистраторов.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаписиСкладскогоЖурналаВРезервеВЕТИС.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втРегистраторы
	|ИЗ
	|	РегистрСведений.ЗаписиСкладскогоЖурналаВРезервеВЕТИС КАК ЗаписиСкладскогоЖурналаВРезервеВЕТИС
	|ГДЕ
	|	ЗаписиСкладскогоЖурналаВРезервеВЕТИС.Регистратор ССЫЛКА Документ.ИсходящаяТранспортнаяОперацияВЕТИС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРегистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС.Товары КАК ИсходящаяТранспортнаяОперацияВЕТИСТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	|		ПО ИсходящаяТранспортнаяОперацияВЕТИСТовары.Ссылка = втРегистраторы.Регистратор
	|ГДЕ
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.ВетеринарноСопроводительныйДокумент <> ЗНАЧЕНИЕ(Справочник.ВетеринарноСопроводительныйДокументВЕТИС.ПустаяСсылка)";
	Регистраторы = ЗапросРегистраторов.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ЗаписиСкладскогоЖурналаВРезервеВЕТИС";
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(
		Параметры.Очередь,
		"Документ.ИсходящаяТранспортнаяОперацияВЕТИС",
		"РегистрСведений.ЗаписиСкладскогоЖурналаВРезервеВЕТИС");
	
	Если Выборка.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Документы.ИсходящаяТранспортнаяОперацияВЕТИС.ОбновитьДвиженияЗаписиСкладскогоЖурналаВРезервеВЕТИС(Выборка.Регистратор);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры, Параметры.Очередь);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось обработать запись регистра ""ЗаписиСкладскогоЖурналаВРезервеВЕТИС"" по регистратору %Регистратор%
			                            |по причине %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Регистратор%", Выборка.Регистратор);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			                         УровеньЖурналаРегистрации.Предупреждение,
			                         Метаданные.РегистрыСведений.ЗаписиСкладскогоЖурналаВРезервеВЕТИС,
			                         Выборка.Регистратор,
			                         ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		"РегистрСведений.ЗаписиСкладскогоЖурналаВРезервеВЕТИС");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли