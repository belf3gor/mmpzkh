#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает сведения о чеке документа-основания.
//
// Параметры:
//  ДокументОснование	 - ДокументСсылка - Ссылка на документ-основание.
// 
// Возвращаемое значение:
//   - Структура - Сведения о чеке. См. НовыйСведенияОЧеке().
//
Функция СведенияОЧеке(ДокументОснование) Экспорт
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ДокументОснование", ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЧекиНПД.ДокументОснование КАК ДокументОснование,
	|	ЧекиНПД.Организация КАК Организация,
	|	ЧекиНПД.Состояние КАК Состояние,
	|	ЧекиНПД.СуммаЧека КАК СуммаЧека,
	|	ЧекиНПД.НомерЧека КАК НомерЧека,
	|	ЧекиНПД.ДатаЧека КАК ДатаЧека,
	|	ЧекиНПД.ДатаАннулированияЧека КАК ДатаАннулированияЧека,
	|	ЧекиНПД.ПричинаАннулирования КАК ПричинаАннулирования,
	|	ЧекиНПД.АдресЧекаНаСайте КАК АдресЧекаНаСайте,
	|	ЧекиНПД.ПрисоединенныйФайлЧека КАК ПрисоединенныйФайлЧека,
	|	ЧекиНПД.ПроизведенВозвратПоЧеку КАК ПроизведенВозвратПоЧеку
	|ИЗ
	|	РегистрСведений.ЧекиНПД КАК ЧекиНПД
	|ГДЕ
	|	ЧекиНПД.ДокументОснование = &ДокументОснование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		СведенияОЧеке = НовыйСведенияОЧеке();
		ЗаполнитьЗначенияСвойств(СведенияОЧеке, Выборка);
		СведенияОЧеке.ОписаниеЧека = ОписаниеЧека(СведенияОЧеке);
		Возврат СведенияОЧеке;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Запускает формирование чека в сервисе Мой налог.
// Нужно использовать в длительной операции.
//
// Параметры:
//  СтруктураПараметров	 - Структура - Структура параметров формирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыФормированияЧека().
//  АдресХранилища		 - Строка - Адрес хранилища, в которое будет помещен результат запуска формирования. См. НовыйРезультатОперацииСЧеком().
//
Процедура СформироватьЧекВФоне(СтруктураПараметров, АдресХранилища) Экспорт
	
	Результат = СформироватьЧек(СтруктураПараметров);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Проверяет результат формирование чека в сервисе Мой налог.
// Нужно использовать в длительной операции.
//
// Параметры:
//  СтруктураПараметров	 - Структура - Структура параметров формирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыОжиданияФормированияЧека().
//  АдресХранилища		 - Строка - Адрес хранилища, в которое будет помещен результат проверки формирования. См. НовыйРезультатОперацииСЧеком().
//
Процедура ПроверитьРезультатФормированияЧекаВФоне(СтруктураПараметров, АдресХранилища) Экспорт
	
	Результат = ПроверитьРезультатФормированияЧека(СтруктураПараметров);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Запускает аннулирование чека в сервисе Мой налог.
// Нужно использовать в длительной операции.
//
// Параметры:
//  СтруктураПараметров	 - Структура - Структура параметров аннулирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыАннулированияЧека().
//  АдресХранилища		 - Строка - Адрес хранилища, в которое будет помещен результат запуска аннулирования. См. НовыйРезультатОперацииСЧеком().
//
Процедура АннулироватьЧекВФоне(СтруктураПараметров, АдресХранилища) Экспорт
	
	Результат = АннулироватьЧек(СтруктураПараметров);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Проверяет результат аннулирования чека в сервисе Мой налог.
// Нужно использовать в длительной операции.
//
// Параметры:
//  СтруктураПараметров	 - Структура - Структура параметров проверки результатов аннулирования чека.
//                                     См. ЧекиНПДКлиентСервер.НовыйПараметрыОжиданияФормированияЧека().
//  АдресХранилища		 - Строка - Адрес хранилища, в которое будет помещен результат проверки аннулирования.
//                                  См. НовыйРезультатОперацииСЧеком().
//
Процедура ПроверитьРезультатАннулированияЧекаВФоне(СтруктураПараметров, АдресХранилища) Экспорт
	
	Результат = ПроверитьРезультатАннулированияЧека(СтруктураПараметров);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Отправляет чек НПД по электронной почте и СМС.
// Нужно использовать в длительной операции.
//
// Параметры:
//  СтруктураПараметров	 - Структура - Параметры отправки, см. ЧекиНПДКлиентСервер.НовыйПараметрыОтправкиЧека().
//  АдресХранилища		 - Строка - Адрес хранилища, в которое будет помещен результат отправки.
//                                  См. НовыйРезультатОтправкиЧека().
//
Процедура ОтправитьЧекВФоне(СтруктураПараметров, АдресХранилища) Экспорт
	
	Результат = ОтправитьЧек(СтруктураПараметров);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Запускает формирование чека в сервисе Мой налог.
//
// Параметры:
//  ПараметрыЧека	 - Структура - Структура параметров формирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыФормированияЧека().
// 
// Возвращаемое значение:
//   - Структура - Результат запуска формирования чека. См. НовыйРезультатОперацииСЧеком().
//
Функция СформироватьЧек(ПараметрыЧека) Экспорт
	
	РезультатФормирования = НовыйРезультатОперацииСЧеком();
	
	ДокументОснование = ПараметрыЧека.Ссылка;
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		РезультатФормирования.Ошибка = Истина;
		РезультатФормирования.ТекстОшибки = НСтр("ru = 'Не указано основание для формирования чека'");
		Возврат РезультатФормирования;
	КонецЕсли;
	
	СведенияОЧеке = СведенияОЧеке(ДокументОснование);
	
	// Проверим, что по чеку не выполнялась регистрация, либо начиналась, но не была
	// завершена. Если регистрация не была завершена, т.е. сервис не вернул пока данных чека,
	// нужно сделать повторную регистрацию дохода. Сервис помнит, что доход уже регистрировался (по Идентификатору)
	// и не выполняет регистрацию, а возвращает данные для запроса результата.
	
	Если СведенияОЧеке <> Неопределено
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ВыполняетсяРегистрация
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ОшибкаПриРегистрации
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ОшибкаПриАннулировании Тогда
		РезультатФормирования.Ошибка = Истина;
		РезультатФормирования.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для документа %1 чек уже сформирован (состояние чека %2)'"),
			ДокументОснование,
			СведенияОЧеке.Состояние);
		Возврат РезультатФормирования;
	КонецЕсли;
	
	ПараметрыРегистрацииДохода = ПараметрыРегистрацииДохода(ПараметрыЧека);
	Ответ = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыРегистрацииДохода);
	
	Если Ответ.Ошибка Тогда
		
		ЗаписатьОшибкуИКодВЖурналРегистрации(НСтр("ru = 'При формировании чека возникла ошибка:'"), Ответ);
		
		РезультатФормирования.Ошибка = Истина;
		РезультатФормирования.ТекстОшибки = Ответ.Сообщение;
		Возврат РезультатФормирования;
		
	КонецЕсли;
	
	СведенияОЧеке = СведенияОЧекеНачалоФормирования(ПараметрыЧека);
	ЗаписатьСведенияОЧеке(СведенияОЧеке);
	
	РезультатФормирования.СведенияОЧеке = СведенияОЧеке;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	РезультатФормирования.СтатусЗапроса = СтатусыЗапросов.Выполняется;
	РезультатФормирования.ПараметрыОжиданияРезультата = Ответ;
	РезультатФормирования.ВремяОжидания = Ответ.ВремяОжидания;
	
	Возврат РезультатФормирования;
	
КонецФункции

// Проверяет результат формирование чека в сервисе Мой налог.
//
// Параметры:
//  ПараметрыЧека	 - Структура - Структура параметров формирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыОжиданияФормированияЧека().
// 
// Возвращаемое значение:
//   - Структура - Результат проверки результата формирования чека. См. НовыйРезультатОперацииСЧеком().
//
Функция ПроверитьРезультатФормированияЧека(ПараметрыЧека) Экспорт
	
	РезультатФормирования = НовыйРезультатОперацииСЧеком();
	
	ДокументОснование = ПараметрыЧека.Ссылка;
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		РезультатФормирования.Ошибка = Истина;
		РезультатФормирования.ТекстОшибки = НСтр("ru = 'Не указано основание для проверки результата формирования чека.'");
		Возврат РезультатФормирования;
	КонецЕсли;
	
	СведенияОЧеке = СведенияОЧеке(ДокументОснование);
	РезультатФормирования.СведенияОЧеке = СведенияОЧеке;
	
	// Проверим, что по чеку начата регистрация дохода.
	Если СведенияОЧеке = Неопределено
		Или СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ВыполняетсяРегистрация Тогда
		РезультатФормирования.Ошибка = Истина;
		РезультатФормирования.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для документа %1 формирование чека не начиналось (состояние чека %2)'"),
			ДокументОснование,
			СведенияОЧеке.Состояние);
		Возврат РезультатФормирования;
	КонецЕсли;
	
	Ответ = ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполнения(ПараметрыЧека.ПараметрыОжиданияРезультата);
	
	РезультатФормирования.СтатусЗапроса = Ответ.Статус;
	РезультатФормирования.ВремяОжидания = Ответ.ВремяОжидания;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	Если Ответ.Статус = СтатусыЗапросов.Выполнено Тогда
		
		СведенияОЧеке.Состояние        = Перечисления.СостоянияЧековНПД.Зарегистрирован;
		СведенияОЧеке.НомерЧека        = Ответ.Результат.ИдентификаторЧека;
		СведенияОЧеке.ОписаниеЧека     = ОписаниеЧека(СведенияОЧеке);
		СведенияОЧеке.АдресЧекаНаСайте = Ответ.Результат.Ссылка;
		
		Если ЗначениеЗаполнено(Ответ.Результат.Base64Строка) Тогда
			ПрисоединитьИзображениеЧекаКДокументуОснованию(СведенияОЧеке,
				ПолучитьДвоичныеДанныеИзBase64Строки(Ответ.Результат.Base64Строка),
				Ответ.Результат.ТипДанных);
		КонецЕсли;
		
		ЗаписатьСведенияОЧеке(СведенияОЧеке);
		
		РезультатФормирования.СведенияОЧеке = СведенияОЧеке;
		
	ИначеЕсли Ответ.Статус = СтатусыЗапросов.Ошибка
		Или Ответ.Статус = СтатусыЗапросов.Отменено Тогда
		
		ЗаписатьОшибкуИКодВЖурналРегистрации(НСтр("ru = 'При проверке формировании чека возникла ошибка:'"), Ответ);
		
		РезультатФормирования.Ошибка = Истина;
		РезультатФормирования.ТекстОшибки = Ответ.Сообщение;
		
		СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.ОшибкаПриРегистрации;
		
		ЗаписатьСведенияОЧеке(СведенияОЧеке);
		
		РезультатФормирования.СведенияОЧеке = СведенияОЧеке;
		
	КонецЕсли;
	
	Возврат РезультатФормирования;
	
КонецФункции

// Запускает аннулирование чека в сервисе Мой налог.
//
// Параметры:
//  ПараметрыЧека	 - Структура - Структура параметров формирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыАннулированияЧека().
// 
// Возвращаемое значение:
//   - Структура - Результат запуска аннулирования чека. См. НовыйРезультатОперацииСЧеком().
//
Функция АннулироватьЧек(ПараметрыЧека) Экспорт
	
	РезультатАннулирования = НовыйРезультатОперацииСЧеком();
	
	ДокументОснование = ПараметрыЧека.Ссылка;
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = НСтр("ru = 'Не указано основание для аннулирования чека'");
		Возврат РезультатАннулирования;
	КонецЕсли;
	
	СведенияОЧеке = СведенияОЧеке(ДокументОснование);
	
	// Проверим, что чек зарегистрирован, по чеку не выполнялось аннулирование, либо аннулирование начиналось,
	// но не было завершено. Если аннулирование не было завершено, т.е. сервис не вернул пока данных чека,
	// нужно сделать повторное аннулирование дохода. Сервис помнит, что доход уже аннулирован (по Идентификатору)
	// и не выполняет аннулирование, а вернет данные для запроса результата.
	
	Если ЧекиНПДКлиентСервер.НужноНачатьФормированиеЧека(СведенияОЧеке) Тогда
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для документа %1 чек еще не сформирован (состояние чека %2). Его аннулирование невозможно.'"),
			ДокументОснование,
			СведенияОЧеке.Состояние);
		Возврат РезультатАннулирования;
	КонецЕсли;
	
	Если СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Зарегистрирован
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ВыполняетсяАннулирование
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ОшибкаПриАннулировании Тогда
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Состояние чека (%1) для документа %2 не подходит для аннулирования.'"),
			СведенияОЧеке.Состояние,
			ДокументОснование);
		Возврат РезультатАннулирования;
	КонецЕсли;
	
	ПараметрыАннулированияЧека = ПараметрыАннулированияЧека(ПараметрыЧека);
	Ответ = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыАннулированияЧека);
	
	Если Ответ.Ошибка Тогда
		ЗаписатьОшибкуИКодВЖурналРегистрации(НСтр("ru = 'При аннулировании чека возникла ошибка:'"), Ответ);
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = Ответ.Сообщение;
		Возврат РезультатАннулирования;
	КонецЕсли;
	
	СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.ВыполняетсяАннулирование;
	СведенияОЧеке.ПричинаАннулирования = ПараметрыЧека.ПричинаОтменыЧека;
	ЗаписатьСведенияОЧеке(СведенияОЧеке);
	
	РезультатАннулирования.СведенияОЧеке = СведенияОЧеке;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	РезультатАннулирования.СтатусЗапроса = СтатусыЗапросов.Выполняется;
	РезультатАннулирования.ПараметрыОжиданияРезультата = Ответ;
	РезультатАннулирования.ПараметрыОжиданияРезультата.Вставить("ВозвратПоДокументу", ПараметрыЧека.ДокументОснование);
	РезультатАннулирования.ВремяОжидания = Ответ.ВремяОжидания;
	Возврат РезультатАннулирования;
	
КонецФункции

// Проверяет результат аннулирования чека в сервисе Мой налог.
//
// Параметры:
//  ПараметрыЧека	 - Структура - Структура параметров проверки аннулирования чека. См. ЧекиНПДКлиентСервер.НовыйПараметрыОжиданияАннулированияЧека().
// 
// Возвращаемое значение:
//   - Структура - Результат проверки результата аннулирования чека. См. НовыйРезультатОперацииСЧеком().
//
Функция ПроверитьРезультатАннулированияЧека(ПараметрыЧека) Экспорт
	
	РезультатАннулирования = НовыйРезультатОперацииСЧеком();
	
	ДокументОснование = ПараметрыЧека.Ссылка;
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = НСтр("ru = 'Не указано основание для проверки результата аннулирования чека.'");
		Возврат РезультатАннулирования;
	КонецЕсли;
	
	СведенияОЧеке = СведенияОЧеке(ДокументОснование);
	РезультатАннулирования.СведенияОЧеке = СведенияОЧеке;
	
	// Проверим, что по чеку начата регистрация дохода.
	Если СведенияОЧеке = Неопределено
		Или СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ВыполняетсяАннулирование Тогда
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для документа %1 аннулирование чека не начиналось (состояние чека %2)'"),
			ДокументОснование,
			СведенияОЧеке.Состояние);
		Возврат РезультатАннулирования;
	КонецЕсли;
	
	Ответ = ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполнения(ПараметрыЧека.ПараметрыОжиданияРезультата);
	
	РезультатАннулирования.СтатусЗапроса = Ответ.Статус;
	РезультатАннулирования.ВремяОжидания = Ответ.ВремяОжидания;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	Если Ответ.Статус = СтатусыЗапросов.Выполнено Тогда
		
		СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.Аннулирован;
		СведенияОЧеке.ДатаАннулированияЧека = ТекущаяДатаСеанса();
		СведенияОЧеке.ОписаниеЧека = ОписаниеЧека(СведенияОЧеке);
		
		Если ЗначениеЗаполнено(Ответ.Результат.Base64Строка) Тогда
			ПрисоединитьИзображениеЧекаКДокументуОснованию(СведенияОЧеке,
				ПолучитьДвоичныеДанныеИзBase64Строки(Ответ.Результат.Base64Строка),
				Ответ.Результат.ТипДанных);
		КонецЕсли;
		
		ЗаписатьСведенияОЧеке(СведенияОЧеке);
		
		ВозвратПоДокументу = ПараметрыЧека.ПараметрыОжиданияРезультата.ВозвратПоДокументу;
		Если ЗначениеЗаполнено(ВозвратПоДокументу) Тогда
			СведенияЧекаОснования = СведенияОЧеке(ВозвратПоДокументу);
			СведенияЧекаОснования.ПроизведенВозвратПоЧеку = Истина;
			СведенияЧекаОснования.ДатаАннулированияЧека = ТекущаяДатаСеанса();
			СведенияЧекаОснования.ПричинаАннулирования = СведенияОЧеке.ПричинаАннулирования;
			СведенияЧекаОснования.ОписаниеЧека = ОписаниеЧека(СведенияЧекаОснования);
			Если ЗначениеЗаполнено(Ответ.Результат.Base64Строка) Тогда
				ПрисоединитьИзображениеЧекаКДокументуОснованию(СведенияЧекаОснования,
					ПолучитьДвоичныеДанныеИзBase64Строки(Ответ.Результат.Base64Строка),
					Ответ.Результат.ТипДанных);
			КонецЕсли;
			ЗаписатьСведенияОЧеке(СведенияЧекаОснования);
			Если Не ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВозвратПоДокументу, "Комментарий")) Тогда
				// Если чек НПД аннулируется не из того документа, из которого был пробит изначально, то для исходного документа
				// в комментарий так же добавляется причина аннулирования чека
				Попытка
					ДокументОбъект = ВозвратПоДокументу.ПолучитьОбъект();
					ДокументОбъект.Заблокировать();
					ДокументОбъект.Комментарий = СведенияЧекаОснования.ПричинаАннулирования;
					ДокументОбъект.Записать();
					ДокументОбъект.Разблокировать();
				Исключение
					ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать причину аннулирования чека НПД в документе %1'"),
						ВозвратПоДокументу);
					ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки);
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
		РезультатАннулирования.СведенияОЧеке = СведенияОЧеке;
		
	ИначеЕсли Ответ.Статус = СтатусыЗапросов.Ошибка
		Или Ответ.Статус = СтатусыЗапросов.Отменено Тогда
		
		ЗаписатьОшибкуИКодВЖурналРегистрации(НСтр("ru = 'При проверке аннулирования чека возникла ошибка:'"), Ответ);
		РезультатАннулирования.Ошибка = Истина;
		РезультатАннулирования.ТекстОшибки = Ответ.Сообщение;
		
		СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.ОшибкаПриАннулировании;
		
		ЗаписатьСведенияОЧеке(СведенияОЧеке);
		
		РезультатАннулирования.СведенияОЧеке = СведенияОЧеке;
		
	КонецЕсли;
	
	Возврат РезультатАннулирования;
	
КонецФункции

// Отправляет чек НПД по электронной почте и СМС.
//
// Параметры:
//  ПараметрыОтправки	 - Структура - Параметры отправки, см. ЧекиНПДКлиентСервер.НовыйПараметрыОтправкиЧека().
// 
// Возвращаемое значение:
//   - Структура - Результат отправки. См. НовыйРезультатОтправкиЧека().
//
Функция ОтправитьЧек(ПараметрыОтправки) Экспорт
	
	РезультатОтправки = НовыйРезультатОтправкиЧека();
	
	ДокументОснование = ПараметрыОтправки.Ссылка;
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		ВызватьИсключение НСтр("ru = 'Не указано основание для отправки чека'");
	КонецЕсли;
	
	СведенияОЧеке = СведенияОЧеке(ДокументОснование);
	
	// Проверим, что чек зарегистрирован или аннулирован.
	
	Если Не ЗначениеЗаполнено(СведенияОЧеке)
		Или (СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Зарегистрирован
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Аннулирован) Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Нельзя отправлять чек в статусе %1.'"), СведенияОЧеке.Состояние);
	КонецЕсли;
	
	Если ПараметрыОтправки.ОтправлятьEmail Тогда
		
		РезультатыОтправкиEmail = ОтправитьEmail(ПараметрыОтправки.АдресЭлектроннойПочты,
			СведенияОЧеке, ПараметрыОтправки.УникальныйИдентификатор);
		РезультатОтправки.EmailОтправлен = Не РезультатыОтправкиEmail.Ошибка;
		РезультатОтправки.ОшибкаОтправкиEmail = РезультатыОтправкиEmail.Ошибка;
		РезультатОтправки.ТекстОшибкиОтправкиEmail = РезультатыОтправкиEmail.ТекстОшибки;
		
	КонецЕсли;
	
	Если ПараметрыОтправки.ОтправлятьSMS Тогда
		РезультатОтправкиSMS = ОтправитьSMS(ПараметрыОтправки.НомерТелефона, СведенияОЧеке);
		РезультатОтправки.SMSОтправлен = Не РезультатОтправкиSMS.Ошибка;
		РезультатОтправки.ОшибкаОтправкиSMS = РезультатОтправкиSMS.Ошибка;
		РезультатОтправки.ТекстОшибкиОтправкиSMS = РезультатОтправкиSMS.ТекстОшибки;
	КонецЕсли;
	
	Возврат РезультатОтправки;
	
КонецФункции

// Запускает длительную операцию по аннулированию чека в сервисе Мой налог.
//
// Параметры:
//	ПараметрыАннулированияЧека - Структура - параметры аннулирования чека.
//	Идентификатор - УникальныйИдентификатор - уникальный идентификатор документа.
//
// Возвращаемое значение:
//   ДлительнаяОперация - Структура.
//
Функция ЗапуститьАннулированиеЧекаВФоне(ПараметрыАннулированияЧека, Идентификатор) Экспорт

	СведенияОЧеке = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыАннулированияЧека.ДокументОснование) Тогда
		СведенияОЧеке = РегистрыСведений.ЧекиНПД.СведенияОЧеке(ПараметрыАннулированияЧека.ДокументОснование);
	КонецЕсли;
	
	//Сделаем запись в регистр сведений ЧекиНПД.
	НаборЗаписей = РегистрыСведений.ЧекиНПД.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(ПараметрыАннулированияЧека.Ссылка);
	
	НоваяСтрока = НаборЗаписей.Добавить();
	
	Если ЗначениеЗаполнено(СведенияОЧеке) Тогда
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СведенияОЧеке);
	КонецЕсли;
	
	НоваяСтрока.ДатаАннулированияЧека = ТекущаяДата();
	НоваяСтрока.ДокументОснование = ПараметрыАннулированияЧека.Ссылка;
	НоваяСтрока.НомерЧека = ПараметрыАннулированияЧека.НомерЧека;
	НоваяСтрока.Организация = ПараметрыАннулированияЧека.Организация;
	НоваяСтрока.Состояние = Перечисления.СостоянияЧековНПД.ВыполняетсяАннулирование;
	НоваяСтрока.ПрисоединенныйФайлЧека = Неопределено;
	
	НаборЗаписей.Записать(Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Идентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Аннулирование чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.АннулироватьЧекВФоне",
		ПараметрыАннулированияЧека,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию по проверке аннулирования чека в сервисе Мой налог.
//
// Параметры:
//	ПараметрыПроцедуры - Структура - параметры аннулирования.
//	Идентификатор - УникальныйИдентификатор - уникальный идентификатор документа.
//
// Возвращаемое значение:
//   ДлительнаяОперация - Структура.
//
Функция ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне(ПараметрыПроцедуры, Идентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Идентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка результата аннулирования чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.ПроверитьРезультатАннулированияЧекаВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
		
	Возврат ДлительнаяОперация;
	
КонецФункции

// Возвращает представление для аннулированного чека НПД
//
// Параметры:
//  Номер - строка - номер чека НПД
//  ДатаАннулирования - дата - дата аннулирования чека НПД
// 
// Возвращаемое значение:
//  Строка - представление аннулированного чека НПД
//
Функция ПредставлениеАннулированногоЧека(Номер, ДатаАннулирования) Экспорт
	
	ШаблонПредставленияЧека = НСтр("ru='%1, аннулирован %2'");
	Возврат СтрШаблон(ШаблонПредставленияЧека, Номер, Формат(ДатаАннулирования, "ДЛФ=D"));
	
КонецФункции

Процедура ПолучитьФайлЧекаВФоне(Параметры, АдресРезультата) Экспорт
	Перем СведенияОЧеке;
	Если НЕ Параметры.Свойство("СведенияОЧеке", СведенияОЧеке)
		ИЛИ Не ЗначениеЗаполнено(СведенияОЧеке)
		ИЛИ НЕ ЗначениеЗаполнено(СведенияОЧеке.АдресЧекаНаСайте)
		Или (СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Зарегистрирован
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Аннулирован
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.ОшибкаПриАннулировании) Тогда
		
		
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеЧека = ПолучитьФайлЧека(СведенияОЧеке.АдресЧекаНаСайте);
	
	Если ДвоичныеДанныеЧека = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ПрисоединитьИзображениеЧекаКДокументуОснованию(СведенияОЧеке, ДвоичныеДанныеЧека,"jpg");
	
	ЗаписатьСведенияОЧеке(СведенияОЧеке);
	
	ПоместитьВоВременноеХранилище(СведенияОЧеке, АдресРезультата);
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция МакетЧекаДляПечати(СведенияОЧеке) Экспорт
	
	// Проверим, что чек зарегистрирован или аннулирован.
	
	Если Не ЗначениеЗаполнено(СведенияОЧеке)
		Или (СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Зарегистрирован
		И СведенияОЧеке.Состояние <> Перечисления.СостоянияЧековНПД.Аннулирован) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ДанныеЧека = РаботаСФайлами.ДанныеФайла(СведенияОЧеке.ПрисоединенныйФайлЧека, , Ложь);
	ДвоичныеДанныеЧека = РаботаСФайлами.ДвоичныеДанныеФайла(СведенияОЧеке.ПрисоединенныйФайлЧека);
	
	Если ЧекХранитсяВТаблице(ДанныеЧека.Расширение) Тогда
		ТабличныйДокумент = ТабличныйДокументЧека(ДвоичныеДанныеЧека);
	Иначе
		ТабличныйДокумент = ТабличныйДокументЧекаИзКартинки(ДвоичныеДанныеЧека);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ТабличныйДокументЧека(ДвоичныеДанныеЧека) Экспорт
	
	БуферДанныхЧека = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ДвоичныеДанныеЧека);
	ПотокВПамяти = Новый ПотокВПамяти(БуферДанныхЧека);
	
	ТаблицаЧека = Новый ТабличныйДокумент;
	ТаблицаЧека.Прочитать(ПотокВПамяти);
	Возврат ТаблицаЧека;
	
КонецФункции

Функция ТабличныйДокументЧекаИзКартинки(ДвоичныеДанныеЧека) Экспорт
	
	ТаблицаЧека = Новый ТабличныйДокумент;
	
	КартинкаЧека = Новый Картинка(ДвоичныеДанныеЧека, Ложь);
	
	РисунокТабличногоДокумента = ТаблицаЧека.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
	РисунокТабличногоДокумента.Картинка = КартинкаЧека;
	РисунокТабличногоДокумента.ГраницаСверху = Ложь;
	РисунокТабличногоДокумента.ГраницаСнизу = Ложь;
	РисунокТабличногоДокумента.ГраницаСлева = Ложь;
	РисунокТабличногоДокумента.ГраницаСправа = Ложь;
	
	РисунокТабличногоДокумента.РазмерКартинки = РазмерКартинки.Пропорционально;
	
	РисунокТабличногоДокумента.Ширина = 90;
	РисунокТабличногоДокумента.Высота = 150;
	
	Возврат ТаблицаЧека
	
КонецФункции

Функция ЧекХранитсяВТаблице(Расширение) Экспорт
	
	Возврат НРег(Расширение) = "mxl";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйСведенияОЧеке()
	
	СведенияОЧеке = Новый Структура;
	СведенияОЧеке.Вставить("ДокументОснование", Неопределено);
	СведенияОЧеке.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СведенияОЧеке.Вставить("СуммаЧека", 0);
	СведенияОЧеке.Вставить("Состояние", Неопределено);
	СведенияОЧеке.Вставить("ОписаниеЧека", "");
	СведенияОЧеке.Вставить("НомерЧека", "");
	СведенияОЧеке.Вставить("ДатаЧека", '00010101');
	СведенияОЧеке.Вставить("ДатаАннулированияЧека", '00010101');
	СведенияОЧеке.Вставить("ПричинаАннулирования", "");
	СведенияОЧеке.Вставить("АдресЧекаНаСайте", "");
	СведенияОЧеке.Вставить("ПрисоединенныйФайлЧека", Неопределено);
	СведенияОЧеке.Вставить("ПроизведенВозвратПоЧеку", Ложь);
	
	Возврат СведенияОЧеке;
	
КонецФункции

Функция НовыйРезультатОперацииСЧеком()
	
	РезультатФормированияЧека = Новый Структура;
	РезультатФормированияЧека.Вставить("Ошибка", Ложь);
	РезультатФормированияЧека.Вставить("ТекстОшибки", "");
	РезультатФормированияЧека.Вставить("СведенияОЧеке", Неопределено);
	РезультатФормированияЧека.Вставить("СтатусЗапроса", Неопределено);
	РезультатФормированияЧека.Вставить("ПараметрыОжиданияРезультата", Неопределено);
	РезультатФормированияЧека.Вставить("ВремяОжидания", 0);
	Возврат РезультатФормированияЧека;
	
КонецФункции

Функция НовыйРезультатОтправкиЧека()
	
	Результат = Новый Структура;
	Результат.Вставить("EmailОтправлен", Ложь);
	Результат.Вставить("ОшибкаОтправкиEmail", Ложь);
	Результат.Вставить("ТекстОшибкиОтправкиEmail", Ложь);
	Результат.Вставить("SMSОтправлен", Ложь);
	Результат.Вставить("ОшибкаОтправкиSMS", Ложь);
	Результат.Вставить("ТекстОшибкиОтправкиSMS", Ложь);
	Возврат Результат;
	
КонецФункции

Функция ОписаниеЧека(СведенияОЧеке)
	
	ЧастиОписания = Новый Массив;
	ЧастиОписания.Добавить(НСтр("ru = 'Чек'"));
	ЧастиОписания.Добавить(" ");
	ЧастиОписания.Добавить(СведенияОЧеке.НомерЧека);
	
	Если ЗначениеЗаполнено(СведенияОЧеке.ДатаЧека) Тогда
		ЧастиОписания.Добавить(" ");
		ЧастиОписания.Добавить(НСтр("ru = 'от'"));
		ЧастиОписания.Добавить(" ");
		ЧастиОписания.Добавить(Формат(СведенияОЧеке.ДатаЧека, "ДЛФ=D"));
	КонецЕсли;
	
	Если СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.Аннулирован
		Или СведенияОЧеке.ПроизведенВозвратПоЧеку Тогда
		ЧастиОписания.Добавить(НСтр("ru = ', аннулирован'"));
		ЧастиОписания.Добавить(" ");
		ЧастиОписания.Добавить(Формат(СведенияОЧеке.ДатаАннулированияЧека, "ДЛФ=D"));
	КонецЕсли;
	
	Возврат СтрСоединить(ЧастиОписания);
	
КонецФункции

Функция ПараметрыРегистрацииДохода(ПараметрыЧека)
	
	ДокументОснование = ПараметрыЧека.Ссылка;
	
	ПараметрыРегистрацииДохода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(
		"РегистрацияДохода");
	ПараметрыРегистрацииДохода.УИДЧека = Строка(ДокументОснование.УникальныйИдентификатор());
	ПараметрыРегистрацииДохода.Организация = ПараметрыЧека.Организация;
	ПараметрыРегистрацииДохода.ДатаРасчета = ПараметрыЧека.Дата;
	ПараметрыРегистрацииДохода.ДатаФормирования = ТекущаяДатаСеанса();
	Если ЗначениеЗаполнено(ПараметрыЧека.Контрагент) Тогда
		
		ДополнительныеСведенияОКонтрагенте = Новый Структура("ЭтоФизическоеЛицо", Ложь);
		ИнтеграцияСПлатформойСамозанятыеПереопределяемый.ДополнительныеСведенияОКонтрагенте(ПараметрыЧека.Контрагент, ДополнительныеСведенияОКонтрагенте);
		
		Если НЕ ДополнительныеСведенияОКонтрагенте.ЭтоФизическоеЛицо Тогда
			ПараметрыРегистрацииДохода.Контрагент = ПараметрыЧека.Контрагент;
		КонецЕсли;
	КонецЕсли;
	ПараметрыРегистрацииДохода.СуммаДокумента = ПараметрыЧека.СуммаДокумента;
	
	Услуги = Новый ТаблицаЗначений;
	Услуги.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Услуги.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	Услуги.Колонки.Добавить("Цена", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Услуги.Колонки.Добавить("Всего", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	НаименованияУслуги = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПараметрыЧека.УслугаНПД, "Наименование,НаименованиеПолное");
	
	НаименованиеУслуги = ?(ЗначениеЗаполнено(НаименованияУслуги.НаименованиеПолное),
		НаименованияУслуги.НаименованиеПолное, НаименованияУслуги.Наименование);
	
	Услуга = Услуги.Добавить();
	Услуга.Наименование = НаименованиеУслуги;
	Услуга.Количество = 1;
	Услуга.Цена = ПараметрыЧека.СуммаДокумента;
	Услуга.Всего = ПараметрыЧека.СуммаДокумента;
	
	ПараметрыРегистрацииДохода.Услуги = Услуги;
	
	Возврат ПараметрыРегистрацииДохода;
	
КонецФункции

Функция ПараметрыАннулированияЧека(ПараметрыЧека)
	
	ДокументОснование = ПараметрыЧека.Ссылка;
	
	ПараметрыАннулированияЧека = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(
		"СторнированиеДохода");
	ПараметрыАннулированияЧека.Организация = ПараметрыЧека.Организация;
	ПараметрыАннулированияЧека.УИДЧека = Строка(ДокументОснование.УникальныйИдентификатор());
	ПараметрыАннулированияЧека.ИдентификаторЧека = ПараметрыЧека.НомерЧека;
	ПараметрыАннулированияЧека.ПричинаОтменыЧека = ПараметрыЧека.ПричинаОтменыЧека;
	
	Возврат ПараметрыАннулированияЧека;
	
КонецФункции

Функция СведенияОЧекеНачалоФормирования(ПараметрыЧека);
	
	СведенияОЧеке = НовыйСведенияОЧеке();
	СведенияОЧеке.ДокументОснование = ПараметрыЧека.Ссылка;
	СведенияОЧеке.Организация = ПараметрыЧека.Организация;
	СведенияОЧеке.СуммаЧека = ПараметрыЧека.СуммаДокумента;
	СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.ВыполняетсяРегистрация;
	СведенияОЧеке.ДатаЧека = ПараметрыЧека.Дата;
	СведенияОЧеке.ОписаниеЧека = ОписаниеЧека(СведенияОЧеке);
	Возврат СведенияОЧеке;
	
КонецФункции

Процедура ЗаписатьСведенияОЧеке(СведенияОЧеке)
	
	НаборЗаписей = РегистрыСведений.ЧекиНПД.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(СведенияОЧеке.ДокументОснование);
	Запись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(Запись, СведенияОЧеке);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ПрисоединитьИзображениеЧекаКДокументуОснованию(СведенияОЧеке, ДвоичныеДанныеИзображенияЧека, РасширениеФайла)
	
	ИмяФайлаЧекаБезТочки = СведенияОЧеке.ОписаниеЧека;
	ИмяФайлаЧека         = ИмяФайлаЧекаБезТочки + "." + РасширениеФайла;
	
	АдресФайлаВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеИзображенияЧека);
	
	ПараметрыФайла = Новый Структура();
	ПараметрыФайла.Вставить("Автор", Пользователи.АвторизованныйПользователь());
	ПараметрыФайла.Вставить("ВладелецФайлов", СведенияОЧеке.ДокументОснование);
	ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяФайлаЧекаБезТочки);
	ПараметрыФайла.Вставить("РасширениеБезТочки", РасширениеФайла);
	ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное");
	ПрисоединенныйФайлЧека = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайлаВХранилище);
	
	СведенияОЧеке.ПрисоединенныйФайлЧека = ПрисоединенныйФайлЧека;
	
	УдалитьИзВременногоХранилища(АдресФайлаВХранилище);
	
КонецФункции

Процедура ЗаписатьОшибкуИКодВЖурналРегистрации(КонтекстОшибки, Ответ)
	
	ЧастиСообщенияОбОшибке = Новый Массив;
	ЧастиСообщенияОбОшибке.Добавить(КонтекстОшибки);
	ЧастиСообщенияОбОшибке.Добавить(Ответ.Сообщение);
	Если Ответ.Свойство("Результат") И ЗначениеЗаполнено(Ответ.Результат) И Ответ.Результат.Свойство("Код") Тогда
		ЧастиСообщенияОбОшибке.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Код ошибки: %1'"), Ответ.Результат.Код));
	КонецЕсли;
	ЗаписатьОшибкуВЖурналРегистрации(СтрСоединить(ЧастиСообщенияОбОшибке, Символы.ПС));
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВЖурналРегистрации(ОписаниеОшибки)
	
	ЗаписьЖурналаРегистрации(ЧекиНПДКлиентСервер.ИмяСобытияЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ЧекиНПД, , ОписаниеОшибки);
	
КонецПроцедуры

#Область ОтправкаЧека

Функция НовыйРезультатОтправки()
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	Возврат Результат;
	
КонецФункции

Функция ОтправитьEmail(АдресЭлектроннойПочты, СведенияОЧеке, УникальныйИдентификатор)
	
	РезультатОтправки = НовыйРезультатОтправки();
	
	УчетнаяЗапись = УчетнаяЗаписьДляОтправки();
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		РезультатОтправки.Ошибка = Истина;
		РезультатОтправки.ТекстОшибки = НСтр("ru = 'Ошибка при отправке электронной почты: нет доступных учетных записей для отправки.'");
		Возврат РезультатОтправки;
	КонецЕсли;
	
	ПараметрыПисьма = ПараметрыПисьма(СведенияОЧеке, УникальныйИдентификатор);
	
	ПараметрыПисьма = ОтправкаПочтовыхСообщений.ПараметрыПисьмаЧекаНПД(
		АдресЭлектроннойПочты, ПараметрыПисьма.Тело, ПараметрыПисьма.Тема, ПараметрыПисьма.Вложения);
	
	Попытка
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(УчетнаяЗапись, ПараметрыПисьма);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		РезультатОтправки.Ошибка = Истина;
		РезультатОтправки.ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецПопытки;
	
	Возврат РезультатОтправки;
	
КонецФункции

Функция ПараметрыПисьма(СведенияОЧеке, УникальныйИдентификатор)
	
	ПараметрыПисьма = НовыйПараметрыПисьма();
	
	ПараметрыПисьма.Тема = СведенияОЧеке.ОписаниеЧека;
	
	ДобавитьЗаголовокПисьма(ПараметрыПисьма.Тело, СведенияОЧеке);
	
	ДобавитьСведенияОЧекеИПрисоединенномФайле(ПараметрыПисьма.Тело, ПараметрыПисьма.Вложения,
		СведенияОЧеке, УникальныйИдентификатор);
	
	ДобавитьСсылкуНаЧек(ПараметрыПисьма.Тело,  СведенияОЧеке.АдресЧекаНаСайте);
	
	Возврат ПараметрыПисьма;
	
КонецФункции

Процедура ДобавитьЗаголовокПисьма(ТелоПисьма, СведенияОЧеке)
	
	ТелоПисьма.Добавить(НСтр("ru = 'Уважаемый покупатель!'"), ТипЭлементаФорматированногоДокумента.Текст);
	ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	
	Если СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.Зарегистрирован 
		И Не СведенияОЧеке.ПроизведенВозвратПоЧеку Тогда
		ТекстПокупка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вы совершили покупку на сумму %1 руб.'"), Формат(СведенияОЧеке.СуммаЧека, "ЧДЦ=2"));
	Иначе
		ТекстПокупка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Покупка по чеку %1 на сумму %2 руб. аннулирована: %3'"),
			СведенияОЧеке.НомерЧека,
			Формат(СведенияОЧеке.СуммаЧека, "ЧДЦ=2"),
			СведенияОЧеке.ПричинаАннулирования);
	КонецЕсли;
	ТелоПисьма.Добавить(ТекстПокупка, ТипЭлементаФорматированногоДокумента.Текст);
	ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	
КонецПроцедуры

Процедура ДобавитьСведенияОЧекеИПрисоединенномФайле(ТелоПисьма, Вложения, СведенияОЧеке, УникальныйИдентификатор)
	
	Если Не ЗначениеЗаполнено(СведенияОЧеке.ПрисоединенныйФайлЧека) Тогда
		Возврат
	КонецЕсли;
	
	ДанныеЧека = РаботаСФайлами.ДанныеФайла(СведенияОЧеке.ПрисоединенныйФайлЧека, , Ложь);
	ДвоичныеДанныеЧека = РаботаСФайлами.ДвоичныеДанныеФайла(СведенияОЧеке.ПрисоединенныйФайлЧека);
	
	Если ЧекХранитсяВТаблице(ДанныеЧека.Расширение) Тогда
		
		ДобавитьВложениеСЧекомВФорматеPDF(ТелоПисьма, Вложения, ДанныеЧека, ДвоичныеДанныеЧека, УникальныйИдентификатор);
		
	Иначе
		
		ВставитьКартинкуЧекаВТелоПисьма(ТелоПисьма, ДвоичныеДанныеЧека);
		 
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьВложениеСЧекомВФорматеPDF(ТелоПисьма, Вложения, ДанныеЧека, ДвоичныеДанныеЧека, УникальныйИдентификатор)
	
	ТелоПисьма.Добавить(НСтр("ru = 'Чек присоединен к письму.'"), ТипЭлементаФорматированногоДокумента.Текст);
	ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	
	АдресЧекаPDF = ПоместитьВоВременноеХранилище(ТабличныйДокументЧекаВPDF(ДвоичныеДанныеЧека), УникальныйИдентификатор);
	
	ВложениеPDF = Новый Структура;
	ВложениеPDF.Вставить("АдресВоВременномХранилище", АдресЧекаPDF);
	ВложениеPDF.Вставить("Представление", ДанныеЧека.Наименование + ".pdf");
	
	Вложения.Добавить(ВложениеPDF);
	
КонецПроцедуры

Процедура ВставитьКартинкуЧекаВТелоПисьма(ТелоПисьма, ДвоичныеДанныеЧека)
	
	КартинкаЧека = Новый Картинка(ДвоичныеДанныеЧека, Ложь);
	ТелоПисьма.Добавить(КартинкаЧека, ТипЭлементаФорматированногоДокумента.Картинка);
	ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	
КонецПроцедуры

Процедура ДобавитьСсылкуНаЧек(ТелоПисьма, АдресЧекаНаСайте)
	
	Если ЗначениеЗаполнено(АдресЧекаНаСайте) Тогда
		ТекстСсылка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вы можете посмотреть чек по ссылке %1'"), АдресЧекаНаСайте);
		ТелоПисьма.Добавить(ТекстСсылка, ТипЭлементаФорматированногоДокумента.Текст);
		ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
		ТелоПисьма.Добавить(, ТипЭлементаФорматированногоДокумента.ПереводСтроки);
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйПараметрыПисьма()
	
	ПараметрыПисьма = Новый Структура();
	ПараметрыПисьма.Вставить("Тема", "");
	ПараметрыПисьма.Вставить("Тело", Новый ФорматированныйДокумент);
	ПараметрыПисьма.Вставить("Вложения", Новый Массив);
	Возврат ПараметрыПисьма;
	
КонецФункции

Функция ТабличныйДокументЧекаВPDF(ДвоичныеДанныеЧека)
	
	ТабличныйДокументЧека = ТабличныйДокументЧека(ДвоичныеДанныеЧека);
	
	ПотокВПамяти = Новый ПотокВПамяти();
	ТабличныйДокументЧека.Записать(ПотокВПамяти, ТипФайлаТабличногоДокумента.PDF);
	Возврат ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

Функция УчетнаяЗаписьДляОтправки()
	
	УчетнаяЗаписьИзНастроек = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("УчетнаяЗаписьЭлектроннойПочты");
	
	Если ЗначениеЗаполнено(УчетнаяЗаписьИзНастроек) Тогда
		Возврат УчетнаяЗаписьИзНастроек;
	КонецЕсли;
	
	// Учетная запись не передана - выбираем первую доступную.
	ДоступныеУчетныеЗаписи = РаботаСПочтовымиСообщениями.ДоступныеУчетныеЗаписи(Истина);
	Если ДоступныеУчетныеЗаписи.Количество() > 0 Тогда
		Возврат ДоступныеУчетныеЗаписи[0].Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОтправитьSMS(НомерТелефона, СведенияОЧеке)
	
	РезультатОтправки = НовыйРезультатОтправки();
	
	Если Не ОтправкаSMS.НастройкаОтправкиSMSВыполнена() Тогда
		РезультатОтправки.Ошибка = Истина;
		РезультатОтправки.ТекстОшибки = НСтр("ru = 'Ошибка при отправке SMS: не выполнена настройка отправки.'");
		Возврат РезультатОтправки;
	КонецЕсли;
	
	ТекстСМС = ТекстSMS(СведенияОЧеке);
	
	НомераПолучателей = Новый Массив;
	НомераПолучателей.Добавить(НомерТелефонаВКаноническийВид(НомерТелефона));
	
	Попытка
		ОтправкаSMS.ОтправитьSMS(НомераПолучателей, ТекстСМС);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		РезультатОтправки.Ошибка = Истина;
		РезультатОтправки.ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецПопытки;
	
	Возврат РезультатОтправки;
	
КонецФункции

Функция ТекстSMS(СведенияОЧеке)
	
	ЧастиТекста = Новый Массив;
	ЧастиТекста.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Чек %1'"), СведенияОЧеке.НомерЧека));
	ЧастиТекста.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'СУММА %1'"), Формат(СведенияОЧеке.СуммаЧека, "ЧДЦ=2")));
	ЧастиТекста.Добавить(Формат(СведенияОЧеке.ДатаЧека, "ДЛФ=DT"));
	Возврат СтрСоединить(ЧастиТекста, ";" + " ");
	
КонецФункции

Функция НомерТелефонаВКаноническийВид(НомерТелефона)
	
	КаноническийНомер = СтрЗаменить(НомерТелефона, "(", "");
	КаноническийНомер = СтрЗаменить(КаноническийНомер, ")", "");
	КаноническийНомер = СтрЗаменить(КаноническийНомер, "-", "");
	КаноническийНомер = СтрЗаменить(КаноническийНомер, " ", "");
	
	ДлинаНомераТелефона = СтрДлина(КаноническийНомер);
	
	Если ДлинаНомераТелефона = 10 Тогда
		КаноническийНомер = "+7" + КаноническийНомер;
	ИначеЕсли ДлинаНомераТелефона = 11
		И Лев(КаноническийНомер, 1) = "8" Тогда
		КаноническийНомер = "+7" + Сред(КаноническийНомер,2);
	КонецЕсли;
	
	Возврат КаноническийНомер;
	
КонецФункции

#КонецОбласти

// Получает файл с изображением чека из сервиса.
//
// Параметры:
//   Чек       - Структура - описание полей чека, см. НовоеОписаниеЧека().
//
// Возвращаемое значение:
//  ДвоичныеДанные - двоичные данные картинки в форме Base64 строки,
//   или Неопределено - если картинка отсутствует.
//
Функция ПолучитьФайлЧека(АдресЧекаНаСайте) Экспорт
	
	Если НЕ ЗначениеЗаполнено(АдресЧекаНаСайте) Тогда
		Возврат Неопределено;
	КонецЕсли;

	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	
	Попытка
		
		ПараметрыПолучения = Новый Структура("Таймаут, ПутьДляСохранения", 20, ИмяВременногоФайла);
		РезультатФайлаЧека = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(АдресЧекаНаСайте, ПараметрыПолучения);
		Если РезультатФайлаЧека.Статус Тогда
			ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяВременногоФайла);
			УдалитьВременныйФайл(ИмяВременногоФайла);
			
			Возврат ДвоичныеДанныеФайла;
			
		Иначе
			ЧастиОписанияОшибки = Новый Массив;
			ЧастиОписанияОшибки.Добавить(НСтр("ru = 'Не удалось получить файл чека.'"));
			ЧастиОписанияОшибки.Добавить(СтрШаблон(НСтр("ru = 'Описание: %1'"), РезультатФайлаЧека.СообщениеОбОшибке));
			ЧастиОписанияОшибки.Добавить(СтрШаблон(НСтр("ru = 'URL: %1'"), АдресЧекаНаСайте));
			Если РезультатФайлаЧека.Свойство("КодСостояния") Тогда
				ЧастиОписанияОшибки.Добавить(СтрШаблон(НСтр("ru = 'Код ошибки: %1'"), РезультатФайлаЧека.КодСостояния));
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Получение файла чека'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , , СтрСоединить(ЧастиОписанияОшибки));
			
			УдалитьВременныйФайл(ИмяВременногоФайла);
		КонецЕсли;
	Исключение
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Получение файла чека'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		УдалитьВременныйФайл(ИмяВременногоФайла);
		
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УдалитьВременныйФайл(Путь)
	
	Попытка
		Файл = Новый Файл(Путь);
		Если Файл.Существует() Тогда
			УдалитьФайлы(Путь);
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Удаление временного файла чека'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли