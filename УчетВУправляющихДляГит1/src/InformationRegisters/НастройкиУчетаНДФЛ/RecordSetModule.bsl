#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.НастройкиУчетаНДФЛ.УстановкаНастроекПоУмолчанию(Запись, ?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	ХарактерыДеятельностиЕНВД = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиЕНВД();
	ХарактерыДеятельностиУСНПатент = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиУСНПатент();
	
	ПроверятьВидДеятельностиДоходовПоАвансам = Ложь;
	
	Для каждого Запись Из ЭтотОбъект Цикл
		
		ЗначенияКлючаЗаписи = Новый Структура("Период, Организация", Запись.Период, Запись.Организация);
		
		// Если в качестве основного вида деятельности выбран вид деятельности ЕНВД или деятельность на Патенте,
		// проверим на соответствие выбранной системы налогообложения
		
		ХарактерОсновнойДеятельности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ОсновнойВидДеятельности, "ХарактерДеятельности");
		
		ОсновнойВидДеятельностиНаЕНВД    = (ХарактерыДеятельностиЕНВД.Найти(ХарактерОсновнойДеятельности) <> Неопределено);
		ОсновнойВидДеятельностиНаПатенте = (ХарактерыДеятельностиУСНПатент.Найти(ХарактерОсновнойДеятельности) <> Неопределено);
		
		НачалоНалоговогоПериода = НачалоГода(Запись.Период);
		КонецНалоговогоПериода  = КонецГода(Запись.Период);
		
		Если (ОсновнойВидДеятельностиНаЕНВД И Не УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Запись.Организация, НачалоНалоговогоПериода, КонецНалоговогоПериода))
			Или (ОсновнойВидДеятельностиНаПатенте И Не УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Запись.Организация, НачалоНалоговогоПериода, КонецНалоговогоПериода)) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность",
				Метаданные().Ресурсы.ОсновнойВидДеятельности.Синоним,
				,
				,
				НСтр("ru = 'Вид деятельности не применим для выбранной системы налогообложения.'"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				РегистрыСведений.НастройкиУчетаНДФЛ.СоздатьКлючЗаписи(ЗначенияКлючаЗаписи),
				"НастройкиУчетаНДФЛ.ОсновнойВидДеятельности",
				,
				Отказ);
			
		КонецЕсли;
		
		Если Не Запись.ВестиУчетПоВидамДеятельности Тогда
			
			// Проверим возможно ли вести Книгу учета доходов и расходов только по одному виду деятельности
			
			ПричинаНеобходимости = "";
			НеобходимоВестиУчетПоВидамДеятельности = РегистрыСведений.НастройкиУчетаНДФЛ.НеобходимоВестиУчетПоВидамДеятельности(
				Запись.Организация, Запись.Период, Запись.ОсновнойВидДеятельности, ПричинаНеобходимости);
			
			Если НеобходимоВестиУчетПоВидамДеятельности Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность",
					Метаданные().Ресурсы.ВестиУчетПоВидамДеятельности.Синоним,
					,
					,
					ПричинаНеобходимости);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					РегистрыСведений.НастройкиУчетаНДФЛ.СоздатьКлючЗаписи(ЗначенияКлючаЗаписи),
					"НастройкиУчетаНДФЛ.ВестиУчетПоВидамДеятельности",
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Запись.ВестиУчетПоВидамДеятельности И Запись.АвансыВключаютсяВДоходыВПериодеПолучения Тогда
			ПроверятьВидДеятельностиДоходовПоАвансам = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ПроверятьВидДеятельностиДоходовПоАвансам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидДеятельностиДоходовПоАвансам");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли