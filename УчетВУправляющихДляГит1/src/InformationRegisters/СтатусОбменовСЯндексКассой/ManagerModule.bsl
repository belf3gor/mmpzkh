
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает дату последнего успешного обмена для указанной настройки.
//
// Параметры:
//  НастройкаЯндексКассы - СправочникСсылка.НастройкиЯндексКассы - Настройка Яндекс.Кассы для которой необходимо вернуть дату последнего успешного обмена.
//
// Возвращаемое значение:
//   Дата - дата последнего успешного обмена, если успешных обменов не было, возвращает пустую дату.
//
Функция ДатаПоследнегоУспешногоОбмена(НастройкаЯндексКассы) Экспорт 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаЯндексКассы", НастройкаЯндексКассы);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СтатусОбменовСЯндексКассой.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена
	               |ИЗ
	               |	РегистрСведений.СтатусОбменовСЯндексКассой КАК СтатусОбменовСЯндексКассой
	               |ГДЕ
	               |	СтатусОбменовСЯндексКассой.НастройкаЯндексКассы = &НастройкаЯндексКассы";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.ДатаПоследнегоУспешногоОбмена;
	КонецЕсли;
	
	Возврат Дата('00010101');
	
КонецФункции

// Функция возвращает дату последнего успешного обмена для указанной организации.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация для которой необходимо вернуть дату последнего успешного обмена.
//
// Возвращаемое значение:
//  Дата - дата последнего успешного обмена, если успешных обменов не было, возвращает пустую дату.
//
Функция ДатаПоследнегоУспешногоОбменаПоОрганизации(Организация) Экспорт 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СтатусОбменовСЯндексКассой.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена
	               |ИЗ
	               |	РегистрСведений.СтатусОбменовСЯндексКассой КАК СтатусОбменовСЯндексКассой
	               |ГДЕ
	               |	СтатусОбменовСЯндексКассой.НастройкаЯндексКассы.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.ДатаПоследнегоУспешногоОбмена;
	КонецЕсли;
	
	Возврат Дата('00010101');
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обновляет дату последнего успешного обмена с сервисом Яндекс.Касса.
//
// Параметры:
//  НастройкаЯндексКассы - СправочникСсылка.НастройкиЯндексКассы - Настройка Яндекс.Кассы которой надо обновить дату последнего успешного обмена.
//  ДатаУспешногоОбмена - Дата - Дата (состав "Дата и время") последнего успешного обмена.
//
Процедура ОбновитьСтатус(НастройкаЯндексКассы, ДатаУспешногоОбмена) Экспорт 

	МенеджерЗаписи = РегистрыСведений.СтатусОбменовСЯндексКассой.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи(НастройкаЯндексКассы));
	МенеджерЗаписи.ДатаПоследнегоУспешногоОбмена = ДатаУспешногоОбмена;
	МенеджерЗаписи.Записать(Истина);

КонецПроцедуры

// Обновляет значение реквизита Организация для статуса обмена для указанной настройки.
//
// Параметры:
//  НастройкаЯндексКассы  - СправочникСсылка.НастройкиЯндексКассы - Настройка Яндекс.Кассы которой надо обновить дату последнего успешного обмена.
//  Организация - СправочникСсылка.Организации - Значение которое необходимо записать в реквизит организация.
//
Процедура ОбновитьЗначениеРеквизитаОрганизация (НастройкаЯндексКассы, Организация) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СтатусОбменовСЯндексКассой.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи(НастройкаЯндексКассы));
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеЗаписи(НастройкаЯндексКассы)
	
	ДанныеЗаписи = РегистрыСведений.СтатусОбменовСЯндексКассой.СоздатьМенеджерЗаписи();
	ДанныеЗаписи.НастройкаЯндексКассы = НастройкаЯндексКассы;
	ДанныеЗаписи.Прочитать();
	
	Если Не ДанныеЗаписи.Выбран() Тогда 
		
		ЗначенияКлюча = Новый Структура();
		ЗначенияКлюча.Вставить("НастройкаЯндексКассы", НастройкаЯндексКассы);
		ЗначенияКлюча.Вставить("Организация", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			НастройкаЯндексКассы, "Организация"));
		ЗначенияКлюча.Вставить("ДатаПоследнегоУспешногоОбмена", ТекущаяДатаСеанса());
		
		ДанныеЗаписи = РегистрыСведений.СтатусОбменовСЯндексКассой.СоздатьКлючЗаписи(ЗначенияКлюча);
		
	КонецЕсли;
	
	Возврат ДанныеЗаписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли