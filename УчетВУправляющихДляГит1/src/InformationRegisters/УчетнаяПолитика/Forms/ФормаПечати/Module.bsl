#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Параметры.ИмяМакета) Тогда
		ТекстСообщения = НСтр("ru = 'Не задано имя макета для печатной формы учетной политики.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Параметры.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Не задана организация для печатной формы учетной политики.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Параметры.Период) Тогда
		ТекстСообщения = НСтр("ru = 'Не задан период для печатной формы учетной политики.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ПараметрыУчетнойПолитики = Новый Структура("Организация,Период", Параметры.Организация, Параметры.Период);
	ДанныеПечатнойФормы = РегистрыСведений.УчетнаяПолитика.ДанныеПечатнойФормыУчетнойПолитики(
		Параметры.ИмяМакета, ПараметрыУчетнойПолитики);
	
	Заголовок = ДанныеПечатнойФормы.ЗаголовокФормы;
	ИмяФайла = СтрЗаменить(ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Заголовок), ".", "_");
	
	ФорматированныйДокумент = ДанныеПечатнойФормы.Документ;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьФорматированныйДокумент(Команда)
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("АдресХранилища", ПолучитьHTMLИзФорматированногоДокумента());
	ДопПараметры.Вставить("ИмяФайла",       ИмяФайла + ".htm");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьРасширениеРаботыСФайламиЗавершение", ЭтотОбъект, ДопПараметры);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодключитьРасширениеРаботыСФайламиЗавершение(РасширениеПодключено, ДопПараметры) Экспорт
	
	Если РасширениеПодключено Тогда
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.ПолноеИмяФайла = ДопПараметры.ИмяФайла;
		ДиалогВыбораФайла.Фильтр = НСтр("ru='Документ HTML (*.htm)|*.htm'");
		ДопПараметрыПродолжение  = Новый Структура("АдресХранилища", ДопПараметры.АдресХранилища);
		ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьФорматированныйДокументПродолжение", ЭтотОбъект, 
			ДопПараметрыПродолжение);
		ДиалогВыбораФайла.Показать(ОписаниеОповещения);
		
	Иначе
		
		ПолучитьФайл(ДопПараметры.АдресХранилища, ДопПараметры.ИмяФайла, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФорматированныйДокументПродолжение(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСохраняемогоФайла = ВыбранныеФайлы[0];
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьФорматированныйДокументЗавершение", ЭтотОбъект);
	ПолучаемыеФайлы    = Новый Массив;
	ОписаниеФайла      = Новый ОписаниеПередаваемогоФайла(ИмяСохраняемогоФайла, ДопПараметры.АдресХранилища);
	ПолучаемыеФайлы.Добавить(ОписаниеФайла);
	НачатьПолучениеФайлов(ОписаниеОповещения, ПолучаемыеФайлы, , Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФорматированныйДокументЗавершение(ПолученныеФайлы, ДопПараметры) Экспорт
	
	Если ПолученныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСохраненногоФайла = ПолученныеФайлы[0].Имя;
	ТекстСообщения       = НСтр("ru='Файл сохранен:'");
	ПоказатьОповещениеПользователя(ТекстСообщения, , ИмяСохраненногоФайла);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьHTMLИзФорматированногоДокумента()
	
	ТекстHTML = "";
	Вложения  = Новый Структура();
	ФорматированныйДокумент.ПолучитьHTML(ТекстHTML, Вложения);
	
	Для Каждого Картинка Из Вложения Цикл
		КартинкаТекстом = "data:image/" + Картинка.Значение.Формат() + ";base64," + Символы.ПС 
			+ Base64Строка(Картинка.Значение.ПолучитьДвоичныеДанные());
		ТекстHTML = СтрЗаменить(ТекстHTML, Картинка.Ключ, КартинкаТекстом);
	КонецЦикла;

	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.УстановитьТекст(ТекстHTML);
	ИмяФайлаHTML      = ПолучитьИмяВременногоФайла();
	ТекстовыйДокумент.Записать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайлаHTML);
	АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	
	УдалитьФайлы(ИмяФайлаHTML);
	
	Возврат АдресХранилища;
	
КонецФункции

#КонецОбласти

