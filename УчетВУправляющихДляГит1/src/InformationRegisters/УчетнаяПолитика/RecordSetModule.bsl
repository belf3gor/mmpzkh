#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено Или ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.УчетнаяПолитика.УстановкаНастроекПоУмолчанию(Запись, ?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Для Каждого Строка Из ЭтотОбъект Цикл
			
		Если ЗначениеЗаполнено(Строка.ВариантУчетаОтложенногоНалога) Тогда
			
			// Флаг ПоддержкаПБУ18 следует устанавливать, если используется любой из вариантов применения ПБУ 18.
			// В этом поле хранится значение функциональной опции, в состав которой входят поля с суммами разниц,
			// используемые повсеместно в объектах конфигурации.
			// Поскольку поле ПоддержкаПБУ18 служебное и однозначно определяется значением поля ВариантУчетаОтложенногоНалога,
			// то отправитель данных может про него не знать и синхронизацию следует выполнить до проверки на ОбменДанными.Загрузка.
			ПрименяетсяПБУ18 = (Строка.ВариантУчетаОтложенногоНалога <> Перечисления.ВариантыУчетаОтложенногоНалога.Нет);
			
			Если Строка.ПоддержкаПБУ18 <> ПрименяетсяПБУ18 Тогда
				Строка.ПоддержкаПБУ18 = ПрименяетсяПБУ18;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОтключаемыеРесурсы = Новый Массив;
	
	// Эти ресурсы используются только в версии КОРП
	Если Не УчетнаяПолитика.ПоддерживаетсяУчетПоЭлементамЗатрат() Тогда
		ОтключаемыеРесурсы.Добавить("УчитыватьРасходыПоСтатьямЗатрат");
		ОтключаемыеРесурсы.Добавить("УчитыватьРасходыПоЭлементамЗатрат");
	КонецЕсли;
	Если Не УчетнаяПолитика.ПоддерживаетсяСписаниеРасходовОбслуживающихПроизводств() Тогда
		ОтключаемыеРесурсы.Добавить("РасходыОбслуживающихПроизводствВключатьВПрочиеРасходы");
	КонецЕсли;

	ОтключитьРесурсы(ОтключаемыеРесурсы);

	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

Процедура ОтключитьРесурсы(ОтключаемыеРесурсы)
	
	Если Не ЗначениеЗаполнено(ОтключаемыеРесурсы) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Для Каждого Ресурс Из ОтключаемыеРесурсы Цикл 
			Если Запись[Ресурс] Тогда
				Запись[Ресурс] = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 
	
#КонецОбласти

#КонецЕсли