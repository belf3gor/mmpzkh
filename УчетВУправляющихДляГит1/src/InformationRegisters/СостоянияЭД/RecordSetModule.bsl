#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;
 
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если НЕ ЭлектронноеВзаимодействиеСлужебный.КонфигурацияИспользуетНесколькоЯзыков() Тогда
			ПредставлениеПрогрессаПодписания = Неопределено;
			ПредставлениеСтатуса = Строка(Запись.СостояниеВерсииЭД);
			Если Запись.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.НаПодписи 
					И ЗначениеЗаполнено(Запись.ЭлектронныйДокумент) Тогда
				ФайлЭД = ОбменСКонтрагентамиСлужебный.ПрисоединенныйФайл(Запись.ЭлектронныйДокумент);
				
				Если ЗначениеЗаполнено(ФайлЭД) Тогда
					ПредставлениеПрогрессаПодписания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлЭД, 
						"ПредставлениеПрогрессаПодписания");
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПредставлениеПрогрессаПодписания) Тогда
				Запись.ПредставлениеСостояния = СтрШаблон("%1 %2", ПредставлениеСтатуса, ПредставлениеПрогрессаПодписания);
			Иначе
				Запись.ПредставлениеСостояния = ПредставлениеСтатуса;
			КонецЕсли;
		Иначе 
			Запись.ПредставлениеСостояния = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
	
#КонецЕсли
