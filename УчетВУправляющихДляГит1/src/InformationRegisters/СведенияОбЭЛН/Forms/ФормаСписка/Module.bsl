#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьСвойствоСпискаПоПараметруФормы("РежимВыбора");
	УстановитьСвойствоСпискаПоПараметруФормы("ВыборГруппИЭлементов");
	УстановитьСвойствоСпискаПоПараметруФормы("МножественныйВыбор");
	УстановитьСвойствоСпискаПоПараметруФормы("ТекущаяСтрока");
	
	Если Элементы.Список.МножественныйВыбор Тогда
		Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	Иначе
		Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
	КонецЕсли;
	
	ВыбранныеНомераЭЛН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ВыбранныеНомераЭЛН");
	Если ТипЗнч(ВыбранныеНомераЭЛН) <> Тип("Массив") Тогда
		ВыбранныеНомераЭЛН = Новый Массив;
	КонецЕсли;
	
	ЭлементУО = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(Список, "");
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ЭлементУО, "ЦветТекста", ЦветаСтиля.ТекстПодобранногоЗначенияЦвет);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "НомерЛисткаНетрудоспособности", ВидСравненияКомпоновкиДанных.ВСписке, ВыбранныеНомераЭЛН);
	ИдентификаторСпискаПодобранных = Список.УсловноеОформление.ПолучитьИдентификаторПоОбъекту(ЭлементУО);
	
	ЭлементУО = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(Список, "ДатаОтправки, Состояние, КомментарийКСостоянию");
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ЭлементУО, "ЦветТекста", ЦветаСтиля.РезультатУспехЦвет);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "Состояние", "=", Перечисления.СостоянияЭЛН.ПринятФСС);
	
	ЭлементУО = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(Список, "ДатаОтправки, Состояние, КомментарийКСостоянию");
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ЭлементУО, "ЦветТекста", ЦветаСтиля.РезультатПроблемаЦвет);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "Состояние", "=", Перечисления.СостоянияЭЛН.НеПринятФСС);
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ИдентификаторСтроки, Поле, СтандартнаяОбработка)
	СписокВыборЗначения(Элемент, ИдентификаторСтроки, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, ИдентификаторСтроки, СтандартнаяОбработка)
	Если Элементы.Список.РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		СтрокаТаблицы = Элементы.Список.ДанныеСтроки(ИдентификаторСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			РезультатВыбора = Новый Структура("НомерЛисткаНетрудоспособности, ЛистокНетрудоспособности, Отказ");
			ЗаполнитьЗначенияСвойств(РезультатВыбора, СтрокаТаблицы);
			РезультатВыбора.Отказ = Ложь;
			ОповеститьОВыборе(РезультатВыбора);
			Если Не РезультатВыбора.Отказ Тогда
				ЭлементУО = Список.УсловноеОформление.ПолучитьОбъектПоИдентификатору(ИдентификаторСпискаПодобранных);
				ЭлементУО.Отбор.Элементы[0].ПравоеЗначение.Добавить(СтрокаТаблицы.НомерЛисткаНетрудоспособности);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьЛистокНетрудоспособности(Команда)
	ПоказатьЗначениеКолонки("ЛистокНетрудоспособности");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодготовленныйКОтправкеРеестр(Команда)
	ПоказатьЗначениеКолонки("ПодготовленныйКОтправкеРеестр");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтправленныйРеестр(Команда)
	ПоказатьЗначениеКолонки("ОтправленныйРеестр");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПринятыйРеестр(Команда)
	ПоказатьЗначениеКолонки("ПринятыйРеестр");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСвойствоСпискаПоПараметруФормы(Ключ)
	Если Параметры.Свойство(Ключ) И ЗначениеЗаполнено(Параметры[Ключ]) Тогда
		Элементы.Список[Ключ] = Параметры[Ключ];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗначениеКолонки(ИмяКолонки)
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Ссылка = ТекущаяСтрока[ИмяКолонки];
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПоказатьЗначение(, Ссылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
