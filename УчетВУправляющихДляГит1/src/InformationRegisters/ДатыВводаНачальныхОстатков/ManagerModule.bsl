#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает дату ввода начальных остатков
//
// Параметры:
// Организация - СправочникСсылка.Организация
//
// Возвращаемое значение:
// Дата или Неопределено, если дата не задана
//
Функция ДатаВводаНачальныхОстатков(Организация) Экспорт
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(ложь);
	
	Если ЗначениеЗаполнено(Организация) И ДоступныеОрганизации.Найти(Организация) = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДоступныеОрганизации", ДоступныеОрганизации);
	
	Если ЗначениеЗаполнено(Организация) Тогда 
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДатыВводаНачальныхОстатков.ДатаВводаНачальныхОстатков
		|ИЗ
		|	РегистрСведений.ДатыВводаНачальныхОстатков КАК ДатыВводаНачальныхОстатков
		|ГДЕ
		|	ДатыВводаНачальныхОстатков.Организация = &Организация";
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МИНИМУМ(ДатыВводаНачальныхОстатков.ДатаВводаНачальныхОстатков), НЕОПРЕДЕЛЕНО) КАК ДатаВводаНачальныхОстатков
		|ИЗ
		|	РегистрСведений.ДатыВводаНачальныхОстатков КАК ДатыВводаНачальныхОстатков
		|ГДЕ
		|	ДатыВводаНачальныхОстатков.Организация В(&ДоступныеОрганизации)";
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДатаВводаНачальныхОстатков = Выборка.ДатаВводаНачальныхОстатков;
	
	Если Не ЗначениеЗаполнено(ДатаВводаНачальныхОстатков) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Убедимся, что начальные остатки фактически введены, так как дата могла быть записана в регистр,
	// если пользователь случайно зашел в помощник ввода остатков
	
	Если ЗначениеЗаполнено(Организация) Тогда 
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВводНачальныхОстатков.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
		|ГДЕ
		|	НЕ ВводНачальныхОстатков.ПометкаУдаления
		|	И ВводНачальныхОстатков.Организация = &Организация";
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВводНачальныхОстатков.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
		|ГДЕ
		|	НЕ ВводНачальныхОстатков.ПометкаУдаления
		|	И ВводНачальныхОстатков.Организация В(&ДоступныеОрганизации)";
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ЕстьВводНачальныхОстатков = Не Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЕстьВводНачальныхОстатков Тогда
		Возврат ДатаВводаНачальныхОстатков;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли