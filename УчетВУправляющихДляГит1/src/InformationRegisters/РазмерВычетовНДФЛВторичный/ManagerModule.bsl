#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьВторичныеДанныеРазмерВычетовНДФЛВторичный(ПараметрыОбновления = Неопределено) Экспорт
	ЗаполнитьВторичныеДанные();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
КонецПроцедуры

Процедура ЗаполнитьВторичныеДанные(КодВычета = Неопределено) Экспорт
	
	Набор = РегистрыСведений.РазмерВычетовНДФЛВторичный.СоздатьНаборЗаписей();
	Набор.ОбменДанными.Загрузка = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерВычетовНДФЛ.Период КАК Период,
	|	РазмерВычетовНДФЛ.КодВычета КАК КодВычета,
	|	РазмерВычетовНДФЛ.Размер КАК Размер,
	|	РазмерВычетовНДФЛ.ОграничениеПоДоходам КАК ОграничениеПоДоходам
	|ИЗ
	|	РегистрСведений.РазмерВычетовНДФЛ КАК РазмерВычетовНДФЛ
	|ГДЕ
	|	РазмерВычетовНДФЛ.КодВычета = &КодВычета
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодВычета,
	|	Период";
	
	Если КодВычета <> Неопределено Тогда
		Запрос.УстановитьПараметр("КодВычета", КодВычета);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РазмерВычетовНДФЛ.КодВычета = &КодВычета", "(ИСТИНА)");
		Набор.Записать();
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Если КодВычета <> Неопределено Тогда
			Набор.Отбор.КодВычета.Установить(КодВычета);
			Набор.Записать();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Пока Выборка.СледующийПоЗначениюПоля("КодВычета") Цикл
		
		Набор.Отбор.КодВычета.Установить(Выборка.КодВычета);
		
		Пока Выборка.Следующий() Цикл
			Если Набор.Количество() > 0 Тогда
				Набор[Набор.Количество() - 1].ДатаОкончания = Выборка.Период - 1;
			КонецЕсли;
			
			Запись = Набор.Добавить();
			Запись.ДатаНачала = Выборка.Период;
			Запись.КодВычета = Выборка.КодВычета;
			Запись.Размер = Выборка.Размер;
			Запись.ОграничениеПоДоходам = Выборка.ОграничениеПоДоходам;
		КонецЦикла;
		
		Набор[Набор.Количество() - 1].ДатаОкончания = ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата();
		Набор.Записать();
		Набор.Очистить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли