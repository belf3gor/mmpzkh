
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаКлиенте
// Обработчик "ПриОткрытии" формы.
//
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Обработчик события "ПриИзменении" поля "Статус".
//
Процедура СтатусПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
// Выполняет открытие файла обмена программой по умолчанию.
//
Процедура КомандаОткрытьФайлОбмена(Команда)
	
	// Получаем значение файла обмена.
	СтруктураСведенийОФайле = ПолучитьСтруктуруСведенийОФайлеОбмена();
	СохраненныйФайл         = СтруктураСведенийОФайле.СохраненныйФайл;
	РасширениеФайла         = СтруктураСведенийОФайле.РасширениеФайла;
	
	Если СохраненныйФайл = Неопределено Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не обнаружен сохраненный файл обмена.");
		
	Иначе
		
		Если РасширениеФайла = "xml" Тогда// xml.
			
			ДополнительныеПараметры = Новый Структура("РасширениеФайла, СохраненныйФайл", ".xml", СохраненныйФайл);
			
			НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("НачатьПолучениеКаталогаВременныхФайловЗавершение", ЭтаФорма, ДополнительныеПараметры));
			
		Иначе
			
			// Если не xml, то сохраняем файл на диск.
			СохранитьФайлОбмена(СохраненныйФайл);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеКаталогаВременныхФайловЗавершение(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	УникальнаяСтрока = Новый УникальныйИдентификатор;
	
	ИмяФайла = ИмяКаталогаВременныхФайлов + УникальнаяСтрока + ДополнительныеПараметры.РасширениеФайла;
	
	Попытка
		ОписаниеОповещения = Новый ОписаниеОповещения("НачатьЗаписьЗавершение", ЭтаФорма, ИмяФайла);
		ДополнительныеПараметры.СохраненныйФайл.НачатьЗапись(ОписаниеОповещения, ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не удалось открыть файл обмена: " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапись".
Процедура НачатьЗаписьЗавершение(ИмяФайла) Экспорт
	
	Попытка
		НачатьЗапускПриложения(Новый ОписаниеОповещения("НачатьЗапускПриложенияЗавершение", ЭтаФорма), ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(СтрШаблон("Не удалось открыть файл обмена: %1", ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапускПриложения".
Процедура НачатьЗапускПриложенияЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия кнопки "Сохранить файл обмена".
//
Процедура КомандаСохранитьФайлОбмена(Команда)
	
	// Получаем значение файла обмена.
	СохраненныйФайл = ПолучитьСохраненныйФайлОбмена();
	
	Если СохраненныйФайл = Неопределено Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не обнаружен сохраненный файл обмена.");
	Иначе
		СохранитьФайлОбмена(СохраненныйФайл);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия кнопки "Открыть штатными средствами".
//
Процедура КомандаПросмотретьСодержимоеФайлаОбмена(Команда)
	
	СтруктураСведенийОФайле = ПолучитьСтруктуруСведенийОФайлеОбмена();
	СохраненныйФайл         = СтруктураСведенийОФайле.СохраненныйФайл;
	РасширениеФайла         = СокрЛП(СтруктураСведенийОФайле.РасширениеФайла);
	
	Если НЕ СохраненныйФайл = Неопределено Тогда
		
		ТекущееРасширениеФайла = ?(Лев(РасширениеФайла, 1) = ".", РасширениеФайла, "." + РасширениеФайла);
		
		ДополнительныеПараметры = Новый Структура("РасширениеФайла, СохраненныйФайл", ТекущееРасширениеФайла, СохраненныйФайл);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандаПросмотретьСодержимоеФайлаОбменаФрагмент", ЭтаФорма, ДополнительныеПараметры);
		
		НачатьПолучениеКаталогаВременныхФайлов(ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьПолучениеКаталогаВременныхФайлов".
Процедура КомандаПросмотретьСодержимоеФайлаОбменаФрагмент(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	УникальнаяСтрока = Новый УникальныйИдентификатор;
	
	ИмяФайла = ИмяКаталогаВременныхФайлов + УникальнаяСтрока + ДополнительныеПараметры.РасширениеФайла;
	
	Попытка
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандаПросмотретьСодержимоеФайлаОбменаЗавершение", ЭтаФорма, ИмяФайла);
		ДополнительныеПараметры.СохраненныйФайл.НачатьЗапись(ОписаниеОповещения, ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(СтрШаблон("Не удалось открыть файл обмена: %1", ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапись".
Процедура КомандаПросмотретьСодержимоеФайлаОбменаЗавершение(ИмяФайла) Экспорт
	
	Попытка
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьУдалениеФайлаПослеЗакрытияФормыПросмотра", ЭтаФорма, ИмяФайла);
		
		ОткрытьФорму("Обработка.УПЖКХ_ПросмотрXML.Форма.Форма", Новый Структура("ИмяФайла", ИмяФайла), ЭтаФорма,,,,ОписаниеОповещения);
		
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(СтрШаблон("Не удалось открыть файл обмена: %1", ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьУдалениеФайлаПослеЗакрытияФормыПросмотра(РезультатЗакрытия, ИмяФайла) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьУдалениеФайлаПослеЗакрытияФормыПросмотраЗавершение", ЭтаФорма);
	
	НачатьУдалениеФайлов(ОписаниеОповещения, ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьУдалениеФайлаПослеЗакрытияФормыПросмотраЗавершение(ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиенте
// Управление видимостью элементов формы.
//
Процедура УправлениеФормой()
	
	ЕстьФайлОбмена = Запись.ЕстьФайлОбмена;
	
	Элементы.Ошибка.Видимость                                   = (НЕ Запись.Статус);
	Элементы.КнопкаОткрытьФайлОбмена.Доступность                = ЕстьФайлОбмена;
	Элементы.КнопкаСохранитьФайлОбмена.Доступность              = ЕстьФайлОбмена;
	Элементы.КнопкаПросмотретьСодержимоеФайлаОбмена.Доступность = ЕстьФайлОбмена;
	
	// Меняем наименование кнопки, если файл - zip архив.
	Если Запись.ЕстьФайлОбмена Тогда
		
		// Ищем запись с файлом в регистре сведений "Журнал обмена".
		СтруктураСведенийОФайле = ПолучитьСтруктуруСведенийОФайлеОбмена();
		
		СохраненныйФайл = СтруктураСведенийОФайле.СохраненныйФайл;
		РасширениеФайла = СтруктураСведенийОФайле.РасширениеФайла;
		
		Если РасширениеФайла = "xml" Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.КнопкаСохранитьФайлОбмена.Видимость = Ложь;
		
		Элементы.КнопкаОткрытьФайлОбмена.Заголовок = "Сохранить файл обмена";
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеФормой()

&НаКлиенте
// Получает сохраненный файл обмена.
//
Функция ПолучитьСохраненныйФайлОбмена()
	
	СтруктураСведенийОФайле = ПолучитьСтруктуруСведенийОФайлеОбмена();
	СохраненныйФайл         = СтруктураСведенийОФайле.СохраненныйФайл;
	
	Возврат СохраненныйФайл;
	
КонецФункции

&НаСервере
// Получает сохраненный файл обмена.
//
Функция ПолучитьСтруктуруСведенийОФайлеОбмена()
	
	// Ищем запись с файлом в регистре сведений "Журнал обмена".
	СтруктураСведенийОФайле = РегистрыСведений.УПЖКХ_ЖурналОбменаСМобильнымПриложением.ПолучитьСведенияОФайлеОбмена(Запись);
	
	Возврат СтруктураСведенийОФайле;
	
КонецФункции

&НаКлиенте
// Выполняет сохранение файла обмена.
//
Процедура СохранитьФайлОбмена(СохраненныйФайл)
	
	ИмяФайла = "saveddata_" + Формат(Запись.ДатаОбмена, "ДФ=дд_ММ_гггг") + "." + РасширениеФайла;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = ИмяФайла;
	Диалог.Фильтр = ?(РасширениеФайла = "", "", "*" + РасширениеФайла + "|*" + РасширениеФайла + "|") + "*.*|*.*";
	Диалог.Заголовок = НСтр("ru = 'Сохранение файла'");
	
	Обработчик = Новый ОписаниеОповещения("СохранитьФайлВыборФайла", ЭтотОбъект, СохраненныйФайл);
	Диалог.Показать(Обработчик);
	
КонецПроцедуры

&НаКлиенте
// Обработчик результата работы процедуры ВыгрузитьВФайл.
//
Процедура СохранитьФайлВыборФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		ПолноеИмяФайла = ВыбранныеФайлы[0];
		
		ДополнительныеПараметры.Записать(ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти