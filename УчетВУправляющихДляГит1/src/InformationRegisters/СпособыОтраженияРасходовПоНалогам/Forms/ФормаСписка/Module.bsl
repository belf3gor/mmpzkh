#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидСравненияОтбора		= ВидСравненияКомпоновкиДанных.ВСписке;
	РежимОтображенияОтбора	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	// добавим пустую организацию в отбор по организации
	
	ОтборОрганизация = Справочники.Организации.ПустаяСсылка();
	Если Параметры.Отбор.Свойство("Организация", ОтборОрганизация)
		И ТипЗнч(ОтборОрганизация) = Тип("СправочникСсылка.Организации") Тогда
	
		ОтборПоОрганизации = Новый Массив;
		ОтборПоОрганизации.Добавить(ОтборОрганизация);
		ОтборПоОрганизации.Добавить(Справочники.Организации.ПустаяСсылка());
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Организация", ОтборПоОрганизации, ВидСравненияОтбора, , Истина, РежимОтображенияОтбора);
		
	КонецЕсли;
		
	Если Параметры.Свойство("ВидНалога")
		И ЗначениеЗаполнено(Параметры.ВидНалога) Тогда
			
		ВыборНалогаДоступен = Ложь;
		
		ЭтаФорма.АвтоЗаголовок = Ложь;
		Если Параметры.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
			ЭтаФорма.Заголовок = НСтр("ru='Налог на имущество: способы отражения расходов'");
		ИначеЕсли Параметры.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
			ЭтаФорма.Заголовок = НСтр("ru='Транспортный налог: способы отражения расходов'");
		ИначеЕсли Параметры.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
			ЭтаФорма.Заголовок = НСтр("ru='Земельный налог: способы отражения расходов'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ВидНалога",
			Параметры.ВидНалога,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина,
			РежимОтображенияОтбора);
		
	Иначе
			
		ВыборНалогаДоступен = Истина;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборНалогаДоступен", ВыборНалогаДоступен);
	ПараметрыФормы.Вставить("Ключ", ВыбраннаяСтрока);
	
	ОткрытьФорму("РегистрСведений.СпособыОтраженияРасходовПоНалогам.ФормаЗаписи", ПараметрыФормы, ЭтаФорма); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	
	// Организация
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Организация");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Организация", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Для всех'"));
	
	
	// ОсновноеСредство
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОсновноеСредство");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ОсновноеСредство", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Для всех'"));
	
	
	// ВидНалога
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидНалога");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВыборНалогаДоступен", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

#КонецОбласти
