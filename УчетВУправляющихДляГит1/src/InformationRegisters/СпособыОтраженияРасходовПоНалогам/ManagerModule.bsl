#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	ИЛИ Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьЗаписиОтражениеНалоговПоУмолчанию() Экспорт

	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда // В подчиненных узлах РИБ не выполняется
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаличиеЗаписей = Новый Структура;
	НаличиеЗаписей.Вставить("ЕстьНалогНаИмущество", Ложь);
	НаличиеЗаписей.Вставить("ЕстьТранспортныйНалог", Ложь);
	НаличиеЗаписей.Вставить("ЕстьЗемельныйНалог", Ложь);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.НалогНаИмущество), ЛОЖЬ) КАК ЕстьНалогНаИмущество,
	|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ТранспортныйНалог), ЛОЖЬ) КАК ЕстьТранспортныйНалог,
	|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ЗемельныйНалог), ЛОЖЬ) КАК ЕстьЗемельныйНалог
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА СпособыОтраженияРасходовПоНалогам.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК НалогНаИмущество,
	|		ВЫБОР
	|			КОГДА СпособыОтраженияРасходовПоНалогам.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ТранспортныйНалог,
	|		ВЫБОР
	|			КОГДА СпособыОтраженияРасходовПоНалогам.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ЗемельныйНалог
	|	ИЗ
	|		РегистрСведений.СпособыОтраженияРасходовПоНалогам КАК СпособыОтраженияРасходовПоНалогам) КАК ВложенныйЗапрос"
	;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НаличиеЗаписей, Выборка);
	КонецЕсли;
	
	Если НЕ (НаличиеЗаписей.ЕстьНалогНаИмущество
			И НаличиеЗаписей.ЕстьТранспортныйНалог
			И НаличиеЗаписей.ЕстьЗемельныйНалог) Тогда
	
		// надо создавать записи
		Набор = РегистрыСведений.СпособыОтраженияРасходовПоНалогам.СоздатьНаборЗаписей();
		
		ПериодЗаписей = '20130101';
		СчетЗатрат   = ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы;
		СтатьяЗатрат = СтатьяЗатратПоНалогам();
		
		Если НЕ НаличиеЗаписей.ЕстьНалогНаИмущество Тогда
			Запись = Набор.Добавить();
			Запись.Период = ПериодЗаписей;
			Запись.ВидНалога  = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество;
			Запись.СчетЗатрат = СчетЗатрат;
			Запись.Субконто1  = СтатьяЗатрат;
			Запись.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
		КонецЕсли;
		
		Если НЕ НаличиеЗаписей.ЕстьТранспортныйНалог Тогда
			Запись = Набор.Добавить();
			Запись.Период = ПериодЗаписей;
			Запись.ВидНалога  = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог;
			Запись.СчетЗатрат = СчетЗатрат;
			Запись.Субконто1  = СтатьяЗатрат;
			Запись.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
		КонецЕсли;
		
		Если НЕ НаличиеЗаписей.ЕстьЗемельныйНалог Тогда
			Запись = Набор.Добавить();
			Запись.Период = ПериодЗаписей;
			Запись.ВидНалога  = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог;
			Запись.СчетЗатрат = СчетЗатрат;
			Запись.Субконто1  = СтатьяЗатрат;
			Запись.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
		КонецЕсли;
		
		Попытка
			Набор.Записать(Ложь);
		Исключение
			ШаблонСообщения	= НСтр("ru = 'Не удалось создать записи по способам отражения расходов по имущественным налогам
                                    |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.СпособыОтраженияРасходовПоНалогам,, 
				ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтатьяЗатратПоНалогам()

	Перем Статья;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтатьиЗатрат.Ссылка КАК СтатьяЗатрат
	|ИЗ
	|	Справочник.СтатьиЗатрат КАК СтатьиЗатрат
	|ГДЕ
	|	НЕ СтатьиЗатрат.Предопределенный
	|	И СтатьиЗатрат.ИмяПредопределенныхДанных = """"
	|	И НЕ СтатьиЗатрат.ПометкаУдаления
	|	И СтатьиЗатрат.ВидРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.НалогиИСборы)
	|	И СтатьиЗатрат.Наименование = ""Имущественные налоги"""
	;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		// найдена статья
		Статья = Выборка.СтатьяЗатрат;
	Иначе
		// статья затрат не найдена, создаем новую
		НоваяСтатья = Справочники.СтатьиЗатрат.СоздатьЭлемент();
		
		НоваяСтатья.Наименование = "Имущественные налоги";
		НоваяСтатья.ВидДеятельностиДляНалоговогоУчетаЗатрат = 
			Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
		НоваяСтатья.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НалогиИСборы;
		
		Попытка
			НоваяСтатья.Записать();
			Статья = НоваяСтатья.Ссылка;
		Исключение
			ТекстСообщения = НСтр("ru = 'Не создана статья затрат для отражения расходов по имущественным налогам'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Статья;
	
КонецФункции

#КонецОбласти

#КонецЕсли
