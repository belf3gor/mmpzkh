#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
// Заполнение записей регистра
//
Процедура ЗаполнитьКорреспонденцииСчетов() Экспорт

	#Если Клиент Тогда
	Состояние(НСтр("ru = 'Выполняется заполнение корреспонденций счетов'"));
#КонецЕсли

	ЗаписиКорреспонденцииСчетов = РегистрыСведений.КорреспонденцииСчетов.СоздатьНаборЗаписей();
	
	МакетыКорреспонденцииСчетов = Метаданные.РегистрыСведений.КорреспонденцииСчетов.Макеты;
	
	Для Каждого МакетКорреспонденцииСчетов Из МакетыКорреспонденцииСчетов Цикл
		
		Если МакетКорреспонденцииСчетов.ТипМакета <> Метаданные.СвойстваОбъектов.ТипМакета.ТекстовыйДокумент Тогда
			Продолжить;
		КонецЕсли;
		
	Макет = РегистрыСведений.КорреспонденцииСчетов.ПолучитьМакет(МакетКорреспонденцииСчетов.Имя).ПолучитьТекст();	
	КорреспонденцииСчетовТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет).Данные;
		
	Для Каждого СтрокаТаблицы Из КорреспонденцииСчетовТаблица Цикл
		
		ЗаписьКорреспонденцииСчетов = ЗаписиКорреспонденцииСчетов.Добавить();
		
		ЗаписьКорреспонденцииСчетов.Идентификатор = Строка (Новый УникальныйИдентификатор);
		ЗаписьКорреспонденцииСчетов.СчетДт = СтрокаТаблицы.Дт;
		ЗаписьКорреспонденцииСчетов.СчетКт = СтрокаТаблицы.Кт;
		ЗаписьКорреспонденцииСчетов.Содержание 			= СтрокаТаблицы.Содержание;
		ЗаписьКорреспонденцииСчетов.ИмяДокумента 		= СтрокаТаблицы.ИмяДокумента;
		ЗаписьКорреспонденцииСчетов.ТипДокумента 		= СтрокаТаблицы.ТипДокумента;
		ЗаписьКорреспонденцииСчетов.ЗакладкаДокумента 	= СтрокаТаблицы.ЗакладкаДокумента;
		ЗаписьКорреспонденцииСчетов.Раздел				= СтрокаТаблицы.Раздел;
		ЗаписьКорреспонденцииСчетов.Подраздел			= СтрокаТаблицы.Подраздел;
		ЗаписьКорреспонденцииСчетов.Журнал				= СтрокаТаблицы.Журнал;
		ЗаписьКорреспонденцииСчетов.СсылкаНаИТС 		= СтрокаТаблицы.СсылкаНаИТС;
		Если ЗначениеЗаполнено(СтрокаТаблицы.ВидОперацииДокумента) Тогда
			ЗаписьКорреспонденцииСчетов.ВидОперацииДокумента = ПолучитьВидОперацииДокументаПоСинониму(СтрокаТаблицы.ИмяДокумента, СтрокаТаблицы.ВидОперацииДокумента);
		КонецЕсли;
		
	КонецЦикла;
КонецЦикла;
	ЗаписиКорреспонденцииСчетов.Записать();
	
	#Если Клиент Тогда
		Состояние(НСтр("ru = 'Заполнение корреспонденций счетов выполнено'"));
	#КонецЕсли
	
КонецПроцедуры

Функция ПолучитьВидОперацииДокументаПоСинониму(ИмяДокумента, СинонимВидаОперации)
	
	Результат = Неопределено;
	
	ИмяРеквизита = ?(ИмяДокумента = "ВводНачальныхОстатков", "РазделУчета", "ВидОперации");
	
	Перечисление = Метаданные.Документы[ИмяДокумента].Реквизиты[ИмяРеквизита].Тип.ПривестиЗначение().Метаданные();
	
	ИмяЗначенияПеречисления = "";
	Для каждого ЗначениеПеречисления Из Перечисление.ЗначенияПеречисления Цикл
		Если ЗначениеПеречисления.Синоним = СинонимВидаОперации Тогда
			Результат = Перечисления[Перечисление.Имя][ЗначениеПеречисления.Имя];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли