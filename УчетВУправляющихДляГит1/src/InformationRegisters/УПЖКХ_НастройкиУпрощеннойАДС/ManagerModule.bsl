/////////////////////////////////////////////////////////////////////////
// ПОЛУЧЕНИЕ НАСТРОЕК УПРОЩЕННОЙ АДС

#Область ПолучитьНастройки

// Функция формирует таблицу с данными по организации и настройками АДС для гиперссылки настройки.
// 
// Возвращаемое значение:
// ТаблицаЗначений  - таблица с настройками для гиперссылки.
//
Функция ПолучитьДанныеОрганизацийИАДСДляПредставленияНастройки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	Организации.Наименование КАК Наименование,
	|	Организации.ИНН КАК ИНН,
	|	Организации.КПП КАК КПП,
	|	ВЫБОР
	|		КОГДА УПЖКХ_НастройкиУпрощеннойАДС.ПриниматьСообщенияОтЖильцовНаЭлПочту ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВведеныНастройки,
	|	ЕСТЬNULL(УПЖКХ_НастройкиУпрощеннойАДС.ПриниматьСообщенияОтЖильцовНаЭлПочту, ЛОЖЬ) КАК ПриниматьСообщенияОтЖильцовНаЭлПочту,
	|	ЕСТЬNULL(УПЖКХ_НастройкиУпрощеннойАДС.АдресЭлПочты, """") КАК АдресЭлПочты
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_НастройкиУпрощеннойАДС КАК УПЖКХ_НастройкиУпрощеннойАДС
	|		ПО Организации.Ссылка = УПЖКХ_НастройкиУпрощеннойАДС.Организация
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления";
		
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;
	
КонецФункции
	
// Функция получает настройки упрощенной АДС для списка организаций
//
// Параметры:
//  Организации	 - массив	 - Список организаций, по которым надо получить настройки,
//                             если указано значение Неопределено, настройки получаются
//                             для всех организаций, не помеченных на удаление
// 
// Возвращаемое значение:
// Соответствие - Соответствие организаций и настроек
//
Функция ПолучитьНастройкиДляОрганизаций(Организации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ЕСТЬNULL(УПЖКХ_НастройкиУпрощеннойАДС.ПриниматьСообщенияОтЖильцовНаЭлПочту, ЛОЖЬ) КАК ПриниматьСообщенияОтЖильцовНаЭлПочту,
	|	ЕСТЬNULL(УПЖКХ_НастройкиУпрощеннойАДС.АдресЭлПочты, """") КАК АдресЭлПочты,
	|	ЕСТЬNULL(УПЖКХ_НастройкиУпрощеннойАДС.ТемыЗаявок, НЕОПРЕДЕЛЕНО) КАК ТемыЗаявок,
	|	ЕСТЬNULL(УПЖКХ_НастройкиУпрощеннойАДС.ЖилецМожетУказатьСвоюТему, ЛОЖЬ) КАК ЖилецМожетУказатьСвоюТему
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УПЖКХ_НастройкиУпрощеннойАДС КАК УПЖКХ_НастройкиУпрощеннойАДС
	|		ПО Организации.Ссылка = УПЖКХ_НастройкиУпрощеннойАДС.Организация
	|ГДЕ
	|	Организации.Ссылка В(&Организации)";
		
	Запрос.Параметры.Вставить("Организации", Организации);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	
	СоответствиеОрганизацийИНастроек = Новый Соответствие();
	
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
		
		СтруктураСНастройками = Новый Структура("ПриниматьСообщенияОтЖильцовНаЭлПочту,
		                                        |АдресЭлПочты,
		                                        |ТемыЗаявок,
		                                        |ЖилецМожетУказатьСвоюТему",
		                                        ВыборкаИзРезультатаЗапроса.ПриниматьСообщенияОтЖильцовНаЭлПочту,
		                                        ВыборкаИзРезультатаЗапроса.АдресЭлПочты,
		                                        ПреобразоватьХранилищеЗначенияВСтрокуСРазделителями(ВыборкаИзРезультатаЗапроса.ТемыЗаявок),
		                                        ВыборкаИзРезультатаЗапроса.ЖилецМожетУказатьСвоюТему);
		
		СоответствиеОрганизацийИНастроек.Вставить(ВыборкаИзРезультатаЗапроса.Организация, СтруктураСНастройками);
		
	КонецЦикла;
	
	Возврат СоответствиеОрганизацийИНастроек;
	
КонецФункции

// Функция преобразует список значений из хранилища значения в строку с разделителями
//
Функция ПреобразоватьХранилищеЗначенияВСтрокуСРазделителями(ХранилищеЗначенияДляПреобразования)
	
	Если ХранилищеЗначенияДляПреобразования = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	МассивЗначенийДляПреобразования = ХранилищеЗначенияДляПреобразования.Получить();
	
	Если МассивЗначенийДляПреобразования = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	СтрокаРезультата = СтрСоединить(МассивЗначенийДляПреобразования, "|");
	
	Возврат СтрокаРезультата;
	
КонецФункции

#КонецОбласти
