#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура возникает перед выполнением записи набора регистра сведений.
Процедура ПередЗаписью(Отказ, Замещение)
	
	ПроверитьПринадлежностьУслугиОднойГруппе(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет не указана ли услуга в других группах услуг одного вида.
Процедура ПроверитьПринадлежностьУслугиОднойГруппе(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГруппировкаУслуг.ВидГруппУслуг,
	|	ГруппировкаУслуг.ГруппаУслуг,
	|	ГруппировкаУслуг.Услуга
	|ИЗ
	|	РегистрСведений.УПЖКХ_ГруппировкиУслуг КАК ГруппировкаУслуг
	|ГДЕ
	|	ГруппировкаУслуг.Услуга В(&Услуги)";
	Запрос.УстановитьПараметр("Услуги", ВыгрузитьКолонку("Услуга"));
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Услуга");
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("Услуга",        Запись.Услуга);
		ПараметрыОтбора.Вставить("ВидГруппУслуг", Запись.ВидГруппУслуг);
		
		Строки = Результат.НайтиСтроки(ПараметрыОтбора);
		Для каждого ИнформацияОбУслуге Из Строки Цикл
			Если ИнформацияОбУслуге.ГруппаУслуг <> Запись.ГруппаУслуг Тогда
				ТекстОшибки = НСтр("ru='Услуга не может быть записана в нескольких группах'");
				УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю(ТекстОшибки, , , , Отказ);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
