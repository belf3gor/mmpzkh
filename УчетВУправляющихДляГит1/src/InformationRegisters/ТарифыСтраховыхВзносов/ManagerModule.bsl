#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Начальное заполнение и обновление информационной базы.
Процедура НачальноеЗаполнение() Экспорт
	Обновить(ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст(), Ложь);
КонецПроцедуры

// Обновляет данные регистра.
Процедура Обновить(ТекстXML, ПолучатьДанныеИзСервиса = Истина) Экспорт
	ЗарплатаКадры.ОбновитьКлассификатор(ТекстXML, ПолучатьДанныеИзСервиса);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Переписывает ставки для указанного тарифа.
//
// Параметры: 
// 	Тариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов
//  ТаблицаТарифов - ТаблицаЗначений - установленные Законом ставки по всем тарифам.
//
Процедура ОбновитьТарифСтраховыхВзносов(Тариф, ТаблицаТарифов = Неопределено) Экспорт
	Если ТаблицаТарифов = Неопределено Тогда
		ТаблицаТарифов = ТаблицаТарифовСтраховыхВзносов();
		ТаблицаТарифов.Индексы.Добавить("ВидТарифа");
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ТарифыСтраховыхВзносов.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	НаборЗаписей.Отбор.ВидТарифа.Установить(Тариф);
	НаборЗаписей.Загрузить(ТаблицаТарифов.Скопировать(Новый Структура("ВидТарифа",Тариф)));
	НаборЗаписей.Записать();
КонецПроцедуры

// Возвращает размеры тарифов взносов в разрезе лет и видов тарифа, 
// установленные Федеральным законом.
//
// Параметры: 
// 	нет
//
// Возвращаемое значение:
//	Таблица значений, соответствующая по структуре регистру сведений ТарифыСтраховыхВзносов.
//
Функция ТаблицаТарифовСтраховыхВзносов() Экспорт
	ТекстXML = ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст();
	СведенияОКлассификаторе = ОбщегоНазначения.ЗначениеИзСтрокиXML(ТекстXML);
	Возврат ЗарплатаКадры.ПолныеИменаВСсылки(СведенияОКлассификаторе.Данные, СоздатьНаборЗаписей().ВыгрузитьКолонки());
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать процедуру НачальноеЗаполнение.
Процедура ЗаполнитьТарифы() Экспорт
	НачальноеЗаполнение();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли