#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ТекущиеНастройки() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ВариантОтправкиPushУведомлений", Перечисления.ВариантыОтправкиPushУведомлений.НеОтправлять);
	Результат.Вставить("ЛогинСервисаПередачиPushУведомлений", "");
	Результат.Вставить("ИспользоватьAPNS", Ложь);
	Результат.Вставить("ИспользоватьGCM",  Ложь);
	Результат.Вставить("КлючCервераОтправителяGCM",  "");
	Результат.Вставить("НомерПриложенияGoogleCloud", "");
	Результат.Вставить("СертификатМобильногоПриложенияIOS", Неопределено);
	Результат.Вставить("ДанныеСертификата",  Неопределено);
	Результат.Вставить("СертификатЗагружен", Ложь);
	
	МенеджерЗаписи = РегистрыСведений.НастройкиОтправкиPushУведомлений.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(Результат, МенеджерЗаписи);
		Результат.ДанныеСертификата  = МенеджерЗаписи.СертификатМобильногоПриложенияIOS.Получить();
		Результат.СертификатЗагружен = ЗначениеЗаполнено(Результат.ДанныеСертификата);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеАутентификации(НастройкиОтправки = Неопределено) Экспорт
	
	Если НастройкиОтправки = Неопределено Тогда
		НастройкиОтправки = ТекущиеНастройки();
	КонецЕсли;
	
	ВариантОтправки   = НастройкиОтправки.ВариантОтправкиPushУведомлений;
	Если ВариантОтправки = Перечисления.ВариантыОтправкиPushУведомлений.ОтправлятьЧерезВспомогательныйСервис Тогда
		Результат = ?(НЕ ПустаяСтрока(НастройкиОтправки.ЛогинСервисаПередачиPushУведомлений), 
			НастройкиОтправки.ЛогинСервисаПередачиPushУведомлений, Неопределено);
	Иначе
		Результат = Новый Соответствие();
		Если НастройкиОтправки.ИспользоватьGCM 
			И НЕ ПустаяСтрока(НастройкиОтправки.КлючCервераОтправителяGCM) Тогда
			Результат.Вставить(ТипПодписчикаДоставляемыхУведомлений.GCM, НастройкиОтправки.КлючCервераОтправителяGCM);
		КонецЕсли;
		Если НастройкиОтправки.ИспользоватьAPNS 
			И НастройкиОтправки.СертификатЗагружен Тогда
			Результат.Вставить(ТипПодписчикаДоставляемыхУведомлений.APNS, НастройкиОтправки.ДанныеСертификата);
		КонецЕсли;
		Если Результат.Количество() = 0 Тогда
			Результат = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ВыбраннаяФорма = "ФормаНастройки";
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти
