#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru='Записать настройку?'");
		Оповещение = Новый ОписаниеОповещения("ЗаписатьНастройкуЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантОтправкиPushУведомленийПриИзменении(Элемент)
	
	Если ВариантОтправкиPushУведомлений <> ПредопределенноеЗначение(
		"Перечисление.ВариантыОтправкиPushУведомлений.ОтправлятьНепосредственно") Тогда
		ИспользоватьAPNS = Ложь;
		ИспользоватьGCM  = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьGCMПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьAPNSПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСертификатЗавершение", ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение, , "", Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКлючСервиса1С(Команда)
	
	ПерейтиПоНавигационнойСсылке("https://pushnotifications.1c.com/push/publishers/new");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьСертификатЗавершение(Результат, АдресВременногоХранилища, ВыбранноеИмя, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		АдресСертификата   = АдресВременногоХранилища;
		СертификатЗагружен = Не ПустаяСтрока(АдресСертификата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкуЗавершение(Ответ, ДопПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
	Модифицированность = Ложь;
	Закрыть();

КонецПроцедуры 

&НаСервере
Процедура ЗаписатьНаСервере()
	
	МенеджерЗаписи = РегистрыСведений.НастройкиОтправкиPushУведомлений.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Прочитать();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЭтотОбъект);
	Если ЗначениеЗаполнено(АдресСертификата) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресСертификата);
		МенеджерЗаписи.СертификатМобильногоПриложенияIOS = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РегистрыСведений.НастройкиОтправкиPushУведомлений.ТекущиеНастройки());
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ИспользоватьСервис1С = Форма.ВариантОтправкиPushУведомлений = ПредопределенноеЗначение(
		"Перечисление.ВариантыОтправкиPushУведомлений.ОтправлятьЧерезВспомогательныйСервис");
	Элементы.ГруппаОтправкаЧерезВспомогательныйСервис.Доступность = ИспользоватьСервис1С;
	
	ОтправлятьНепосредственно = Форма.ВариантОтправкиPushУведомлений = ПредопределенноеЗначение(
		"Перечисление.ВариантыОтправкиPushУведомлений.ОтправлятьНепосредственно");
	Элементы.ГруппаОтправкаНепосредственно.Доступность = ОтправлятьНепосредственно;
	Элементы.ГруппаОтправкаЧерезGCM.Доступность  = ОтправлятьНепосредственно И Форма.ИспользоватьGCM;
	Элементы.ГруппаОтправкаЧерезAPNS.Доступность = ОтправлятьНепосредственно И Форма.ИспользоватьAPNS;
	
КонецПроцедуры

#КонецОбласти


