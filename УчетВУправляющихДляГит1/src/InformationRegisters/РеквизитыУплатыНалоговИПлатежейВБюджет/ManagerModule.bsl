#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НайтиПоКБК(Организация, КБК, КодТерритории) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("КБК",           КБК); // Настройка может быть выполнена так, что надо искать по части КБК - без символов 14-17 (см. заполнение реквизитов в клиент-банке). Но мы этим пренебрежем для простоты. Все равно настройка настолько непонятная, что вряд ли с ней кто-то справится.
	Запрос.УстановитьПараметр("КодТерритории", КодТерритории);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	Реквизиты.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Реквизиты.ВидПлатежа КАК ВидПлатежа,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПоказательТипа КАК ПоказательТипа,
	|	1 КАК Приоритет,
	|	Реквизиты.КодТерритории КАК КодТерритории
	|ПОМЕСТИТЬ ПодходящиеКлючи
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ВидПлатежа.КодБК = &КБК
	|	И Реквизиты.Организация = &Организация
	|	И (Реквизиты.РегистрацияВНалоговомОргане.КодПоОКАТО = &КодТерритории
	|			ИЛИ Реквизиты.РегистрацияВНалоговомОргане.КодПоОКТМО = &КодТерритории)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	Реквизиты.РегистрацияВНалоговомОргане,
	|	Реквизиты.ВидПлатежа,
	|	Реквизиты.Организация,
	|	Реквизиты.ПоказательТипа,
	|	2,
	|	NULL
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ВидПлатежа.КодБК = &КБК
	|	И Реквизиты.Организация = &Организация
	|	И Реквизиты.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	Реквизиты.РегистрацияВНалоговомОргане,
	|	Реквизиты.ВидПлатежа,
	|	Реквизиты.Организация,
	|	Реквизиты.ПоказательТипа,
	|	3,
	|	NULL
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ВидПлатежа.КодБК = &КБК
	|	И Реквизиты.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И Реквизиты.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ПодходящиеКлючи.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ МинимальныйПриоритет
	|ИЗ
	|	ПодходящиеКлючи КАК ПодходящиеКлючи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.ПоказательТипа КАК ПоказательТипа,
	|	Реквизиты.ВидПлатежа КАК Налог,
	|	Реквизиты.ВидПлатежа.СчетУчета КАК СчетУчета,
	|	Реквизиты.Получатель КАК Получатель,
	|	Реквизиты.СчетПолучателя КАК СчетПолучателя,
	|	Реквизиты.ВидПеречисленияВБюджет КАК ВидПеречисленияВБюджет,
	|	Реквизиты.СтатусСоставителя КАК СтатусСоставителя,
	|	Реквизиты.ОчередностьПлатежа КАК ОчередностьПлатежа,
	|	Реквизиты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Реквизиты.НазначениеПлатежа КАК НазначениеПлатежа,
	|	Реквизиты.ПоказательОснования КАК ПоказательОснования,
	|	Реквизиты.ПоказательПериода КАК ПоказательПериода,
	|	Реквизиты.КодТерритории КАК КодТерритории
	|ИЗ
	|	ПодходящиеКлючи КАК ПодходящиеКлючи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МинимальныйПриоритет КАК МинимальныйПриоритет
	|		ПО ПодходящиеКлючи.Приоритет = МинимальныйПриоритет.Приоритет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК Реквизиты
	|		ПО ПодходящиеКлючи.РегистрацияВНалоговомОргане = Реквизиты.РегистрацияВНалоговомОргане
	|			И ПодходящиеКлючи.ВидПлатежа = Реквизиты.ВидПлатежа
	|			И ПодходящиеКлючи.Организация = Реквизиты.Организация
	|			И ПодходящиеКлючи.ПоказательТипа = Реквизиты.ПоказательТипа";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		
		Выборка = Результат.Выбрать();
		Если Выборка.Количество() <> 1 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("СчетУчета");
		Реквизиты.Вставить("Налог");
		Реквизиты.Вставить("ВидПеречисленияВБюджет");
		Реквизиты.Вставить("ПоказательТипа");
		Реквизиты.Вставить("Получатель");
		Реквизиты.Вставить("СчетПолучателя");
		Реквизиты.Вставить("СтатусСоставителя");
		Реквизиты.Вставить("ОчередностьПлатежа");
		Реквизиты.Вставить("НазначениеПлатежа");
		Реквизиты.Вставить("СтатьяДвиженияДенежныхСредств");
		Реквизиты.Вставить("ПоказательОснования");
		Реквизиты.Вставить("ПоказательПериода");
		
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
		
		ПравилаЗаполнения = ПлатежиВБюджетНастройки.ПравилаЗаполненияРеквизитовПлатежа(Реквизиты.Налог);
		Если ПравилаЗаполнения.СохранятьКодТерриторииПриЗаписи Тогда
			Реквизиты.Вставить("КодОКАТО", Выборка.КодТерритории);
		КонецЕсли;
		
		Возврат Реквизиты;
		
	КонецЕсли;
	
КонецФункции

Функция ДанныеЗаполнения(КлючЗаписи, Период, Организация, ВидНалоговогоОбязательства = Неопределено, ПоказателиПериода = Неопределено, ЗаполнятьСумму = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                 КлючЗаписи.Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", КлючЗаписи.РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("ВидПлатежа",                  КлючЗаписи.ВидПлатежа);
	Запрос.УстановитьПараметр("ПоказательТипа",              КлючЗаписи.ПоказательТипа);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройка.ВидПлатежа КАК Налог,
	|	Настройка.ВидПлатежа.ВидНалога КАК ВидНалога,
	|	Настройка.ВидПлатежа.КодБК КАК КодБК,
	|	Настройка.ВидПлатежа.СчетУчета КАК СчетУчета,
	|	Настройка.Организация КАК Организация,
	|	Настройка.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Настройка.РегистрацияВНалоговомОргане.КПП КАК КПП,
	|	Настройка.ПоказательТипа КАК ПоказательТипа,
	|	Настройка.Получатель КАК Контрагент,
	|	Настройка.Получатель.ИНН КАК ИННПолучателя,
	|	Настройка.Получатель.КПП КАК КПППолучателя,
	|	Настройка.СчетПолучателя КАК СчетКонтрагента,
	|	Настройка.ВидПеречисленияВБюджет КАК ВидПеречисленияВБюджет,
	|	Настройка.СтатусСоставителя КАК СтатусСоставителя,
	|	Настройка.ПоказательОснования КАК ПоказательОснования,
	|	Настройка.ОчередностьПлатежа КАК ОчередностьПлатежа,
	|	Настройка.НазначениеПлатежа КАК НазначениеПлатежа,
	|	Настройка.ПоказательПериода КАК ПоказательПериода,
	|	Настройка.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Настройка.КодТерритории КАК КодТерритории
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК Настройка
	|ГДЕ
	|	Настройка.ВидПлатежа = &ВидПлатежа
	|	И Настройка.Организация = &Организация
	|	И Настройка.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И Настройка.ПоказательТипа = &ПоказательТипа";
	
	Настройка = Запрос.Выполнить().Выбрать();
	Если НЕ Настройка.Следующий() Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Налог");
	ДанныеЗаполнения.Вставить("КодБК");
	ДанныеЗаполнения.Вставить("СчетУчета");
	ДанныеЗаполнения.Вставить("РегистрацияВНалоговомОргане");
	ДанныеЗаполнения.Вставить("ПоказательТипа");
	
	ДанныеЗаполнения.Вставить("Контрагент");
	ДанныеЗаполнения.Вставить("ИННПолучателя");
	ДанныеЗаполнения.Вставить("КПППолучателя");
	ДанныеЗаполнения.Вставить("СчетКонтрагента");
	
	ДанныеЗаполнения.Вставить("ВидПеречисленияВБюджет");
	ДанныеЗаполнения.Вставить("СтатусСоставителя");
	ДанныеЗаполнения.Вставить("ПоказательОснования");
	
	ДанныеЗаполнения.Вставить("ОчередностьПлатежа");
	ДанныеЗаполнения.Вставить("НазначениеПлатежа");
	
	ДанныеЗаполнения.Вставить("СтатьяДвиженияДенежныхСредств");
	
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Настройка);
	
	ПравилаЗаполнения = ПлатежиВБюджетНастройки.ПравилаЗаполненияРеквизитовПлатежа(КлючЗаписи.ВидПлатежа);
	
	Если ПравилаЗаполнения.СохранятьКодТерриторииПриЗаписи И ЗначениеЗаполнено(Настройка.КодТерритории) Тогда
		
		КодТерритории = Настройка.КодТерритории;
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(КодТерритории, Период);
		ДанныеЗаполнения.Вставить("КодОКАТО", КодТерритории);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Настройка.РегистрацияВНалоговомОргане) Тогда
		
		Если ПравилаЗаполнения.ЗаполнятьКодТерриторииПоРегистрации Тогда
			
			КодТерритории = Справочники.РегистрацииВНалоговомОргане.КодТерритории(
				Настройка.РегистрацияВНалоговомОргане, Период);
			
			Если ЗначениеЗаполнено(КодТерритории) Тогда
				ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(КодТерритории, Период);
				ДанныеЗаполнения.Вставить("КодОКАТО", КодТерритории);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Настройка.КПП) Тогда
			ДанныеЗаполнения.Вставить("КПП", Настройка.КПП);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Настройка.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж Тогда
		
		ДанныеЗаполнения.Вставить("ПоказательПериода",              Настройка.ПоказательПериода);
		ДанныеЗаполнения.Вставить("НазначениеПлатежа",              Настройка.НазначениеПлатежа);
		
		Если ПустаяСтрока(ДанныеЗаполнения.НазначениеПлатежа) И ЗначениеЗаполнено(Настройка.Налог) Тогда
			ДанныеЗаполнения.НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(Настройка.Налог);
		КонецЕсли;
		
		ДанныеЗаполнения.Вставить("ШаблонНазначенияПлатежаВБюджет", Настройка.НазначениеПлатежа);
		
	Иначе
		
		Если ЗначениеЗаполнено(Настройка.ВидНалога) Тогда
			// Указан поставляемый вид налога. Получаем значения КБК и счета учета с проверкой актуальности.
			
			Если ПоказателиПериода = Неопределено Тогда
				ПоказателиПериода = ПлатежиВБюджетПереопределяемый.ПоказателиНалоговогоПериода(Организация, Настройка.ВидНалога, Период);
			КонецЕсли;
			
			ДанныеЗаполнения.Вставить("КодБК",
				Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Настройка.Налог, ВидНалоговогоОбязательства, Период, ПоказателиПериода.Период));
			ДанныеЗаполнения.Вставить("СчетУчета",
				Справочники.ВидыНалоговИПлатежейВБюджет.СчетУчета(Настройка.Налог, Период));
		КонецЕсли;
		
		Если ПустаяСтрока(Настройка.НазначениеПлатежа) Тогда
			// Автоназначение платежа
			
			Если ПоказателиПериода = Неопределено Тогда
				ПоказателиПериода = ПлатежиВБюджетПереопределяемый.ПоказателиНалоговогоПериода(Организация, Настройка.ВидНалога, Период);
			КонецЕсли;
			
			НалоговыйПериод = ПоказателиПериода.ПоказательПериода;
			ШаблонНазначенияПлатежаВБюджет = "";
			
			Если Настройка.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж Тогда
				НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(
					Настройка.Налог,, Настройка.Организация,,, ШаблонНазначенияПлатежаВБюджет);
			Иначе
				НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(
					Настройка.Налог,, Настройка.Организация,, ПоказателиПериода.ПредставлениеНалоговогоПериода, ШаблонНазначенияПлатежаВБюджет);
			КонецЕсли;
			
			ДанныеЗаполнения.Вставить("ШаблонНазначенияПлатежаВБюджет", ШаблонНазначенияПлатежаВБюджет);
		Иначе
			ПериодУплаты = Период;
			
			// Налог обычно платим в месяце, следующем за окончанием налогового периода,
			// если это не платеж по конкретной дате
			Если Настройка.ПоказательПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьМесяц() Тогда
				ПериодУплаты = НачалоМесяца(Период) - 1;
			ИначеЕсли Настройка.ПоказательПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьКвартал() Тогда
				ПериодУплаты = НачалоКвартала(Период) - 1;
			ИначеЕсли Настройка.ПоказательПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьГод() Тогда
				ПериодУплаты = НачалоГода(Период) - 1;
			КонецЕсли;
			
			// Пользовательское назначение платежа
			
			НалоговыйПериод = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПериодУплаты, Настройка.ПоказательПериода);
			Если Настройка.ПоказательПериода = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение() Тогда
				НазначениеПлатежа = Настройка.НазначениеПлатежа;
			Иначе
				НазначениеПлатежа = СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 %2'"), Настройка.НазначениеПлатежа, ПлатежиВБюджетКлиентСервер.ПредставлениеНалоговогоПериода(НалоговыйПериод)));
			КонецЕсли;
			
			ДанныеЗаполнения.Вставить("ШаблонНазначенияПлатежаВБюджет", Настройка.НазначениеПлатежа);
		КонецЕсли;
		
		Если Настройка.ПоказательПериода = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение() Тогда
			ДанныеЗаполнения.Вставить("ПоказательПериода", Настройка.ПоказательПериода);
		Иначе
			ДанныеЗаполнения.Вставить("ПоказательПериода", НалоговыйПериод);
		КонецЕсли;
		
		ДанныеЗаполнения.Вставить("НазначениеПлатежа", НазначениеПлатежа);
		
	КонецЕсли;
	
	// Сумма платежа
	
	Если ЗаполнятьСумму
		И ЗначениеЗаполнено(Настройка.СчетУчета)
		И (БухгалтерскийУчетПовтИсп.СчетВИерархии(Настройка.СчетУчета, ПланыСчетов.Хозрасчетный.РасчетыПоНалогам)
		Или БухгалтерскийУчетПовтИсп.СчетВИерархии(Настройка.СчетУчета, ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию)) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Настройка.Организация);
		Запрос.УстановитьПараметр("СчетУчета",   Настройка.СчетУчета);
		
		ШаблонТекстаЗапроса =
		"ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОстатки.СуммаОстатокКт) КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			,
		|			Счет = &СчетУчета,
		|			[ВидыСубконто],
		|			Организация = &Организация
		|				[Условия]) КАК ХозрасчетныйОстатки";
		
		Аналитика = РасчетыСБюджетом.АналитикаНаСчетеРасчетовСБюджетом(
			Настройка.СчетУчета,
			Настройка.Организация,
			Настройка.РегистрацияВНалоговомОргане,
			Неопределено,
			Настройка.КодБК,
			Настройка.ПоказательОснования,
			Настройка.ПоказательТипа,
			Ложь); // Оборотные субконто исключаем
			
		ПараметрыТекста = Новый Структура;
		Если Аналитика.Количество() = 0 Тогда
			ПараметрыТекста.Вставить("ВидыСубконто", "");
			ПараметрыТекста.Вставить("Условия",      "");
		Иначе
			
			ПараметрыТекста.Вставить("ВидыСубконто", "&ВидыСубконто");
			Запрос.УстановитьПараметр("ВидыСубконто", Аналитика.ВыгрузитьКолонку("ВидСубконто"));
			
			УсловияНаСубконто = "";
			Для Индекс = 0 По Аналитика.Количество() - 1 Цикл
				ИмяСубконто = "Субконто" + (Индекс + 1);
				Запрос.УстановитьПараметр(ИмяСубконто, Аналитика[Индекс].Значение);
				УсловияНаСубконто = УсловияНаСубконто + " И " + ИмяСубконто + " = &" + ИмяСубконто;
			КонецЦикла;
			
			ПараметрыТекста.Вставить("Условия", УсловияНаСубконто);
			
		КонецЕсли;
		
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекстаЗапроса, ПараметрыТекста);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.Сумма > 0 Тогда
			ДанныеЗаполнения.Вставить("СуммаДокумента", Выборка.Сумма);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция ПолучитьНастройкуРеквизитыПлатежаВБюджет(УчетныеДанные, Организация, РегистрацияВНалоговомОргане) Экспорт
	
	ОбъединительЗапросов = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	ТекстыЗапросов = Новый Массив;
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		ТекстыЗапросов.Добавить("ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	СписокРеквизитов.ВидПлатежа КАК ВидПлатежа,
		|	СписокРеквизитов.Организация КАК Организация,
		|	СписокРеквизитов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	СписокРеквизитов.ПоказательТипа КАК ПоказательТипа
		|ИЗ
		|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК СписокРеквизитов
		|ГДЕ
		|	СписокРеквизитов.ВидПлатежа.СчетУчета = &СчетУчета
		|	И СписокРеквизитов.Организация = &Организация
		|	И СписокРеквизитов.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	КонецЕсли;
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	2 КАК Приоритет,
	|	СписокРеквизитов.ВидПлатежа КАК ВидПлатежа,
	|	СписокРеквизитов.Организация КАК Организация,
	|	СписокРеквизитов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СписокРеквизитов.ПоказательТипа КАК ПоказательТипа
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК СписокРеквизитов
	|ГДЕ
	|	СписокРеквизитов.ВидПлатежа.СчетУчета = &СчетУчета
	|	И СписокРеквизитов.Организация = &Организация
	|	И СписокРеквизитов.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	3 КАК Приоритет,
	|	СписокРеквизитов.ВидПлатежа КАК ВидПлатежа,
	|	СписокРеквизитов.Организация КАК Организация,
	|	СписокРеквизитов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СписокРеквизитов.ПоказательТипа КАК ПоказательТипа
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК СписокРеквизитов
	|ГДЕ
	|	СписокРеквизитов.ВидПлатежа.СчетУчета = &СчетУчета
	|	И СписокРеквизитов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И СписокРеквизитов.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)");
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросов, ОбъединительЗапросов);
	
	Запрос = Новый Запрос;
	
	НомерСубконтоУровниБюджета = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НомерСубконто(
		УчетныеДанные.СчетУчета,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.УровниБюджетов);
	Если НомерСубконтоУровниБюджета > 0 Тогда
		РасположениеЭлементаКБК = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодЭлементаДоходов");
		Если РасположениеЭлементаКБК <> Неопределено Тогда
			Запрос.УстановитьПараметр("КодыЭлементаДохода", ПлатежиВБюджет.КодыЭлементаДоходаПоУровнюБюджета(
				УчетныеДанные["Субконто" + НомерСубконтоУровниБюджета]));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"СписокРеквизитов.ВидПлатежа.СчетУчета = &СчетУчета",
				"СписокРеквизитов.ВидПлатежа.СчетУчета = &СчетУчета
				|	И ПОДСТРОКА(СписокРеквизитов.ВидПлатежа.КодБК, "
				+ РасположениеЭлементаКБК.Начало
				+ ", " + РасположениеЭлементаКБК.Длина + ") В(&КодыЭлементаДохода)");
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет ВОЗР";
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("СчетУчета",   УчетныеДанные.СчетУчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат КлючЗаписиРеквизитыПлатежаВБюджет(Выборка);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает реквизиты для уплаты налога
//
// Параметры:
//  Налог                       - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//  Организация                 - СправочникСсылка.Организации
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение:
//  Структура - элементы структуры соответствуют ресурсам регистра РеквизитыУплатыНалоговИПлатежейВБюджет
//
Функция КлючНастройкиУплатыНалога(Налог, Организация, РегистрацияВНалоговомОргане) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидПлатежа",                  Налог);
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1 КАК Приоритет,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.ВидПлатежа,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.Организация,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.РегистрацияВНалоговомОргане,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.ПоказательТипа
		|ИЗ
		|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК РеквизитыУплатыНалоговИПлатежейВБюджет
		|ГДЕ
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.ВидПлатежа = &ВидПлатежа
		|	И РеквизитыУплатыНалоговИПлатежейВБюджет.Организация = &Организация
		|	И &УсловиеПоРегистрацииВНалоговомОргане";
	
	Если НалоговыйУчет.УчетВРазрезеНалоговыхОрганов() Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоРегистрацииВНалоговомОргане",
			"РеквизитыУплатыНалоговИПлатежейВБюджет.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
		
		ТекстЗапроса = ТекстЗапроса + 
			"
			|ОБЪЕДИНИТЬ ВСЕ
			|" + 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	2 КАК Приоритет,
			|	РеквизитыУплатыНалоговИПлатежейВБюджет.ВидПлатежа,
			|	РеквизитыУплатыНалоговИПлатежейВБюджет.Организация,
			|	РеквизитыУплатыНалоговИПлатежейВБюджет.РегистрацияВНалоговомОргане,
			|	РеквизитыУплатыНалоговИПлатежейВБюджет.ПоказательТипа
			|ИЗ
			|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК РеквизитыУплатыНалоговИПлатежейВБюджет
			|ГДЕ
			|	РеквизитыУплатыНалоговИПлатежейВБюджет.ВидПлатежа = &ВидПлатежа
			|	И РеквизитыУплатыНалоговИПлатежейВБюджет.Организация = &Организация
			|	И РеквизитыУплатыНалоговИПлатежейВБюджет.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)";
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоРегистрацииВНалоговомОргане", "ИСТИНА");
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
		"
		|ОБЪЕДИНИТЬ ВСЕ
		|" + 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	3 КАК Приоритет,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.ВидПлатежа,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.Организация,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.РегистрацияВНалоговомОргане,
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.ПоказательТипа
		|ИЗ
		|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК РеквизитыУплатыНалоговИПлатежейВБюджет
		|ГДЕ
		|	РеквизитыУплатыНалоговИПлатежейВБюджет.ВидПлатежа = &ВидПлатежа
		|	И РеквизитыУплатыНалоговИПлатежейВБюджет.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = КлючЗаписиРеквизитыПлатежаВБюджет(Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиПоРеквизитамПлатежа(КБК, Организация, Контрагент) Экспорт
	
	Результат = Новый Структура; // Если не нашли, то вернем Неопределено
	Результат.Вставить("Налог",                         Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка());
	Результат.Вставить("СтатьяДвиженияДенежныхСредств", Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	
	Если Не ПлатежиВБюджетКлиентСервер.КБКЗадан(КБК) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Ищем по всем значениям КодПодвидаДоходов
	ГраницыПеременнойЧастиКБК = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	ШаблонКБК = Лев(КБК, ГраницыПеременнойЧастиКБК.Начало - 1)
		+ СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыПеременнойЧастиКБК.Длина)
		+ Сред(КБК, ГраницыПеременнойЧастиКБК.Начало + ГраницыПеременнойЧастиКБК.Длина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ШаблонКБК",   ШаблонКБК);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Настройки.ВидПлатежа КАК Налог,
	|	Настройки.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Настройки.ВидПлатежа.КодБК КАК КБК,
	|	Настройки.Организация КАК Организация,
	|	Настройки.Получатель КАК Контрагент
	|ИЗ
	|	РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет КАК Настройки
	|ГДЕ
	|	Настройки.ВидПлатежа.КодБК ПОДОБНО &ШаблонКБК
	|	И Настройки.ВидПлатежа.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И (Настройки.Организация = &Организация
	|			ИЛИ Настройки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Налог,
	|	СтатьяДвиженияДенежныхСредств";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Выберем лучший
	МаксимальныйПриоритет = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Приоритет = 1; // За соответствие шаблону
		
		Если Выборка.Контрагент = Контрагент Тогда
			Приоритет = Приоритет + 2;
		КонецЕсли;
		
		Если Выборка.Организация = Организация Тогда
			Приоритет = Приоритет + 4;
		КонецЕсли;
		
		Если Выборка.КБК = КБК Тогда
			Приоритет = Приоритет + 8;
		КонецЕсли;
		
		Если Приоритет > МаксимальныйПриоритет Тогда
			МаксимальныйПриоритет = Приоритет;
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ОчиститьРеквизитыЗаписейПоВсемНалогамИВзносам() Экспорт
	
	НаборЗаписей = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КлючЗаписиРеквизитыПлатежаВБюджет(Выборка)
	
	ЗначениеКлюча = Новый Структура;
	ЗначениеКлюча.Вставить("ВидПлатежа");
	ЗначениеКлюча.Вставить("Организация");
	ЗначениеКлюча.Вставить("РегистрацияВНалоговомОргане");
	ЗначениеКлюча.Вставить("ПоказательТипа");
	
	ЗаполнитьЗначенияСвойств(ЗначениеКлюча, Выборка);
	
	ПараметрыКонструктора = Новый Массив;
	ПараметрыКонструктора.Добавить(ЗначениеКлюча);
	
	Возврат Новый(Тип("РегистрСведенийКлючЗаписи.РеквизитыУплатыНалоговИПлатежейВБюджет"), ПараметрыКонструктора);
	
КонецФункции

#КонецОбласти

#КонецЕсли
