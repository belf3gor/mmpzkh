#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтборНабораЗаписей = Параметры.Отбор;
	
	Организация = ОтборНабораЗаписей.Организация;
	ВидДеятельности = ОтборНабораЗаписей.ВидДеятельности;
	
	Если Не ЗначениеЗаполнено(ВидДеятельности) Или Не ЗначениеЗаполнено(Организация) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Сортировать("Период");
		ИдентификаторПоследнейЗаписи = НаборЗаписей[НаборЗаписей.Количество() - 1].ПолучитьИдентификатор();
		Элементы.НаборЗаписей.ТекущаяСтрока = ИдентификаторПоследнейЗаписи;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.РегиональныеОсобенностиЕНВД) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		ПоследняяЗапись = НаборЗаписей.НайтиПоИдентификатору(ИдентификаторПоследнейЗаписи);
		Если ПоследняяЗапись <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПоследняяЗапись);
			ТекущиеДанные.Период = ДобавитьМесяц(ТекущиеДанные.Период, 3); // Следующий квартал
		Иначе
			ТекущиеДанные.Период = НачалоГода(ТекущаяДата());
			ТекущиеДанные.Организация = Организация;
			ТекущиеДанные.ВидДеятельности = ВидДеятельности;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	НаборЗаписей.Сортировать("Период");
	
	Элементы.НаборЗаписей.ТекущаяСтрока = Элемент.ТекущиеДанные.ПолучитьИдентификатор();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		ИдентификаторПоследнейЗаписи = НаборЗаписей[НаборЗаписей.Количество() - 1].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПослеУдаления(Элемент)
	
	Если НаборЗаписей.Количество() > 0 Тогда
		ИдентификаторПоследнейЗаписи = НаборЗаписей[НаборЗаписей.Количество() - 1].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запись = Элементы.НаборЗаписей.ТекущиеДанные;
	Если Запись <> Неопределено Тогда
		Период = ?(ЗначениеЗаполнено(Запись.Период), Запись.Период, НачалоКвартала(ТекущаяДата()));
		Запись.Период = ДобавитьМесяц(Период, 3 * Направление);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти