#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает показатели расчета ЕНВД, учитывающие региональные особенности
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ВидДеятельности - СправочникСсылка.ВидыДеятельностиЕНВД
//  Период - Дата - период расчета налога
//
// Возвращаемое значение:
//  Структура со свойствами:
//   * КорректирующийКоэффициент - Число(4, 3) - корректирующий коэффициент К2
//   * НалоговаяСтавка - Число(3, 1) - налоговая ставка
//
Функция РегиональныеОсобенностиВидаДеятельности(Организация, ВидДеятельности, Период) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидДеятельности) Тогда
		Возврат РегиональныеОсобенностиПоУмолчанию();
	КонецЕсли;
	
	ВидыДеятельности = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидДеятельности);
	
	ФизическиеПоказатели = РегиональныеОсобенностиВидовДеятельности(
		Организация, ВидыДеятельности, Период);
	
	Возврат ФизическиеПоказатели.Получить(ВидДеятельности);
	
КонецФункции

// Возвращает показатели расчета ЕНВД, учитывающие региональные особенности
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ВидыДеятельности - Массив
//  Период - Дата - период расчета налога
//
// Возвращаемое значение:
//  Соответствие, где ключ - ссылка на вид деятельности, а значение - структура со войствами:
//   * КорректирующийКоэффициент - Число(4, 3) - корректирующий коэффициент К2
//   * НалоговаяСтавка - Число(3, 1) - налоговая ставка
//
Функция РегиональныеОсобенностиВидовДеятельности(Организация, ВидыДеятельности, Период) Экспорт
	
	РегиональныеОсобенности = Новый Соответствие;
	Для Каждого ВидДеятельности Из ВидыДеятельности Цикл
		РегиональныеОсобенности.Вставить(ВидДеятельности, РегиональныеОсобенностиПоУмолчанию());
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",           НачалоКвартала(Период));
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("ВидыДеятельности", ВидыДеятельности);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегиональныеОсобенностиЕНВДСрезПоследних.ВидДеятельности,
	|	РегиональныеОсобенностиЕНВДСрезПоследних.КорректирующийКоэффициент,
	|	РегиональныеОсобенностиЕНВДСрезПоследних.НалоговаяСтавка
	|ИЗ
	|	РегистрСведений.РегиональныеОсобенностиЕНВД.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ВидДеятельности В (&ВидыДеятельности)) КАК РегиональныеОсобенностиЕНВДСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(РегиональныеОсобенности[Выборка.ВидДеятельности], Выборка);
	КонецЦикла;
	
	Возврат РегиональныеОсобенности;
	
КонецФункции

Процедура ЗаписатьРегиональныеОсобенности(КорректирующийКоэффициент, НалоговаяСтавка, КлючЗаписи, Знач Период, СоздатьНовуюЗапись = Ложь) Экспорт
	
	Если Не СоздатьНовуюЗапись Тогда
		
		Отбор = Новый Структура("Организация, ВидДеятельности", КлючЗаписи.Организация, КлючЗаписи.ВидДеятельности);
		СрезПоследних = РегистрыСведений.РегиональныеОсобенностиЕНВД.СрезПоследних(Период, Отбор);
		Если СрезПоследних.Количество() <> 0 Тогда
			Период = СрезПоследних[0].Период;
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.РегиональныеОсобенностиЕНВД.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючЗаписи);
	МенеджерЗаписи.Период                    = Период;
	МенеджерЗаписи.КорректирующийКоэффициент = КорректирующийКоэффициент;
	МенеджерЗаписи.НалоговаяСтавка           = НалоговаяСтавка;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РегиональныеОсобенностиПоУмолчанию()
	
	РегиональныеОсобенности = Новый Структура;
	РегиональныеОсобенности.Вставить("КорректирующийКоэффициент", 1);
	РегиональныеОсобенности.Вставить("НалоговаяСтавка", УчетЕНВДКлиентСервер.НалоговаяСтавкаПоУмолчанию());
	
	Возврат РегиональныеОсобенности;
	
КонецФункции

#КонецОбласти

#КонецЕсли

