#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает физический показатель для расчета ЕНВД
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ВидДеятельности - СправочникСсылка.ВидыДеятельностиЕНВД
//  Период - Дата - период расчета налога
//
// Возвращаемое значение:
//  Число(10, 0) - значение физического показателя
//
Функция ФизическийПоказательВидаДеятельности(Организация, ВидДеятельности, Период) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидДеятельности) Тогда
		Возврат ФизическийПоказательПоУмолчанию();
	КонецЕсли;
	
	ВидыДеятельности = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидДеятельности);
	
	ФизическиеПоказатели = ФизическиеПоказателиВидовДеятельностей(
		Организация, ВидыДеятельности, Период);
	
	Возврат ФизическиеПоказатели.Получить(ВидДеятельности);
	
КонецФункции

// Возвращает показатели расчета ЕНВД, учитывающие региональные особенности
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ВидыДеятельности - Массив
//  Период - Дата - период расчета налога
//
// Возвращаемое значение:
//  Соответствие, где ключ - ссылка на вид деятельности, а значение является значением физического показателя
//
Функция ФизическиеПоказателиВидовДеятельностей(Организация, ВидыДеятельности, Период) Экспорт
	
	ФизическиеПоказатели = Новый Соответствие;
	Для Каждого ВидДеятельности Из ВидыДеятельности Цикл
		ФизическиеПоказатели.Вставить(ВидДеятельности, ФизическийПоказательПоУмолчанию());
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",           НачалоМесяца(Период));
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("ВидыДеятельности", ВидыДеятельности);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФизическиеПоказателиЕНВДСрезПоследних.ВидДеятельности,
	|	ФизическиеПоказателиЕНВДСрезПоследних.ФизическийПоказатель
	|ИЗ
	|	РегистрСведений.ФизическиеПоказателиЕНВД.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ВидДеятельности В (&ВидыДеятельности)) КАК ФизическиеПоказателиЕНВДСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ФизическиеПоказатели[Выборка.ВидДеятельности] = Выборка.ФизическийПоказатель;
	КонецЦикла;
	
	Возврат ФизическиеПоказатели;
	
КонецФункции

Процедура ЗаписатьФизическийПоказатель(ФизическийПоказатель, КлючЗаписи, Знач Период, СоздатьНовуюЗапись = Ложь) Экспорт
	
	Если Не СоздатьНовуюЗапись Тогда
		
		Отбор = Новый Структура("Организация, ВидДеятельности", КлючЗаписи.Организация, КлючЗаписи.ВидДеятельности);
		СрезПоследних = РегистрыСведений.ФизическиеПоказателиЕНВД.СрезПоследних(Период, Отбор);
		Если СрезПоследних.Количество() <> 0 Тогда
			Период = СрезПоследних[0].Период;
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ФизическиеПоказателиЕНВД.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючЗаписи);
	МенеджерЗаписи.Период               = Период;
	МенеджерЗаписи.ФизическийПоказатель = ФизическийПоказатель;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура УдалитьФизическийПоказатель(КлючЗаписи, Период) Экспорт
	
	НаборЗаписей = РегистрыСведений.ФизическиеПоказателиЕНВД.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(КлючЗаписи.Организация);
	НаборЗаписей.Отбор.ВидДеятельности.Установить(КлючЗаписи.ВидДеятельности);
	НаборЗаписей.Отбор.Период.Установить(Период);
	
	НаборЗаписей.Прочитать();
	Для Каждого Запись Из НаборЗаписей Цикл
		НаборЗаписей.Удалить(Запись);
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ФизическийПоказательПоУмолчанию()
	
	Возврат 1;
	
КонецФункции

#КонецОбласти

#КонецЕсли

