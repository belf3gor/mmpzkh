////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Инициализация служебных параметров отбора
	ОписанияТиповВидовСубконто = Новый Структура;
	ОписанияТиповВидовСубконто.Вставить("Номенклатура"                  , ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура.ТипЗначения);
	ОписанияТиповВидовСубконто.Вставить("Склад"                         , ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады.ТипЗначения);
	ОписанияТиповВидовСубконто.Вставить("Контрагент"                    , ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты.ТипЗначения);
	ОписанияТиповВидовСубконто.Вставить("ДоговорКонтрагента"            , ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры.ТипЗначения);
	ОписанияТиповВидовСубконто.Вставить("Партия"                        , ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии.ТипЗначения);
	ОписанияТиповВидовСубконто.Вставить("ДокументРасчетовСКонтрагентами", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами.ТипЗначения);
	
	ИнициализироватьНастройки();
	
	НастроитьДоступностьОтбора();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ИнициализироватьНастройки();
	
	НастроитьДоступностьОтбора();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Запишем пользовательские настройки и их представление
	ЗаполнитьЗначенияСвойств(ТекущийОбъект, ЗаполнениеФормСтатистики.СериализоватьРезультатНастройки(Настройки));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрСообщения = Новый Структура;
	ПараметрСообщения.Вставить("Организация");
	ПараметрСообщения.Вставить("ОбъектНаблюдения");
	ПараметрСообщения.Вставить("Заполнять");
	ЗаполнитьЗначенияСвойств(ПараметрСообщения, Запись);
	
	ПараметрСообщения.Вставить("Детализировать", Ложь);
	
	Оповестить("НастройкаЗаполненияФормСтатистики.Изменение", ПараметрСообщения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗагрузитьНастройку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНаблюденияПриИзменении(Элемент)
	
	ИнициализироватьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьПриИзменении(Элемент)
	НастроитьДоступностьОтбора();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ГРУППЫ ОТБОР

&НаКлиенте
Процедура ОтборыПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокПараметров = ПолучитьПараметрыВыбораЗначенияОтбора();
	
	БухгалтерскиеОтчетыКлиент.ОтборыПравоеЗначениеНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка, СписокПараметров, Настройки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ИнициализироватьНастройки()
	
	Если ОбъектКоторымИнициализированКомпоновщик = Запись.ОбъектНаблюдения Тогда
		Возврат;
	Иначе 
		ОбъектКоторымИнициализированКомпоновщик = Запись.ОбъектНаблюдения;
	КонецЕсли;
	
	ЗаполнениеФормСтатистики.ИнициализироватьКомпоновщикНастроекПользователя(Настройки, Запись.ОбъектНаблюдения, УникальныйИдентификатор);
	
	ЗагрузитьНастройку();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьОтбора()
	
	Элементы.ЗаголовокОтбор.ТолькоПросмотр = Не Запись.Заполнять;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройку()
	
	ЗаполнениеФормСтатистики.ЗагрузитьНастройку(Настройки, Запись.Настройка)
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыВыбораЗначенияОтбора()
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Номенклатура"      , Неопределено);
	СписокПараметров.Вставить("Склад"             , Неопределено);
	СписокПараметров.Вставить("Организация"       , Запись.Организация);
	СписокПараметров.Вставить("Контрагент"        , Неопределено);
	СписокПараметров.Вставить("ДоговорКонтрагента", Неопределено);
	
	Возврат СписокПараметров;
	
КонецФункции
