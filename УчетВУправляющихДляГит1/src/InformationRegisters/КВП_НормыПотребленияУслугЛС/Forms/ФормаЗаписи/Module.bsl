

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

// Обработчик события формы "При создании на сервере".
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запись.Период = НачалоМесяца(УПЖКХ_ТиповыеМетодыКлиентСервер.ПолучитьРабочуюДату());
	
	Если Параметры.Свойство("ОтборОбъект") Тогда
		Запись.ЛицевойСчет = Параметры.ОтборОбъект;
		Элементы.ЛицевойСчет.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	// Параметр "Услуга" приходит в случае, когда форма открывается после копирования
	// нормы потребления в элементе справочника "Здания" или в элементе справочника "Лицевые счета".
	// В этом случае заполним остальные поля.
	Если Параметры.Свойство("Услуга") Тогда
		Запись.Услуга           = Параметры.Услуга;
		Запись.НормаПотребления = Параметры.НормаПотребления;
	КонецЕсли;
	РазмерНормы = УПЖКХ_РаботаСОбъектамиУчетаСервер.УстановитьРазмерНормы(Запись.НормаПотребления);
	
	Если ЗначениеЗаполнено(Запись.ЛицевойСчет) Тогда
		Если ТипЗнч(Запись.ЛицевойСчет) = Тип("СправочникСсылка.КВП_Здания") Тогда
			НормаНаЛицевойСчет = Ложь;
		Иначе 
			НормаНаЛицевойСчет = Истина;
		КонецЕсли;
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.НормаНаЛицевойСчет", НормаНаЛицевойСчет);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.НормаПотребления.ПараметрыВыбора = НовыеПараметры;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события формы "Начало выбора".
&НаКлиенте
Процедура НормаПотребленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	Если ТипЗнч(Запись.ЛицевойСчет) = Тип("СправочникСсылка.КВП_Здания") Тогда
		КВП_РаботаСДиалогамиКлиент.ВыбратьНормуПотребления(Элемент, СтандартнаяОбработка, Запись.Услуга, Ложь);
	ИначеЕсли ТипЗнч(Запись.ЛицевойСчет) = Тип("СправочникСсылка.КВП_ЛицевыеСчета") Тогда
		КВП_РаботаСДиалогамиКлиент.ВыбратьНормуПотребления(Элемент, СтандартнаяОбработка, Запись.Услуга, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события формы "После записи".
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ОбновитьТаблицуНорм");
КонецПроцедуры

// Обработчик события формы "При изменении".
&НаКлиенте
Процедура НормаПотребленияПриИзменении(Элемент)
	РазмерНормы = УПЖКХ_РаботаСОбъектамиУчетаСервер.УстановитьРазмерНормы(Запись.НормаПотребления);
КонецПроцедуры

// Обработчик "При изменении"  события поля Период.
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Запись.Период = НачалоМесяца(Запись.Период );
КонецПроцедуры

#КонецОбласти