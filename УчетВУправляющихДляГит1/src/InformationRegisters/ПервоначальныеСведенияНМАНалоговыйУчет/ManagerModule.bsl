
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьСпособОтраженияРасходов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПервоначальныеСведенияНМАНалоговыйУчет.Регистратор КАК Регистратор,
	|	ВЫРАЗИТЬ(ПервоначальныеСведенияНМАНалоговыйУчет.Регистратор КАК Документ.ПринятиеКУчетуНМА).ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ВЫРАЗИТЬ(ПервоначальныеСведенияНМАНалоговыйУчет.Регистратор КАК Документ.ПринятиеКУчетуНМА).СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	ЛОЖЬ КАК СпособОтраженияРасходовАналогичноАмортизации
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет КАК ПервоначальныеСведенияНМАНалоговыйУчет
	|ГДЕ
	|	ПервоначальныеСведенияНМАНалоговыйУчет.ПорядокВключенияСтоимостиВСоставРасходов = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ПустаяССылка)
	|	И ПервоначальныеСведенияНМАНалоговыйУчет.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА";
	
	ВыборкаЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗаписи.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаЗаписи.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаНабора Из НаборЗаписей Цикл
			ЗаполнитьЗначенияСвойств(СтрокаНабора, ВыборкаЗаписи);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
