#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

Функция ИменаВидовНормируемыхРасходов() Экспорт
	
	// Соответствие имен видов расходов именам ресурсов регистра
	
	ВидыРасходов = Новый Соответствие; 
	ВидыРасходов.Вставить(
		"ДоляРасходовНаРекламу",
		"РасходыНаРекламуНормируемые");
	ВидыРасходов.Вставить(
		"ДоляПредставительскихРасходов",
		"ПредставительскиеРасходы");
	ВидыРасходов.Вставить(
		"ДоляРасходовНаДобровольноеМедицинскоеСтрахование",
		"ДобровольноеЛичноеСтрахование");
	ВидыРасходов.Вставить(
		"ДоляРасходовНаДобровольноеСтрахованиеЖизни",
		"ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников");
	ВидыРасходов.Вставить(
		"ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев",
		"ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности");
	ВидыРасходов.Вставить(
		"ДоляРасходовНаВозмещениеПроцентовРаботникам",
		"РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов");
		
	Возврат ВидыРасходов;
		
КонецФункции

Функция ВыполненРасчетДолейСписанияКосвенныхРасходов(Период, Организация) Экспорт
	
	// Проверяем наличие записей в регистре, а не факт выполнения регламентной операции
	// Записи могут быть введены и без выполнения регламентной операции
	
	ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",       НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",        КонецМесяца(Период));
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДолиСписанияКосвенныхРасходов.Организация
	|ИЗ
	|	РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДолиСписанияКосвенныхРасходов
	|ГДЕ
	|	ДолиСписанияКосвенныхРасходов.Организация = &ГоловнаяОрганизация
	|	И ДолиСписанияКосвенныхРасходов.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция РегламентнаяОперация(Период, Организация) Экспорт
	
	Возврат Документы.РегламентнаяОперация.НайтиДокумент(
		Период,
		ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация),
		Перечисления.ВидыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов);
	
КонецФункции

#КонецЕсли
