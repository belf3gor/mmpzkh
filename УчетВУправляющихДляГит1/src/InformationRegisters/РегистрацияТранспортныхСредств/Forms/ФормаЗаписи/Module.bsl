
#Область ОписаниеПеременных

&НаКлиенте
Перем СтруктураСохраняемыхРеквизитов;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		
		Запись.Период = ТекущаяДатаСеанса();
		
		Если Запись.ВидЗаписи = Перечисления.ВидЗаписиОРегистрации.Регистрация Тогда
			Запись.ВключатьВНалоговуюБазу = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.ОсновноеСредство) Тогда
			ЗаполнитьПараметрыПоОсновномуСредству();
		КонецЕсли;
	
		НастроитьФормуПоОбъекту();
		
	КонецЕсли;
	
	УстановитьЗаголовокФормы(ЭтаФорма, Параметры.Ключ.Пустой());
	
	СтарыйГод = 0;
	ТекущаяНалоговаяЛьгота = Запись.НалоговаяЛьгота;
	ЗаполнитьДанныеОНалоговомОргане();
	ЦветТекстаИнформационнойНадписи = ЦветаСтиля.ТекстИнформационнойНадписи;
	
	ЗаполнитьСписокВыбораПовышающегоКоэффициента(
		Элементы.ПовышающийКоэффициент.СписокВыбора, 
		Запись.Период);
		
	УстановитьТекстПодсказкиСпискаПовышающихКоэффициентов(
		Элементы.ПовышающийКоэффициент.РасширеннаяПодсказка.Заголовок, 
		Запись.Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СтруктураСохраняемыхРеквизитов = Новый Структура;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастроитьФормуПоОбъекту();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Параметры.Ключ.Организация <> ТекущийОбъект.Организация
		ИЛИ Параметры.Ключ.ОсновноеСредство <> ТекущийОбъект.ОсновноеСредство
		ИЛИ Параметры.Ключ.Период <> ТекущийОбъект.Период Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период",           ТекущийОбъект.Период);
		Запрос.УстановитьПараметр("Организация",      ТекущийОбъект.Организация);
		Запрос.УстановитьПараметр("ОсновноеСредство", ТекущийОбъект.ОсновноеСредство);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(РегистрацияТранспортныхСредств.ВидЗаписи) КАК ВидЗаписи
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Период = &Период
		|	И РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.ОсновноеСредство = &ОсновноеСредство";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			
			Выборка.Следующий();
			
			ШаблонСообщения = НСтр("ru = '%1 по основному средству <%2> 
				|уже есть запись <%3> в организации <%4>!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
				Формат(ТекущийОбъект.Период, "ДЛФ=D"), ТекущийОбъект.ОсновноеСредство, ТекущийОбъект.ВидЗаписи,
				ТекущийОбъект.Организация);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Запись.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации 
			И ЗначениеЗаполнено(ТекущийОбъект.НалоговыйОрган) Тогда
			
			ТекущийОбъект.НалоговыйОрган = Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
			
	КонецЕсли;
		
	Если Год(ТекущийОбъект.Период) >= 2014 Тогда
		ТекущийОбъект.КодПоОКАТО = "";
	КонецЕсли;
	
	// Данные информационной базы, влияющие на список КБК
	Если ТекущийОбъект.ВключатьВНалоговуюБазу
	   И Не Перечисления.УсловияПримененияТребованийЗаконодательства.ЕстьТранспортныеСредства() Тогда
		ПараметрыЗаписи.Вставить("ПоявилисьТранспортныеСредства", Истина);
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ВедетсяУчетПлатежейВПлатонПередЗаписью",
		ПолучитьФункциональнуюОпцию("ВедетсяУчетПлатежейВПлатон"));
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Запись.ВидЗаписи = Перечисления.ВидЗаписиОРегистрации.Регистрация Тогда
		
		Если Запись.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
		
			Если НЕ ЗначениеЗаполнено(Запись.НалоговыйОрган) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Налоговый орган'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Запись.НалоговыйОрган", , Отказ);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Запись.КодПоОКТМО) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Код по ОКТМО'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Запись.КодПоОКТМО", , Отказ);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Запись.КодПоОКАТО) И Год(Запись.Период) < 2014 Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Код по ОКАТО'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Запись.КодПоОКАТО", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Запись.ОбщаяСобственность Тогда
			
			Если НЕ ЗначениеЗаполнено(Запись.ДоляВПравеОбщейСобственностиЧислитель) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Доля в праве общей собственности (числитель)'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Запись.ДоляВПравеОбщейСобственностиЧислитель", , Отказ);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Запись.ДоляВПравеОбщейСобственностиЗнаменатель) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Доля в праве общей собственности (знаменатель)'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Запись.ДоляВПравеОбщейСобственностиЗнаменатель", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		Ошибки = Новый Массив;
		
		ЗаполнитьНалоговуюСтавку(ЭтаФорма, Ошибки);
		
		Для Каждого Ошибка Из Ошибки Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибка.ТекстСообщения, , Ошибка.Поле, ,Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ВключатьВНалоговуюБазу
	   И ПараметрыЗаписи.Свойство("ПоявилисьТранспортныеСредства") Тогда
		// в связи с записанными данными необходимо добавить КБК
		Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ВедетсяУчетПлатежейВПлатонПослеЗаписи", 
		ПолучитьФункциональнуюОпцию("ВедетсяУчетПлатежейВПлатон"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Перем ВедетсяУчетПлатежейВПлатонПередЗаписью, ВедетсяУчетПлатежейВПлатонПослеЗаписи;
	
	Если ПараметрыЗаписи.Свойство("ВедетсяУчетПлатежейВПлатонПередЗаписью", ВедетсяУчетПлатежейВПлатонПередЗаписью)
		И ПараметрыЗаписи.Свойство("ВедетсяУчетПлатежейВПлатонПослеЗаписи", ВедетсяУчетПлатежейВПлатонПослеЗаписи) Тогда
		
		Если ВедетсяУчетПлатежейВПлатонПередЗаписью <> ВедетсяУчетПлатежейВПлатонПослеЗаписи Тогда
			ОбновитьИнтерфейс();
		КонецЕслИ;
		
	КонецЕсли;
	
	Оповестить("ИзмененаИнформацияОС");
	
	УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	
	Если ИмяСобытия = "ИзмененаРегистрацияВНалоговомОргане" 
		ИЛИ ИмяСобытия = "Запись_Организации" Тогда
	
		ЗаполнитьДанныеОНалоговомОргане();
		
	ИначеЕсли ИмяСобытия = "ИзмененОбъектОС" Тогда
		
		ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПериодПриИзмененииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеСредствоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство)
		И ЗначениеЗаполнено(Запись.Организация) Тогда
		
		ЗаполнитьПараметрыПоОсновномуСредству();
	КонецЕсли;
	
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаТранспортногоСредстваПриИзменении(Элемент)
	
	СтруктураКодов = КодыВидовТС.Получить(Запись.КодВидаТранспортногоСредства);
	
	Если СтруктураКодов <> Неопределено Тогда
		
		КодЕдиницыИзмерения = Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.НайтиПоЗначению(СтруктураКодов.КодЕдиницыИзмерения);
		Если КодЕдиницыИзмерения <> Неопределено Тогда
			Запись.ЕдиницаИзмеренияНалоговойБазы = КодЕдиницыИзмерения.Значение;
		Иначе
			Запись.ЕдиницаИзмеренияНалоговойБазы = "";
		КонецЕсли;
			
		УстановитьЗаголовокНалоговойБазы(ЭтаФорма);
	КонецЕсли;
	
	ЗаполнитьНаименованиеВидаТС(ЭтаФорма);
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаТранспортногоСредстваНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СписокВыбораКодовТС.Количество() = 0 Тогда
	
		СписокВыбораКодовТС = Новый СписокЗначений;
		СписокВыбораКодовТС.Добавить("51000", НСтр("ru = '51000 Автомобили легковые'"));
		СписокВыбораКодовТС.Добавить("51004", НСтр("ru = '51004 - прочие автомобили легковые (кроме включенных по кодам 56600, 56700)'"));
		СписокВыбораКодовТС.Добавить("52001", НСтр("ru = '52001 Автомобили грузовые (кроме включенных по коду 57000)'"));
		
	КонецЕсли;
	
	Если СписокВыбораКодовТС.НайтиПоЗначению("ФормаВыбораКодаТС") = Неопределено Тогда
		СписокВыбораКодовТС.Добавить("ФормаВыбораКодаТС", НСтр("ru = 'Другие коды видов ТС...'"));
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИзСпискаКодВидаТранспортногоСредстваЗавершение", ЭтотОбъект, Элемент);
	ПоказатьВыборИзСписка(Оповещение, СписокВыбораКодовТС, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияНалоговойБазыПриИзменении(Элемент)
	
	УстановитьЗаголовокНалоговойБазы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭкологическийКлассНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",		"РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта",	"РегистрацияТранспортныхСредств");
	ПараметрыФормы.Вставить("НазваниеМакета",	"КодыЭкологическихКлассов");
	ПараметрыФормы.Вставить("ТекущийПериод",	Запись.Период);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЭкологическийКлассНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПостановкаНаУчетВНалоговомОрганеПриИзменении(Элемент)
	
	Если Запись.ПостановкаНаУчетВНалоговомОргане = ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане") Тогда
		ВосстановитьРеквизиты(Запись, "НалоговыйОрган, КодПоОКТМО, КодПоОКАТО", СтруктураСохраняемыхРеквизитов);
	Иначе
		СохранитьРеквизиты(Запись, "НалоговыйОрган, КодПоОКТМО, КодПоОКАТО", СтруктураСохраняемыхРеквизитов);
	КонецЕсли;
	
	НастроитьНалоговыйОрган(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганПриИзменении(Элемент)
	
	НалоговыйОрганПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяЛьготаПриИзменении(Элемент)
	
	// Сохраним значения
	Если ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = Запись.КодНалоговойЛьготы;
		РегиональныйКодЛьготыОсвобождениеОтНалогообложения = Запись.РегиональныйКодЛьготы;
		
	ИначеЕсли ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		РегиональныйКодЛьготыСнижениеНалоговойСтавки = Запись.РегиональныйКодЛьготы;
		СохранитьРеквизиты(Запись, "ЛьготнаяСтавка", СтруктураСохраняемыхРеквизитов);
		Запись.ЛьготнаяСтавка = 0;
		
	ИначеЕсли ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах = Запись.РегиональныйКодЛьготы;
		СохранитьРеквизиты(Запись, "ПроцентУменьшения", СтруктураСохраняемыхРеквизитов);
		Запись.ПроцентУменьшения = 0;
		
	ИначеЕсли ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = Запись.КодНалоговойЛьготы;
		СохранитьРеквизиты(Запись, "СуммаУменьшения", СтруктураСохраняемыхРеквизитов);
		Запись.СуммаУменьшения = 0;
		
	КонецЕсли;
	
	Запись.КодНалоговойЛьготы = "";
	Запись.РегиональныйКодЛьготы = "";
	
	// Восстановим значения
	Если Запись.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		Запись.КодНалоговойЛьготы = КодНалоговойЛьготыОсвобождениеОтНалогообложения;
		Запись.РегиональныйКодЛьготы = РегиональныйКодЛьготыОсвобождениеОтНалогообложения;
		
	ИначеЕсли Запись.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		Запись.КодНалоговойЛьготы = "20230";
		Запись.РегиональныйКодЛьготы = РегиональныйКодЛьготыСнижениеНалоговойСтавки;
		ВосстановитьРеквизиты(Запись, "ЛьготнаяСтавка", СтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли Запись.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		Запись.КодНалоговойЛьготы = "20220";
		Запись.РегиональныйКодЛьготы = РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах;
		ВосстановитьРеквизиты(Запись, "ПроцентУменьшения", СтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли Запись.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		Запись.КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму;
		ВосстановитьРеквизиты(Запись, "СуммаУменьшения", СтруктураСохраняемыхРеквизитов);
		
	КонецЕсли;
	
	ТекущаяНалоговаяЛьгота = Запись.НалоговаяЛьгота;
	УстановитьДоступностьНалоговыхЛьгот(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",		"РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта",	"РегистрацияТранспортныхСредств");
	ПараметрыФормы.Вставить("НазваниеМакета",	"ЛьготыПоТранспортномуНалогу");
	Если Запись.НалоговаяЛьгота = 
			ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		ПараметрыФормы.Вставить("ТекущийПериод",	'20070101');
	Иначе
		ПараметрыФормы.Вставить("ТекущийПериод",	ЭтаФорма.Запись.Период);
	КонецЕсли;

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодНалоговойЛьготыНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура РегиональныйКодЛьготыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОснованиеЛьготы", ЭтаФорма.Запись.РегиональныйКодЛьготы);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуРегиональныйКодЛьготыЗавершение", ЭтотОбъект);
	ОткрытьФорму(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВводаОснованияЛьготы", 
		ПараметрыФормы, 
		ЭтаФорма,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОбщаяСобственностьПриИзменении(Элемент)
	
	УстановитьВидимостьДолиСобственности(ЭтаФорма);
	
	Если Запись.ОбщаяСобственность Тогда
		ВосстановитьРеквизиты(Запись, "ДоляВПравеОбщейСобственностиЧислитель, ДоляВПравеОбщейСобственностиЗнаменатель", СтруктураСохраняемыхРеквизитов);
	Иначе
		СохранитьРеквизиты(Запись, "ДоляВПравеОбщейСобственностиЧислитель, ДоляВПравеОбщейСобственностиЗнаменатель", СтруктураСохраняемыхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "НалоговыйОрганСсылка" Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", Запись.Организация);
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОКТМОСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОКТМОСсылка" Тогда
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
		
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Ключ", НалоговыйОрган);
			
			ФормаНалоговогоОргана = ОткрытьФорму("Справочник.РегистрацииВНалоговомОргане.ФормаОбъекта", ПараметрыФормы);
			
			ЭлементКодПоОКТМО = ФормаНалоговогоОргана.Элементы.Найти("КодПоОКТМО");
			Если ЭлементКодПоОКТМО <> Неопределено Тогда
				ФормаНалоговогоОргана.ТекущийЭлемент = ЭлементКодПоОКТМО;
			КонецЕсли;
			
			СтандартнаяОбработка = Ложь;
			
		Иначе
			
			ПараметрыФормы = Новый Структура("Ключ", Запись.Организация);
			ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяБазаПриИзменении(Элемент)
	
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодПоОКТМОПриИзменении(Элемент)
	
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяСтавкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "НалоговаяСтавкаСсылка" Тогда
		
		ОтборНабораЗаписей = Новый Структура("Период,ОКТМО");
		ЗаполнитьЗначенияСвойств(ОтборНабораЗаписей, ПараметрыСтавки);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", ОтборНабораЗаписей);
		ПараметрыФормы.Вставить("ПараметрыСтавки", ПараметрыСтавки);
		
		ОткрытьФорму("РегистрСведений.СтавкиТранспортногоНалога.ФормаСписка", ПараметрыФормы);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПараметрыПоОсновномуСредству()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОсновноеСредство", Запись.ОсновноеСредство);
	Запрос.УстановитьПараметр("Организация",      Запись.Организация);
	Запрос.УстановитьПараметр("Период",           Запись.Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.НалоговыйОрган,
	|	РегистрацияТранспортныхСредств.КодПоОКАТО,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ОбщаяСобственность,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ОсновноеСредство = &ОсновноеСредство) КАК РегистрацияТранспортныхСредств";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		
		НастроитьФормуПоОбъекту();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОбъекту()
	
	ВыполнитьИнициализацию();
	
	УстановитьВидимость();
	УстановитьВидимостьДолиСобственности(ЭтаФорма);
	УстановитьЗаголовокНалоговойБазы(ЭтаФорма);
	ЗаполнитьНаименованиеВидаТС(ЭтаФорма);
	УстановитьДоступностьНалоговыхЛьгот(ЭтаФорма);
	УстановитьГоловнуюОрганизацию(ЭтаФорма);
	НастроитьНалоговыйОрган(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьИнициализацию()
	
	Если ИнициализацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализацияВыполнена = Истина;
	
	СписокКодовЕдиницИзмеренияТС = ПолучитьСписокКодовЕдиницИзмеренияТС();
	Для Каждого ЕдиницаИзмеренияТС Из СписокКодовЕдиницИзмеренияТС Цикл
		Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.Добавить(ЕдиницаИзмеренияТС.Значение, ЕдиницаИзмеренияТС.Представление);
	КонецЦикла;
	
	ЗаполнитьКодыВидовТС();
	
	Если Запись.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = Запись.КодНалоговойЛьготы;
		РегиональныйКодЛьготыОсвобождениеОтНалогообложения = Запись.РегиональныйКодЛьготы;
	ИначеЕсли Запись.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		КодНалоговойЛьготыСнижениеНалоговойСтавки = Запись.КодНалоговойЛьготы;
		РегиональныйКодЛьготыСнижениеНалоговойСтавки = Запись.РегиональныйКодЛьготы;
	ИначеЕсли Запись.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = Запись.КодНалоговойЛьготы;
		РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах = Запись.РегиональныйКодЛьготы;
	ИначеЕсли Запись.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = Запись.КодНалоговойЛьготы;
		РегиональныйКодЛьготыУменьшениеСуммыНалогаНаСумму = Запись.РегиональныйКодЛьготы;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокКодовЕдиницИзмеренияТС()
	
	Список = Новый СписокЗначений;
	
	Список.Добавить("251", НСтр("ru='л. с.'"));
	Список.Добавить("999", НСтр("ru='Кгс'"));
	Список.Добавить("181", НСтр("ru='БРТ'"));
	Список.Добавить("796", НСтр("ru='шт'"));
	
	Возврат Список;
	
КонецФункции // ПолучитьСписокКодовЕдиницИзмеренияТС()

&НаСервере
Процедура ЗаполнитьКодыВидовТС()
	
	ТекущийПоказательГод = Год(Запись.Период);
	
	Если СтарыйГод = 0
		ИЛИ (СтарыйГод >= 2012 И ТекущийПоказательГод < 2012)
		ИЛИ (СтарыйГод < 2012 И ТекущийПоказательГод >= 2012) Тогда 
		
		ТаблицаКодовВидовТС = ПолучитьКодыВидовТС(ТекущийПоказательГод);
		СтарыйГод = ТекущийПоказательГод;

		СтруктураКода = Новый Структура("Наименование, КодЕдиницыИзмерения");
		
		СоответствиеКодов = Новый Соответствие;
		Для каждого СтрокаКода Из ТаблицаКодовВидовТС Цикл
			ЗаполнитьЗначенияСвойств(СтруктураКода, СтрокаКода);
			СоответствиеКодов.Вставить(СтрокаКода.Код, Новый ФиксированнаяСтруктура(СтруктураКода));
		КонецЦикла;
		КодыВидовТС = Новый ФиксированноеСоответствие(СоответствиеКодов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКодыВидовТС(Год)

	КодыВидовТС = Новый ТаблицаЗначений();
	КодыВидовТС.Колонки.Добавить("Код");
	КодыВидовТС.Колонки.Добавить("Наименование");
	КодыВидовТС.Колонки.Добавить("КодЕдиницыИзмерения");
	
	Макет = РегистрыСведений.РегистрацияТранспортныхСредств.ПолучитьМакет("ВидыТранспортныхСредств");
	
	Если Год < 2011 Тогда
		ТекущаяОбласть = Макет.Области.Найти("Область2007");
	ИначеЕсли Год < 2012 Тогда
		ТекущаяОбласть = Макет.Области.Найти("Область2011");
	ИначеЕсли Год < 2013 Тогда
		ТекущаяОбласть = Макет.Области.Найти("Область2012");
	Иначе
		ТекущаяОбласть = Макет.Области.Найти("Область2013");
	КонецЕсли;

	Если НЕ (ТекущаяОбласть = Неопределено) Тогда
	
		Для НомерСтр = ТекущаяОбласть.Верх По ТекущаяОбласть.Низ Цикл
			
			// Перебираем строки макета.
			КодПоказателя       = СокрП(Макет.Область(НомерСтр, 1).Текст);
			Название            = СокрП(Макет.Область(НомерСтр, 2).Текст);
			КодЕдиницыИзмерения = СокрП(Макет.Область(НомерСтр, 3).Текст);
			
			Если КодПоказателя = "###" Тогда
				Прервать;
			ИначеЕсли ПустаяСтрока(КодПоказателя) Тогда
				Продолжить;
			Иначе
				НоваяСтрока = КодыВидовТС.Добавить();
				НоваяСтрока.Код                 = КодПоказателя;
				НоваяСтрока.Наименование        = Название;
				НоваяСтрока.КодЕдиницыИзмерения = КодЕдиницыИзмерения;
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КодыВидовТС;

КонецФункции // ПолучитьКодыВидовТС()

&НаСервере
Процедура УстановитьВидимость()
	
	Элементы.ГруппаПараметрыРегистрации.Видимость = Запись.ВидЗаписи = Перечисления.ВидЗаписиОРегистрации.Регистрация;
	Элементы.КодПоОКАТО.Видимость                 = Год(Запись.Период) < 2014;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДолиСобственности(Форма)
	
	Форма.Элементы.ГруппаДоляВПравеНаТС.Видимость = Форма.Запись.ОбщаяСобственность;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьНалоговыхЛьгот(Форма)
	
	НалоговаяЛьгота = Форма.Запись.НалоговаяЛьгота;
	Элементы = Форма.Элементы;
	
	ОсвобождениеОтНалогообложения = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения");
	
	СнижениеНалоговойСтавки = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки");
	
	УменьшениеСуммыНалогаВПроцентах = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах");
		
	УменьшениеСуммыНалогаНаСумму = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму");
		
	Элементы.ГруппаКодНалоговойЛьготы.Видимость = 
		ОсвобождениеОтНалогообложения ИЛИ СнижениеНалоговойСтавки ИЛИ УменьшениеСуммыНалогаВПроцентах ИЛИ УменьшениеСуммыНалогаНаСумму;
	Элементы.ДекорацияКодЛьготы.Видимость = 
		ОсвобождениеОтНалогообложения ИЛИ СнижениеНалоговойСтавки ИЛИ УменьшениеСуммыНалогаВПроцентах;
	Элементы.РегиональныйКодЛьготы.Видимость = 
		ОсвобождениеОтНалогообложения ИЛИ СнижениеНалоговойСтавки ИЛИ УменьшениеСуммыНалогаВПроцентах;
	Элементы.ЛьготнаяСтавка.Видимость = СнижениеНалоговойСтавки;
	Элементы.ПроцентУменьшения.Видимость = УменьшениеСуммыНалогаВПроцентах;
	Элементы.СуммаУменьшения.Видимость = УменьшениеСуммыНалогаНаСумму;
	
	Элементы.КодНалоговойЛьготы.ТолькоПросмотр = СнижениеНалоговойСтавки ИЛИ УменьшениеСуммыНалогаВПроцентах;
	Элементы.КодНалоговойЛьготы.КнопкаВыбора = ОсвобождениеОтНалогообложения ИЛИ УменьшениеСуммыНалогаНаСумму;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокНалоговойБазы(Форма)
	
	ЕдиницаИзмеренияНалоговойБазы = Форма.Запись.ЕдиницаИзмеренияНалоговойБазы;
	
	Если ПустаяСтрока(ЕдиницаИзмеренияНалоговойБазы) Тогда
		Заголовок = НСтр("ru = 'Налоговая база'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "251" Тогда
		Заголовок = НСтр("ru = 'Мощность двигателя'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "999" Тогда
		Заголовок = НСтр("ru = 'Сила тяги'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "181" Тогда
		Заголовок = НСтр("ru = 'Валовая вместимость'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "796" Тогда
		Заголовок = НСтр("ru = 'Налоговая база'");
	Иначе
		Заголовок = НСтр("ru = 'Налоговая база'");
	КонецЕсли;
	
	Форма.Элементы.НалоговаяБаза.Заголовок = Заголовок;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНаименованиеВидаТС(Форма)
	
	Если ПустаяСтрока(Форма.Запись.КодВидаТранспортногоСредства) Тогда
		Форма.Элементы.КодВидаТранспортногоСредства.РасширеннаяПодсказка.Заголовок = "";
	Иначе
		СтруктураКодов = Форма.КодыВидовТС.Получить(Форма.Запись.КодВидаТранспортногоСредства);
		Если СтруктураКодов = Неопределено Тогда
			Форма.Элементы.КодВидаТранспортногоСредства.РасширеннаяПодсказка.Заголовок = "";
		Иначе
			Форма.Элементы.КодВидаТранспортногоСредства.РасширеннаяПодсказка.Заголовок = СтруктураКодов.Наименование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	ЗаполнитьКодыВидовТС();
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство)
		И ЗначениеЗаполнено(Запись.Организация) Тогда
		
		ЗаполнитьПараметрыПоОсновномуСредству();
	КонецЕсли;
	
	УстановитьВидимость();
	
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
	ЗаполнитьСписокВыбораПовышающегоКоэффициента(
		Элементы.ПовышающийКоэффициент.СписокВыбора,
		Запись.Период);
		
	УстановитьТекстПодсказкиСпискаПовышающихКоэффициентов(
		Элементы.ПовышающийКоэффициент.РасширеннаяПодсказка.Заголовок, 
		Запись.Период);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство)
		И ЗначениеЗаполнено(Запись.Организация) Тогда
		
		ЗаполнитьПараметрыПоОсновномуСредству();
	КонецЕсли;
	
	ЗаполнитьДанныеОНалоговомОргане();
	УстановитьГоловнуюОрганизацию(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьГоловнуюОрганизацию(Форма)
	
	Форма.ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Форма.Запись.Организация);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзСпискаКодВидаТранспортногоСредстваЗавершение(Результат, Элемент) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Значение <> "ФормаВыбораКодаТС" Тогда
			
			Запись.КодВидаТранспортногоСредства = Результат.Значение;
			Модифицированность = Истина;
			КодВидаТранспортногоСредстваПриИзменении(Элемент);
			
		Иначе
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ТекущийКод", Элемент.ТекстРедактирования);
			ПараметрыФормы.Вставить("Дата", Запись.Период);
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Элемент", Элемент);
			
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодВидаТранспортногоСредстваНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			ОткрытьФорму("РегистрСведений.РегистрацияТранспортныхСредств.Форма.ФормаВыбораКодаВидаТранспортногоСредства", ПараметрыФормы,ЭтаФорма,,,,ОповещениеОЗакрытии);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаТранспортногоСредстваНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	ВыбранноеЗначение = РезультатЗакрытия;
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Запись.КодВидаТранспортногоСредства = ВыбранноеЗначение.КодВидаТС;
		
		Если СписокВыбораКодовТС.НайтиПоЗначению(ВыбранноеЗначение.КодВидаТС) = Неопределено Тогда
			
			СписокВыбораКодовТС.Вставить(0, ВыбранноеЗначение.КодВидаТС, ВыбранноеЗначение.КодВидаТС + " " + ВыбранноеЗначение.Наименование); 
			
			ЗначениеДляОткрытияФормыВыбора = СписокВыбораКодовТС.НайтиПоЗначению("ФормаВыбораКодаТС");
			Если ЗначениеДляОткрытияФормыВыбора <> Неопределено Тогда
				СписокВыбораКодовТС.Удалить(ЗначениеДляОткрытияФормыВыбора);
			КонецЕсли;
			
			МаксимальноеКоличество = 6;
			КоличествоЗначений     = СписокВыбораКодовТС.Количество();
			
			Если КоличествоЗначений > МаксимальноеКоличество Тогда
				Для ОбратныйИндекс = 1 По КоличествоЗначений Цикл
					ЗначениеСписка = СписокВыбораКодовТС[КоличествоЗначений - ОбратныйИндекс];
					Если СписокВыбораКодовТС.Индекс(ЗначениеСписка) > (МаксимальноеКоличество - 1) Тогда
						СписокВыбораКодовТС.Удалить(ЗначениеСписка);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		КодВидаТранспортногоСредстваПриИзменении(Элемент);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЭкологическийКлассНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	КодЭкологическогоКласса = РезультатЗакрытия;
	
	Если КодЭкологическогоКласса <> Неопределено Тогда
		Запись.ЭкологическийКласс = КодЭкологическогоКласса;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СохранитьРеквизиты(Объект, СписокРеквизитов, СтруктураСохраняемыхРеквизитов)
	
	ВременнаяСтруктура = Новый Структура(СписокРеквизитов);
	Для каждого КлючИЗначение Из ВременнаяСтруктура Цикл
		СтруктураСохраняемыхРеквизитов.Вставить(КлючИЗначение.Ключ, Объект[КлючИЗначение.Ключ]);
		Объект[КлючИЗначение.Ключ] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Объект[КлючИЗначение.Ключ]));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВосстановитьРеквизиты(Объект, СписокРеквизитов, СтруктураСохраняемыхРеквизитов)
	
	ВременнаяСтруктура = Новый Структура(СписокРеквизитов);
	Для каждого КлючИЗначение Из ВременнаяСтруктура Цикл
		Если СтруктураСохраняемыхРеквизитов.Свойство(КлючИЗначение.Ключ) Тогда
			Объект[КлючИЗначение.Ключ] = СтруктураСохраняемыхРеквизитов[КлючИЗначение.Ключ]
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НалоговыйОрганПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.НалоговыйОрган) Тогда 
		Запись.КодПоОКТМО = Запись.НалоговыйОрган.КодПоОКТМО;
		Запись.КодПоОКАТО = Запись.НалоговыйОрган.КодПоОКАТО;
	КонецЕсли;
	
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		Запись.КодНалоговойЛьготы = РезультатЗакрытия;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРегиональныйКодЛьготыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Запись.РегиональныйКодЛьготы = Результат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСтавкуНаСервере(ДанныеДляЗаполнения, НалоговаяСтавка, ПараметрыСтавки, Ошибки)

	ПараметрыСтавки = Новый Структура(
		"Период,
		|ОКТМО,
		|НаименованиеОбъектаНалогообложения,
		|МинимальноеЗначениеМощности,
		|МаксимальноеЗначениеМощности,
		|МинимальноеКоличествоЛетСГодаВыпускаТС,
		|МаксимальноеКоличествоЛетСГодаВыпускаТС");
	
	// Для кодов автономных округов в составе края (области), являющихся субъектами РФ,
	// значимыми для идентификации субъекта являются 1-й, 2-й и 3-й знаки.
	КодПоОКТМО = Лев(ДанныеДляЗаполнения.ОКТМО, 3);

	// Для остальных субъектов РФ значимые для идентификации знаки 1-й и 2-й.
	Если НЕ (КодПоОКТМО = "118" ИЛИ КодПоОКТМО = "718" ИЛИ КодПоОКТМО = "719") Тогда
		КодПоОКТМО = Лев(КодПоОКТМО, 2);
	КонецЕсли;

	Макет = РегистрыСведений.РегистрацияТранспортныхСредств.ПолучитьМакет("КодыВидовИКатегорииТС");
	КодыВидовИКатегорииТС = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	
	НайденнаяСтрока = КодыВидовИКатегорииТС.Найти(ДанныеДляЗаполнения.КодВидаТранспортногоСредства, "КодВида");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат;
	Иначе
		КатегорияТС = НайденнаяСтрока.Категория;
	КонецЕсли;
	
	Год = Год(ДанныеДляЗаполнения.Период);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Период", ?(Год > 2014, ДанныеДляЗаполнения.Период, Дата('20150101')));
	Запрос.УстановитьПараметр("ОКТМО", ?(СтрДлина(КодПоОКТМО) = 3, КодПоОКТМО + "00000", КодПоОКТМО + "000000"));
	Запрос.УстановитьПараметр("НаименованиеОбъектаНалогообложения", Перечисления.КатегорииТранспортныхСредств[КатегорияТС]);
	Запрос.УстановитьПараметр("НалоговаяБаза", ДанныеДляЗаполнения.НалоговаяБаза);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(СтавкиТранспортногоНалогаСрезПоследних.Период) КАК Период,
	|	СтавкиТранспортногоНалогаСрезПоследних.ОКТМО,
	|	СтавкиТранспортногоНалогаСрезПоследних.НаименованиеОбъектаНалогообложения,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеКоличествоЛетСГодаВыпускаТС
	|ПОМЕСТИТЬ ВТСтавкиТранспортногоНалога
	|ИЗ
	|	РегистрСведений.СтавкиТранспортногоНалога.СрезПоследних(
	|			&Период,
	|			ОКТМО = &ОКТМО
	|				И НаименованиеОбъектаНалогообложения = &НаименованиеОбъектаНалогообложения
	|				И МинимальноеЗначениеМощности <= &НалоговаяБаза
	|				И (МаксимальноеЗначениеМощности = 0
	|					ИЛИ &НалоговаяБаза <= МаксимальноеЗначениеМощности)) КАК СтавкиТранспортногоНалогаСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	СтавкиТранспортногоНалогаСрезПоследних.ОКТМО,
	|	СтавкиТранспортногоНалогаСрезПоследних.НаименованиеОбъектаНалогообложения,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеКоличествоЛетСГодаВыпускаТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСтавкиТранспортногоНалога.Период КАК Период,
	|	ВТСтавкиТранспортногоНалога.ОКТМО КАК ОКТМО,
	|	ВТСтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	ВТСтавкиТранспортногоНалога.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	ВТСтавкиТранспортногоНалога.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	ВТСтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	ВТСтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	ВЫБОР
	|		КОГДА ВТСтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС > 0
	|				ИЛИ ВТСтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	СтавкиТранспортногоНалога.НалоговаяСтавка
	|ИЗ
	|	РегистрСведений.СтавкиТранспортногоНалога.СрезПоследних(
	|			&Период,
	|			ОКТМО = &ОКТМО
	|				И НаименованиеОбъектаНалогообложения = &НаименованиеОбъектаНалогообложения
	|				И МинимальноеЗначениеМощности <= &НалоговаяБаза
	|				И (МаксимальноеЗначениеМощности = 0
	|					ИЛИ &НалоговаяБаза <= МаксимальноеЗначениеМощности)) КАК СтавкиТранспортногоНалогаСрезПоследних,
	|	ВТСтавкиТранспортногоНалога КАК ВТСтавкиТранспортногоНалога
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиТранспортногоНалога КАК СтавкиТранспортногоНалога
	|		ПО ВТСтавкиТранспортногоНалога.Период = СтавкиТранспортногоНалога.Период
	|			И ВТСтавкиТранспортногоНалога.ОКТМО = СтавкиТранспортногоНалога.ОКТМО
	|			И ВТСтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения = СтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения
	|			И ВТСтавкиТранспортногоНалога.МинимальноеЗначениеМощности = СтавкиТранспортногоНалога.МинимальноеЗначениеМощности
	|			И ВТСтавкиТранспортногоНалога.МаксимальноеЗначениеМощности = СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности
	|			И ВТСтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС = СтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС
	|			И ВТСтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС = СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НалоговаяСтавкаЗависитОтГодаВыпускаТС = Выборка.НалоговаяСтавкаЗависитОтГодаВыпускаТС;
			
			Если НалоговаяСтавкаЗависитОтГодаВыпускаТС Тогда
				ДатаВыпуска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляЗаполнения.ОсновноеСредство, "ДатаВыпуска");
					
				Если ЗначениеЗаполнено(ДатаВыпуска) Тогда
						
					КоличествоЛетСГодаВыпускаТС = Год - Год(ДатаВыпуска);
					Если Выборка.МинимальноеКоличествоЛетСГодаВыпускаТС <= КоличествоЛетСГодаВыпускаТС
						И (НЕ ЗначениеЗаполнено(Выборка.МаксимальноеКоличествоЛетСГодаВыпускаТС)
						   ИЛИ КоличествоЛетСГодаВыпускаТС <= Выборка.МаксимальноеКоличествоЛетСГодаВыпускаТС) Тогда
						
						ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Выборка);
						НалоговаяСтавка = Выборка.НалоговаяСтавка;
						Возврат;
						
					КонецЕсли;
						
				Иначе
					
					Ошибка = Новый Структура("ТекстСообщения, Поле",
						НСтр("ru = 'Ставка налога установлена с учетом количества лет, прошедших с года выпуска транспортного средства.
						|Укажите дату выпуска основного средства.'"),
						"Запись.ОсновноеСредство");
					
					Ошибки.Добавить(Ошибка);
					
					НалоговаяСтавка = 0;
					Возврат;
					
				КонецЕсли;
			Иначе
				ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Выборка);
				НалоговаяСтавка = Выборка.НалоговаяСтавка;
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("РаботаВЛокальномРежиме") Тогда
		Возврат;
	КонецЕсли;
	
	СтавкиТранспортногоНалога = РегистрыСведений.СтавкиТранспортногоНалога.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	ИмяМакета = "СтавкиНалога" + ?(Год > 2014, Формат(Год, "ЧГ="), "2015");
	
	Если Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств.Макеты.Найти(ИмяМакета) = Неопределено Тогда
		Ошибка = Новый Структура("ТекстСообщения,Поле", 
			СтрШаблон(НСтр("ru = 'Отсутствует информация о ставках на налоговый период %1'"), Формат(Год, "ЧГ=")), "");
		
		Ошибки.Добавить(Ошибка);
		Возврат;
	КонецЕсли;
	
	Макет = РегистрыСведений.РегистрацияТранспортныхСредств.ПолучитьМакет(ИмяМакета);
	
	НалоговыеСтавки = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	НалоговыеСтавки.Колонки.A.Имя = "КодПоОКТМО";
	НалоговыеСтавки.Колонки.B.Имя = "КатегорияТС";
	НалоговыеСтавки.Колонки.C.Имя = "МинимальноеЗначениеМощности";
	НалоговыеСтавки.Колонки.D.Имя = "МаксимальноеЗначениеМощности";
	НалоговыеСтавки.Колонки.E.Имя = "МинимальноеКоличествоЛетСГодаВыпускаТС";
	НалоговыеСтавки.Колонки.F.Имя = "МаксимальноеКоличествоЛетСГодаВыпускаТС";
	НалоговыеСтавки.Колонки.G.Имя = "Размер";
	
	Отбор = Новый Структура("КодПоОКТМО,КатегорияТС", КодПоОКТМО, КатегорияТС);
	
	Ставки = НалоговыеСтавки.НайтиСтроки(Отбор);
	
	Если Ставки.Количество() = 1 Тогда
		
		НалоговаяСтавка = Число(Ставки[0].Размер);
		
		НоваяСтавка = СтавкиТранспортногоНалога.Добавить();
		НоваяСтавка.Период = Дата(Год, 1, 1);
		НоваяСтавка.ОКТМО = ?(СтрДлина(КодПоОКТМО) = 3, КодПоОКТМО + "00000", КодПоОКТМО + "000000");
		НоваяСтавка.НаименованиеОбъектаНалогообложения = Перечисления.КатегорииТранспортныхСредств[КатегорияТС];
		НоваяСтавка.НалоговаяСтавка = НалоговаяСтавка;
		
	ИначеЕсли Ставки.Количество() > 1 Тогда
		
		Для Каждого Ставка Из Ставки Цикл
			
			МинимальноеЗначениеМощности = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(
				Ставка.МинимальноеЗначениеМощности);
			МаксимальноеЗначениеМощности = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(
				Ставка.МаксимальноеЗначениеМощности);
			МинимальноеКоличествоЛетСГодаВыпускаТС = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(
				Ставка.МинимальноеКоличествоЛетСГодаВыпускаТС);
			МаксимальноеКоличествоЛетСГодаВыпускаТС = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(
				Ставка.МаксимальноеКоличествоЛетСГодаВыпускаТС);
			
			Если МинимальноеЗначениеМощности <= ДанныеДляЗаполнения.НалоговаяБаза
				И (НЕ ЗначениеЗаполнено(МаксимальноеЗначениеМощности)
				   ИЛИ ДанныеДляЗаполнения.НалоговаяБаза <= МаксимальноеЗначениеМощности) Тогда
				
				Если ЗначениеЗаполнено(МинимальноеКоличествоЛетСГодаВыпускаТС)
					ИЛИ ЗначениеЗаполнено(МаксимальноеКоличествоЛетСГодаВыпускаТС) Тогда
					
					ДатаВыпуска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляЗаполнения.ОсновноеСредство, "ДатаВыпуска");
					
					Если ЗначениеЗаполнено(ДатаВыпуска) Тогда
						
						КоличествоЛетСГодаВыпускаТС = Год - Год(ДатаВыпуска);
						Если МинимальноеКоличествоЛетСГодаВыпускаТС <= КоличествоЛетСГодаВыпускаТС
							И (НЕ ЗначениеЗаполнено(МаксимальноеКоличествоЛетСГодаВыпускаТС)
							   ИЛИ КоличествоЛетСГодаВыпускаТС <= МаксимальноеКоличествоЛетСГодаВыпускаТС) Тогда
							
							НалоговаяСтавка = Ставка.Размер;
							
							НоваяСтавка = СтавкиТранспортногоНалога.Добавить();
							НоваяСтавка.Период = Дата(?(Год > 2014, Год, 2015), 1, 1);
							НоваяСтавка.ОКТМО = ?(СтрДлина(КодПоОКТМО) = 3, КодПоОКТМО + "00000", КодПоОКТМО + "000000");
							НоваяСтавка.НаименованиеОбъектаНалогообложения = Перечисления.КатегорииТранспортныхСредств[КатегорияТС];
							НоваяСтавка.МинимальноеЗначениеМощности = МинимальноеЗначениеМощности;
							НоваяСтавка.МаксимальноеЗначениеМощности = МаксимальноеЗначениеМощности;
							НоваяСтавка.МинимальноеКоличествоЛетСГодаВыпускаТС = МинимальноеКоличествоЛетСГодаВыпускаТС;
							НоваяСтавка.МаксимальноеКоличествоЛетСГодаВыпускаТС = МаксимальноеКоличествоЛетСГодаВыпускаТС;
							НоваяСтавка.НалоговаяСтавка = НалоговаяСтавка;
							
							Прервать;
							
						КонецЕсли;
						
					Иначе
						
						Ошибка = Новый Структура("ТекстСообщения, Поле",
							НСтр("ru = 'Ставка налога установлена с учетом количества лет, прошедших с года выпуска транспортного средства.
							|Укажите дату выпуска основного средства.'"),
							"Запись.ОсновноеСредство");
						
						Ошибки.Добавить(Ошибка);
						
						НалоговаяСтавка = 0;
						Прервать;
						
					КонецЕсли;
					
				Иначе
				
					НалоговаяСтавка = Ставка.Размер;
					
					НоваяСтавка = СтавкиТранспортногоНалога.Добавить();
					НоваяСтавка.Период = Дата(?(Год > 2014, Год, 2015), 1, 1);
					НоваяСтавка.ОКТМО = ?(СтрДлина(КодПоОКТМО) = 3, КодПоОКТМО + "00000", КодПоОКТМО + "000000");
					НоваяСтавка.НаименованиеОбъектаНалогообложения = Перечисления.КатегорииТранспортныхСредств[КатегорияТС];
					НоваяСтавка.МинимальноеЗначениеМощности = МинимальноеЗначениеМощности;
					НоваяСтавка.МаксимальноеЗначениеМощности = МаксимальноеЗначениеМощности;
					НоваяСтавка.НалоговаяСтавка = НалоговаяСтавка;
					
					ЗаполнитьЗначенияСвойств(ПараметрыСтавки, НоваяСтавка);
					
					Прервать;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СтавкиТранспортногоНалога <> Неопределено Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Набор = РегистрыСведений.СтавкиТранспортногоНалога.СоздатьНаборЗаписей();
	
		Для Каждого Ставка Из СтавкиТранспортногоНалога Цикл
			
			Набор.Отбор.Период.Установить(Ставка.Период);
			Набор.Отбор.ОКТМО.Установить(Ставка.ОКТМО);
			Набор.Отбор.НаименованиеОбъектаНалогообложения.Установить(Ставка.НаименованиеОбъектаНалогообложения);
			Набор.Отбор.МинимальноеЗначениеМощности.Установить(Ставка.МинимальноеЗначениеМощности);
			Набор.Отбор.МаксимальноеЗначениеМощности.Установить(Ставка.МаксимальноеЗначениеМощности);
			Набор.Отбор.МинимальноеКоличествоЛетСГодаВыпускаТС.Установить(Ставка.МинимальноеКоличествоЛетСГодаВыпускаТС);
			Набор.Отбор.МаксимальноеКоличествоЛетСГодаВыпускаТС.Установить(Ставка.МаксимальноеКоличествоЛетСГодаВыпускаТС);
			
			Набор.Очистить();
			
			НоваяСтавка = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтавка, Ставка);
			
			Набор.Записать();
			
			ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Ставка);
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОНалоговомОргане()
	
	НалоговыйОрганПредставление = НСтр("ru = '<не указан>'");
	ОКТМОПредставление = НСтр("ru = '<не указан>'");
	
	НалоговыйОрганГиперссылка = "";
	ОКТМОГиперссылка = "";
	
	Если ЗначениеЗаполнено(Запись.Организация) Тогда
	
		НалоговыйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "РегистрацияВНалоговомОргане");
		ОКТМО = "";
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НалоговыйОрган, "Код, КПП, КодПоОКТМО");
			
			Если ЗначениеЗаполнено(Реквизиты.КПП) Тогда
				НалоговыйОрганПредставление = СтрШаблон(НСтр("ru = 'КПП %1 в инспекции %2'"), Реквизиты.КПП, Реквизиты.Код);
			Иначе
				НалоговыйОрганПредставление = СтрШаблон(НСтр("ru = 'В инспекции %1'"), Реквизиты.Код);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Реквизиты.КодПоОКТМО) Тогда
				ОКТМО = Реквизиты.КодПоОКТМО;
				ОКТМОПредставление = СокрЛП(Реквизиты.КодПоОКТМО);
			Иначе
				ОКТМОГиперссылка = "ОКТМОСсылка";
			КонецЕсли;
			
		Иначе
			
			НалоговыйОрганГиперссылка = "НалоговыйОрганСсылка";
			
		КонецЕсли;
		
	КонецЕсли;
	
	НалоговыйОрганСсылка = Новый ФорматированнаяСтрока(НалоговыйОрганПредставление,,,, НалоговыйОрганГиперссылка);
	ОКТМОСсылка = Новый ФорматированнаяСтрока(ОКТМОПредставление,,,, ОКТМОГиперссылка);
	
	ЗаполнитьНалоговуюСтавку(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьНалоговыйОрган(Форма)
	
	ПоМестуНахожденияОрганизации = Форма.Запись.ПостановкаНаУчетВНалоговомОргане = 
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации");
		
	Форма.Элементы.ГруппаНалоговыйОрганПоМестуНахожденияОрганизации.Видимость = ПоМестуНахожденияОрганизации;
	Форма.Элементы.ГруппаДругойНалоговыйОрган.Видимость = НЕ ПоМестуНахожденияОрганизации;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНалоговуюСтавку(Форма, Ошибки = Неопределено)
	
	Запись = Форма.Запись;
	
	НалоговаяСтавкаПредставление = НСтр("ru = '<не определена>'");
	НалоговаяСтавкаГиперссылка = "";
	
	Если Ошибки = Неопределено Тогда
		Ошибки = Новый Массив;
	КонецЕсли;
	
	Отказ = Ложь;

	Если НЕ ЗначениеЗаполнено(Запись.Период) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Дата'")), "Запись.Период");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если Запись.ПостановкаНаУчетВНалоговомОргане =
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане") Тогда

		ОКТМОДляЗаполнения = Запись.КодПоОКТМО;

		Если НЕ ЗначениеЗаполнено(Запись.КодПоОКТМО) Тогда
			Отказ = Истина;
			
			Ошибка = Новый Структура("ТекстСообщения, Поле",
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'ОКТМО'")), "Запись.КодПоОКТМО");
			Ошибки.Добавить(Ошибка);
		КонецЕсли;

	Иначе

		ОКТМОДляЗаполнения = Форма.ОКТМО;

		Если НЕ ЗначениеЗаполнено(Форма.ОКТМО) Тогда
			Отказ = Истина;
			
			Ошибка = Новый Структура("ТекстСообщения, Поле",
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'ОКТМО'")), "ОКТМОСсылка");
			Ошибки.Добавить(Ошибка);
		КонецЕсли;

	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Запись.КодВидаТранспортногоСредства) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Код вида ТС'")), "Запись.КодВидаТранспортногоСредства");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Запись.НалоговаяБаза) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Форма.Элементы.НалоговаяБаза.Заголовок), "Запись.НалоговаяБаза");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если НЕ Отказ Тогда
		
		ДанныеДляЗаполнения = Новый Структура("
			|Период,
			|ОсновноеСредство,
			|Организация,
			|ОКТМО,
			|КодВидаТранспортногоСредства,
			|НалоговаяБаза");
			
		ЗаполнитьЗначенияСвойств(ДанныеДляЗаполнения, Запись);
		ДанныеДляЗаполнения.ОКТМО = ОКТМОДляЗаполнения;
		
		ЗаполнитьСтавкуНаСервере(ДанныеДляЗаполнения,
			Форма.НалоговаяСтавка,
			Форма.ПараметрыСтавки,
			Ошибки);
			
		Если Ошибки.Количество() = 0 Тогда
			НалоговаяСтавкаПредставление = СтрШаблон("%1 %2", СокрЛП(Форма.НалоговаяСтавка), Форма.ВалютаРегламентированногоУчета);
			НалоговаяСтавкаГиперссылка = "НалоговаяСтавкаСсылка";
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.НалоговаяСтавкаСсылка = Новый ФорматированнаяСтрока(НалоговаяСтавкаПредставление,,,, НалоговаяСтавкаГиперссылка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(Форма, НоваяЗапись = Ложь)
	
	Форма.АвтоЗаголовок = Ложь;
	
	Создание = ?(НоваяЗапись, НСтр("ru = ' (создание)'"), "");
	
	Если Форма.Запись.ВидЗаписи = ПредопределенноеЗначение("Перечисление.ВидЗаписиОРегистрации.Регистрация") Тогда
		Форма.Заголовок = СтрШаблон(НСтр("ru = 'Регистрация транспортного средства%1'"), Создание);
	Иначе
		Форма.Заголовок = СтрШаблон(НСтр("ru = 'Снятие с регистрационного учета%1'"), Создание);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораПовышающегоКоэффициента(СписокВыбора, Период)
	
	СписокВыбора.Очистить();
	
	Если Год(Период) >= 2018 Тогда
		
		СписокВыбора.Добавить(0, "-");
		СписокВыбора.Добавить(1.1, "1,1");
		СписокВыбора.Добавить(2, "2");
		СписокВыбора.Добавить(3, "3");
		
	Иначе
				
		СписокВыбора.Добавить(0, "-");
		СписокВыбора.Добавить(1.1, "1,1");
		СписокВыбора.Добавить(1.3, "1,3");
		СписокВыбора.Добавить(1.5, "1,5");
		СписокВыбора.Добавить(2, "2");
		СписокВыбора.Добавить(3, "3");
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстПодсказкиСпискаПовышающихКоэффициентов(Подсказка, Период)
	
	ЖирныйШрифт = Новый Шрифт(,, Истина);
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru='Исчисление суммы налога производится с учетом повышающего коэффициента:
		|'")));
		
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока("1,1", ЖирныйШрифт));
	
	Если Год(Период) >= 2018 Тогда
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло не более 3 лет;
		|'")));
		
	Иначе
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
			"ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло от 2 до 3 лет;
			|'")));
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока("1,3", ЖирныйШрифт));
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
			"ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло от 1 года до 2 лет;
			|'")));
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока("1,5", ЖирныйШрифт));
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
			"ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло не более 1 года;
			|'")));
		
	КонецЕсли;
	
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока("2", ЖирныйШрифт));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru=' - в отношении легковых автомобилей средней стоимостью от 5 миллионов до 10 миллионов рублей включительно, с года выпуска которых прошло не более 5 лет;
		|'")));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока("3", ЖирныйШрифт));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru=' - в отношении легковых автомобилей средней стоимостью от 10 миллионов до 15 миллионов рублей включительно, с года выпуска которых прошло не более 10 лет;
		|'")));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока("3", ЖирныйШрифт));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru=' - в отношении легковых автомобилей средней стоимостью от 15 миллионов рублей, с года выпуска которых прошло не более 20 лет.
		|'")));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru='При этом исчисление сроков начинается с года выпуска соответствующего легкового автомобиля.
		|Порядок расчета средней стоимости легковых автомобилей определяется федеральным органом исполнительной власти, осуществляющим функции по выработке государственной политики и нормативно-правовому регулированию в сфере торговли. Перечень легковых автомобилей средней стоимостью от 3 миллионов рублей размещается ежегодно не позднее 1 марта на официальном сайте указанного органа в информационно-телекоммуникационной сети ""Интернет""'")));
	
	Подсказка = Новый ФорматированнаяСтрока(МассивСтрок);	
	
КонецПроцедуры	

#КонецОбласти
