
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаКлиенте
// Обработчик "ПриОткрытии" формы.
//
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиенте
// Управление видимостью элементов формы.
//
Процедура УправлениеФормой()
	
	Элементы.Ошибка.Видимость                    = (НЕ Запись.Статус);
	Элементы.КнопкаОткрытьФайлОбмена.Доступность = Запись.ЕстьФайлОбмена;
	
	Если Запись.ЕстьФайлОбмена Тогда
		
		// Ищем запись с файлом в регистре сведений "Журнал обмена".
		СтруктураСведенийОФайле = ПолучитьСтруктуруСведенийОФайлеОбмена();
		
		СохраненныйФайл = СтруктураСведенийОФайле.СохраненныйФайл;
		РасширениеФайла = СтруктураСведенийОФайле.РасширениеФайла;
		
		Если РасширениеФайла = "zip" Тогда
			Элементы.КнопкаОткрытьФайлОбмена.Заголовок = "Сохранить файл обмена";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеФормой()

&НаКлиенте
// Выполняет открытие файла обмена программой по умолчанию.
//
Процедура КомандаОткрытьФайлОбмена(Команда)
	
	// Получаем значение файла обмена.
	СтруктураСведенийОФайле = ПолучитьСтруктуруСведенийОФайлеОбмена();
	СохраненныйФайл         = СтруктураСведенийОФайле.СохраненныйФайл;
	РасширениеФайла         = СтруктураСведенийОФайле.РасширениеФайла;
	
	Если СохраненныйФайл = Неопределено Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Не обнаружен сохраненный файл обмена.");
	Иначе
		
		Если РасширениеФайла = "zip" Тогда
			
			// Если zip, то сохраняем файл на диск.
			СохранитьФайлОбмена(СохраненныйФайл);
			
		Иначе
			
			ДополнительныеПараметры = Новый Структура("РасширениеФайла, СохраненныйФайл", ".txt", СохраненныйФайл);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("НачатьПолучениеКаталогаВременныхФайловЗавершение", ЭтаФорма, ДополнительныеПараметры);
			
			НачатьПолучениеКаталогаВременныхФайлов(ОписаниеОповещения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьПолучениеКаталогаВременныхФайлов".
Процедура НачатьПолучениеКаталогаВременныхФайловЗавершение(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	УникальнаяСтрока = Новый УникальныйИдентификатор;
	
	ИмяФайла = ИмяКаталогаВременныхФайлов + УникальнаяСтрока + ДополнительныеПараметры.РасширениеФайла;
	
	Попытка
		ОписаниеОповещения = Новый ОписаниеОповещения("НачатьЗаписьЗавершение", ЭтаФорма, ИмяФайла);
		ДополнительныеПараметры.СохраненныйФайл.НачатьЗапись(ОписаниеОповещения, ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(СтрШаблон("Не удалось открыть файл обмена: %1", ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапись".
Процедура НачатьЗаписьЗавершение(ИмяФайла) Экспорт
	
	Попытка
		НачатьЗапускПриложения(Новый ОписаниеОповещения("НачатьЗапускПриложенияЗавершение", ЭтаФорма), ИмяФайла);
	Исключение
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке(СтрШаблон("Не удалось открыть файл обмена: %1", ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Обработчик метода "НачатьЗапускПриложения".
Процедура НачатьЗапускПриложенияЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

&НаСервере
// Получает сохраненный файл обмена.
//
Функция ПолучитьСтруктуруСведенийОФайлеОбмена()
	
	// Ищем запись с файлом в регистре сведений "Журнал обмена".
	СтруктураСведенийОФайле = РегистрыСведений.УПЖКХ_ЖурналОбменаСГолосовымиСервисами.ПолучитьСведенияОФайлеОбмена(Запись);
	
	Возврат СтруктураСведенийОФайле;
	
КонецФункции

&НаКлиенте
// Выполняет сохранение файла обмена.
//
Процедура СохранитьФайлОбмена(СохраненныйФайл)
	
	ИмяФайла = СтрШаблон("saveddata_%1", Формат(Запись.ДатаОбмена, "ДФ=дд_ММ_гггг"));
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = ИмяФайла;
	Диалог.Фильтр         = СтрШаблон("%1*.*|*.*", ?(РасширениеФайла = "", "", СтрШаблон("*%1|*%1|", РасширениеФайла)));
	Диалог.Расширение     = РасширениеФайла;
	Диалог.Заголовок      = НСтр("ru = 'Сохранение файла'");
	
	Обработчик = Новый ОписаниеОповещения("СохранитьФайлВыборФайла", ЭтотОбъект, СохраненныйФайл);
	Диалог.Показать(Обработчик);
	
КонецПроцедуры

&НаКлиенте
// Обработчик результата работы процедуры ВыгрузитьВФайл.
//
Процедура СохранитьФайлВыборФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		ПолноеИмяФайла = ВыбранныеФайлы[0];
		
		ДополнительныеПараметры.Записать(ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти