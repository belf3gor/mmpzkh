#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

///////////////////////////////////////////////////////////////////////////////
// Блок функций первоначального заполнения и обновления ИБ

// Создает и заполняет соответствие, ключом которого является категория удержания, 
// а значением - структура значений, определяющая заполнение свойств удержания данной категории.
//
// Параметры
//	- КатегорияУдержания - ПеречислениеСсылка.КатегорииУдержаний.
//
// Возвращаемое значение - соответствие
//
Функция СвойстваУдержанийПоКатегориям() Экспорт
	
	СвойстваПоКатегориям = Новый Соответствие;
	
	// Значение по умолчанию
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ПустаяСсылка(), СвойстваУдержанияПоКатегорииПоУмолчанию());
	
	// Профсоюзные взносы
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.Профвзносы;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы, Описание);
	
	// Исполнительный лист
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ИсполнительныйЛист, Описание);
	
	// Вознаграждение платежного агента
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента, Описание);
	
	// ДСВ
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ДСВ;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ДСВ, Описание);
	
	// ДобровольныеВзносыВНПФ
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ, Описание);
	
	Возврат СвойстваПоКатегориям;
		
КонецФункции

Процедура СоздатьУдержанияПоНастройкам() Экспорт
	
	СвойстваУдержаний = СвойстваУдержанийПоКатегориям();
	
	// Исполнительный лист
	Описание = ОписаниеУдержания();
	Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ИсполнительныйЛист;
	ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ИсполнительныйЛист]);
	Описание.Код						= НСтр("ru = 'ИСПДК'");
	Описание.Наименование				= НСтр("ru = 'Исполнительный лист'");
	СоздатьИзменитьУдержанияПоОписанию(Описание);
	
	// Профсоюзные взносы
	Описание = ОписаниеУдержания();
	Описание.КатегорияУдержания		= Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы;
	ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы]);
	Описание.Код					= НСтр("ru = 'ПРФВЗ'");
	Описание.Наименование			= НСтр("ru = 'Профсоюзные взносы'");
	СоздатьИзменитьУдержанияПоОписанию(Описание);
	
	// ДСВ
	Описание = ОписаниеУдержания();
	Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ДСВ;
	ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ДСВ]);
	Описание.Код						= НСтр("ru = 'ДСВЗН'");
	Описание.Наименование				= НСтр("ru = 'Добровольные страховые взносы'");
	СоздатьИзменитьУдержанияПоОписанию(Описание);
	
	// Добровольные взносы в НПФ
	Описание = ОписаниеУдержания();
	Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ;
	ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ]);
	Описание.Код						= НСтр("ru = 'ДВНПФ'");
	Описание.Наименование				= НСтр("ru = 'Добровольные взносы в НПФ'");
	СоздатьИзменитьУдержанияПоОписанию(Описание);
	
	// Вознаграждение платежному агенту.
	Описание = ОписаниеУдержания();
	Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента;
	ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента]);
	Описание.Код						= НСтр("ru = 'АГЕНТ'");
	Описание.Наименование				= НСтр("ru = 'Вознаграждение платежного агента'");
	СоздатьИзменитьУдержанияПоОписанию(Описание);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеУдержания()
	
	Описание = Новый Структура(
	"Код, 
	|Наименование,
	|КатегорияУдержания,
	|ВидОперацииПоЗарплате,
	|СсылкаНаОбъект");
	
	Возврат Описание;
	
КонецФункции

Функция СвойстваУдержанияПоКатегорииПоУмолчанию()
	
	Описание = Новый Структура("ВидОперацииПоЗарплате");
	
	Возврат Описание;
	
КонецФункции

Процедура СоздатьИзменитьУдержанияПоОписанию(Описание)
	
	УдержанияПоКатегории = РасчетЗарплаты.УдержанияПоКатегории(Описание.КатегорияУдержания);
	Если УдержанияПоКатегории.Количество() > 0 Тогда
		УдержаниеОбъект = УдержанияПоКатегории[0].ПолучитьОбъект();
	Иначе
		УдержаниеОбъект = Неопределено;
	КонецЕсли;
	
	Если УдержаниеОбъект = Неопределено Тогда
		// Устанавливаем ссылку новому объекту
		УдержаниеСсылка = ПланыВидовРасчета.Удержания.ПолучитьСсылку();
		УдержаниеОбъект = ПланыВидовРасчета.Удержания.СоздатьВидРасчета();
		УдержаниеОбъект.УстановитьСсылкуНового(УдержаниеСсылка);
		Описание.СсылкаНаОбъект = УдержаниеСсылка;
	Иначе
		УдержаниеСсылка = УдержаниеОбъект.Ссылка;
		УдержаниеОбъект.ПометкаУдаления = Ложь;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(УдержаниеОбъект, Описание);
	УдержаниеОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
